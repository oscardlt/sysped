********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
11XXX * 01 YBC PTF11 FLEKSIBEL KUNDENR (MEN MÅ ALLTID SETTES)
********************************************************************
     FEDIRE1K   IF   E           K DISK
     FEDIC      UF   E             DISK
     FEDIM      O    E             DISK
     FEPOH      UF A E           K DISK
     FEPOL      UF A E           K DISK
     FKALENDER  IF   E           K DISK
     D                 DS
     D  RDATA                  1   1024
     D  RTYP                   1      2
     D                 DS
     D  FE                     1    256
     D                                     DIM(256)
     D  FELT                   1    256
     D                 DS
     D  AA                     1   1024
     D                                     DIM(1024)
     D  AADS                   1   1024
     D                 DS
     D  ZDEC                   1      7  6
     D  ZDEC6                  2      7
     D  ZHEL                  11     22  0
     D                 DS
     D  A                      1     79    DIM(79)
     D                 DS
     D  B                      1     18    DIM(18)
      */////////////
      * MAIN LOGIC /
      */////////////
     C                   EXSR      SRINIT
      *
     C     UMBR          SETLL     EDIRE1K
     C     UMBR          READE     EDIRE1K                                91
     C     *IN91         DOWEQ     *OFF

     C                   SELECT
     C     RTYP          WHENEQ    '00'
     C                   EXSR      HODE00
     C     RTYP          WHENEQ    '10'
     C                   EXSR      HODE10
     C     RTYP          WHENEQ    '11'
     C                   EXSR      HODE11
     C     RTYP          WHENEQ    '20'
     C                   EXSR      LINJE20
     C                   OTHER
     C                   ENDSL

     C     UMBR          READE     EDIRE1K                                91
     C                   ENDDO

     C                   IF        XOPDOPD = 'J'
     C                   EXSR      SROPD
     C                   END

     C     LR            TAG
     C                   MOVE      '1'           *INLR
      *
      *////////////////////////////////////////////////////////////
      * SR for init                                        SRINIT /
      *////////////////////////////////////////////////////////////
     C     SRINIT        BEGSR
      *
     C     EPOHK         KLIST
     C                   KFLD                    EHKUND
     C                   KFLD                    EHBES
     C     EPOLK         KLIST
     C                   KFLD                    EHKUND
     C                   KFLD                    EHBES
     C                   KFLD                    ELLIN
     C                   KFLD                    XELULIN
     C     EPOLK9        KLIST
     C                   KFLD                    EHKUND
     C                   KFLD                    EHBES
     C                   KFLD                    ELLIN
     C                   KFLD                    XELULIN9
     C     *LIKE         DEFINE    ELULIN        XELULIN
     C     *LIKE         DEFINE    ELULIN        XELULIN9
     C                   EVAL      EHKUND = 12497
     C                   EVAL      XELULIN9 = 99999
     C     *LIKE         DEFINE    EHTTD         XEHTTD
     C     *LIKE         DEFINE    EHETD         XEHETD
     C     *LIKE         DEFINE    EHSTD         XEHSTD
     C     *LIKE         DEFINE    EHETA         XEHETA
     C     *LIKE         DEFINE    EHANTS        XEHANTS
     C     *LIKE         DEFINE    EHANTK        XEHANTK
     C     *LIKE         DEFINE    EHVKT         XEHVKT
     C     *LIKE         DEFINE    EHM3          XEHM3
     C                   MOVE      ' '           XOPDDT            1
     C                   MOVE      'N'           XOPDOPD           1
      *
     C     *ENTRY        PLIST
     C                   PARM                    UMBR             10
      * Hente dagens dato og tid
     C                   CALL      'SYGE15C'
     C                   PARM                    WDATO             8
     C                   PARM                    WTID              6
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      * HODE 00 Start av fil                               HODE00 /
      *////////////////////////////////////////////////////////////
     C     HODE00        BEGSR
      * Init
     C                   MOVEL     RDATA         AADS
     C                   Z-ADD     1             WX
      * 0. Posttype
     C                   EXSR      GETDED
      * 1. Teknisk avsender
     C                   EXSR      GETDED
     C                   MOVEL     FELT          M0004
      * 2. Teknisk mottager
     C                   EXSR      GETDED
     C                   MOVEL     FELT          M0010
      * 3. Teknisk dato
     C                   EXSR      GETDED
     C*                  MOVEL     FELT          EUROOH3
      * 4. Teknisk klokkeslett
     C                   EXSR      GETDED
     C*                  MOVEL     FELT          EUROOH4
      * 5. Teknisk referanse
     C                   EXSR      GETDED
     C                   MOVEL     FELT          M0062
      * 6. Funksjonsreferanse
     C                   EXSR      GETDED
      *
     C     1             CHAIN     EDIC                               51
     C                   ADD       1             CMN
     C                   UPDATE    EDICR
     C                   Z-ADD     CMN           MMN
     C*                  MOVEL     UMBR          XA08              8
     C*                  MOVE      XA08          MSN
     C                   MOVE      UMBR          MSN
     C                   EVAL      M0065  = 'ORDERS'
     C                   EVAL      M1001  = 'FLA'
     C                   EVAL      MSR    = 'R'
     C                   EVAL      MST    = 'C'
     C                   MOVE      WDATO         MDT
     C                   MOVE      WTID          MTM
N01   *
N01  C                   SELECT
N01  C                   WHEN      M0004 = 'EUROPRIS'
N01  C                   EVAL      EHKUND = 12497
N01  C                   WHEN      M0004 = 'EUROSKO'
N01  C                   EVAL      EHKUND = 10136
N01  C                   WHEN      M0004 = 'SHOEDAY' OR M0004 = 'SCOROC'
N01  C                   EVAL      EHKUND = 13705
N01  C                   ENDSL
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      * HODE 10 Hode 1
      *////////////////////////////////////////////////////////////
     C     HODE10        BEGSR
      *
     C                   IF        XOPDOPD = 'J'
     C                   EXSR      SROPD
     C                   END
      * Init
     C                   MOVEL     RDATA         AADS
     C                   Z-ADD     1             WX
      * 0. Posttype
     C                   EXSR      GETDED
      * 1. Ordrenummer
     C                   EXSR      GETDED
     C                   MOVEL     FELT          M0068
     C                   WRITE     EDIMR
     C                   MOVEL     FELT          EHBES
      * SJEKK OM BESTILLING ER FRA FØR
     C     EPOHK         CHAIN     EPOH                               50
      * 2. Ordredato
     C                   EXSR      GETDED
     C                   MOVEL     FELT          EHDATO
      * 3. Transportkode
     C                   EXSR      GETDED
     C                   MOVEL     FELT          EHTRKO
      * 4. Lev.bet kode
     C                   EXSR      GETDED
     C                   MOVEL     FELT          EHLBET
      * 5. Lev.bet sted
     C                   EXSR      GETDED
     C                   MOVEL     FELT          EHLBES
      * 6. Fra landkode
      * vilkår 50 = kun ved ny lagt til for land/havn far -til (deretter styrt av CC..)
     C                   EXSR      GETDED
     C   50              MOVEL     FELT          EHFLK
      * 7. Til landkode
     C                   EXSR      GETDED
     C                   IF        FELT = *BLANKS
     C                   EVAL      EHTLK = 'NO'
     C                   ELSE
     C   50              MOVEL     FELT          EHTLK
     C                   END
      * 8. Fra sted
     C                   EXSR      GETDED
     C   50              MOVEL     FELT          EHFSTE
      * 9. Til sted
     C                   EXSR      GETDED
     C   50              MOVEL     FELT          EHTSTE
      * 10. Facility
     C                   EXSR      GETDED
     C                   MOVEL     FELT          EHFACI
      * 11. Lev. Referanse
     C                   EXSR      GETDED
     C                   MOVEL     FELT          EHLREF
      * 12. Innkjøper ID
     C                   EXSR      GETDED
     C                   MOVEL     FELT          EHIID
      * 13. Innkjøper navn
     C                   EXSR      GETDED
     C                   MOVEL     FELT          EHINV
      * 14. Innkjøper mailadresse
     C                   EXSR      GETDED
     C                   MOVEL     FELT          EHIEM
      * 15. Innkjøpers telefonnr
     C                   EXSR      GETDED
     C                   MOVEL     FELT          EHITLF
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      * HODE 11 Leverandør
      *////////////////////////////////////////////////////////////
     C     HODE11        BEGSR
      * Init
     C                   MOVEL     RDATA         AADS
     C                   Z-ADD     1             WX
      * 0. Posttype
     C                   EXSR      GETDED
      * 1. Leverandørnummer
     C                   EXSR      GETDED
     C                   MOVEL     FELT          EHLEVN
      * 2. Navn
     C                   EXSR      GETDED
     C                   MOVEL     FELT          EHLNAV
      * 3. Adresse1
     C                   EXSR      GETDED
     C                   MOVEL     FELT          EHLAD1
      * 4. Adresse2
     C                   EXSR      GETDED
     C                   MOVEL     FELT          EHLAD2
      * 5. Adresse3
     C                   EXSR      GETDED
     C                   MOVEL     FELT          EHLAD3
      * 6. Adresse4
     C                   EXSR      GETDED
     C                   MOVEL     FELT          EHLAD4
      * 7. E-mail
     C                   EXSR      GETDED
     C                   MOVEL     FELT          EHLEM
      * 8. Referanse hos lev.
     C                   EXSR      GETDED
     C                   MOVEL     FELT          EHLKP
      * 9. Telefon
     C                   EXSR      GETDED
     C                   MOVEL     FELT          EHLTLF
      * 10. Telefax
     C                   EXSR      GETDED
     C                   MOVEL     FELT          EHLFAX
      * Skriv post
     C                   IF        *IN50
     C                   WRITE     EPOHR
     C                   EVAL      XOPDDT  = 'J'
     C                   ELSE
     C                   IF        EHAVD = -9999 AND EHOPD = -9999999
     C                   EVAL      EHAVD = 0
     C                   EVAL      EHOPD = 0
     C                   END
     C                   UPDATE    EPOHR
     C                   EVAL      XOPDDT  = 'N'
     C                   END
     C                   EVAL      XOPDOPD = 'J'
     C                   EVAL      XEHETD  = 99999999
     C                   EVAL      XEHETA  = 99999999
     C                   EVAL      XEHANTS = 0
     C                   EVAL      XEHANTK = 0
     C                   EVAL      XEHVKT  = 0
     C                   EVAL      XEHM3   = 0
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      * Ordrelinje                                        LINJE20 /
      *////////////////////////////////////////////////////////////
     C     LINJE20       BEGSR
     C                   CLEAR                   EPOLR
      * Ordrenummer
     C                   EVAL      ELKUND = EHKUND
     C                   EVAL      ELBES  = EHBES
      * Teknisk referanse
     C*                  MOVEL     EUROOH6       EUROOL2
      * Init
     C                   MOVEL     RDATA         AADS
     C                   Z-ADD     1             WX                4 0
      * 0. Posttype
     C                   EXSR      GETDED
      * 1. Linjenummer
     C                   EXSR      GETDED
     C                   MOVEA     FELT          A
     C                   EXSR      GETNUM
     C                   Z-ADD     ZNUM          ELLIN
      * 2. Underlinjenummer
     C                   EXSR      GETDED
     C                   MOVEA     FELT          A
     C                   EXSR      GETNUM
     C                   Z-ADD     ZNUM          XELULIN
      * SJEKK UNDERLINJE ER LAGET FRA FØR
     C     EPOLK         CHAIN     EPOL                               50
     C   50EPOLK9        CHAIN     EPOL                               50
      * 3. Art.nr Europris
     C                   EXSR      GETDED
     C                   MOVEL     FELT          ELART
      * 4. Art.nr Leverandør
     C                   EXSR      GETDED
     C                   MOVEL     FELT          ELARTL
      * 5. Art. beskrivelse
     C                   EXSR      GETDED
     C                   MOVEL     FELT          ELABES
      * 6. Art. beskrivelse
     C*                  EXSR      GETDED
     C*                  CAT       FELT:1        ELABES
      * 6. Ant. F-pak bestillt
     C                   EXSR      GETDED
     C                   MOVEA     FELT          A
     C                   EXSR      GETNUM
     C                   Z-ADD     ZNUM          ELANTS
     C                   Z-ADD     ZNUM          ELNTSL
     C                   EVAL      XEHANTS = XEHANTS + ELNTSL
      * 7. Ant. D-pak bestillt
     C                   EXSR      GETDED
     C                   MOVEA     FELT          A
     C                   EXSR      GETNUM
      * KUN OPPDATERE FØRST GANG
     C                   IF        XOPDDT = 'J'
     C                   Z-ADD     ZNUM          ELANKL
     C                   Z-ADD     ZNUM          ELNTKL
     C                   EVAL      XEHANTK = XEHANTK + ELNTKL
N01  C                   IF        (ELANTS = 0 AND M0004 = 'EUROSKO') OR
N01  C                             (ELANTS = 0 AND M0004 = 'SHOEDAY') OR
N01  C                             (ELANTS = 0 AND M0004 = 'SCOROC')
N01  C                   EVAL      ELANTS = ELANKL
N01  C                   EVAL      ELNTSL = ELNTKL
N01  C                   EVAL      XEHANTS = XEHANTS + ELNTSL
N01  C                   END
      ** Ant stk pr kll (om det ikke går opp i heltall rund av EN STK NED)
     c     ELANKL        ifne      *zeros
     c     ELANTS        div       ELANKL        xxSTKL            9 2
     c                   move      xxSTKL        Deci              2 0
     c     Deci          ifne      *zeros
     c     xxSTKL        andge     1
     c                   sub       1             xxSTKL            9 2
     c                   endif
     c                   eval      ELSTKL = xxSTKL
     c                   endif
     C                   END
      * 8. Bvekt bestillt
     C                   EXSR      GETDED
     C                   MOVEA     FELT          A
     C                   EXSR      GETNUM
     C                   IF        XOPDDT = 'J'
     C                   Z-ADD     ZNUM          ELVKTL
     c                   eval      ELVKTL  = ELVKTL * ELANTS
     C                   EVAL      XEHVKT  = XEHVKT + ELVKTL
     C                   END
      * 9. Nvekt bestillt
     C                   EXSR      GETDED
     C                   MOVEA     FELT          A
     C                   EXSR      GETNUM
     C                   IF        XOPDDT = 'J'
     C                   Z-ADD     ZNUM          ELVKNL
     c                   eval      ELVKNL  = ELVKNL * ELANTS
     C                   END
      * 10. Volume, M3 bstillt
     C                   EXSR      GETDED
     C                   MOVEA     FELT          A
     C                   EXSR      GETNUM
     C                   IF        XOPDDT = 'J'
     C                   Z-ADD     ZNUM          ELM3L
     C                   EVAL      XEHM3   = XEHM3 + ELM3L
     C                   END
      * 11. Shipdato, tidligest (alltid enten motta i fil eller reberegne, aldri beholde)
     C                   MOVE      *zeros        ELTTD
     C                   EXSR      GETDED
     C*                  IF        XELULIN = 0 AND FELT <> *BLANKS
     C                   IF        FELT <> *BLANKS
Nxx  c                             and ELSTA <= '0    '
     C                   MOVEL     FELT          ELTTD
     C                   END
      * 12. Shipdato, forventet
     C                   EXSR      GETDED
     C*                  IF        XELULIN = 0 AND FELT <> *BLANKS
     C                   IF        FELT <> *BLANKS
Nxx  c                             and ELSTA <= '0    '
     C                   MOVEL     FELT          ELETD
     C                   END
      * 13. Shipdato, senest  (alltid enten motta i fil eller reberegne, aldri beholde)
     C                   MOVE      *zeros        ELSTD
     C                   EXSR      GETDED
     C*                  IF        XELULIN = 0 AND FELT <> *BLANKS
     C                   IF        FELT <> *BLANKS
Nxx  c                             and ELSTA <= '0    '
     C                   MOVEL     FELT          ELSTD
     C                   END
      * 14. Ankomstdato, forventet
     C**                 IF        XELULIN = 0 AND FELT <> *BLANKS
     C                   EXSR      GETDED
     C*                  IF        XELULIN = 0 AND FELT <> *BLANKS
     C                   IF        FELT <> *BLANKS
Nxx  c                             and ELSTA <= '0    '
     C                   MOVEL     FELT          ELETA
     C                   END
      * Arbeid med dato
     C                   EXSR      SRDATO
      * 15. BL-nr
     C                   EXSR      GETDED
Nxx  c                   IF        ELSTA <= '0    '
     C                   MOVEL     FELT          ELBLNR
Nxx  c                   ENDIF
      * Skriv post
     C                   EVAL      ELULIN = XELULIN
     C                   IF        *IN50
     c                   eval      ELSTA ='0'                                   PENDING
     c*                  eval      ELSTA2='S'                                   SEND BEKR
jov  c                   IF        ELANTS <> 0
     C                   WRITE     EPOLR
jov  C                   END
     C                   ELSE
     C                   IF        ELAVD = -9999 AND ELOPD = -9999999
jov  C                             and   ELANTS <> 0
     C                   EVAL      ELAVD = 0
     C                   EVAL      ELOPD = 0
     C                   END
jov   * får 0 i bestilt ant stk=Sletteanmodning
jov  c                   IF        ELANTS = 0
jov  C                   EVAL      ELAVD = -9999
jov  C                   EVAL      ELOPD = -9999999
jov  c                   endif
     C                   UPDATE    EPOLR
     C                   END
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      *                                                    SRDATO /
      *////////////////////////////////////////////////////////////
     C     SRDATO        BEGSR
      * skipningsvindu = -4 / + 7 dg
     C                   IF        ELTTD = 0 AND ELETD <> 0
     C     ELETD         CHAIN     KALENDER                           33
     C*    1             DO        7
     C     1             DO        4
     C                   READP     KALENDER                               33
     C                   ENDDO
     C                   EVAL      ELTTD = KADATE
     C                   END
      *
     C                   IF        ELSTD = 0 AND ELETD <> 0
     C     ELETD         CHAIN     KALENDER                           33
     C     1             DO        7
     C                   READ      KALENDER                               33
     C                   ENDDO
     C                   EVAL      ELSTD = KADATE
     C                   END
      *
     C                   IF        XOPDDT = 'J'
     C                   IF        XEHETD > ELETD
     C                   EVAL      XEHETD = ELETD
     C                   EVAL      XEHTTD = ELTTD
     C                   EVAL      XEHSTD = ELSTD
     C                   EVAL      XEHETA = ELETA
     C                   END
     C                   END
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      *                                                    SROPD  /
      *////////////////////////////////////////////////////////////
     C     SROPD         BEGSR
      *
     C*                  IF        XOPDDT <> 'J'
     C     EPOHK         SETLL     EPOL
     C     EPOHK         READE(N)  EPOL                                   50
     C                   IF        *IN50 = *OFF
     C                   EVAL      XEHETD = ELETD
     C                   EVAL      XEHTTD = ELTTD
     C                   EVAL      XEHSTD = ELSTD
     C                   EVAL      XEHETA = ELETA
     C                   EVAL      XOPDDT = 'J'
     C                   END
     C                   DOW       *IN50 = *OFF
     C                   IF        XEHETD <> ELETD
     C                   EVAL      XOPDDT = 'N'
     C                   END
     C     EPOHK         READE(N)  EPOL                                   50
     C                   ENDDO
     C*                  END
      *
     C     EPOHK         CHAIN     EPOH                               50
     C                   IF        *IN50 = *OFF
     C*                  IF        XOPDDT = 'J'
     C                   EVAL      EHTTD  = XEHTTD
     C                   EVAL      EHETD  = XEHETD
     C                   EVAL      EHSTD  = XEHSTD
     C                   EVAL      EHETA  = XEHETA
     C*                  END
     C                   EVAL      EHANTS = XEHANTS
     C                   EVAL      EHANTK = XEHANTK
     C                   EVAL      EHVKT  = XEHVKT
     C                   EVAL      EHM3   = XEHM3
     C                   UPDATE    EPOHR
     C                   END
     C                   CLEAR                   EPOHR
N01   *
N01  C                   SELECT
N01  C                   WHEN      M0004 = 'EUROPRIS'
     C                   EVAL      EHKUND = 12497
N01  C                   WHEN      M0004 = 'EUROSKO'
N01  C                   EVAL      EHKUND = 10136
N01  C                   WHEN      M0004 = 'SHOEDAY' OR M0004 = 'SCOROC'
N01  C                   EVAL      EHKUND = 13705
N01  C                   ENDSL
N01   *
     C                   EVAL      XELULIN9 = 99999
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      * Get Data Element                                   GETDED /
      *////////////////////////////////////////////////////////////
     C     GETDED        BEGSR
     C                   MOVEA     *BLANKS       FE
     C     WX            IFGE      1023
     C                   GOTO      ENDDED
     C                   ENDIF
     C     1             DO        256           NR                3 0
     C                   MOVE      AA(WX)        TEGN              1
     C                   ADD       1             WX
     C     TEGN          IFEQ      ';'
     C                   GOTO      ENDDED
     C                   ENDIF
     C                   MOVEA     TEGN          FE(NR)
     C     WX            IFGE      1023
     C                   GOTO      ENDDED
     C                   ENDIF
     C                   ENDDO
     C     ENDDED        ENDSR
      *////////////////////////////////////////////////////////////
      *  Get numerical value                               GETNUM /
      *////////////////////////////////////////////////////////////
     C     GETNUM        BEGSR
     C                   MOVE      *ZEROS        ZHEL
     C                   MOVE      *ZEROS        ZDEC
     C                   MOVE      *ZEROS        B
     C                   Z-ADD     1             AX                2 0
     C                   Z-ADD     1             BX                2 0
      * Get Integer
     C     A(AX)         DOWGE     '0'
     C     ZHEL          MULT      10            ZHEL
     C                   MOVE      A(AX)         ZHEL
     C                   ADD       1             AX
     C                   ENDDO
      * Decimal point ?
     C     A(AX)         IFEQ      '.'
     C     A(AX)         OREQ      ','
     C                   ADD       1             AX
     C     A(AX)         DOWGE     '0'
     C                   MOVE      A(AX)         B(BX)
     C                   ADD       1             AX
     C                   ADD       1             BX
     C                   ENDDO
     C                   MOVEA     B             ZDEC6
     C                   ENDIF
      * Concatenate
     C                   Z-ADD     ZHEL          ZNUM             18 6
     C                   ADD       ZDEC          ZNUM
     C                   ENDSR
