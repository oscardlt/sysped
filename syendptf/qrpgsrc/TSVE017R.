********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
12XXX * 01 CB  PTF12 MANGE SMÅ ENDRINGER
12088 * 02 CB  PTF12  Transportkostnad in i CIF
********************************************************************
      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/TSVE017R MODULE(*LIBL/TSVE017R *LIBL/INCGI)
CRTPGM*        ACTGRP(*NEW) AUT(*USE)
      *
      *********************************************************************
      /copy SYSPEDS/qrpgsrcpy,hspecs
      /copy SYSPEDS/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FSVTVK     IF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy SYSPEDS/qrpgsrcpy,prototypeb
      /copy SYSPEDS/qrpgsrcpy,usec
      /copy SYSPEDS/qrpgsrcpy,variables3
      *
      * Varible for
     D WSUSER          S             10
     D SVEH_VAKD       S              3
     D SVEH_VAKU       S              7  4
     D SVEH_FABL       S             11  3
     D SVEV_STVA       S             11  0
     D SVEV_STVA2      S             11  0
     D SVEV_FABL       S             11  3
N02  D SVEH_SYDT       S              8  0
N02  D SVEH_VUFR       S             11  3
N02  D SVEH_VUKU       S              7  4
N02  D SVEH_VUVA       S              3
     D A07             S              7
     D A09             S              9
     D A11             S             11
     D JSONstring      s          32000
     D Error           S            150
     D AntLest         S              9  0

      *get input-string
      /copy syspeds/qrpgsrcpy,prolog3

      * Parse input
     C                   exsr      Parse
      * Open files etc
     C                   EXSR      INIT
      *
     c                   callp     gethtmlifs(
     C                             '/syspeddev/html/JSON.html':
     C                             '/CGITAG/')
     C                   callp     wrtsection('top')
      *
      * ............................................................................................
      * skriv JSON-Object start..(alltid '{' + x ant param. m/komma mellom (IKKE komma etter siste))
      * ............................................................................................
      *
      /free
        JSONstring='{'
        +'¬user¬ : ¬'+%trim(WSUSER)+'¬, '
        +'¬errMsg¬ : ¬'+%trim(Error)+'¬, ';
      /end-free
      * JSONfix escaper " i tekst,  for deretter å bytte ¬->"
     c                   call      'JSONFIX'
     c                   parm                    JSONstring
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')
      *
      * ............................................................................................
      * Skriv JSON-LISTE Start (en liste er EN parameter(fra [ til   )
      * ............................................................................................
     C                   eval      JSONstring ='"taxcalc" : ['
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')
      *
      * Hæmta detta ena ærendet.
      *
     c     Error         ifeq      *blanks
     C                   EXSR      AVG
      * ............................................................................................
      * Skriv en JSON-Array-linje pr menypunkt - komma før hvert nytt element
      * ............................................................................................
      /free
          JSONstring=*blanks;
       JSONstring=%trim(JSONstring)+'{'
          +'¬svev_stva¬ : ¬'+%trim(%editc(SVEV_STVA:'4'))+'¬, '
          +'¬svev_stva2¬ : ¬'+%trim(%editc(SVEV_STVA2:'4'))+'¬}';
      /end-free
     C                   call      'JSONFIX'
     C                   parm                    JSONstring
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')
      *
     c                   endif
     c                   eval      JSONstring =']'
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')
      * ............................................................................................
      * Skriv JSON-string Avsluttning
      * ............................................................................................
     c                   eval      JSONstring ='}'
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')
      *
      * Quit
     C                   exsr      Exit


      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Lukk fil
     C                   CLOSE     BRIDF
     C  N50              CLOSE     SVTVK

      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   MOVE      *ON           *INLR
     C                   endsr
      *
      *********************************************************
     C     Parse         Begsr
      *********************************************************
      * Get input
     C                   EVAL      WSUSER     =  zhbgetvar('USER')
     C                   EVAL      SVEH_VAKD  =  zhbgetvar('SVEH_VAKD')
     C                   EVAL      A07        =  zhbgetvar('SVEH_VAKU')
     C                   EVAL      SVEH_VAKU  =  c2n2(A07)
     C                   EVAL      A11        =  zhbgetvar('SVEH_FABL')
     C                   EVAL      SVEH_FABL  =  c2n2(A11)
     C                   EVAL      A11        =  zhbgetvar('SVEV_STVA')
     C                   EVAL      SVEV_STVA  =  c2n2(A11)
     C                   EVAL      A11        =  zhbgetvar('SVEV_STVA2')
     C                   EVAL      SVEV_STVA2 =  c2n2(A11)
     C                   EVAL      A11        =  zhbgetvar('SVEV_FABL')
     C                   EVAL      SVEV_FABL  =  c2n2(A11)
N02  C                   EVAL      SVEH_VUVA  =  zhbgetvar('SVEH_VUVA')
N02  C                   EVAL      A11        =  zhbgetvar('SVEH_VUFR')
N02  C                   EVAL      SVEH_VUFR  =  c2n2(A11)
N02  C                   EVAL      A11        =  zhbgetvar('SVEH_VUKU')
N02  C                   EVAL      SVEH_VUKU  =  c2n2(A11)
N02  C                   EVAL      A11        =  zhbgetvar('SVEH_SYDT')
N02  C                   EVAL      SVEH_SYDT  =  c2n2(A11)
N01  C                   IF        SVEH_VAKD=*BLANKS
N01  C                   EVAL      SVEH_VAKD  =  'SEK'
N01  C                   ENDIF
N01  C                   IF        SVEH_VAKU=*ZEROS
N01  C                   EVAL      SVEH_VAKU  =  1
N01  C                   ENDIF
N01  C                   IF        SVEH_FABL=*ZEROS
N01  C                   EVAL      SVEH_FABL  =  1
N01  C                   ENDIF
N01  C                   IF        SVEV_FABL=*ZEROS
N01  C                   EVAL      SVEV_FABL  =  1
N01  C                   ENDIF
N02  C                   IF        SVEH_VUVA=*BLANKS
N02  C                   EVAL      SVEH_VUVA  =  'SEK'
N02  C                   ENDIF
N02  C                   IF        SVEH_VUKU=*ZEROS
N02  C                   EVAL      SVEH_VUKU  =  1
N02  C                   ENDIF
N02  C                   IF        SVEH_SYDT=*ZEROS
N02  C                   EVAL      SVEH_SYDT  =  20991231
N02  C                   ENDIF
     c                   endsr
      *
      *********************************************************
     C     INIT          Begsr
      *********************************************************
     C                   OPEN      BRIDF
      * Test innlogging
     C     WSUSER        CHAIN     BRIDF                              50
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C                   call      BILIBL
     C                   end
      *
     C                   Z-ADD     0             WTN               2 0
     C                   Z-ADD     0             ZCIFTOT          18 4
     C                   Z-ADD     0             ZCIFTOT2         18 4
     C     *LIKE         DEFINE    SVEH_FABL     ZVEH_FABL
N02  C     *LIKE         DEFINE    SVEH_VUFR     ZVEH_VUFR
     C     *LIKE         DEFINE    SVVK_KD       ZVVK_KD
     C     *LIKE         DEFINE    SVVK_DTS      ZVVK_DTS
     C     SVTVKKEY      KLIST
     C                   KFLD                    ZVVK_KD
     C                   KFLD                    ZVVK_DTS
      *
     C   50              eval      Error = 'Invalid userID'
     C  N50              OPEN      SVTVK
      *_________________________________
      * Hæmta dagens datum och klockslag
      *"""""""""""""""""""""""""""""""""
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6

     c                   endsr
      *
      *_________________________________________________
      * Avgiftsberækning                             AVG
      *"""""""""""""""""""""""""""""""""""""""""""""""""
     C     AVG           BEGSR
      * Rækna ut totalt CIF værde
     C                   EXSR      SRCIFTOT
      * Starta med Statistiskt værde
     C                   EXSR      SRCIFDET
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Berækna CIF totalt                              SRCIFTOT /
      *////////////////////////////////////////////////////////////
     C     SRCIFTOT      BEGSR
      *__________________________________
      * Summera alla kostnader och avdrag
      *""""""""""""""""""""""""""""""""""
      * Fakturabelopp i Svenska kronor
     C     SVEH_FABL     MULT(H)   SVEH_VAKU     ZVEH_FABL
     C                   EVAL      ZVVK_KD=SVEH_VAKD
     C                   EVAL      ZVVK_DTS=99999999
     C     SVTVKKEY      SETGT     SVTVK
     C                   READP     SVTVK                                  64
N02  C                   DOW       *IN64 = *OFF
N02  c                   IF        SVEH_SYDT >= SVVK_DTS
N02  C                   LEAVE
N02  C                   END
N02  C                   READP     SVTVK                                  64
N02  c                   ENDDO
     C                   IF        SVVK_OMR > 0
     C     ZVEH_FABL     DIV       SVVK_OMR      ZVEH_FABL
     C                   ENDIF
     C                   EVAL      ZCIFTOT=ZVEH_FABL
     C                   EVAL      ZCIFTOT2=ZVEH_FABL
N02   * Transport CIF
N02  C                   IF        SVEH_VUFR > 0
N02  C                   EVAL      *IN64=*OFF
N02  C     SVEH_VUFR     MULT      SVEH_VUKU     ZVEH_VUFR
N02  C                   EVAL      ZVVK_KD=SVEH_VUVA
N02  C                   EVAL      ZVVK_DTS=99999999
N02  C     SVTVKKEY      SETGT     SVTVK
N02  C                   READP     SVTVK                                  64
N02  C                   DOW       *IN64 = *OFF
N02  c                   IF        SVEH_SYDT >= SVVK_DTS
N02  C                   LEAVE
N02  C                   END
N02  C                   READP     SVTVK                                  64
N02  c                   ENDDO
N02  C                   IF        SVVK_OMR > *ZEROS
N02  C     ZVEH_VUFR     DIV       SVVK_OMR      ZVEH_VUFR
N02  C                   ENDIF
N02  C                   ADD       ZVEH_VUFR     ZCIFTOT
N02  C                   ENDIF
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Berækna CIF før varupost                        SRCIFDET /
      *////////////////////////////////////////////////////////////
     C     SRCIFDET      BEGSR
     C                   IF        SVEH_FABL = ZCIFTOT
     C                   IF        SVEV_STVA = *ZEROS
     C                   EVAL      SVEV_STVA = SVEV_FABL
     C                   ENDIF
     C                   IF        SVEV_STVA2 = *ZEROS
     C                   EVAL      SVEV_STVA2 = SVEV_FABL
     C                   ENDIF
     C                   ELSE
     C                   IF        SVEH_FABL > 0
     C     SVEV_FABL     DIV       SVEH_FABL     ZFAKTOR          18 8
     C                   ENDIF
     C                   IF        SVEV_STVA = *ZEROS
     C     ZCIFTOT       MULT      ZFAKTOR       SVEV_STVA
     C                   ENDIF
     C                   IF        SVEV_STVA2 = *ZEROS
     C     ZCIFTOT2      MULT      ZFAKTOR       SVEV_STVA2
     C                   ENDIF
     C                   ENDIF
     C                   IF        SVEV_FABL > *ZEROS
     C                   IF        SVEV_STVA = *ZEROS
     C                   EVAL      SVEV_STVA = 1
     C                   ENDIF
     C                   IF        SVEV_STVA2 = *ZEROS
     C                   EVAL      SVEV_STVA2 = 1
     C                   ENDIF
     C                   ENDIF
     C                   ENDSR
