      *
      ***********************************************************
      *** PROGRAM SKAL SENDE UniversalEvent til CW1 (CES)     ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01-26                            ***
      *** LEDIGE INDIKATORER 27-32 35-99                      ***
      ***********************************************************
     H DFTACTGRP(*NO)
     FFIRM      IF   E           K DISK
     FFRISOK    IF   E           K DISK
     FSADH      IF   E           K DISK
     FSAEH      IF   E           K DISK
     FEDIC      UF A E             DISK
     FEDIS      UF A E           K DISK
     FEDIM      O  A E             DISK
     FTRACKL02  IF A E           K DISK
      /copy ifsio_h
     D X_S0020         S             14A
     D XTYPE           S              2A
     D XTYPE1          S             10A
     D XTYPE2          S             10A
      *
     D FILE_o          s             10I 0
     D String          s            512a
     D rc              s             10i 0
     D Idx             s              5u 0
     D FILNAM          s            100A
     D XCHGAUT         s            200A
     D LF              c                   x'25'
     D RunCmd          PR                  EXTPGM('QCMDEXC')
     D  Cmd                         200A   CONST
     D  Len                          15P 5 CONST
     D                SDS
     D  ZUSER                254    263
      *
XXX  C*                  EVAL      *INLR = *ON
XXX  C*                  RETURN
     C                   EXSR      INIT
      *
     C                   EXSR      SRSNDST
      *
     C                   EXSR      SRUNIEVT
      *
     C                   EXSR      OPDEDIM
      *
     C                   EXSR      OPDTRACK
      *
     C                   EXSR      SRCLOSE
      *
     C                   EXSR      SRSNDSL
      *
     C                   IF        UTYPE = 'REL' OR UTYPE = 'CAN'               DTK/DIU
      *
      * SEND CLEARED
     C                   SELECT
     C                   WHEN      UIE = 'IMP'
     C                   EVAL      UTYPE2 = 'CLR'
     C                   WHEN      UIE = 'EXP'
     C                   EVAL      UTYPE2 = 'ECC'
     C                   ENDSL
     C                   CALL      'SYXML54'
     C                   PARM                    UAVD
     C                   PARM                    UTDN
     C                   PARM                    UTYPE2            3
     C                   PARM                    UIE
      *
     C                   END
      *
     C                   IF        UTYPE = 'REL'                                DTK
      * SEND PDF
     C                   EVAL      UTYPE2 = 'DDI'
     C                   CALL      'SYXML55'
     C                   PARM                    UAVD
     C                   PARM                    UTDN
     C                   PARM                    UTYPE2            3
     C                   PARM                    UREF             17
     C                   PARM                    UIE
      *
     C                   SELECT
     C                   WHEN      UIE = 'IMP'
      * SEND toll, avgift og moms
     C                   CALL      'SYXML56I'
     C                   PARM                    UAVD
     C                   PARM                    UTDN
     C                   PARM                    UREF
     C                   WHEN      UIE = 'EXP'
      * SEND avgift
     C                   CALL      'SYXML56E'
     C                   PARM                    UAVD
     C                   PARM                    UTDN
     C                   PARM                    UREF
     C                   ENDSL
      *
     C                   END
      *
     C                   MOVE      *ON           *INLR
     C                   RETURN
      *
      ***********************************************
     C     SRSNDST       BEGSR
      ***********************************************
      *
     C     1             CHAIN     EDIC                               33
     C                   ADD       1             CSN
     C                   UPDATE    EDICR
      *
     C                   MOVE      *BLANKS       SDATA          1024
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
      *
     C                   EVAL      S0004 = X0004
     C                   EVAL      S0010 = X0010
     C                   EVAL      S0020 = %TRIM(X0020) + '-' + %TRIM(UTYPE)
     C                   Z-ADD     1             S0036
     C                   EVAL      SSN   = CSN
     C                   EVAL      SSR   = 'S'
     C                   EVAL      SST   = 'X'
     C                   MOVE      WDT           SDT
     C                   MOVE      WTM           STM
     C                   EVAL      SLIB  = *BLANKS
     C                   EVAL      SFILE = '*IFS'
     C                   EVAL      SMBR  = *BLANKS
     C                   EVAL      SMBRS  = 0
      *
     C                   EVAL      FILNAM = '/' + %TRIM(FILIBF) + 'X/'
     C                             + %TRIM(S0010) + '/EDISE1K/BACKUP/'
     C                             + %TRIM(S0020) + %TRIM(WDT)
     C                             + %TRIM(WTM) + '.xml'
      * 1208 = UTF-8
     C                   eval      FILE_o = open( %TrimR( FilNam )
     C                               : O_CREAT+O_TRUNC+O_WRONLY
     C                                + O_TEXT_CREAT+O_CCSID+O_TEXTDATA
     C                               : S_IRUSR + S_IWUSR
     C                               : 1208
     C                               : 0)
      *
     C                   EVAL      SIFS   = FILNAM
     C                   WRITE     EDISR
      *
     C                   EVAL      XSSN = SSN
      *
     C                   EVAL      SDATA = '<?xml version="1.0" '
     C                             + 'encoding="UTF-8"?>'
     C                   EXSR      OPDFIL
      *
     C                   ENDSR
      *
      ***********************************************
     C     SRUNIEVT      BEGSR
      ***********************************************
      * <UniversalEvent>
     C                   EVAL      SDATA = '<UniversalEvent>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '<Event>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '<EventTime>' + %SUBST(WDT:1:4) + '-'
     C                             + %SUBST(WDT:5:2) +'-'+ %SUBST(WDT:7:2) +'T'
     C                             + %SUBST(WTM:1:2) +':'+ %SUBST(WTM:3:2) +':'
     C                             + %SUBST(WTM:5:2) + '</EventTime>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '<EventType>CES</EventType>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '<EventReference>' + %TRIM(XTYPE)
     C                             + '</EventReference>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '<ContextCollection>'
     C                   EXSR      OPDFIL
      *
     C                   EVAL      SDATA = '<Context>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '<Type>EntryNumber</Type>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '<Value>'
     C                             + %TRIM(UAVD) + '-' + %TRIM(UTDN)            Avdeling/oppdragsnr
     C                             + '</Value>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '</Context>'
     C                   EXSR      OPDFIL
      *
     C                   EVAL      SDATA = '<Context>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '<Type>DeclarationReference</Type>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '<Value>' + %TRIM(X0020) +'</Value>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '</Context>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '<Context>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '<Type>EntryNumberType</Type>'
     C                   EXSR      OPDFIL
     C                   SELECT
     C                   WHEN      UIE = 'IMP'
     C                   EVAL      SDATA = '<Value>IMP</Value>'
     C                   WHEN      UIE = 'EXP'
     C                   EVAL      SDATA = '<Value>EXP</Value>'
     C                   ENDSL
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '</Context>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '<Context>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '<Type>EntryNumberCountryOfIssue'
     C                             + '</Type>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '<Value>NO</Value>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '</Context>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '</ContextCollection>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '</Event>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '</UniversalEvent>'
     C                   EXSR      OPDFIL
      *
     C                   ENDSR
      *
      ***********************************************
     C     SRCLOSE       BEGSR
      ***********************************************
      *
     C                   callp     close( FILE_o )
     C                   EVAL      XCHGAUT = 'CHGAUT OBJ(' + XAPS
     C                             + %TRIM(FILNAM) + XAPS
     C                             + ') USER(*PUBLIC) DTAAUT(*RWX) OBJAUT(*ALL)'
     C                   CALLP     RUNCMD(XCHGAUT:200)
      * CHGATR OBJ('home/cb/a.xml') ATR(*CCSID) VALUE(1208)
     C                   MOVE      *BLANKS       UCMD           1024
     C                   Z-ADD     1024          UCMDL            15 5
     C                   EVAL      UCMD = 'CHGATR OBJ(' + XAPS
     C                             + %TRIM(FILNAM) + XAPS + ') '
     C                             + 'ATR(*CCSID) VALUE(1208)'
     C                   CALL      'QCMDEXC'                            33
     C                   PARM                    UCMD
     C                   PARM                    UCMDL
      *
     C                   ENDSR
      *
      ***********************************************
     C     SRSNDSL       BEGSR
      ***********************************************
      *
     C     XSSN          CHAIN     EDIS                               33
     C  N33              MOVE      'X'           SST
     C  N33              UPDATE    EDISR
      *
     C                   ENDSR
      *
      ***********************************************
     C     OPDEDIM       BEGSR
      ***********************************************
     C     1             CHAIN     EDIC                               33
     C                   ADD       1             CMN
     C                   UPDATE    EDICR
      *
     C                   EVAL      M0004 = S0004
     C                   EVAL      M0010 = S0010
     C                   MOVEL     CMN           M0062
     C                   EVAL      M0065 = 'UNIEVT'
     C                   EVAL      M0068 = %TRIM(X0020) + '-' + %TRIM(UTYPE)
     C                   EVAL      M1001 = '929'
     C                   EVAL      M1004 = X0020
     C                   EVAL      M1225 = 'EVT'
     C                   EVAL      M1N07 = %TRIM(UTYPE)
     C                   EVAL      MSN = SSN
     C                   EVAL      MMN = CMN
     C                   EVAL      MSR = 'S'
     C                   EVAL      MST = 'C'
     C                   MOVE      WDT           MDT
     C                   MOVE      WTM           MTM
     C                   MOVE      UAVD          MAVD
     C                   MOVE      UTDN          MTDN
     C                   WRITE     EDIMR
      *
     C                   ENDSR
      *
      ***********************************************
     C     OPDTRACK      BEGSR
      ***********************************************
      *
     C                   EVAL      TTAVD  = MAVD
     C                   EVAL      TTOPD  = MTDN
     C                   EVAL      TTACTI = 'CW1'
     C                   EVAL      TTDATE = MDT
     C                   EVAL      TTTIME = MTM
     C                   EVAL      TTUSER = ZUSER
     C                   EVAL      TTDATL = MDT
     C                   EVAL      TTTIML = MTM
     C                   EVAL      TTEDEV = 'CES'
     C                   EVAL      TTEDRE = UTYPE
     C                   EVAL      TTMANU = 'E'
     C                   EVAL      TTNAME = 'SYXML53'
     C                   EVAL      TTTEXT = 'CW1-Shipment(' + %TRIM(X0020)
     C                             + ') is ' + %TRIM(XTYPE1) + ' by customs.'
     C                   EVAL      TTTEXL = 'CW1-Shipment(' + %TRIM(X0020)
     C                             + ') er ' + %TRIM(XTYPE2) + ' av TVINN.'
     C                   WRITE     TRACKFR
      *
     C                   ENDSR
      *
      ***********************************************
     C     OPDFIL        BEGSR
      ***********************************************
     C                   EVAL      SDATA=(%trimr(SDATA))+ LF
     c                   callp     write(File_o : %addr(Sdata)
     c                                : %len(%trimr(Sdata)) )
      *
     C                   ENDSR
      *
      ***********************************************
     C     INIT          BEGSR
      ***********************************************
     C     FRISOKK       KLIST
     C                   KFLD                    FSAVD
     C                   KFLD                    FSOPD
     C                   KFLD                    FSKODE
     C     TRACKL02K     KLIST
     C                   KFLD                    FSAVD
     C                   KFLD                    FSOPD
     C                   KFLD                    TTFBNR
     C                   KFLD                    TTACTI
     C     SADKEY        KLIST
     C                   KFLD                    FSAVD
     C                   KFLD                    FSOPD
      *
     C     *ENTRY        PLIST
     C                   PARM                    UAVD              4
     C                   PARM                    UTDN              7
     C                   PARM                    UTYPE             3
     C                   PARM                    UIE               3
      *
     C     *LIKE         DEFINE    S0004         X0004
     C     *LIKE         DEFINE    S0010         X0010
     C     *LIKE         DEFINE    S0020         X0020
     C     *LIKE         DEFINE    SSN           XSSN
     C                   MOVE      X'7D'         XAPS              1
     C                   MOVE      UAVD          FSAVD
     C                   MOVE      UTDN          FSOPD
      *
     C     *LOVAL        SETLL     FIRM
     C                   READ      FIRM                                   33
     C                   SELECT
     C                   WHEN      UIE = 'IMP'
     C                   EVAL      FSKODE = '*CR'
     C                   WHEN      UIE = 'EXP'
     C                   EVAL      FSKODE = '*CE'
     C                   ENDSL
     C     FRISOKK       CHAIN     FRISOK                             33
     C  N33              MOVEL     FSSOK         X0020                          CW1 ORDRE NR
     C  N33              EVAL      FSKODE = '*CA'
     C  N33FRISOKK       CHAIN     FRISOK                             33
     C  N33              MOVEL     FSSOK         X0010                          CW1 AVSENDER
     C  N33              EVAL      FSKODE = '*CM'
     C  N33FRISOKK       CHAIN     FRISOK                             33
     C  N33              MOVE      FSSOK         X0004                          CW1 MOTTAKER
     C                   IF        *IN33 = *OFF
     C                   EVAL      TTACTI = 'CW1'
     C     TRACKL02K     SETLL     TRACKL02
     C     TRACKL02K     READE     TRACKL02                               34
     C                   DOW       *IN34 = *OFF
     C                   IF        UTYPE = 'IMP' AND TTEDRE = 'CLR'
     C                             OR UTYPE = 'EXP' AND TTEDRE = 'ECC'
      * NÅR TOLLDEKL. ER KLARERT, KAN MAN IKKE SENDE FLERE EVENTS.
     C                   LEAVE
     C                   END
     C     TRACKL02K     READE     TRACKL02                               34
     C                   ENDDO
     C  N34              EVAL      *IN33 = *ON
     C*                  EVAL      *IN33 = *OFF
     C                   END
      *
      * *IN33 på -> Ikke CW1-oppdrag, avslutt
     C   33              EVAL      *INLR = *ON
     C   33              RETURN
      *
     C                   SELECT
     C                   WHEN      UTYPE = 'REL'                                DTK
     C                   EVAL      XTYPE  = '01'
     C                   EVAL      XTYPE1 = 'Released'
     C                   EVAL      XTYPE2 = 'godkjent'
     C                   WHEN      UTYPE = 'REJ'                                Avvist
     C                   EVAL      XTYPE  = 'XX'
     C                   EVAL      XTYPE1 = 'Rejected'
     C                   EVAL      XTYPE2 = 'avvist'
     C                   WHEN      UTYPE = 'CAN'                                Kansellert
     C                   EVAL      XTYPE = 'Cancelled'
     C                   EVAL      XTYPE1 = '10'
     C                   EVAL      XTYPE2 = 'avbrutt'
     C                   WHEN      UTYPE = 'MAN'                                Manuell behandling
     C                   EVAL      XTYPE  = '02'
     C                   EVAL      XTYPE1 = 'Manuelly processing'
     C                   EVAL      XTYPE2 = 'manuell behandling'
     C                   WHEN      UTYPE = 'DKT'                                Dokument kontroll
     C                   EVAL      XTYPE  = '03'
     C                   EVAL      XTYPE1 = 'Document controll'
     C                   EVAL      XTYPE2 = 'dokuemnt kontroll'
     C                   WHEN      UTYPE = 'VKT'                                Vare kontroll
     C                   EVAL      XTYPE  = '04'
     C                   EVAL      XTYPE1 = 'Items controll'
     C                   EVAL      XTYPE2 = 'varekontroll'
     C                   WHEN      UTYPE = 'MSG'                                Melding     l
     C                   EVAL      XTYPE  = '05'
     C                   EVAL      XTYPE1 = 'Message'
     C                   EVAL      XTYPE2 = 'melding'
     C                   OTHER
     C                   EVAL      XTYPE = '*Unknow'
     C                   EVAL      XTYPE1 = '00'
     C                   EVAL      XTYPE2 = '*Ukjent'
     C                   ENDSL
      *
     C                   SELECT
     C                   WHEN      UIE = 'IMP'
     C     SADKEY        CHAIN     SADH                               33
     C                   EVAL      UREF = %TRIM(SITLL) + '-' + %TRIM(SITLE)
     C                   WHEN      UIE = 'EXP'
     C     SADKEY        CHAIN     SAEH                               33
     C                   EVAL      UREF = %TRIM(SETLL) + '-' + %TRIM(SETLE)
     C                   ENDSL
      *
     C                   ENDSR
      *
