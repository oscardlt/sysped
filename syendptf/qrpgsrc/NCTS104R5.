      *****************************************************************
      *** PROGRAM SKAL KONVERTERE INNGÅENDE IE51                    ***
      *** No Release for transit                                    ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER          ***
      *** LEDIGE INDIKATORER 01-26                                  ***
      *** LEDIGE INDIKATORER 28-32 34-97                            ***
      *****************************************************************
     H DFTACTGRP(*NO)
     FXMLRCVF   IF   E           K DISK
     FXMLRCLF   IF   E           K DISK
     FTRACKF    O  A E           K DISK
     FNCTSEH    UF   E           K DISK
     FTVINF     O  A E           K DISK
     FTRKODL01  IF   E           K DISK
     FEDIC      UF   E             DISK
     FEDIM      O  A E             DISK
     FEDIM5     IF   E           K DISK    RENAME(EDIMR:EDIMR5)
     D                sds
     D ZUSER                 254    263
      *  Prototype for 'SYGE15C'
     d syge15c         pr                  extpgm('SYGE15C')
     d wdt_                                like(wdt)
     d wtm_                                like(wtm)
      *  Prototype for ncts104r5
     d ncts104r5       pr
     d usn_                                like(usn)
      *  *ENTRY Interface for Main Procedure
     d ncts104r5       pi
     d usn                            9
      *----------------------------------------------------------------------
      * Stand Alone Fields - TOP
      *----------------------------------------------------------------------
     d x1_xrlev1       s                   like(xrlev1)
     d x1_xrlev2       s                   like(xrlev2)
     d x2_xrlev1       s                   like(xrlev1)
     d x2_xrlev2       s                   like(xrlev2)
     d x2_xrlev3       s                   like(xrlev3)
     d x3_xrlev1       s                   like(xrlev1)
     d x3_xrlev2       s                   like(xrlev2)
     d x3_xrlev3       s                   like(xrlev3)
     d x3_xrlev4       s                   like(xrlev4)
     d xn              s              3s 0
     d x_thtrdt        s              8
     d x_thtrnr        s                   like(thtrnr)
     d x_IE51          s              1a
     d x_sn            s              3s 0
     d x_xrvrd         s                   like(xrvrd)
     d wdt             s              8
     d wtm             s              6
     d                 ds
     d x_rdt                   1      8  0
     d x_rdta                  1      8
     d                 ds
     d x_rtm                   1      6  0
     d x_rtma                  1      6
     d                 ds
     d f0078a                  1     70a
     d f0078b                 71    140a
     d f0078c                141    210a
     d f0078d                211    280a
     d f0078e                281    350a
     d ft                      1    350a   dim(5)
      *
     D UP              C                   CONST('ABCDEFGHIJKLMNOPQRST-
     D                                     UVWXYZÆØÅ')
     D LO              C                   CONST('abcdefghijklmnopqrst-
     D                                     uvwxyzæøå')
      *----------------------------------------------------------------------
     IEDIMR5
     I              M0004                       X0004
     I              M0010                       X0010
     I              M0035                       X0035
     I              M0062                       X0062
     I              M0065                       X0065
     I              M0068                       X0068
     I              M0068A                      X0068A
     I              M0068B                      X0068B
     I              M0068C                      X0068C
     I              M0068D                      X0068D
     I              M0068E                      X0068E
     I              M0068F                      X0068F
     I              M1001                       X1001
     I              M1004                       X1004
     I              M1225                       X1225
     I              M2005B                      X2005B
     I              M3039D                      X3039D
     I              M3039E                      X3039E
     I              M5004D                      X5004D
     I              M1N07                       X1N07
     I              M1N08                       X1N08
     I              M9N01                       X9N01
     I              MSN                         XSN
     I              MMN                         XMN
     I              MSR                         XSR
     I              MST                         XST
     I              MDT                         XDT
     I              MTM                         XTM
     I              MVEN                        XVEN
     I              MAVD                        XAVD
     I              MTDN                        XTDN
      /free

       exsr init;

       exsr srxml;

       *inlr = *on;
       return;

       //**********************************************
       begsr srxml;
       //**********************************************

         x1_xrlev1 = 0;
         x1_xrlev2 = 0;

         chain (xrsn:x1_xrlev1:x1_xrlev2) xmlrcvf;
         dow %found;
           select;
           when xrnv = 'messageSender';
             m0004  = xrverd;
           when xrnv = 'messageRecipient';
             m0010  = xrverd;
           when xrnv = 'preparationDateAndTime';
           when xrnv = 'messageIdentification';
             m0062  = xrverd;
             m1004  = xrverd;
           when xrnv = 'messageType' and xrverd = 'CC051C';
             x_ie51 = 'J';
             chain 1 edic;
             cmn = cmn + 1;
             update edicr;
             fmn = cmn;
             mmn = cmn;
           when xrnv = 'correlationIdentifier';
           when xrnv = 'TransitOperation';
             exsr srxml1;
           when xrnv = 'CustomsOfficeOfDeparture';
           when xrnv = 'HolderOfTheTransitProcedure';
           when xrnv = 'FunctionalError';
             exsr srxml2;
           endsl;

           x1_xrlev1 = x1_xrlev1 + 1;
           chain (xrsn:x1_xrlev1:x1_xrlev2) xmlrcvf;
         enddo;

         if x_ie51 = 'J';
           exsr opdopd;
           exsr ttsr;
         endif;

         endsr;

       //**********************************************
       begsr srxml1;
       //**********************************************

         ft    = *blanks;
         fln   = fln + 1;
         xn    = 0;

         x2_xrlev1 = xrlev1;
         x2_xrlev2 = 1;
         x2_xrlev3 = 0;

         chain (xrsn:x2_xrlev1:x2_xrlev2:x2_xrlev3) xmlrcvf;
         dow %found;
           select;
           when xrnv = 'LRN';
             x0068 = xrverd;
           when xrnv = 'MRN';
             x_thtrnr = xrverd;
             if x0068 = *blanks;
               x0068 = xrverd;
             endif;
           when xrnv = 'businessRejectionType';
             f0077 = xrverd;
           when xrnv = 'rejectionDateAndTime';
             x_rdta = %subst(xrverd:1:4) + %subst(xrverd:6:2)
                   + %subst(xrverd:9:2);
             x_rtma = %subst(xrverd:12:2) + %subst(xrverd:15:2)
                   + %subst(xrverd:18:2);
             xn     = xn + 1;
             ft(xn) = 'Rejection date and time: ' + %trim(xrverd);
           when xrnv = 'rejectionCode';
             ft(xn) = %trim(ft(xn)) + '  Code: ' + %trim(xrverd);
           when xrnv = 'rejectionReason';
             if xrverd = '*EXT, TILLEGGSFIL';
               chain (xrsn:x2_xrlev1:x2_xrlev2:x2_xrlev3:xrlev4) xmlrclf;
             else;
               xrvrd = xrverd;
             endif;
             xn     = xn + 1;
             ft(xn) = %subst(xrvrd:1:70);
             xn     = xn + 1;
             ft(xn) = %subst(xrvrd:71:70);
             xn     = xn + 1;
             ft(xn) = %subst(xrvrd:141:70);
             xn     = xn + 1;
             ft(xn) = %subst(xrvrd:211:70);
             if xn < 5 and %subst(xrvrd:281:70) <> *blanks;
               ft(5) = %subst(xrvrd:281:70);
             endif;
             write tvinff;
           endsl;

           x2_xrlev2 = x2_xrlev2 + 1;
           chain (xrsn:x2_xrlev1:x2_xrlev2:x2_xrlev3) xmlrcvf;
         enddo;

       endsr;

       //**********************************************
       begsr srxml2;
       //**********************************************

         x2_xrlev1 = xrlev1;
         x2_xrlev2 = 1;
         x2_xrlev3 = 0;

         chain (xrsn:x2_xrlev1:x2_xrlev2:x2_xrlev3) xmlrcvf;
         dow %found;
           select;
           when xrnv = 'errorPointer';
             ft     = *blanks;
             xn     = 0;
             fln    = fln + 1;
             f0077  = 'ERR';
             if xrverd = '*EXT, TILLEGGSFIL';
               chain (xrsn:x2_xrlev1:x2_xrlev2:x2_xrlev3:xrlev4) xmlrclf;
             else;
               xrvrd = xrverd;
             endif;
             ft(1)  = %subst(xrvrd:1:70);
             ft(2)  = %subst(xrvrd:71:70);
             ft(3)  = %subst(xrvrd:141:70);
             ft(4)  = %subst(xrvrd:211:70);
             ft(5)  = %subst(xrvrd:281:70);
             write tvinff;
           when xrnv = 'errorCode';
             f0077  = xrverd;
           when xrnv = 'errorReason';
             ft     = *blanks;
             xn     = 0;
             fln    = fln + 1;
             if xrverd = '*EXT, TILLEGGSFIL';
               chain (xrsn:x2_xrlev1:x2_xrlev2:x2_xrlev3:xrlev4) xmlrclf;
             else;
               xrvrd = xrverd;
             endif;
             ft(1)  = %subst(xrvrd:1:70);
             ft(2)  = %subst(xrvrd:71:70);
             ft(3)  = %subst(xrvrd:141:70);
             ft(4)  = %subst(xrvrd:211:70);
             ft(5)  = %subst(xrvrd:281:70);
             write tvinff;
           endsl;

           x2_xrlev2 = x2_xrlev2 + 1;
           chain (xrsn:x2_xrlev1:x2_xrlev2:x2_xrlev3) xmlrcvf;
         enddo;

       endsr;

       //**********************************************
       begsr opdopd;
       //**********************************************

         chain x0068 edim5;
         if %found;
           chain (xavd: xtdn) nctseh;
           if %found;
             thst = 'S';
             update nctsehr;
           endif;
         endif;

         m0065  = 'XMLRES';
         m0068  = x0068;
         m1001  = 'TET';
         m1225  = '051';
         msn    = xrsn;
         msr    = 'R';
         mst    = 'C';
         mdt    = %float(wdt);
         mtm    = %float(wtm);
         m0068a = x0068a;
         m0068b = x0068b;
         m1n07  = '051';
         mavd   = thavd;
         mtdn   = thtdn;
         write edimr;

       endsr;

       //**********************************************
       begsr ttsr;
       //**********************************************

         ttavd  = thavd;
         ttopd  = thtdn;
         ttacti = 'T51';
         ttdate = %float(wdt);
         tttime = %float(wtm);
         ttuser = zuser;
         tttext = 'Rejection from office of departure: '
                  + %trim(%editw(x_rdt:'    .  .  '))
                  + ' ' + %trim(%editw(x_rtm:'  :  :  '));
         tttexl = 'Avvist av avsenders tollkontor: '
                  + %trim(%editw(x_rdt:'    .  .  '))
                  + ' ' + %trim(%editw(x_rtm:'  :  :  '));
         write trackfr;

       endsr;

       //**********************************************
       begsr init;
       //**********************************************

         xrsn = %dec(usn:9:0);
         syge15c ( wdt : wtm );

       endsr;

      /end-free
