********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10003 * 01 YBC PTF10 KLARGJØR TIL EKSTERN REFERANSE
xxxxxx*xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
11133 * 02 YBC PTF11 VEKT ER ENDRET FRA 9.0 TIL 11.3, EGET FELT FOR GARANTI
********************************************************************
      *
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      CA03(03 'Avslutt program')
     A                                      CF04(04 'Søking')
     A                                      CA12(12 'Gå til forrige bilde')
     A                                      HLPPNLGRP(HLPSCR SVX003PNL)
     A                                      HLPTITLE('VEDLIKEHOLD AV AVDELINGSO-
     A                                      PPLYSNINGER TET')
     A                                      HELP
     A                                      ALTHELP
      *
     A          R SV003D01
     A                                  1  3'SYSPED/SVX003R'
     A                                      COLOR(BLU)
     A                                  1 20'Underhåll standardupplysningar'
     A                                      COLOR(WHT)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(GRN)
     A                                  1 70TIME
     A                                      COLOR(GRN)
     A                                  2  3'Firmakod'
     A            FIFIRM         2A  O  2 16TEXT('FIRMAKODE')
     A                                      COLOR(TRQ)
     A                                  2 32'NCTS Export'
     A                                      COLOR(WHT)
     A                                  2 58MSGCON(022 DIV0047 *LIBL/SYMSGF)
     A                                      COLOR(TRQ)
     A                                  4  2'Vælj och tryck Enter:'
     A                                      COLOR(BLU)
     A                                  6  2'Avdelning  . . . . . . . . . . . :'
     A            WSAAVD         4Y 0B  6 39DSPATR(UL)
     A  99                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                 23  2'F3=Avsluta'
     A                                      COLOR(BLU)
     A  99                             24  2MSGCON(078 SPA0001 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
      *
     A          R SV003D02
     A            MSGNR          7A  P
     A                                  1  3'SYSPED/SVX003R'
     A                                      COLOR(BLU)
     A                                  1 20'Underhåll standardupplysningar'
     A                                      COLOR(WHT)
     A                                  1 76'Del-1'
     A                                      COLOR(YLW)
     A                                  2 28'NCTS Export'
     A                                      COLOR(WHT)
     A                                  4  2'Avdelning:'
     A            WSAAVD         4Y 0O  4 15TEXT('AVDELINGSNR')
     A                                      COLOR(BLU)
     A                                      EDTCDE(4)
     A            KOANVN        33A  O  4 28TEXT('AVDELINGSNAVN')
     A                                      COLOR(BLU)
     A                                  6  2'Vælj och tryck Enter:'
     A                                      COLOR(BLU)
     A                                  7  4'Antal exemplar føljesedel:'
     A            WSNTTD         1Y 0B  7 40TEXT('Antall følgeseddel')
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
     A                                  8  4'Antal exemplar lastlista:'
     A            WSNTLL         1Y 0B  8 40TEXT('Antall lasteliste')
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
     A                                  9  4'Tryck formular før lastlista:'
     A            WSFMLL         1A  B  9 40DSPATR(UL)
     A                                      VALUES(' ' 'J' 'N')
     A                                  9 48'(J=Ja, N=Nej)'
     A                                 10  4'Sist anvænda internnummer:'
     A            WSTDN          7Y 0B 10 40TEXT('TELLER TIL SAD-IMPORT')
     A                                      DSPATR(UL)
     A  98                                  DSPATR(RI)
     A  98                                  DSPATR(PC)
     A                                      EDTCDE(4)
     A                                 12  2'Vælj och tryck Enter:'
     A                                      COLOR(BLU)
     A                                 13  4'Førenklad procedur ?'
     A            WSENKL         1A  B 13 40TEXT('FORENKLET PROSEDYRE?')
     A                                      DSPATR(UL)
     A                                      VALUES('J' 'N')
     A                                 13 48'(J=Førenklad, N=Normal)'
     A                                 14  4'UNB ID Avsændare:'
     A            W0004         35A  B 14 40TEXT('NODI ID AVDELINGEN')
     A                                      DSPATR(UL)
     A  95                                  DSPATR(RI)
     A  95                                  DSPATR(PC)
     A  95                                  COLOR(RED)
     A                                 15  6'UNB ID Mottagare:'
     A            W0010         35A  B 15 40TEXT('NODI ID TOLLVESENET')
     A                                      DSPATR(UL)
     A  94                                  DSPATR(RI)
     A  94                                  DSPATR(PC)
     A  94                                  COLOR(RED)
     A                                 16  8'Application Reference:'
     A            W0026         14A  B 16 40
     A                                 17  4MSGCON(033 DIV1723 *LIBL/SYMSGF)
     A            W0035          1A  B 17 40TEXT('Testindikator')
     A                                      VALUES(' ' '1' '2')
N01  A                                 19  4'Importeres eksterne data ? '
N01  A            WSEKST         1   B 19 40VALUES('J' 'N' 'R')
N01  A                                 19 48'J/N/R  Ved R kan egen ref'
N01  A                                 20 48'inntastes for søking'
     A                                 23  2'F3=Avsluta utan ændring'
     A                                      COLOR(BLU)
     A                                 23 28'F12=Gå til førra utan ændring  '
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
      *
     A          R SV003D03
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1  3'SYSPED/SVX003R'
     A                                      COLOR(BLU)
     A                                  1 22'NCTS Export standardværden'
     A                                      COLOR(WHT)
     A                                  1 52'Ref.'
     A            WSAAVD         4Y 0O  1 57COLOR(WHT)
     A                                      EDTCDE(4)
     A            WSTDN          7Y 0O  1 62COLOR(WHT)
     A                                      EDTCDE(4)
     A            THSG           3A  O  1 70COLOR(WHT)
     A                                  1 76'Del-2'
     A                                      COLOR(YLW)
     A                                  2  2'1.Dekl.'
     A            WSDK           5A  B  2 10TEXT('Deklarasjonstype')
     A                                      DSPATR(UL)
     A  99                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                  2 16'4.L.lista'
     A            WSLSTL         4Y 0B  2 26EDTCDE(4)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  2 31'5.V.pos'
     A            WSVPOS         5Y 0B  2 39EDTCDE(4)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  2 45'6.Kolli'
     A            WSNTK          7Y 0B  2 53EDTCDE(4)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  2 61'B.vikt'
S02  A*           WSVKB          9Y 1B  2 68EDTCDE(4)
N02  A            WSVKB         11Y 3B  2 68EDTCDE(4)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  3  2'2.Avsænd. '
     A            WSKNS          8Y 0B  3 13TEXT('Kundenr selger')
     A                                      DSPATR(UL)
     A  98                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSTINS        17A  B  3 22TEXT('Tin kode selger')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  3 40'9.Skickas'
     A            WSTSN1        30A  B  3 50DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSNAS         30A  B  4  2TEXT('Navn selger')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  4 41'Tillbaka'
     A            WSTSN2        30A  B  4 50DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSADS1        30A  B  5  2TEXT('Adresse-1 selger')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  5 40'15.Avs/utf.land'
     A            WSALK          2A  B  5 56TEXT('Landk avs/utf-land')
     A                                      DSPATR(UL)
     A  97                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSPNS          9A  B  6  2TEXT('Postnr selger')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSPSS         24A  B  6 12TEXT('Poststed selger')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  6 40'17.Be.land'
     A            WSBLK          2A  B  6 56TEXT('Landk avs/utf-land')
     A                                      DSPATR(UL)
     A  96                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                  6 60'Reg.dat '
     A            THDT           8Y 0O  6 69TEXT('Registreringsdato')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTWRD('    .  .  ')
     A                                  7  2'Landkod '
     A            WSLKS          2A  B  7 12TEXT('Landkode selger')
     A                                      DSPATR(UL)
     A  95                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                  7 22'Språkkod '
     A            WSSKS          2A  B  7 32TEXT('Språkkode selger')
     A                                      DSPATR(UL)
     A  94                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                  7 40'18.Tr.midel  vid avgång ID/LK/SK'
     A                                  7 78'TM'
     A                                  8  2'8.Mottag. '
     A            WSKNK          8Y 0B  8 13TEXT('Kundenr kjøper')
     A                                      DSPATR(UL)
     A  93                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSTINK        17A  B  8 22TEXT('Tin kode kjøper')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSTAID        27A  B  8 43TEXT('Identitet')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSTALK         2A  B  8 71TEXT('Landkode')
     A                                      DSPATR(UL)
     A  92                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSTASK         2A  B  8 74TEXT('Språkkode')
     A                                      DSPATR(UL)
     A  91                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSTRMI         2Y 0B  8 78TEXT('Transportmåte')
     A                                      DSPATR(UL)
     A  63                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(9)
     A            WSNAK         30A  B  9  2TEXT('Navn kjøper')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  9 40'19.Cont.'
     A            WSKDC          1A  B  9 49TEXT('Container 0/1')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSADK1        30A  B 10  2TEXT('Adresse-1 kjøper')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 10 40'21.Tr.middel ved grense ID/LK/SK'
     A                                 10 75'TM'
     A            WSPNK          9A  B 11  2TEXT('Postnr kjøper')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSPSK         24A  B 11 12TEXT('Poststed kjøper')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSTGID        27A  B 11 43TEXT('Identitet')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSTGLK         2A  B 11 71TEXT('Landkode')
     A                                      DSPATR(UL)
     A  90                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSTGSK         2A  B 11 74TEXT('Språkkode')
     A                                      DSPATR(UL)
     A  89                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSTRM          2Y 0B 11 78TEXT('Transportmåte')
     A                                      DSPATR(UL)
     A  86                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(9)
     A                                 12  2'Landkod '
     A            WSLKK          2A  B 12 12TEXT('Landkode kjøper')
     A                                      DSPATR(UL)
     A  88                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 12 22'Språkkod '
     A            WSSKK          2A  B 12 32TEXT('Språkkode kjøper')
     A                                      DSPATR(UL)
     A  87                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 12 40'L.st:'
     A            WSLASD        17A  B 12 46TEXT('Laststed')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSLAS2        17A  B 12 64TEXT('Laststed')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 13  2'50. Ansvar'
     A            WSKNA          8Y 0B 13 13TEXT('Kundenr H.ansvalig')
     A                                      DSPATR(UL)
     A  85                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSTINA        17A  B 13 22TEXT('Tin kode H.ansvalig')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 13 40'Auto.lagringsplats'
     A            WSLSD         17A  B 13 60TEXT('AUTO.LAGRINGSSTED')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSNAA         30A  B 14  2TEXT('Navn H.ansvalig')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 14 40'Autoriserad næringsl.'
     A                                 14 64'Dat: '
     A            WSCDT          8Y 0B 14 70TEXT('Akseptdato')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSADA1        30A  B 15  2TEXT('Adresse-1 H.ansvalig')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSCNA         17A  B 15 40TEXT('Autorisert næringsliv')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSPNA          9A  B 16  2TEXT('Postnr H.ansvalig')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSPSA         24A  B 16 12TEXT('Poststed H.ansvalig')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSCNR         12A  B 16 40TEXT('Autorisasjonsnr')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 16 55'Reservp. nummerserie:'
     A            WSNPNS         1A  B 16 77DSPATR(UL)
     A  64                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 17  2'Landkod '
     A            WSLKA          2A  B 17 12TEXT('Landkode H.ansvalig')
     A                                      DSPATR(UL)
     A  84                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 17 22'Språkkod '
     A            WSSKA          2A  B 17 32TEXT('Språkkode H.ansvalig')
     A                                      DSPATR(UL)
     A  83                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 17 40'C. Avg. tullkont'
     A            WSCATS         8A  B 17 57TEXT('AVG.TOLLSTED')
     A                                      DSPATR(UL)
     A  82                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 18  2'51.'
     A            WSTSD1         8A  B 18  6TEXT('Transitt tollsted 1')
     A                                      DSPATR(UL)
     A  81                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSTSD2         8A  B 18 15TEXT('Transitt tollsted 2')
     A                                      DSPATR(UL)
     A  80                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSTSD3         8A  B 18 24TEXT('Transitt tollsted 3')
     A                                      DSPATR(UL)
     A  79                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSTSD4         8A  B 18 33TEXT('Transitt tollsted 4')
     A                                      DSPATR(UL)
     A  78                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSTSD5         8A  B 18 42TEXT('Transitt tollsted 5')
     A                                      DSPATR(UL)
     A  77                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSTSD6         8A  B 18 51TEXT('Transitt tollsted 6')
     A                                      DSPATR(UL)
     A  76                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSTSD7         8A  B 18 60TEXT('Transitt tollsted 7')
     A                                      DSPATR(UL)
     A  75                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSTSD8         8A  B 18 69TEXT('Transitt tollsted 8')
     A                                      DSPATR(UL)
     A  74                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 19  2'52.'
     A            WSGKD          1Y 0B 19  6TEXT('Garanti kode')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSGFT1        24A  B 19  8TEXT('Garanti ref.nr')
     A                                      DSPATR(UL)
     A  67                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSGFT2        35A  B 19 33TEXT('Annen Garanti')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSGADK         4A  B 19 69TEXT('Adgangkode')
     A                                      DSPATR(UL)
     A  73                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSGBGI         1Y 0B 19 74TEXT('Begrensing innen EF')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSGBGU         2A  B 19 76TEXT('Begrensing utenfor')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
S02  A*                                20  2'GAR(% AV FAKT.)/GAR.BEL. '
S02  A*           WSGBL         11Y 0B 20 28TEXT('GARANTI')
N02  A                                 20  2'Garanti %:'
N02  A            WSGPR          3Y 1B 20 13TEXT('PROSENT')
     A                                      DSPATR(UL)
     A  66                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
N02  A                                 20 18'Gar. bel:'
N02  A            WSGBL         11Y 0B 20 28TEXT('GARANTI')
N02  A                                      DSPATR(UL)
N02  A  66                                  DSPATR(RI)
N02  A                                      COLOR(YLW)
N02  A                                      EDTCDE(Z)
     A                                 20 41'VAL.'
     A            WSGVK          3A  B 20 46TEXT('VALUTAKODE')
     A                                      DSPATR(UL)
     A  65                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 20 52'53. B.tullkont'
     A            WSTSB          8A  B 20 67TEXT('Bestemmels tollsted')
     A                                      DSPATR(UL)
     A  72                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 21  2'D.'
     A                                 21  5'Ktl.res'
     A            WSDKR          2A  B 21 13TEXT('Kontroll resultat')
     A                                      DSPATR(UL)
     A  70                                  DSPATR(RI)
     A                                      COLOR(YLW)
S02  A*                                21 16'Frist'
S02  A*           WSDDT          8Y 0B 21 22TEXT('Tiodsfrist')
N02  A                                 21 16'Frist(dag)'
N02  A            WSDDT          3Y 0B 21 27EDTCDE(Z)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
S02  A*                                     EDTCDE(4)
     A                                 21 31'Plombering Ant/KD/SK'
     A            WSDANT         4Y 0B 21 52TEXT('Antall forsegling')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSDFKD        20A  B 21 57TEXT('FORSEGLING-ID KODE')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSDFSK         2A  B 21 78TEXT('FORSEGLING-ID Språk')
     A                                      DSPATR(UL)
     A  69                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 22  2'Språkkod føljedokument'
     A            WSSKFD         2A  B 22 25TEXT('Språkkode')
     A                                      DSPATR(UL)
     A  68                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 22 32'Dekl.ort.'
     A            WSDST         15A  B 22 42DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 22 58'Dekl.språk'
     A            WSDSK          2A  B 22 69DSPATR(UL)
     A  71                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 23  1'F3=Avsluta u/uppdatera  '
     A                                      COLOR(BLU)
     A                                 23 28'F4=Søk koder '
     A                                      COLOR(BLU)
     A                                 23 42'F12=Gå til førra utan att uppdater-
     A                                      a'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
      *
     A          R SV003D04
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/SVX003R'
     A                                      COLOR(BLU)
     A                                  1 23'Standardværden føranmælan'
     A                                      COLOR(WHT)
     A                                  1 52'Ref.'
     A            WSAAVD         4Y 0O  1 57COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  1 76'Del-3'
     A                                      COLOR(YLW)
     A                                  3  2'Sækerhet.:'
     A            WSSIK          1Y 0B  3 13TEXT('Sikkerhet')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                      VALUES(0 1)
     A                                  3 15'Ank.dat/tid.:'
     A            WSDTA          6Y 0B  3 29TEXT('DATO')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(Y)
     A            WSTMA          4Y 0B  3 38TEXT('TID')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTWRD('  :  ')
     A                                  3 44'Sær.omstændigheter:'
     A            WSSPOM         1A  B  3 64DSPATR(UL)
     A  97                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                  3 66'Bet.kod.Trp:'
     A            WSTKBM         1A  B  3 79DSPATR(UL)
     A  96                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                  4  2'K. ref:'
     A            WSKREF        70A  B  4 10DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  5  2'Transportørs ref:'
     A            WSTREF        35A  B  5 20DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  6  2'Lossplats:'
     A            WSLOSD        35A  B  6 13DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  6 50'Språkkod.:'
     A            WSLOSDSK       2A  B  6 61DSPATR(UL)
     A  95                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                  7  2'Landkoder før resplan.:'
     A            WSLKR1         2A  B  7 26DSPATR(UL)
     A  94                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSLKR2         2A  B  7 29DSPATR(UL)
     A  93                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSLKR3         2A  B  7 32DSPATR(UL)
     A  92                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSLKR4         2A  B  7 35DSPATR(UL)
     A  91                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSLKR5         2A  B  7 38DSPATR(UL)
     A  90                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSLKR6         2A  B  7 41DSPATR(UL)
     A  89                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSLKR7         2A  B  7 44DSPATR(UL)
     A  88                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSLKR8         2A  B  7 47DSPATR(UL)
     A  87                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                  9  2'Avsændare sækerhet:'
     A            WSKNSS         8Y 0B  9 22TEXT('Kundenr selger')
     A                                      DSPATR(UL)
     A  86                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSTINSS       17A  B  9 31TEXT('Tin kode selger')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSNASS        30A  B 10  2TEXT('Navn selger')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSADSS1       30A  B 10 42TEXT('Adresse-1 selger')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSPNSS         9A  B 11  2TEXT('Postnr selger')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSPSSS        24A  B 11 12TEXT('Poststed selger')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 11 42'Landkod'
     A            WSLKSS         2A  B 11 52TEXT('Landkode selger')
     A                                      DSPATR(UL)
     A  85                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 11 62'Språkkod'
     A            WSSKSS         2A  B 11 72TEXT('Språkkode selger')
     A                                      DSPATR(UL)
     A  84                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 12  2'Mottagare sækerhet:'
     A            WSKNKS         8Y 0B 12 22TEXT('Kundenr kjøper')
     A                                      DSPATR(UL)
     A  83                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSTINKS       17A  B 12 31TEXT('Tin kode kjøper')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSNAKS        30A  B 13  2TEXT('Navn kjøper')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSADKS1       30A  B 13 42TEXT('Adresse-1 kjøper')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSPNKS         9A  B 14  2TEXT('Postnr kjøper')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSPSKS        24A  B 14 12TEXT('Poststed kjøper')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 14 42'Landkod'
     A            WSLKKS         2A  B 14 52TEXT('Landkode kjøper')
     A                                      DSPATR(UL)
     A  82                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 14 62'Språkkod'
     A            WSSKKS         2A  B 14 72TEXT('Språkkode kjøper')
     A                                      DSPATR(UL)
     A  81                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 15  2'Transportør:'
     A            WSKNT          8Y 0B 15 22TEXT('Kundenr transportør')
     A                                      DSPATR(UL)
     A  80                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSTINT        17A  B 15 31TEXT('Tin kode transportør')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSNAT         30A  B 16  2TEXT('Navn transportør')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSADT1        30A  B 16 42TEXT('Adresse-1 transportør')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSPNT          9A  B 17  2TEXT('Postnr transportør')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSPST         24A  B 17 12TEXT('Poststed transportør')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 17 42'Landkod'
     A            WSLKT          2A  B 17 52TEXT('Landkode H.ansvalig')
     A                                      DSPATR(UL)
     A  79                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 17 62'Språkkod'
     A            WSSKT          2A  B 17 72TEXT('Språkkode H.ansvalig')
     A                                      DSPATR(UL)
     A  78                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 23  1'F3=Avsluta'
     A                                      COLOR(BLU)
     A                                 23 14'F4=Søk'
     A                                      COLOR(BLU)
     A                                 23 23'F12=Avbryt'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
