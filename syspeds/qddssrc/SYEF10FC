     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPDOC('STD' 'HELPNU' 'HLP400')
     A                                      HELP
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  6  2
     A  98                                  DSPATR(PC)
     A                                      VALUES(' ' '2' '4')
     A            DFST           1   O  6  5DSPATR(UL)
     A            IMLOP          3S 0O  6  8DSPATR(UL)
     A            HEGN          15A  O  6 14COLOR(YLW)
     A            HESDF          5A  O  6 30
     A            HESDT          5A  O  6 36
     A            KNAVN         30A  O  6 42
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0017)
     A                                      SFLPAG(0016)
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A                                  1  2'SYSPED/SYEF10C'
     A                                      COLOR(BLU)
     A                                  1 18MSGCON(020 SHU1100 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            UAVD           4Y 0O  1 48EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  1 53'/'
     A            UOPD           7Y 0O  1 55EDTCDE(4)
     A                                      COLOR(WHT)
     A            ZRECNO         4S 0O  1 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  2 65TIME
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(020 DIV0017 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  1MSGCON(077 SHU1101 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
      *
     A          R BUNN
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF03(03 'Avslutt')
     A                                      CF06(06 'NY')
     A                                      CF12(12)
     A                                      SLNO(22)
     A                                      CLRL(03)
     A                                      OVERLAY
     A                                  2  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  2 22MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A            MSGNR          7A  P
     A  30        MSGTXT        78A  O  3  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A                                  2 42MSGCON(018 DIV0022 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
      *
     A          R NYVARE
     A                                      OVERLAY
     A                                  1  2'SYSPED/SYFI10A'
     A                                      COLOR(BLU)
     A                                  1 20MSGCON(030 DIV0378 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            UAVD           4Y 0O  1 52EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  1 57'/'
     A            UOPD           7Y 0O  1 59EDTCDE(4)
     A                                      COLOR(WHT)
     A            ZRECNO         4S 0O  1 76DSPATR(ND)
     A                                  2 65TIME
     A                                  4  3MSGCON(078 SHU0157 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  5 12MSGCON(069 SHU0156 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
