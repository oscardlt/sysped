     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      CF03(03 'AVSLUTT')
     A                                      ALTHELP
     A                                      HELP
      *
     A          R GSS052DA                  SFL
     A            WSNR           1A  B  5  2VALUES(' ' '4')
     A            ORONO          7Y 0O  5  4EDTCDE(4)
     A            ORRTNG         1A  O  5 12
     A            ORCNO          8Y 0O  5 14EDTCDE(4)
     A            ORSNO          5Y 0O  5 23EDTCDE(4)
     A            ORBCD          5A  O  5 29
     A            ORDTL          8Y 0H
     A            WXDTL          6Y 0O  5 35EDTCDE(4)
     A            ORLC           5A  O  5 42
     A            POCONO        20A  H
     A            XPOCONO       17A  O  5 48
     A            ORCLI          5Y 0O  5 66EDTCDE(4)
     A            ORWT           7Y 1O  5 72EDTCDE(4)
     A            PAPKD          1A  O  5 80COLOR(YLW)
      *
     A          R GSS052DB                  SFLCTL(GSS052DA)
     A                                      CF05(05 'FRISK')
     A                                      CF12(12 'SØK')
     A                                      CF10(10 'TIL PRØVE-FAKTURA')
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A                                      SFLSIZ(0017)
     A                                      SFLPAG(0016)
     A                                  1  2'SHIPSYS/GSS052R'
     A                                      COLOR(BLU)
     A                                  1 21'Inquery prices'
     A                                      COLOR(WHT)
     A            ZRECNO         4S 0O  1 56SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1 62DATE
     A                                      EDTCDE(Y)
     A                                  1 72TIME
     A                                  2  2'Total pieces:'
     A            WSCLI          5Y 0O  2 16DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  2 26'Total weight:'
     A            WSWT           9Y 1O  2 40DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  2 54'Total volume:'
     A            WSVOL          7Y 2O  2 68DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  3  2'Options:  4=Delete'
     A                                      COLOR(BLU)
     A                                  4  2'Oo Ord.No R ClientNo SupNo BoatC D-
     A                                      .Date Locat Clinets order No. Piece-
     A                                       Weight P'
     A                                      COLOR(WHT)
      *
     A          R GSS052DC
     A                                      SLNO(21)
     A                                  1  2' '
     A                                  3  2'F3/12=End   F5=Refresh   F10=Inq. -
     A                                      price'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  4  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R GSS052DG
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF05(05 'FRISK')
     A                                      CF12(12 'SØK')
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SHIPSYS/GSS052R'
     A                                      COLOR(BLU)
     A                                  1 21'Inquery prices'
     A                                      COLOR(WHT)
     A                                  1 62DATE
     A                                      EDTCDE(Y)
     A                                  1 72TIME
     A                                  2  2'Total pieces:'
     A            WSCLI          5Y 0O  2 16DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  2 26'Total weight:'
     A            WSWT           9Y 1O  2 40DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  2 54'Total volume:'
     A            WSVOL          7Y 2O  2 68DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  3  2'Options:  4=Delete'
     A                                      COLOR(BLU)
     A                                  4  2'Oo Ord.No R ClientNo SupNo BoatC D-
     A                                      .Date Locat Clients order No. Piece-
     A                                       Weight P'
     A                                      COLOR(WHT)
     A                                 23  2'F3/12=End   F5=Refresh'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
