/* GENERER PDF-FRAKTBREV BASERT PÅ OPPDRAG - TA MED FILNAVN TIL KALLENDE PROG */
/*       TA MED FILNAVN TIL KALLENDE CGI-PROG  FOR VISNING I VINDU            */
/* *********************************************************************   */
/*       * RETTINGER I DETTE PROGRAM:                                      */
/* PTFnr:*Sek Sig Vers  Beskrivelse..........................              */
/* """"""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""    */
/* 10XXX * 02 JOV PTF11 LPI(12) + SIDESTØRRELSE/OVERFLOW DOBLET SYBRINGTMS */
/*                              + SIDEBREDDE- 90->100                      */
/* ********************************************************************    */
             PGM        PARM(&FIR &AVD &OPD &FBN &KODE &STMF)
             DCL        VAR(&FIR) TYPE(*CHAR) LEN(2)
             DCL        VAR(&AVD) TYPE(*CHAR) LEN(4)
             DCL        VAR(&OPD) TYPE(*CHAR) LEN(7)
             DCL        VAR(&FBN) TYPE(*CHAR) LEN(3)
             DCL        VAR(&FILEN) TYPE(*CHAR) LEN(10)
             DCL        VAR(&INFOP) TYPE(*CHAR) LEN(1)
             DCL        VAR(&STMF) TYPE(*CHAR) LEN(50)
             DCL        VAR(&QJOB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&QUSER) TYPE(*CHAR) LEN(10)
             DCL        VAR(&QNBR) TYPE(*CHAR) LEN(6)
             DCL        VAR(&PRANT) TYPE(*CHAR) LEN(2) /* Antall +
                          utskrifter. */
             DCL        VAR(&VB) TYPE(*CHAR) LEN(1) /* Hvis &VB = +
                          'J', skal sende valgbilde ut til bruker. */
             DCL        VAR(&PO) TYPE(*CHAR) LEN(1) VALUE('O') /*=OppdURL*/
             DCL        VAR(&KODE) TYPE(*CHAR) LEN(1) /* Kode = 'F', +
                          'T'. 'F'=fraktbrev, 'T'=tollpassfraktbrev  */
             DCL        VAR(&PDFDIR) TYPE(*CHAR) LEN(1) VALUE('J')

             CHGDTAARA  DTAARA(*LDA (1 1000)) VALUE(' ')
             CHGDTAARA  DTAARA(*LDA (1 2)) VALUE(&FIR)
             CHGDTAARA  DTAARA(*LDA (15 4)) VALUE(&AVD)
             CHGDTAARA  DTAARA(*LDA (82 7)) VALUE(&OPD)
             CHGDTAARA  DTAARA(*LDA (89 3)) VALUE(&FBN)
             CHGDTAARA  DTAARA(*LDA (22 8)) VALUE('00000000')     /*UPRO*/
             CHGDTAARA  DTAARA(*LDA (64 6)) VALUE('000000')     /*UDT */

             RTVJOBA    JOB(&QJOB) USER(&QUSER) NBR(&QNBR)

    /* OVERRIDE PRINTERFILE/SKRIV-FR.BEV  */
             OVRPRTF    FILE(SYFA12P) DEV(FAX3812) DEVTYPE(*AFPDS) +
                          PAGESIZE(140 100) LPI(12) CPI(10) OVRFLW(140) +
                          OUTQ(SYCGIOUTQ) FORMTYPE(*STD) SPLFOWN(*JOB)

             CALL       PGM(SYFA12) PARM(&PRANT &KODE &VB &PO &PDFDIR)

             CHGVAR     VAR(&STMF) VALUE('/syspeddev/temp/' *CAT +
                          'FB' *CAT &AVD *CAT &OPD *CAT '.PDF')

             ADDLIBLE   LIB(MMAIL)
             MONMSG     MSGID(CPF0000)
             CALL       PGM(SYGE40) PARM(&INFOP)
             IF         COND(&INFOP = 'N') THEN(DO)
             CVTSPLFPDF SPLF(SYFA12P) TOPDF(&STMF) +
                          JOB(&QNBR/&QUSER/&QJOB) SPLNBR(*LAST)
                          ENDDO
             IF         COND(&INFOP = 'J') THEN(DO)
             CHGVAR     VAR(&FILEN) VALUE('SYFA12P')
             CALL       PGM(INF001CL) PARM(&FILEN &QNBR &QUSER &QJOB +
                          &STMF)
                          ENDDO
             RMVLIBLE   LIB(MMAIL)
             MONMSG     MSGID(CPF0000)

             DLTSPLF    FILE(SYFA12P) JOB(&QNBR/&QUSER/&QJOB) +
                          SPLNBR(*LAST)
             MONMSG     MSGID(CPF0000)
    /* */
 SLUTT:      ENDPGM
