********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10027 * 01 YBC PTF10 VIS ORDRETYPE
10XXX * 02 YBC PTF10 OPSJON 5 ER OGSÅ FOR SPØRRING
********************************************************************
      *
      ***********************************************************
      *** PROGRAM SKAL ARBEIDE MED REFERANSE                  ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01 02 07-11 13-24                ***
      *** LEDIGE INDIKATORER 28 29 31 32 34-39 41-84 95-97    ***
      ***********************************************************
      *
N01  FGSSORF    IF   E           K DISK
     FGSSRFF    UF A E           K DISK
     FGSSLGF    O  A E             DISK
     FKUFAST    IF   E           K DISK
     FGSS019D   CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               382    383B 0
     D  WSSUBN               378    379B 0
     D                 DS
     D  RFDTR                  1      8  0
     D  RFDTRÅ                 1      4  0
     D  RFDTRM                 5      6  0
     D  RFDTRD                 7      8  0
     D                 DS
     D  W2DTR                  1      6  0
     D  W2DTRD                 1      2  0
     D  W2DTRM                 3      4  0
     D  W2DTRÅ                 5      6  0
     D                 DS
     D  RFDTE                  1      8  0
     D  RFDTEÅ                 1      4  0
     D  RFDTEM                 5      6  0
     D  RFDTED                 7      8  0
     D                 DS
     D  W2DTE                  1      6  0
     D  W2DTED                 1      2  0
     D  W2DTEM                 3      4  0
     D  W2DTEÅ                 5      6  0
     D                SDS
     D  UUSER                254    263
      *
     C                   EXSR      INIT
N01   *
N01   * VED ORDRETYPE='T', ER DET KRAV Å OPPGI MRN-NR
N01  C     UONO          CHAIN     GSSORF                             33
N01  C                   IF        ORTYPE = 'T'
N01  C     UONO          SETLL     GSSRFF
N01  C     UONO          READE(N)  GSSRFF                                 33
N01  C                   DOW       *IN33 = *OFF
N01  C                   MOVE      RFLNO         XLNO
N01  C                   IF        RFKD = 'MRNI'
N01  C                   MOVE      'J'           XMRNOK
N01  C                   GOTO      START
N01  C                   END
N01  C     UONO          READE(N)  GSSRFF                                 33
N01  C  N33              ENDDO
N01  C                   MOVE      'N'           XMRNOK            1
N01  C                   ADD       1             XLNO
N01  C                   EXSR      MOVB2
N01  C                   MOVEL     'MRNI'        WSKD
N01  C                   Z-ADD     4             LINNBR
N01  C                   GOTO      START5
N01  C                   END
      *
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
     C                   MOVE      *ZEROS        XLNO              3 0
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     UONO          SETLL     GSSRFF
     C     UONO          READE(N)  GSSRFF                                 94
     C     *IN94         DOWEQ     '0'
     C                   ADD       1             ZRECNO
     C                   MOVE      RFLNO         XLNO
     C                   WRITE     SUBFIL
     C     UONO          READE(N)  GSSRFF                                 94
     C  N94              ENDDO
     C                   ADD       1             XLNO
     C                   Z-ADD     ZRECNO        YRECNO            4 0
     C                   IF        ZRECNO > 1
     C                   Z-ADD     1             ZRECNO
     C                   END
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     2             POSNBR
      *
     C     START3        TAG
     C                   SETOFF                                       9998
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYWRK
     C                   ELSE
     C                   WRITE     BUNN
     C                   EXFMT     SUBCTL
     C                   END
      *
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C                   Z-ADD     WSSUBN        XRECNO            4 0
      *
     C                   SETOFF                                       9830
      *** AVSLUTT
     C   03
     COR 12              GOTO      SLUTT
      *** FRISK OPP
     C   05              GOTO      START
      *** LAGE NY
     C   06              DO
     C                   EXSR      MOVB2
     C                   GOTO      START5
     C                   END
      *
S02  C* N27ZRECNO        IFNE      0
N02  C     ZRECNO        IFNE      0
     C                   EXSR      HENT
     C                   END
     C   98XRECNO        IFNE      0
     C                   Z-ADD     XRECNO        ZRECNO
     C                   END
     C                   GOTO      START3
      *
     C     START5        TAG
     C                   EXFMT     BILDE01
     C                   MOVEA     '00'          *IN(89)
     C                   MOVE      '0'           *IN30
      *
     C   04              DO
     C                   EXSR      SØKDTA
     C                   GOTO      START5
     C                   END
     C   12              EXSR      MOVB2
     C  N12              EXSR      TESTB1
     C  N12
     CAN 30              GOTO      START5
N01  C                   IF        XMRNOK = 'N'
N01  C                   MOVE      'J'           XMRNOK
N01  C                   GOTO      START
N01  C                   END
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     2             POSNBR
     C                   GOTO      START3
      *
     C     SLUTT         TAG
     C   03              Z-ADD     3             UFLAGG
     C   12              Z-ADD     1             UFLAGG
     C                   SETON                                        LR
     C                   RETURN
      *
     C***********************************************
     C     TESTB1        BEGSR
     C***********************************************
     C     KUFASK        CHAIN     KUFAST                             90
     C     *IN90         IFEQ      '1'
     C                   MOVE      'YBC1849'     MSGNR
     C                   SETON                                        30
     C                   Z-ADD     3             LINNBR
     C                   Z-ADD     8             POSNBR
     C                   END
      *
     C     WSREF         COMP      *BLANKS                                89
     C   89*IN30         IFEQ      '0'
     C                   MOVE      'YBC1850'     MSGNR
     C                   Z-ADD     4             LINNBR
     C                   Z-ADD     8             POSNBR
     C                   SETON                                        30
     C                   END
      *
     C   30              GOTO      SLTB1
      *
      *
      *** OPPDATERE PÅ REGISTER OG SUBFIL
     C   40RFKEY1        CHAIN     GSSRFF                             33
     C  N40RFKEY2        CHAIN     GSSRFF                             33
     C  N33
     CANN40              GOTO      SLTB1
      *
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   SELECT
     C     *IN33         WHENEQ    '1'
     C                   MOVE      WDT           RFDTR
     C                   MOVE      *BLANKS       RFDTE
     C     WSKD          WHENNE    RFKD
     C     WSREF         ORNE      RFREF
     C                   MOVE      WDT           RFDTE
     C                   ENDSL
      * LOGG TIDSPUNKT
     C                   MOVE      'N'           XSLT              1
     C                   EXSR      SRLOGG
     C                   EXSR      MOVWRF
      *
     C  N33              UPDATE    GSSRFFR
     C   33              MOVE      'M'           RFEDI
     C   33              WRITE     GSSRFFR
      *
     C   40              UPDATE    SUBFIL
     C  N40              ADD       1             YRECNO
     C  N40              Z-ADD     YRECNO        ZRECNO
     C  N40              WRITE     SUBFIL
     C                   WRITE     SUBCTL
     C                   EXSR      MOVB2
      *
     C     SLTB1         ENDSR
      *
     C***********************************************
     C     SRLOGG        BEGSR
     C***********************************************
     C                   MOVE      UUSER         LGUSER
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVE      WDT           LGDT
     C                   MOVE      WTM           LGTID
     C                   Z-ADD     1             LGLN
     C                   MOVEL     'GSS019R'     LGPGM
     C                   Z-ADD     5             LGNIVÅ
     C                   MOVE      WDT           LGHDT
     C                   MOVEL     WTM           LGHTID
     C                   MOVEL     'MAIN'        LGFTYP
     C                   MOVE      UONO          LGONO
     C                   MOVE      *ZEROS        LGLNO
      *
     C                   SELECT
     C                   WHEN      XSLT = 'J'
     C                   EVAL      LGAGM = RFREF
     C                   EVAL      LGANY = *BLANKS
     C                   EVAL      WSKD  = RFKD
     C     KUFASK        CHAIN     KUFAST                             33
     C                   EVAL      LGTXT = KFTXT
     C                   WRITE     GSSLGFR
     C                   WHEN      *IN33
     C                   EVAL      LGAGM = *BLANKS
     C                   EVAL      LGANY = WSREF
     C                   EVAL      LGTXT = KFTXT
     C                   WRITE     GSSLGFR
     C                   WHEN      WSREF <> RFREF
     C                   EVAL      LGAGM = RFREF
     C                   EVAL      LGANY = WSREF
     C                   EVAL      LGTXT = KFTXT
     C                   WRITE     GSSLGFR
     C                   ENDSL
      *
     C                   ENDSR
      *
     C***********************************************
     C     HENT          BEGSR
     C***********************************************
     C                   Z-ADD     0             LINNBR
     C                   SETOFF                                       919299
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C   98              DO
     C   99              SETOFF                                       98
     C                   Z-ADD     1             LINNBR
     C                   GOTO      SLHENT
     C                   END
      *
     C     ZRECNO        DIV       15            LINNBR
     C                   MVR                     XLIN              2 0
     C     XLIN          IFEQ      0
     C                   Z-ADD     15            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
      *
     C  N27WSNR          IFEQ      '2'
     C                   MOVE      ' '           WSNR
     C     WSREF         COMP      '*DELETE'                              98
     C   98              GOTO      SLHENT
     C                   EXSR      CHGRF
     C   12              GOTO      SLHENT
     C                   END
      *
     C  N27WSNR          IFEQ      '4'
     C     RFEDI         ANDNE     'E'
     C                   MOVE      ' '           WSNR
     C     WSREF         COMP      '*DELETE'                              98
     C   98              GOTO      SLHENT
     C                   EXSR      DLTRF
     C   12              GOTO      SLHENT
     C                   END
      *
     C     WSNR          IFEQ      '5'
     C                   MOVE      ' '           WSNR
     C     WSREF         COMP      '*DELETE'                              98
     C   98              GOTO      SLHENT
     C                   EXSR      DSPRF
     C   12              GOTO      SLHENT
     C                   END
      *
     C                   END
      *
     C     SLHENT        TAG
     C                   ADD       4             LINNBR
     C                   Z-ADD     2             POSNBR
     C                   SETOFF                                       99
     C   98              DO
     C                   MOVE      'DIV0012'     MSGNR
     C                   SETON                                            30
     C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     CHGRF         BEGSR
     C***********************************************
     C                   EXSR      MOVRFW
     C                   SETON                                        99
     C     STCHG         TAG
     C                   EXFMT     BILDE01
     C                   MOVEA     '00'          *IN(89)
     C                   MOVE      '0'           *IN30
      *
     C   04              DO
     C                   EXSR      SØKDTA
     C                   GOTO      STCHG
     C                   END
     C   12              DO
     C                   UPDATE    SUBFIL
     C                   EXSR      MOVB2
     C                   END
     C  N12              EXSR      TESTB1
     C  N12
     CAN 30              GOTO      STCHG
     C     XLIN          IFEQ      0
     C                   Z-ADD     9             LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     DLTRF         BEGSR
     C***********************************************
     C                   MOVE      RFDTRD        W2DTRD
     C                   MOVE      RFDTRM        W2DTRM
     C                   MOVE      RFDTRÅ        W2DTRÅ
     C                   MOVE      RFDTED        W2DTED
     C                   MOVE      RFDTEM        W2DTEM
     C                   MOVE      RFDTEÅ        W2DTEÅ
     C                   SETON                                        99
     C                   EXFMT     BILDE02
      *
     C  N12              DO
     C     RFKEY1        CHAIN     GSSRFF                             33
     C  N33              DELETE    GSSRFFR
     C  N33              MOVE      'J'           XSLT              1
     C  N33              EXSR      SRLOGG
     C                   EXSR      MOVB1
     C                   END
     C                   UPDATE    SUBFIL
     C     XLIN          IFEQ      0
     C                   Z-ADD     9             LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     DSPRF         BEGSR
     C***********************************************
     C                   MOVE      RFDTRD        W2DTRD
     C                   MOVE      RFDTRM        W2DTRM
     C                   MOVE      RFDTRÅ        W2DTRÅ
     C                   MOVE      RFDTED        W2DTED
     C                   MOVE      RFDTEM        W2DTEM
     C                   MOVE      RFDTEÅ        W2DTEÅ
     C                   SETON                                        99
     C                   EXFMT     BILDE02
      *
     C                   UPDATE    SUBFIL
     C     XLIN          IFEQ      0
     C                   Z-ADD     9             LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     MOVWRF        BEGSR
     C***********************************************
     C     *IN33         IFEQ      '1'
     C                   MOVE      UONO          RFONO
     C                   MOVE      XLNO          RFLNO
     C                   ADD       1             XLNO
     C                   END
     C                   MOVE      WSKD          RFKD
     C                   MOVE      WSREF         RFREF
      *
     C                   ENDSR
      *
     C***********************************************
     C     MOVRFW        BEGSR
     C***********************************************
     C                   MOVE      RFKD          WSKD
     C                   MOVE      RFREF         WSREF
     C                   SETON                                        40
     C                   Z-ADD     3             LINNBR
     C                   Z-ADD     8             POSNBR
      *
     C                   ENDSR
      *
     C***********************************************
     C     MOVB1         BEGSR
     C***********************************************
     C                   MOVE      *BLANKS       RFKD
     C                   MOVE      *BLANKS       RFREF
     C                   MOVEL     '*DELETE'     RFREF
      *
     C                   ENDSR
      *
     C***********************************************
     C     MOVB2         BEGSR
     C***********************************************
     C                   SETOFF                                       40
     C                   MOVE      *BLANKS       WSKD
     C                   MOVE      *BLANKS       WSREF
     C                   Z-ADD     3             LINNBR
     C                   Z-ADD     8             POSNBR
      *
     C                   ENDSR
      *
     C***********************************************
     C     SØKDTA        BEGSR
     C***********************************************
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
      *
     C     LINNBR        IFEQ      3
     C     POSNBR        ANDGE     8
     C     POSNBR        ANDLE     10
     C                   CALL      'SYKUKD'
     C                   PARM                    KFTYP
     C                   PARM                    UUPDAT
     C                   PARM                    WSKD
     C                   Z-ADD     4             LINNBR
     C                   Z-ADD     8             POSNBR
     C                   GOTO      SLSØK
     C                   END
      *
     C                   MOVE      'DIV1037'     MSGNR
     C                   SETON                                        30
      *
     C     SLSØK         ENDSR
      *
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     RFKEY1        KLIST
     C                   KFLD                    UONO
     C                   KFLD                    RFLNO
     C     RFKEY2        KLIST
     C                   KFLD                    UONO
     C                   KFLD                    XLNO
     C     KUFASK        KLIST
     C                   KFLD                    KFTYP
     C                   KFLD                    WSKD
      *
     C     *ENTRY        PLIST
     C                   PARM                    UONO              7 0
     C                   PARM                    UTYPE             3
     C                   PARM                    UFLAGG            1 0
      *
     C     UTYPE         COMP      'DSP'                                  27
     C                   WRITE     WINDOWSZ
     C                   MOVEL     'SSORDREF'    KFTYP
     C                   MOVE      'J'           UUPDAT            1
      *
     C                   ENDSR
      *
