     FEDIRE     IF   E           K DISK
     FEDIC      UF   E             DISK
     FEDIS      O  A E             DISK
     FEDIM      O  A E             DISK
     FSVZH      O  A E             DISK
     FSVZV      O  A E             DISK
     D NU              S              1    DIM(15)
     D KD              S              5    DIM(10)
     D                 DS
     D  WTIME                  1     14  0
     D  WTID                   1      6  0
     D  WDD                    7      8  0
     D  WMM                    9     10  0
     D  WÅÅ                   11     14  0
     D                 DS
     D  WDATO                  1      8  0
     D  WÅ                     1      4  0
     D  WM                     5      6  0
     D  WD                     7      8  0
     D  LDADTA         DS          1024
     D  FE                     1    256
     D                                     DIM(256)
     D  FELT                   1    256
     D  AA                   301    380
     D                                     DIM(80)
     D  AADS                 301    380
     D  AB                   401    480
     D                                     DIM(80)
     D  ABDS                 401    480
     D  U0065                501    506
     D  SEGNAM               507    509
     D  TN                   601    602  0
     D  EOF                  603    605
     D  TEGN                 606    606
     D  LOKAHE               607    607
     D  UMN                  608    616  0
     D  UMBR                 617    626
     D                 DS
     D  XMBR                   1     10
     D  XMBRA                  1      1
     D  XMBRB                  2     10S 0
      *________________________
      * DS for decimal handling
      *""""""""""""""""""""""""
     D A               S              1    DIM(79)
     D B               S              1    DIM(18)
     D                 DS
     D  ZDEC                   1      7  6
     D  ZDEC6                  2      7
     D  ZHEL                  11     22  0
      */////////////
      * MAIN LOGIC /
      */////////////
     C                   EXSR      SRINIT
     C     SEGSTA        TAG
     C  N91              EXSR      GETSEG
     C   91              GOTO      SLUTT
     C     SEGNAM        CASEQ     'UNB'         UNB
     C     SEGNAM        CASEQ     'UNH'         UNH
     C     SEGNAM        CASEQ     'BGM'         BGM
     C     SEGNAM        CASEQ     'CNT'         CNT
     C     SEGNAM        CASEQ     'CST'         CST
     C     SEGNAM        CASEQ     'DOC'         DOC
     C     SEGNAM        CASEQ     'EQD'         EQD
     C     SEGNAM        CASEQ     'FII'         FII
     C     SEGNAM        CASEQ     'GIS'         GIS
     C     SEGNAM        CASEQ     'PAC'         PAC
     C     SEGNAM        CASEQ     'RFF'         RFF
     C     SEGNAM        CASEQ     'PCI'         PCI
     C     SEGNAM        CASEQ     'FTX'         FTX
     C     SEGNAM        CASEQ     'LOC'         LOC
     C     SEGNAM        CASEQ     'DTM'         DTM
     C     SEGNAM        CASEQ     'MEA'         MEA
     C     SEGNAM        CASEQ     'MOA'         MOA
     C     SEGNAM        CASEQ     'NAD'         NAD
     C     SEGNAM        CASEQ     'CTA'         CTA
     C     SEGNAM        CASEQ     'COM'         COM
     C     SEGNAM        CASEQ     'TAX'         TAX
     C     SEGNAM        CASEQ     'TDT'         TDT
     C     SEGNAM        CASEQ     'SEL'         SEL
     C     SEGNAM        CASEQ     'UNS'         UNS
     C     SEGNAM        CASEQ     'UNT'         UNT
     C     SEGNAM        CASEQ     'UNZ'         UNZ
     C                   ENDCS
     C                   GOTO      SEGSTA
     C     SLUTT         TAG
     C                   MOVE      '1'           *INLR
      *////////////////////////////////////////////////////////////
      * SR for init                                        SRINIT /
      *////////////////////////////////////////////////////////////
     C     SRINIT        BEGSR
      * INPUT PARAMETERS
     C     *ENTRY        PLIST
     C                   PARM                    WLIB             10
     C                   PARM                    WFIL             10
     C                   PARM                    WMBR             10
     C                   PARM                    WNUMR            10 0
      * Open input file
     C   91              GOTO      ENDINI
      * HENTE DAGENS DATO OG TID
     C                   TIME                    WTIME
     C                   Z-ADD     WÅÅ           WÅ
     C                   Z-ADD     WDD           WD
     C                   Z-ADD     WMM           WM
      * INIT AV ARBEIDSFELT
     C                   MOVE      *ZEROS        NUMVRD           15 0
     C                   MOVE      *ZEROS        NUTEGN            1 0
     C                   MOVE      *BLANKS       SEGNAM
     C                   MOVE      *BLANKS       AADS
     C                   MOVE      *BLANKS       ABDS
     C                   MOVEA     *BLANKS       FE
     C                   MOVEA     *BLANKS       NU
     C                   MOVEA     *BLANKS       KD
     C                   MOVE      *ZEROS        XN                3 0

      * Datafelt AB i EDIRE (80 lang) går rett inn i array AB ved READ
      * TN=81 soerger for automatisk overflyt array AB->AA ved kall av GETCH
     C                   MOVE      '0'           *IN91
     C                   MOVE      '0'           *IN98
     C                   MOVE      '0'           *IN99
     C                   EVAL      UMBR=WMBR
     C     WMBR          SETLL     EDIRE
     C     WMBR          READE     EDIRE                                  91
     C     *IN91         DOWEQ     '0'
     C     'UNB+'        SCAN      RDATA         TN                       20
     C  N20WMBR          READE     EDIRE                                  91
     C   91              GOTO      ENDINI
     C  N20              ENDDO

     C                   MOVE      RDATA         ABDS
     C     TN            IFGT      1
     C                   SUB       1             TN
     C                   ELSE
     C                   Z-ADD     81            TN
     C                   ENDIF
     C                   BITON     '17'          TEGN
     C                   BITOFF    '023456'      TEGN
     C                   MOVE      'NO '         EOF
     C                   MOVE      ' '           LOKAHE

      * Spesielt reserverte tegn defineres her med både grafisk verdi og
      * med en logisk verdi. Den grafiske verdien testes det mot i rutinen
      * som henter tegn. Finner denne rutinen at at tegnet er ment som en
      * spesialverdi (ikke ? foran), byttes tegnet med tilsvarende LOGISK VERDI
      * (De logiske verdiene er ikke lesbre tegn,og vil aldri forekomme i den
      * innkommende datastroemmen). '?' når brukt som opphevelse foran annet
      * spesialtegn vil bli "spist" av henterutinen (GETCH) og en vil derfor
      * kunne betrakte spoersmålstegn som grafisk tegn i resten av programmet.

      * Ved andre standarder for spesialtegn er det bare å bytte verdiene til
      * de GRAFISKE tegnene her. (Programmet arbeider da med riktige verdier).
     C                   MOVE      '?'           GRASPØ            1
     C                   MOVE      '+'           GRAPLU            1
     C                   MOVE      ':'           GRAKOL            1
     C                   BITON     '123457'      GRAAPP            1
     C                   BITOFF    '06'          GRAAPP
      * Logiske verdier for spesialtegn
      * LOGAPP = slutt på segmentet. I NODI er tegnet "'".
     C                   BITON     '17'          LOGAPP            1
     C                   BITOFF    '023456'      LOGAPP
      * HVIS KOMMUNEDATA, INITIERE MED LOGAPP (GÅ RETT PÅ UNB)
     C                   MOVE      LOGAPP        TEGN
      * LOGPLU = sparasjonstegn for Tag/dataelement. I NODI er tegnet "+".
     C                   BITON     '16'          LOGPLU            1
     C                   BITOFF    '023457'      LOGPLU
      * LOGKOL = sparasjonstegn for delelement. I NODI er tegnet ":".
     C                   MOVE      X'39'         LOGKOL            1
     C*                  BITON     '167'         LOGKOL            1
     C*                  BITOFF    '02345'       LOGKOL
      * LOGBLA = utenlandske måten å vise blank.
     C                   BITON     '01234567'    LOGBLA            1

     C     ENDINI        ENDSR
      *////////////////////////////////////////////////////////////
      * SR for å hente neste tegn i datastroem             GETCH  /
      *////////////////////////////////////////////////////////////
     C     GETCH         BEGSR
      * Rutinen henter "TEGN" fra array AA. Ved slutten av AA "toemmes" AB inn
      * i AA. (AB benyttes kun for alltid å kunne ha "lookahead-tegn" - LOKAHE)
      * 91 settes når en ved "Toemming" finner at AB-arrayet er tomt.

      * "?" spises av SR når "?" kun varsler grafisk betydning av spesialtegn.
      *Når tegn ved utgang av SR er ',+ eller :  er betydningen av ind. 98 :
      *    98 ON -> kun grafisk betydning (uforandret)
      *    98 OFF-> spesialtegn (Bytt tegn mot ikke lesbar logisk verdi LOG???)
     C                   MOVE      '0'           *IN98
     C     STACH         TAG
      * Må ny record hentes inn i array AA (fra AB)?????
     C     TN            IFEQ      81
     C     EOF           IFEQ      'YES'
     C                   SETON                                            91
     C                   GOTO      ENDCH
     C                   ENDIF
     C                   MOVE      ABDS          AADS
     C  N99WMBR          READE     EDIRE                                  99
     C  N99              MOVE      RDATA         ABDS
     C   99              MOVE      *BLANKS       ABDS
     C   99              MOVE      'YES'         EOF
     C                   Z-ADD     1             TN
     C                   ENDIF
      * Hent det tegn som TN peker på - Tegnet etter er "Lookahead"
     C                   MOVE      AA(TN)        TEGN
     C                   ADD       1             TN
     C     TN            IFLE      80
     C                   MOVE      AA(TN)        LOKAHE            1
     C                   ELSE
     C                   MOVE      AB(1)         LOKAHE
     C                   ENDIF
      * Dersom TEGN er "?" og LOKAHE er spes.tegn - "spis" spoersmålstegnet
      * Når 98 er on her betyr det at "?" foran spesialtegn alt er "spist"
      *        evt. nytt "?" er da grafisk tegn - LOKAHE skal da IKKE testes.
     C  N98TEGN          IFEQ      GRASPØ
     C     LOKAHE        COMP      GRAAPP                                 98
     C  N98LOKAHE        COMP      GRAPLU                                 98
     C  N98LOKAHE        COMP      GRAKOL                                 98
     C  N98LOKAHE        COMP      GRASPØ                                 98
     C   98              GOTO      STACH
     C                   END
      * Ved N98 og ',+ eller : i TEGN betyr det at det IKKE var opphevelsestegn
      * foran. Vi har med andre ord et LOGISK SPESIALTEN INNE
     C  N98TEGN          IFEQ      GRAAPP
     C                   MOVE      LOGAPP        TEGN
     C                   ELSE
     C     TEGN          IFEQ      GRAPLU
     C                   MOVE      LOGPLU        TEGN
     C                   ELSE
     C     TEGN          IFEQ      GRAKOL
     C                   MOVE      LOGKOL        TEGN
     C                   ELSE
     C     TEGN          IFEQ      LOGBLA
     C                   MOVE      ' '           TEGN
     C                   END
     C                   END
     C                   END
     C                   END

     C     ENDCH         ENDSR
      *////////////////////////////////////////////////////////////
      * SR for å hente neste Segment                       GETSEG /
      *////////////////////////////////////////////////////////////
     C     GETSEG        BEGSR
      * "Spis tegn" til appostrof er lest
     C     TEGN          DOWNE     LOGAPP
     C                   EXSR      GETCH
     C   91              GOTO      ENDSEG
     C                   END
     C* "Spis tegn" til første IKKE blanke
     C                   MOVEA     *BLANKS       FE
     C                   EXSR      GETCH
     C     TEGN          DOWEQ     ' '
     C                   EXSR      GETCH
     C   91              GOTO      ENDSEG
     C                   ENDDO
     C                   MOVEA     TEGN          FE(1)
      * Har nå lest appostrof - de 3 neste vil være SEGMENTNAVN
     C     2             DO        3             NR                3 0
     C                   EXSR      GETCH
     C   91              GOTO      ENDSEG
     C                   MOVEA     TEGN          FE(NR)
     C                   END
     C                   MOVEL     FELT          SEGNAM
      * La TEGN være '+' etter segmentnavnet ved utgang av SR
     C                   EXSR      GETCH
      * Segmentnavn returneres nå i variabel 'SEGNAM'
     C     ENDSEG        ENDSR
      *////////////////////////////////////////////////////////////
      * Get Data Element                                   GETDED /
      *////////////////////////////////////////////////////////////
     C     GETDED        BEGSR
     C                   MOVEA     *BLANKS       FE
     C     1             DO        256           NR                3 0
     C                   EXSR      GETCH
     C     TEGN          IFEQ      LOGAPP
     C     TEGN          OREQ      LOGPLU
     C     TEGN          OREQ      LOGKOL
     C                   GOTO      ENDDED
     C                   ENDIF
     C                   MOVEA     TEGN          FE(NR)
     C                   ENDDO
     C     ENDDED        ENDSR
      *////////////////////////////////////////////////////////////
      * CNT                                                  CNT  /
      *////////////////////////////////////////////////////////////
     C     CNT           BEGSR
      *_____________
      * C270 CONTROL
      *"""""""""""""
      * 6069 Control qualifier
     C                   EXSR      GETDED
     C                   MOVEL     FELT          CNT_6069          3
     C     TEGN          CABEQ     LOGAPP        CNTEND
     C     TEGN          CABEQ     LOGPLU        CNTEND
      * 6066 Control value
     C                   EXSR      GETDED
     C                   MOVEL     FELT          CNT_6066         18
     C                   MOVEA     CNT_6066      A
     C                   EXSR      GETNUM
     C     TEGN          CABEQ     LOGAPP        CNTEND
     C     TEGN          CABEQ     LOGPLU        CNTEND
      * 6411 Measure value
     C                   EXSR      GETDED
     C     TEGN          CABEQ     LOGAPP        CNTEND

     C     CNTEND        TAG

     C                   SELECT
     C     CNT_6069      WHENEQ    '11'
     C                   EVAL      SVZH_KOTA=CNT_6066
     C     CNT_6069      WHENEQ    '5'
     C                   EVAL      SVZH_ANTV=ZNUM
     C     CNT_6069      WHENEQ    '16'
     C                   EVAL      SVZH_SELN=ZNUM
     C                   ENDSL

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR for å behandle COM-segmentet                      COM  /
      *////////////////////////////////////////////////////////////
     C     COM           BEGSR
      *_____________
      * C076 CONTACT
      *"""""""""""""
      * 3148 Telefonnummer
     C                   EXSR      GETDED
     C                   SELECT
     C     NAD_3035      WHENEQ    'EX'
     C                   MOVEL     FELT          SVZH_AVTL
     C     NAD_3035      WHENEQ    'AE'
     C                   MOVEL     FELT          SVZH_OMTL
     C                   ENDSL
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR for å behandle CST-segmentet                      CST  /
      *////////////////////////////////////////////////////////////
     C     CST           BEGSR
      *___________________________
      * Skriv til forrige varupost
      *"""""""""""""""""""""""""""
     C                   IF        SVZV_VPNR > 0
     C                   WRITE     SVZVR
     C                   ENDIF
      *___________________
      * Clear recordformat
      *"""""""""""""""""""
     C                   CLEAR                   SVZVR
     C                   EVAL      SVZV_TUID=SVZH_TUID
      *_______________________
      * 1496 GOODS ITEM NUMBER
      *"""""""""""""""""""""""
     C                   EXSR      GETDED
     C                   MOVEA     FELT          A
     C                   EXSR      GETNUM
     C                   EVAL      SVZV_VPNR=ZNUM
     C                   EVAL      SVZV_VANO=ZNUM
      *____________________________
      * C246 CUSTOMS IDENTITY CODES
      *""""""""""""""""""""""""""""
      * 7361 Customs code identification
     C                   EXSR      GETDED
     C                   MOVEL     FELT          SVZV_VATA
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR for å behandle CTA-segmentet                      CTA  /
      *////////////////////////////////////////////////////////////
     C     CTA           BEGSR
      *__________
      * 3139 CODE
      *""""""""""
     C                   EXSR      GETDED
      *_____________
      * C056 DETAILS
      *"""""""""""""
      * 3413 ID
     C                   EXSR      GETDED
      * 3412 Handlæggare
     C                   EXSR      GETDED
     C                   SELECT
     C     NAD_3035      WHENEQ    'EX'
     C                   MOVEL     FELT          SVZH_AVHA
     C     NAD_3035      WHENEQ    'AE'
     C                   MOVEL     FELT          SVZH_OMHA
     C                   ENDSL
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR for å behandle DOC-segmentet                       DOC /
      *////////////////////////////////////////////////////////////
     C     DOC           BEGSR
      *___________________________
      * C002 DOCUMENT/MESSAGE NAME
      *"""""""""""""""""""""""""""
      * 1001 Document/message name, coded
     C                   EXSR      GETDED
     C                   MOVEL     FELT          DOC_1001          3
      * 1131 Code list qualifier
     C                   EXSR      GETDED
     C                   MOVEL     FELT          DOC_1131          3
     C     TEGN          CABEQ     LOGAPP        DOCEND
      * 3055 Code list responsible agency, coded
     C                   EXSR      GETDED
     C                   MOVEL     FELT          DOC_3055          3
     C     TEGN          CABEQ     LOGAPP        DOCEND
      * 1000 Document/message name
     C                   EXSR      GETDED
     C                   MOVEL     FELT          DOC_1000         35
     C     TEGN          CABEQ     LOGAPP        DOCEND
      *______________________________
      * C503 DOCUMENT/MESSAGE DETAILS
      *""""""""""""""""""""""""""""""
      * 1004 Document/message number
     C                   EXSR      GETDED
     C                   MOVEL     FELT          DOC_1004         35
     C     TEGN          CABEQ     LOGAPP        DOCEND
     C     DOCEND        TAG
     C                   SELECT
     C     DOC_1001      WHENEQ    'ZBH'
     C                   SELECT
     C     SVZV_BIT1     WHENEQ    *BLANKS
     C     SVZV_BII1     ANDEQ     *BLANKS
     C                   MOVEL     DOC_1000      SVZV_BIT1
     C                   MOVEL     DOC_1004      SVZV_BII1
     C     SVZV_BIT2     WHENEQ    *BLANKS
     C     SVZV_BII2     ANDEQ     *BLANKS
     C                   MOVEL     DOC_1000      SVZV_BIT2
     C                   MOVEL     DOC_1004      SVZV_BII2
     C     SVZV_BIT3     WHENEQ    *BLANKS
     C     SVZV_BII3     ANDEQ     *BLANKS
     C                   MOVEL     DOC_1000      SVZV_BIT3
     C                   MOVEL     DOC_1004      SVZV_BII3
     C     SVZV_BIT4     WHENEQ    *BLANKS
     C     SVZV_BII4     ANDEQ     *BLANKS
     C                   MOVEL     DOC_1000      SVZV_BIT4
     C                   MOVEL     DOC_1004      SVZV_BII4
     C     SVZV_BIT5     WHENEQ    *BLANKS
     C     SVZV_BII5     ANDEQ     *BLANKS
     C                   MOVEL     DOC_1000      SVZV_BIT5
     C                   MOVEL     DOC_1004      SVZV_BII5
     C     SVZV_BIT6     WHENEQ    *BLANKS
     C     SVZV_BII6     ANDEQ     *BLANKS
     C                   MOVEL     DOC_1000      SVZV_BIT6
     C                   MOVEL     DOC_1004      SVZV_BII6
     C     SVZV_BIT7     WHENEQ    *BLANKS
     C     SVZV_BII7     ANDEQ     *BLANKS
     C                   MOVEL     DOC_1000      SVZV_BIT7
     C                   MOVEL     DOC_1004      SVZV_BII7
     C     SVZV_BIT8     WHENEQ    *BLANKS
     C     SVZV_BII8     ANDEQ     *BLANKS
     C                   MOVEL     DOC_1000      SVZV_BIT8
     C                   MOVEL     DOC_1004      SVZV_BII8
     C     SVZV_BIT9     WHENEQ    *BLANKS
     C     SVZV_BII9     ANDEQ     *BLANKS
     C                   MOVEL     DOC_1000      SVZV_BIT9
     C                   MOVEL     DOC_1004      SVZV_BII9
     C                   ENDSL
     C     DOC_1001      WHENEQ    '190'
     C                   SELECT
     C     SVZV_TIK1     WHENEQ    *BLANKS
     C                   MOVEL     DOC_1131      SVZV_TIK1
     C                   MOVEL     DOC_1000      SVZV_TIT1
     C                   MOVEL     DOC_1004      SVZV_TIX1
     C     SVZV_TIK2     WHENEQ    *BLANKS
     C                   MOVEL     DOC_1131      SVZV_TIK2
     C                   MOVEL     DOC_1000      SVZV_TIT2
     C                   MOVEL     DOC_1004      SVZV_TIX2
     C     SVZV_TIK3     WHENEQ    *BLANKS
     C                   MOVEL     DOC_1131      SVZV_TIK3
     C                   MOVEL     DOC_1000      SVZV_TIT3
     C                   MOVEL     DOC_1004      SVZV_TIX3
     C     SVZV_TIK4     WHENEQ    *BLANKS
     C                   MOVEL     DOC_1131      SVZV_TIK4
     C                   MOVEL     DOC_1000      SVZV_TIT4
     C                   MOVEL     DOC_1004      SVZV_TIX4
     C     SVZV_TIK5     WHENEQ    *BLANKS
     C                   MOVEL     DOC_1131      SVZV_TIK5
     C                   MOVEL     DOC_1000      SVZV_TIT5
     C                   MOVEL     DOC_1004      SVZV_TIX5
     C     SVZV_TIK6     WHENEQ    *BLANKS
     C                   MOVEL     DOC_1131      SVZV_TIK6
     C                   MOVEL     DOC_1000      SVZV_TIT6
     C                   MOVEL     DOC_1004      SVZV_TIX6
     C     SVZV_TIK7     WHENEQ    *BLANKS
     C                   MOVEL     DOC_1131      SVZV_TIK7
     C                   MOVEL     DOC_1000      SVZV_TIT7
     C                   MOVEL     DOC_1004      SVZV_TIX7
     C     SVZV_TIK8     WHENEQ    *BLANKS
     C                   MOVEL     DOC_1131      SVZV_TIK8
     C                   MOVEL     DOC_1000      SVZV_TIT8
     C                   MOVEL     DOC_1004      SVZV_TIX8
     C     SVZV_TIK9     WHENEQ    *BLANKS
     C                   MOVEL     DOC_1131      SVZV_TIK9
     C                   MOVEL     DOC_1000      SVZV_TIT9
     C                   MOVEL     DOC_1004      SVZV_TIX9
     C                   ENDSL
     C                   ENDSL
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR for å behandle DTM-segmentet                      DTM  /
      *////////////////////////////////////////////////////////////
     C     DTM           BEGSR
      *______________________
      * C507 DATE/TIME/PERIOD
      *""""""""""""""""""""""
      * 2005 Qualifier
     C                   EXSR      GETDED
     C                   MOVEL     FELT          DTM_2005          3
      * 2380 Date
     C                   EXSR      GETDED
     C                   SELECT
     C     DTM_2005      WHENEQ    '9'
     C                   MOVEL     FELT          SVZH_KON4
     C     DTM_2005      WHENEQ    '182'
     C                   MOVEL     FELT          SVZH_UTDT
     C     DTM_2005      WHENEQ    '148'
     C                   MOVEL     FELT          SVZH_MODT
     C     DTM_2005      WHENEQ    '268'
     C                   MOVEL     FELT          SVZH_KON2
     C                   ENDSL
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR for å behandle EQD-segmentet                      EQD  /
      *////////////////////////////////////////////////////////////
     C     EQD           BEGSR
      *_________________________
      * 8053 EQUIPMENT QUALIFIER
      *"""""""""""""""""""""""""
     C                   EXSR      GETDED
     C                   MOVEL     FELT          EQD_8053          3
      *______________________________
      * C237 EQUIPMENT IDENTIFICATION
      *""""""""""""""""""""""""""""""
      * 8260 Equipment identification number
     C                   EXSR      GETDED
      * 1131 Code list qualifier
     C                   EXSR      GETDED
      * 3055 Code list responsible agency, coded C an..3 -
     C                   EXSR      GETDED
      * 3207 Country, coded C an..3 La
     C                   EXSR      GETDED
     C                   SELECT
     C     EQD_8053      WHENEQ    'CH'
     C                   SELECT
     C     SVZH_RES1     WHENEQ    *BLANKS
     C                   MOVEL     FELT          SVZH_RES1
     C     SVZH_RES2     WHENEQ    *BLANKS
     C                   MOVEL     FELT          SVZH_RES2
     C     SVZH_RES3     WHENEQ    *BLANKS
     C                   MOVEL     FELT          SVZH_RES3
     C     SVZH_RES4     WHENEQ    *BLANKS
     C                   MOVEL     FELT          SVZH_RES4
     C     SVZH_RES5     WHENEQ    *BLANKS
     C                   MOVEL     FELT          SVZH_RES5
     C     SVZH_RES6     WHENEQ    *BLANKS
     C                   MOVEL     FELT          SVZH_RES6
     C     SVZH_RES7     WHENEQ    *BLANKS
     C                   MOVEL     FELT          SVZH_RES7
     C     SVZH_RES8     WHENEQ    *BLANKS
     C                   MOVEL     FELT          SVZH_RES8
     C     SVZH_RES9     WHENEQ    *BLANKS
     C                   MOVEL     FELT          SVZH_RES9
     C                   ENDSL
     C                   ENDSL
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR for å behandle FII-segmentet                      FII  /
      *////////////////////////////////////////////////////////////
     C     FII           BEGSR
      *_____________________
      * 3035 PARTY QUALIFIER
      *"""""""""""""""""""""
     C                   EXSR      GETDED
     C                   MOVEL     FELT          FII_3035          3
      *____________________________
      * C078 ACCOUNT IDENTIFICATION
      *""""""""""""""""""""""""""""
     C                   EXSR      GETDED
     C     TEGN          CABEQ     LOGAPP        FIIEND
      *________________________________
      * C088 INSTITUTION IDENTIFICATION
      *""""""""""""""""""""""""""""""""
     C                   EXSR      GETDED
     C                   EXSR      GETDED
     C                   EXSR      GETDED
     C                   EXSR      GETDED
     C                   EXSR      GETDED
     C                   EXSR      GETDED
     C                   EXSR      GETDED
     C                   EXSR      GETDED
     C                   MOVEL     FELT          FII_3436         70

     C     FIIEND        TAG
     C                   SELECT
     C     FII_3035      WHENEQ    'XX'
     C                   MOVEL     FII_3436      SVZH_KOMR
     C                   ENDSL

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR for å behandle FTX-segmentet                      FTX  /
      *////////////////////////////////////////////////////////////
     C     FTX           BEGSR
     C                   EVAL      FTX_4440A=*BLANKS
     C                   EVAL      FTX_4440B=*BLANKS
     C                   EVAL      FTX_4440C=*BLANKS
     C                   EVAL      FTX_4440D=*BLANKS
     C                   EVAL      FTX_4440E=*BLANKS
      *_______________
      * 4451 QUALIFIER
      *"""""""""""""""
     C                   EXSR      GETDED
     C                   MOVEL     FELT          FTX_4451          3
      *__________
      * 4453 CODE
      *""""""""""
     C                   EXSR      GETDED
     C                   MOVEL     FELT          FTX_4453          3
     C     TEGN          CABEQ     LOGAPP        FTXEND
      *____________________
      * C107 TEXT REFERENCE
      *""""""""""""""""""""
      * 4441 Free text identification
     C                   EXSR      GETDED
     C                   MOVEL     FELT          FTX_4441         17
     C     TEGN          CABEQ     LOGAPP        FTXEND
      *____________________
      * C108 TEXT LITERAL C
      *""""""""""""""""""""
      * 4440 Free text
     C                   EXSR      GETDED
     C                   MOVEL     FELT          FTX_4440A        70
     C     TEGN          CABEQ     LOGAPP        FTXEND
      * 4440 Free text
     C                   EXSR      GETDED
     C                   MOVEL     FELT          FTX_4440B        70
     C     TEGN          CABEQ     LOGAPP        FTXEND
      * 4440 Free text
     C                   EXSR      GETDED
     C                   MOVEL     FELT          FTX_4440C        70
     C     TEGN          CABEQ     LOGAPP        FTXEND
      * 4440 Free text
     C                   EXSR      GETDED
     C                   MOVEL     FELT          FTX_4440D        70
     C     TEGN          CABEQ     LOGAPP        FTXEND
      * 4440 Free text
     C                   EXSR      GETDED
     C                   MOVEL     FELT          FTX_4440E        70
     C     TEGN          CABEQ     LOGAPP        FTXEND
      * 4440 Free text
     C                   EXSR      GETDED
     C                   MOVEL     FELT          FTX_4440F        70
     C     TEGN          CABEQ     LOGAPP        FTXEND
      *
     C     FTXEND        TAG
     C                   SELECT
     C     FTX_4451      WHENEQ    'PAI'
     C                   MOVEL     FTX_4453      SVZH_BETK
     C     FTX_4451      WHENEQ    'ADO'
     C                   MOVEL     FTX_4441      SVZH_KON1
     C     FTX_4451      WHENEQ    'AAA'
     C                   MOVEL     FTX_4440A     SVZV_VASL
     C                   MOVEL     FTX_4440B     SVZV_VAS2
     C                   MOVEL     FTX_4440C     SVZV_VAS3
     C                   MOVEL     FTX_4440D     SVZV_VAS4
     C                   MOVEL     FTX_4440E     SVZV_VAS5

     C     FTX_4451      WHENEQ    'SU'
     C                   SELECT
     C     SVZV_SUKO     WHENEQ    *BLANKS
     C                   MOVEL     FTX_4441      SVZV_SUKO
     C     SVZV_SUK6     WHENEQ    *BLANKS
     C                   MOVEL     FTX_4441      SVZV_SUK6
     C     SVZV_SUKB     WHENEQ    *BLANKS
     C                   MOVEL     FTX_4441      SVZV_SUKB
     C                   ENDSL
     C                   SELECT
     C     SVZV_SUKO     WHENNE    *BLANKS
     C     SVZV_SUTX     ANDEQ     *BLANKS
     C                   MOVEL     FTX_4440A     SVZV_SUTX
     C                   MOVEL     FTX_4440B     SVZV_SUT2
     C                   MOVEL     FTX_4440C     SVZV_SUT3
     C                   MOVEL     FTX_4440D     SVZV_SUT4
     C                   MOVEL     FTX_4440E     SVZV_SUT5
     C     SVZV_SUK6     WHENNE    *BLANKS
     C     SVZV_SUT6     ANDEQ     *BLANKS
     C                   MOVEL     FTX_4440A     SVZV_SUT6
     C                   MOVEL     FTX_4440B     SVZV_SUT7
     C                   MOVEL     FTX_4440C     SVZV_SUT8
     C                   MOVEL     FTX_4440D     SVZV_SUT9
     C                   MOVEL     FTX_4440E     SVZV_SUTA
     C     SVZV_SUKB     WHENNE    *BLANKS
     C     SVZV_SUTB     ANDEQ     *BLANKS
     C                   MOVEL     FTX_4440A     SVZV_SUTB
     C                   MOVEL     FTX_4440B     SVZV_SUTC
     C                   MOVEL     FTX_4440C     SVZV_SUTD
     C                   MOVEL     FTX_4440D     SVZV_SUTE
     C                   MOVEL     FTX_4440E     SVZV_SUTF
     C                   ENDSL

     C                   ENDSL
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR for å behandle FTX-segmentet - 2                 FTX2  /
      *////////////////////////////////////////////////////////////
     C     FTX2          BEGSR
     C                   SELECT
     C     FTX_4440A     WHENEQ    *BLANKS
     C                   MOVEL     FELT          FTX_4440A        70
     C     FTX_4440B     WHENEQ    *BLANKS
     C                   MOVEL     FELT          FTX_4440B        70
     C     FTX_4440C     WHENEQ    *BLANKS
     C                   MOVEL     FELT          FTX_4440C        70
     C     FTX_4440D     WHENEQ    *BLANKS
     C                   MOVEL     FELT          FTX_4440D        70
     C     FTX_4440E     WHENEQ    *BLANKS
     C                   MOVEL     FELT          FTX_4440E        70
     C     FTX_4440F     WHENEQ    *BLANKS
     C                   MOVEL     FELT          FTX_4440F        70
     C     FTX_4440G     WHENEQ    *BLANKS
     C                   MOVEL     FELT          FTX_4440G        70
     C     FTX_4440H     WHENEQ    *BLANKS
     C                   MOVEL     FELT          FTX_4440H        70
     C     FTX_4440I     WHENEQ    *BLANKS
     C                   MOVEL     FELT          FTX_4440I        70
     C     FTX_4440J     WHENEQ    *BLANKS
     C                   MOVEL     FELT          FTX_4440J        70
     C                   ENDSL
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR for å behandle GIS-segmentet                      GIS  /
      *////////////////////////////////////////////////////////////
     C     GIS           BEGSR
      *_________________________
      * C529 PRCESSING INDICATOR
      *"""""""""""""""""""""""""
      * 7365 Processing indicator, coded
     C                   EXSR      GETDED
     C                   MOVEL     FELT          GIS_7365          3
      * 1131 Code list qualifier
     C                   EXSR      GETDED
     C                   MOVEL     FELT          GIS_1131          3
     C     TEGN          CABEQ     LOGAPP        GISEND
      * 3055 Code list responsible agency, coded
     C                   EXSR      GETDED
     C     TEGN          CABEQ     LOGAPP        GISEND
      * 7187 Processing type identification
     C                   EXSR      GETDED
     C                   MOVEL     FELT          GIS_7187         17
      *
     C     GISEND        TAG
      *
     C                   SELECT
     C     GIS_1131      WHENEQ    '71'
     C                   MOVEL     GIS_7365      SVZH_TART
     C     GIS_1131      WHENEQ    '109'
     C                   MOVEL     GIS_7365      SVZH_CONT
     C     GIS_1131      WHENEQ    'ZUR'
     C                   MOVEL     GIS_7365      SVZH_ZURK
     C     GIS_1131      WHENEQ    'ZKS'
     C                   MOVEL     GIS_7365      SVZH_ZKSK
     C     GIS_1131      WHENEQ    'AML'
     C                   MOVEL     GIS_7365      SVZH_AMLK
     C     GIS_1131      WHENEQ    'ZOT'
     C                   MOVEL     GIS_7365      SVZH_ZOTK
     C     GIS_1131      WHENEQ    '116'
     C                   MOVEL     GIS_7365      SVZV_FOKD
     C     GIS_1131      WHENEQ    'Z11'
     C                   MOVEL     GIS_7365      SVZV_CALL
     C     GIS_1131      WHENEQ    'ZF'
     C     GIS_7365      IFEQ      '1'
     C                   MOVEL     GIS_7187      SVZV_EUP1
     C                   ELSE
     C                   MOVEL     GIS_7187      SVZV_EUP2
     C                   ENDIF
     C     GIS_1131      WHENEQ    'ZA'
     C                   SELECT
     C     GIS_7365      WHENEQ    '3'
     C                   MOVEL     GIS_7187      SVZV_VATI
     C     GIS_7365      WHENEQ    '4'
     C                   MOVEL     GIS_7187      SVZV_VAT4
     C     GIS_7365      WHENEQ    '5'
     C                   MOVEL     GIS_7187      SVZV_VAT5
     C                   ENDSL
     C                   ENDSL
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR for å behandle LOC-segmentet                      LOC  /
      *////////////////////////////////////////////////////////////
     C     LOC           BEGSR
      *_______________
      * 3227 QUALIFIER
      *"""""""""""""""
     C                   EXSR      GETDED
     C                   MOVEL     FELT          LOC_3227          3
      *________
      * C517 ID
      *""""""""
      * 3225 ID
     C                   EXSR      GETDED
     C                   MOVEL     FELT          LOC_3225         25
     C     TEGN          CABEQ     LOGAPP        LOCEND
      * 1131 Code list qualifier
     C                   EXSR      GETDED
     C                   MOVEL     FELT          LOC_1131          3
     C     TEGN          CABEQ     LOGAPP        LOCEND
      * 3055 Code list responsible agency, coded
     C                   EXSR      GETDED
     C                   MOVEL     FELT          LOC_3055          3
     C     TEGN          CABEQ     LOGAPP        LOCEND
      * 3224 Place/location C an..70 Landkod, se kodlista K418
     C                   EXSR      GETDED
     C                   MOVEL     FELT          LOC_3224         70
     C     TEGN          CABEQ     LOGAPP        LOCEND
      *
     C     LOCEND        TAG
     C                   SELECT
     C     LOC_3227      WHENEQ    '17'
     C                   MOVEL     LOC_3225      SVZH_UTFA
     C     LOC_3227      WHENEQ    '18'
     C                   MOVEL     LOC_3225      SVZH_GOLK
     C     LOC_3227      WHENEQ    '27'
     C                   MOVEL     LOC_3225      SVZV_ULKD
     C     LOC_3227      WHENEQ    '35'
     C                   MOVEL     LOC_3225      SVZH_AVUT
     C     LOC_3227      WHENEQ    '36'
     C                   MOVEL     LOC_3225      SVZH_AUBE
     C     LOC_3227      WHENEQ    '116'
     C                   MOVEL     LOC_3225      SVZH_UTEX
     C     LOC_3227      WHENEQ    'ZIL'
     C                   MOVEL     LOC_3225      SVZV_LAGI
     C                   MOVEL     LOC_1131      SVZV_LAGT
     C                   MOVEL     LOC_3224      SVZV_LAGL
     C                   ENDSL
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR for å behandle MEA-segmentet                      MEA  /
      *////////////////////////////////////////////////////////////
     C     MEA           BEGSR
      *_______________
      * 6311 QUALIFIER
      *"""""""""""""""
     C                   EXSR      GETDED
     C                   MOVEL     FELT          MEA_6311          3
      *_____________
      * C502 DETAILS
      *"""""""""""""
     C                   EXSR      GETDED
     C                   MOVEL     FELT          MEA_6313          3
      *____________
      * C174 VALUES
      *""""""""""""
      * 6411 qualifier
     C                   EXSR      GETDED
      * 6314 qualifier
     C                   EXSR      GETDED
     C                   MOVEA     FELT          A
     C                   EXSR      GETNUM
     C                   SELECT
     C     MEA_6313      WHENEQ    'AAA'
     C                   EVAL      SVZV_NETO=FELT
     C     MEA_6313      WHENEQ    'AAB'
     C                   EVAL      SVZV_BRUT=FELT
     C     MEA_6313      WHENEQ    'AAD'
     C                   EVAL      SVZH_BRUT=FELT
     C     MEA_6313      WHENEQ    'AAS'
     C                   EVAL      SVZV_ANKV=ZNUM
     C                   ENDSL
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR for å behandle MOA-segmentet                      MOA  /
      *////////////////////////////////////////////////////////////
     C     MOA           BEGSR
      *_______________
      * C516 MONETARY AMOUNT
      *"""""""""""""""
      * 5025 Monetary amount type qualifier
     C                   EXSR      GETDED
     C                   MOVEL     FELT          MOA_5025          3
     C     TEGN          CABEQ     LOGAPP        MOAEND
      * 5004 Monetary amount
     C                   EXSR      GETDED
     C                   MOVEA     FELT          A
     C                   MOVEL     FELT          MOA_5004         11
     C                   EXSR      GETNUM
     C     TEGN          CABEQ     LOGAPP        MOAEND
      * 6345 Currency, coded
     C                   EXSR      GETDED
     C                   MOVEL     FELT          MOA_6345          3

     C     MOAEND        TAG

     C                   SELECT
     C     MOA_5025      WHENEQ    '66'
     C                   EVAL      SVZV_FABL=ZNUM
     C     MOA_5025      WHENEQ    '123'
     C                   EVAL      SVZV_STVA=MOA_5004
     C     MOA_5025      WHENEQ    '70'
     C                   EVAL      SVZH_VUFO=ZNUM
     C     MOA_5025      WHENEQ    '160'
     C                   EVAL      SVZH_OVKO=ZNUM
     C     MOA_5025      WHENEQ    '142'
     C                   EVAL      SVZH_KARA=ZNUM
     C     MOA_5025      WHENEQ    '103'
     C                   EVAL      SVZH_ANRA=ZNUM
     C     MOA_5025      WHENEQ    '144'
     C                   EVAL      SVZH_VUFR=ZNUM
     C                   EVAL      SVZH_VUVA=MOA_6345
     C     MOA_5025      WHENEQ    '39'
     C                   EVAL      SVZH_FABL=ZNUM
     C                   EVAL      SVZH_VAKD=MOA_6345
     C                   ENDSL
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR for å behandle NAD-segmentet                      NAD  /
      *////////////////////////////////////////////////////////////
     C     NAD           BEGSR
     C                   EVAL      NAD_3039=*BLANKS
     C                   EVAL      NAD_1131=*BLANKS
     C                   EVAL      NAD_3036=*BLANKS
     C                   EVAL      NAD_3042A=*BLANKS
     C                   EVAL      NAD_3042B=*BLANKS
     C                   EVAL      NAD_3164=*BLANKS
     C                   EVAL      NAD_3251=*BLANKS
     C                   EVAL      NAD_3207=*BLANKS
      *_______________
      * 3035 QUALIFIER
      *"""""""""""""""
     C                   EXSR      GETDED
     C                   MOVEL     FELT          NAD_3035          2
      *__________________
      * C082 PARTY ID DET
      *""""""""""""""""""
      * 3039 Org.nr
     C                   EXSR      GETDED
     C                   MOVEL     FELT          NAD_3039         12
     C     TEGN          CABEQ     LOGAPP        NADEND
     C     TEGN          CABEQ     LOGPLU        NAD001
      * 1131 Landkod
     C                   EXSR      GETDED
     C                   MOVEL     FELT          NAD_1131          2
     C     TEGN          CABEQ     LOGAPP        NADEND
      *______________________
      * C058 NAME AND ADDRESS
      *""""""""""""""""""""""
     C     NAD001        TAG
     C                   EXSR      GETDED
     C                   MOVEL     FELT          NAD_3124          1
     C     TEGN          CABEQ     LOGAPP        NADEND
      *________________
      * C080 PARTY NAME
      *""""""""""""""""
      * 3036 Namn
     C                   EXSR      GETDED
     C                   MOVEL     FELT          NAD_3036         35
     C     TEGN          CABEQ     LOGAPP        NADEND
      *____________
      * C059 STREET
      *""""""""""""
      * 3042 Gatuadress 1
     C                   EXSR      GETDED
     C                   MOVEL     FELT          NAD_3042A        35
     C     TEGN          CABEQ     LOGAPP        NADEND
     C     TEGN          CABEQ     LOGPLU        NAD002
      * 3042 Gatuadress 2
     C                   EXSR      GETDED
     C                   MOVEL     FELT          NAD_3042B        35
     C     TEGN          CABEQ     LOGAPP        NADEND
      *_______________________
      * 3164 CITY (Postadress)
      *"""""""""""""""""""""""
     C     NAD002        TAG
     C                   EXSR      GETDED
     C                   MOVEL     FELT          NAD_3164         35
     C     TEGN          CABEQ     LOGAPP        NADEND
      *____________________
      * 3229 COUNTRY SUB ID
      *""""""""""""""""""""
     C                   EXSR      GETDED
     C     TEGN          CABEQ     LOGAPP        NADEND
      *________________
      * 3251 POSTNUMMER
      *""""""""""""""""
     C                   EXSR      GETDED
     C                   MOVEL     FELT          NAD_3251         15
     C     TEGN          CABEQ     LOGAPP        NADEND
      *_____________
      * 3207 LANDKOD
      *"""""""""""""
     C                   EXSR      GETDED
     C                   MOVEL     FELT          NAD_3207          2

     C     NADEND        TAG
     C                   SELECT
     C     NAD_3035      WHENEQ    'EX'
     C     NAD_1131      CAT       NAD_3039:0    SVZH_AVEO
     C                   MOVEL     NAD_3036      SVZH_AVNA
     C                   MOVEL     NAD_3042A     SVZH_AVA1
     C                   MOVEL     NAD_3042B     SVZH_AVA2
     C                   MOVEL     NAD_3164      SVZH_AVPA
     C                   MOVEL     NAD_3251      SVZH_AVPN
     C                   MOVEL     NAD_3207      SVZH_AVLK
     C     NAD_3035      WHENEQ    'CN'
     C     NAD_1131      CAT       NAD_3039:0    SVZH_MOEO
     C                   MOVEL     NAD_3036      SVZH_MONA
     C                   MOVEL     NAD_3042A     SVZH_MOA1
     C                   MOVEL     NAD_3042B     SVZH_MOA2
     C                   MOVEL     NAD_3164      SVZH_MOPA
     C                   MOVEL     NAD_3251      SVZH_MOPN
     C                   MOVEL     NAD_3207      SVZH_MOLK
     C     NAD_3035      WHENEQ    'DT'
     C     NAD_1131      CAT       NAD_3039:0    SVZH_DKEO
     C                   MOVEL     NAD_3124      SVZH_DKKO
     C                   MOVEL     NAD_3036      SVZH_DKNA
     C                   MOVEL     NAD_3042A     SVZH_DKA1
     C                   MOVEL     NAD_3042B     SVZH_DKA2
     C                   MOVEL     NAD_3164      SVZH_DKPA
     C                   MOVEL     NAD_3251      SVZH_DKPN
     C                   MOVEL     NAD_3207      SVZH_DKLK
     C     NAD_3035      WHENEQ    'AE'
     C     NAD_1131      CAT       NAD_3039:0    SVZH_OMEO
     C                   MOVEL     NAD_3124      SVZH_OMTY
     C                   MOVEL     NAD_3036      SVZH_OMNA
     C                   MOVEL     NAD_3042A     SVZH_OMA1
     C                   MOVEL     NAD_3042B     SVZH_OMA2
     C                   MOVEL     NAD_3164      SVZH_OMPA
     C                   MOVEL     NAD_3251      SVZH_OMPN
     C                   MOVEL     NAD_3207      SVZH_OMLK
     C     NAD_3035      WHENEQ    'EI'
     C                   MOVEL     NAD_3039      SVZH_KON3
     C                   ENDSL

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR for å behandle PAC-segmentet                      PAC  /
      *////////////////////////////////////////////////////////////
     C     PAC           BEGSR
     C                   EVAL      PAC_7064B=*BLANKS
      *________________________
      * 7224 NUMBER OF PACKAGES
      *""""""""""""""""""""""""
     C                   EXSR      GETDED
      *_______________________
      * C531 PACKAGING DETAILS
      *"""""""""""""""""""""""
      * 7075 Packaging level, coded
     C                   EXSR      GETDED
      *__________________
      * C202 PACKAGE TYPE
      *""""""""""""""""""
      * 7065 Type of packages identification
     C                   EXSR      GETDED
      *_________________________________
      * C402 PACKAGE TYPE IDENTIFICATION
      *"""""""""""""""""""""""""""""""""
      * 7077 Item description list, coded
     C                   EXSR      GETDED
     C                   MOVEL     FELT          PAC_7077          4
     C     TEGN          CABEQ     LOGAPP        PACEND
      * 7064 Type of packages
     C                   EXSR      GETDED
     C                   MOVEL     FELT          PAC_7064          5
     C     TEGN          CABEQ     LOGAPP        PACEND
      * 7143 Item number type, coded
     C                   EXSR      GETDED
     C     TEGN          CABEQ     LOGAPP        PACEND
      * 7064 Type of packages
     C                   EXSR      GETDED
     C                   MOVEL     FELT          PAC_7064B         5

     C     PACEND        TAG
     C                   SELECT
     C     SVZV_KOSL     WHENEQ    *BLANKS
     C                   EVAL      SVZV_KOTA=PAC_7064
     C                   EVAL      SVZV_DEL1=PAC_7064B
     C                   EVAL      SVZV_KOSL=PAC_7077
     C     SVZV_KOS2     WHENEQ    *BLANKS
     C                   EVAL      SVZV_KOT2=PAC_7064
     C                   EVAL      SVZV_DEL2=PAC_7064B
     C                   EVAL      SVZV_KOS2=PAC_7077
     C     SVZV_KOS3     WHENEQ    *BLANKS
     C                   EVAL      SVZV_KOT3=PAC_7064
     C                   EVAL      SVZV_DEL3=PAC_7064B
     C                   EVAL      SVZV_KOS3=PAC_7077
     C     SVZV_KOS4     WHENEQ    *BLANKS
     C                   EVAL      SVZV_KOT4=PAC_7064
     C                   EVAL      SVZV_DEL4=PAC_7064B
     C                   EVAL      SVZV_KOS4=PAC_7077
     C     SVZV_KOS5     WHENEQ    *BLANKS
     C                   EVAL      SVZV_KOT5=PAC_7064
     C                   EVAL      SVZV_DEL5=PAC_7064B
     C                   EVAL      SVZV_KOS5=PAC_7077
     C                   ENDSL
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR for å behandle PCI-segmentet                      PCI  /
      *////////////////////////////////////////////////////////////
     C     PCI           BEGSR
      *__________________________
      * 4233 MARKING INTSRUCTIONS
      *""""""""""""""""""""""""""
     C                   EXSR      GETDED
      *__________________
      * C210 MARKS&LABELS
      *""""""""""""""""""
      * 7102 Godsmærkning
     C                   EXSR      GETDED
     C                   MOVEL     FELT          PCI_7102A        42
     C     TEGN          CABEQ     LOGAPP        PCIEND
      * 7102 Godsmærkning
     C                   EXSR      GETDED
     C                   MOVEL     FELT          PCI_7102B         7
     C                   CAT       PCI_7102B:0   PCI_7102A

     C     PCIEND        TAG
     C                   SELECT
     C     SVZV_GODM     WHENEQ    *BLANKS
     C                   MOVEL     PCI_7102A     SVZV_GODM
     C     SVZV_GOD2     WHENEQ    *BLANKS
     C                   MOVEL     PCI_7102A     SVZV_GOD2
     C     SVZV_GOD3     WHENEQ    *BLANKS
     C                   MOVEL     PCI_7102A     SVZV_GOD3
     C     SVZV_GOD4     WHENEQ    *BLANKS
     C                   MOVEL     PCI_7102A     SVZV_GOD4
     C     SVZV_GOD5     WHENEQ    *BLANKS
     C                   MOVEL     PCI_7102A     SVZV_GOD5
     C                   ENDSL
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR for å behandle RFF-segmentet                      RFF  /
      *////////////////////////////////////////////////////////////
     C     RFF           BEGSR
      *_______________
      * C506 REFERENCE
      *"""""""""""""""
      * 1153 Qualifier
     C                   EXSR      GETDED
     C                   MOVEL     FELT          RFF_1153          3
      * 1154 Number
     C                   EXSR      GETDED
     C                   SELECT
     C     RFF_1153      WHENEQ    'ZTI'
     C                   MOVEL     FELT          SVZH_TUID
     C     RFF_1153      WHENEQ    'ABE'
     C                   MOVEL     FELT          SVZH_RFAB
     C     RFF_1153      WHENEQ    'ABI'
     C                   MOVEL     FELT          SVZH_UPPS
     C     RFF_1153      WHENEQ    'AMJ'
     C                   MOVEL     FELT          SVZH_SAOM
     C     RFF_1153      WHENEQ    'ACD'
     C                   MOVEL     FELT          SVZH_RFAC
     C     RFF_1153      WHENEQ    'AAQ'
     C                   SELECT
     C     SVZV_CO01     WHENEQ    *BLANKS
     C                   MOVEL     FELT          SVZV_CO01
     C     SVZV_CO02     WHENEQ    *BLANKS
     C                   MOVEL     FELT          SVZV_CO02
     C     SVZV_CO03     WHENEQ    *BLANKS
     C                   MOVEL     FELT          SVZV_CO03
     C     SVZV_CO04     WHENEQ    *BLANKS
     C                   MOVEL     FELT          SVZV_CO04
     C     SVZV_CO05     WHENEQ    *BLANKS
     C                   MOVEL     FELT          SVZV_CO05
     C     SVZV_CO06     WHENEQ    *BLANKS
     C                   MOVEL     FELT          SVZV_CO06
     C     SVZV_CO07     WHENEQ    *BLANKS
     C                   MOVEL     FELT          SVZV_CO07
     C     SVZV_CO08     WHENEQ    *BLANKS
     C                   MOVEL     FELT          SVZV_CO08
     C     SVZV_CO09     WHENEQ    *BLANKS
     C                   MOVEL     FELT          SVZV_CO09
     C     SVZV_CO10     WHENEQ    *BLANKS
     C                   MOVEL     FELT          SVZV_CO10
     C     SVZV_CO11     WHENEQ    *BLANKS
     C                   MOVEL     FELT          SVZV_CO11
     C     SVZV_CO12     WHENEQ    *BLANKS
     C                   MOVEL     FELT          SVZV_CO12
     C     SVZV_CO13     WHENEQ    *BLANKS
     C                   MOVEL     FELT          SVZV_CO13
     C     SVZV_CO14     WHENEQ    *BLANKS
     C                   MOVEL     FELT          SVZV_CO14
     C     SVZV_CO15     WHENEQ    *BLANKS
     C                   MOVEL     FELT          SVZV_CO15
     C     SVZV_CO16     WHENEQ    *BLANKS
     C                   MOVEL     FELT          SVZV_CO16
     C     SVZV_CO17     WHENEQ    *BLANKS
     C                   MOVEL     FELT          SVZV_CO17
     C     SVZV_CO18     WHENEQ    *BLANKS
     C                   MOVEL     FELT          SVZV_CO18
     C     SVZV_CO19     WHENEQ    *BLANKS
     C                   MOVEL     FELT          SVZV_CO19
     C     SVZV_CO20     WHENEQ    *BLANKS
     C                   MOVEL     FELT          SVZV_CO20
     C                   ENDSL
     C                   ENDSL
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR for å behandle SEL-segmentet                      SEL  /
      *////////////////////////////////////////////////////////////
     C     SEL           BEGSR
      *_________________
      * 9308 SEAL NUMBER
      *"""""""""""""""""
     C                   EXSR      GETDED
     C                   MOVEL     FELT          SEL_9308          3
      *_________________
      * C215 SEAL ISSUER
      *"""""""""""""""""
      * 9303 Sealing party, coded
     C                   EXSR      GETDED
      * 1131 Code list qualifier
     C                   EXSR      GETDED
      * 3055 Code list responsible agency
     C                   EXSR      GETDED
      * 9302 Sealing party
     C                   EXSR      GETDED
     C                   SELECT
     C     SEL_9308      WHENEQ    '0'
     C                   SELECT
     C     SVZH_SEL1     WHENEQ    *BLANKS
     C                   MOVEL     FELT          SVZH_SEL1
     C     SVZH_SEL2     WHENEQ    *BLANKS
     C                   MOVEL     FELT          SVZH_SEL2
     C     SVZH_SEL3     WHENEQ    *BLANKS
     C                   MOVEL     FELT          SVZH_SEL3
     C     SVZH_SEL4     WHENEQ    *BLANKS
     C                   MOVEL     FELT          SVZH_SEL4
     C     SVZH_SEL5     WHENEQ    *BLANKS
     C                   MOVEL     FELT          SVZH_SEL5
     C     SVZH_SEL6     WHENEQ    *BLANKS
     C                   MOVEL     FELT          SVZH_SEL6
     C     SVZH_SEL7     WHENEQ    *BLANKS
     C                   MOVEL     FELT          SVZH_SEL7
     C     SVZH_SEL8     WHENEQ    *BLANKS
     C                   MOVEL     FELT          SVZH_SEL8
     C     SVZH_SEL9     WHENEQ    *BLANKS
     C                   MOVEL     FELT          SVZH_SEL9
     C                   ENDSL
     C                   ENDSL
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR for å behandle TAX-segmentet                      TAX  /
      *////////////////////////////////////////////////////////////
     C     TAX           BEGSR
      *_____________________________________
      * 5283 DUTY/TAX/FEE FUNCTION QUALIFIER
      *"""""""""""""""""""""""""""""""""""""
     C                   EXSR      GETDED
     C                   MOVEL     FELT          TAX_5283          3
      *_______________________
      * C241 DUTY/TAX/FEE TYPE
      *"""""""""""""""""""""""
      * 5153 Duty/tax/fee type, coded
     C                   EXSR      GETDED
     C                   MOVEL     FELT          TAX_5153          3
      *_________________________________
      * C533 DUTY/TAX/FEE ACCOUNT DETAIL
      *"""""""""""""""""""""""""""""""""
      * 5289 Duty/tax/fee account identification
     C                   EXSR      GETDED
      *___________________________________
      * 5286 DUTY/TAX/FEE ASSESSMENT BASIS
      *"""""""""""""""""""""""""""""""""""
     C                   EXSR      GETDED
     C                   MOVEL     FELT          TAX_5286         15
      *_________________________
      * C243 DUTY/TAX/FEE DETAIL
      *"""""""""""""""""""""""""
      * 5279 Duty/tax/fee rate identification
     C                   EXSR      GETDED
      * 1131 Code list qualifier
     C                   EXSR      GETDED
      * 3055 Code list responsible agency, coded
     C                   EXSR      GETDED
      * 5278 Duty/tax/fee rate
     C                   EXSR      GETDED
     C                   MOVEL     FELT          TAX_5278         17
      * 5273 Duty/tax/fee rate basis identification
     C                   EXSR      GETDED
     C                   MOVEL     FELT          TAX_5273         12

     C                   SELECT
     C     TAX_5283      WHENEQ    '6'
     C                   SELECT
     C     SVZV_TXA1     WHENEQ    *BLANKS
     C                   MOVEL     TAX_5153      SVZV_TXA1
     C                   MOVEL     TAX_5286      SVZV_TXB1
     C                   MOVEL     TAX_5278      SVZV_TXC1
     C                   MOVEL     TAX_5273      SVZV_TXD1
     C     SVZV_TXA2     WHENEQ    *BLANKS
     C                   MOVEL     TAX_5153      SVZV_TXA2
     C                   MOVEL     TAX_5286      SVZV_TXB2
     C                   MOVEL     TAX_5278      SVZV_TXC2
     C                   MOVEL     TAX_5273      SVZV_TXD2
     C     SVZV_TXA3     WHENEQ    *BLANKS
     C                   MOVEL     TAX_5153      SVZV_TXA3
     C                   MOVEL     TAX_5286      SVZV_TXB3
     C                   MOVEL     TAX_5278      SVZV_TXC3
     C                   MOVEL     TAX_5273      SVZV_TXD3
     C     SVZV_TXA4     WHENEQ    *BLANKS
     C                   MOVEL     TAX_5153      SVZV_TXA4
     C                   MOVEL     TAX_5286      SVZV_TXB4
     C                   MOVEL     TAX_5278      SVZV_TXC4
     C                   MOVEL     TAX_5273      SVZV_TXD4
     C     SVZV_TXA5     WHENEQ    *BLANKS
     C                   MOVEL     TAX_5153      SVZV_TXA5
     C                   MOVEL     TAX_5286      SVZV_TXB5
     C                   MOVEL     TAX_5278      SVZV_TXC5
     C                   MOVEL     TAX_5273      SVZV_TXD5
     C     SVZV_TXA6     WHENEQ    *BLANKS
     C                   MOVEL     TAX_5153      SVZV_TXA6
     C                   MOVEL     TAX_5286      SVZV_TXB6
     C                   MOVEL     TAX_5278      SVZV_TXC6
     C                   MOVEL     TAX_5273      SVZV_TXD6
     C     SVZV_TXA7     WHENEQ    *BLANKS
     C                   MOVEL     TAX_5153      SVZV_TXA7
     C                   MOVEL     TAX_5286      SVZV_TXB7
     C                   MOVEL     TAX_5278      SVZV_TXC7
     C                   MOVEL     TAX_5273      SVZV_TXD7
     C     SVZV_TXA8     WHENEQ    *BLANKS
     C                   MOVEL     TAX_5153      SVZV_TXA8
     C                   MOVEL     TAX_5286      SVZV_TXB8
     C                   MOVEL     TAX_5278      SVZV_TXC8
     C                   MOVEL     TAX_5273      SVZV_TXD8
     C     SVZV_TXA9     WHENEQ    *BLANKS
     C                   MOVEL     TAX_5153      SVZV_TXA9
     C                   MOVEL     TAX_5286      SVZV_TXB9
     C                   MOVEL     TAX_5278      SVZV_TXC9
     C                   MOVEL     TAX_5273      SVZV_TXD9
     C                   ENDSL
     C                   ENDSL

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR for å behandle TDT-segmentet                      TDT  /
      *////////////////////////////////////////////////////////////
     C     TDT           BEGSR
     C                   MOVE      *BLANKS       TDT_8051
     C                   MOVE      *BLANKS       TDT_8028
     C                   MOVE      *BLANKS       TDT_8067
     C                   MOVE      *BLANKS       TDT_8179
     C                   MOVE      *BLANKS       TDT_8213
     C                   MOVE      *BLANKS       TDT_8453
      *_______________
      * 8051 QUALIFIER
      *"""""""""""""""
     C                   EXSR      GETDED
     C                   MOVEL     FELT          TDT_8051          3
      *_________________________________
      * 8028 CONVEYANCE REFERENCE NUMBER
      *"""""""""""""""""""""""""""""""""
     C                   EXSR      GETDED
     C                   MOVEL     FELT          TDT_8028         17
      *_______________________
      * C220 MODE OF TRANSPORT
      *"""""""""""""""""""""""
      * 8067 Mode of transport, coded
     C                   EXSR      GETDED
     C                   MOVEL     FELT          TDT_8067          3
     C     TEGN          CABEQ     LOGAPP        TDTEND
      *_______________________
      * C228 TRANSPORT MEANS
      *"""""""""""""""""""""""
      * 8179 Type of means of transport identification
     C                   EXSR      GETDED
     C                   MOVEL     FELT          TDT_8179          8
     C     TEGN          CABEQ     LOGAPP        TDTEND
      *______________
      * C040 CARRIERR
      *""""""""""""""
      * 3127 Carrier identification
     C                   EXSR      GETDED
      *______________________________
      * 8101 TRANSIT DIRECTION, CODED
      *""""""""""""""""""""""""""""""
     C                   EXSR      GETDED
      *_______________________________________
      * C401 EXCESS TRANSPORTATION INFORMATION
      *"""""""""""""""""""""""""""""""""""""""
      * 8457 Excess transportation reason, coded
     C                   EXSR      GETDED
      *______________________________
      * C222 TRANSPORT IDENTIFICATION
      *""""""""""""""""""""""""""""""
      * 8213 Id. of means of transport identification
     C                   EXSR      GETDED
     C                   MOVEL     FELT          TDT_8213          9
     C     TEGN          CABEQ     LOGAPP        TDTEND
      * 1131 Code list qualifier
     C                   EXSR      GETDED
      * 3055 Code list responsible agency, coded
     C                   EXSR      GETDED
      * 8212 Id. of means of transport
     C                   EXSR      GETDED
     C                   MOVEL     FELT          TDT_8212         35
     C     TEGN          CABEQ     LOGAPP        TDTEND
      * 8453 Nationality of means of transport, coded
     C                   EXSR      GETDED
     C                   MOVEL     FELT          TDT_8453          3

     C     TDTEND        TAG
     C                   SELECT
     C     TDT_8051      WHENEQ    '1'
     C                   MOVEL     TDT_8067      SVZH_TRIN
     C     TDT_8051      WHENEQ    '10'
     C                   MOVEL     TDT_8179      SVZV_BETK
     C                   MOVEL     TDT_8213      SVZV_FNKD
     C     TDT_8051      WHENEQ    '11'
     C                   MOVEL     TDT_8067      SVZH_TRGR
     C                   MOVEL     TDT_8212      SVZH_TRID
     C                   MOVEL     TDT_8453      SVZH_TRLK
     C     TDT_8051      WHENEQ    '12'
     C                   MOVEL     TDT_8212      SVZH_TRAI
     C                   ENDSL

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR for UNB                                           UNB  /
      *////////////////////////////////////////////////////////////
     C     UNB           BEGSR
      *___________________
      * Clear recordformat
      *"""""""""""""""""""
     C                   CLEAR                   EDISR
     C                   EVAL      XMBR=WMBR
     C                   EVAL      SSN=XMBRB
     C                   MOVE      'C'           SST
     C                   MOVE      'R'           SSR
     C                   MOVEL     WDATO         SDT
     C                   MOVEL     WTID          STM
     C                   MOVEL     WLIB          SLIB
     C                   MOVEL     WFIL          SFILE
     C                   MOVEL     WMBR          SMBR
     C                   EVAL      S0036=1
      *_______________________
      * S001 SYNTAX IDENTIFIER
      *"""""""""""""""""""""""
      * 0001 Syntax identifier
     C                   EXSR      GETDED
      * 0002 Syntax version number
     C                   EXSR      GETDED
      *________________________
      * S002 INTERCHANGE SENDER
      *""""""""""""""""""""""""
      * 0004 Sender identification
     C                   EXSR      GETDED
     C                   MOVEL     FELT          S0004
     C     TEGN          CABEQ     LOGPLU        UNB001
      * 0007 Partner identification code qualifier
     C                   EXSR      GETDED
     C     TEGN          CABEQ     LOGPLU        UNB001
      * 0008 Address for reverse routing
     C                   EXSR      GETDED
      *___________________________
      * S003 INTERCHANGE RECIPIENT
      *"""""""""""""""""""""""""""
     C     UNB001        TAG
      * 0010 Recipient identification
     C                   EXSR      GETDED
     C                   MOVEL     FELT          S0010
     C     TEGN          CABEQ     LOGPLU        UNB002
      * 0007 Partner identification code qualifier
     C                   EXSR      GETDED
     C     TEGN          CABEQ     LOGPLU        UNB002
      * 0014 Routing address
     C                   EXSR      GETDED
      *______________________________
      * S004 DATE/TIME OF PREPARATION
      *""""""""""""""""""""""""""""""
     C     UNB002        TAG
      * 0017 Date of preparation
     C                   EXSR      GETDED
      * 0019 Time of preparation
     C                   EXSR      GETDED
      *___________________________________
      * 0020 INTERCHANGE CONTROL REFERENCE
      *"""""""""""""""""""""""""""""""""""
     C                   EXSR      GETDED
     C                   MOVEL     FELT          S0020
     C     TEGN          CABEQ     LOGAPP        UNBEND
      *___________________________________
      * S005 RECIPIENTS REFERENCE PASSWORD
      *"""""""""""""""""""""""""""""""""""
      * 0022 Recipients reference/password
     C                   EXSR      GETDED
     C     TEGN          CABEQ     LOGAPP        UNBEND
     C     TEGN          CABEQ     LOGPLU        UNB003
      * 0025 Recipients reference/password qualifier
     C                   EXSR      GETDED
     C     TEGN          CABEQ     LOGAPP        UNBEND
      *___________________________
      * 0026 APPLICATION REFERENCE
      *"""""""""""""""""""""""""""
     C     UNB003        TAG
     C                   EXSR      GETDED
     C                   MOVEL     FELT          S0026
     C     TEGN          CABEQ     LOGAPP        UNBEND
      *______________________________
      * 0029 PROCESSING PRIORITY CODE
      *""""""""""""""""""""""""""""""
     C                   EXSR      GETDED
     C     TEGN          CABEQ     LOGAPP        UNBEND
      *_____________________________
      * 0031 ACKNOWLEDGEMENT REQUEST
      *"""""""""""""""""""""""""""""
     C                   EXSR      GETDED
     C     TEGN          CABEQ     LOGAPP        UNBEND
      *________________________________
      * 0032 COMMUNICATION AGREEMENT ID
      *""""""""""""""""""""""""""""""""
     C                   EXSR      GETDED
     C     TEGN          CABEQ     LOGAPP        UNBEND
      *____________________
      * 0035 TEST INDICATOR
      *""""""""""""""""""""
     C                   EXSR      GETDED
     C                   MOVEL     FELT          S0035
     C     UNBEND        ENDSR
      *////////////////////////////////////////////////////////////
      * SR for UNZ                                           UNZ  /
      *////////////////////////////////////////////////////////////
     C     UNZ           BEGSR
      *_______________________________
      * 0036 INTERCHANGE CONTROL COUNT
      *"""""""""""""""""""""""""""""""
     C                   EXSR      GETDED
     C                   MOVEA     FELT          NU
     C                   MOVE      *ZEROS        NUMVRD
     C     1             DO        6             NR                                         '+'
     C     NU(NR)        IFLT      '0'
     C     NU(NR)        ORGT      '9'
     C                   GOTO      UNZ001
     C                   END
     C                   MULT      10            NUMVRD
     C                   MOVE      NU(NR)        NUTEGN
     C                   ADD       NUTEGN        NUMVRD
     C                   END
     C     UNZ001        TAG
     C                   Z-ADD     NUMVRD        S0036
      *_______________
      * Skriv til EDIS
      *"""""""""""""""
     C     WNUMR         MULT      80            SMBRS
     C                   WRITE     EDISR
      *______________________________________
      * Skicka CONTRL i retur till Tullverket
      *""""""""""""""""""""""""""""""""""""""
     C                   CALL      'SVZ003R'
     C                   PARM                    S0020

     C     UNZEND        ENDSR
      *////////////////////////////////////////////////////////////
      * SR for å behandle UNH-segmentet                      UNH  /
      *////////////////////////////////////////////////////////////
     C     UNH           BEGSR
      *___________________
      * Clear recordformat
      *"""""""""""""""""""
     C                   CLEAR                   EDIMR
     C                   CLEAR                   SVZHR
     C     1             CHAIN     EDIC
     C                   ADD       1             CMN
     C                   UPDATE    EDICR
     C                   EVAL      MSR='R'
     C                   EVAL      MST='C'
     C                   EVAL      MSN=SSN
     C                   EVAL      MMN=CMN
     C                   EVAL      M0004=S0004
     C                   EVAL      M0010=S0010
     C                   EVAL      M0035=S0035
     C                   EVAL      MDT=SDT
     C                   EVAL      MTM=STM
      *______________________________
      * 0062 MESSAGE REFERENCE NUMBER
      *""""""""""""""""""""""""""""""
     C                   EXSR      GETDED
     C                   MOVEL     FELT          M0062
      *________________________
      * S009 MESSAGE IDENTIFIER
      *""""""""""""""""""""""""
      * 0065 Message identifier
     C                   EXSR      GETDED
     C                   MOVEL     FELT          M0065
      * 0052 Message type version number
     C                   EXSR      GETDED
      * 0054 Message type release number
     C                   EXSR      GETDED
      * 0051 Controlling agency
     C                   EXSR      GETDED
     C     TEGN          CABEQ     LOGAPP        UNHEND
     C     TEGN          CABEQ     LOGPLU        UNH001
      * 0057 Association assigned code
     C                   EXSR      GETDED
     C                   MOVEL     FELT          X0057            10
     C     TEGN          CABEQ     LOGAPP        UNHEND
      *_____________________________
      * 0068 COMMON ACCESS REFERENCE
      *"""""""""""""""""""""""""""""
     C     UNH001        TAG
     C                   EXSR      GETDED
     C                   MOVEL     FELT          M0068
     C     TEGN          CABEQ     LOGAPP        UNHEND
      *____________________________
      * S010 STATUS OF THE TRANSFER
      *""""""""""""""""""""""""""""
      * 0070 Sequence message transfer number
     C                   EXSR      GETDED
     C     TEGN          CABEQ     LOGAPP        UNHEND
      * 0073 First/last sequence message transfer indication
     C                   EXSR      GETDED
     C     UNHEND        ENDSR
      *////////////////////////////////////////////////////////////
      * SR for å behandle BGM-segmentet                      BGM  /
      *////////////////////////////////////////////////////////////
     C     BGM           BEGSR
      *___________________________
      * C002 DOCUMENT/MESSAGE NAME
      *"""""""""""""""""""""""""""
      * 1001 Document/message name, coded
     C                   EXSR      GETDED
     C                   MOVEL     FELT          SVZH_MTYP
     C     TEGN          CABEQ     LOGPLU        BGM001
      * 1131 Code list qualifier
     C                   EXSR      GETDED
     C     TEGN          CABEQ     LOGAPP        BGMEND
     C     TEGN          CABEQ     LOGPLU        BGM001
      * 3055 Code list responsible agency code
     C                   EXSR      GETDED
     C     TEGN          CABEQ     LOGAPP        BGMEND
     C     TEGN          CABEQ     LOGPLU        BGM001
      * 1000 Document message name
     C                   EXSR      GETDED
     C                   MOVEL     FELT          SVZH_DEK1
     C     TEGN          CABEQ     LOGAPP        BGMEND
      *_____________________________________
      * C106 DOCUMENT/MESSAGE IDENTIFICATION
      *"""""""""""""""""""""""""""""""""""""
     C     BGM001        TAG
      * 1004 Document/message number
     C                   EXSR      GETDED
     C                   MOVEL     FELT          SVZH_MRNN
     C     TEGN          CABEQ     LOGAPP        BGMEND
     C     TEGN          CABEQ     LOGPLU        BGM002
      * 1056 Version
     C                   EXSR      GETDED
     C     TEGN          CABEQ     LOGAPP        BGMEND
     C     TEGN          CABEQ     LOGPLU        BGM002
      * 1060 Revision number
     C                   EXSR      GETDED
     C     TEGN          CABEQ     LOGAPP        BGMEND
      *_____________________________
      * 1225 MESSAGE FUNCTION, CODED
      *"""""""""""""""""""""""""""""
     C     BGM002        TAG
     C                   EXSR      GETDED
     C                   MOVEL     FELT          SVZH_DEK2
     C     TEGN          CABEQ     LOGAPP        BGMEND
      *__________________________
      * 4343 RESPONSE TYPE, CODED
      *""""""""""""""""""""""""""
     C                   EXSR      GETDED
     C     BGMEND        ENDSR
      *////////////////////////////////////////////////////////////
      * SR for UNS                                           UNS  /
      *////////////////////////////////////////////////////////////
     C     UNS           BEGSR
      *______________________________
      * 0081 SECTION IDENTIFICATIONER
      *""""""""""""""""""""""""""""""
     C                   EXSR      GETDED
      *___________________________
      * Skriv til forrige varupost
      *"""""""""""""""""""""""""""
     C                   IF        FELT='S'
     C                   WRITE     SVZVR
     C                   ENDIF
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR for UNT                                           UNT  /
      *////////////////////////////////////////////////////////////
     C     UNT           BEGSR
      *_____________________________________
      * 0074 NUMBER OF SEGMENTS IN A MESSAGE
      *"""""""""""""""""""""""""""""""""""""
     C                   EXSR      GETDED
      *______________________________
      * 0062 MESSAGE REFERENCE NUMBER
      *""""""""""""""""""""""""""""""
     C                   EXSR      GETDED
     C                   WRITE     EDIMR
     C                   IF        M0065='CUSDEC'
     C                   WRITE     SVZHR
     C                   CALL      'SVZ002R'
     C                   PARM                    SVZH_TUID
     C                   PARM      'C'           CALLTYP           1
     C                   ENDIF

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Get numerical value                               GETNUM /
      *////////////////////////////////////////////////////////////
     C     GETNUM        BEGSR
     C                   MOVE      *ZEROS        ZHEL
     C                   MOVE      *ZEROS        ZDEC
     C                   MOVE      *ZEROS        B
     C                   Z-ADD     1             AX                2 0
     C                   Z-ADD     1             BX                2 0
      * Get Integer
     C     A(AX)         DOWGE     '0'
     C     ZHEL          MULT      10            ZHEL
     C                   MOVE      A(AX)         ZHEL
     C                   ADD       1             AX
     C                   ENDDO
      * Decimal point ?
     C     A(AX)         IFEQ      '.'
     C     A(AX)         OREQ      ','
     C                   ADD       1             AX
     C     A(AX)         DOWGE     '0'
     C                   MOVE      A(AX)         B(BX)
     C                   ADD       1             AX
     C                   ADD       1             BX
     C                   ENDDO
     C                   MOVEA     B             ZDEC6
     C                   ENDIF
      * Concatenate
     C                   Z-ADD     ZHEL          ZNUM             18 6
     C                   ADD       ZDEC          ZNUM
     C                   ENDSR
