     FISTAPLU2  UF   E           K DISK
     FDORDL09   IF   E           K DISK
     FKUNDL01   IF   E           K DISK
     FVAREL01   IF   E           K DISK
     FLLHEAD    UF A E           K DISK
     FLLLINE    O  A E             DISK
     FLLVARE01  IF A E           K DISK
     FLLAGJD02  IF   E           K DISK
     FLLAGJL01  IF   E           K DISK
     FLLTELL    UF A E             DISK
     D                 DS
     D  XORDR                  1     11
     D  XFIRM                  1      2
     D  XAAR                   3      4  0
     D  XPSDN                  5     11  0
     D                 DS
     D  LVLLOK                 1     25
     D  LVLOK1                 1      5
     D  LVLOK2                 6     25
      *
     C                   EXSR      SRINIT
      *
     C     ISTL2K        CHAIN     ISTAPLU2                           33
     C                   MOVE      'P'           PLSTAT
     C                   MOVE      WDT           PLDATO
     C                   MOVEL     WTM           PLTID
     C                   UPDATE    ISTAPLUR
      *
     C     DORD9K        SETLL     DORDL09
     C     DORD9K        READE     DORDL09                                33
     C                   EXSR      SRHODE
     C     *IN33         DOWEQ     '0'
     C     AKTKOD        IFEQ      'A'
     C                   EXSR      SRLIN
     C                   END
     C     DORD9K        READE     DORDL09                                33
     C  N33              ENDDO
      *
     C     LHMN          CHAIN     LLHEAD                             33
     C                   MOVE      XHANT0        LHANT0
     C                   MOVE      XHANTL        LHANTL
     C                   MOVE      XHWVEK        LHWVEK
     C                   MOVE      XHWKUB        LHWKUB
     C                   MOVE      XHTLIN        LHTLIN
     C                   UPDATE    LLHEADR
      *
     C                   MOVE      '1'           *INLR
     C                   RETURN
      *
      *////////////////////////////////////////////////////////////
      * SR for HODE                                        SRHODE /
      *////////////////////////////////////////////////////////////
     C     SRHODE        BEGSR
      *
     C     KUND1K        CHAIN     KUNDL01                            34
      *
      * OPPDATER HODE
     C                   CLEAR                   LLHEADR
     C     1             CHAIN     LLTELL                             34                    1
     C                   ADD       1             LLREC1
     C                   UPDATE    LLTELLR
     C                   MOVE      LLREC1        LHMN
     C     2             CHAIN     LLTELL                             33
     C                   ADD       1             LLREC1
     C                   UPDATE    LLTELLR
     C                   Z-ADD     LLREC1        LHALN
      *
     C                   MOVE      'E'           LHOPR
     C                   MOVE      UGRP          LHGRP
     C                   MOVE      UPNR          LHPNR
      *
     C                   MOVE      KNAVN         LHBNA
     C                   MOVE      ADR1          LHBA1
     C                   MOVE      ADR2          LHBA2
     C                   MOVE      POSTNR        LHBPN
     C                   MOVEL     ADR3          LHBA3
     C                   MOVEL     LAND          LHBLK
      *
     C                   MOVE      WDT           LHTDT
     C                   MOVEL     UPSDN         LHPAK
     C                   MOVEL     XORDR         LHORDR
     C                   MOVE      WDT           LHMDT
     C                   Z-ADD     1             LHKLL
     C                   Z-ADD     1             LHBPAL
      *
     C                   WRITE     LLHEADR
      *
     C     SLHODE        ENDSR
      *
      *////////////////////////////////////////////////////////////
      * SR for LINJE                                        SRLIN /
      *////////////////////////////////////////////////////////////
     C     SRLIN         BEGSR
      *
     C     VARE1K        CHAIN     VAREL01                            34
      *
     C                   MOVEL     LHMN          LLMN
     C                   ADD       1             LLVN
     C                   MOVEL     VARENR        LLANR
     C     LLV01K        CHAIN     LLVARE01                           34
     C     *IN33         IFEQ      '1'
     C                   CLEAR                   LLVARER
     C                   MOVE      UGRP          LVGRUP
     C                   MOVE      UPNR          LVPNR
     C                   MOVEL     VARENR        LVANR
     C                   MOVEL     VARTE1        LVVB
     C                   MOVE      'STK'         LVEH0
     C                   MOVEL     'STYKK'       LVEH0T
     C                   MOVE      'ESK'         LVEH1
     C                   MOVEL     'ESKE'        LVEH1T
     C                   MOVE      'KRT'         LVEH2
     C                   MOVEL     'KARTONG'     LVEH2T
     C                   MOVE      'PAL'         LVEH3
     C                   MOVEL     'PALL'        LVEH3T
     C                   MOVE      'STK'         LVMEH
     C                   Z-ADD     0             LVMIN
     C                   MOVEL     LAGNR         LVLOK1
     C                   MOVEL     LPLASS        LVLOK2
     C                   Z-ADD     BVEKT         LVVKT0
     C     VOLUM         DIV(H)    1000          LVKUB0
     C                   WRITE     LLVARER
     C                   END
     C                   MOVEL     VARTE1        LLVB
     C                   Z-ADD     LEVERT        LLANT0
     C                   MOVE      *ZEROS        LLANT3
     C                   MOVE      *ZEROS        LLANT2
     C                   MOVE      *ZEROS        LLANT1
     C                   Z-ADD     LEVERT        LLANTL
     C                   MOVE      *ZEROS        LLWNT0
     C                   MOVE      *ZEROS        LLWNT3
     C                   MOVE      *ZEROS        LLWNT2
     C                   MOVE      *ZEROS        LLWNT1
     C                   MOVE      *ZEROS        LLWNTL
     C                   MOVE      *ZEROS        LLWVEK
     C                   MOVE      *ZEROS        LLWKUB
     C     LLANTL        MULT(H)   LVVKT0        LLVEK
     C     LLANTL        MULT(H)   LVKUB0        LLKUB
      *
     C                   MOVE      LLANT0        LLWNT0
     C                   MOVE      LLANT3        LLWNT3
     C                   MOVE      LLANT2        LLWNT2
     C                   MOVE      LLANT1        LLWNT1
     C                   MOVE      LLANTL        LLWNTL
     C                   MOVE      LLVEK         LLWVEK
     C                   MOVE      LLKUB         LLWKUB
     C                   MOVE      *BLANKS       LLLOK
      *
     C     *LIKE         DEFINE    LLWNT0        UWNT0
     C     *LIKE         DEFINE    LLWNTL        UWNTL
     C     *LIKE         DEFINE    LLWNT1        UWNT1
     C     *LIKE         DEFINE    LLWNT2        UWNT2
     C     *LIKE         DEFINE    LLWNT3        UWNT3
     C                   CALL      'LL022R'
     C                   PARM      LVGRUP        LHGRP
     C                   PARM                    LHPNR
     C                   PARM                    LLANR
     C                   PARM                    LLSER
     C                   PARM                    LLMN
     C                   PARM                    LLVN
     C                   PARM      LLWNT0        UWNT0
     C                   PARM                    UWNTL
     C                   PARM                    UWNT1
     C                   PARM                    UWNT2
     C                   PARM                    UWNT3
     C                   PARM                    UWVEK             7 2
     C                   PARM                    UWKUB             7 4
     C                   PARM                    LLLOK
     C                   PARM                    UNTLK             3 0
     C                   PARM                    UFLAGG            1 0
      *
     C     UFLAGG        IFEQ      0
     C                   MOVE      UWNTL         LLWNTL
     C                   MOVE      UWNT1         LLWNT1
     C                   MOVE      UWNT2         LLWNT2
     C                   MOVE      UWNT3         LLWNT3
     C                   MOVE      UNTLK         LLNTLK
     C     LD02K         CHAIN     LLAGJD02                           34
     C     LJ01K         CHAIN     LLAGJL01                           34
     C                   Z-ADD     VAVERD        LLPRI
     C                   ELSE
     C                   MOVE      *ZEROS        LLWNT0
     C                   MOVE      *ZEROS        LLWNT3
     C                   MOVE      *ZEROS        LLWNT2
     C                   MOVE      *ZEROS        LLWNT1
     C                   MOVE      *ZEROS        LLWNTL
     C                   MOVE      *ZEROS        LLWVEK
     C                   MOVE      *ZEROS        LLWKUB
     C                   MOVE      *ZEROS        LLPRI
     C                   END
      *
     C                   MOVE      'ESK'         LLAEH1
     C                   MOVE      'KRT'         LLAEH2
     C                   MOVE      'PAL'         LLAEH3
     C                   MOVE      *BLANKS       LLSER
      *
     C                   WRITE     LLLINER
      *
     C                   ADD       LLWNT0        XHANT0
     C                   ADD       LLWNTL        XHANTL
     C                   ADD       LLWVEK        XHWVEK
     C                   ADD       LLWKUB        XHWKUB
     C                   ADD       1             XHTLIN
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      * SR for init                                        SRINIT /
      *////////////////////////////////////////////////////////////
     C     SRINIT        BEGSR
     C     *ENTRY        PLIST
     C                   PARM                    UGRP              8
     C                   PARM                    UPNR              8
     C                   PARM                    UFIRM             2
     C                   PARM                    UAAR              4
     C                   PARM                    UPSDN             7
      *
     C     *LIKE         DEFINE    LHANT0        XHANT0
     C     *LIKE         DEFINE    LHANTL        XHANTL
     C     *LIKE         DEFINE    LHWVEK        XHWVEK
     C     *LIKE         DEFINE    LHWKUB        XHWKUB
     C     *LIKE         DEFINE    LHTLIN        XHTLIN
      * DEF AV KEYS
     C     ISTL2K        KLIST
     C                   KFLD                    UFIRM
     C                   KFLD                    XAAR4
     C                   KFLD                    XPSDN
     C     DORD9K        KLIST
     C                   KFLD                    UFIRM
     C                   KFLD                    OTBKOD
     C                   KFLD                    XAAR4
     C                   KFLD                    XPSDN
     C     KUND1K        KLIST
     C                   KFLD                    UFIRM
     C                   KFLD                    KUNDNR
     C     VARE1K        KLIST
     C                   KFLD                    UFIRM
     C                   KFLD                    VARENR
     C     LLV01K        KLIST
     C                   KFLD                    LHGRP
     C                   KFLD                    LHPNR
     C                   KFLD                    LLANR
     C     LD02K         KLIST
     C                   KFLD                    LHGRP
     C                   KFLD                    LHPNR
     C                   KFLD                    LLMN
     C                   KFLD                    LLVN
     C     LJ01K         KLIST
     C                   KFLD                    LHGRP
     C                   KFLD                    LHPNR
     C                   KFLD                    VDMN
      *
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVE      'O'           OTBKOD
     C                   MOVE      UAAR          XAAR4             4 0
     C                   MOVE      UFIRM         XFIRM
     C                   MOVE      UAAR          XAAR
     C                   MOVE      UPSDN         XPSDN
     C     1             CHAIN(N)  LLTELL                             33
     C                   IF        *IN33
     C                   Z-ADD     1001          LLREC1
     C                   WRITE     LLTELLR
     C                   END
     C     2             CHAIN(N)  LLTELL                             33
     C                   IF        *IN33
     C                   Z-ADD     1001          LLREC1
     C                   WRITE     LLTELLR
     C                   END
      *
     C                   ENDSR
      *
