********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
12054 * 01 CB  PTF12 Oppdater gammelt valutaregister
12XXX * 02 CB  PTF12 Set sluttdato på gammel kurs
12XXX * 03 CB  PTF12 Lægg till i kodfil
********************************************************************
     FTULLTAX   IF   F 1024        DISK    BLOCK(*YES)
     FSVTX23F   UF A E             DISK
     FSVTX23F2  UF A E             DISK
N03  FSVTVKT    IF   E           K DISK
     FSVTVK     O  A E             DISK
N03  FSVTX03F   O  A E             DISK

     D  A              C                   '<monetaryExchangePeriod '
     D  B              C                   '<monetaryExchangeRate '
     D  C              C                   '</monetaryExchangePeriod>'

     D W6              S              6

     D                 DS
     D  WDT10                  1     10
     D  WDT10Y                 1      4
     D  WDT10M                 6      7
     D  WDT10D                 9     10
     D                 DS
     D  WDT08                  1      8
     D  WDT08Y                 1      4
     D  WDT08M                 5      6
     D  WDT08D                 7      8

     ITULLTAX   AA  01
     I                                  1 1024  RDATA

     C                   EXSR      SLETT

     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6

     C                   READ      TULLTAX                                51
     C     *IN51         DOWEQ     *OFF

     C     A             SCAN      RDATA                                  41
     C     B             SCAN      RDATA                                  42
     C     C             SCAN      RDATA                                  43
     C
     C                   SELECT
     C                   WHEN      *IN41
     C                   CLEAR                   SVTX23R
     C                   EXSR      XML2
     C                   WHEN      *IN42
     C                   EXSR      XML3
     C                   WHEN      *IN43
     C
     C                   EVAL      WDT10=SVTX23_03
     C                   EVAL      WDT08Y=WDT10Y
     C                   EVAL      WDT08M=WDT10M
     C                   EVAL      WDT08D=WDT10D

     C     WDT08         IFGE      WDT
     C     SVTX23_01     ANDEQ     'SEK'
     C                   WRITE     SVTX23R
     C                   ENDIF
     C                   ENDSL

     C                   READ      TULLTAX                                51
     C                   ENDDO
     C                   EVAL      *INLR=*ON
      *______________________________________________________________
      * Les <monetaryExchangePeriod                              XML2
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     XML2          BEGSR

     C     'yUnitCode="' SCAN      RDATA         X                        20
     C                   IF        *IN20
     C                   ADD       11            X
     C     '"'           SCAN      RDATA :X      Y                        21
     C                   IF        *IN20
     C     Y             SUB       X             Z
     C                   EVAL      SVTX23_01=%SUBST(RDATA :X:Z)
     C                   ENDIF
     C                   ENDIF

     C     'SID="'       SCAN      RDATA         X                        20
     C                   IF        *IN20
     C                   ADD       5             X
     C     '"'           SCAN      RDATA :X      Y                        21
     C                   IF        *IN20
     C     Y             SUB       X             Z
     C                   EVAL      SVTX23_02=%SUBST(RDATA :X:Z)
     C                   ENDIF
     C                   ENDIF

     C     'dateEnd="'   SCAN      RDATA         X                 3 0    20
     C                   IF        *IN20
     C                   ADD       9             X
     C     '"'           SCAN      RDATA :X      Y                 3 0    21
     C                   IF        *IN20
     C     Y             SUB       X             Z                 3 0
     C                   EVAL      SVTX23_03=%SUBST(RDATA :X:Z)
     C                   ENDIF
     C                   ENDIF

     C     'dateStart="' SCAN      RDATA         X                        20
     C                   IF        *IN20
     C                   ADD       11            X
     C     '"'           SCAN      RDATA :X      Y                        21
     C                   IF        *IN20
     C     Y             SUB       X             Z
     C                   EVAL      SVTX23_04=%SUBST(RDATA :X:Z)
     C                   ENDIF
     C                   ENDIF

     C     'national="'  SCAN      RDATA         X                        20
     C                   IF        *IN20
     C                   ADD       10            X
     C     '"'           SCAN      RDATA :X      Y                        21
     C                   IF        *IN20
     C     Y             SUB       X             Z
     C                   EVAL      SVTX23_05=%SUBST(RDATA :X:Z)
     C                   ENDIF
     C                   ENDIF

     C     'hangeType="' SCAN      RDATA         X                        20
     C                   IF        *IN20
     C                   ADD       11            X
     C     '"'           SCAN      RDATA :X      Y                        21
     C                   IF        *IN20
     C     Y             SUB       X             Z
     C                   EVAL      SVTX23_06=%SUBST(RDATA :X:Z)
     C                   ENDIF
     C                   ENDIF

     C                   IF        SVTX23_03=*BLANKS
     C                   EVAL      SVTX23_03='9999-12-31'
     C                   ENDIF

     C                   EVAL      WDT10=SVTX23_03
     C                   EVAL      WDT08Y=WDT10Y
     C                   EVAL      WDT08M=WDT10M
     C                   EVAL      WDT08D=WDT10D

     C     WDT08         IFLT      WDT
     C                   EVAL      SVTX23_01=*BLANKS
     C                   ENDIF
     C     SVTX23_01     IFNE      'SEK'
     C                   EVAL      SVTX23_01=*BLANKS
     C                   ENDIF

     C                   ENDSR
      *______________________________________________________________
      * Les <monetaryExchangeRate                                XML3
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     XML3          BEGSR

     C     SVTX23_01     IFNE      *BLANKS

     C                   CLEAR                   SVTX23R2

     C     'yUnitCode="' SCAN      RDATA         X                 3 0    20
     C                   IF        *IN20
     C                   ADD       11            X
     C     '"'           SCAN      RDATA :X      Y                 3 0    21
     C                   IF        *IN20
     C     Y             SUB       X             Z                 3 0
     C                   EVAL      SVTX232_02=%SUBST(RDATA :X:Z)
     C                   ENDIF
     C                   ENDIF

     C     'rsionRate="' SCAN      RDATA         X                        20
     C                   IF        *IN20
     C                   ADD       11            X
     C     '"'           SCAN      RDATA :X      Y                        21
     C                   IF        *IN20
     C     Y             SUB       X             Z
     C                   EVAL      W6=%SUBST(RDATA :X:Z)
      /free
         monitor;
           svtx232_03 = %dec(w6:5:4);
           on-error 105;
         endmon;
      /end-free
     C                   ENDIF
     C                   ENDIF

     C     'national="'  SCAN      RDATA         X                        20
     C                   IF        *IN20
     C                   ADD       10            X
     C     '"'           SCAN      RDATA :X      Y                        21
     C                   IF        *IN20
     C     Y             SUB       X             Z
     C                   EVAL      SVTX232_04=%SUBST(RDATA :X:Z)
     C                   ENDIF
     C                   ENDIF

     C                   EVAL      SVTX232_01=SVTX23_01
     C                   WRITE     SVTX23R2
N01  C                   EXSR      ADDOLD

     C                   ENDIF

     C                   ENDSR
      *______________________________________________________________
      * Slett alle poster i filen                               SLETT
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     SLETT         BEGSR

     C                   READ      SVTX23F                                51
     C                   DOW       *IN51 = *OFF
     C                   DELETE    SVTX23R
     C                   READ      SVTX23F                                51
     C                   ENDDO

     C                   EVAL      *IN51=*OFF
     C                   READ      SVTX23F2                               51
     C                   DOW       *IN51 = *OFF
     C                   DELETE    SVTX23R2
     C                   READ      SVTX23F2                               51
     C                   ENDDO

N02   * Endre sluttdato på gammel kurs
N02  C                   CALL      'SVT123R2'

     C                   ENDSR
N01   *______________________________________________________________
N01   * Konvertera till SVTVK(gammal kursfil)                  ADDOLD
N01   *""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
N01  C     ADDOLD        BEGSR
N01  C                   CLEAR                   SVTVKR
N01  C                   EVAL      SVVK_KD=SVTX232_02
N01
N01  C                   EVAL      WDT10=SVTX23_04
N01  C                   EVAL      WDT08Y=WDT10Y
N01  C                   EVAL      WDT08M=WDT10M
N01  C                   EVAL      WDT08D=WDT10D
N01
N01  C                   MOVEL     WDT08         SVVK_DTS
N01
N01  C                   EVAL      WDT10=SVTX23_03
N01  C                   EVAL      WDT08Y=WDT10Y
N01  C                   EVAL      WDT08M=WDT10M
N01  C                   EVAL      WDT08D=WDT10D
N01
N01  C                   MOVEL     WDT08         SVVK_DTE
N01  C                   EVAL      SVVK_OMR=1
     C                   IF        SVVK_KD='JPY'
N01  C                   EVAL      SVVK_OMR=100
     C                   ENDIF
N01  C                   EVAL      SVVK_KRS=1/SVTX232_03
N01  C                   WRITE     SVTVKR
N03   *
N03  C                   CLEAR                   SVTX03R
N03  C                   EVAL      SVTX03_01='A'
N03  C                   EVAL      SVTX03_02='MDX'
N03  C                   MOVEL     SVVK_KD       SVTX03_03
N03  C                   MOVEL     SVVK_KD       WVAL              3
N03  C     WVAL          CHAIN     SVTVKT
N03  C                   IF        %FOUND
N03  C                   MOVEL     SVVKT_TX      SVTX03_04
N03  C                   ENDIF
N03  C                   MOVEL     SVVK_DTE      SVTX03_05
N03  C                   MOVEL     SVVK_DTS      SVTX03_06
N03  C                   WRITE     SVTX03R
N03  C                   ENDSR
