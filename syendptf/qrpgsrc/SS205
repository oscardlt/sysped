********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/SS205 MODULE(*LIBL/SS205
CRTPGM*        *LIBL/CHECKUSR *LIBL/INCGI) ACTGRP(CGI) AUT(*USE)
      *
********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     UF   E           K DISK    USROPN
     FCUNDL01   IF   E           K DISK    USROPN
     FGSSDGF    IF   E           K DISK    USROPN
      *********************************************************************
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      /copy syspeds/qrpgsrcpy,variables3
     D IfsMultIndicators...
     d                 ds
     D  NoErrors                       n
     D  NameTooLong                    n
     D  NotAccessible                  n
     D  NoFilesUsable                  n
     D  DupSections                    n
     D  FileIsEmpty                    n
      *
      * Client input variables
     D user            s             10
     D UsrSpcName      s             10
     D WSONO           S              7
     D BSONO           S              7  0
     D Frames          s              1
     D WSSST           S              1
     D PGM             C                   CONST('SYSPED/CHECKUSR')
      *
     D OddEven         s             10
      * For xlate mellom Upper Case og Lower Case
     D UC              C                   CONST('ABCDEFGHIJKLMNOPQRST-
     D                                     UVWXYZÆØÅ')
     D LC              C                   CONST('abcdefghijklmnopqrst-
     D                                     uvwxyzæøå')
      *
      /copy syspeds/qrpgsrcpy,prolog3
      *
      * Get program start time for calculating execution time
     C                   time                    timedata1
      * Parse input / cvt to numeric
     C                   exsr      Parse
      * File open / keys
     C                   EXSR      INIT
      *
     C                   CALLB     PGM
     C                   PARM                    BIBRID
     C                   PARM                    UsrSpcName
     C                   PARM                    UINN              1
     C                   IF        UINN = 'N'
     C                   GOTO      SLUTT
     C                   END
      * dato/tid siste vellykkede operasjon.
     C     USER          CHAIN     BRIDF                              50
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVE      WDT           BIDTF
     C                   MOVE      WTM           BITIDF
     C                   UPDATE    BRIDFR
      *
      * Read output skeleton html member into memory
     C                   exsr      LoadHtml
      * Set section variables
     C                   exsr      Settop
     C                   callp     wrtsection('top')
      *
      * HOVEDRUTINE
     C     BSONO         SETLL     GSSDGF
     C     BSONO         READE     GSSDGF                                 35
     C                   DOW       *IN35 = *OFF
     C                   EXSR      SETROW
     C                   callp     wrtsection('row')
     C     BSONO         READE     GSSDGF                                 35
     C                   ENDDO
      *
     C                   callp     wrtsection('bot')
      *
      * Quit
     c     Slutt         tag
     C                   exsr      Exit
      *
     C                   eval      *inlr = *on
     C                   return
      *
      *
      *=====================================================================
      * Parse input / cvt to numeric
      *=====================================================================
     C     Parse         begsr
      * Parse variables from QUERY_STRING environment variable
     C                   eval      user     = zhbgetvar('user')
     C                   EVAL      UsrSpcName  = zhbgetvar('UsrSpcName')
     C                   eval      WSONO   = zhbgetvar('WSONO')
     C                   eval      BSONO  = c2n2(WSONO)
      *
     C                   endsr
      *
      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
     C                   callp     wrtsection('endhtml')
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   CLOSE     BRIDF
     C                   close     CUNDL01
     C                   CLOSE     GSSDGF
      *
     C                   endsr
      *
      *=====================================================================
      * Read output skeleton html member into memory
      *=====================================================================
     C     LoadHtml      begsr
cccccC                   eval      IfsMultIndicators = gethtmlifsmult(
cccccC                             '/syspeddev/html/Header/eSpedTop.html -
cccccC                             /syspeddev/html/SS205.html -
cccccC                             /syspeddev/html/Footer/eSpedBot.html':
cccccC                             '/CGITAG/')
     C                   endsr
      *
      *=====================================================================
      *  Set variable data for section /$top
      *=====================================================================
     C     SetTop        begsr
      *
     C                   callp     updHTMLvar('ROWHEAD':RowHead)
     C                   callp     updHTMLvar('LogoImg':LogoImg)
     C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updHTMLvar('USER':USER)
     C     CUNDL01K      CHAIN     CUNDL01                            33
     C                   callp     updhtmlvar('KUNDE':
     C                             %trim(%editc(KUNDNR:'Z')))
     C                   callp     updhtmlvar('KNAVN':KNAVN)
     C                   callp     updHtmlVar('UsrSpcName':UsrSpcName)
     C                   callp     updHtmlVar('WSONO':
     C                             %trim(%editc(BSONO:'Z')))
      *
     C                   endsr
      *
      *=====================================================================
      *  Set variable data for section /$row
      *=====================================================================
     C     Setrow        begsr
     c     OddEven       IFEQ      ODD
     c                   eval      OddEven = EVEN
     c                   else
     c                   eval      OddEven = ODD
     c                   end
     C                   callp     updHTMLvar('ODDEVEN':OddEven)
      *
     C                   callp     updhtmlvar('DGLOP':
     C                             %trim(%editc(DGLOP:'Z')))
     C                   callp     updhtmlvar('DGUNNR':
     C                             %trim(%editc(DGUNNR:'Z')))
     C                   callp     updhtmlvar('DGPSN':DGPSN)
     C                   callp     updhtmlvar('DGPSN2':DGPSN2)
     C                   callp     updhtmlvar('DGCOG':DGCOG)
     C                   callp     updhtmlvar('DGPG':DGPG)
     C                   callp     updhtmlvar('DGTR':DGTR)
     C                   callp     updhtmlvar('DGNOP':
     C                             %trim(%editC(DGNOP:'Z')))
     C                   callp     updhtmlvar('DGKOP':DGKOP)
     C                   callp     updhtmlvar('DGGWK':
     C                             %trim(%editC(DGGWK:'L')))
     C                   callp     updhtmlvar('DGNW':
     C                             %trim(%editC(DGNW:'L')))
     C                   callp     updhtmlvar('DGNWT':DGNWT)
     C                   callp     updhtmlvar('DGADD':DGADD)
     C                   callp     updhtmlvar('DGPFRT':
     C                             %trim(%editc(DGPFRT:'Z')))
     C                   callp     updhtmlvar('DGLC':DGLC)
     C                   callp     updhtmlvar('DGCLC':DGCLC)
      *
     C                   endsr
      *
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
     C                   OPEN      BRIDF
     C     USER          CHAIN(N)  BRIDF                              50
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
      *
      * hent Parametre som går igjen i mange prog:
      *      Odd/Even/Rowhead-farger,Logobilde,Språk,Intern/Ekstern bruker,  mm
     c                   call      'ESGETPAR'
     c                   parm                    BIBRID
     c                   parm                    BIFIRM
     c                   parm                    BIKUNR
     c                   parm                    BIKNG
     c                   parm                    RowHead          10
     c                   parm                    Odd              10
     c                   parm                    Even             10
     c                   parm                    LogoImg          50
     c                   parm                    XSPRÅK            2
     c                   parm                    XINTERN           1
     c                   parm                    ParmDS1        1024
     c                   parm                    ParmDS2        1024
     c                   parm                    ParmDS3        1024
      * HENTER PARAMETER
     C                   CALL      'ESGETOS'
     C                   parm                    BIBRID
     C                   parm                    BIFIRM
     C                   parm                    BIKUNR
     C                   parm                    BIKNG
     C                   parm                    ParmOS1        1024
      *
     C                   OPEN      GSSDGF
     C                   OPEN      CUNDL01
      *
     C     CUNDL01K      klist
     C                   kfld                    BIFIRM
     C                   kfld                    BIKUNR
      *
     C                   endsr
      *
