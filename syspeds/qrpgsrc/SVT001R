     FEDIRE1K   IF   E           K DISK
     FSVTARW    O  A E             DISK
      *____________________
      * DS for input string
      *""""""""""""""""""""
     D                 DS
     D  A                      1   1024
     D                                     DIM(1024)                            Input string
     D  ADS                    1   1024
      *_____________________
      * DS for output string
      *"""""""""""""""""""""
     D                 DS
     D  B                      1    265
     D                                     DIM(265)                             Output string
     D  SVTA_DATA              1    265
      *//////////////
      *  Hodelogikk /
      *//////////////
     C                   EXSR      SRA
     C                   EXSR      SRB
     C                   MOVE      '1'           *INLR
      *////////////////////////////////////////////////////////////
      *  Initiering                                           SRA /
      *////////////////////////////////////////////////////////////
     C     SRA           BEGSR
      *________________
      * Input parameter
      *""""""""""""""""
     C     *ENTRY        PLIST
     C                   PARM                    WMBR             10
      *__________________
      * Define Workfields
      *""""""""""""""""""
     C                   Z-ADD     0             AX                3 0          Index for A
     C                   Z-ADD     1             BX                3 0          Index for B
     C                   Z-ADD     0             AXV               4 0          Leftindx A
     C                   Z-ADD     0             AXH               4 0          Rightindx A
     C                   MOVE      *BLANKS       WCHAR             1            Test character
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  SRB                                                  SRB /
      *////////////////////////////////////////////////////////////
     C     SRB           BEGSR
      *
     C     WMBR          SETLL     EDIRE1K
     C     WMBR          READE     EDIRE1K                                51
     C     *IN51         DOWEQ     '0'
      *
     C                   MOVE      *BLANKS       ADS
     C                   MOVEL     RDATA         ADS
     C                   Z-ADD     1             AXV
     C                   Z-ADD     1024          AXH
     C                   EXSR      SRÅ
      *
     C     WMBR          READE     EDIRE1K                                51
     C  N51              END
      *
     C     BX            IFGT      1
     C     SVTA_DATA     ANDNE     *BLANKS
     C                   WRITE     SVTARWR
     C                   ENDIF
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR for output string, read input from left              SRÅ
      *////////////////////////////////////////////////////////////
     C     SRÅ           BEGSR
      *________________________________________________
      * Read input string and place it in output string
      *""""""""""""""""""""""""""""""""""""""""""""""""
     C     AXV           DO        AXH           AXV
     C                   MOVE      A(AXV)        WCHAR
      * Move to output string
     C     WCHAR         IFNE      X'25'
     C                   MOVE      WCHAR         B(BX)
     C                   ADD       1             BX
     C                   ENDIF
      * If character is HEX25 Then add a record
     C     WCHAR         IFEQ      X'25'
     C     SVTA_DATA     IFNE      *BLANKS
     C                   WRITE     SVTARWR
     C                   ENDIF
     C                   Z-ADD     1             BX
     C                   MOVE      *BLANKS       SVTA_DATA
     C                   ELSE
      * If arrayindex for output array is over 265 Then add a record
     C     BX            IFGT      265
     C     SVTA_DATA     IFNE      *BLANKS
     C                   WRITE     SVTARWR
     C                   ENDIF
     C                   Z-ADD     1             BX
     C                   MOVE      *BLANKS       SVTA_DATA
     C                   ENDIF
     C                   ENDIF
      *
     C                   ENDDO
      *
     C                   ENDSR
