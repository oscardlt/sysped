*****A**************************************************************
     A* RETTINGER I DETTE PROGRAM:
PTFnrA*Sek Sig Vers Beskrivelse...........................
"""""A*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
P8068A* 01 TH  PTF8 NY HELPFUNKSJON  KUN DDS
*****A**************************************************************
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPPNLGRP(HLPSCR SYPO02PNL)
     A                                      HLPTITLE('VEDLIKEHOLD AV SONETABELL-
     A                                      ER POSTAL')
     A                                      HELP
     A                                      ALTHELP
     A          R NOERASE
     A                                      ASSUME
     A                                  1  2' '
     A          R BILDE01
     A                                      WINDOW(7 10 6 62 *NOMSGLIN)
     A                                      OVERLAY
     A                                      CF03(03 'Avslutt program')
     A                                      CF04(04 'SØK')
     A                                      CF12(03)
     A                                      CSRLOC(LINNBR     POSNBR)
S01  A*         H                           HLPDOC('SYPO02020' 'SYPO02' 'SY400L-
S01  A*                                     O/MAINT')
N01  A          H                           HLPPNLGRP(SYPO02020 SYPO02PNL)
     A                                      HLPARA(01 001 06 060)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/SYPO02'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(040 SHU0062 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  2  2MSGCON(025 SHU0030 *LIBL/SYMSGF)
     A            SOPROD    R        B  2 28REFFLD(SONETR/SOPROD *LIBL/SONET)
     A                                      DSPATR(UL)
     A  71                                  DSPATR(RI)
     A                                  2 36MSGCON(025 SHU0052 *LIBL/SYMSGF)
     A                                  3  2MSGCON(025 SHU0031 *LIBL/SYMSGF)
     A            SOAVDE         4  0B  3 28
     A                                      EDTCDE(4)
     A                                      DSPATR(UL)
     A  72                                  DSPATR(RI)
     A                                  3 36MSGCON(025 SHU0053 *LIBL/SYMSGF)
     A                                  4  2MSGCON(025 SHU0063 *LIBL/SYMSGF)
     A            SOZONO    R        B  4 28REFFLD(SONETR/SOZONO *LIBL/SONET)
     A                                      DSPATR(UL)
     A  73                                  DSPATR(RI)
     A                                  5  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5 22MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5 42MSGCON(018 DIV0032 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        58A  O  6  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                              6 20'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R BILDE1B
     A                                      CF03(03 'Avslutt program')
     A                                      CF04(04 'SØK')
     A                                      CF12(12 'FORIGE BILDE')
     A                                      CF10(10 'Slett sonetabellen')
     A                                      CF23(23 'KVITTER FOR SLETT')
     A                                      CSRLOC(LINNBR     POSNBR)
     A          H                           HLPPNLGRP(SYPO02030 SYPO02PNL)
     A                                      HLPARA(01 001 05 080)
     A          H                           HLPPNLGRP(SYPO02040 SYPO02PNL)
     A                                      HLPARA(06 001 06 080)
     A          H                           HLPPNLGRP(SYPO02050 SYPO02PNL)
     A                                      HLPARA(07 001 07 080)
     A          H                           HLPPNLGRP(SYPO02060 SYPO02PNL)
     A                                      HLPARA(08 001 08 080)
     A          H                           HLPPNLGRP(SYPO02070 SYPO02PNL)
     A                                      HLPARA(09 001 09 080)
     A          H                           HLPPNLGRP(SYPO02080 SYPO02PNL)
     A                                      HLPARA(10 001 10 080)
     A          H                           HLPPNLGRP(SYPO02090 SYPO02PNL)
     A                                      HLPARA(11 001 11 080)
     A          H                           HLPPNLGRP(SYPO02100 SYPO02PNL)
     A                                      HLPARA(12 001 12 080)
     A          H                           HLPPNLGRP(SYPO02110 SYPO02PNL)
     A                                      HLPARA(13 001 13 080)
     A          H                           HLPPNLGRP(SYPO02120 SYPO02PNL)
     A                                      HLPARA(14 001 14 080)
     A          H                           HLPPNLGRP(SYPO02130 SYPO02PNL)
     A                                      HLPARA(15 001 15 080)
     A          H                           HLPPNLGRP(SYPO02030 SYPO02PNL)
     A                                      HLPARA(16 001 24 080)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/SYPO02'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(050 SHU0062 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  2 70TIME
     A                                      COLOR(BLU)
     A                                  3  2MSGCON(025 SHU0030 *LIBL/SYMSGF)
     A            SSPROD    R        O  3 28REFFLD(SONESR/SSPROD *LIBL/SONES)
     A                                  3 36MSGCON(025 SHU0052 *LIBL/SYMSGF)
     A                                  4  2MSGCON(025 SHU0031 *LIBL/SYMSGF)
     A            SSAVDE    R        O  4 28REFFLD(SONESR/SSAVDE *LIBL/SONES)
     A                                  4 36MSGCON(025 SHU0053 *LIBL/SYMSGF)
     A                                  5  2MSGCON(025 SHU0063 *LIBL/SYMSGF)
     A            SSZONO    R        O  5 28REFFLD(SONESR/SSZONO *LIBL/SONES)
     A                                  6  2MSGCON(025 SHU0055 *LIBL/SYMSGF)
     A            WSOMRM    R        B  6 28REFFLD(SONESR/SSOMRM *LIBL/SONES)
     A                                      EDTCDE(4)
     A  70                                  DSPATR(RI)
     A                                      DSPATR(UL)
     A                                  7  2MSGCON(025 SHU0056 *LIBL/SYMSGF)
     A            WSOMRL    R        B  7 28REFFLD(SONESR/SSOMRL *LIBL/SONES)
     A                                      DSPATR(UL)
     A  71                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                  8  2MSGCON(025 SHU0057 *LIBL/SYMSGF)
     A            WSMIMA    R        B  8 28REFFLD(SONESR/SSMIMA *LIBL/SONES)
     A                                      DSPATR(UL)
     A            WSGREN    R        B  8 30REFFLD(SONESR/SSGREN *LIBL/SONES)
     A                                      EDTCDE(4)
     A                                  9  2MSGCON(025 SHU0108 *LIBL/SYMSGF)
     A            WSVALK    R        B  9 28REFFLD(SONESR/SSVALK *LIBL/SONES)
     A  73                                  DSPATR(RI)
     A                                      DSPATR(UL)
     A                                 10  2MSGCON(025 SHU0109 *LIBL/SYMSGF)
     A            WSTYPE    R        B 10 28REFFLD(SONESR/SSTYPE *LIBL/SONES)
     A                                      DSPATR(UL)
     A  72                                  DSPATR(RI)
     A                                 11  2MSGCON(025 SHU0110 *LIBL/SYMSGF)
     A            WSMINI    R        B 11 28REFFLD(SONESR/SSMINI *LIBL/SONES)
     A                                      EDTCDE(4)
     A                                      DSPATR(UL)
     A                                 11 39'/'
     A            WSMAXI    R        B 11 41REFFLD(SONESR/SSMAXI *LIBL/SONES)
     A                                      EDTCDE(4)
     A                                      DSPATR(UL)
     A                                 11 52'/'
     A            WSGRPR    R        B 11 54REFFLD(SONESR/SSGRPR *LIBL/SONES)
     A                                      EDTCDE(4)
     A                                      DSPATR(UL)
     A                                 12  2MSGCON(025 SHU0111 *LIBL/SYMSGF)
     A            WSGREF    R        B 12 28REFFLD(SONESR/SSGREF *LIBL/SONES)
     A                                      DSPATR(UL)
     A  74                                  DSPATR(RI)
     A                                 13  2MSGCON(025 SHU0112 *LIBL/SYMSGF)
     A            WSGRES    R        B 13 28REFFLD(SONESR/SSGRES *LIBL/SONES)
     A                                      DSPATR(UL)
     A  75                                  DSPATR(RI)
     A                                 14  2MSGCON(025 SHU0113 *LIBL/SYMSGF)
     A            WSGREP    R        B 14 28REFFLD(SONESR/SSGREP *LIBL/SONES)
     A                                      EDTCDE(4)
     A                                      DSPATR(UL)
     A                                 15  2MSGCON(024 SHU0423 *LIBL/SYMSGF)
     A            WSTEXT        50A  B 15 28CHECK(LC)
     A            WSTEX2        50A  B 16 28CHECK(LC)
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(018 DIV0032 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 42MSGCON(018 JOV0271 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 62MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                             24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  5  3VALUES(' ' '2' '4')
     A  98                                  DSPATR(RI)
     A  98                                  DSPATR(PC)
     A            SOPRIC    R        O  5  6REFFLD(SONETR/SOPRIC *LIBL/SONET)
     A                                      EDTCDE(4)
     A            SSVALK    R        O  5 23REFFLD(SONESR/SSVALK *LIBL/SONES)
     A            SOVEKT    R        O  5 31REFFLD(SONETR/SOVEKT *LIBL/SONET)
     A  27                                  DSPATR(ND)
     A                                      EDTCDE(4)
     A            SOPRPR    R        O  5 45REFFLD(SONETR/SOPRPR *LIBL/SONET)
     A  27                                  DSPATR(ND)
     A                                      EDTCDE(4)
     A            SOADJU    R        O  5 54REFFLD(SONETR/SOADJU *LIBL/SONET)
     A  27                                  DSPATR(ND)
     A                                      EDTCDE(4)
     A            SOMMBE    R        O  5 64REFFLD(SONETR/SOMMBE *LIBL/SONET)
     A  27                                  DSPATR(ND)
     A                                      EDTCDE(4)
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0017)
     A                                      SFLPAG(0016)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
S01  A*         H                           HLPDOC('SYPO02140' 'SYPO02' 'SY400L-
S01  A*                                     O/MAINT')
N01  A          H                           HLPPNLGRP(SYPO02140 SYPO02PNL)
     A                                      HLPARA(01 001 24 080)
     A            ZRECNO         4S 0O  3 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYSPED/SYPO02'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(050 SHU0062 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  2 70TIME
     A                                      COLOR(BLU)
     A                                  2  2MSGCON(009 SHU0064 *LIBL/SYMSGF)
     A            SOZONO    R        O  2 12REFFLD(SONETR/SOZONO *LIBL/SONET)
     A                                      COLOR(WHT)
     A                                  2 22MSGCON(012 SHU0035 *LIBL/SYMSGF)
     A            SOPROD    R        O  2 36REFFLD(SONETR/SOPROD *LIBL/SONET)
     A                                      COLOR(WHT)
     A                                  2 44MSGCON(012 DIV0050 *LIBL/SYMSGF)
     A            SOAVDE         4  0O  2 58
     A                                      COLOR(WHT)
     A                                  3  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N27                              3 34MSGCON(030 DIV0017 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  27                              3 34MSGCON(015 YBC0367 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N27                              4  2MSGCON(078 SHU0065 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A  27                              4  2MSGCON(078 YBC0361 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
      *
     A          R BUNN
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF03(03 'Avslutt program')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                      CF12(12 'FORRIGE')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A                                  2  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3  2MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N27                              3 21MSGCON(018 DIV0022 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N27                              3 40MSGCON(018 DIV0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 59MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  98        MSG206        78A  O  4  2MSGID(DIV 0012 SYMSGF)
     A                                      COLOR(WHT)
     A N98                              4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
      *
     A          R VARELI
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
S01  A*         H                           HLPDOC('SYPO02150' 'SYPO02' 'SY400L-
S01  A*                                     O/MAINT')
N01  A          H                           HLPPNLGRP(SYPO02150 SYPO02PNL)
     A                                      HLPARA(02 001 02 024)
S01  A*         H                           HLPDOC('SYPO02160' 'SYPO02' 'SY400L-
S01  A*                                     O/MAINT')
N01  A          H                           HLPPNLGRP(SYPO02160 SYPO02PNL)
     A                                      HLPARA(02 025 02 032)
S01  A*         H                           HLPDOC('SYPO02170' 'SYPO02' 'SY400L-
S01  A*                                     O/MAINT')
N01  A          H                           HLPPNLGRP(SYPO02170 SYPO02PNL)
     A                                      HLPARA(02 033 02 046)
S01  A*         H                           HLPDOC('SYPO02180' 'SYPO02' 'SY400L-
S01  A*                                     O/MAINT')
N01  A          H                           HLPPNLGRP(SYPO02180 SYPO02PNL)
     A                                      HLPARA(02 047 02 056)
S01  A*         H                           HLPDOC('SYPO02190' 'SYPO02' 'SY400L-
S01  A*                                     O/MAINT')
N01  A          H                           HLPPNLGRP(SYPO02190 SYPO02PNL)
     A                                      HLPARA(02 057 02 064)
S01  A*         H                           HLPDOC('SYPO02200' 'SYPO02' 'SY400L-
S01  A*                                     O/MAINT')
N01  A          H                           HLPPNLGRP(SYPO02200 SYPO02PNL)
     A                                      HLPARA(02 065 02 080)
     A N27                              1  2MSGCON(079 SHU0065 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A  27                              1  2MSGCON(079 YBC0361 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            WSPRIC    R        B  2  6REFFLD(SONETR/SOPRIC *LIBL/SONET)
     A                                      EDTCDE(4)
     A                                      DSPATR(PC)
     A N27        SSVALK    R        B  2 26REFFLD(SONESR/SSVALK *LIBL/SONES)
     A                                      DSPATR(PR)
     A N27        WSVEKT    R        B  2 34REFFLD(SONETR/SOVEKT *LIBL/SONET)
     A  89                                  DSPATR(RI)
     A  89                                  DSPATR(PC)
     A  40                                  DSPATR(PR)
     A  40                                  COLOR(WHT)
     A N27        WSPRPR    R        B  2 48REFFLD(SONETR/SOPRPR *LIBL/SONET)
     A N27        WSADJU    R        B  2 58REFFLD(SONETR/SOADJU *LIBL/SONET)
     A                                  3  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  4  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                              4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R VAREL2
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                  2  2MSGCON(012 DIV0026 *LIBL/SYMSGF)
     A            W2VEKT    R        I  2 15REFFLD(PRODTR/PTVEKT *LIBL/PRODT)
     A                                      COLOR(WHT)
     A                                      DSPATR(UL)
     A                                  3  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
      *
     A          R VAREL3
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A N27                              1  2MSGCON(079 SHU0065 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A  27                              1  2MSGCON(079 YBC0361 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            SOPRIC    R        O  2  6REFFLD(SONETR/SOPRIC *LIBL/SONET)
     A                                      EDTCDE(4)
     A N27        SSVALK    R        O  2 23REFFLD(SONESR/SSVALK *LIBL/SONES)
     A N27        SOVEKT    R        O  2 31REFFLD(SONETR/SOVEKT *LIBL/SONET)
     A                                      EDTCDE(4)
     A N27        SOPRPR    R        O  2 45REFFLD(SONETR/SOPRPR *LIBL/SONET)
     A                                      EDTCDE(4)
     A N27        SOADJU    R        O  2 54REFFLD(SONETR/SOADJU *LIBL/SONET)
     A                                      EDTCDE(4)
     A N27        SOMMBE    R        O  2 64REFFLD(SONETR/SOMMBE *LIBL/SONET)
     A                                      EDTCDE(4)
     A                                  3  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
      *
     A          R NYVARE
     A                                      CF03(03 'Avslutt program')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                      CF12(12 'FORRIGE')
     A                                  1  2'SYSPED/SYPO02'
     A                                      COLOR(BLU)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  2 70TIME
     A                                      COLOR(BLU)
     A                                  2  2MSGCON(009 SHU0064 *LIBL/SYMSGF)
     A            SOZONO    R        O  2 12REFFLD(SONETR/SOZONO *LIBL/SONET)
     A                                      COLOR(WHT)
     A                                  2 22MSGCON(012 SHU0035 *LIBL/SYMSGF)
     A            SOPROD    R        O  2 36REFFLD(SONETR/SOPROD *LIBL/SONET)
     A                                      COLOR(WHT)
     A                                  2 44MSGCON(012 DIV0050 *LIBL/SYMSGF)
     A            SOAVDE         4  0O  2 58
     A                                      COLOR(WHT)
     A                                  3  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N27                              3 34MSGCON(030 DIV0017 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  27                              3 34MSGCON(015 YBC0367 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N27                              4  2MSGCON(078 SHU0065 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A  27                              4  2MSGCON(078 YBC0361 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                 22  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23  2MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N27                             23 21MSGCON(018 DIV0022 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N27                             23 40MSGCON(018 DIV0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 59MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  98                             24  2MSGCON(78 DIV0012 SYMSGF)
     A                                      COLOR(WHT)
     A N98                             24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A          R BEKREFT
     A                                      WINDOW(11 10 6 40 *NOMSGLIN)
     A                                      OVERLAY
     A                                      CF23(23 'BEKREFTE')
     A                                      CF12(12)
     A                                  1  2'SYSPED/SYPO02'
     A                                      COLOR(BLU)
     A                                  2  2MSGCON(036 YBC0362 *LIBL/SYMSGF)
     A                                      COLOR(RED)
     A                                  3  2MSGCON(036 YBC0363 *LIBL/SYMSGF)
     A                                      COLOR(RED)
     A                                  4  2MSGCON(036 YBC0364 *LIBL/SYMSGF)
     A                                      COLOR(RED)
     A                                  5  2MSGCON(036 YBC0365 *LIBL/SYMSGF)
     A                                      COLOR(RED)
     A                                  6  2MSGCON(036 YBC0366 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
