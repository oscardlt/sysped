     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPDOC('SYTR100000' 'SYTR10' 'SY400-
     A                                      LO/TRANS')
     A                                      ALTHELP
     A                                      HELP
      * "NOERASE" MÅ HA FOR Å IKKE TØMME BILDE SOM MAN KOMME FRA
     A          R NOERASE
     A                                      ASSUME
     A                                  1  2' '
      * MAN KUN DEFINNERE VINDU PÅ REKORDNIVÅ
     A          R WINDOWSZ
     A                                      WINDOW(*DFT 19 70 *NOMSGLIN)
     A                                      OVERLAY
     A          R BILDE
     A                                      WINDOW(*DFT 19 70 *NOMSGLIN)
     A                                      OVERLAY
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF03(12)
     A                                      CF12(12)
     A                                      CF05(05 'REFR U/FILTER')
     A                                      CF08(08 'MAILADR')
     A                                      CF10(10 'SENDMAIL')
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR2         7A  P
     A                                  1  1'SYSPED/SYFI10XE'
     A                                      COLOR(BLU)
     A                                  1 19'Epost-advisering ved tillegsgebyr'
     A                                      COLOR(WHT)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  2  1'Gebyret'
     A            FAVK           3A  O  2  9COLOR(WHT)
     A            KGENOT        20A  O  2 13COLOR(WHT)
     A                                  2 34'skal forhåndsvarsles ved belastnin-
     A                                      g'
     A                                  3  1'En mail med standardisert tekst se-
     A                                      ndes til....               '
     A                                  4  1'Kunde'
     A            XXKUNR         8Y 0O  4  7EDTCDE(4)
     A                                      COLOR(WHT)
     A            NAVNADR       55A  O  4 16COLOR(WHT)
     A                                  5  1'Kp:'
     A            KPER01X       10A  O  5  5COLOR(WHT)
     A                                  5 16'Adr:'
     A            MADR01        50A  O  5 21COLOR(WHT)
     A                                  6  1'Kp:'
     A            KPER02X       10A  O  6  5COLOR(WHT)
     A                                  6 16'Adr:'
     A            MADR02        50A  O  6 21COLOR(WHT)
     A                                  7  1'Kp:'
     A            KPER03X       10A  O  7  5COLOR(WHT)
     A                                  7 16'Adr:'
     A            MADR03        50A  O  7 21COLOR(WHT)
     A                                  8  1'Kp:'
     A            KPER04X       10A  O  8  5COLOR(WHT)
     A                                  8 16'Adr:'
     A            MADR04        50A  O  8 21COLOR(WHT)
     A                                  9  1'Kp:'
     A            KPER05X       10A  O  9  5COLOR(WHT)
     A                                  9 16'Adr:'
     A            MADR05        50A  O  9 21COLOR(WHT)
     A                                 10  1'Eposten vil inneholde fritekst fra-
     A                                       fakt-linja (også tillegslinjer F )'
     A                                 11  1'Ekstra forklaring/begrunnelse for -
     A                                      belastningen:'
     A            WSBEG01       70A  B 12  1CHECK(LC)
     A                                      COLOR(YLW)
     A            WSBEG02       70A  B 13  1CHECK(LC)
     A                                      COLOR(YLW)
     A            WSBEG03       70A  B 14  1CHECK(LC)
     A                                      COLOR(YLW)
     A            WSBEG04       70A  B 15  1CHECK(LC)
     A                                      COLOR(YLW)
     A            WSBEG05       70A  B 16  1CHECK(LC)
     A                                      COLOR(YLW)
     A            TILLEGG       65A  O 17  2COLOR(WHT)
     A                                 18  2'F8=Legg til Kpers/Epostadr'
     A                                      COLOR(BLU)
     A                                 18 33'F10=Send Epost'
     A                                      COLOR(BLU)
     A                                 18 53'F12=Avbryt       '
     A                                      COLOR(BLU)
     A  30        MSGTX2        65A  O 19  2MSGID(&MSGNR2 SYMSGF)
     A                                      COLOR(WHT)
     A          R BILDE2
     A                                      WINDOW(*DFT 7 70 *NOMSGLIN)
     A                                      OVERLAY
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF03(12)
     A                                      CF12(12)
     A                                      CF10(10)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR2         7A  P
     A                                  1  1'SYSPED/SYFI10XE'
     A                                      COLOR(BLU)
     A                                  1 19'Epost-adresse mangler / legg til '
     A                                      COLOR(WHT)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  2  1'Kunde'
     A            XXKUNR         8Y 0O  2  7EDTCDE(4)
     A                                      COLOR(WHT)
     A            NAVNADR       55A  O  2 16COLOR(WHT)
     A                                  3  2'Kontaktperson.:'
     A            WSONTA        30A  B  3 18COLOR(YLW)
     A                                  4  2'Epost-adresse.:'
     A            WSMAIL        65A  B  5  2CHECK(LC)
     A                                      COLOR(YLW)
     A                                  6 33'F10=Send Epost'
     A                                      COLOR(BLU)
     A                                  6 53'F12=Avbryt       '
     A                                      COLOR(BLU)
     A  30        MSGTX2        65A  O  7  2MSGID(&MSGNR2 SYMSGF)
     A                                      COLOR(WHT)
