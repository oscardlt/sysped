      *
      ***********************************************************
      *** PROGRAM SKAL ARBEIDE MED GARANTINRG                 ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01 02 06-11 13-24
      *** LEDIGE INDIKATORER 28 29 31 32 34-87 95-97
      ***********************************************************
      *
     FTRUHGN    UF A E           K DISK
     FKODTS2    IF   E           K DISK
     FKODTVAL2  IF   E           K DISK
     FTR025D    CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
      *
     C                   EXSR      INIT
      *
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
     C     UGFT1         IFNE      *BLANKS
     C                   MOVE      UGFT1         TGGFT1
     C                   MOVE      UGADK         TGGADK
     C                   MOVE      UGBGI         TGGBGI
     C                   MOVE      UGBGU         TGGBGU
     C                   MOVE      UGBL          TGGBL
     C                   MOVE      UGVK          TGGVK
     C                   ADD       1             ZRECNO
     C                   WRITE     SUBFIL
     C                   END
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     TRHGNK        SETLL     TRUHGN
     C     TRHGNK        READE(N)  TRUHGN                                 94
      *
     C     *IN94         DOWEQ     '0'
     C                   ADD       1             ZRECNO
     C                   WRITE     SUBFIL
     C     TRHGNK        READE(N)  TRUHGN                                 94
     C  N94              END
      *
     C                   Z-ADD     ZRECNO        YRECNO            5 0
     C     ZRECNO        IFGT      0
     C                   Z-ADD     1             ZRECNO
     C                   END
     C                   Z-ADD     4             LINNBR
     C                   Z-ADD     2             POSNBR
      *
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYWRK
     C                   ELSE
     C                   WRITE     BUNN
     C                   EXFMT     SUBCTL
     C                   END
      *
     C                   SETOFF                                       9830
     C                   MOVEA     '00'          *IN(89)
     C   03
     COR 12              GOTO      SLUTT
      *** FRISK OPP
     C   05              DO
     C  N27              EXSR      OPDFIL
     C                   GOTO      START
     C                   END
      *
     C     *IN06         IFEQ      '1'
     C                   EXSR      CRTGN
     C                   GOTO      START3
     C                   END
      *
     C     ZRECNO        IFNE      0
     C                   EXSR      HENT
     C                   END
     C                   GOTO      START3
      *
     C     SLUTT         TAG
     C  N27              EXSR      OPDFIL
     C                   SETON                                        LR
     C                   RETURN
      *
     C***********************************************
     C     OPDFIL        BEGSR
     C***********************************************
     C     TRHGNK        SETLL     TRUHGN
     C     TRHGNK        READE     TRUHGN                                 33
     C     *IN33         DOWEQ     '0'
     C                   DELETE    TRUHGNR
     C     TRHGNK        READE     TRUHGN                                 33
     C  N33              ENDDO
      *
     C                   MOVE      *BLANKS       UGFT1
     C                   MOVE      *BLANKS       UGADK
     C                   MOVE      *ZEROS        UGBGI
     C                   MOVE      *BLANKS       UGBGU
     C                   MOVE      *ZEROS        UGBL
     C                   MOVE      *BLANKS       UGVK
     C                   MOVE      *ZEROS        XANTL             3 0
     C     YRECNO        IFGT      0
     C     1             CHAIN     SUBFIL                             33
     C     TGGFT1        IFNE      '*DELETE'
     C                   MOVE      TGGFT1        UGFT1
     C                   MOVE      TGGADK        UGADK
     C                   MOVE      TGGBGI        UGBGI
     C                   MOVE      TGGBGU        UGBGU
     C                   MOVE      TGGBL         UGBL
     C                   MOVE      TGGVK         UGVK
     C                   ADD       1             XANTL
     C                   END
     C     2             DO        YRECNO        ZRECNO
     C     ZRECNO        CHAIN     SUBFIL                             33
     C     TGGFT1        IFNE      '*DELETE'
     C     XANTL         IFEQ      0
     C                   MOVE      TGGFT1        UGFT1
     C                   MOVE      TGGADK        UGADK
     C                   MOVE      TGGBGI        UGBGI
     C                   MOVE      TGGBGU        UGBGU
     C                   MOVE      TGGBL         UGBL
     C                   MOVE      TGGVK         UGVK
     C                   ADD       1             XANTL
     C                   ELSE
     C                   ADD       1             XANTL
     C                   WRITE     TRUHGNR
     C                   END
     C                   END
     C                   ENDDO
     C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     HENT          BEGSR
     C***********************************************
     C                   MOVEA     '00'          *IN(91)
     C                   MOVEA     '00'          *IN(98)
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C   98              DO
     C   99              MOVE      '0'           *IN98
     C                   GOTO      SLHENT
     C                   END
     C  N98WSNR          IFEQ      '2'
     C     TGGFT1        COMP      '*DELETE'                              98
     C   98              GOTO      SLHENT
     C                   EXSR      CHGGN
     C                   END
     C  N98WSNR          IFEQ      '4'
     C     TGGFT1        COMP      '*DELETE'                              98
     C   98              GOTO      SLHENT
     C                   EXSR      DLTGN
     C                   END
     C                   END
      *
     C     SLHENT        TAG
     C   98              DO
     C                   MOVE      'DIV0012'     MSGNR
     C                   SETON                                            30
     C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     TESTB         BEGSR
     C***********************************************
     C     WSGFT1        IFEQ      *BLANKS
     C                   MOVE      '1'           *IN30
     C                   MOVE      '1'           *IN90
     C                   MOVE      'YBC0838'     MSGNR
     C                   GOTO      SLTB
     C                   END
      *
     C     WSGADK        IFEQ      *BLANKS
     C                   MOVE      'YBC0811'     MSGNR
     C                   MOVE      '1'           *IN30
     C                   MOVE      '1'           *IN89
     C                   GOTO      SLTB
     C                   END
      *
     C     WSGBGU        IFNE      *BLANKS
     C     KODTS2K       CHAIN     KODTS2                             88
     C                   IF        *IN88
     C                   MOVE      'SPL0001'     MSGNR
     C                   MOVE      '1'           *IN30
     C                   GOTO      SLTB
     C                   END
     C                   END
      *
     C     WSGBL         IFEQ      0
     C                   MOVE      'YBC1901'     MSGNR
     C                   MOVE      '1'           *IN30
     C                   MOVE      '1'           *IN87
     C                   GOTO      SLTB
     C                   END
      *
     C     WSGVK         CHAIN     KODTVAL2                           86
     C     *IN86         IFEQ      '1'
     C                   MOVE      'SPV0001'     MSGNR
     C                   MOVE      '1'           *IN30
     C                   MOVE      '1'           *IN86
     C                   GOTO      SLTB
     C                   END
      *
     C     SLTB          ENDSR
      *
     C***********************************************
     C     CRTGN         BEGSR
     C***********************************************
     C                   MOVE      *BLANKS       WSGFT1
     C                   MOVE      *BLANKS       WSGADK
     C                   MOVE      *ZEROS        WSGBGI
     C                   MOVE      *BLANKS       WSGBGU
     C                   MOVE      *ZEROS        WSGBL
      *
     C     STCRT         TAG
     C                   EXFMT     VARELI
     C                   MOVEA     '00000'       *IN(86)
     C                   MOVE      '0'           *IN30
      *
     C   04              DO
     C                   CALL      'SYFI10L'
     C                   PARM                    WSGBGU
     C                   PARM                    USPED
     C                   GOTO      STCRT
     C                   END
     C   12              GOTO      SLCRT
      *
     C                   EXSR      TESTB
     C   30              GOTO      STCRT
      *
     C                   MOVE      WSGFT1        TGGFT1
     C                   MOVE      WSGADK        TGGADK
     C                   MOVE      WSGBGI        TGGBGI
     C                   MOVE      WSGBGU        TGGBGU
     C                   MOVE      WSGBL         TGGBL
     C                   MOVE      WSGVK         TGGVK
     C                   ADD       1             YRECNO
     C                   MOVE      YRECNO        ZRECNO
     C                   WRITE     SUBFIL
     C                   WRITE     SUBCTL
      *
     C     SLCRT         ENDSR
      *
     C***********************************************
     C     CHGGN         BEGSR
     C***********************************************
     C                   MOVE      '1'           *IN99
     C                   MOVE      TGGFT1        WSGFT1
     C                   MOVE      TGGADK        WSGADK
     C                   MOVE      TGGBGI        WSGBGI
     C                   MOVE      TGGBGU        WSGBGU
     C                   MOVE      TGGBL         WSGBL
     C                   MOVE      TGGVK         WSGVK
      *
     C     STCHG         TAG
     C                   EXFMT     VARELI
     C                   MOVEA     '00000'       *IN(86)
     C                   MOVE      '0'           *IN30
      *
     C   04              DO
     C                   CALL      'SYFI10L'
     C                   PARM                    WSGBGU
     C                   PARM                    USPED
     C                   GOTO      STCHG
     C                   END
     C   12              GOTO      SLCHG
      *
     C                   EXSR      TESTB
     C   30              GOTO      STCHG
      *
     C                   MOVE      WSGFT1        TGGFT1
     C                   MOVE      WSGADK        TGGADK
     C                   MOVE      WSGBGI        TGGBGI
     C                   MOVE      WSGBGU        TGGBGU
     C                   MOVE      WSGBL         TGGBL
     C                   MOVE      WSGVK         TGGVK
      *
     C     SLCHG         TAG
     C                   MOVE      *BLANKS       WSNR
     C                   UPDATE    SUBFIL
      *
     C                   ENDSR
      *
     C***********************************************
     C     DLTGN         BEGSR
     C***********************************************
     C                   MOVE      '1'           *IN99
     C                   EVAL      TGGFT1 = '*DELETE'
     C                   MOVE      *BLANKS       TGGADK
     C                   MOVE      *ZEROS        TGGBGI
     C                   MOVE      *BLANKS       TGGBGU
     C                   MOVE      *ZEROS        TGGBL
     C                   MOVE      *BLANKS       TGGVK
     C                   UPDATE    SUBFIL
      *
     C                   ENDSR
      *
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     TRHGNK        KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    UTDN
     C     KODTS2K       KLIST
     C                   KFLD                    KS2UNI
     C                   KFLD                    WSGBGU
      *
     C     *ENTRY        PLIST
     C                   PARM                    UAVD              4 0
     C                   PARM                    UTDN              7 0
     C                   PARM                    UGFT1            24
     C                   PARM                    UGADK             4
     C                   PARM                    UGBGI             1 0
     C                   PARM                    UGBGU             2
     C                   PARM                    UGBL             11 0
     C                   PARM                    UGVK              3
     C                   PARM                    UTYPE             3
      *
     C                   WRITE     WINDOWSZ
     C                   MOVE      'N'           USPED             1
     C                   MOVE      'S2 '         KS2UNI
     C                   MOVE      UAVD          TGAVD
     C                   MOVE      UTDN          TGTDN
     C     UTYPE         COMP      'DSP'                                  27
      *
     C                   ENDSR
      *
