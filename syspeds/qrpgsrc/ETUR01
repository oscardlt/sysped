      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/ETUR01 MODULE(*LIBL/ETUR01
CRTPGM*        *LIBL/CHECKUSR *LIBL/INCGI) ACTGRP(CGI) AUT(*USE)
      *
      *********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 03 JOV turstatus - link 'Open' dersom gml er status er closed(A,B,C,P...)
10XXX * 04 JOV turstatus - vis henger dersom bilnr = '.'
10233 * 05 JOV viser nå turene i motsatt rekkefølge
10233 * 06 JOV espedTop/BOT ligge i /syspeddeb/html/
********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
     FBRIDF     UF   E           K DISK    USROPN
     FBRPARF    IF   E           K DISK    USROPN
     FTURER1    IF   E           K DISK    USROPN
      *********************************************************************
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      /copy syspeds/qrpgsrcpy,variables3
     D IfsMultIndicators...
     d                 ds
     D  NoErrors                       n
     D  NameTooLong                    n
     D  NotAccessible                  n
     D  NoFilesUsable                  n
     D  DupSections                    n
     D  FileIsEmpty                    n
      *
      * Client input variables
     D user            s             10
     D UsrSpcName      s             10
     D Frames          s              1
N01  D WSSAVD          S              4
N01  D WBSAVD          S              4  0
     D WSSST           S              1
     D PGM             C                   CONST('SYSPED/CHECKUSR')
      * For xlate mellom Upper Case og Lower Case
     D UC              C                   CONST('ABCDEFGHIJKLMNOPQRST-
     D                                     UVWXYZÆØÅ')
     D LC              C                   CONST('abcdefghijklmnopqrst-
     D                                     uvwxyzæøå')
      *
      /copy syspeds/qrpgsrcpy,prolog3
      *
      * Get program start time for calculating execution time
     C                   time                    timedata1
      * Parse input / cvt to numeric
     C                   exsr      Parse
      * File open / keys
     C                   EXSR      INIT
      *
     C                   CALLB     PGM
     C                   PARM                    BIBRID
     C                   PARM                    UsrSpcName
     C                   PARM                    UINN              1
     C                   IF        UINN = 'N'
     C                   GOTO      SLUTT
     C                   END
      * dato/tid siste vellykkede operasjon.
     C     USER          CHAIN     BRIDF                              50
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVE      WDT           BIDTF
     C                   MOVE      WTM           BITIDF
     C                   UPDATE    BRIDFR
      *
      * Read output skeleton html member into memory
     C                   exsr      LoadHtml
      * Set section variables
     C                   exsr      Settop
      * Frames = Y = init / første runde
     C     FRAMES        IFeq      'Y'
     C                   callp     wrtsection('all')
     c                   goto      slutt
     C                   END
      *
s06  C*                  callp     wrtsection('stdtop')
N06  C                   callp     wrtsection('StdtopTab')
     C                   callp     wrtsection('top')
     C                   IF        XTURTYPE = 'O'
     C                   callp     wrtsection('OST00')
     C                   callp     wrtsection('FET00')
     C                   callp     wrtsection('OST01')
     C                   ELSE
     C                   callp     wrtsection('LTT00')
     C                   callp     wrtsection('FET00')
     C                   callp     wrtsection('LTT01')
     C                   END
     C                   callp     wrtsection('FET01')
      * HOVEDRUTINE
      * Vis ordre på trip.
S05  C*    TURER1K       SETLL     TURER1
S05  C*    TURER1K       READE     TURER1                                 35
N05  C     TURER1K       SETGT     TURER1
N05  C     TURER1K       READPE    TURER1                                 35
     C                   DOW       *IN35 = *OFF
     C                   EXSR      SRFILTER
     C                   IF        XVIS = 'J'
     C                   EXSR      SETROW
     C                   callp     wrtsection('row')
     C                   IF        XTURTYPE = 'O'
     C                   callp     wrtsection('OST02')
     C                   ELSE
     C                   callp     wrtsection('LTT02')
     C                   END
     C                   callp     wrtsection('FET02')
     C                   END
S05  C*    TURER1K       READE     TURER1                                 35
N05  C     TURER1K       READPE    TURER1                                 35
     C                   ENDDO
      *
     C                   callp     wrtsection('bot')
      *
N06  C                   callp     wrtsection('botStd')
      * Quit
     c     Slutt         tag
     C                   exsr      Exit
      *
     C                   eval      *inlr = *on
     C                   return
      *
      *
      *=====================================================================
      * Parse input / cvt to numeric
      *=====================================================================
     C     Parse         begsr
      * Parse variables from QUERY_STRING environment variable
     C                   eval      user     = zhbgetvar('user')
     C                   EVAL      UsrSpcName  = zhbgetvar('UsrSpcName')
     C                   eval      WSSST    = zhbgetvar('WSSST')
     C     LC:UC         XLATE     WSSST         WSSST
     C                   eval      WSSAVD   = zhbgetvar('WSSAVD')
     C                   EVAL      WBSAVD   = c2n2(WSSAVD)
     C                   eval      FRAMES   = zhbgetvar('FRAMES')
      *
     C                   endsr
      *
      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   CLOSE     BRIDF
     C                   CLOSE     BRPARF
     C                   CLOSE     TURER1
      *
     C                   endsr
      *
      *=====================================================================
      * Read output skeleton html member into memory
      *=====================================================================
     C     LoadHtml      begsr
     C                   IF        FRAMES = 'Y'
     C                   eval      IfsMultIndicators = gethtmlifsmult(
s06  C*                            '/syspeddev/html/Header/eSpedTop.html -
n06  C                             '/syspeddev/html/eSpedTop.html -
     C                             /syspeddev/html/ETUR01F.html -
s06  C*                            /syspeddev/html/Footer/eSpedBot.html':
n06  C                             /syspeddev/html/eSpedBot.html':
     C                             '/CGITAG/')
     C                   ELSE
     C                   eval      IfsMultIndicators = gethtmlifsmult(
s06  C*                            '/syspeddev/html/Header/eSpedTop.html -
n06  C                             '/syspeddev/html/eSpedTop.html -
     C                             /syspeddev/html/ETUR01.html -
s06  C*                            /syspeddev/html/Footer/eSpedBot.html':
n06  C                             /syspeddev/html/eSpedBot.html':
     C                             '/CGITAG/')
     C                   END
     C                   endsr
      *=====================================================================
      *  TEST OM TUR SKAL VISES
      *=====================================================================
     C     SRFILTER      begsr
      *
     C                   MOVE      'N'           XVIS              1
     C                   IF        XTURTYPE = 'O'
     C                   IF        ((TUSG = 'WEB') AND (TUATA = 0))
     C                   MOVE      'J'           XVIS
     C                   END
     C                   ELSE
     C                   IF        TURUND <> 0
     C                   MOVE      'J'           XVIS
     C                   END
     C                   END
      *
     C                   endsr
      *
      *=====================================================================
      *  Set variable data for section /$top
      *=====================================================================
     C     SetTop        begsr
      *
     C                   callp     updHTMLvar('ROWHEAD':RowHead)
     C                   callp     updHTMLvar('LogoImg':LogoImg)
     C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updHTMLvar('USER':USER)
     C                   callp     updHtmlVar('UsrSpcName':UsrSpcName)
     C                   callp     updHTMLvar('FRAMES':FRAMES)
     C                   callp     updHTMLvar('WSSST':WSSST)
     C                   callp     updHTMLvar('WSSAVD':
     C                             %trim(%editc(WBSAVD:'Z')))
      *
     C                   endsr
      *
      *=====================================================================
      *  Set variable data for section /$row
      *=====================================================================
     C     Setrow        begsr
      *
     C                   callp     updHTMLvar('TUPRO':
     C                             %trim(%editc(TUPRO:'Z')))
     C                   MOVE      TUPRO         TUPRO2            8
     C                   callp     updHTMLvar('TUPRO2':TUPRO2)
     C                   callp     updHTMLvar('TUSTEF':TUSTEF)
     C                   callp     updHTMLvar('TUSTET':TUSTET)
     C                   callp     updHTMLvar('TUETD':
     C                             %trim(%editW(TUETD:'    .  .  ')))
     C                   callp     updHTMLvar('TUETA':
     C                             %trim(%editW(TUETA:'    .  .  ')))
     C                   callp     updHTMLvar('TUATD':
     C                             %trim(%editW(TUATD:'    .  .  ')))
     C                   callp     updHTMLvar('TUCON1':TUCON1)
      *
N04  c                   if        TUBILN = '.' and TUHENG<>' '
N04  c                   eval      TUBILN = TUHENG
N04  c                   endif
     C                   callp     updHTMLvar('TUBILN':TUBILN)
     C                   callp     updHTMLvar('TUDT':
     C                             %trim(%editW(TUDT:'    .  .  ')))
     C                   callp     updHTMLvar('TUDTT':
     C                             %trim(%editW(TUDTT:'    .  .  ')))
      * ved landbasert - kan endre satus ved klikk på link
     C                   move      *blanks       TurClose        100
s01  c*                  if        XTURTYPE = 'L' and TUST = ' '
N01  c                   if        XTURTYPE = 'L'
N01  c                   if        TUST = ' '
     c                   eval      TurClose='&nbsp;&nbsp;&nbsp;&nbsp;'
     C                             +'<a href="#" OnClick="CloseTrip('
     c                             +%trim(%editc(TUPRO:'Z'))+')">Close</a>'
N01  c                   else
N01  c                   eval      TurClose='&nbsp;&nbsp;&nbsp;&nbsp;'
N01  C                             +'<a href="#" OnClick="OpenTrip('
N01  c                             +%trim(%editc(TUPRO:'Z'))+')">Open</a>'
N01  c                   endif
     c                   endif
     C                   callp     updHTMLvar('TURCLOSE':TURCLOSE)
      *
     C                   endsr
      *
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
     C                   OPEN      BRIDF
     C     USER          CHAIN(N)  BRIDF                              50
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
      *
      * hent Parametre som går igjen i mange prog:
      *      Odd/Even/Rowhead-farger,Logobilde,Språk,Intern/Ekstern bruker,  mm
     c                   call      'ESGETPAR'
     c                   parm                    BIBRID
     c                   parm                    BIFIRM
     c                   parm                    BIKUNR
     c                   parm                    BIKNG
     c                   parm                    RowHead          10
     c                   parm                    Odd              10
     c                   parm                    Even             10
     c                   parm                    LogoImg          50
     c                   parm                    XSPRÅK            2
     c                   parm                    XINTERN           1
     c                   parm                    ParmDS1        1024
     c                   parm                    ParmDS2        1024
     c                   parm                    ParmDS3        1024
      * HENTER PARAMETER
     C                   CALL      'ESGETOS'
     C                   parm                    BIBRID
     C                   parm                    BIFIRM
     C                   parm                    BIKUNR
     C                   parm                    BIKNG
     C                   parm                    ParmOS1        1024
      *
     C                   OPEN      BRPARF
     C                   OPEN      TURER1
      *
     C     TURER1K       klist
     C                   kfld                    WSSST
     C                   kfld                    XAVD
     C     BrParKey      klist
     C                   kfld                    PABRID
     C                   kfld                    PAKUNR
     C                   kfld                    PAID
      *
     C                   MOVE      BIBRID        PABRID
     C                   MOVE      *zeros        PAKUNR
      * Bruke oversjø eller landtransport
     C                   MOVE      'TURPL'       PAID
     C     BrParKey      chain     BRPARF                             33
     C  N33              MOVEL     PAVRDA        XTURTYPE          1
     C   33              MOVE      'O'           XTURTYPE
      * Brukeren kan kun jobbe for følgende Attestasjonskode.
     C                   IF        XTURTYPE = 'O'
     C                   MOVE      'OSAVD'       PAID
     C                   ELSE
     C                   MOVE      'LTAVD'       PAID
     C                   END
     C     BrParKey      chain     BRPARF                             33
     C  N33              Z-ADD     PAVRDN        XAVD              4 0
     C                   IF        WBSAVD <> 0
     C                   EVAL      XAVD = WBSAVD
     C                   END
      *
     C                   BITON     '123457'      GRAAPP            1
     C                   BITOFF    '06'          GRAAPP
      *
     C                   endsr
      *
