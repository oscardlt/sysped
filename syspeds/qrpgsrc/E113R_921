     H DATEDIT(*YMD)
     FEDIS      IF   E           K DISK
     FEDIM      IF   E           K DISK
     FECNI921   IF   E           K DISK
     FEEQD921   IF   E           K DISK
     FELOC921   IF   E           K DISK
     FETDT921   IF   E           K DISK
     FEFTX921   IF   E           K DISK
     FENAD921   IF   E           K DISK
     FERFF921   IF   E           K DISK
     FETOD921   IF   E           K DISK
     FEGID921   IF   E           K DISK
     FEMEA921   IF   E           K DISK
     FEPCI921   IF   E           K DISK
     FZEADF     O  A E             DISK
     FZREETXT   O  A E             DISK
     D A               S              1    DIM(79)
     D B               S              1    DIM(18)
      *________________________
      * DS for decimal handling
      *""""""""""""""""""""""""
     D                 DS
     D  ZDEC                   1      7  6
     D  ZDEC6                  2      7
     D  ZHEL                  11     22  0
     D                 DS
     D  WMN2                   1      9  0
      */////////////
      * MAIN LOGIC /
      */////////////
      *___________
      * Initiering
      *"""""""""""
     C                   EXSR      INIT
      *________
      * CONVERT
      *""""""""
     C                   EXSR      RTVD
      *
     C                   MOVE      '1'           *INLR
      *
      *////////////////////////////////////////////////////////////
      *                                                      INIT /
      *////////////////////////////////////////////////////////////
     C     INIT          BEGSR
     C     KEYMS0        KLIST
     C                   KFLD                    ZMN
     C                   KFLD                    ZGN0
     C     KEYMS1        KLIST
     C                   KFLD                    ZMN
     C                   KFLD                    ZGN1
     C                   KFLD                    ZREPB0
      *
     C     *LIKE         DEFINE    NMN           ZMN
     C     *LIKE         DEFINE    NGN           ZGN0
     C     *LIKE         DEFINE    NGN           ZGN1
     C     *LIKE         DEFINE    NREP0         ZREPB0
     C     *LIKE         DEFINE    HEHAWB        WEHAWB
     C     *LIKE         DEFINE    HETRI         WETRI
     C     *LIKE         DEFINE    HETRC         WETRC
     C     *LIKE         DEFINE    HETRCN        WETRCN
     C     *LIKE         DEFINE    HESDF         WESDF
     C     *LIKE         DEFINE    HENAA         WENAA
     C     *LIKE         DEFINE    HESDL         WESDL
      * INIT WORKFIELDS
     C                   MOVE      *BLANKS       WEHAWB
     C                   MOVE      *BLANKS       WETRI
     C                   MOVE      *BLANKS       WETRC
     C                   MOVE      *BLANKS       WETRCN
     C                   MOVE      *BLANKS       WESDF
     C                   MOVE      *BLANKS       WENAA
     C                   MOVE      *BLANKS       WESDL
      *
     C     *ENTRY        PLIST
     C                   PARM                    WMN1              9
     C                   MOVEL     WMN1          WMN2
     C                   Z-ADD     WMN2          ZMN
      * Get msg and interchange values
     C     ZMN           CHAIN     EDIM                               51
     C     MSN           CHAIN     EDIS                               51
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      * RETREIVE INFO FROM EDIFACT DATABASE TO SYSPED400     RTVD /
      *////////////////////////////////////////////////////////////
     C     RTVD          BEGSR
      *_______________________
      * READ ALL MAIN SEGMENTS
      *"""""""""""""""""""""""
      * NAD09
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     09            ZGN0
     C     KEYMS0        SETLL     ENAD921                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS0        READE     ENAD921                                51
     C     *IN51         DOWEQ     '0'
      *
     C     N3035         IFEQ      'MR'                                         RECEIVING AG
     C                   MOVEL     N3124A        WENAA
     C                   MOVE      '1'           *IN51
     C                   END
      *
     C  N51KEYMS0        READE     ENAD921                                51
     C  N51              END
     C                   END
      * LOC05
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     5             ZGN0
     C     KEYMS0        SETLL     ELOC921                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS0        READE     ELOC921                                51
     C     *IN51         DOWEQ     '0'
      *
     C     L3227         IFEQ      '5  '
     C                   MOVEL     E3055A        WESDF
     C                   ELSE
     C     L3227         IFEQ      '20 '
     C                   MOVEL     E3055A        WESDL
     C                   END
     C                   END
      *
     C  N51KEYMS0        READE     ELOC921                                51
     C  N51              ENDDO
     C                   END
      * LOC13
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     13            ZGN0
     C     KEYMS0        SETLL     ELOC921                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS0        READE     ELOC921                                51
     C     *IN51         DOWEQ     '0'
      *
     C     L3227         IFEQ      '5'
     C                   MOVEL     E3055A        WESDF
     C                   ELSE
     C     L3227         IFEQ      'ZZZ'
     C                   MOVEL     E3055A        WETRI
     C                   END
     C                   END
      *
     C  N51KEYMS0        READE     ELOC921                                51
     C  N51              ENDDO
     C                   END
      * EQD13
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     13            ZGN0
     C     KEYMS0        SETLL     EEQD921                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS0        READE     EEQD921                                51
     C     *IN51         DOWEQ     '0'
      *
     C     E8053         IFEQ      'CN'
     C                   MOVE      '1'           WETRC
     C                   MOVEL     E8260         WETRCN
     C                   MOVE      '1'           *IN51
     C                   ELSE
     C     E8053         IFEQ      'TE'
     C                   MOVEL     E8260         WEHAWB
     C                   MOVE      '1'           *IN51
     C                   END
     C                   END
      *
     C  N51KEYMS0        READE     EEQD921                                51
     C  N51              ENDDO
     C                   END
      * TDT05
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     5             ZGN0
     C     KEYMS0        SETLL     ETDT921                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS0        READE     ETDT921                                51
     C     *IN51         DOWEQ     '0'
      *
     C     T8067         IFEQ      '3'
     C                   MOVEL     T8212         WETRCN
     C                   MOVEL     T8212         WEHAWB
     C                   MOVEL     T8453         WETRI
     C                   MOVE      '1'           *IN51
     C                   END
      *
     C  N51KEYMS0        READE     ETDT921                                51
     C  N51              ENDDO
     C                   END
      *___________________
      * READ CONSIGNEMENTS
      *"""""""""""""""""""
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     15            ZGN0
     C     KEYMS0        SETLL     ECNI921                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS0        READE     ECNI921                                51
     C     *IN51         DOWEQ     '0'
      *
     C                   EXSR      RTVDB
      *
     C     KEYMS0        READE     ECNI921                                51
     C  N51              ENDDO
     C                   ENDIF
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * RETREIVE INFO FROM EDIFACT DATABASE TO SYSPED400    RTVDB /
      *////////////////////////////////////////////////////////////
     C     RTVDB         BEGSR
      *__________________
      * INIT. DATAFELTER
      *""""""""""""""""""
     C                   CLEAR                   HEADFR
     C                   MOVE      'A'           HEUR
     C                   MOVE      'EDI'         HESG
     C                   MOVEL     SLIB          HELIB
     C                   MOVEL     SFILE         HEFILE
     C                   MOVEL     SMBR          HEMBR
     C                   MOVE      'IFCSUM'      HETYPE
     C                   MOVE      '921'         HELVL
     C                   Z-ADD     ZMN           HESNN
     C                   Z-ADD     MSN           HEUNIK
     C                   MOVEL(P)  ZMN           HEREFF
     C                   CAT       '_':0         HEREFF
     C                   CAT       C1490:0       HEREFF
     C                   MOVEL     WEHAWB        HEHAWB
     C                   MOVEL     WETRI         HETRI
     C                   MOVEL     WETRC         HETRC
     C                   MOVEL     WETRCN        HETRCN
     C                   MOVEL     WESDF         HESDF
     C                   MOVEL     WENAA         HENAA
     C                   MOVEL     WESDL         HESDL
      *__________________
      * READ ALL SEGMENTS
      *""""""""""""""""""
     C                   Z-ADD     CREP0         ZREPB0
     C                   EXSR      LOC17
     C                   EXSR      TOD17
     C                   EXSR      NAD25
     C                   EXSR      RFF29
     C                   EXSR      FTX31
     C                   EXSR      GID31
     C                   EXSR      LOC31
     C                   EXSR      FTX33
     C                   EXSR      MEA34
     C                   EXSR      PCI37
      *___________________
      * WRITE TO SYSPED400
      *"""""""""""""""""""
     C                   WRITE     HEADFR
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      * LOC17                                               LOC17 /
      *////////////////////////////////////////////////////////////
     C     LOC17         BEGSR
     C                   MOVE      '0'           *IN52
     C                   Z-ADD     17            ZGN1
     C     KEYMS1        SETLL     ELOC921                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS1        READE     ELOC921                                52
     C     *IN52         DOWEQ     '0'
      *
     C     L3227         IFEQ      '1'
     C                   MOVEL     L3224         HESDT
     C                   MOVE      '1'           *IN52
     C                   END
      *
     C     KEYMS1        READE     ELOC921                                52
     C  N52              ENDDO
     C                   ENDIF
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      * TOD17                                               TOD17 /
      *////////////////////////////////////////////////////////////
     C     TOD17         BEGSR
     C                   MOVE      '0'           *IN52
     C                   Z-ADD     17            ZGN1
     C     KEYMS1        SETLL     ETOD921                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS1        READE     ETOD921                                52
     C     *IN52         IFEQ      '0'
      *
     C                   MOVEL     T4053         HEFR
      *
     C                   ENDIF
     C                   ENDIF
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      * NAD25                                               NAD25 /
      *////////////////////////////////////////////////////////////
     C     NAD25         BEGSR
     C                   MOVE      '0'           *IN52
     C                   Z-ADD     25            ZGN1
     C     KEYMS1        SETLL     ENAD921                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS1        READE     ENAD921                                52
     C     *IN52         DOWEQ     '0'
      *
     C                   SELECT
     C     N3035         WHENEQ    'CZ'                                         GODSAVSENDER
     C     N3124A        IFNE      *BLANKS
     C                   MOVEL     N3124A        HENAS
     C                   MOVEL     N3124A        HENASF
     C                   ENDIF
     C     N3124B        IFNE      *BLANKS
     C                   MOVEL     N3124B        HEADS1
     C                   ENDIF
     C     N3124C        IFNE      *BLANKS
     C                   MOVEL     N3124C        HEADS2
     C                   ENDIF
     C     N3124D        IFNE      *BLANKS
     C                   MOVEL     N3124D        HEADS3
     C                   ENDIF
     C     N3036A        IFNE      *BLANKS
     C                   MOVEL     N3036A        HENAS
     C                   MOVEL     N3036A        HENASF
     C                   ENDIF
     C     N3042A        IFNE      *BLANKS
     C                   MOVEL     N3042A        HEADS1
     C                   ENDIF
     C     N3251         IFNE      *BLANKS
     C                   MOVEL     N3251         WEADS1            9
     C                   MOVEL     N3164         WEADS2           21
     C                   MOVEL     WEADS1        HEADS3
     C                   MOVE      WEADS2        HEADS3
     C                   ENDIF
     C     N3035         WHENEQ    'BY'                                         KJØP.FAKT
     C     N3124A        IFNE      *BLANKS
     C                   MOVEL     N3124A        HENAKF
     C                   ENDIF
     C     N3036A        IFNE      *BLANKS
     C                   MOVEL     N3036A        HENAKF
     C                   ENDIF
     C     N3124A        IFNE      *BLANKS
     C                   MOVEL     N3124A        HENAK
     C                   ENDIF
     C     N3124B        IFNE      *BLANKS
     C                   MOVEL     N3124B        HEADK1
     C                   ENDIF
     C     N3124C        IFNE      *BLANKS
     C                   MOVEL     N3124C        HEADK2
     C                   ENDIF
     C     N3124D        IFNE      *BLANKS
     C                   MOVEL     N3124D        HEADK3
     C                   ENDIF
     C     N3036A        IFNE      *BLANKS
     C                   MOVEL     N3036A        HENAK
     C                   ENDIF
     C     N3042A        IFNE      *BLANKS
     C                   MOVEL     N3042A        HEADK1
     C                   ENDIF
     C     N3251         IFNE      *BLANKS
     C*                    MOVELN3251     HESDVT
     C                   MOVEL     N3251         WEADS1
     C                   MOVEL     N3164         WEADS2
     C                   MOVEL     WEADS1        HEADK3
     C                   MOVE      WEADS2        HEADK3
     C                   ENDIF
     C     N3035         WHENEQ    'DP'                                         LEV.ADR
     C     N3124A        IFNE      *BLANKS
     C                   MOVEL     N3124A        HENAK
     C                   ENDIF
     C     N3124B        IFNE      *BLANKS
     C                   MOVEL     N3124B        HEADK1
     C                   ENDIF
     C     N3124C        IFNE      *BLANKS
     C                   MOVEL     N3124C        HEADK2
     C                   ENDIF
     C     N3124D        IFNE      *BLANKS
     C                   MOVEL     N3124D        HEADK3
     C                   ENDIF
     C     N3036A        IFNE      *BLANKS
     C                   MOVEL     N3036A        HENAK
     C                   ENDIF
     C     N3042A        IFNE      *BLANKS
     C                   MOVEL     N3042A        HEADK1
     C                   ENDIF
     C     N3251         IFNE      *BLANKS
     C*                    MOVELN3251     HESDVT
     C                   MOVEL     N3251         WEADS1
     C                   MOVEL     N3164         WEADS2
     C                   MOVEL     WEADS1        HEADK3
     C                   MOVE      WEADS2        HEADK3
     C                   ENDIF
      *
     C                   ENDSL
      *
     C     KEYMS1        READE     ENAD921                                52
     C  N52              ENDDO
     C                   ENDIF
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      * RFF29                                               RFF29 /
      *////////////////////////////////////////////////////////////
     C     RFF29         BEGSR
     C                   MOVE      '0'           *IN52
     C                   Z-ADD     29            ZGN1
     C     KEYMS1        SETLL     ERFF921                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS1        READE     ERFF921                                52
     C     *IN52         DOWEQ     '0'
      *
     C     R1153         IFEQ      'ACL'
     C                   MOVEL     R1154         HERFA
     C                   MOVEL     R1154         HEREFF
     C                   MOVE      '1'           *IN52
     C                   ENDIF
      *
     C  N52KEYMS1        READE     ERFF921                                52
     C  N52              ENDDO
     C                   ENDIF
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      * FTX31                                               FTX31 /
      *////////////////////////////////////////////////////////////
     C     FTX31         BEGSR
     C                   CLEAR                   FREETXTR
     C                   MOVE      HEUNIK        FRUNIK
     C                   MOVE      HEREFF        FRREFF
     C                   Z-ADD     MDT           FRTDT
     C                   MOVE      '0'           *IN52
     C                   Z-ADD     31            ZGN1
     C     KEYMS1        SETLL     EFTX921                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS1        READE     EFTX921                                52
     C     *IN52         DOWEQ     '0'
      *
     C     F4451         IFEQ      'AAI'
     C                   ADD       1             FRTLI
     C                   MOVEL     F4440A        FRTTXT
     C                   WRITE     FREETXTR
     C     F4440B        IFNE      *BLANKS
     C                   ADD       1             FRTLI
     C                   MOVEL     F4440B        FRTTXT
     C                   WRITE     FREETXTR
     C                   END
     C     F4440C        IFNE      *BLANKS
     C                   ADD       1             FRTLI
     C                   MOVEL     F4440C        FRTTXT
     C                   WRITE     FREETXTR
     C                   END
     C     F4440D        IFNE      *BLANKS
     C                   ADD       1             FRTLI
     C                   MOVEL     F4440D        FRTTXT
     C                   WRITE     FREETXTR
     C                   END
     C     F4440E        IFNE      *BLANKS
     C                   ADD       1             FRTLI
     C                   MOVEL     F4440E        FRTTXT
     C                   WRITE     FREETXTR
     C                   END
     C                   ENDIF
      *
     C  N52KEYMS1        READE     EFTX921                                52
     C  N52              ENDDO
     C                   ENDIF
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      * GID31                                               GID31 /
      *////////////////////////////////////////////////////////////
     C     GID31         BEGSR
     C                   MOVE      '0'           *IN52
     C                   Z-ADD     31            ZGN1
     C     KEYMS1        SETLL     EGID921                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS1        READE     EGID921                                52
     C     *IN52         DOWEQ     '0'
      *
     C                   MOVEA     G7224A        A
     C                   EXSR      GETNUM
     C     HENT          IFEQ      0
     C                   ADD       ZNUM          HENT
     C                   MOVEL     G7064A        HEVS1
     C                   ELSE
     C                   ADD       1             FRTLI
     C                   MOVEL(P)  G7224A        FRTTXT
     C     FRTTXT        CAT       G7064A:1      FRTTXT
     C                   WRITE     FREETXTR
     C                   ENDIF
      *
     C     KEYMS1        READE     EGID921                                52
     C  N52              ENDDO
     C                   ENDIF
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      * LOC31                                               LOC31 /
      *////////////////////////////////////////////////////////////
     C     LOC31         BEGSR
     C                   MOVE      '0'           *IN52
     C                   Z-ADD     31            ZGN1
     C     KEYMS1        SETLL     ELOC921                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS1        READE     ELOC921                                52
     C     *IN52         DOWEQ     '0'
      *
     C     L3227         IFEQ      '14'
     C                   MOVEL     L3224         HESDFF
     C                   MOVE      '1'           *IN52
     C                   END
      *
     C     KEYMS1        READE     ELOC921                                52
     C  N52              ENDDO
     C                   ENDIF
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      * FTX33                                               FTX33 /
      *////////////////////////////////////////////////////////////
     C     FTX33         BEGSR
     C                   MOVE      '0'           *IN52
     C                   Z-ADD     33            ZGN1
     C     KEYMS1        SETLL     EFTX921                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS1        READE     EFTX921                                52
     C     *IN52         DOWEQ     '0'
      *
     C     F4451         IFEQ      'AAA'
     C     HEVS1         CAT       F4440A:1      HEVS1
     C                   MOVEL     F4440B        HEVS2
     C                   MOVE      '1'           *IN52
     C                   END
      *
     C     KEYMS1        READE     EFTX921                                52
     C  N52              ENDDO
     C                   ENDIF
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      * MEA34                                               MEA34 /
      *////////////////////////////////////////////////////////////
     C     MEA34         BEGSR
     C                   MOVE      '0'           *IN52
     C                   Z-ADD     34            ZGN1
     C     KEYMS1        SETLL     EMEA921                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS1        READE     EMEA921                                52
     C     *IN52         DOWEQ     '0'
      *
     C                   MOVEA     M6314         A
     C                   EXSR      GETNUM
     C                   SELECT
     C     M6311         WHENEQ    'WT '
     C     M6313         ANDEQ     'G  '
     C                   Z-ADD     ZNUM          HEVKT
     C     M6311         WHENEQ    'LMT'
     C                   Z-ADD     ZNUM          HELM
     C     M6311         WHENEQ    'VOL'
     C                   Z-ADD     ZNUM          HEM3
     C                   ENDSL
      *
     C     KEYMS1        READE     EMEA921                                52
     C  N52              ENDDO
     C                   ENDIF
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      * PCI37                                               PCI37 /
      *////////////////////////////////////////////////////////////
     C     PCI37         BEGSR
     C                   MOVE      '0'           *IN52
     C                   Z-ADD     37            ZGN1
     C     KEYMS1        SETLL     EPCI921                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS1        READE     EPCI921                                52
     C     *IN52         IFEQ      '0'
      *
     C                   MOVEL     P7102A        HEGM1
     C                   MOVEL     P7102B        HEGM2
      *
     C                   ENDIF
     C                   ENDIF
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      *  Get numerical value                               GETNUM /
      *////////////////////////////////////////////////////////////
     C     GETNUM        BEGSR
     C                   MOVE      *ZEROS        ZHEL
     C                   MOVE      *ZEROS        ZDEC
     C                   MOVE      *ZEROS        B
     C                   Z-ADD     1             AX                2 0
     C                   Z-ADD     1             BX                2 0
      * Get Integer
     C     A(AX)         DOWGE     '0'
     C     ZHEL          MULT      10            ZHEL
     C                   MOVE      A(AX)         ZHEL
     C                   ADD       1             AX
     C                   ENDDO
      * Decimal point ?
     C     A(AX)         IFEQ      '.'
     C     A(AX)         OREQ      ','
     C                   ADD       1             AX
     C     A(AX)         DOWGE     '0'
     C                   MOVE      A(AX)         B(BX)
     C                   ADD       1             AX
     C                   ADD       1             BX
     C                   ENDDO
     C                   MOVEA     B             ZDEC6
     C                   ENDIF
      * Concatenate
     C                   Z-ADD     ZHEL          ZNUM             18 6
     C                   ADD       ZDEC          ZNUM
     C                   ENDSR
