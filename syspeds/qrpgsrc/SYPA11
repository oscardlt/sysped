     FPALFIR    UF   E             DISK
     FPALLD     O  A E           K DISK
     FPAPA      UF A E           K DISK
     FFIRM      IF   E           K DISK
     FTRAN      IF   E           K DISK
     FCUNDL01   IF   E           K DISK
     D                 DS
     D  PA                     1      8
     D                                     DIM(8)
     D  PART                   1      8
     D                 DS
     D  DTÅ                    1      4  0
     D  DTM                    5      6  0
     D  DTD                    7      8  0
     D  IDAG                   1      8  0
     D                SDS
     D  ZJOB                 244    253
     D  ZUSER                254    263
     D  ZJOBNR               264    269  0
     D                 DS
     D  PFOT                   1      2
     D  PFKSIF                 1      1
     D  PFKPOS                 2      2
     D                 DS
     D  PFFRAN                 1      3
     D  PFTSIF                 1      1
     D  PFTPOS                 2      2
      *
     C     *ENTRY        PLIST
     C                   PARM                    XXAVGI            8 0
     C                   PARM                    XXAVGK            1
     C                   PARM                    XXMOTT            8 0
     C                   PARM                    XXMOTK            1
     C                   PARM                    XXVIA             1
     C                   PARM                    XXANT             5 0
     C                   PARM                    XXBILN           10
     C                   PARM                    XXBEIE            1
     C                   PARM                    XXTXT            20
     C                   PARM                    UFLAGG            1
     C*
     C* NÅR XXVIA ER UTFYLT (X) BETYR DET AT SUM-TRANSAKAJ. FRA -> TIL
     C* GÅR VIA LAGER-HOLDER (INGEN TRANS DIR.MELLOM 2 EKSTERNE PARTER)
     C* LAGER-HOLDER FORELØPIG HARDKODET MED NR. 1
     C*
      * DAGENS DATO
     C                   MOVE      UDAY          DTD
     C                   MOVE      UMONTH        DTM
     C     UYEAR         ADD       2000          DTÅ
     C*
     C     1             CHAIN(N)  PALFIR                             33
     C     *LOVAL        SETLL     FIRM
     C                   READ      FIRM                                   33
     C     PARKEY        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    YYPART
     C*
      * PARTS-KODE: BLANK, FERDIG PALLE-PARTSNR.
      *             K=KUNDE - SKAP P.PART ETTER REGEL....
      *             T=TRANSO- SKAP P.PART ETTER REGEL....
      * AVGIVER
     C* SIKRE RIKTIG PALLEPARTS-NR / OPPRETTE HVIS NY..
     C                   MOVE      XXAVGI        YYPART            8 0
     C                   MOVE      XXAVGI        PART
     C                   MOVE      XXAVGK        PARTKO            1
     C                   EXSR      SKAPPA
     C                   MOVE      PART          XXAVGI
      * MOTTAKER
     C* SIKRE RIKTIG PALLEPARTS-NR / OPPRETTE HVIS NY..
     C                   MOVE      XXMOTT        YYPART
     C                   MOVE      XXMOTT        PART
     C                   MOVE      XXMOTK        PARTKO
     C                   EXSR      SKAPPA
     C                   MOVE      PART          XXMOTT
      *
     C                   EXSR      LAGPOS
      *
      *
     C                   SETON                                        LR
     C                   RETURN
      *
      ******************************************
     C     LAGPOS        BEGSR
      ******************************************
      * HENT NYTT BUNTNR/LØPENR
     C     1             CHAIN     PALFIR                             33
     C                   MOVE      PFLØNR        PALØNR
     C                   ADD       1             PFLØNR
     C  N33              UPDATE    PALFIRR
     C                   MOVE      'U'           PASTAT
     C                   MOVE      IDAG          PAKVDT
     C                   MOVE      XXBILN        PABILN
     C                   MOVE      XXTXT         PATXT
     C                   MOVE      ZUSER         PAUSER
     C                   MOVE      IDAG          PADATO
      *
      * - SIDE A (AVGIR  PALLER)
     C                   MOVE      'A'           PAAB
     C                   MOVE      XXAVGI        PAPPNR
     C                   MOVE      ' '           PABEIE
     C     XXBEIE        IFEQ      'A'
     C                   MOVE      'X'           PABEIE
     C                   END
     C                   Z-SUB     XXANT         PAANT
     C                   EXSR      JUSSAL
     C                   MOVE      PPNAVN        PAPNVN
     C                   WRITE     PALLDR
      *
      *
      * HVIS VIA LAGER-HOLDER... SAMME BUNTNR NYTT LØPENR
     C     XXVIA         IFNE      ' '
     C     XXVIA         ANDNE     'N'
      * - SIDE B (LAGERHOLDER MOTTAR PALLER I FØRSTE DEL-BILAG)
     C                   MOVE      'B'           PAAB
     C                   Z-ADD     1             PAPPNR
     C                   MOVE      ' '           PABEIE
     C                   Z-ADD     XXANT         PAANT
     C                   EXSR      JUSSAL
     C                   MOVE      PPNAVN        PAPNVN
     C                   WRITE     PALLDR
      * HENT LØPENR FOR SISTE DEL AV BILAGET
     C     1             CHAIN     PALFIR                             33
     C                   MOVE      PFLØNR        PALØNR
     C                   ADD       1             PFLØNR
     C  N33              UPDATE    PALFIRR
      *
      * - SIDE A (LAGERHOLDER AVGIR PALLER I ANDRE DEL-BILAG)
     C                   MOVE      'A'           PAAB
     C                   MOVE      ' '           PABEIE
     C                   Z-SUB     XXANT         PAANT
     C                   EXSR      JUSSAL
     C                   MOVE      PPNAVN        PAPNVN
     C                   WRITE     PALLDR
     C                   END
      *
      *
      * - SIDE B (MOTTAR PALLER)
     C                   MOVE      'B'           PAAB
     C                   MOVE      XXMOTT        PAPPNR
     C                   MOVE      ' '           PABEIE
     C     XXBEIE        IFEQ      'B'
     C                   MOVE      'X'           PABEIE
     C                   END
     C                   Z-ADD     XXANT         PAANT
     C                   EXSR      JUSSAL
     C                   MOVE      PPNAVN        PAPNVN
     C                   WRITE     PALLDR
      *
     C                   ENDSR
      *
      *
      ******************************************
     C     JUSSAL        BEGSR
      ******************************************
     C     PAPPNR        CHAIN     PAPA                               35
     C     *IN35         IFEQ      '0'
     C                   ADD       PAANT         PPSALT
     C                   UPDATE    PAPAR
     C                   END
     C                   ENDSR
      *
      ******************************************
     C     SKAPPA        BEGSR
      ******************************************
      * HVIS KODE ULIK BLANK SKAL OPPGITT NR MANIPULERES FØR SJEKK
      * FRA FIRMAREG: KUNDE:  (PFOT) SETT INN SIFFER X I POS Y (BAKFRA)
      *               TRANSP:(PFFRAN)SETT INN SIFFER X I POS Y (BAKFRA)
      * HVIS UKJENT PALLEPART - SKAP DEN NÅ MED SALDO 0.
      *
      * MANIPULER KUNDENR-> PALLEPARTSNR
     C     PARTKO        IFEQ      'K'
     C     PFKSIF        IFGE      '1'
     C     PFKSIF        ANDLE     '9'
     C     PFKPOS        ANDGE     '4'
     C     PFKPOS        ANDLE     '8'
     C                   MOVE      PFKPOS        NUM1              1 0
     C     9             SUB       NUM1          POS               1 0
     C                   MOVEA     PFKSIF        PA(POS)
     C                   END
     C                   END
      * MANIPULER TRANSPORTØRNR-> PALLEPARTSNR
     C     PARTKO        IFEQ      'T'
     C     PFTSIF        IFGE      '1'
     C     PFTSIF        ANDLE     '9'
     C     PFTPOS        ANDGE     '4'
     C     PFTPOS        ANDLE     '8'
     C                   MOVE      PFTPOS        NUM1              1 0
     C     9             SUB       NUM1          POS               1 0
     C                   MOVEA     PFTSIF        PA(POS)
     C                   END
     C                   END
      *
      * SJEKK OM PARTEN FINNES - HVIS IKKE, OPPRETT NÅ
     C                   MOVE      PART          PAKEY             8 0
     C     PAKEY         CHAIN     PAPA                               35
     C     *IN35         IFEQ      '0'
     C                   UPDATE    PAPAR
     C                   ELSE
     C                   CLEAR                   PAPAR
     C                   MOVE      PAKEY         PPNR
     C                   SELECT
     C     PARTKO        WHENEQ    'K'
     C                   MOVE      YYPART        PPKUNR
     C     PARKEY        CHAIN     CUNDL01                            33
     C  N33              MOVEL     KNAVN         PPNAVN
     C  N33              MOVEL     SONAVN        PPALFA
     C     PARTKO        WHENEQ    'T'
     C     PARKEY        CHAIN     TRAN                               33
     C  N33              MOVE      VMTRKU        PPKUNR
     C  N33              MOVEL     VMNAVN        PPNAVN
     C  N33              MOVEL     VMALFA        PPALFA
     C                   OTHER
     C                   MOVEL     '*UKJENT'     PPNAVN
     C                   MOVEL     '*UKJENT'     PPALFA
     C                   ENDSL
     C                   WRITE     PAPAR
     C                   END
      *
     C                   ENDSR
      *
