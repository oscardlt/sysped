     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      CA03(03 'Avslutt')
     A                                      ALTHELP
     A                                      HELP
      *
     A          R E125D01                   SFL
     A            WSNR           1A  B  6  2VALUES(' ' '5' '8' '9')
     A            MMN            9S 0H
     A            M1004         35A  H
     A            MST            1A  O  6  5
     A  51                                  COLOR(YLW)
     A            XFK            1A  O  6  8
     A            XFN           10A  O  6 12
     A  51                                  COLOR(YLW)
     A            XGMFN         35A  H
     A            XDT            8Y 0O  6 23EDTWRD('    .  .  ')
     A            XTIDW          4Y 0O  6 34EDTWRD('0  :  ')
     A            XFNO          10A  H
     A            XBN           15A  O  6 40
     A            XKON           7A  O  6 56
     A            XDTF           8Y 0O  6 64EDTWRD('    .  .  ')
     A            XNAS          13A  O  7  5DSPATR(UL)
     A            XNAS20        20A  H
     A            XAVD           3  0H
     A            XNAK          17A  O  7 19DSPATR(UL)
     A            XNETTO        11Y 2O  7 37EDTCDE(L)
     A                                      DSPATR(UL)
     A            XMVAT         11Y 2O  7 51EDTCDE(L)
     A                                      DSPATR(UL)
     A            XTOTAL        11Y 2O  7 65EDTCDE(L)
     A                                      DSPATR(UL)
      *
     A          R E125D02                   SFLCTL(E125D01)
     A                                      SFLSIZ(0009)
     A                                      SFLPAG(0008)
     A                                      CA05(05 'Frisk')
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            ZRECNO         4S 0O  1 56SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYEDI/E125R_93A'
     A                                      COLOR(BLU)
     A                                  1 20'Arbeid med faktura/kreditnota'
     A                                      COLOR(WHT)
     A                                  1 62DATE
     A                                      EDTCDE(Y)
     A                                  1 72TIME
     A                                  2  2'Velg opsjon:   5=Se EDIfact   8=Go-
     A                                      dkjent   9=Ikke godgjent'
     A                                      COLOR(BLU)
     A                                  3  2'Op St F/K F/K-nr.    Meld. dato/ti-
     A                                      d   Bestillingsnr.  Ordrenr Forfall-
     A                                      sDT'
     A                                      COLOR(WHT)
     A* 27                              3  2'Op St F/K F/K-nr.    Meld. dato/ti-
     A*                                     d   Gammel faktnr.  Ordrenr Forfall-
     A*                                     sDT'
     A*                                     COLOR(WHT)
     A                                  4  2'   Selgersnavn   Kjøpersnavn      -
     A                                       Netto totall  MVA totall    F/K to-
     A                                      tall  '
     A                                      COLOR(WHT)
     A            W2ST           1A  B  5  5DSPATR(UL)
     A            W2FN          10A  B  5 12DSPATR(UL)
     A                                  5 30'Avdeling:'
     A            W2AVD          3Y 0B  5 40DSPATR(UL)
     A                                      EDTCDE(4)
      *
     A          R E125D03
     A                                      SLNO(23)
     A                                      CLRL(02)
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'F3=Avslutt   F5=Frisk'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  2  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                              2 40'(C) COPYRIGHT  SYSTEMA A/S  1997  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R E125D04
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CA05(05 'Frisk')
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYEDI/E125R_93A'
     A                                      COLOR(BLU)
     A                                  1 20'Arbeid med faktura/kreditnota'
     A                                      COLOR(WHT)
     A                                  1 62DATE
     A                                      EDTCDE(Y)
     A                                  1 72TIME
     A                                  2  2'Velg opsjon:   5=Se EDIfact   8=Go-
     A                                      dkjent   9=Ikke godgjent'
     A                                      COLOR(BLU)
     A                                  3  2'Op St F/K F/K-nr.    Meld. dato/ti-
     A                                      d   Bestillingsnr.  Ordrenr Forfall-
     A                                      sDT'
     A                                      COLOR(WHT)
     A* 27                              3  2'Op St F/K F/K-nr.    Meld. dato/ti-
     A*                                     d   Gammel faktnr.  Ordrenr Forfall-
     A*                                     sDT'
     A*                                     COLOR(WHT)
     A                                  4  2'   Selgersnavn   Kjøpersnavn      -
     A                                       Netto totall  MVA totall    F/K to-
     A                                      tall  '
     A                                      COLOR(WHT)
     A            W2ST           1A  B  5  5DSPATR(UL)
     A            W2FN          10A  B  5 12DSPATR(UL)
     A                                  5 30'Avdeling:'
     A            W2AVD          3Y 0B  5 40DSPATR(UL)
     A                                      EDTCDE(4)
     A                                  7  2'Ingen faktura/kreditnota finnes.'
     A                                      COLOR(RED)
     A                                 23  2'F3=Avslutt   F5=Frisk'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                             24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R E125D10
     A                                      WINDOW(*DFT 9 45 *NOMSGLIN)
     A                                      CA12(12 'Avbryt')
     A                                      OVERLAY
     A                                  1  2'Gi begrunnelse av ikke godkjent'
     A                                      COLOR(WHT)
     A                                  2  2'Overfør til L1?'
     A            WSOVRF         1A  B  2 18DSPATR(UL)
     A                                      VALUES('J' 'N')
     A                                  2 22'(J eller N)'
     A            WSTXT1        40A  B  3  2DSPATR(UL)
     A                                      DSPATR(PC)
     A            WSTXT2        40A  B  4  2DSPATR(UL)
     A            WSTXT3        40A  B  5  2DSPATR(UL)
     A            WSTXT4        40A  B  6  2DSPATR(UL)
     A            WSTXT5        40A  B  7  2DSPATR(UL)
     A                                  8  2'F12=Cancel'
     A                                      COLOR(BLU)
     A  99                              9  2MSGCON(40 YBC0001 SYMSGF)
     A                                      COLOR(WHT)
