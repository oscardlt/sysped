      *
      ************************************************************
      *** PROGRAM SKAL OVERRIDE PRINTERSTYRING
      ************************************************************
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 JOV PTF10 Personlig printer fra SYPARF / USERID
10XXX * 02 SH  PTF10 Personlig labelskriver til 114
10XXX * 03 JOV PTF10 Ved 114=Merkelapp arkiv=liten, primært fra PPMER, eller PPLAB
********************************************************************
     FUTSKRS    IF   E           K DISK
     FKODTFM    IF   E           K DISK
     FKODTP     IF   E           K DISK
N01  FSYPARL2   IF   E           K DISK
     FKODTA     IF   E           K DISK
     FSYGE18FM  CF   E             WORKSTN USROPN
     F                                     INFDS(FBAREA)
      *
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
      * CURSOR LOKASJON AV AKTIV VINDU
     D  WSCLOW               382    383B 0
N01   *___________________________
N01   * Finner username
N01   *"""""""""""""""""""""""""""
N01  D                SDS
N01  D  ZJOB                 244    253
N01  D  ZUSER                254    263
N01  D  ZJOBNR               264    269  0
     D                UDS
     D  UDVAVD                15     18
     D  WSSFSI                19     21
     C*
     C                   EXSR      INIT
     C                   Z-ADD     3             LINNBR
     C   50              Z-ADD     4             LINNBR
     C   50              Z-ADD     14            POSNBR
      *
     C     START         TAG
      *
     C     Skjult        ifne      'J'
     C                   EXFMT     BILDE01
     c                   end
     C                   SETOFF                                       309899
     C     WSCLOW        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C*
     C   03              MOVE      '1'           UIN01
     C   03              GOTO      SLUTT
     C*
     C   04              DO
      *
      * SØK AVDELING
      *
     C     LINNBR        IFEQ      3
     C                   CALL      'SYFA14B'
     C                   PARM                    WSVAVD
     C                   EXSR      TESTAV
     C                   GOTO      START
     C                   END
      *
      * SØK STRINGKODE
      *
     C     LINNBR        IFEQ      6
     C                   MOVE      'S'           MODE              1
     C                   CALL      'SYFA08'
     C                   PARM                    MODE
     C                   PARM                    WSSTRI
     C                   GOTO      START
     C                   END
     C                   MOVE      'SHU0720'     MSGNR
     C                   SETON                                        30
     C                   GOTO      START
     C                   END
      *
      *
     C     *IN50         IFEQ      '0'
     C     WSVAVD        ORNE      FORAVD
     C                   EXSR      TESTAV
     C     Skjult        ifne      'J'
     C                   GOTO      START
     c                   end
     C                   END
     C                   EXSR      TEST
     C     Skjult        ifne      'J'
     C   30              GOTO      START
     C                   close     SYGE18FM
     c                   end
     C                   MOVE      WSVAVD        UDVAVD
     C                   MOVE      WSPRIN        UPRT
     C                   MOVE      WSLASE        ULAS
     C                   MOVE      WSSTRI        USTR
     C                   MOVE      WSPATY        UPTY
     C                   MOVE      '*NO '        UHOLD
     C                   MOVE      '*NO '        USAVE
     C     WSHOLD        IFEQ      'J'
     C     WSHOLD        OREQ      'Y'
     C                   MOVE      '*YES'        UHOLD
     C                   END
     C     WSSAVE        IFEQ      'J'
     C     WSSAVE        OREQ      'Y'
     C                   MOVE      '*YES'        USAVE
     C                   END
     C                   Z-ADD     WSKOPI        UKOPI
     C*
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
      **********************************************************
     C     TESTAV        BEGSR
      **********************************************************
      *
      * TESTER AVDELING
      *
     C     KODTAK        CHAIN     KODTA                              99
     C                   Z-ADD     3             LINNBR
     C                   Z-ADD     14            POSNBR
     C     *IN99         IFEQ      '1'
     C                   MOVE      'SPA0001'     MSGNR
     C                   SETON                                        30
     C                   GOTO      UTT
     C                   END
      *
      * HENTER PRINTERSTYRING
      *

     C     KODTPK        CHAIN     KODTP                              99
     C     *IN99         IFEQ      '1'
     C                   Z-ADD     3             LINNBR
     C                   Z-ADD     14            POSNBR
     C                   MOVE      'SHU0704'     MSGNR
     C                   SETON                                        30
     C                   GOTO      UTT
     C                   END
     C     WSVAVD        IFNE      FORAVD
     C     FORAVD        OREQ      0
     C                   MOVE      KOPNVN        WSPRIN
     C                   MOVE      KOPLAS        WSLASE
     C                   MOVE      KOPFM         WSSTRI
     C                   MOVE      KOPTY         WSPATY
N01   * Ved LABEL kan det være persinlig printer
N01  c                   move      ZUSER         SYUSER
N01  C                   move      'PPLAS'       SYPAID
N03  C     PERSPK        CHAIN     SYPARL2                            33
s02  C*    WSDOKN        IFEQ      67
n02  C     WSDOKN        IFEQ      114
S03  C*  33              move      'PPLAB'       SYPAID
N03  C                   move      'PPMER'       SYPAID
N03  C     PERSPK        CHAIN     SYPARL2                            33
N03  C   33              move      'PPLAB'       SYPAID
N03  C   33PERSPK        CHAIN     SYPARL2                            33
N01  c                   endif
S03  C*    PERSPK        CHAIN     SYPARL2                            33
N01  C     *IN33         IFEQ      '0'
N01  C                   MOVEL     SYVRDA        WSPRIN
N01  c                   endif
     C                   END
     C                   Z-ADD     4             LINNBR
     C                   Z-ADD     14            POSNBR
     C                   MOVE      WSVAVD        FORAVD
     C                   SETON                                        50
     C     UTT           ENDSR
      **********************************************************
     C     TEST          BEGSR
      **********************************************************
     C                   SETOFF                                       989796
     C                   SETOFF                                       959493
     C                   SETOFF                                       92
     C     WSPRIN        COMP      *BLANKS                                98
     C     *IN98         IFEQ      '0'
     C                   CALL      'SYGE00C'
     C                   PARM                    WSPRIN
     C                   PARM      '*LIBL'       LIBL             10
     C                   PARM                    FEILO             1
     C                   PARM      '*OUTQ'       UTKO             10
     C     FEILO         COMP      '0'                                9898
     C                   END
     C   98              Z-ADD     4             LINNBR
     C   98              Z-ADD     14            POSNBR
     C   98              MOVE      'SHU0710'     MSGNR
     C   98              SETON                                        30
     C     WSLASE        IFNE      'J'
     C     WSLASE        ANDNE     'A'
     C     WSLASE        ANDNE     'P'
     C     WSLASE        ANDNE     'F'
     C     WSLASE        ANDNE     ' '
     C  N30              MOVE      'SHU0707'     MSGNR
     C  N30              Z-ADD     5             LINNBR
     C  N30              Z-ADD     14            POSNBR
     C  N30              SETON                                        30
     C                   SETON                                        97
     C                   END
     C     WSSTRI        IFNE      *BLANKS
     C     WSSTRI        CHAIN     KODTFM                             96
     C   96
     CANN30              Z-ADD     6             LINNBR
     C   96
     CANN30              Z-ADD     14            POSNBR
     C   96
     CANN30              MOVE      'SHU0708'     MSGNR
     C   96
     CANN30              SETON                                        30
     C                   END
     C     WSPATY        COMP      *BLANKS                                95
     C   95
     CANN30              Z-ADD     7             LINNBR
     C   95
     CANN30              Z-ADD     14            POSNBR
     C   95
     CANN30              MOVE      'SHU0709'     MSGNR
     C   95
     CANN30              SETON                                        30
     C     WSHOLD        IFNE      'J'
     C     WSHOLD        ANDNE     'Y'
     C     WSHOLD        ANDNE     ' '
     C     WSHOLD        ANDNE     'N'
     C  N30              Z-ADD     8             LINNBR
     C  N30              Z-ADD     14            POSNBR
     C  N30              MOVE      'SHU0712'     MSGNR
     C  N30              SETON                                        30
     C                   SETON                                        94
     C                   END
     C     WSSAVE        IFNE      'J'
     C     WSSAVE        ANDNE     'Y'
     C     WSSAVE        ANDNE     ' '
     C     WSSAVE        ANDNE     'N'
     C  N30              Z-ADD     8             LINNBR
     C  N30              Z-ADD     18            POSNBR
     C  N30              MOVE      'SHU0713'     MSGNR
     C  N30              SETON                                        30
     C                   SETON                                        93
     C                   END
     C     WSKOPI        COMP      0                                      92
     C  N30
     CAN 92              MOVE      'SHU0715'     MSGNR
     C  N30
     CAN 92              Z-ADD     9             LINNBR
     C  N30
     CAN 92              Z-ADD     14            POSNBR
     C  N30
     CAN 92              SETON                                        30
     C                   ENDSR
      **********************************************************
     C     INIT          BEGSR
      **********************************************************
     C     KODTAK        KLIST
     C                   KFLD                    WSAUNI
     C                   KFLD                    WSVAVD
     C                   MOVE      'A'           WSAUNI            1
     C*
     C     KODTPK        KLIST
     C                   KFLD                    WSPUNI
     C                   KFLD                    WSVAVD
     C                   KFLD                    WSDOKN
     C                   MOVE      'P'           WSPUNI            1
      *
N01  c     PERSPK        Klist
N01  C                   kfld                    SYUSER
N01  C                   kfld                    SYPAID
     C*
     C     *ENTRY        PLIST
     C                   PARM                    UDOKN             3 0
     C                   PARM                    UPRT             10
     C                   PARM                    ULAS              1
     C                   PARM                    USTR              5
     C                   PARM                    UPTY             10
     C                   PARM                    UHOLD             4
     C                   PARM                    USAVE             4
     C                   PARM                    UKOPI             2 0
     C                   PARM                    UIN01             1
     C*
     C     UIN01         ifne      'S'
     C                   OPEN      SYGE18FM
     C                   else
     C                   move      'J'           Skjult            1
     C                   end
      *
     C                   MOVE      '0'           UIN01
     C                   MOVE      UDOKN         WSDOKN            3 0
     C     WSDOKN        CHAIN     UTSKRS                             33
     C*
     C                   MOVE      *ZEROS        FORAVD            4 0
     C                   TESTN                   UDVAVD               88
     C   88              MOVE      UDVAVD        WSVAVD
     C   88              EXSR      TESTAV
     C   88
     CANN30              MOVE      UDVAVD        FORAVD            4 0
     C  N88              MOVE      *ZEROS        WSVAVD
     C                   Z-ADD     1             WSKOPI
     C     Skjult        ifne      'J'
     C                   WRITE     WINDOWSZ
     C                   end
     C                   ENDSR
