     A*****************************************************************
     A*   KJØREKVITTERING/OPPDRAGSFORMULAR                            *
     A* ANTALL LINJE=72,  ANTALL TEGN PR. LINJE=132, OVERFLOW=72      *
     A*****************************************************************
     A          R HEADP                     SKIPB(1)
     A            HOHT1         80A        1
     A                                      SPACEB(1)
     A            HOHT2         80A        1
     A                                      SPACEB(1)
     A            HOHT3         80A        1
     A                                      SPACEB(1)
     A            HOHT4         80A        1
     A                                      SPACEB(1)
     A            HOHT5         80A        1
     A                                      SPACEB(1)
     A            HOHT6         80A        1
     A                                      SPACEB(1)
     A            HOHT7         80A        1
     A                                      SPACEB(1)
     A          R HEAD0                     SKIPB(1)
     A                                     1' '
     A          R HEAD1A                    SPACEB(2)
     A                                     2MSGCON(9 DIV4021 SYMSGF)
     A            TUBILN        10A       15
     A                                    48MSGCON(8 DIV4044 SYMSGF)
     A            TUSJ20        20A       60
     A          R HEAD1B
     A                                     2MSGCON(9 DIV4021 SYMSGF)
     A            TUBILN        10A       15
     A                                    48MSGCON(8 DIV4044 SYMSGF)
     A            TUSJ20        20A       60
     A*
     A                                     2MSGCON(12 DIV4088 SYMSGF)
     A                                      SPACEB(1)
     A            TUNAT         30A       15
     A*
     A*
     A          R HEAD1C
     A                                     2MSGCON(12 DIV4088 SYMSGF)
     A            TUNAT         30A       15
     A                                    22MSGCON(11 DIV4043 SYMSGF)
     A                                      SPACEB(2)
     A            TUHENG        10A       35
     A*
     A                                     2MSGCON(8 DIV4087 SYMSGF)
     A                                      SPACEB(1)
     A            TUSTEF         5A       11
     A                                    16'/'
     A            TUSONF         5A       17
     A            TUSDF         20A       23
     A                                    44'->'
     A            TUSTET         5A       46
     A                                    51'/'
     A            TUSONT         5A       52
     A            TUSDT         20A       58
     A                                     2MSGCON(8 DIV4045 SYMSGF)
     A                                      SPACEB(1)
     A            DATOFR         6S 0     11EDTCDE(Y)
     A                                    22MSGCON(11 DIV4046 SYMSGF)
     A            TUAVD          4S 0     35
     A                                    40'/'
     A            TUPRO          8S 0     42
     A                                    52MSGCON(11 DIV4055 SYMSGF)
     A            TURUND         8S 0     65
     A                                     2MSGCON(13 DIV4047 SYMSGF)
     A                                      SPACEB(1)
     A            TINR01        15A       15
     A                                    30'/'
     A            TINR02        15A       31
     A                                    46'/'
     A            TINR03        15A       47
     A                                    62'/'
     A            TINR04        15A       63
     A            TINR05        15A       15
     A                                      SPACEB(1)
     A                                    30'/'
     A            TINR06        15A       31
     A                                    46'/'
     A            TINR07        15A       47
     A                                    62'/'
     A            TINR08        15A       63
     A                                     2MSGCON(20 DIV4048 SYMSGF)
     A                                      SPACEB(2)
     A          R DETT                      SPACEB(1)
     A            FRTTXT        70A        2
     A          R HEADFE                    SPACEB(2)
     A                                     2MSGCON(79 DIV4049 SYMSGF)
     A          R HEADF2                    SPACEA(1)
     A                                     2MSGCON(79 DIV4049 SYMSGF)
     A          R DETFE
     A            FEDATP         6S 0     14EDTCDE(Y)
     A            FETIME         4S 0     28
     A            FEFROM         5A       40
     A            FETO           5A       48
     A            FETREF         7S 0     64
     A          R DETFB                     SPACEB(1)
     A N33N34     LNAVN         30         2SPACEA(1)
     A                                     2MSGCON(6 DIV4050 SYMSGF)
     A                                     9MSGCON(5 DIV1296 SYMSGF)
     A                                    23MSGCON(4 DIV4051 SYMSGF)
     A                                    33MSGCON(6 DIV4052 SYMSGF)
     A                                    46 '->'
     A                                    54MSGCON(9 DIV4053 SYMSGF)
     A          R HEADSJ                    SPACEB(2)
     A                                     2MSGCON(15 DIV3776 SYMSGF)
     A            TUSJA1         8S 0     18EDTCDE(Z)
     A            TUSJN1        30A       28
     A                                     2MSGCON(15 DIV3777 SYMSGF)
     A                                      SPACEB(2)
     A            TUSJA2         8S 0     18EDTCDE(Z)
     A            TUSJN2        30A       28
     A          R HEADRE                    SPACEB(2)
     A                                     2MSGCON(70 DIV4054 SYMSGF)
     A                                     2MSGCON(70 PRI9999 SYMSGF)
     A          R DETRE                     SPACEB(1)
     A            RESJ           8        18
     A            RENBR          8        28
     A            REVAL          3A       38
     A            REBEL         13S 2     41EDTCDE(J)
