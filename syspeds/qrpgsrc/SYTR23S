********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 SH  PTF10 feil ved kun tillegg  ..
10XXX * 02 SH  PTF12 feil ved feil i bupÅÅR
********************************************************************
      *
      **************************************************************
      *** Beregner summer for transportør hittil iår             ***
      **************************************************************
     FWRKTAVR   UF a E           K DISK
     FTRAVH5    IF   E           K DISK
     FTRAVV5    IF   E           K DISK
     FFAK3      IF   E           K DISK
     FFIRM      IF   E           K DISK
     Fkosbu     IF   E           K DISK
     C*
     C                   EXSR      INIT
      *
     c                   setoff                                       33
      *
      *    finn siste lest og deretter leses TRAVH
      *    legger her første i inneværende år
      *
     c     key           setll     travh5
     c     key           reade     travh5                                 33
     c     *in33         ifeq      '0'
     c                   move      thfakt        siste             7 0
     C                   exsr      lestravh
     C                   end
      *
      *
      *    leser poster i travv for direkteføring
      *
     c                   exsr      lestravv
      *
     c                   seton                                        lr
     c                   return
     c
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C*
     C     *ENTRY        PLIST
     C                   PARM                    UTNR              8 0          *ALPHA
     C                   PARM                    UAAR              2 0
     C                   PARM                    UFAKT             7 0
      *
     C                   exsr      tomm
     C     key           KLIST
     C                   kfld                    THTNR
     C                   kfld                    THÅR
      *
     C     keytravv      KLIST
     C                   kfld                    tvtavd
     C                   kfld                    tvtunr
      *
      *
     C     keyfak3a      KLIST
     C                   kfld                    stat1             1
     C                   kfld                    tvfakt
     C                   move      'O'           stat1
      *
     C     keyfak3b      KLIST
     C                   kfld                    stat2             1
     C                   kfld                    tvfakt
     C                   move      'G'           stat2
      *
     C     keyfak3c      KLIST
     C                   kfld                    stat3             1
     C                   kfld                    tvfakt
     C                   move      'F'           stat3
      *
     c                   move      utnr          thtnr
     c                   move      uaar          thÅr
     c                   move      utnr          tvtunr
      *
     c                   read      firm                                   34
     C                   ENDSR
     C***********************************************
     C     tomm          BEGSR
     C***********************************************
     c     *loval        setll     wrktavr
     c                   setoff                                       33
     c     *in(33)       doweq     '0'
     c                   read      WRKTAVR                                33
     c     *in(33)       ifeq      '0'
     c                   delete    wrktavrr
     c                   end
     c                   end
     c                   move      *zeros        umva
     c                   move      *zeros        mva
     c                   move      *zeros        mmva
     c                   move      *zeros        tot
     c                   setoff                                       33
     c                   endsr
     C***********************************************
     C     lestravv      BEGSR
     C***********************************************
      * For å lese tilleggslinjer
      *
     c                   setoff                                       35
     c     keytravv      setll     travv5
      *
      *
     c     *in(35)       DOWEQ     '0'
     C     keytravv      READE     TRAVV5                                 35
      *
     C     *IN(35)       IFEQ      '0'
     c     tvfakt        andle     ufakt
      *
      * hent periode og momskode fra fakturafil
      *
     c                   move      'J'           fakdm
     c     keyfak3a      chain     fak3                               36
     c   36keyfak3b      chain     fak3                               36
     c   36keyfak3c      chain     fak3                               36
      *
      * henter mva fra kosbu hvis kobling finnes
      *
     c     tvbnrb        ifne      *zeros
     C     TVBNRB        CHAIN     KOSBU                              40
     c     *in40         ifeq      '0'
     c     bukdm         ifeq      ' '
     c                   move      'J'           fakdm
     c                   else
     c                   move      bukdm         fakdm
     c                   end
N01  C     *IN36         IFEQ      '1'
s02  C*                  Z-ADD     BUPÅR         FAÅR
n02  C                   Z-ADD     UAAR          FAÅR
N01  C                   SETOFF                                       36
N01  c                   z-add     tvfakt        fafakt
N01  C                   END
     c                   end
     c                   end
      *
      * hvis justering på siste er den ikke med i fakturafil enda
      *
s02  c*  36ufakt         ifeq      tvfakt
s02  c*                  z-add     tvfakt        fafakt
s02  C*                  Z-ADD     uaar          FAÅR
s02  c*                  setoff                                       36
s02  c*                  end
      *
      * ta kun med inneværende år
      *
     c     *in36         ifeq      '0'
     c     faÅR          andeq     uaar
     c                   clear                   wrktavrr
     c                   move      fafakt        wrfakt
     c     wrfakt        chain     wrktavr                            37
      *
     c     fakdm         ifeq      'N'
      * uten mva
     c                   add       tvbll         umva
     c                   add       tvbll         tot
     c                   else
      * med mva
     c                   add       tvbll         mmva
     C                   MOVE      FITAX         FITAXB            4 4
      *
     C     FITAXD        IFNE      *ZEROS
     C     FITAX2        ANDNE     *ZEROS
     C     fadato        ANDLT     FITAXD
     c     fadato        andne     0
     C                   MOVE      FITAX2        FITAXB            4 4
     C                   END
     C*
     c     tvbll         mult      fitaxb        mvav             13 2
     c                   add       tvbll         tot
     c                   add       mvav          tot
     c                   add       mvav          mva
     c                   end
      *
     c   37              write     wrktavrr
     c  N37              update    wrktavrr
     c                   move      *zeros        umva
     c                   move      *zeros        mva
     c                   move      *zeros        mmva
     c                   move      *zeros        tot
     c                   end
     c                   end
     c                   end
     C                   ENDSR
      *
     C***********************************************
     C     lestravh      BEGSR
     C***********************************************
      *
     c     key           setll     travh5
      *
     c     *in(33)       doweq     '0'
      *
      * les til fakturanummer som programmet er kallt med
      *
     c     key           reade     travh5                                 33
     c  N33thfakt        comp      ufakt                              33
      *
      * brudd på fakturanummer eller siste lest i fil
      *
     c     thfakt        ifne      siste
     c     *in33         oreq      '1'
     c                   exsr      brutravh
     c                   end
      *
      * detaljbehandling
      *
     c  N33thst          ifne      ' '
     c     thrtnr        andne     99999998
     c                   exsr      dettravh
     c                   end
      *
     c                   end
     c                   endsr
     C*
     C******************************************************************
     C     brutravh      BEGSR
     C******************************************************************
      *
      * MOMSREGEL
      *
     C                   MOVE      FITAX         FITAXB            4 4
     C     FITAXD        IFNE      *ZEROS
     C     FITAX2        ANDNE     *ZEROS
     C     lesdat        ANDLT     FITAXD
     C                   MOVE      FITAX2        FITAXB            4 4
     c                   end
      *
     c     mmva          mult      fitaxb        mva
     c     mmva          add       umva          tot
     c     tot           add       mva           tot
     c     tot           ifne      0
     c                   z-add     siste         wrfakt
     c                   write     wrktavrr
     c                   end
     c                   move      *zeros        umva
     c                   move      *zeros        mva
     c                   move      *zeros        mmva
     c                   move      *zeros        tot
      *
     C                   ENDSR
      *
     C*
     C******************************************************************
     C     dettravh      BEGSR
     C******************************************************************
      *
      *
     c                   move      thfakt        siste             7 0
     c                   move      thdato        lesdat            8 0
      *
      * beregner hittil
     c     thkdm         ifeq      'J'
     c                   add       thtot         mmva
     c                   else
     c                   add       thtot         umva
     c                   end
     C                   ENDSR
      *
      *
