     A*****************************************************************
     A*   KJØREKVITTERING/OPPDRAGSFORMULAR                            *
     A* ANTALL LINJE=72,  ANTALL TEGN PR. LINJE=80, OVERFLOW=72       *
     A*****************************************************************
     A          R KJØP
NV5  A            KFMT1         75A     1  1
NV5  A            KFMT2         75A     1  1
     A            HOHT1         80A     1  1
     A            HOHT2         80A     2  1
     A            HOHT3         80A     3  1
     A            HOHT4         80A     4  1
     A            HOHT5         80A     5  1
     A            HOHT6         80A     6  1
     A            HOHT7         80A     7  1
     A                                  9  2MSGCON(9 DIV0197 SYMSGF)
     A            HENAK         30     10 13
     A                                 10 50'******************************'
     A                                      HIGHLIGHT
     A            HEADK1        30     11 13
     A                                 11 50'* MELDING OM OPPKRAVSSENDING *'
     A                                      HIGHLIGHT
     A            HEADK2        30     12 13
     A                                 12 50'******************************'
     A                                      HIGHLIGHT
     A            HEADK3        30     13 13
     A                                 15  2'Tlf/Faxnr:'
     A            KTLF          15     15 13
     A                                 15 29'/'
     A            KFAXNR        15     15 31
     A                                 15 50MSGCON(5 DIV0062 SYMSGF)
     A            UDT1           2  0  15 65
     A                                 15 67'.'
     A            UDT2           2  0  15 68
     A                                 15 70'.'
     A            UDT3           2  0  15 71
     A                                 16  2MSGCON(9 DIV0200 SYMSGF)
     A                                 16 50MSGCON(9 DIV0201 SYMSGF)
     A            HEAVD          4  0  16 60
     A                                 16 64'/'
     A            HEOPD          7  0  16 65
     A                                 16 72'/'
     A            HESG           3     16 73
     A            HENAS         30     17 13
     A                                 17 50'Snd.nr:'
     A            HXSNDN        20     17 58
     A            HEADS1        30     18 13
     A            HEADS2        30     19 13
     A            HEADS3        30     20 13
     A                                 22  2'Fra avsender angitt ovenfor er'
     A                                      HIGHLIGHT
     A                                    +1'følgende varer ankommet: '
     A                                      HIGHLIGHT
     A                                 24  1MSGCON(77 DIV1679 SYMSGF)
     A N78                                  UNDERLINE
     A            HENT           7  0  25  1EDTCDE(4)
     A            HEVS1         25A    25  9
     A            HEVKT          9  0  25 35EDTCDE(4)
     A            HEM3           7  3  25 46EDTCDE(4)
     A            HEFBV          9  0  25 56EDTCDE(4)
     A            HEGM1         15A    25 66
     A            HEVS2         25A    26  9
     A                                 26 48'LM:'
     A            HELM           4  2  26 56EDTCDE(4)
     A            HEGM2         15A    26 66
     A                                 26  1'________________________'
     A                                 26 25'________________________'
     A                                 26 49'________________________'
     A                                 26 73'_______ '
     A
     A                                 28  2'For utlevering av godset vennligst'
     A                                      HIGHLIGHT
     A                                    +1'ta kontakt med '
     A                                      HIGHLIGHT
     A            KNAVN         30     29  2HIGHLIGHT
     A                                 29 34'på tlf.'
     A                                      HIGHLIGHT
     A            TLF           15     29 42HIGHLIGHT
     A                                 29 58'el.fax'
     A                                      HIGHLIGHT
     A            TFAXNR        15     29 65HIGHLIGHT
     A                                 31  2'Innbetaling av etterkravet må skje'
     A                                      HIGHLIGHT
     A                                    +1'i bank/eller post før varen blir'
     A                                      HIGHLIGHT
     A                                    +1'utlevert.'
     A                                      HIGHLIGHT
     A                                 32  2'Som kvittering godtas kun kassa-st'
     A                                      HIGHLIGHT
     A                                    +0'empel eller bekreftelse fra bank'
     A                                      HIGHLIGHT
     A                                 33  2'på at avsenders konto er belastet.'
     A                                      HIGHLIGHT
     A                                 35  2'Varene ligger fritt på lager i 3 d'
     A                                      HIGHLIGHT
     A                                    +0'ager.'
     A                                      HIGHLIGHT
     A                                    +1'Etter dette påløper lagerleie.'
     A                                      HIGHLIGHT
     A
     A                                 37  2MSGCON(24 YBC0491 SYMSGF)
     A                                      HIGHLIGHT
     A            HEFT1A        30     37 27HIGHLIGHT
BHR  A  75                             37 58'Herav MVA:'
BHR  A                                      HIGHLIGHT
BHR  A  75        XMOMS          8  2  37 69EDTCDE(3)
BHR  A                                      HIGHLIGHT
     A                                 38 27'Betales til ktonr:'
     A                                      HIGHLIGHT
     A            HEFT1B        15     38 46HIGHLIGHT
BHR  A  75                             39 27'BELØPET OMFATTER INNFØRSELS-MVA ET'
BHR  A                                      HIGHLIGHT
BHR  A  75                                +0'C.'
BHR  A                                      HIGHLIGHT
BHR  A  75                             40  2'Med mindre annet avtales, ber vi o'
BHR  A                                      HIGHLIGHT
BHR  A  75                                +0'm at vedlagte giro-blankett'
BHR  A                                      HIGHLIGHT
BHR  A  75                             41  2'benyttes ved innbetaling av etterk'
BHR  A                                      HIGHLIGHT
BHR  A  75                                +0'ravet.'
BHR  A                                      HIGHLIGHT
BHR  A
BHR  A          R GIRO
BHR  A            TOTBEL        13  2  45 30EDTCDE(J)
BHR  A                                 48  7'Innbetaling av etterkrav.'
BHR  A                                      HIGHLIGHT
BHR  A                                 49  7MSGCON(5 DIV0062 SYMSGF)
BHR  A            XDT1           2  0  49 17
BHR  A                                 49 19'.'
BHR  A            XDT2           2  0  49 20
BHR  A                                 49 22'.'
BHR  A            XDT3           2  0  49 23
BHR  A                                 50  7MSGCON(9 DIV0201 SYMSGF)
BHR  A            XXAVD          4  0  50 17
BHR  A                                 50 21'/'
BHR  A            XXOPD          7  0  50 22
BHR  A                                 50 29'/'
BHR  A            XXSG           3     50 30
BHR  A                                 51  7'Snd.nr:'
BHR  A            XXSNDN        20     51 17
BHR  A            XXNAK         30     56  7
BHR  A            KNAVN         30     56 46
BHR  A            XXADK1        30     57  7
BHR  A            ADR1          30     57 46
BHR  A            XXADK2        30     58  7
BHR  A            ADR2          30     58 46
BHR  A            XXADK3        30     59  7
BHR  A            POSTNR         4  0  59 46
BHR  A            ADR3          24     59 52
BHR  A                                 65  1'H'
BHR  A            TOTKR         11  0  65 29EDTCDE(3)
BHR  A                                      HIGHLIGHT
BHR  A            TOTØRE         2  0  65 42HIGHLIGHT
BHR  A            BANKG         15     65 52HIGHLIGHT
