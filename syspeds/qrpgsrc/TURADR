********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 SH  PTF10 mindre retting adr1   ..
10XXX * 02 JOV PTF10 Fr-brev kan være DUPPet, kolliID på DUPen  mm
10XXX * 03 JOV PTF10 F11 er bilde for turoversikt
10XXX * 04 JOV PTF10 IKKE TEI MEV Z.. VED KALL PÅ T&T PÅ TURNIVÅ
10XXX * 05 SH  PTF10 ny parameter sytt01
10XXX * 06 YBC PTF10 TEST OM SIGNATUR ER GYLDIG
********************************************************************
********************************************************************
      * Program for å arbeide med IMPORT-TUR , adressekontroll mm
********************************************************************
     FHEADF     UF   E           K DISK
     FHEAD11    IF   E           K DISK
     F                                     RENAME(HEADFR:HEAD11R)
     FHEAD17    IF   E           K DISK
     F                                     RENAME(HEADFR:HEAD17R)
     FDOKUF     UF   E           K DISK
     FFIRM      IF   E           K DISK
     FTrackL02  IF   E           K DISK
     FPONRN     IF   E           K DISK
     FKODTA     IF   E           K DISK
N06  FKODTSF    IF   E           K DISK
     FTURADRFM  CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
N02  D                 DS
N02  D  DFBREF                 1     17
N02  D  WSBRTX                 1      5
N02  D  WSBRAV                 6      9
N02  D  WSBRST                10     10
N02  D  WSBROP                11     17
NRFA D                 DS
NRFA D  WSMAWB35               1     35
NRFA D  WSMAWB                 1     15
     D                 DS
     D  HEFT1                  1     51
     D  HEFT1A                 1     30
     D  HEFT1B                31     45
     D                 DS
     D  HENAS                  1     30
     D  HENASX                 1      8
     D                 DS
     D  DFADUL                 1     30
     D  DFST13                 1     13
     D                 DS
     D  DFNT                   1      7  0
     D  DFNT3                  5      7  0
     D                 DS
     D  DFVKT                  1      9  0
     D  DFVKT3                 7      9  0
     D                 DS
     D  DFNAT                  1     20
     D  DFNAT5                 1      5
     D                UDS
     D  UFIRM                  1      2
     D  UAVD                  15     18  0
     D  USG                   19     21
     C                   EXSR      INIT
     c     WSTUR         cabne     *zeros        Start0
      *
     C     VALGTAG       TAG
     C                   EVAL      *IN03=*OFF
     C                   EVAL      *IN12=*OFF
     C                   EXFMT     VALG
     C   03              GOTO      SLUTT
     C   12              GOTO      SLUTT
      *
     C     Start0        tag
     c                   movea     '00'          *in(71)
     c                   move      '0'           *in(30)
     c     WSTUR         IFEQ      *zeros
     c     WSMAWB        andne     *blanks
     c*    WSMAWB        chain     Head17                             71
     c     WSMAWB35      chain     Head17                             71
     c     *in71         ifeq      '1'
     C                   MOVE      *ON           *IN30
     C                   MOVE      'JOV1473'     MSGNR
     c                   goto      ValgTag
     c                   end
     c                   movel     HEPRO         WSTUR
     c                   end
     C*
     c     WSTUR         comp      *zeros                                 72
     c  N72WSTUR         chain     Head11                             72
     c     *in72         ifeq      '1'
     C                   MOVE      *ON           *IN30
     C                   MOVE      'SPT0017'     MSGNR
     c                   goto      ValgTag
     c                   end
     C*
     C* OK tur angitt / vis oppdrag...
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     2             POSNBR
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
      * Sett ind, for underutvalg  (*IN50 brukes i DSPF)
     C                   MOVE      '0'           *IN50                          EKSKLUDERT??
     C                   MOVE      'N'           UNDUTV            1
     C                   MOVE      'J'           INCL              1
     C     W2AVD         IFNE      *ZEROS
     C     W2OPD         ORNE      *ZEROS
     C     W2TRAN        ORNE      *ZEROS
N04  C     W2AGE         ORNE      *ZEROS
     C                   MOVE      'J'           UNDUTV
     C                   MOVE      ' '           INCL
     C                   END
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     WSTUR         SETLL     HEAD11
     C*
     C     WSTUR         READE     HEAD11                                 94
     c  N94              movel     HERFA         WSMAWB
     C     *IN94         DOWEQ     '0'
     C     DOKKEY        CHAIN(N)  DOKUF                              33
     C     UNDUTV        IFEQ      'J'
     C                   EXSR      INCLUD
     C                   END
     C     INCL          IFEQ      'J'
     C                   EXSR      TSTSUBF
     C                   ADD       1             ZRECNO
     C                   WRITE     SUBFIL
     C                   ELSE
     C                   MOVE      '1'           *IN50                           EKSKLUDERT
     C                   END
     C     WSTUR         READE     HEAD11                                 94
     C  N94              END
      *
     C                   MOVE      'J'           FRTOPP            1
      * Blank Underutvalg
     C*** behold         EXSR      MOBLW2
      *
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C*                  Z-ADD     6             LINNBR
     C*                  Z-ADD     2             POSNBR
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYWRK
     C* Ingen underutvalg, likevel ingen poster ->FORIGE uansett tast..
     C     *IN50         IFEQ      '0'
     C                   MOVE      '1'           *IN03
     C                   GOTO      SLUTT
     C                   END
     C                   ELSE
     C     FRTOPP        IFEQ      'J'
     C                   Z-ADD     1             ZRECNO
     C                   MOVE      'N'           FRTOPP            1
     C                   END
     C                   WRITE     SUBCTL
     C                   EXFMT     BUNN
     C                   END
     C*
     C                   SETOFF                                       9899
     C*** AVSLUTT
     C   03              GOTO      SLUTT
     C*
     C*** FRISK OPP
     C   05              GOTO      START
     C*
     C*** Underutvalg
     C     *IN08         IFEQ      '1'
     C                   EXFMT     VAREL2
     C   12              GOTO      START3
     C                   GOTO      START
     C                   END
     C*** Bytt signatur
     C     *IN09         IFEQ      '1'
     c                   move      USG           W3SG
N06  C     STBYTTESG     TAG
     C                   EXFMT     VAREL3
N06  C                   MOVE      '0'           *IN99
N06  C                   MOVE      '0'           *IN30
     C   12              GOTO      START3
N06  C     KODTSFK       CHAIN     KODTSF                             99
N06  C                   IF        *IN99
N06  C                   MOVE      'SPS0001'     MSGNR
N06  C                   MOVE      '1'           *IN30
N06  C                   GOTO      STBYTTESG
N06  C                   END
     c                   exsr      Byttsign
     C                   GOTO      START
     C                   END
      * Track&Trace på turnivå (normalt TEI)
     C     *IN10         IFEQ      '1'
     C                   EXSR      SRTTT
     C                   GOTO      START
     C                   END
     C*** Forrige
     C   12              GOTO      VALGTAG
     C*
     C*
     C     ZRECNO        IFNE      0
     C                   EXSR      HENTSR
     C                   END
      * Enter - også uten utført valg - vis samme bilde/poster
     C                   GOTO      START3
     C*
     C*
     C*
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
     C*

     C***********************************************
     C     Byttsign      BEGSR
     C***********************************************
     c     W3SG          cabeq     *blanks       UtBsg
     C*
     C     WSTUR         SETLL     HEAD11
     C     WSTUR         READE     HEAD11                                 94
     C     *IN94         DOWEQ     '0'
     C     HEAKEY        CHAIN     HEADF                              33
     c  N33              Move      W3SG          HESG
     c  N33              update    HEADFR
     C     DOKKEY        CHAIN     DOKUF                              33
     c  N33              Move      W3SG          DFSG
     c  N33              update    DOKUFR
     C     WSTUR         READE     HEAD11                                 94
     C  N94              END
      *
     c     UtBsg         endsr
     C***********************************************
     C     TSTSUBF       BEGSR
     C***********************************************
     C                   MOVEA     '00000'       *IN(55)
     C* FEIL UTLEV-POSTNR, HIGHLIGHT POSTNR
     C                   MOVE      ' '           TSTTYP            1
     C                   MOVE      DFPOUL        TSTPOUL           4
     C                   MOVE      '0'           TSTPORES          1
     C                   CALL      'TSTPNR'
     C                   PARM                    TSTTYP
     C                   PARM                    TSTPOUL
     C                   PARM                    TSTPORES
     C                   MOVE      TSTPORES      *IN55
     C* UTLEV-FORBEHOLD - HIGHLIGHT MOTTAKER
     C     HEFT1A        IFNE      *BLANKS                                       FORBEH...
     C     HEKFT1        ANDEQ     *BLANKS                                       IKKE KVITTE
     C                   MOVE      *ON           *IN56
     C                   END
     C* farge oppdrnr  Finnes...  POD=ingen, TEU= Grønn, TEI=Gul, Ingen=RØD
     C                   movea     '000'         *IN(61)
     C                   move      'POD'         TTACTI
     c     TrackKey      Chain     TrackL02                           33
     c     *in33         ifeq      *on
     C                   move      'TEU'         TTACTI
     c     TrackKey      Chain     TrackL02                           33
     C  N33              movea     '001'         *IN(61)                         TEU = Grønn
     c     *in33         ifeq      *on
     C                   move      'TEI'         TTACTI
     c     TrackKey      Chain     TrackL02                           33
     C  N33              movea     '010'         *IN(61)                         TEI = Gul
     C   33              movea     '100'         *IN(61)                         Ingen=Rød
     c                   end
     c                   end
     C                   ENDSR
     C*
     C***********************************************
     C     INCLUD        BEGSR
     C***********************************************
     C                   MOVE      'J'           INCL
      *
     C     W2AVD         IFNE      *ZEROS
     C     W2AVD         ANDNE     DFAVD
     C                   MOVE      'N'           INCL
     C                   GOTO      ENDINC
     C                   END
     C     W2OPD         IFNE      *ZEROS
     C     W2OPD         ANDNE     DFOPD
     C                   MOVE      'N'           INCL
     C                   GOTO      ENDINC
     C                   END
     C     W2tran        IFNE      *ZEROS
     C     W2tran        ANDNE     DFtran
     C                   MOVE      'N'           INCL
     C                   GOTO      ENDINC
     C                   END
N11  C     W2age         IFNE      *ZEROS
N11  C     W2age         ANDNE     HEKNA
N11  C                   MOVE      'N'           INCL
N11  C                   GOTO      ENDINC
N11  C                   END
     C*
     C     ENDINC        ENDSR
     C*
     C***********************************************
     C     HENTSR        BEGSR
     C***********************************************
     C     WSCLOC        DIV       256           LINNBR            3 0
     C                   MVR                     POSNBR            3 0
     C                   move      LINNBR        GMLNBR            3 0
     C                   move      POSNBR        GMPNBR            3 0
     C                   SETOFF                                       30
     C                   SETOFF                                       919299
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C   98              DO
     C   99              SETOFF                                       98
     C                   GOTO      SLHENT
     C                   END
     C     WSNR          IFEQ      '2'                                          SLETTE
     C                   MOVE      ' '           WSNR
     C                   EXSR      CHGSI
     C   12              GOTO      SLHENT
     C                   END
      * Vis fraktbrev
     C     WSNR          IFEQ      '5'
     C                   MOVE      ' '           WSNR
     C                   CALL      'SYFM21C'
     C                   PARM                    FIFIRM
     C                   PARM                    DFAVD
     C                   PARM                    DFOPD
     C                   PARM      1             UFBNR             3 0
     C                   PARM      0             UFLAGG            1 0
     C                   SETON                                        99
     C                   EXSR      TSTSUBF
     C                   UPDATE    SUBFIL
     C                   END
      * Vedlikehold av oppdrag
     C     WSNR          IFEQ      '6'
     C     HEAKEY2       CHAIN(N)  HEADF

     c                   select
     c     HEUR          wheneq    'A'
     C                   MOVE      ' '           WSNR
     C                   CALL      'SYFA10'
     C                   PARM                    DFAVD
     C                   PARM                    DFOPD
     C                   PARM                    HESG              3
     C                   PARM      'CHG'         UTYPE             3
     C                   PARM      ' '           UKODE             1
     C                   PARM      ' '           UDUMMY            1
     C                   PARM      0             UAVDNY            4 0
     c     HEUR          wheneq    'C'
     C                   MOVE      ' '           WSNR
     C                   CALL      'SYFI10'
     C                   PARM                    DFAVD
     C                   PARM                    DFOPD
     C                   PARM                    HESG              3
     C                   PARM      'CHG'         UTYPE             3
     C                   PARM      ' '           UKODE             1
     C                   PARM      ' '           UDUMMY            1
     C                   PARM      0             UAVDNY            4 0
     c                   endsl
     C     HEAKEY2       CHAIN(N)  HEADF
     C                   EXSR      TSTSUBF
     C                   SETON                                        99
     C                   UPDATE    SUBFIL
     C                   END
     C     WSNR          IFEQ      '7'                                          SLETTE
     C                   MOVE      ' '           WSNR
     C                   EXSR      CHGFBR
     C   12              GOTO      SLHENT
     C                   END
      * Vis på web
     C     WSNR          IFEQ      'W'
     C                   MOVE      ' '           WSNR
     C                   exsr      DSPWEB
     C                   END
      * Print kopi av merkelapp
     C     WSNR          IFEQ      'M'                                          SLETTE
     C                   MOVE      ' '           WSNR
     C                   EXSR      PrtMlapp
     C   30              GOTO      SLHENT
     C                   END
      * Se T&Tlogg på oppdragsnivå
     C     WSNR          IFEQ      'L'                                          SLETTE
     C                   EXSR      SRTT
     C                   END
N02   * Se Kolli
N02  C     WSNR          IFEQ      'K'
N02  C                   EXSR      SRKLL
N02  C                   END
     C*
     C                   END
     C*
     C     SLHENT        TAG
     C                   move      GMLNBR        LINNBR
     C                   move      GMPNBR        POSNBR
     C*
     C                   ENDSR
      ***********************************************
     C     DSPWEB        BEGSR
      ***********************************************
N02  c                   move      DFAVD         HEAVD
N02  c                   move      DFOPD         HEOPD
     C                   SETON                                        99
     C                   EVAL      Overskr = 'Se SYSPED oppdrag på WEB (eSped) '
     C                   EVAL      WebType = 'ESPEO'
     C                   move      DFAVD         HEAVDA            4
     C                   move      DFOPD         HEOPDA            7
     C                   EVAL      WebSok  = '&heavd='+HEAVDA+
     C                             '&heopd='+HEOPDA
     C                   CALL      'WEBLINKR'
     C                   PARM                    Overskr          40
     C                   PARM                    WebType           5
     C                   PARM                    WebSok           50
      *
     C     HEAKEY2       CHAIN(N)  HEADF                              33
     C     DOKKEY        CHAIN(N)  DOKUF                              34
     C                   EXSR      TSTSUBF
     C                   UPDATE    SUBFIL
      *
     C                   ENDSR
      *
      ***********************************************
     C     PrtMlapp      BEGSR
      ***********************************************
      *
N04  C                   SETON                                        99
N02  c                   move      DFAVD         HEAVD
N02  c                   move      DFOPD         HEOPD
s02  C**   DOKKEY        CHAIN(N)  DOKUF                              34
s02  C**   WSBRTX        ifeq      'U.op:'
s02  c**                 move      WSBRAV        DFAVD
s02  c**                 move      WSBROP        DFOPD
s02  c**                 endif
     C                   CALL      'A13LABR'
     C                   PARM      '*GR       '  XXUSER           10
     C                   PARM                    DFAVD
     C                   PARM                    DFOPD
     C                   PARM                    DFFBNR
     C                   PARM                    XXANKK            4 0
      *
N02   * DFavd/opd kan være ødelagt av U.op.
N02  c                   move      HEAVD         DFAVD
N02  c                   move      HEOPD         DFOPD
     C     HEAKEY2       CHAIN(N)  HEADF                              33
     C     DOKKEY        CHAIN(N)  DOKUF                              34
     C                   EXSR      TSTSUBF
     C                   UPDATE    SUBFIL
      *
     C                   ENDSR
      *
      ***********************************************
     C     SRTT          BEGSR
      ***********************************************
      * T&T å oppdragsnivå.
N04  C                   SETON                                        99
N02  c                   move      DFAVD         HEAVD
N02  c                   move      DFOPD         HEOPD
     C                   SETON                                        99
     C                   Z-ADD     1             TTFLAG            1 0
N04  C                   Z-ADD     0             TTTUR             8 0
     C                   CALL      'SYTT01CL'
     C                   PARM                    DFAVD
     C                   PARM                    DFOPD
N04  C                   PARM                    TTTUR
     C                   PARM                    TTFLAG
     C                   MOVE      ' '           WSNR
     C     HEAKEY2       CHAIN(N)  HEADF                              33
     C     DOKKEY        CHAIN(N)  DOKUF                              34
     C                   EXSR      TSTSUBF
     C                   UPDATE    SUBFIL
     C                   ENDSR
N02   ***********************************************
N02  C     SRKLL         BEGSR
N02   ***********************************************
N04  C                   SETON                                        99
N02  c                   move      DFAVD         HEAVD
N02  c                   move      DFOPD         HEOPD
N02   * Vis kolli
N02  C     DOKKEY        CHAIN(N)  DOKUF                              34
N02   * dersom fraktbrevet overført til utk.oppdrag finnes kolli-IDene de
N02  c     WSBRTX        ifeq      'U.op:'
N02  c                   move      WSBRAV        DFAVD
N02  c                   move      WSBROP        DFOPD
N02  c                   endif
N02  C                   SETON                                        99
N02  C                   MOVE      *ZEROS        ULINR             2 0
N02  C                   MOVE      *BLANK        UVIS              1
N02  C                   CALL      'SYFA10B'
N02  C                   PARM                    DFAVD
N02  C                   PARM                    DFOPD
N02  C                   PARM                    DFFBNR
N02  C                   PARM                    ULINR
N02  C                   PARM                    UVIS
N02   * DFavd/opd kan være ødelagt av U.op.
N02  c                   move      HEAVD         DFAVD
N02  c                   move      HEOPD         DFOPD
N02  C                   MOVE      ' '           WSNR
N02  C     HEAKEY2       CHAIN(N)  HEADF                              33
N02  C     DOKKEY        CHAIN(N)  DOKUF                              34
N02  C                   EXSR      TSTSUBF
N02  C                   UPDATE    SUBFIL
N02  C                   ENDSR
      *
      ***********************************************
     C     SRTTT         BEGSR
      ***********************************************
      * T&T på TUR-nivå. (Legge inn en kollektiv hendelse).-
     C                   SETON                                        99
N04  C*                  move      'TEI'         UUACTI
N04  C                   move      'Z  '         UUACTI
     C                   CALL      'SYTT05'
     C                   PARM                    WSTUR
     C                   PARM                    W2TRAN
     C                   PARM                    UUACTI            3
      *
      *
     C                   ENDSR
     C***********************************************
     C     CHGSI         BEGSR
     C***********************************************
     C*    WSCLOC        DIV       256           LINNBR            3 0
     C*                  MVR                     POSNBR            3 0
     C                   MOVE      DFAVD         HEAVD
     C                   MOVE      DFOPD         HEOPD
     C                   EXSR      MOVFW
     C                   SETON                                        99
     C                   Z-ADD     25            LINNBR
     C                   Z-ADD     24            POSNBR
     c     StrChg        tag
     C                   EXFMT     VARELI
     C                   SETOFF                                       3092
     C     *IN04         IFEQ      *ON
     C     *IN10         OREQ      *ON
     C                   EXSR      SØKDTA
     C                   GOTO      StrChg
     C                   END
     C  N12              DO
     C                   MOVE      ' '           TSTTYP            1
     C                   MOVE      WSPOUL        TSTPOUL           4
     C                   MOVE      '0'           TSTPORES          1
     C                   CALL      'TSTPNR'
     C                   PARM                    TSTTYP
     C                   PARM                    TSTPOUL
     C                   PARM                    TSTPORES
     C     TSTPORES      IFEQ      '1'
     C                   MOVE      *ON           *IN30
     C                   MOVE      *ON           *IN92
     C                   Z-ADD     25            LINNBR
     C                   Z-ADD     86            POSNBR
     C                   MOVE      'JOV1333'     MSGNR
     C                   GOTO      StrChg
     C                   ENDIF
      *
     C     TSTPOUL       CHAIN     PONRN                              33
     C     HEAKEY        CHAIN     HEADF                              33
     C     DOKKEY        CHAIN     DOKUF                              34
     C                   EXSR      MOVWF
     C  N33              UPDATE    HEADFR
     C  N34              UPDATE    DOKUFR
     C*                  EXSR      TSTSUBF
     C
     C                   END
     C                   EXSR      TSTSUBF
     C                   UPDATE    SUBFIL
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     CHGFBR        BEGSR
     C***********************************************
N02  c                   move      DFAVD         HEAVD
N02  c                   move      DFOPD         HEOPD
     C                   SETON                                        99
     C     KODTAK        CHAIN     KODTA                              99
     C                   CALL      'SYFI10A'
     C                   PARM                    FIFIRM
     C                   PARM                    DFAVD
     C                   PARM                    DFOPD
     C                   PARM                    DFFBNR
     C                   PARM                    KOAKNR
     C                   PARM                    XTRNV            30
     C                   PARM                    XKDME             1
     C                   PARM                    XKDE              1
     C                   PARM                    XTOLL             2 0
     C                   PARM                    FILFB
     C                   PARM                    FILTB
     C                   PARM                    WSMSG            62
     C                   PARM      0             UFLAGG
     C                   PARM                    TEST              1
     C*
     C     HEAKEY2       CHAIN(N)  HEADF                              33
     C     DOKKEY        CHAIN(N)  DOKUF                              34
     C                   EXSR      TSTSUBF
     C                   UPDATE    SUBFIL
     C                   ENDSR
     C*
     C*
     C***********************************************
     C     SØKDTA        BEGSR
     C***********************************************
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
      * SØK PÅ POSTNR
     C     POSNBR        IFLT      115
     C*                  EVAL      XXPNR  =  *BLANKS
     C                   MOVE      WSPOUL        XXPNR
     C                   EVAL      XXNAV  =  DFST15
     C                   EVAL      UUTYPE =  'G'                                Gate
     C     POSNBR        IFGT      85
     C                   EVAL      UUTYPE =  'P'                                Postnr
     c                   end
     C                   CALL      'SYGE45'
     C                   PARM                    WSAD1L
     C                   PARM                    XXPNR             4
     C                   PARM                    XXNAV            30
     C                   PARM                    UUTYPE            1
     C     UUTYPE        IFEQ      'V'                                          Valg er gjort
     c                   MOVE      XXPNR         WSPOUL
     c                   MOVEL     XXNAV         DFST15
     C                   SETOFF                                       3092
     C                   end
     C                   ELSE
      * SØK TRANSPORTØR
     C                   CALL      'SYTR04A'
     C                   PARM                    WSTRAN
     C                   end
      *
     C                   ENDSR
     C***********************************************
     C     MOVFW         BEGSR
     C***********************************************
      * FIL -> WORK
     C                   MOVE      DFNAVL        WSNAVL
     C                   MOVE      DFAD1L        WSAD1L
     C                   MOVE      DFPOUL        WSPOUL
     C                   MOVE      DFNT3         WSNT3
     C                   MOVE      DFVKT3        WSVKT3
     C                   MOVE      DFTRAN        WSTRAN
     C                   ENDSR
     C***********************************************
     C     MOVWF         BEGSR
     C***********************************************
      * WORK -> FIL
     C                   MOVE      WSNAVL        DFNAVL
     C                   MOVE      WSAD1L        DFAD1L
     C                   MOVE      WSPOUL        DFPOUL
     C                   MOVE      PONNAV        DFADUL
      *
     C                   MOVE      WSNAVL        DFNAVM
     C                   MOVE      WSAD1L        DFAD1M
     C                   MOVEL     WSPOUL        TMP35            35
     C                   MOVE      PONNAV        TMP35
s01  C*                  MOVE      TMP35         DFAD1M
n01  C                   MOVEL     TMP35         DFAD3M
      *
     C                   MOVE      WSNT3         DFNT3
     C                   MOVE      WSVKT3        DFVKT3
     C                   MOVE      WSTRAN        DFTRAN
     C                   SELECT
     C     DFTRAN        WHENEQ    1
     C                   EVAL      DFKNS=*BLANKS
     C                   EVAL      DFPROK=*BLANKS
     C                   EVAL      DFNAT='Scancargo AS   '
     C                   EVAL      DFPRO1='Egen Kjøring     '
     C     DFTRAN        WHENEQ    50052
     C                   EVAL      DFNAT='Tollpost Globe '
     C                   EVAL      DFKNS='3117256    '
     C                   EVAL      DFPROK='TG9'
     C                   EVAL      DFPRO1='Tollpost Stykkgods'
     C     DFTRAN        WHENEQ    50112
     C                   EVAL      DFNAT='Posten Lettgods'
     C                   EVAL      DFKNS='00100085638'
     C                   EVAL      DFPROK='PO7'
     C                   EVAL      DFPRO1='Posten stykkgods  '
     C                   ENDSL
      *
     C                   MOVE      DFNAVM        HENAK
     C                   MOVE      DFAD1M        HEADK1
     C                   MOVE      DFAD3M        HEADK3
      *
     C                   Z-ADD     DFNT          HENT
     C                   Z-ADD     DFVKT         HEVKT
     C                   ENDSR
     C*
     C***********************************************
     C*    MOBLW2        BEGSR
     C***********************************************
     C*                  ENDSR
     C***********************************************
     C     MOBLWS        BEGSR
     C***********************************************
     C                   MOVE      *ZEROS        DFAVD
     C                   MOVE      *ZEROS        DFOPD
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     *ENTRY        PLIST
     C                   PARM                    AATUR             8
N04  C                   PARM                    AAAGE             8
N03  C                   PARM                    AAFUNK            1
     c                   move      AATUR         WSTUR
N04  c                   move      AAAGE         W2AGE
      *
     C     HEAKEY        KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C     HEAKEY2       KLIST
     C                   KFLD                    DFAVD
     C                   KFLD                    DFOPD
      *
     C     DOKKEY        KLIST
     C                   KFLD                    DFRI
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C                   KFLD                    DFFBNR
     C                   MOVE      'F'           DFRI
     C                   Z-ADD     001           DFFBNR
N02  C     DOKFMKey      KLIST
N02  C                   KFLD                    DFRI
N02  C                   KFLD                    FMav              4 0
N02  C                   KFLD                    FMop              7 0
N02  C                   KFLD                    DFFBNR
N02  C                   MOVE      'F'           DFRI
N02  C                   Z-ADD     001           DFFBNR
     C     ADRKEY        KLIST
     C                   KFLD                    DFNAVL
     C                   KFLD                    DFAD1L
     C                   KFLD                    DFPOUL
     C     KODTAK        KLIST
     C                   KFLD                    YAUNI
     C                   KFLD                    DFAVD
     C                   MOVE      'A'           YAUNI             1
N06  C     KODTSFK       KLIST
N06  C                   KFLD                    KOSFUN
N06  C                   KFLD                    W3SG
N06  C                   MOVE      'SF'          KOSFUN
     C     TrackKey      KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C                   KFLD                    TTFBNR
     C                   KFLD                    TTACTI
      *
     C     *LOVAL        SETLL     FIRM
     C                   READ      FIRM                                   33
      *
      * DSfelt
     C                   move      *zeros        DFNT
     C                   move      *zeros        DFVKT
     C                   ENDSR
     C*
