             PGM        (&UTSKRIFTNR)
             DCL        VAR(&UTSKRIFTNR) TYPE(*CHAR) LEN(10)
             DCL        VAR(&BRUKER) TYPE(*CHAR) LEN(10)
             DCL        VAR(&SVAR) TYPE(*CHAR) LEN(1)
             DCL        VAR(&DOKNAVN) TYPE(*CHAR) LEN(8)
             DCL        VAR(&WS) TYPE(*CHAR) LEN(10)
/* ER OFFICE INSTALLERT PÅ MASKINEN ?SJEKK BIBL. QOFC*/
             ALCOBJ     OBJ((QOFC *LIB *SHRRD))
             MONMSG     MSGID(CPF0939) EXEC(GOTO CMDLBL(SLUTT))
/* FINNES MØNSTERDOKUM./HAR BRUKEREN TILGANG TIL DET ?*/
             CHKDLO     DLO(DOCPRT) FLR(QPGMR) OBJTYPE(*DOC) +
                          USRID(*CURRENT)
             MONMSG     MSGID(CPF8A82 CPF8A77 CPF9006 CPF8A11) +
                          EXEC(GOTO CMDLBL(SLUTT))
/* SVARER J PÅ OVERFØRING TIL DOKUMENT ???*/
/* (SJEKK MOT ENROLLMENT ER FJERNET*/
    /* I SÅ FALL SPØR OM UTSKRIFT SKAL FLYTTES TIL MAPPE */
             RTVUSRPRF  RTNUSRPRF(&BRUKER)
             CALL       PGM(SYYA02A) PARM(&BRUKER &DOKNAVN &SVAR)
             IF         COND(&SVAR = 'N') THEN(GOTO CMDLBL(SLUTT))
    /* */
             RTVJOBA    JOB(&WS) /* Hente arbeidstsjonskode WS */
             CLRPFM     FILE(PRTDOCA) MBR(&WS)
             MONMSG     MSGID(CPF3141) EXEC(ADDPFM FILE(PRTDOCA) +
                          MBR(&WS))
             OVRDBF     FILE(PRTDOCA) TOFILE(PRTDOCA) MBR(&WS)
             CLRPFM     FILE(PRTDOCB) MBR(&WS)
             MONMSG     MSGID(CPF3141) EXEC(ADDPFM FILE(PRTDOCB) +
                          MBR(&WS))
             OVRDBF     FILE(PRTDOCB) TOFILE(PRTDOCB) MBR(&WS)
    /* */
    /* KOPIER SPOOLFILE OVER I DATABASEFIL (4 FØRSTE=SKIP/SPACEINFO) */
             CPYSPLF    FILE(&UTSKRIFTNR) TOFILE(PRTDOCA) +
                          SPLNBR(*LAST) TOMBR(&WS) CTLCHAR(*PRTCTL)
             MONMSG     MSGID(CPF3309) EXEC(GOTO INTET)
             DLTSPLF    FILE(&UTSKRIFTNR) SPLNBR(*LAST)
             MONMSG     MSGID(CPF0000)
    /* SETTE INN BLANKLINJER ETTER INFO I SKIPP/SPACEB-FELT*/
             CALL       PGM(SYYA02B)
    /* */
             MRGDOC     FROMDOC(DOCPRT) FROMFLR(QPGMR) +
                          TODOC(&DOKNAVN) TOFLR(&BRUKER) +
                          REPLACE(*YES) MRGTYPE(*FILE) +
                          DTAFILE(*LIBL/PRTDOCB) DTAMBR(&WS) +
                          JOBQ(*NO) ADJUST(*PAGES) MLTLINRPT(*NO) +
                          DSPOPT(*EDIT)
             GOTO SLUTT
 INTET:      SNDPGMMSG  MSG('Ingen utskrift.')
 SLUTT:      ENDPGM
