      *****************************************************************
      * OVERSIKT OVER BILAG SOM AVVIKER PERIODEMESSIG FRA OPPDRAGET ***
      * (= SYSTEMET FORESLÅR AT BELØPENE FLYTTES FRA FØRT PERIODE   ***
      * TIL OPPDRAGETS PERIODE FOR Å FÅ ET RIKTIG PER.AVST. REGNSK.)***
      ** PROGRAMMET ER KLARGJORT FOR Å KUNNE KJØRES I EN UPDATE-MODUS
      ** (UOPDAT=J) I SÅ FALL MERKES KONTROLLERTE FAKT.LINJER I FELT
      ** FACD12 MED 'A' DERSOM PERIODE-AVVIK, OG MED 'J' HVIS OK.
      ** I UPDATE-MODUS VIL OPPDRAG SOM ENNÅ IKKE HAR OPD-PERIODE
      ** (NULL-FAKT ELLER BARE KOSTNADER) FÅ SATT HEDTCP/ÅP/MP BASERT
      ** PÅ OPPDRAGSDATO!!! (KAN DA RISIKERE VED SEN FAKTURING (ETTER
      ** AT KOSTN-LINJER ER KONTROLLERT/MERKET AVVIKENENDE, AT BÅDE
      ** KOSTN-LINJER/INNT.LINJER FORESLÅS PERIODEFLYTTET. (ØNSKELIG??)
      ** (ALTERNATIVT KAN EN VURDERE Å LA VÆRE Å OPPDATERE LINJENE MED
      **  KONTROLLERT-STATUS SÅ LENGE OPD-PERI ER UDEFINERT....)
      *****************************************************************
     FWRKPER    UF A E           K DISK
     FWRKPER1   IF   E           K DISK
     F                                     RENAME(WPERR:WPER1R)
     FFAK12     UF   E           K DISK
     FHEADF     UF   E           K DISK
     FTURER14   IF   E           K DISK
     FFIRM      IF   E           K DISK
     FKODTA     IF   E           K DISK
     FKODTGE    IF   E           K DISK
     FKODTG2    IF   E           K DISK
     FSYKS14PF  O    E             PRINTER OFLIND(*IN88)
     D                 DS
     D  FADS                   1     30
     D  KGENOT                 1     20
     D  HEDTOP                22     29  0
     D  OPDPER                22     27  0
     D                 DS
     D  HEDTCP                 1      2  0
     D  HEDTÅP                 3      4  0
     D  HEDTMP                 5      6  0
     D  HOVPER                 1      6  0
     D                 DS
     D  FACC                   1      2  0
     D  FAÅR                   3      4  0
     D  FAMND                  5      6  0
     D  FAKPER                 1      6  0
     D                 DS
     D  NYLVL                  1     25
     D  AVPERC                 1      2  0
     D  AVPERÅ                 3      4  0
     D  AVPERM                 5      6  0
     D  AVÅÅMM                 1      6  0
     D  AVFPEC                 7      8  0
     D  AVFPEÅ                 9     10  0
     D  AVFPEM                11     12  0
     D  AVFØÅM                 7     12  0
     D  AVKONT                13     17  0
     D  AVKSTE                18     21  0
     D  AVKBÆR                22     25  0
     D                 DS
     D  GMLVL                  1     25
     D  GMPERC                 1      2  0
     D  GMPERÅ                 3      4  0
     D  GMPERM                 5      6  0
     D  GMÅÅMM                 1      6  0
     D  GMFPEC                 7      8  0
     D  GMFPEÅ                 9     10  0
     D  GMFPEM                11     12  0
     D  GMKONT                13     17  0
     D  GMKSTE                18     21  0
     D  GMKBÆR                22     25  0
     C*
     C                   EXSR      INIT
      * LEGG I WORKFILE FAKT-LIN NÅR AVVIK I PERIODE I FORH. TIL OPD
     C                   EXSR      ADDWRK
     C*
     C* HENT INN FRA SORTERT WORKFILE - SKRIV HVERT OPPDRAG
     C*
     C   60*LOVAL        SETLL     WRKPER
     C   61*LOVAL        SETLL     WRKPER1
     C   60              READ      WRKPER                                 35
     C   61              READ      WRKPER1                                35
     C     *IN35         DOWNE     '1'
     C                   EXSR      ENPOST
     C   60              READ      WRKPER                                 35
     C   61              READ      WRKPER1                                35
     C  N35              END
     C*
     C* DERSOM GRATOT ULIK 0 - SKRIV SISTE NIVÅBRUDD + GRANDTOTAL
     C     GRATOT        IFNE      *ZEROS
     C   88              WRITE     HEAD0D
     C                   WRITE     DETT
     C                   WRITE     GRAT
     C                   END
     C*
     C                   SETON                                        LR
     C                   RETURN
     C*
     C***********************************************
     C     ADDWRK        BEGSR
     C***********************************************
     C* Les alle overførte fakt-linjer som ikke er periode-kontrollert
     C     FA12KE        SETLL     FAK12                                  33
     C     ' '           READE     FAK12                                  33
     C     *IN33         DOWEQ     '0'
     C     FAKPER        CABGT     UPERT6        UTLES
      *
     C     UAVD          IFEQ      *ZEROS                                         * ALLE AVD
     C     UAVD          OREQ      FAAVD                                          ..EL.RIKT.
     C     HEADFK        CHAIN(N)  HEADF                              34
     C     *IN34         IFEQ      '0'
      *
     C* Finn "Hovedperiode" til oppdraget
     C* er den udefinert (kun kostnad ført) settes den etter oppdr.dato
      *** MIDLERTIDIG!!!!'  BRUKER OPPDRAGETS PERIODE UBETINGET....
     C     HOVPER        IFEQ      *ZEROS
     C     UOPDAT        IFEQ      'J'
     C     HEADFK        CHAIN     HEADF                              34
     C                   END
     C                   MOVE      OPDPER        HOVPER
     C     UOPDAT        IFEQ      'J'
     C                   UPDATE    HEADFR
     C                   END
     C                   END
      * Legg i wrkfile hvis avvikende periode  ...
     C     HOVPER        IFNE      FAKPER
     C                   EXSR      ADDWR2
     C                   END
      *
      * Oppdater kontrollert-status hvis ikke prøvekjøring
     C     UOPDAT        IFEQ      'J'
     C     HOVPER        IFNE      FAKPER
     C                   MOVE      'A'           FACD12
     C                   ELSE
     C                   MOVE      'J'           FACD12
     C                   END
     C                   UPDATE    FAKTR
     C                   END
      *
     C                   END                                                      OPD FINNES
     C                   END                                                      AVD OK.
     C     ' '           READE     FAK12                                  33
     C                   END
      *
     C     UTLES         TAG
     C                   ENDSR
     C*
     C*
     C*
     C***********************************************
     C     ADDWR2        BEGSR
     C***********************************************
     C                   CLEAR                   WPERR
     C                   MOVE      FAAVD         AVOAVD
     C                   MOVE      FAOPD         AVOPD
     C                   Z-ADD     FAFAKT        AVBINR
     C                   MOVE      HEPRO         AVTUR
     C                   MOVE      HEDTCP        AVPERC
     C                   MOVE      HEDTÅP        AVPERÅ
     C                   MOVE      HEDTMP        AVPERM
     C                   MOVE      FACC          AVFPEC
     C                   MOVE      FAÅR          AVFPEÅ
     C                   MOVE      FAMND         AVFPEM
     C* Hent sted bærer fra avd.register
     C                   MOVE      FAAVD         AVKSTE
     C     KODTAK        CHAIN     KODTA                              33
     C     *IN33         IFEQ      '0'
     C                   MOVE      KOAKON        AVKSTE
     C                   MOVE      KOABÆR        AVKBÆR
     C                   END
     C* Hent Konto m.m. fra gebyrkoderegister
     C                   MOVE      FAVK          XXVK
     C     KODGEK        CHAIN     KODTGE                             33
     C*Dersom fra transp.modul og varekode av type transportkostnad:
     C* --- bruk bilkoden for å hente konto
     C  N33KGEAVR        IFEQ      'T'
     C     HEUR          ANDEQ     'H'
     C     HEPRO         ANDNE     *ZEROS
     C                   MOVE      *BLANKS       TUBILK
     C     HEPRO         CHAIN     TURER14                            34
     C     TUBILK        IFNE      '   '
     C                   MOVE      TUBILK        XXVK
     C     KODGEK        CHAIN     KODTGE                             34
     C                   END
     C                   END
     C  N33KODG2K        CHAIN     KODTG2                             33
     C     *IN33         IFEQ      '0'
      *KOSTNAD
     C     FAKDA         IFEQ      'K'
     C                   MOVE      'K'           AVKOIN
     C     FAKDM         IFEQ      'J'
     C                   MOVE      KG2MOT        AVKONT
     C                   ELSE
     C                   MOVE      KG2UTG        AVKONT
     C                   END
     C                   ELSE
      *INNTEKT
     C                   MOVE      ' '           AVKOIN
     C                   MOVE      KGEKON        AVKONT
     C     FAKDM         IFEQ      'N'
     C     KG2KON        ANDNE     *ZEROS
     C                   MOVE      KG2KON        AVKONT
     C                   END
     C                   END
     C                   END
     C* Legg ut beløp i lokal valuta
     C                   Z-SUB     FABELN        AVBELL
     C                   MOVE      FADS          AVTEXT
     C* Skriv posten til workfile
     C                   WRITE     WPERR
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     ENPOST        BEGSR
     C***********************************************
     C* INITIER NIVÅBRUDD VED 1. LESTE POST.
     C     GMLVL         IFEQ      *BLANKS
     C                   MOVE      NYLVL         GMLVL
     C                   END
     C* Sidebrudd ?
     C     *IN88         IFEQ      *ON
     C                   WRITE     HEAD0D
     C                   MOVE      *OFF          *IN88
     C                   END
     C*
     C* SKRIV / NULLSTILL BRUDDSUM VED ENDRING AV PER/KTO/STE/BÆR/TYPE
     C* (både ved detaljert / summert utskrift)
     C     GMLVL         IFNE      *BLANKS
     C     GMLVL         ANDNE     NYLVL
     C* Skriv bruddsum
     C                   WRITE     DETT
     C* Bruddsum på oppdragsperiodenivå??
     C     GMÅÅMM        IFNE      AVÅÅMM
     C                   WRITE     GRAT
     C                   MOVE      *ZEROS        GRATOT
      * Hvis så og detaljert, alltid sidebrudd
     C     UDET          IFEQ      'J'
     C     UDET          OREQ      'Y'
     C                   WRITE     HEAD0D
     C                   MOVE      *OFF          *IN88
     C                   END
     C                   END
      * Kun summer - sidebrud ved endret avdeling
     C     UDET          IFNE      'J'
     C     UDET          ANDNE     'Y'
     C     USORT         ANDEQ     '2'
     C     AVKSTE        ANDNE     GMKSTE
     C                   WRITE     HEAD0D
     C                   MOVE      *OFF          *IN88
     C                   END
     C* Initier for neste bruddsum
     C                   MOVE      NYLVL         GMLVL
     C                   MOVE      *ZEROS        DETTOT           13 2
     C                   END
     C     AVFØÅM        COMP      AVÅÅMM                               51
     C* SKRIV ALLE DETALJER ????
     C     UDET          IFEQ      'J'
     C     UDET          OREQ      'Y'
     C                   WRITE     DET
     C                   END
     C*
     C* AKKUMULER INN I BRUDDSUM OG GRAND TOTAL (både ved detalj/sum)
     C                   ADD       AVBELL        DETTOT
     C                   ADD       AVBELL        GRATOT           13 2
     C*
     C                   ENDSR
     C*
     C*****************************************************************
     C     INIT          BEGSR
     C*****************************************************************
     C*
     C     *ENTRY        PLIST
     C                   PARM                    UUAVD             4
     C                   PARM                    UUPERF            4
     C                   PARM                    UUPERT            4
     C                   PARM                    USORT             1
     C                   PARM                    UDET              1
      *** MIDLERTIDIG!!!!'
     C                   MOVE      'N'           UOPDAT            1
     C* FLYTT PARAM INN I NUMERISKE FELT
     C                   MOVE      UUAVD         UAVD              4 0
     C                   MOVE      UUPERF        UPERF             4 0
     C                   MOVEL     UUPERF        UPERFÅ            2 0
     C                   MOVE      UUPERF        UPERFM            2 0
     C     UPERFÅ        IFGT      50
     C                   MOVE      '19'          UPERFC            2 0
     C                   ELSE
     C                   MOVE      '20'          UPERFC
     C                   END
     C                   Z-ADD     UPERF         UPERF6            6 0
     C                   MOVEL     UPERFC        UPERF6
     C                   MOVE      UUPERT        UPERT             4 0
     C     UPERT         IFGT      5012
     C     UPERT         ADD       190000        UPERT6            6 0
     C                   ELSE
     C     UPERT         ADD       200000        UPERT6
     C                   END
     C* SETT INDIKATOR FOR SORTERINGSFIL
     C     USORT         COMP      '1'                                    60
     C     USORT         COMP      '2'                                    61
     C     USORT         IFLT      '1'
     C     USORT         ORGT      '2'
     C                   SETON                                            60
     C                   END
     C     UDET          IFEQ      'J'
     C     UDET          OREQ      'Y'
     C                   SETON                                            50
     C                   END
     C     HEADFK        KLIST
     C                   KFLD                    FAAVD
     C                   KFLD                    FAOPD
     C     KODTAK        KLIST
     C                   KFLD                    KAUNI
     C                   KFLD                    AVKSTE
     C                   MOVE      'A'           KAUNI             1
     C     KODGEK        KLIST
     C                   KFLD                    GEUNI
     C                   KFLD                    XXVK              3
     C                   MOVE      'GE'          GEUNI             2
     C     KODG2K        KLIST
     C                   KFLD                    G2UNI
     C                   KFLD                    XXVK
     C                   MOVE      'G2'          G2UNI             2
     C     FA12KE        KLIST
     C                   KFLD                    BLANK
     C                   KFLD                    UPERFC
     C                   KFLD                    UPERFÅ
     C                   KFLD                    UPERFM
     C                   MOVE      ' '           BLANK             1
     C*
     C                   READ      FIRM                                   33
     C* Framtving heading på 1. side (Overflw-indikator)
     C                   MOVE      *ON           *IN88
     C*
     C                   ENDSR
