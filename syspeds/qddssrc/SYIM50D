********************************************************************
     A* RETTINGER I DETTE PROGRAM:
PTFnrA*Sek Sig Vers Beskrivelse...........................
"""""A*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
P504XA* 02 CB  PTF5 VEDLIKEHOLD KONTAKTPERSONER
PXXXXA* 03 YBC PTF5 VEDLIKEHOLD AV MILJØAVGIFT
********************************************************************
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPDOC('VEDLKUN' 'HELPNU' 'HLP400')
     A                                      HELP
     A                                      ALTHELP
     A          R BILDE01
     A                                      CF04(04 'Søk kunder')
     A                                      CF03(03 'Avslutt')
     A                                      CF10(10 'SEND MAIL')
     A            MSGNR          7A  P
     A                                  1  4'SYSPED/SYIM50'
     A                                      COLOR(BLU)
     A                                  1 26'Arbeide med ufortollet gods'
     A                                      COLOR(WHT)
     A                                  1 62DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  1 72TIME
     A                                      COLOR(BLU)
     A                                  2  4'Filnavn  utvalg....:'
     A            WSTEXT        30A  B  2 26CHECK(LC)
     A                                  3  4'Fra til avdeling...:'
     A            AVDFRA         4Y 0B  3 26EDTCDE(4)
     A            AVDTIL         4Y 0B  3 31EDTCDE(4)
     A                                  4  4'Fra godsnummer.....:'
     A            WSGNFA         4A  B  4 26
     A  37                                  DSPATR(PC)
     A  37                                  DSPATR(RI)
     A            WSGNFB         5A  B  4 31
     A            WSGNFC         3A  B  4 37
     A            WSGNFD         3A  B  4 41
     A                                  5  4'Til godsnummer.....:'
     A            WSGNTA         4A  B  5 26
     A            WSGNTB         5A  B  5 31
     A            WSGNTC         3A  B  5 37
     A            WSGNTD         3A  B  5 41
     A                                  6  4'Dato fra til.......:'
     A            DATFRA         6Y 0B  6 26EDTCDE(4)
     A            DATTIL         6Y 0B  6 33EDTCDE(4)
     A                                  7  4'Utelat oppdragstyper:'
     A            ZOT01          2A  B  7 26
     A            ZOT02          2A  B  7 29
     A            ZOT03          2A  B  7 32
     A            ZOT04          2A  B  7 35
     A            ZOT05          2A  B  7 38
     A            ZOT06          2A  B  7 41
     A            ZOT07          2A  B  7 44
     A            ZOT08          2A  B  7 47
     A            ZOT09          2A  B  7 50
     A            ZOT10          2A  B  7 53
     A                                  8  4'Uten løpenummer.....:'
     A            LOPENR         1A  B  8 26
     A                                  8 29MSGCON(005 TBR0254 *LIBL/SYMSGF)
     A                                  9  4'Uten notater........:'
     A            NOTAT          1A  B  9 26
     A                                  9 29MSGCON(005 TBR0254 *LIBL/SYMSGF)
     A                                 10  4'Kun med signaturene.:'
     A            S01            3A  B 10 26
     A            S02            3A  B 10 30
     A            S03            3A  B 10 34
     A            S04            3A  B 10 38
     A            S05            3A  B 10 42
     A            S06            3A  B 10 46
     A            S07            3A  B 10 50
     A            S08            3A  B 10 54
     A            S09            3A  B 10 58
     A            S10            3A  B 10 62
     A                                 11  4'Kun AGENTNUMMER.....:'
     A            WSKUND         8Y 0B 11 26EDTCDE(4)
     A  35                                  DSPATR(PC)
     A  35                                  DSPATR(RI)
     A            KNAVNS        30A  O 11 35
     A                                 12  4'Høyeste logg dato...:'
     A            LOGDAT         6Y 0B 12 26EDTCDE(4)
     A                                 13  4'Ta med eksport......:'
     A            WSEJN          1A  B 13 26
     A                                 17  4'F3 GIR OVERGANG TIL GENERERTE LIST-
     A                                      ER    F10 = LAG NY LISTE'
     A                                      COLOR(BLU)
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 21MSGCON(018 DIV0032 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 40'F10 = generer fil'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                             24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A                                 13 29'(J/N)'
