             PGM
             DCL        VAR(&INST) TYPE(*CHAR) LEN(1) VALUE('1')
             DCL        VAR(&LIB) TYPE(*CHAR) LEN(10) VALUE('          ')
             DCL        VAR(&STMF) TYPE(*CHAR) LEN(30) +
                            VALUE('                              ')
             DCL        VAR(&FILE) TYPE(*CHAR) LEN(15) VALUE('               ')

             RTVDTAARA  DTAARA(EDIDTA (171 10)) RTNVAR(&LIB)
             CHGVAR     VAR(&FILE) VALUE('TARIFF.TXT')
             CHGVAR     VAR(&STMF) VALUE(&LIB *TCAT '/TOLL/' *TCAT &FILE)
/* HENT FRA TOLLVESENET    */
             CALL SYTIHENTC PARM(&STMF &FILE)
             DLTF       FILE(QTEMP/SYTIRCV)
             MONMSG     MSGID(CPF0000)
             CRTPF      FILE(QTEMP/SYTIRCV) RCDLEN(256)
/* Konverter fra PC-tekst-fil, til FLAT tekstfil  */
             QSYS/CPYFRMSTMF FROMSTMF(&STMF) +
                          TOMBR('QSYS.LIB/QTEMP.LIB/SYTIRCV.FILE/SYTI+
                          RCV.MBR') MBROPT(*REPLACE) CVTDTA(*TBL) +
                          TBL('qsys.lib/sysped.lib/QTCPEBCNO.tbl')
/* Filen finnes ikke - hopp ut  */
             MONMSG     MSGID(CPFA095) EXEC(DO)
             SNDPGMMSG  MSG('Aktuell txt-fil finnes ikke i IFS +
                          /home/sytoll/   Konvertering IKKE +
                          kjørt!!!') MSGTYPE(*INFO)
             GOTO       CMDLBL(SLUTT)
             ENDDO
/* konverterer tariffen, henter kapittel fra TARIKAP  */
/* Inn: SYTIRCV,TARIKAP    Ut: TARINY  */
             CLRPFM     FILE(TARINY)

             CALL       PGM(SYTITA1) PARM(&INST)

 /* MODULEN IKKE INSTALLERT?? ELLER 0 POSTER LEST */
             IF         COND(&INST = '1') THEN(GOTO CMDLBL(SLUTT))
 /* ENDRET FILLAYOUT */
             IF         COND(&INST = '2') THEN(DO)
             SNDPGMMSG  MSG('Formatet på den nedlastede fil stemmer +
                          ikke med din programversjon. Ta +
                          .......... kontakt med SYSTEMA for å få +
                          oppgradert ditt program.)') MSGTYPE(*INFO)
             GOTO CMDLBL(SLUTT)
             ENDDO

             OVRPRTF    FILE(LISTE) OUTQ(*JOB)

 /* Oppdater GAMLE TARIFFnr, SLETT UTGÅTTE i TARI   */
             OVRDBF     FILE(TARIGM) TOFILE(TARI)
             CALL       PGM(SYTITA4)

             SNDPGMMSG  MSG('Konvertering av aktuell(e) fil(er) ER +
                          KJØRT. Sjekk utkrift "LISTE".......... +
                          (For hver konvertert fil: EN LISTE for +
                          nye/endrede. EN LISTE for slettede.)') +
                          MSGTYPE(*INFO)

SLUTT:       ENDPGM
