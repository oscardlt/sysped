     A                                      REF(*LIBL/EDIM)
     A          R SYLG01PA                  SKIPB(1)
     A                                      HIGHLIGHT
     A            KFMT1         75A        1SPACEA(1)
     A            KFMT2         75A        1SPACEA(1)
     A                                     3'EDI  Sendninger med'
     A            VMNAVN        30A       23
     A                                    51DATE EDTWRD('0 .  .  ')
     A                                    60TIME EDTWRD('0 :  :  ')
     A                                    71'SYLG01P'
     A                                     3'Sender:'
     A                                      SPACEB(2)
     A            FIFT          45A       11
     A                                     3'Tidsrom.....'
     A                                      SPACEB(2)
     A                                    17'Fra:'
     A            ZDTF      R             22REFFLD(MDT)
     A                                      EDTWRD('    .  .  ')
     A            ZTMF      R             33REFFLD(MTM)
     A                                      EDTWRD('0 :  :  ')
     A                                    43'Til:'
     A            ZDTT      R             48REFFLD(MDT)
     A                                      EDTWRD('    .  .  ')
     A            ZTMT      R             59REFFLD(MTM)
     A                                      EDTWRD('0 :  :  ')
     A  50                                 3'KUN..Prod:'
     A  50                                  SPACEB(1)
     A  50        ZPROD1         5A       14
     A  50        ZPROD2         5A       20
     A  50        ZPROD3         5A       26
     A  50                                31'Pt.1/2:'
     A  50        ZPRO1A        20A       38
     A  50                                58'/'
     A  50        ZPRO2A        20A       59
     A  51                                 3'KUN..Fra/Til avd:'
     A  51                                  SPACEB(1)
     A  51        ZAVDF          4  0     21EDTCDE(Z)
     A  51                                26'/'
     A  51        ZAVDT          4  0     28EDTCDE(Z)
     A                                     3'Sendingsnummer'
     A                                      SPACEB(2)
     A                                    21'Mottaker'
     A                                    42'Sted'
     A                                    60'Antall'
     A                                    68'Vekt'
     A                                    74'Tid'
     A                                    67'Volum'
     A                                      SPACEB(1)
     A                                      SPACEA(1)
     A          R SYLG01PB                  SPACEB(1)
     A            X1004         17         3
      * VIDERE ENDRINEGER IKKE MERKET
     A            XMOTT         20        21
     A            XPOST          4        42
     A            XSTED         15        47
     A            XANT           4  0     62EDTCDE(Z)
     A            XVEKT          4  0     68EDTCDE(Z)
     A            XTM            4  0     73EDTWRD('  :  ')
     A                                     1' '
     A                                      SPACEB(1)
     A  52        DFAVD          4  0     10EDTCDE(Z)
     A  52                                14'/'
     A  52        DFOPD          7  0     15EDTCDE(Z)
     A  52                                22'/'
     A  52        DFFBNR         3  0     23EDTCDE(Z)
     A  52                                27'-->'
     A  52        DFBREF        17        31
     A            XVOL           5  3     66EDTCDE(K)
     A          R SYLG01PØ                  SPACEB(2)
     A                                     2' -------------------------  S i s +
     A                                       t e  S i d e    ------------------+
     A                                       --------  '
     A                                    40'SUM KOLLI/VEKT...:'
     A                                      SPACEB(1)
     A            YANT           5  0     61EDTCDE(Z)
     A            YVEKT          7  0     69EDTCDE(K)
     A                                     3'KVITTERING:_____________________'
     A                                      SPACEB(1)
     A                                    40'ANTALL SENDNINGER:'
     A            YSND           4  0     62EDTCDE(Z)
