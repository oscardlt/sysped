*****A**************************************************************
     A* RETTINGER I DETTE PROGRAM:
PTFnrA*Sek Sig Vers Beskrivelse...........................
"""""A*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
P5105A* 01 YBC PTF5 LENGDE PÅ ADRESSENR ER ØKT FRA 3.0 TIL 5.0
*****A**************************************************************
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPDOC('VEDLSTED' 'HELPNU' 'HLP400')
     A                                      HELP
     A                                      ALTHELP
     A          R SUBFIL                    SFL
     A            KUKUN1         8S 0H
     A            WSNR           1A  I  5  3VALUES(' ' '1' '2' '4')
     A            SFINTN    R        O  5  6REFFLD(KUNKOR/KUINTN *LIBL/KUNKO)
     A            SFKUN2    R        O  5 24REFFLD(KUNKOR/KUKUN2 *LIBL/KUNKO)
     A            SFALFA    R        O  5 34REFFLD(KUNKOR/KUALFA *LIBL/KUNKO)
     A            KNAVN         30A  O  5 45
E01  A            SFVADR         5Y 0O  6 29EDTCDE(4)
     A            KADR3         30A  O  6 45
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0008)
     A                                      SFLPAG(0007)
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A            ZRECNO         4S 0O  2 64SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYSPED/SYFA81'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(040 SHU0243 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                  2 70TIME
     A            KUKUN1    R        O  2  4REFFLD(KUNKOR/KUKUN1 *LIBL/KUNKO)
     A                                      COLOR(WHT)
     A            W1NAVN        30A  O  2 15COLOR(WHT)
     A                                  3 34MSGCON(030 DIV0017 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3  2MSGCON(025 DIV1034 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(077 SHU0244 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A          R BUNN
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF03(03 'AVSLUTT')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                      CF10(10 'VAREADR')
     A                                      CF12(12 'FORIGE BILDE')
     A                                      SLNO(23)
     A                                      CLRL(02)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1  2MSGCON(010 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  1 14MSGCON(010 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  1 26MSGCON(010 DIV0022 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  1 38MSGCON(010 DIV0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  1 50MSGCON(014 JOV1308 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  1 68MSGCON(012 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  2  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                              2 40'(C) COPYRIGHT  SYSTEMA A/S  1992  '
     A                                      COLOR(WHT)
     A          R VARELI
     A                                      CF12(12 'Forrige')
     A                                      CF04(04 'søk')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1  6MSGCON(060 SHU0519 *LIBL/SYMSGF)
     A            WSINTN    R        B  2  6REFFLD(KUNKOR/KUINTN *LIBL/KUNKO)
     A            WSKUN2    R        B  2 25REFFLD(KUNKOR/KUKUN2 *LIBL/KUNKO)
     A  40                                  DSPATR(PR)
     A                                      EDTCDE(4)
E01  A            WSVADR         5Y 0B  2 35EDTCDE(4)
     A  40                                  DSPATR(PR)
     A            WSALFA    R        B  2 46REFFLD(KUNKOR/KUALFA *LIBL/KUNKO)
     A                                  3  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 25MSGCON(018 DIV0032 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  4  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                              4 40'(C) COPYRIGHT  SYSTEMA A/S  1992  '
     A                                      COLOR(WHT)
     A          R VAREL2
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  2  2MSGCON(015 DIV1147 *LIBL/SYMSGF)
     A                                  2 19MSGCON(004 TRO0186 *LIBL/SYMSGF)
     A            WSINTN        17A  I  2 24
     A                                  2 45MSGCON(005 TRO0185 *LIBL/SYMSGF)
     A            WSALFA        10   I  2 51
N01  A                                  3  2MSGCON(025 JOV1192 *LIBL/SYMSGF)
N01  A            WSPNR          4A  I  3 28DSPATR(UL)
     A                                  3 60MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  4  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                              4 40'(C) COPYRIGHT  SYSTEMA A/S  1992  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A          R VAREL3
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A                                  1  5MSGCON(077 SHU0244 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            KUINTN    R        O  2  9REFFLD(KUNKOR/KUINTN *LIBL/KUNKO)
     A            KUKUN2    R        O  2 22REFFLD(KUNKOR/KUKUN2 *LIBL/KUNKO)
     A                                  3  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 40'(C) COPYRIGHT  SYSTEMA A/S  1992  '
     A                                      COLOR(WHT)
     A          R NYKUNDE
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF03(03 'AVSLUTT')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                      CF10(10 'VAREADR')
     A                                      CF12(12 'FORIGE BILDE')
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/SYFA81'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(050 SHU0243 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                  2 70TIME
     A            KUKUN1    R        O  2  4REFFLD(KUNKOR/KUKUN1 *LIBL/KUNKO)
     A                                      COLOR(WHT)
     A            W1NAVN        30A  O  2 15COLOR(WHT)
     A                                  3  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 34MSGCON(030 DIV0017 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(077 SHU0244 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                 23  2MSGCON(010 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 14MSGCON(010 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 26MSGCON(010 DIV0022 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 38MSGCON(010 DIV0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 50MSGCON(014 JOV1308 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 68MSGCON(012 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                             24 40'(C) COPYRIGHT  SYSTEMA A/S  1992  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A          R BILDE1
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF03(03 'EXIT')
     A                                      CF04(04 'Søk kunde')
     A                                      CF12(03)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/SYFA81'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(050 SHU0243 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                  2 70TIME
     A                                 11  2MSGCON(012 DIV0199 *LIBL/SYMSGF)
     A            KUKUN1         8Y 0B 11 17DSPATR(UL)
     A                                      EDTCDE(4)
     A  70                                  DSPATR(RI)
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(018 DIV0032 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                             24 40'(C) COPYRIGHT  SYSTEMA A/S  1992  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
