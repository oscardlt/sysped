      *****************************************************************
      *** PROGRAM SKAL KONVERTERE INNGÅENDE IE906                   ***
      *** XML Nack                                                  ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER          ***
      *** LEDIGE INDIKATORER 01-26                                  ***
      *** LEDIGE INDIKATORER 28-32 34-97                            ***
      *****************************************************************
     H DFTACTGRP(*NO)
     FXMLRCVF   IF   E           K DISK
     FXMLRCLF   IF   E           K DISK
     FTRACKF    O  A E           K DISK
     FNCTSEH    UF   E           K DISK
     FNCTSEH05  UF   E           K DISK    RENAME(NCTSEHR:NCTSEHR5)
     FNCTSIH05  UF   E           K DISK
     FTVINF     O  A E             DISK
     FEDIC      UF   E             DISK
     FEDIM      O  A E             DISK
     FEDIM2     IF   E           K DISK    RENAME(EDIMR:EDIMR2)
     FEDIM5     IF   E           K DISK    RENAME(EDIMR:EDIMR5)
     D                sds
     D ZUSER                 254    263
      *  Prototype for 'SYGE15C'
     d syge15c         pr                  extpgm('SYGE15C')
     d wdt_                                like(wdt)
     d wtm_                                like(wtm)
      *  Prototype for ncts104ra
     d ncts104rc       pr
     d usn_                                like(usn)
      *  *ENTRY Interface for Main Procedure
     d ncts104rc       pi
     d usn                            9
      *----------------------------------------------------------------------
      * Stand Alone Fields - TOP
      *----------------------------------------------------------------------
     d x1_xrlev1       s                   like(xrlev1)
     d x1_xrlev2       s                   like(xrlev2)
     d x2_xrlev1       s                   like(xrlev1)
     d x2_xrlev2       s                   like(xrlev2)
     d x2_xrlev3       s                   like(xrlev3)
     d x3_xrlev1       s                   like(xrlev1)
     d x3_xrlev2       s                   like(xrlev2)
     d x3_xrlev3       s                   like(xrlev3)
     d x3_xrlev4       s                   like(xrlev4)
     d x_thtrnr        s                   like(thtrnr)
     d x_IE906         s              1a
     d x_elin          s              3s 0
     d x_ecol          s              3s 0
     d wdt             s              8
     d wtm             s              6
     d                 ds
     d x_pdt                   1      8  0
     d x_pdta                  1      8
     d                 ds
     d x_ptm                   1      6  0
     d x_ptma                  1      6
     d                 ds
     d f0078a                  1     70a
     d f0078b                 71    140a
     d f0078c                141    210a
     d f0078d                211    280a
     d f0078e                281    350a
     d ft                      1    350a   dim(5)
     d                 ds
     d thavd                   1      4  0
     d tiavd                   1      4  0
     d                 ds
     d thtdn                   1      7  0
     d titdn                   1      7  0
      *
     D UP              C                   CONST('ABCDEFGHIJKLMNOPQRST-
     D                                     UVWXYZÆØÅ')
     D LO              C                   CONST('abcdefghijklmnopqrst-
     D                                     uvwxyzæøå')
      *----------------------------------------------------------------------
     IEDIMR2
     I              M0004                       X0004
     I              M0010                       X0010
     I              M0035                       X0035
     I              M0062                       X0062
     I              M0065                       X0065
     I              M0068                       X0068
     I              M0068A                      X0068A
     I              M0068B                      X0068B
     I              M0068C                      X0068C
     I              M0068D                      X0068D
     I              M0068E                      X0068E
     I              M0068F                      X0068F
     I              M1001                       X1001
     I              M1004                       X1004
     I              M1225                       X1225
     I              M2005B                      X2005B
     I              M3039D                      X3039D
     I              M3039E                      X3039E
     I              M5004D                      X5004D
     I              M1N07                       X1N07
     I              M1N08                       X1N08
     I              M9N01                       X9N01
     I              MSN                         XSN
     I              MMN                         XMN
     I              MSR                         XSR
     I              MST                         XST
     I              MDT                         XDT
     I              MTM                         XTM
     I              MVEN                        XVEN
     I              MAVD                        XAVD
     I              MTDN                        XTDN
     IEDIMR5
     I              M0004                       X0004
     I              M0010                       X0010
     I              M0035                       X0035
     I              M0062                       X0062
     I              M0065                       X0065
     I              M0068                       X0068
     I              M0068A                      X0068A
     I              M0068B                      X0068B
     I              M0068C                      X0068C
     I              M0068D                      X0068D
     I              M0068E                      X0068E
     I              M0068F                      X0068F
     I              M1001                       X1001
     I              M1004                       X1004
     I              M1225                       X1225
     I              M2005B                      X2005B
     I              M3039D                      X3039D
     I              M3039E                      X3039E
     I              M5004D                      X5004D
     I              M1N07                       X1N07
     I              M1N08                       X1N08
     I              M9N01                       X9N01
     I              MSN                         XSN
     I              MMN                         XMN
     I              MSR                         XSR
     I              MST                         XST
     I              MDT                         XDT
     I              MTM                         XTM
     I              MVEN                        XVEN
     I              MAVD                        XAVD
     I              MTDN                        XTDN
      /free

       exsr init;

       exsr srxml;

       *inlr = *on;
       return;

       //**********************************************
       begsr srxml;
       //**********************************************

         x1_xrlev1 = 0;
         x1_xrlev2 = 0;

         chain (xrsn:x1_xrlev1:x1_xrlev2) xmlrcvf;
         dow %found;
           select;
           when xrnv = 'messageRecipient';
             m0010  = xrverd;
           when xrnv = 'preparationDateAndTime';
             x_pdta = %subst(xrverd:1:4) + %subst(xrverd:6:2)
                      + %subst(xrverd:9:2);
             x_ptma = %subst(xrverd:12:2) + %subst(xrverd:15:2)
                      + %subst(xrverd:18:2);
           when xrnv = 'messageIdentification';
             m0062  = xrverd;
             m1004  = xrverd;
           when xrnv = 'messageType' and %subst(xrverd:3:3) = '906';
             x_ie906 = 'J';
             chain 1 edic;
             cmn = cmn + 1;
             update edicr;
             fmn = cmn;
             mmn = cmn;
           when xrnv = 'correlationIdentifier';
           when xrnv = 'Header';
             exsr srxml1;
           when xrnv = 'XMLError';
             exsr srxml2;
           endsl;

           x1_xrlev1 = x1_xrlev1 + 1;
           chain (xrsn:x1_xrlev1:x1_xrlev2) xmlrcvf;
         enddo;

         if x_ie906 = 'J';
           exsr opdopd;
           exsr ttsr;
         endif;

         endsr;

       //**********************************************
       begsr srxml1;
       //**********************************************

         x2_xrlev1 = xrlev1;
         x2_xrlev2 = 1;
         x2_xrlev3 = 0;

         chain (xrsn:x2_xrlev1:x2_xrlev2:x2_xrlev3) xmlrcvf;
         dow %found;
           select;
           when xrnv = 'LRN';
             x0068 = xrverd;
           when xrnv = 'MRN';
             x_thtrnr = xrverd;
           endsl;

           x2_xrlev2 = x2_xrlev2 + 1;
           chain (xrsn:x2_xrlev1:x2_xrlev2:x2_xrlev3) xmlrcvf;
         enddo;

       endsr;

       //**********************************************
       begsr srxml2;
       //**********************************************

         x2_xrlev1 = xrlev1;
         x2_xrlev2 = 1;
         x2_xrlev3 = 0;

         chain (xrsn:x2_xrlev1:x2_xrlev2:x2_xrlev3) xmlrcvf;
         dow %found;
           select;
           when xrnv = 'errorPointer';
             ft     = *blanks;
             fln    = fln + 1;
             f0077  = 'ERR';
             if xrverd = '*EXT, TILLEGGSFIL';
               chain (xrsn:x2_xrlev1:x2_xrlev2:x2_xrlev3:xrlev4) xmlrclf;
             else;
               xrvrd = xrverd;
             endif;
             ft(1)  = %subst(xrvrd:1:70);
             ft(2)  = %subst(xrvrd:71:70);
             ft(3)  = %subst(xrvrd:141:70);
             ft(4)  = %subst(xrvrd:211:70);
             ft(5)  = %subst(xrvrd:281:70);
             write tvinff;
           when xrnv = 'errorCode';
             f0077 = xrverd;
           when xrnv = 'errorReason';
             ft     = *blanks;
             fln    = fln + 1;
             ft(1)  = xrverd;
             write tvinff;
           when xrnv = 'origimalAttributeValie';
             ft     = *blanks;
             fln    = fln + 1;
             f0077  = *blanks;
             ft(1)  = %subst(xrvrd:1:70);
             ft(2)  = %subst(xrvrd:71:70);
             ft(3)  = %subst(xrvrd:141:70);
             ft(4)  = %subst(xrvrd:211:70);
             ft(5)  = %subst(xrvrd:281:70);
             write tvinff;
           endsl;

           x2_xrlev2 = x2_xrlev2 + 1;
           chain (xrsn:x2_xrlev1:x2_xrlev2:x2_xrlev3) xmlrcvf;
         enddo;

       endsr;

       //**********************************************
       begsr opdopd;
       //**********************************************

         if x0068 <> *blanks;
         // LRN mottatt
           chain x0068 edim5;
           if %found;
             chain (xavd: xtdn) nctseh;
             if %found;
               thst = 'M';
               update nctsehr;
             endif;
           endif;
         else;
         // MRN mottatt (både avsendere og mottakere kan ha MRN).
           chain x_thtrnr nctseh05;
           if %found;
           // Autorisert avsendere
             setgt (thavd: thtdn) edim2;
             readpe (thavd: thtdn) edim2;
             dow not %eof;
               if x0068a <> 0;
                 leave;
               endif;
               readpe (thavd: thtdn) edim2;
             enddo;
             thst = 'M';
             update nctsehr5;
             m1001  = 'TET';
           else;
           // Autorisert mottakere
             chain x_thtrnr nctsih05;
             if %found;
               readpe (tiavd: titdn) edim2;
               dow not %eof;
                 if m1001 = 'TEI';
                   leave;
                 endif;
                 readpe (tiavd: titdn) edim2;
               enddo;
               tist = 'M';
               update nctsihr;
               m1001  = 'TEI';
             endif;
           endif;
         endif;

         m0065  = 'XMLCTL';
         m0068  = x0068;
         //m1001  = 'TET';
         m1225  = '906';
         msn    = xrsn;
         msr    = 'R';
         mst    = 'C';
         mdt    = %float(wdt);
         mtm    = %float(wtm);
         m0068a = x0068a;
         m0068b = x0068b;
         m1n07  = '906';
         mavd   = thavd;
         mtdn   = thtdn;
         write edimr;

       endsr;

       //**********************************************
       begsr ttsr;
       //**********************************************

         ttavd  = thavd;
         ttopd  = thtdn;
         ttacti = '906';
         ttdate = %float(wdt);
         tttime = %float(wtm);
         ttuser = zuser;
         tttext = 'XML Nack NCTS: '
                  + %trim(%editw(x_pdt:'    .  .  '))
                  + ' ' + %trim(%editw(x_ptm:'  :  :  '));
         tttexl = 'XML teknisk feil NCTS: '
                  + %trim(%editw(x_pdt:'    .  .  '))
                  + ' ' + %trim(%editw(x_ptm:'  :  :  '));
         write trackfr;

       endsr;

       //**********************************************
       begsr init;
       //**********************************************

         xrsn = %dec(usn:9:0);
         syge15c ( wdt : wtm );

       endsr;

      /end-free
