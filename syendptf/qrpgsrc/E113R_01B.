     H DATEDIT(*YMD)
     FEDIS      IF   E           K DISK
     FEDIM      IF   E           K DISK
     FECNI01B   IF   E           K DISK
     FECNT01B   IF   E           K DISK
     FECOM01B   IF   E           K DISK
     FEDTM01B   IF   E           K DISK
     FEEQD01B   IF   E           K DISK
     FELOC01B   IF   E           K DISK
     FETDT01B   IF   E           K DISK
     FEFTX01B   IF   E           K DISK
     FENAD01B   IF   E           K DISK
     FERFF01B   IF   E           K DISK
     FETOD01B   IF   E           K DISK
     FEGID01B   IF   E           K DISK
     FEMEA01B   IF   E           K DISK
     FEMOA01B   IF   E           K DISK
     FEGIN01B   IF   E           K DISK
     FEPCI01B   IF   E           K DISK
     FFIRM      IF   E           K DISK
     FCUNDL01   IF   E           K DISK
     FZTELL     UF A E           K DISK
     FZEADF     O  A E             DISK
     FZREETXT   O  A E             DISK
     FZRISOK    O  A E             DISK
     FZOKUFM    O  A E             DISK
     D A               S              1    DIM(79)
     D B               S              1    DIM(18)
     D TABA            S             35    DIM(12) CTDATA PERRCD(1)
     D TABB            S              8    DIM(12) ALT(TABA)
      *________________________
      * DS for decimal handling
      *""""""""""""""""""""""""
     D                 DS
     D  ZDEC                   1      7  6
     D  ZDEC6                  2      7
     D  ZHEL                  11     22  0
     D                 DS
     D  WMN2                   1      9  0
     D                 DS
     D  XFT                    1    512
     D  FT                     1    490    DIM(7)
      */////////////
      * MAIN LOGIC /
      */////////////
      *___________
      * Initiering
      *"""""""""""
     C                   EXSR      INIT
      *________
      * CONVERT
      *""""""""
     C                   EXSR      RTVD
      *
     C                   MOVE      '1'           *INLR
      *
      *////////////////////////////////////////////////////////////
      *                                                      INIT /
      *////////////////////////////////////////////////////////////
     C     INIT          BEGSR
     C     CUNDL01K      KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    KUNDNR
     C     KEYMS0        KLIST
     C                   KFLD                    ZMN
     C                   KFLD                    ZGN0
     C     KEYMS1        KLIST
     C                   KFLD                    ZMN
     C                   KFLD                    ZGN1
     C                   KFLD                    ZREPB0
     C     KEYMS2        KLIST
     C                   KFLD                    ZMN
     C                   KFLD                    ZGN2
     C                   KFLD                    ZREPC0
     C                   KFLD                    ZREPC1
     C     KEYMS3        KLIST
     C                   KFLD                    ZMN
     C                   KFLD                    ZGN3
     C                   KFLD                    ZREPD0
     C                   KFLD                    ZREPD1
     C                   KFLD                    ZREPD2
      *
     C     *LIKE         DEFINE    NMN           ZMN
     C     *LIKE         DEFINE    NGN           ZGN0
     C     *LIKE         DEFINE    NGN           ZGN1
     C     *LIKE         DEFINE    NGN           ZGN2
     C     *LIKE         DEFINE    NGN           ZGN3
     C     *LIKE         DEFINE    NREP0         ZREPB0
     C     *LIKE         DEFINE    NREP0         ZREPC0
     C     *LIKE         DEFINE    NREP1         ZREPC1
     C     *LIKE         DEFINE    NREP0         ZREPD0
     C     *LIKE         DEFINE    NREP1         ZREPD1
     C     *LIKE         DEFINE    NREP2         ZREPD2
     C     *LIKE         DEFINE    HEHAWB        WEHAWB
     C     *LIKE         DEFINE    HETRI         WETRI
     C     *LIKE         DEFINE    HETRC         WETRC
     C     *LIKE         DEFINE    HETRCN        WETRCN
     C     *LIKE         DEFINE    HESDF         WESDF
     C     *LIKE         DEFINE    HESDT         WESDT
     C     *LIKE         DEFINE    HEKNA         WEKNA
     C     *LIKE         DEFINE    HESDL         WESDL
      * INIT WORKFIELDS
     C                   MOVE      *BLANKS       WEHAWB
     C                   MOVE      *BLANKS       WETRI
     C                   MOVE      *BLANKS       WETRC
     C                   MOVE      *BLANKS       WETRCN
     C                   MOVE      *BLANKS       WESDF
     C                   MOVE      *BLANKS       WESDT
     C                   MOVE      *ZEROS        WEKNA
     C                   MOVE      *BLANKS       WESDL
      *
     C     *ENTRY        PLIST
     C                   PARM                    WMN1              9
     C                   MOVEL     WMN1          WMN2
     C                   Z-ADD     WMN2          ZMN
      * Get msg and interchange values
     C     ZMN           CHAIN     EDIM                               51
     C     MSN           CHAIN     EDIS                               51
     C     *LOVAL        SETLL     FIRM
     C                   READ      FIRM                                   51
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      * RETREIVE INFO FROM EDIFACT DATABASE TO SYSPED400     RTVD /
      *////////////////////////////////////////////////////////////
     C     RTVD          BEGSR
      *_______________________
      * READ ALL MAIN SEGMENTS
      *"""""""""""""""""""""""
      * NAD16
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     16            ZGN0
     C     KEYMS0        SETLL     ENAD01B                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS0        READE     ENAD01B                                51
     C     *IN51         DOWEQ     '0'
      *
     C     N3035         IFEQ      'CA'                                         SENDING AG
     C                   Z-ADD     0             WEKNA
     C     N3039         LOOKUP    TABA          TABB                     20
     C     *IN20         IFEQ      *ON
     C                   MOVEL     TABB          WEKNA
     C                   MOVE      '1'           *IN51
     C                   ENDIF
     C                   ENDIF
      *
     C  N51KEYMS0        READE     ENAD01B                                51
     C  N51              END
     C                   END
      * LOC10
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     10            ZGN0
     C     KEYMS0        SETLL     ELOC01B                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS0        READE     ELOC01B                                51
     C     *IN51         DOWEQ     '0'
      *
     C                   Z-ADD     CREP0         ZREPB0
     C     KEYMS1        CHAIN     EDTM01B                            52
     C                   Z-ADD     CREP0         ZREPB0
     C                   SELECT
     C*                  WHEN      L3227 = '9'
     C                   WHEN      L3227 = '7'
     C                   MOVEL     E3055A        WESDF
     C  N52              MOVEL     D2380         W2380_F          35
     C                   WHEN      L3227 = '88'
     C                   MOVEL     E3055A        WESDT
     C  N52              MOVEL     D2380         W2380_T          35
     C*    L3227         IFEQ      'ZZZ'
     C*                  MOVEL     E3055A        WETRI
     C*                  END
     C                   ENDSL
      *
     C  N51KEYMS0        READE     ELOC01B                                51
     C  N51              ENDDO
     C                   END
      * EQD21
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     21            ZGN0
     C     KEYMS0        SETLL     EEQD01B                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS0        READE     EEQD01B                                51
     C     *IN51         DOWEQ     '0'
      *
     C     E8053         IFEQ      'CN'
     C                   MOVE      '1'           WETRC
     C                   MOVEL     E8260         WETRCN
     C                   MOVE      '1'           *IN51
     C                   ELSE
     C     E8053         IFEQ      'TE'
     C                   MOVEL     E8260         WEHAWB
     C                   MOVE      '1'           *IN51
     C                   END
     C                   END
      *
     C  N51KEYMS0        READE     EEQD01B                                51
     C  N51              ENDDO
     C                   END
      * TDT09
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     9             ZGN0
     C     KEYMS0        SETLL     ETDT01B                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS0        READE     ETDT01B                                51
     C     *IN51         DOWEQ     '0'
      *
     C                   MOVEL     T8212         WEHAWB
      *
     C  N51KEYMS0        READE     ETDT01B                                51
     C  N51              ENDDO
     C                   END
      *___________________
      * READ CONSIGNEMENTS
      *"""""""""""""""""""
     C                   Z-ADD     0             WPOS              3 0
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     25            ZGN0
     C     KEYMS0        SETLL     ECNI01B                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS0        READE     ECNI01B                                51
     C     *IN51         DOWEQ     '0'
      *
     C                   EXSR      RTVDB
      *
     C     KEYMS0        READE     ECNI01B                                51
     C  N51              ENDDO
     C                   ENDIF
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * RETREIVE INFO FROM EDIFACT DATABASE TO SYSPED400    RTVDB /
      *////////////////////////////////////////////////////////////
     C     RTVDB         BEGSR
      *__________________
      * INIT. DATAFELTER
      *""""""""""""""""""
     C                   CLEAR                   HEADFR
     C                   MOVE      'H'           HEUR
     C                   Z-ADD     WEKNA         HEKNA
     C                   Z-ADD     WEKNA         KUNDNR
     C     CUNDL01K      CHAIN     CUNDL01                            33
     C  N33              EVAL      HENAA = KNAVN
      *
     C                   MOVEL     's'           HESDLK
     C                   MOVEL     's'           HESDL
     C                   MOVEL     C1004         HERFA
     C                   ADD       1             WPOS
     C                   MOVEL     WEHAWB        HEHAWB
     C                   MOVEL     WPOS          HEPOS1
     C                   MOVEL     SLIB          HELIB
     C                   MOVEL     SFILE         HEFILE
     C                   MOVEL     SMBR          HEMBR
     C                   MOVE      'IFCSUM'      HETYPE
     C                   MOVE      '01B'         HELVL
     C                   Z-ADD     ZMN           HESNN
     C                   IF        WEHAWB = *BLANKS
     C                   EVAL      HEREFF = M1004
     C                   ELSE
     C                   EVAL      HEREFF = %TRIM(WEHAWB) + ' ' + %TRIM(M1004)
     C                   END
     C     FIFIRM        CHAIN     ZTELL                              33
     C                   IF        *IN33
     C                   EVAL      FIRMA = FIFIRM
     C                   Z-ADD     1000001       HEUNIK
     C                   Z-ADD     1000002       ZUNIK
     C                   WRITE     ZTELLR
     C                   ELSE
     C                   Z-ADD     ZUNIK         HEUNIK
     C                   ADD       1             ZUNIK
     c                   UPDATE    ZTELLR
     c                   END
      *__________________
      * READ ALL SEGMENTS
      *""""""""""""""""""
     C                   Z-ADD     CREP0         ZREPB0
     C                   EXSR      CNT25
     C                   EXSR      FTX25
     C*                  EXSR      LOC20
     C                   EXSR      TOD31
     C                   EXSR      RFF32
     C                   EXSR      NAD43
     C                   EXSR      GID50
      *___________________
      * WRITE TO SYSPED400
      *"""""""""""""""""""
     C                   WRITE     HEADFR
      *________________
      * WRITE TO FRISOK
      *""""""""""""""""
     C                   CLEAR                   FRISOKR
     C                   Z-ADD     HEAVD         FSAVD
     C                   Z-ADD     HEOPD         FSOPD
     C                   MOVEL     'HEL'         FSKODE
     C                   MOVEL     C1004         FSSOK
     C                   MOVEL     HEUNIK        FSUNIK
     C                   MOVEL     HEREFF        FSREFF
     C                   WRITE     FRISOKR
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      * LOC20                                               LOC20 /
      *////////////////////////////////////////////////////////////
     C*    LOC20         BEGSR
     C*                  MOVE      '0'           *IN52
     C*                  Z-ADD     20            ZGN1
     C*    KEYMS1        SETLL     ELOC96A                                20
     C*    *IN20         IFEQ      '1'
     C*    KEYMS1        READE     ELOC96A                                52
     C*    *IN52         DOWEQ     '0'
      *
     C*    L3227         IFEQ      '1'
     C*                  MOVEL     L3224         HESDT
     C*                  MOVE      '1'           *IN52
     C*                  END
      *
     C*    KEYMS1        READE     ELOC96A                                52
     C* N52              ENDDO
     C*                  ENDIF
      *
     C*                  ENDSR
      *
      *////////////////////////////////////////////////////////////
      * TOD31                                               TOD31 /
      *////////////////////////////////////////////////////////////
     C     TOD31         BEGSR
     C                   MOVE      '0'           *IN52
     C                   Z-ADD     31            ZGN1
     C     KEYMS1        SETLL     ETOD01B                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS1        READE     ETOD01B                                52
     C     *IN52         IFEQ      '0'
      *
     C                   MOVEL     T4053         HEFR
      *
     C                   ENDIF
     C                   ENDIF
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      * NAD43                                               NAD43 /
      *////////////////////////////////////////////////////////////
     C     NAD43         BEGSR
     C                   MOVE      '0'           *IN52
     C                   Z-ADD     43            ZGN1
     C     KEYMS1        SETLL     ENAD01B                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS1        READE     ENAD01B                                52
     C     *IN52         DOWEQ     '0'
      *
     C                   SELECT
     C     N3035         WHENEQ    'CN'                                         LEV.ADR
     C     N3124A        IFNE      *BLANKS
     C                   MOVEL     N3124A        HENAK
     C                   ENDIF
     C     N3124B        IFNE      *BLANKS
     C                   MOVEL     N3124B        HEADK1
     C                   ENDIF
     C     N3124C        IFNE      *BLANKS
     C                   MOVEL     N3124C        HEADK2
     C                   ENDIF
     C     N3124D        IFNE      *BLANKS
     C                   MOVEL     N3124D        HEADK3
     C                   ENDIF
     C     N3036A        IFNE      *BLANKS
     C                   MOVEL     N3036A        HENAK
     C                   ENDIF
     C     N3042A        IFNE      *BLANKS
     C                   MOVEL     N3042A        HEADK1
     C                   ENDIF
     C     N3251         IFNE      *BLANKS
     C                   MOVEL     N3251         WEADS1            9
     C                   MOVEL     N3164         WEADS2           21
     C                   MOVEL     WEADS1        HEADK3
     C                   MOVE      WEADS2        HEADK3
     C                   MOVEL     N3251         HESDVT
     C                   MOVE      ' '           HESDVT
     C                   MOVEL     'NO'          HELKK
     C                   ENDIF
      * HENT COM
     C                   Z-ADD     44            ZGN2
     C                   Z-ADD     NREP0         ZREPC0
     C                   Z-ADD     NREP1         ZREPC1
     C     KEYMS2        CHAIN     ECOM01B                            53
     C     *IN53         IFEQ      *OFF
     C                   MOVEL     C3155         HEKDL
     C     HEKDL         CAT       C3148:1       HEKDL
     C                   ENDIF
      *
     C     N3035         WHENEQ    'CZ'                                         AVS.
     C     N3124A        IFNE      *BLANKS
     C                   MOVEL     N3124A        HENAS
     C                   ENDIF
     C     N3124B        IFNE      *BLANKS
     C                   MOVEL     N3124B        HEADS1
     C                   ENDIF
     C     N3124C        IFNE      *BLANKS
     C                   MOVEL     N3124C        HEADS2
     C                   ENDIF
     C     N3124D        IFNE      *BLANKS
     C                   MOVEL     N3124D        HEADS3
     C                   ENDIF
     C     N3036A        IFNE      *BLANKS
     C                   MOVEL     N3036A        HENAS
     C                   ENDIF
     C     N3042A        IFNE      *BLANKS
     C                   MOVEL     N3042A        HEADS1
     C                   ENDIF
     C     N3251         IFNE      *BLANKS
     C                   MOVEL     N3251         WEADS1            9
     C                   MOVEL     N3164         WEADS2           21
     C                   MOVEL     WEADS1        HEADS3
     C                   MOVE      WEADS2        HEADS3
     C                   MOVEL     N3251         HESDFF
     C                   MOVE      ' '           HESDFF
     C                   ENDIF
     C     N3207         IFNE      *BLANKS
     C                   MOVEL     N3207         HELKS
     C                   ENDIF
      *
     C                   ENDSL
      *
     C     KEYMS1        READE     ENAD01B                                52
     C  N52              ENDDO
     C                   ENDIF
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      * RFF32                                               RFF32 /
      *////////////////////////////////////////////////////////////
     C     RFF32         BEGSR
     C                   MOVE      '0'           *IN52
     C                   Z-ADD     32            ZGN1
     C     KEYMS1        SETLL     ERFF01B                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS1        READE     ERFF01B                                52
     C     *IN52         DOWEQ     '0'
      *
     C                   EVAL      FSKODE = R1153
     C                   EVAL      FSSOK  = R1154
     C                   MOVEL     HEUNIK        FSUNIK
     C                   MOVEL     HEREFF        FSREFF
     C                   WRITE     FRISOKR
      *
     C  N52KEYMS1        READE     ERFF01B                                52
     C  N52              ENDDO
     C                   ENDIF
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      * CNT25                                               CNT25 /
      *////////////////////////////////////////////////////////////
     C     CNT25         BEGSR
     C                   MOVE      '0'           *IN52
     C                   Z-ADD     25            ZGN1
     C     KEYMS1        SETLL     ECNT01B                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS1        READE     ECNT01B                                52
     C     *IN52         DOWEQ     '0'
      *
     C                   MOVEA     C6066         A
     C                   EXSR      GETNUM
     C                   SELECT
     C                   WHEN      C6069 = '7' AND C6411 = 'KGM'
     C                   Z-ADD(H)  ZNUM          HEVKT
     C                   WHEN      C6069 = '57' AND C6411 = 'MTR'
     C                   Z-ADD(H)  ZNUM          HELM
     C                   WHEN      C6069 = '15' AND C6411 = 'MTQ'
     C                   Z-ADD(H)  ZNUM          HEM3
     C                   WHEN      C6069 = '11'
     C                   Z-ADD(H)  ZNUM          HENT
     C                   OTHER
     C                   ENDSL
      *
     C     KEYMS1        READE     ECNT01B                                52
     C  N52              ENDDO
     C                   ENDIF
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      * FTX25                                               FTX25 /
      *////////////////////////////////////////////////////////////
     C     FTX25         BEGSR
     C                   CLEAR                   FREETXTR
     C                   MOVE      HEUNIK        FRUNIK
     C                   MOVE      HEREFF        FRREFF
     C                   Z-ADD     MDT           FRTDT
     C                   MOVE      '0'           *IN52
     C                   Z-ADD     25            ZGN1
     C     KEYMS1        SETLL     EFTX01B                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS1        READE     EFTX01B                                52
     C     *IN52         DOWEQ     '0'
      *
     C     F4451         IFEQ      'DIN'
     C     F4451         OREQ      'HAN'
     C                   EVAL      XFT = F4440A
     C                   EXSR      SROPDFT
     C     F4440B        IFNE      *BLANKS
     C                   EVAL      XFT = F4440B
     C                   EXSR      SROPDFT
     C                   END
     C     F4440C        IFNE      *BLANKS
     C                   EVAL      XFT = F4440C
     C                   EXSR      SROPDFT
     C                   END
     C     F4440D        IFNE      *BLANKS
     C                   EVAL      XFT = F4440D
     C                   EXSR      SROPDFT
     C                   END
     C     F4440E        IFNE      *BLANKS
     C                   EVAL      XFT = F4440E
     C                   EXSR      SROPDFT
     C                   END
     C                   ENDIF
      *
     C  N52KEYMS1        READE     EFTX01B                                52
     C  N52              ENDDO
     C                   ENDIF
      * LEGG IN AVGANGSDATO I FITEKST
     C     W2380_F       IFNE      *BLANKS
     C                   ADD       1             FRTLI
     C                   MOVEL     'AVG.DATO'    FRTTXT
     C     FRTTXT        CAT       W2380_F:1     FRTTXT
     C                   WRITE     FREETXTR
     C                   END
      * LEGG IN AVGANGSDATO I FITEKST
     C     W2380_T       IFNE      *BLANKS
     C                   ADD       1             FRTLI
     C                   MOVEL     'ANK.DATO'    FRTTXT
     C     FRTTXT        CAT       W2380_T:1     FRTTXT
     C                   WRITE     FREETXTR
     C                   END
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      * SROPDFT                                                   /
      *////////////////////////////////////////////////////////////
     C     SROPDFT       BEGSR
      *
     C     1             DO        7             XN                3 0
     C                   IF        FT(XN) <> *BLANKS
     C                   ADD       1             FRTLI
     C                   EVAL      FRTTXT = FT(XN)
     C                   WRITE     FREETXTR
     C                   END
     C                   ENDDO
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      * GID50                                               GID50 /
      *////////////////////////////////////////////////////////////
     C     GID50         BEGSR
     C                   MOVE      '0'           *IN52
     C                   Z-ADD     50            ZGN1
     C     KEYMS1        SETLL     EGID01B                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS1        READE     EGID01B                                52
     C     *IN52         DOWEQ     '0'
      *
     C                   MOVEA     G7224A        A
     C                   EXSR      GETNUM
     C     HENT          IFEQ      0
     C                   ADD       ZNUM          HENT
     C                   MOVEL     G7065A        HEVS1
      * HENT FTX
     C                   Z-ADD     50            ZGN2
     C                   Z-ADD     GREP0         ZREPC0
     C                   Z-ADD     GREP1         ZREPC1
     C     KEYMS2        CHAIN     EFTX01B                            53
     C     *IN53         IFEQ      *OFF
     C     HEVS1         CAT       F4440A:1      HEVS1
     C                   ENDIF
     C                   ELSE
     C                   ADD       1             FRTLI
     C                   MOVEL(P)  G7224A        FRTTXT
     C     FRTTXT        CAT       G7064A:1      FRTTXT
      * HENT FTX
     C                   Z-ADD     50            ZGN2
     C                   Z-ADD     GREP0         ZREPC0
     C                   Z-ADD     GREP1         ZREPC1
     C     KEYMS2        CHAIN     EFTX01B                            53
     C     *IN53         IFEQ      *OFF
     C     FRTTXT        CAT       F4440A:1      FRTTXT
     C                   ENDIF
     C                   WRITE     FREETXTR
     C                   ENDIF
      *
     C*                  EXSR      MEA52
     C                   EXSR      PCI55
      *
     C     KEYMS1        READE     EGID01B                                52
     C                   ENDDO
     C                   ENDIF
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * MEA52                                               MEA52 /
      *////////////////////////////////////////////////////////////
     C     MEA52         BEGSR
     C                   MOVE      '0'           *IN53
     C                   Z-ADD     52            ZGN1
     C                   Z-ADD     GREP1         ZREPC1
     C     KEYMS2        SETLL     EMEA01B                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS1        READE     EMEA01B                                53
     C     *IN53         DOWEQ     '0'
      *
     C                   MOVEA     M6314         A
     C                   EXSR      GETNUM
     C                   SELECT
     C                   WHEN      M6311 = 'WT' AND M6313 = 'AAE'
     C                   ADD       ZNUM          HEVKT
     C                   WHEN      M6311 = 'LMT' AND M6411 = 'MTR'
     C                   Z-ADD     ZNUM          HELM
     C                   WHEN      M6311 = 'VOL' AND M6411 = 'MTQ'
     C                   ADD       ZNUM          HEM3
     C                   OTHER
     C                   ENDSL
      *
     C     KEYMS2        READE     EMEA01B                                53
     C  N52              ENDDO
     C                   ENDIF
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      * PCI55                                               PCI55 /
      *////////////////////////////////////////////////////////////
     C     PCI55         BEGSR
     C                   EVAL      FMST   = 'E'
     C                   EVAL      FMRI   = 'F'
     C                   EVAL      FMFBNR = 1
     C                   EVAL      FMLINR = 0
     C                   EVAL      FMREFF = HEREFF
     C                   EVAL      FMUNIK = HEUNIK
      *
     C                   MOVE      '0'           *IN53
     C                   Z-ADD     55            ZGN2
     C                   Z-ADD     GREP0         ZREPC0
     C                   Z-ADD     GREP1         ZREPC1
     C     KEYMS2        SETLL     EPCI01B                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS2        READE     EPCI01B                                53
     C     *IN53         DOWEQ     '0'
      *
      * HENT KOLLI-ID
     C                   EXSR      GIN55
      *
     C     KEYMS2        READE     EPCI01B                                53
     C                   ENDDO
     C                   ENDIF
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      * GIN55                                               GIN55 /
      *////////////////////////////////////////////////////////////
     C     GIN55         BEGSR
     C                   EVAL      FMLINR = FMLINR + 1
     C                   EVAL      FMMKNR = 0
      *
     C                   MOVE      '0'           *IN54
     C                   Z-ADD     55            ZGN3
     C                   Z-ADD     PREP0         ZREPD0
     C                   Z-ADD     PREP1         ZREPD1
     C                   Z-ADD     PREP2         ZREPD2
     C     KEYMS3        SETLL     EGIN01B                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS3        READE     EGIN01B                                54
     C     *IN54         DOWEQ     '0'
      *
     C                   SELECT
     C                   WHEN      G7402A <> *BLANKS
     C                   EVAL      FMMKNR = FMMKNR + 1
     C                   EVAL      FMMRK1 = G7402A
     C                   WRITE     DOKUFMR
     C                   WHEN      G7402B <> *BLANKS
     C                   EVAL      FMMKNR = FMMKNR + 1
     C                   EVAL      FMMRK1 = G7402B
     C                   WRITE     DOKUFMR
     C                   WHEN      G7402C <> *BLANKS
     C                   EVAL      FMMKNR = FMMKNR + 1
     C                   EVAL      FMMRK1 = G7402C
     C                   WRITE     DOKUFMR
     C                   WHEN      G7402D <> *BLANKS
     C                   EVAL      FMMKNR = FMMKNR + 1
     C                   EVAL      FMMRK1 = G7402D
     C                   WRITE     DOKUFMR
     C                   WHEN      G7402E <> *BLANKS
     C                   EVAL      FMMKNR = FMMKNR + 1
     C                   EVAL      FMMRK1 = G7402E
     C                   WRITE     DOKUFMR
     C                   WHEN      G7402F <> *BLANKS
     C                   EVAL      FMMKNR = FMMKNR + 1
     C                   EVAL      FMMRK1 = G7402F
     C                   WRITE     DOKUFMR
     C                   WHEN      G7402G <> *BLANKS
     C                   EVAL      FMMKNR = FMMKNR + 1
     C                   EVAL      FMMRK1 = G7402G
     C                   WRITE     DOKUFMR
     C                   WHEN      G7402H <> *BLANKS
     C                   EVAL      FMMKNR = FMMKNR + 1
     C                   EVAL      FMMRK1 = G7402H
     C                   WRITE     DOKUFMR
     C                   WHEN      G7402I <> *BLANKS
     C                   EVAL      FMMKNR = FMMKNR + 1
     C                   EVAL      FMMRK1 = G7402I
     C                   WRITE     DOKUFMR
     C                   WHEN      G7402J <> *BLANKS
     C                   EVAL      FMMKNR = FMMKNR + 1
     C                   EVAL      FMMRK1 = G7402J
     C                   WRITE     DOKUFMR
     C                   ENDSL
      *
     C     KEYMS3        READE     EGIN01B                                54
     C                   ENDDO
     C                   ENDIF
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      *  Get numerical value                               GETNUM /
      *////////////////////////////////////////////////////////////
     C     GETNUM        BEGSR
     C                   MOVE      *ZEROS        ZHEL
     C                   MOVE      *ZEROS        ZDEC
     C                   MOVE      *ZEROS        B
     C                   Z-ADD     1             AX                2 0
     C                   Z-ADD     1             BX                2 0
      * Get Integer
     C     A(AX)         DOWGE     '0'
     C     ZHEL          MULT      10            ZHEL
     C                   MOVE      A(AX)         ZHEL
     C                   ADD       1             AX
     C                   ENDDO
      * Decimal point ?
     C     A(AX)         IFEQ      '.'
     C     A(AX)         OREQ      ','
     C                   ADD       1             AX
     C     A(AX)         DOWGE     '0'
     C                   MOVE      A(AX)         B(BX)
     C                   ADD       1             AX
     C                   ADD       1             BX
     C                   ENDDO
     C                   MOVEA     B             ZDEC6
     C                   ENDIF
      * Concatenate
     C                   Z-ADD     ZHEL          ZNUM             18 6
     C                   ADD       ZDEC          ZNUM
     C                   ENDSR
**
LCL05                              00001000
