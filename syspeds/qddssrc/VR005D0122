     A*****************************************************************
     A*   5797-AWD    IBM NORWAY 1988                                 *
     A*   5797-AWD    COPYRIGHT IBM CORP 1988                         *
     A*   REFER TO INSTRUCTIONS ON COPYRIGHT NOTICE NO. G120-2083     *
     A*****************************************************************
     A                                      REF(IBMFRF)
     A                                      MSGLOC(24)
     A                                      ALTHELP
     A                                      PRINT
     A                                      HLPDOC(VR00501 VR005 IBMFFT)
     A                                      HELP
     A          R VR005B01
     A                                      OVERLAY
     A          H                           HLPDOC(VR00501 VR005 IBMFFT)
     A                                      HLPARA(01 001 21 080)
     A                                  1 55'Dato:'
     A                                      DSPATR(HI)
     A                                  1 61DATE
     A                                      EDTCDE(Y)
     A                                      DSPATR(HI)
     A            WWSID     R        O  1 70REFFLD(WSID)
     A                                      DSPATR(HI)
     A            WUSER     R        O  2  3REFFLD(USER)
     A                                      DSPATR(HI)
     A            WJOBNR    R        O  2 14REFFLD(JOBNR)
     A                                      DSPATR(HI)
     A                                  2 30'Valg av firma'
     A                                      DSPATR(HI)
     A                                  2 55'Tid :'
     A                                      DSPATR(HI)
     A                                  2 61TIME
     A                                      DSPATR(HI)
     A                                  2 70'VR005'
     A                                      DSPATR(HI)
     A                                  3  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A          R VR005B02
     A                                      OVERLAY
     A  89                                  PUTOVR
     A          H                           HLPDOC(VR00501 VR005 IBMFFT)
     A                                      HLPARA(01 001 21 080)
     A          H                           HLPDOC(VR00502 VR005 IBMFFT)
     A                                      HLPARA(22 001 24 080)
     A                                  8 19'Tast inn firmagruppe....'
     A            WFKODE    R        B  8 44REFFLD(FKODE)
     A  30                                  DSPATR(PR)
     A  89                                  OVRDTA
     A  70                                  ERRMSG('Datoformat må være+
     A                                       DDMMÅÅ eller ÅÅMMDD')
     A  69                                  ERRMSG('Bruker har ikke +
     A                                      adgang til å kjøre på +
     A                                      dette firma')
     A  67                                  ERRMSG('Bruker/Firmaident +
     A                                      finnes ikke')
     A                                 11 19'Tast inn firmakode......'
     A            WFIRMA    R        B 11 44REFFLD(FIRMA)
     A                                      CHECK(ER)
     A  89                                  OVRDTA
     A  89                                  OVRATR
     A  70                                  ERRMSG('Datoformat må være+
     A                                       DDMMÅÅ eller ÅÅMMDD')
     A  69                                  ERRMSG('Bruker har ikke +
     A                                      adgang til å kjøre på +
     A                                      dette firma')
     A  67                                  ERRMSG('Bruker/Firmaident +
     A                                      finnes ikke')
