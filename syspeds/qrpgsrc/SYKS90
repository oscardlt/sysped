      *
      ****************************************************************
      *** PROGRAM SOM AVSLUTTER TURER                              ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER         ***
      *** LEDIGE INDIKATORER 01 02 04 07 09-11 13-24               ***
      *** LEDIGE INDIKATORER 34-90 95-97                           ***
      *** INDIKATOR 32 PÅ = KALLE FRA SYEX10                       ***
      ***           32 AV = KALLE FRA SYEX88CL                     ***
      ****************************************************************
      *
     FAVSLUTL1  UF A E           K DISK
     FTELL      IF   E           K DISK
     FHEAD8     IF   E           K DISK
     FHEAD11    IF   E           K DISK
     F                                     RENAME(HEADFR:HEADFR11)
     FFAKT      IF   E           K DISK
     FTURER     IF   E           K DISK
     FSYKS90FM  CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     D AL              S              1    DIM(20)
     D                SDS
     D  ZUSER                254    263
     D                 DS
     D  XULTID                 1     14  0
     D  XULTM                  1      6  0
     D  XULDD                  7      8  0
     D  XULMM                  9     10  0
     D  XULÅÅ                 11     14  0
     D                 DS
     D  UDATO                  1      8  0
     D  UAAR                   1      4  0
     D  UMND                   5      6  0
     D  UDAG                   7      8  0
     D                 DS
     D  HEDTOP                 1      8  0
     D  HEPERI                 1      6  0
     D                UDS
     D  UFIRM                  1      2
     D  UAVD                  15     18  0
     D  USIGN                 19     21
     C*
     C                   EXSR      INIT
     C*
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     TURE2K        SETLL     HEAD8
     C     START1        TAG
     C                   EXSR      LESFIL
      *
     C     START2        TAG
      *
     C  N94              DO
     C                   DO        14
     C     ZRECNO        IFEQ      0
     C     ZRECNO        OREQ      XRECNO
     C                   MOVE      HEPRO         WSPRO
     C                   END
     C                   ADD       1             ZRECNO
     C                   EXSR      SUMTUR
     C                   WRITE     SUBFIL
     C                   EXSR      LESFIL
     C  N94              END
     C                   END
     C*
     C                   MOVE      ZRECNO        XRECNO            5 0
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYWRK
     C                   ELSE
     C                   WRITE     SUBCTL
     C                   EXFMT     BUNN
     C                   END
     C*
     C                   SETOFF                                       9830
     C   03              GOTO      SLUTT
     C   12              GOTO      SLUTT
     C   05              DO
     C                   GOTO      START
     C                   END
     C   08              GOTO      START6
     C*
     C*** BLA VIDERE
     C   25              DO
     C                   Z-ADD     XRECNO        ZRECNO
     C                   SETOFF                                       25
     C                   GOTO      START2
     C                   END
     C*
     C     ZRECNO        IFEQ      0
     C                   GOTO      START3
     C                   ELSE
     C                   EXSR      HENTSR
     C                   GOTO      START3
     C                   END
     C*
     C*
     C*** SØK FRA AVD TUR
     C     START6        TAG
     C                   EXFMT     SØKTUR
     C                   SETOFF                                       9930
     C   03              GOTO      SLUTT
     C   12              GOTO      START3
     C*
     C                   EXSR      TSTSØK
     C     WSPRO         IFEQ      0
     C                   Z-ADD     1             WSPRO
     C                   END
     C   30              GOTO      START6
     C                   GOTO      START
     C*
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
     C*
     C*
     C***********************************************
     C     HENTSR        BEGSR
     C***********************************************
     C                   Z-ADD     1             RUNDE             5 0
     C                   MOVEA     '00'          *IN(91)
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C* SETTE AVSLUT STATUS
     C  N98WSNR          IFEQ      'A'
     C                   ADD       1             RUNDE
     C                   EXSR      AVSLUT
     C  N30              MOVE      'A'           WSAVSL
     C                   UPDATE    SUBFIL
     C                   END
     C* SETTE FRIGJØR STATUS
     C  N98WSNR          IFEQ      'F'
     C                   ADD       1             RUNDE
     C                   EXSR      FRIGJO
     C  N30              MOVE      ' '           WSAVSL
     C                   UPDATE    SUBFIL
     C                   END
     C  N98WSNR          IFEQ      '6'
     C                   ADD       1             RUNDE
     C                   EXSR      SKRIV
     C                   UPDATE    SUBFIL
     C                   END
     C                   END
     C*
     C   98              DO
     C     RUNDE         IFEQ      0
     C                   MOVE      'DIV0012'     MSGNR
     C                   SETON                                            30
     C                   ELSE
     C                   SETOFF                                       98
     C                   END
     C                   END
     C*
     C     SLHENT        ENDSR
     C*
     C***********************************************
     C     TSTSØK        BEGSR
     C***********************************************
     C     WSAVD         IFNE      0
     C     WSPRO         ORNE      0
     C     WSAVD         CHAIN     TELL                               99
     C   99AVDZER        CHAIN     TELL                               99
     C   99              MOVE      'SPA0009'     MSGNR
     C   99              SETON                                        30
     C                   GOTO      SLTST
     C                   END
     C*
     C*
     C                   MOVE      'DIV0983'     MSGNR
     C                   SETON                                        9930
     C*
     C     SLTST         ENDSR
     C*
     C*
     C***********************************************
     C     LESFIL        BEGSR
     C***********************************************
     C     STLES         TAG
     C     WSAVD         READE     HEAD8                                  94
     C  N94              DO
     C     TUKEY         CHAIN     TURER                              44
     C     *IN44         IFNE      '1'
     C     HEPRO         ADD       1             WSPRO
     C     TURE2K        SETLL     HEAD8
     C                   GOTO      STLES
     C                   END
     C     TUKEY         CHAIN     AVSLUTL1                           26
     C* KUN AVSLUTTEDE
     C     WSSTAT        IFEQ      'A'
     C     HEPRO         ADD       1             WSPRO
     C     TURE2K        SETLL     HEAD8
     C   26              GOTO      STLES
     C                   END
     C* KUN IKKE AVSLUTTEDE
     C     WSSTAT        IFEQ      'I'
     C     HEPRO         ADD       1             WSPRO
     C     TURE2K        SETLL     HEAD8
     C  N26              GOTO      STLES
     C                   END
     C     HEPRO         ADD       1             WSPRO
     C     TURE2K        SETLL     HEAD8
     C                   END
      *
     C                   MOVE      ' '           WSAVSL
     C  N26              MOVE      'A'           WSAVSL
     C*
     C*
     C*
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     FAKEY         KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C     TUKEY         KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEPRO
     C     TURE2K        KLIST
     C                   KFLD                    WSAVD
     C                   KFLD                    WSPRO
     C*
     C     *ENTRY        PLIST
     C                   PARM                    UIMP              1
     C                   PARM                    U10               1
     C                   PARM                    UPRO              8 0
     C                   PARM                    UTRAN             1
     C*
     C                   Z-ADD     1             WSPRO
     C     *LIKE         DEFINE    WSAVD         AVDZER
     C                   MOVE      UAVD          WSAVD
     C                   MOVE      *ZEROS        HEDTOP
     C                   ENDSR
     C*
     C*
     C***********************************************
     C     AVSLUT        BEGSR
     C***********************************************
     C     TUKEY         CHAIN     AVSLUTL1                           33
     C   33              DO
     C                   CLEAR                   AVSLUTFR
     C                   TIME                    XULTID           14 0
     C                   MOVE      XULTM         AVTIME
     C                   MOVE      XULDD         UDAG
     C                   MOVE      XULMM         UMND
     C                   MOVE      XULÅÅ         UAAR
     C                   MOVE      UDATO         AVDATE
     C                   MOVE      USIGN         AVSG
     C                   MOVE      ZUSER         AVUSER
     C                   MOVE      HEAVD         AVAVD
     C                   MOVE      HEPRO         AVPRO
     C                   WRITE     AVSLUTFR
     C                   END
     C* FEIL
     C*
     C  N33              MOVE      'SHU0569'     MSGNR
     C  N33              SETON                                        9930
     C                   ENDSR
     C*
     C***********************************************
     C     FRIGJO        BEGSR
     C***********************************************
     C     TUKEY         CHAIN     AVSLUTL1                           33
     C  N33              DELETE    AVSLUTFR
     C* FEIL
     C*
     C   33              MOVE      'SHU0570'     MSGNR
     C   33              SETON                                        9930
     C                   ENDSR
     C*
     C***********************************************
     C     SUMTUR        BEGSR
     C***********************************************
     C* TAVARE PÅ INITIELL AVDELING
     C                   MOVE      HEAVD         GGAVD             4 0
     C                   SETOFF                                       33
     C                   MOVE      *ZEROS        WSINNT
     C                   MOVE      *ZEROS        WSKOST
     C                   MOVE      *ZEROS        WSRESU
     C     HEPRO         SETLL     HEAD11
     C     *IN33         DOWEQ     '0'
     C     HEPRO         READE     HEAD11                                 33
     C     *IN33         IFEQ      '0'
     C                   EXSR      SUMFAK
     C                   END
     C                   END
     C                   MOVE      GGAVD         HEAVD
     C                   ENDSR
     C*
     C***********************************************
     C     SUMFAK        BEGSR
     C***********************************************
     C                   SETOFF                                       34
     C     FAKEY         SETLL     FAKT
     C     *IN34         DOWEQ     '0'
     C     FAKEY         READE     FAKT                                   34
     C     *IN34         IFEQ      '0'
      *
     C     FAOPKO        IFNE      'D'
     C     FASK          IFEQ      'S'
     C     FASK          OREQ      'K'
     C     FASK          OREQ      'X'
     C     FASK          OREQ      'I'
      *
     C     FAKDA         IFEQ      'K'
     C                   ADD       FABELN        WSKOST
     C                   ELSE
     C                   ADD       FABELN        WSINNT
     C                   END
     C                   END
     C                   END
      *
     C                   END
     C                   END
     C     WSKOST        ADD       WSINNT        WSRESU                 25
     C                   ENDSR
     C***********************************************
     C     SKRIV         BEGSR
     C***********************************************
     C                   CALL      'SYKS90P'
     C                   PARM                    UFIRM
     C                   PARM                    HEAVD
     C                   PARM                    HEPRO
     C                   ENDSR
