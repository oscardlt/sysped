**********************************************************************************************
      * KOBLE TIL OPPDRAG VIA FRI SØKEVEI(FILNAVN) FRA MAPPE FRISOK
**********************************************************************************************
      * Kopi av ARCA012R. Kallt av ARCA023R
      * OBS - FØR KJØRING må typen være definert i fil ARKTXT
      *       og denne bør gjerne peke på "eksternt" arkivlager med
      *       avvikende bane (f.eks /pdf/scan ) (fil ARKEXT)
**********************************************************************************************
      * 01 JOV PTF12 Hvis lagring i google cloud send rett dit (den rutinen sletter lokalt)
**********************************************************************************************
      *
     H DFTACTGRP(*NO)
      *""""""""""""""""
     FFIRMARC   IF   E           K DISK
N01  FFIRMGOOG  IF   E           K DISK
     FARKIVP    O    E             DISK
     FFIRM      IF   E             DISK
     FARKTXT    IF   E           K DISK    PREFIX(X_)
     FARKEXT    IF   E           K DISK    PREFIX(Y_)
      *""""""""""""""""
     D DirName         S            100A
     D EntryName       S            120A
     D EntryPath       S            256A
      *________________
      * Andre variabler
      *""""""""""""""""
     D TYPE            S              2
     D NULL            S              1A   INZ(X'00')
     D*CMDLINE         S            512
     D*CMDLEN          S             15  5
     D NEWDIR          S            200
     D NEWFILE         S            200
     D NEWOBJ          S            200
     D NEWRAN          S             10

     C                   EXSR      INIT
     C                   EXSR      MOV
     C     OkMov         IFEQ      'J'
     C                   EXSR      ARK
     C                   end
     C                   EVAL      *inlr = *on
     C                   RETURN
      *
      ****************************************************************
     C     INIT          BEGSR
      ****************************************************************
      *
      *
     C     *entry        PLIST
     C                   PARM                    DirName
     C                   PARM                    EntryName
     C                   PARM                    EntryPath
     C                   PARM                    UAVD              4
     C                   PARM                    UOPD              7
      *
      *
      * Definiere parameterliste
      *
     C*    QCMDEXC       PLIST
     C*                  PARM                    CMDLINE
     C*                  PARM                    CMDLEN
     C                   READ      FIRMARC                                41

     C     EXKEY         KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    X_ARKLAG
      * Type er hardkodet
     C                   EVAL      TYPE = 'ZH'
      *
     C                   ENDSR
      *
      ***********************************************************************
     C     MOV           BEGSR
      ***********************************************************************
      *
      * Les firmafil for å få tak i firmakode.
     C                   READ      FIRM
      * Hent dato og klokkeslett
     C                   CALL      'SYGE15C'
     C                   PARM                    WDATO             8
     C                   PARM                    WTID              6
      * Hent slutt på filnavn (ex. .pdf) fra frafil
     c                   eval      par512=EntryName
     c                   call      'SYGE112'
     c                   parm                    par512          512
     c                   parm                    NEWEND           10
      * Generer random number
     C                   CALL      'ARCRAN'
     C                   PARM                    NEWRAN
      * Definier ny katalog Hent fra standard
     C                   EVAL      NEWDIR = '/' + %trimr(ARCANE)
      * Er det på dokumenttypenivå angitt avvikende katalog?(via EXT lager)
     C                   EVAL      *IN51 = *OFF
     C     TYPE          CHAIN     ARKTXT                             51
     C                   IF        *IN51 = *OFF
     C     X_ARKLAG      IFNE      *BLANKS
     C     EXKEY         CHAIN     ARKEXT                             51
     C                   IF        *IN51 = *OFF
     C                   EVAL      NEWDIR = '/' + %trimr(Y_ARCANE)
     C                   ENDIF
     C                   ENDIF
     C                   ENDIF
      * Definier nytt filnavn
     C                   EVAL      NEWFILE= %trim(TYPE)
     C                             + %SUBST(WDATO:1:4)
     C                             + %trimr(UAVD)
     C                             + %trimr(UOPD)
     C                             + NEWRAN
     C                             + NEWEND
      * Lag nytt objektnavn
     C                   EVAL      NEWOBJ = %trim(NEWDIR) + '/'
     C                             + %trimr(NEWFILE)
      * Flytte objekt
     C*                  EVAL      CmdLine = 'MOV OBJ(' + X'7D'
     C*                            + %trimr(EntryPath) + X'7D'
     C*                            + ') TOOBJ(' +X'7D'
     C*                            + %trimr(NEWOBJ) + X'7D' + ')'
     C*                  EVAL      CmdLen = %len(%trim(CmdLine))
     C                   MOVE      'J'           OkMov             1
     C                   MOVEl     EntryPath     FRA             120
     C                   MOVEl     NewObj        Til             120
     C                   CALL      'ARCAMOVC'
     C                   PARM                    FRA
     C                   PARM                    TIL
     C                   PARM                    OkMov
N01   *Hvis google cloud benyttes - send det i skyen med en gang
N01  C                   IF        OkMov = 'J'
N01  C     FIFIRM        chain     FIRMGOOG
N01  C                   if        %found and GOBUID <> *blanks
N01  c                   eval      Companyid = GOBUID
N01  c                   eval      UlocalPath= %trim(NEWDIR)+'/'
N01  c                   eval      USrcFile  = NEWFILE
N01  c                   eval      UNewFile  = *blanks
N01  c                   eval      Udelete   = 'J'
N01  c                   call      'ARC99WSU1'
N01  c                   parm                    Companyid        10
N01  c                   parm                    UlocalPath      100
N01  c                   parm                    uSrcFile        120
N01  c                   parm                    udelete           1
N01  c                   parm                    uNewFile         60
N01  c                   parm                    Uok               1
N01  c                   endif
N01  c                   endif
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Skriv til ARKIVP                                     ARK /
      *////////////////////////////////////////////////////////////
     C     ARK           BEGSR
      * Skriv til ARKIVP
     C                   CLEAR                   ARKIVR

     C                   EVAL      ARSTAT = 'A'
     C                   EVAL      ARTYPE = TYPE
     C                   EVAL      ARFIRM = FIFIRM
     C                   MOVE      UAVD          ARAVD
     C                   MOVE      UOPD          AROPD
     C                   EVAL      ARUSER = 'SY400USR'
     C                   move      WDATO         ARDATE
     C                   movel     WTID          ARTIME
     C                   EVAL      ARKUND = 0
     C                   EVAL      ARLINK = NEWFILE
     C                   EVAL      ARRFK  = EntryName

     C                   WRITE     ARKIVR
      *
     C                   ENDSR
      *
