      *
      ***********************************************************
      *** PROGRAM FOR BEREGNING AV EAN 128 - KONTROLLSIFFER
      *** PARAMETER "UKID" MÅ HA HØYRESTILLT VERDI (MAX 21 EKSKL KRT.S)
      *** MED FORANSTÅENDE NULLER
      *** MEN MED 0/BLANK I SISTE KARAKTER (=RETUR-KONTROLLSIFFER)
      ***********************************************************
      * EKSEMPEL:
      * SIFFERPL.   22 21 20 19 18 17 16 15 14 13 12 11 10 09 08 07 06 05 04 03 02 01
      * EAN-NR       0  0  0  0  0  1  2  1  2  3  4  5  0  0  0  1  2  3  4  5  6  K
      * TRINN 1: FRA SIFFERPOS. 2 SUMMER ANNETHVERT SIFER (=24)
      *              0  +  0  +  0  +  2  +  2  +  4  +  4  +  0  +  2  +  4  +  6
      * TRINN 2: SUM FRA 1 MULT 3 ( =(24*3)=72 )
      * TRINN 3: FRA SIFFERPOS. 3 SUMMER ANNETHVERT SIFER (=17)
      *                 0  +  0  +  1  +  1  +  3  +  5  +  0  +  1  +  3  +  5
      * TRINN 4: ADDER RES FRA 2 OG 3 (72 + 17 = 99)
      * TRINN 5:     NÆRMESTE HØYERE 10-TALL ER 100
      *              KONTROLLSIFFER = 100 - 99 = 1
      *              (HVIS SUM I 4 ENDER PÅ 0, KTR-SIFFER=>0)
      ***********************************************************
      *
      *
     D                 DS
     D  AN                     1     21
     D                                     DIM(21)
     D  AN21                   1     21
     D                 DS
     D  SI                     1     22  0
     D                                     DIM(22)
     D  SI21                   1     21  0
     D  SI22                  22     22  0
      *
     C     *ENTRY        PLIST
     C                   PARM                    UKID             22
      *
     C     UKID          IFNE      *BLANKS
      *
     C                   MOVEL     UKID          AN21
     C     1             DO        21            XN                3 0
     C     AN(XN)        IFLT      '0'
     C     AN(XN)        ORGT      '9'
     C                   GOTO      SLUTT
     C                   END
     C                   ENDDO
      * TRINN 1
     C                   MOVE      *ZEROS        SI
     C                   MOVE      AN21          SI21
     C                   Z-ADD     21            XN
     C     XN            DOWGT     1
     C                   ADD       SI(XN)        XTOT              3 0
     C                   SUB       2             XN
     C                   ENDDO
      * TRINN 2
     C                   MULT      3             XTOT
      * TRINN 3 OG 4 (ADDERER RETT INN I VARIABEL FRA TRINN 2)
     C                   Z-ADD     20            XN
     C     XN            DOWGT     0
     C                   ADD       SI(XN)        XTOT
     C                   SUB       2             XN
     C                   ENDDO
      * TRINN 5
     C                   MOVE      XTOT          SI22
     C     SI22          IFGT      0
     C     10            SUB       SI22          SI22
     C                   END
      *
     C                   MOVE      SI22          UKID
      *
     C                   END
      *
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
      *
