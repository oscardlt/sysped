     A*____________________________
     A* SYSPED / BUDSJETTERTE KOSTNADER
     A*""""""""""""""""""""""""""""
     A                                      REF(SR)
     A          R KOSBUR
     A            BUST           1          COLHDG('STATUS')
     A            BUBNR          7S 0       COLHDG('BILAGSNUMMER')
     A            BUPCC          2S 0       COLHDG('PERIODE-ÅRHUNDRE')
     A            BUPÅR          2S 0       COLHDG('PERIODE-ÅR')
     A            BUPMN          2S 0       COLHDG('PERIODE-MÅNED')
     A            BUTNR          8P 0       COLHDG('TRANSPORTØRNR.')
     A            BULNR          8P 0       COLHDG('LEVERANDØRSNR.')
     A            BUVAL          3          COLHDG('VALUTAKODE')
     A            BUBL           9P 2       COLHDG('BELØP')
     A            BUVK           3          COLHDG('VAREKODE')
     A            BUSG           3          COLHDG('ATT.KODE/SIGNATUR')
     A            BUTAVD    R               REFFLD(AVD)
     A                                      COLHDG('AVDELING FOR TUR')
     A            BUTUNR    R               REFFLD(PRO)
     A                                      COLHDG('TUR/PROSJEKTNR')
     A            BUOAVD    R               REFFLD(AVD)
     A                                      COLHDG('AVDELING FOR OPPDRAG')
     A            BUOPD     R               REFFLD(OPD)
     A                                      COLHDG('OPPDARG(REKVIS.)')
     A            BUBLST         1          COLHDG('BELAST KU/TR/INT')
     A            BUTYPE         1          COLHDG('AVT./ESTIMAT/UTFAKT')
     A            BUTXT         35          COLHDG('Freetext')
     A            BUBILK         3          COLHDG('BILKODE')
     A            BUBILN        10          COLHDG('BILNUMMER')
     A            BUFEDT    R               REFFLD(DT)
     A                                      COLHDG('FERGEDATO')
     A            BUFER          1          COLHDG('FERGE KODE (F)')
     A            BUBNR2         7S 0       COLHDG('FØRT BIL.NR.')
     A* SLAVELINJER *  AKTUELT FØRT OG FREMST SOM TILLEGG TIL FERGEREKVISISJON
     A* NÅR BUBNR3 = 9999999 ER BILAGET ET HOVEDBILAG MED SLAVER UNDER SEG
     A*     BULNR3 INNEHOLDER DA ANTALL SLAVELINJER
     A* NÅR BUBNR3 = ANNET TALL ER BILAGET EN SLAVE UNDER ET HOVEDBILAG
     A*     BULNR3 INNEHOLDER LØPENUMMER TIL DENNESLAVELINJEN
     A            BUBNR3         7S 0       COLHDG('SLAVE AV BUDSJ.B.NR.')
     A            BULNR3         2S 0       COLHDG('LØPENR SLAVELINJER.')
     A            BUBLFD         9P 2       COLHDG('FORDELT BELØP(BRT)')
     A            BUKDM     R               REFFLD(KDM)
     A                                      COLHDG('Momskode')
     A          K BUBNR
