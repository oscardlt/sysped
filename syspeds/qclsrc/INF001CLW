/* ENDRER AUTORITET PÅ OBJEKT FØR FLYTTING  SE NY2:   */
             PGM        PARM(&SPLF &QNBR &QUSER &QJOB &TILBAN)
             DCL        VAR(&TILBAN) TYPE(*CHAR) LEN(50)
             DCL        VAR(&FRABAN) TYPE(*CHAR) LEN(90)
             DCL        VAR(&RUNDE) TYPE(*DEC) LEN(6) VALUE(0)
             DCL        VAR(&NBR) TYPE(*DEC) LEN(6) VALUE(0)
             DCL        VAR(&SPLF) TYPE(*CHAR) LEN(10)
             DCL        VAR(&QJOB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&QUSER) TYPE(*CHAR) LEN(10)
             DCL        VAR(&QNBR) TYPE(*CHAR) LEN(6)
             DCL        VAR(&NBRA) TYPE(*CHAR) LEN(6)
/*                      */
/* FINNER SPOOLFILENUMMER              */
/*                                     */
 NESTE:
/*                                                                   */
             CHGVAR     VAR(&NBR) VALUE(&NBR + 1)
             IF         COND(&NBR *GE 9000) THEN(RETURN)
NESTE2:      CHGSPLFA   FILE(&SPLF) JOB(&QNBR/&QUSER/&QJOB) +
                          SPLNBR(&NBR) OUTQ(MMAIL)
                                                                  /**/
/*     Hvis feilm. CPF3303, gå til NESTE og forsøk på nytt          */
/*     Hvis feilm. CPF3341, SKREVET UT ALLEREDE                     */
/*     Hvis feilm. CPF3344, FLYTTET UTKØ OG FORSØK på nytt          */
                                                                  /**/
             MONMSG     MSGID(CPF3303 CPF3341 CPF3344) EXEC(GOTO CMDLBL(NESTE))
/* FIL IBRUK */
/*           MONMSG     MSGID(CPF3341) EXEC(DO)       */
/*                           */
/* KOMMER HIT HVIS OK        */
/*                           */
             CHGVAR     VAR(&NBRA) VALUE(&NBR)
             CALL       PGM(INF001R) PARM(&SPLF &QNBR &QUSER &QJOB +
                          &NBRA &FRABAN)
/*                           */
/*                           */
/* PRØV SLETT TILOBJEKT      */
/*                           */
             RMVLNK     OBJLNK(&TILBAN)
             MONMSG     MSGID(CPF0000)
             DLYJOB     DLY(1)
             GOTO       CMDLBL(NY2)
/*                           */
NY:
             DLYJOB     DLY(3)
             CHGVAR     VAR(&RUNDE) VALUE(&RUNDE + 1)
/*                                                             */
/*        PRØVER NY RELEASE FOR HVER 5. RUNDE                  */
/*                                                             */
             IF         COND(&RUNDE *EQ 5) THEN(DO)
             STRPRTWTR DEV(MMAIL)
             MONMSG     MSGID(CPF0000)
             DLYJOB     DLY(10)
             RLSSPLF    FILE(&SPLF) JOB(&QNBR/&QUSER/&QJOB) +
                          SPLNBR(&NBR)
             MONMSG     MSGID(CPF0000)
             CHGVAR     VAR(&RUNDE) VALUE(0)
             ENDDO
/*                           */
/* PRØV FLYTT PDF OVER       */
/*                           */
NY2:         CHGAUT     OBJ(&TILBAN) USER(*PUBLIC) DTAAUT(*RWX) +
                          OBJAUT(*ALL)
             MONMSG     MSGID(CPF0000)
             CHGAUT     OBJ(&FRABAN) USER(*PUBLIC) DTAAUT(*RWX) +
                          OBJAUT(*ALL)
             MONMSG     MSGID(CPF0000)
             MOV        OBJ(&FRABAN) TOOBJ(&TILBAN)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(NY))
             CHGAUT     OBJ(&TILBAN) USER(*PUBLIC) DTAAUT(*RWX) +
                          OBJAUT(*ALL)
             MONMSG     MSGID(CPF0000)
             ENDPGM
