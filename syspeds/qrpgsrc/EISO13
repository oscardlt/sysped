      *********************************************************************
      * ARBEID MED PLUKKLISTE (LINJE)
      * PROGRAM BEHANDLER INPUT FRA SYL004 OG VISER UTVALGT HEADLING
      *
CRTPG+*  CRTPGM SYSPED/EISO13 MODULE(*LIBL/EISO13 *LIBL/EISO13B
CRTPGM*        *LIBL/CHECKUSR *LIBL/INCGI) ACTGRP(CGI) AUT(*USE)
      *
      *
      *********************************************************************
      * MAIN PROGRAM FLOW
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
      * Boats for sale data base file (LF on BOATSALE by boat serial number)
     FISOLFL1   UF A E           K DISK    USROPN
     FISOFL8    IF   E           K DISK    USROPN
     FBRIDF     UF   E           K DISK    USROPN
     FBRPARF    if   e           k disk    usropn
      *--------------------------------------------------------------------
      * Prototype definitions and standard system API error structure
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      * Variables common to CGI programs
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
      * VARIABLES SPECIFIC TO THIS PROGRAM
      *--------------------------------------------------------------------
      * Variables received from the input string
     D lng             s              2
     D UsrSpcName      s             10
     D WSUSER          s             10
      * Variables used to load the external html source member
     D Lib             s             10
     D Fn              s             10
     D Mbr             s             10
      * Boats serial numbers received in the input string
     D ItemCount       s             10i 0
     D SernoC          s            105a
     D i               s             10i 0
     D TB              S              1    DIM(7)
     D PGM             C                   CONST('SYSPED/CHECKUSR')
     D PGM2            C                   CONST('SYSPED/EISO13B')
     D FEIL01          C                   CONST('Du må velge partsforklaring.')
     D FEIL02          C                   CONST('Du må velge ansvarlig part.')
     D FEIL03          C                   CONST('Du må gi beskrivelse.')
      *
     D r               s             10i 0
     D Spaces          s             12a   inz('&nbsp;&nbsp;')
     D FEILMELDING     S             80
     D WSSAKN          S              7
     D BSSAKN          S              7  0
     D WSLLNR          S              3
     D BSLLNR          S              3  0
     D WSLPFK          S              5
     D WSLANS          S              5
     D WSLAV1          S             75
     D WSLAV2          S             75
     D WSLAV3          S             75
     D XXNY            S              1
     D                 DS
     D XXREF                   1     20
     D XXSAKN                  1      7
     D XXLNR                   8     10
     D XXUSER                 11     20
      *
      *--------------------------------------------------------------------
      * Prolog common to CGI programs
      * -Receive input from the remote browser
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prolog3
      *=====================================================================
      * MAIN PROCESSING LINE
      *=====================================================================
      * Get program start time for calculating execution time
     C                   time                    timedata1
      * Parse input string
     C                   exsr      Parse
      *------------------
      * Retrieve number of boats to be shown
     C                   EVAL      ItemCount = ZhbGetVarCnt('serno')
      * Show the requested boats
     C                   IF        ItemCount = 0
     C                   eval      SernoC = zhbgetvar('xxref')
     C                   eval      WSUSER  = zhbgetvar('USER')
     C                   EVAL      UsrSpcName  = zhbgetvar('UsrSpcName')
     C                   eval      WSSAKN  = zhbgetvar('WSSAKN')
     C                   eval      BSSAKN  = c2n2(WSSAKN)
     C                   eval      WSLLNR  = zhbgetvar('WSLLNR')
     C                   eval      BSLLNR  = c2n2(WSLLNR)
     C                   eval      XXNY    = zhbgetvar('XXNY')
     C                   ELSE
     C                   eval      i = 1
     C                   eval      SernoC = ZhbGetVar('serno':i)
     C                   eval      WSUSER  = zhbgetvar('USER':i)
     C                   EVAL      UsrSpcName  = zhbgetvar('UsrSpcName':i)
     C                   MOVEL     SERNOC        XXREF
     C                   eval      BSSAKN  = c2n2(XXSAKN)
     C                   eval      BSLLNR  = c2n2(XXLNR)
     C                   MOVE      XXLNR         ISLLNR
     C                   END
      * Initier
     C                   EXSR      INIT
     C                   CALLB     PGM
     C                   PARM                    BIBRID
     C                   PARM                    UsrSpcName
     C                   PARM                    UINN              1
     C                   IF        UINN = 'N'
     C                   GOTO      SLUTT
     C                   END
     C     WSUSER        CHAIN     BRIDF                              50
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVE      WDT           BIDTF
     C                   MOVE      WTM           BITIDF
     C                   UPDATE    BRIDFR
      *
     C     BSSAKN        CHAIN     ISOFL8                             33
      *
     C                   SELECT
     C                   WHEN      XXNY = 'J'
     C                   MOVE      *BLANKS       FEILMELDING
     C                   MOVE      *BLANKS       FMELDNR
     C                   WHEN      XXNY = 'E'
     C     ISOLFK        CHAIN(N)  ISOLFL1                            33
     C                   MOVE      ISLPFK        WSLPFK
     C                   MOVE      ISLANS        WSLANS
     C                   MOVE      ISLAV1        WSLAV1
     C                   MOVE      ISLAV2        WSLAV2
     C                   MOVE      ISLAV3        WSLAV3
     C                   OTHER
     C                   EXSR      ARBLINJE
     C                   ENDSL
      *
     C                   CALLB     PGM2
     C                   PARM                    BSSAKN
     C                   PARM                    BSLLNR
     C                   PARM                    WSUSER
     C                   PARM                    UsrSpcName
     C                   PARM                    BIFARG
     C                   PARM                    WSLPFK
     C                   PARM                    WSLANS
     C                   PARM                    WSLAV1
     C                   PARM                    WSLAV2
     C                   PARM                    WSLAV3
     C                   PARM                    FMELDNR
     C                   PARM                    FEILMELDING
      *
      * Send output buffer and quit
     C     SLUTT         TAG
     C                   exsr      Exit
      *=====================================================================
      * Send output buffer and quit
      *=====================================================================
     C     Exit          begsr
      * Compute and display run time
     C                   IF        UINN = 'N'
     C                   time                    timedata2
     C     timedata2     subdur    timedata1     ms:*ms
     C                   eval      sec = ms / 1000000
     C                   callp     updhtmlvar('runtime':
     C                             %trim(%editw(sec:'     0 .   ')))
     C                   callp     wrtsection('endhtml')
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
     C                   END
      *
     C                   close     ISOLFL1
     C                   close     ISOFL8
     C                   close     BRIDF
     C                   close     BRPARF
     C                   eval      *inlr = *on
      *
     C                   return
     C                   endsr
      *=====================================================================
      * Parse input string
      *=====================================================================
     C     Parse         begsr
     C                   eval      lng     = zhbgetvar('lng')
     C                   eval      WSLPFK  = zhbgetvar('WSLPFK')
     C                   eval      WSLANS  = zhbgetvar('WSLANS')
     C                   eval      WSLAV1  = zhbgetvar('WSLAV1')
     C                   eval      WSLAV2  = zhbgetvar('WSLAV2')
     C                   eval      WSLAV3  = zhbgetvar('WSLAV3')
     C                   eval      XXNY    = zhbgetvar('XXNY')

     C                   endsr
      *=====================================================================
      *  Initier
      *=====================================================================
     C     INIT          begsr
     C                   OPEN      BRIDF
     C     WSUSER        CHAIN(N)  BRIDF                              50
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   CALLB     'INCGI'
     C                   PARM                    BISTDL
     C                   else
      * Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
     C                   open      ISOLFL1                              50
     C                   open      ISOFL8                               50
     C                   open      BRPARF                               50
     C     ISOLFK        klist
     C                   kfld                    BSSAKN
     C                   kfld                    BSLLNR
     c     ISOLFKSIST    klist
     C                   kfld                    BSSAKN
     C                   KFLD                    ISLLNRSIST
     C     BRPARFK       klist
     C                   kfld                    PABRID
     C                   kfld                    PAKUNR
     C                   kfld                    PAID
      *
     C     *LIKE         DEFINE    ISLLNR        ISLLNRSIST
     C                   MOVE      *BLANKS       FMELDNR           1
     C                   MOVE      *BLANKS       FEILMELDING
     C                   Z-ADD     999           ISLLNRSIST
      *
      * VAREGRUPPER (BRUKER-NIVÅ)
     C                   EVAL      PABRID = WSUSER
     C                   EVAL      PAKUNR = 0
      *
     C                   MOVEL     'SPRÅK'       PAID
     C     BRPARFK       chain     BRPARF                             33
     C                   IF        (*IN33 OR (PAVRDA <> 'EN'))
     C                   MOVE      *BLANKS       XSPRÅK            2
     C                   ELSE
     C                   MOVE      'EN'          XSPRÅK            2
     C                   END
      *
     C                   endsr
      *=====================================================================
      *  ARBEID MED LINJE
      *=====================================================================
     C     ARBLINJE      BEGSR
      * TEST INNGÅENDE DATA
     C                   EXSR      TESTB
      *
     C                   IF        FEILMELDING = *BLANKS
     C                   EXSR      OPDLINJE
     C                   END
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
     C     TESTB         BEGSR
      *////////////////////////////////////////////////////////////
     C                   MOVE      *BLANKS       FEILMELDING
     C                   MOVE      *BLANKS       FMELDNR
      * TEST PARTSFORKLARING
     C                   IF        WSLPFK = *BLANKS
     C                   MOVEL     FEIL01        FEILMELDING
     C                   MOVE      *ON           *IN33
     C                   END
      * TEST ANSVARLIG PART
     C                   IF        WSLANS = *BLANKS
     C                   MOVEL     FEIL02        FEILMELDING
     C                   MOVE      *ON           *IN33
     C                   END
      * TEST BESKRIVELSE AV ÅRSAK
     C                   IF        WSLAV1 = *BLANKS
     C                   MOVEL     FEIL03        FEILMELDING
     C                   MOVE      *ON           *IN33
     C                   END
      *
     C     SLTESTB       ENDSR
      *
      *=====================================================================
      *  Oppdater linje
      *=====================================================================
     C     OPDLINJE      begsr
      *
     C                   IF        BSLLNR = 0
     C     ISOLFKSIST    SETLL     ISOLFL1
     C     BSSAKN        READPE(N) ISOLFL1                                33
     C                   IF        *IN33 = *OFF
     C                   ADD       1             ISLLNR
     C                   ELSE
     C                   Z-ADD     1             ISLLNR
     C                   ENDIF
     C                   MOVE      BSSAKN        ISLSAK
     C                   ELSE
     C     ISOLFK        CHAIN     ISOLFL1                            33
     C                   END
      *
     C                   MOVE      WSLPFK        ISLPFK
     C                   MOVE      WSLANS        ISLANS
     C                   MOVE      WSLAV1        ISLAV1
     C                   MOVE      WSLAV2        ISLAV2
     C                   MOVE      WSLAV3        ISLAV3
     C                   MOVE      WDT           ISLRDA
     C                   MOVE      WTM           ISLRTM
     C                   MOVE      WDT           ISLRDS
     C                   MOVE      WTM           ISLRTS
      *
     C                   IF        BSLLNR = 0
     C                   WRITE     ISOLFR
     C                   ELSE
     C                   UPDATE    ISOLFR
     C                   END
      *
     C                   MOVE      *ZEROS        BSLLNR
     C                   MOVE      *BLANKS       WSLPFK
     C                   MOVE      *BLANKS       WSLANS
     C                   MOVE      *BLANKS       WSLAV1
     C                   MOVE      *BLANKS       WSLAV2
     C                   MOVE      *BLANKS       WSLAV3
      *
     C                   endsr
      *
