     A*****************************************************************
     A*   TRM3 RETTET                                                 *
     A*   DAGBOK                                                      *
     A* ANTALL LINJE=48,  ANTALL TEGN PR. LINJE=132,                  *
     A*****************************************************************
     A          R HEADP
     A                                  2  1MSGCON(20 DIV2309 SYMSGF)
     A            WSDTD          2  0   2 23EDTCDE(Z)
     A                                  2 25'.'
     A            WSDTM          2  0   2 26
     A                                  2 28'.'
     A            WSDTÅ          2  0   2 29
     A                                  2 43'* * *'
     A            FIFT          45      2 50
     A                                  2 96'* * *'
     A                                  2107MSGCON(6 DIV2310 SYMSGF)
     A            UDAY           2  0   2115EDTCDE(Z)
     A                                  2117'.'
     A            UMONTH         2  0   2118
     A                                  2120'.'
     A            UYEAR          2  0   2121
     A  50                              3 43'* * *'
     A  50        KOANVN        33      3 50
     A  50                              3 96'* * *'
     A  51                              3 43'* * *'
     A  51                              3 50MSGCON(33 DIV2324 SYMSGF)
     A  51                              3 96'* * *'
     A N50N51                           3 43'* * *'
     A N50N51                           3 50MSGCON(7 DIV2325 SYMSGF)
     A N50N51     WSAV01         4  0   3 58EDTCDE(Z)
     A N50N51     WSAV02         4  0   3 63EDTCDE(Z)
     A N50N51     WSAV03         4  0   3 68EDTCDE(Z)
     A N50N51     WSAV04         4  0   3 73EDTCDE(Z)
     A N50N51     WSAV05         4  0   3 78EDTCDE(Z)
     A N50N51                           3 96'* * *'
     A                                  3107MSGCON(6 DIV2311 SYMSGF)
     A            SIDE           3  0   3120EDTCDE(Z)
     A                                  4  1MSGCON(10 DIV2312 SYMSGF)
     A            DABILK         3      4 13
     A                                  4 17'-'
     A            DABTXT        20      4 19
     A N50N51     WSAV06         4  0   4 58EDTCDE(Z)
     A N50N51     WSAV07         4  0   4 63EDTCDE(Z)
     A N50N51     WSAV08         4  0   4 68EDTCDE(Z)
     A N50N51     WSAV09         4  0   4 73EDTCDE(Z)
     A N50N51     WSAV10         4  0   4 78EDTCDE(Z)
     A                                  6  1MSGCON(130 DIV2313 SYMSGF)
     A          R HEADL                     SPACEB(2)
     A                                     1MSGCON(45 DIV2383 SYMSGF)
     A          R DET                       SPACEB(1)
     A            DABILN        10A        1
     A            DAHENG        10A       12
     A            DASJNX        17A       23
     A            XADTF          6  0     41EDTCDE(Z)
     A            DASDF         20A       48
     A            XADTT          6  0     68EDTCDE(Z)
     A            DASDT         20A       75
     A            DAAVD          4  0     95EDTCDE(Z)
     A            DAPRO          8  0    100EDTCDE(Z)
     A          R DETOP                     SPACEB(1)
     A            HEAVD          4  0     10EDTCDE(Z)
     A                                    14'/'
     A            HEOPD          7  0     15EDTCDE(Z)
     A            HENAAX        20A       23
     A            XRSDFD         6S 0     44
     A            TRSDFK         4S 0     51
     A            HESDF          5A       56
     A                                    62'->'
     A            XRSDTD         6S 0     65
     A            TRSDTK         4S 0     72
     A            HESDT          5A       77
     A                                    83'Vekt:'
     A            HEVKT          9  0     88EDTCDE(Z)
     A                                    98'M3:'
     A            HEM3           7  3    101EDTWRD('    ,   ')
     A                                   110'LM:'
     A            HELM           4  2    113EDTWRD('  ,  ')
     A          R DETBLA                    SPACEB(1)
     A                                     1' '
