      *
     A                                      DSPSIZ(27 132 *DS4)
     A                                      PRINT
     A                                      HELP
     A                                      ALTHELP
      *
     A          R NOERASE
     A                                      ASSUME
     A                                  1  2' '
     A          R WINDOWSZ
     A                                      WINDOW(*DFT 19 100 *NOMSGLIN)
     A                                      OVERLAY
      *
     A          R SUBFIL                    SFL
     A            WSNR           2Y 0B  5  2EDTCDE(4)
     A            EHAVD          4Y 0O  5  5TEXT('Avdeling')
     A                                      EDTCDE(4)
     A  51                                  COLOR(YLW)
     A  52                                  COLOR(PNK)
     A            EHTDN          7Y 0O  5 10TEXT('Oppdrag')
     A                                      EDTCDE(M)
     A  51                                  COLOR(YLW)
     A  52                                  COLOR(PNK)
     A            EHST           1A  O  5 19TEXT('Status')
     A            EHPR           2A  O  5 22TEXT('Prosedyre')
     A            EHRG          11A  H
     A            EH0068A        8Y 0H
     A            EH0068B        6Y 0H
     A            EHTRNR        18A  H
     A            X_IMPREF      27A  O  5 25TEXT('Impprt ref.')
     A            EHETYP         2A  O  5 53TEXT('Eksport type')
     A            EHETYPT       30A  H
     A            EHEID         18A  O  5 56TEXT('Eksport Id')
     A            EHTRID        17A  H
     A            EHNTK          7Y 0O  5 79TEXT('Kolli')
     A                                      EDTCDE(4)
     A            EHVKB         13Y 3O  5 87TEXT('Kolli')
     A                                      EDTCDE(4)
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      WINDOW(WINDOWSZ)
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0012)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A                                      CF03(03 'Avslutt program')
     A                                      CF12(12 'AVBRYT')
     A                                      CF05(05 'Frisk')
     A N49                                  CF06(06 'Lag ny')
     A                                      CF08(08 'Søk')
     A N49                                  CF10(10 'Plukk')
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A            ZRECNO         4S 0O  1 70SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYSPED/SAD162R'
     A                                      COLOR(BLU)
     A                                  1 20'Vedlikehold HouseConsignment - Lis-
     A                                      te'
     A                                      COLOR(WHT)
     A                                  1 90DATE
     A                                      EDTCDE(Y)
     A                                  2 90TIME
     A                                  2  2'Avd.:'
     A            UAVD           4Y 0O  2  8TEXT('AVDELINGSNR')
     A                                      COLOR(BLU)
     A                                      EDTCDE(4)
     A                                  2 13'Turnr:'
     A            UPRO           8Y 0O  2 20TEXT('Turnr')
     A                                      COLOR(BLU)
     A                                      EDTCDE(4)
     A                                  2 29'M-ID:'
     A            UMID          18A  O  2 35TEXT('UUID')
     A                                      COLOR(BLU)
     A N49                              2 62'90=Send   99=Kanseller'
     A                                      COLOR(BLU)
     A  49                              3  2'Velg:  5=Se oppdrag  9=EDI-logg  1-
     A                                      1=Splitt oppdrag  15=Se SAD  51=Ark-
     A                                      iv  52=Opplast'
     A                                      COLOR(BLU)
     A N49                              3  2'Velg:  2=Endre  4=Slett  5=Se oppd-
     A                                      rag  9=EDI-logg  11=Splitt OPD  15=-
     A                                      Se SAD  51=Arkiv  52=Opplast'
     A                                      COLOR(BLU)
     A                                  4  2'Op Avd. Oppdrag  St Pr Ref. fortol-
     A                                      ling eller NCTS  Eksport Type/Id   -
     A                                          XXX   Kolli  Brt.vekt'
     A                                      COLOR(WHT)
      *
     A          R BUNN
     A                                      WINDOW(WINDOWSZ)
     A                                 18  2'F3=Avslutt   F5=Frisk   F12=Avbryt'
     A                                      COLOR(BLU)
     A N49                             18 42'F6=Lag ny   F10=Plukk'
     A                                      COLOR(BLU)
     A  30        MSGTXT        68A  O 19  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R NYVARE
     A                                      WINDOW(WINDOWSZ)
     A                                      CF03(03 'Avslutt program')
     A                                      CF05(05 'Frisk')
     A N49                                  CF06(06 'Lag ny')
     A                                      CF08(08 'Søk')
     A N49                                  CF10(10 'Plukk')
     A                                      CF12(12 'AVBRYT')
     A                                  1  2'SYSPED/SAD162R'
     A                                      COLOR(BLU)
     A                                  1 20'Vedlikehold HouseConsignment - Lis-
     A                                      te'
     A                                      COLOR(WHT)
     A                                  1 90DATE
     A                                      EDTCDE(Y)
     A                                  2 90TIME
     A                                  2  2'Avd.:'
     A            UAVD           4Y 0O  2  8TEXT('AVDELINGSNR')
     A                                      COLOR(BLU)
     A                                      EDTCDE(4)
     A                                  2 13'Turnr:'
     A            UPRO           8Y 0O  2 20TEXT('Turnr')
     A                                      COLOR(BLU)
     A                                      EDTCDE(4)
     A                                  2 29'M-ID:'
     A            UMID          18A  O  2 35TEXT('UUID')
     A                                      COLOR(BLU)
     A N49                              2 62'90=Send   99=Kanseller'
     A                                      COLOR(BLU)
     A  49                              3  2'Velg:  5=Se oppdrag  9=EDI-logg  1-
     A                                      1=Splitt oppdrag  15=Se SAD  51=Ark-
     A                                      iv  52=Opplast'
     A                                      COLOR(BLU)
     A N49                              3  2'Velg:  2=Endre  4=Slett  5=Se oppd-
     A                                      rag  9=EDI-logg  11=Splitt OPD  15=-
     A                                      Se SAD  51=Arkiv  52=Opplast'
     A                                      COLOR(BLU)
     A                                  4  2'Op Avd. Oppdrag  St Pr Ref. fortol-
     A                                      ling eller NCTS  Eksport Type/Id   -
     A                                          Ser   Kolli  Brt.vekt'
     A                                      COLOR(WHT)
     A                                 18  2'F3=Avslutt   F5=Frisk   F12=Avbryt'
     A                                      COLOR(BLU)
     A N49                             18 42'F6=Lag ny   F10=Plukk'
     A                                      COLOR(BLU)
     A  30        MSGTXT        68A  O 19  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
