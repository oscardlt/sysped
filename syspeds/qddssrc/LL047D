*****A**************************************************************
     A* RETTINGER I DETTE PROGRAM:
PTFnrA*Sek Sig Vers Beskrivelse...........................
"""""A*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
P6XXXA* 01 YBC PTF6 UTSKRIFT AV LINJER MED DIFF<>0.
P6XXXA* 02 YBC PTF6 OPPTELLINGSLISTENR
P6XXXA* 03 YBC PTF6 SALDO ER ØKT FRA 5.0 TIL 7.0
P7XXXA* 04 YBC PTF7 FEILMELDING
P8XXXA* 05 YBC PTF8 BATCHNR
*****A**************************************************************
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HELP
     A                                      ALTHELP
      *
     A          R BILDE1
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CA03(03 'EXIT')
     A                                      CF04(04 'Spørr')
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1  2'SYSPED/LL047R'
     A                                      COLOR(BLU)
     A                                  1 21MSGCON(40 YBC1552 SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 67DATE
     A                                      EDTCDE(Y)
     A                                  2 67TIME
     A                                  3  3MSGCON(15 YBC1519 SYMSGF)
     A            WSGRUP         8Y 0B  3 20DSPATR(UL)
     A  99                                  DSPATR(RI)
     A  99                                  COLOR(WHT)
     A                                      EDTCDE(4)
     A N49        NVNGRU        30A  O  3 29COLOR(WHT)
     A  49                              3  3MSGCON(15 YBC1520 SYMSGF)
     A                                  4  3MSGCON(15 YBC1521 SYMSGF)
     A            WSKJED         8Y 0B  4 20DSPATR(UL)
     A  98                                  DSPATR(RI)
     A  98                                  COLOR(WHT)
     A                                      EDTCDE(4)
     A            NVNKJE        30A  O  4 29COLOR(WHT)
N02  A                                  7  3MSGCON(15 YBC1553 SYMSGF)
N02  A            WSLNR          7Y 0B  7 20DSPATR(UL)
N02  A  97                                  DSPATR(RI)
N02  A  97                                  COLOR(WHT)
N02  A                                      EDTCDE(4)
N02  A            WSSLT          1A  B  7 30DSPATR(UL)
N02  A  96                                  DSPATR(RI)
N02  A  96                                  COLOR(WHT)
N02  A                                  7 33MSGCON(30 YBC1554 SYMSGF)
     A                                 23  2MSGCON(18 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(18 DIV0032 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
      *
     A          R SUBFIL                    SFL
     A            LTMN           7  0H
     A            LTLLOK        25A  H
     A            LTLOK1         5A  O  5  2
     A            LTLOK2        20A  O  5  8
     A            LTANR         15A  O  5 29
E03  A            LTSALD         7  0O  5 45EDTCDE(4)
     A N27                                  DSPATR(ND)
E03  A            WSOPTS         7  0B  5 53EDTCDE(4)
     A  28                                  DSPATR(PR)
E03  A            LTDIFF         7  0O  5 61EDTCDE(L)
     A N27                                  DSPATR(ND)
     A                                      COLOR(WHT)
E03  A            LTSTAT         1A  O  5 70
     A N27                                  DSPATR(ND)
     A  27                                  COLOR(YLW)
N05  A            LTBAT1         8A  O  5 72
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0016)
     A                                      SFLPAG(0015)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                      CA03(03 'AVSLUTT')
     A                                      CA05(05 'Frisk')
N01  A                                      CF07(07 'Print')
     A                                      CF10(10 'MATCHE')
     A                                      CA12(12 'AVBRYT')
     A  27                                  CA23(23 'Oppdater')
     A                                      OVERLAY
     A                                  1  2'SYSPED/LL047R'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(40 YBC1552 SYMSGF)
     A                                      DSPATR(HI)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                  1 70TIME
     A            ZRECNO         4S 0O  2 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  2  2MSGCON(11 YBC1555 SYMSGF)
     A            WSGRUP         8Y 0O  2 14EDTCDE(4)
     A                                      COLOR(BLU)
     A            NVNGRU        30A  O  2 24COLOR(BLU)
N02  A                                  2 59MSGCON(10 YBC1556 SYMSGF)
N02  A            WSLNR          7Y 0O  2 70EDTCDE(4)
N02  A                                      COLOR(YLW)
     A                                  3  2MSGCON(11 YBC1557 SYMSGF)
     A            WSKJED         8Y 0O  3 14EDTCDE(4)
     A                                      COLOR(BLU)
     A            NVNKJE        30A  O  3 24COLOR(BLU)
     A                                  3 59MSGCON(10 YBC1558 SYMSGF)
     A            WSLTDT         8Y 0O  3 70EDTWRD('    .  .  ')
     A                                      COLOR(YLW)
     A  27                              4  2MSGCON(78 YBC1559 SYMSGF)
     A                                      DSPATR(HI)
     A N27                              4  2MSGCON(78 YBC1560 SYMSGF)
     A                                      DSPATR(HI)
      *
     A          R BUNN
S01  A*                                23  2'F3=Avslutt   F5=Frisk   F10=Match -
S01  A*                                       F12=Avbryt'
N01  A                                 23  2MSGCON(57 YBC1561 SYMSGF)
     A                                      COLOR(BLU)
E01  A  27                             23 62MSGCON(18 YBC1562 SYMSGF)
     A                                      COLOR(BLU)
N04  A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
N04  A                                      COLOR(WHT)
     A N30                             24 40'(C) COPYRIGHT  SYSTEMA A/S  2001  '
     A                                      COLOR(WHT)
N04  A            MSGNR          7A  P
      *
     A          R NYVARE
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CA03(03 'AVSLUTT')
     A                                      CA12(12 'AVBRYT')
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/LL047R'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(40 YBC1552 SYMSGF)
     A                                      DSPATR(HI)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                  1 70TIME
     A                                  2  2MSGCON(11 YBC1555 SYMSGF)
     A            WSGRUP         8Y 0O  2 14EDTCDE(4)
     A                                      COLOR(BLU)
     A            NVNGRU        30A  O  2 24COLOR(BLU)
N02  A                                  2 59MSGCON(10 YBC1556 SYMSGF)
N02  A            WSLNR          7Y 0O  2 70EDTCDE(4)
N02  A                                      COLOR(YLW)
     A                                  3  2MSGCON(11 YBC1557 SYMSGF)
     A            WSKJED         8Y 0O  3 14EDTCDE(4)
     A                                      COLOR(BLU)
     A            NVNKJE        30A  O  3 24COLOR(BLU)
     A                                  4  2MSGCON(78 YBC1560 SYMSGF)
     A                                      DSPATR(HI)
     A                                  6  2MSGCON(78 YBC1563 SYMSGF)
     A                                      COLOR(RED)
     A                                 23  2MSGCON(18 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A                                 24 40'(C) COPYRIGHT  SYSTEMA A/S  2001  '
     A                                      COLOR(WHT)
N01  A*
     A          R PRTDIFF
     A*%%TS  SD  20030102  091346  JOVO        REL-V5R1M0  5722-WDS
     A                                      WINDOW(*DFT 4 50 *NOMSGLIN)
     A                                      OVERLAY
     A                                      CA12(12 'AVBRYT')
     A                                  1  2'LL049R'
     A                                      COLOR(BLU)
     A                                  1 10MSGCON(015 YBC1564 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A            WSPRT         10A  B  3 18DSPATR(UL)
     A  90                                  DSPATR(HI)
     A  90                                  DSPATR(PC)
     A N90                              4  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  90                              4  2MSGCON(048 YBC1566 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  3  2MSGCON(015 YBC1565 *LIBL/SYMSGF)
