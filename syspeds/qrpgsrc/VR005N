SY    *   SYSTEMA ENDRINGER MERKET SY I MARGEN         ***************
      *****************************************************************
      *   5797-AWD    IBM NORWAY 1988                                 *
      *   5797-AWD    COPYRIGHT IBM CORP 1988                         *
      *   REFER TO INSTRUCTIONS ON COPYRIGHT NOTICE NO. G120-2083     *
      *****************************************************************
      *****************************************************************
      **      Det er foretatt endringer i dette program.              *
E55   **      Endringer er foretatt ifølge endringsmelding nr: 55     *
      *****************************************************************
      *
      *       PROGRAMMET TESTER FIRMAIDENTIFIKASJON OG BYGGER OPP
      *       LOCAL DATA AREA FOR DENNE BRUKEREN
      *       STANDARD VERDIER VEDRØRENE SKRIVERE BLIR LAGT INN
      *       I LOCAL DATA AREA
      *
      *       PROGRAMMET HAR EN TABELL ARRC FOR COPYRIGHT
      *
      *       PROGRAMMET VR001 KALLES FOR Å SETTE STANDARD PARAMETRE
      *       FOR UTSKRIFTER I LOCAL DATA AREA. PARAMETER IN03 BRUKES
      *       FOR Å TESTE OM PROGRAMMET ER AVBRUTT.
      *
      *       INDIKATORER I BRUK.
      *             03 = AVLUTT PROGRAMMET
      *             05 = ANNULLERING AV INNTASTEDE DATA
      *             30 = PROTECT PÅ FIRMAGRUPPE
      *             60 = BRUKER ER IKKE REGISTRERT
N24P  *             63 = ARKIVPOST I SYSXF FINNES IKKE
      *             64 = ARBEIDSSTASJON (SKRIVER) ER IKKE REGISTRERT
      *             65 = FIRMAKODE   ER IKKE REGISTRERT
      *             66 = FIRMABEGREP ER IKKE REGISTRERT
      *             67 = FIRMABEGREP OG BRUKER ER IKKE REGISTRERT
      *             68 = DET FINNES IKKE FLER RECORDS PÅ SAMME BRUKER
      *                  I USERF
      *             69 = FEILMELDING, BRUKER FINNES IKKE
      *             70 = FEILMELDING, FEIL DATOFORMAT
      *             76 = Brukes i autostart 3.P-meny
      *             78 = Brukes i autostart 3.P-meny
      *             88 = AVSLUTT BILDEBEHANDLING
      *             89 = INDIKATOR FOR OVERRIDE PÅ BILDET
      *
      *       SKJERMBILDEFILEN VR005D01 HAR BILDE FOR INNTASTING AV
      *       FIRMABEGREP.  (FIRMAGRUPPE, FIRMAKODE).
      *
      *       FØLGENDE FEIL MELDINGER GIS:
      *
      *       70     DATOFORMAT MÅ VÆRE DDMMÅÅ ELLER ÅÅMMDD
      *       69     BRUKER HAR IKKE ADGANG TIL Å KJØRE PÅ DETTE FIRMA
      *       67     BRUKER/FIRMAIDENT FINNES IKKE
      *
SY   F*VR005D01  CF   E             WORKSTN
SY   FVR005D01  CF   E             WORKSTN USROPN
     F                                     INFDS(FEILDS)
     F                                     INFSR(FEILSR)
     FSYSAL01   IF   E           K DISK
     FUSERL02   IF   E           K DISK
     FUSERL03   IF   E           K DISK
     F                                     RENAME(USERR:USEROK)
     FSKIDL01   IF   E           K DISK
N24P FSYSXL01   IF   E           K DISK
     FTPMER5BF  UF   E           K DISK    USROPN
     FFEILLOGG  O    E             PRINTER USROPN
      *
      /EJECT
     D ARRC            S             60    DIM(4) CTDATA PERRCD(1)
      *
      *
      *----------------------------------------------------------------
      * LOCAL DATA AREA
      *----------------------------------------------------------------
      *
     D DATA1         EUDS                  EXTNAME(LDADS)
     D  IN301                301    301
E55  D  LDBIBL               451    700
     D  LDAUTO              1010   1012
N55   *****************************************************************
N55   * BIBL1 tilsvarer følgende posisjoner i LDAen:
N55   *
N55   * Feltnavn   Fra pos.  Til pos.
N55   * -----------------------------
N55   *  BIBLIO      451       460
N55   *  BIBLDA      461       470
N55   *  WIN03       471       471
N55   *  WSTART      472       472
N55   *  GMLBIB      473       482
N55   *  BIBLEN      483       492
N55   *  GMLEND      493       502
N55   *  ELI2SA      503       512
N55   *  GML2SA      513       522
N55   *  ELI3SA      523       532
N55   *  GML3SA      533       542
N55   *  ELI4SA      543       552
N55   *  GML4SA      553       562
N55   *  ELI5SA      563       572
N55   *  GML5SA      573       582
N55   *  ELI6SA      583       592
N55   *  GML6SA      593       602
N55   *  ELI7SA      603       612
N55   *  GML7SA      613       622
N55   *  ELI8SA      623       632
N55   *  GML8SA      633       642
N55   *  ELI9SA      643       652
N55   *  GML9SA      653       662
N55   *****************************************************************
     D BIBL1           DS
     D  BIBLIO                 1     10
     D  BIBLDA                11     20
     D  WIN03                 21     21
     D  WSTART                22     22
     D  GMLBIB                23     32
     D  BIBLEN                33     42
     D  GMLEND                43     52
     D  ELI2SA                53     62
     D  GML2SA                63     72
     D  ELI3SA                73     82
     D  GML3SA                83     92
     D  ELI4SA                93    102
     D  GML4SA               103    112
     D  ELI5SA               113    122
     D  GML5SA               123    132
N55  D  ELI6SA               133    142
N55  D  GML6SA               143    152
N55  D  ELI7SA               153    162
N55  D  GML7SA               163    172
N55  D  ELI8SA               173    182
N55  D  GML8SA               183    192
N55  D  ELI9SA               193    202
N55  D  GML9SA               203    212
     D                SDS
     D  ZPROG                  1     10
     D  ZPMSG                 40     46
     D  ZBIBLO                81     90
     D  ZJOBB                244    253
     D  ZBRKR                254    263
     D  ZJNR                 264    269
N24P  * DS for splitting av DAT1SX SYSXF
N24P D                 DS
N24P D  DAT1SX                 1     70
N24P D  WARKSC                 1      2
N24P  *
     D FEILDS          DS
     D  ZSTAT            *STATUS
     D  ZFILE            *FILE
     D  ZLINE                 30     37
     D  ZFMT                  38     45
     D  ZFMSG                 46     52
     D  ZDSP                 273    282
     D  CURLIN               370    371B 0
     D  ZMAJ                 401    402
     D  ZMIN                 403    404
      *----------------------------------------------------------------
      *   Info-ark
     D VC021           PR                  EXTPGM('VC021')
      /EJECT
      *
      *----------------------------------------------------------------
      *  KEY LISTS
      *----------------------------------------------------------------
      *
SY    * SKAL ALDRI ÅPNES-DENNE METODE GIR MINIMALE ENDRINGER
SY   C                   MOVE      'J'           BATCH             1
SY   C     BATCH         IFEQ      'N'
SY   C                   OPEN      VR005D01
SY   C                   END
     C     KEYA          KLIST                                                  <SYSAL01
     C                   KFLD                    WFKODE
     C                   KFLD                    WFIRMA
     C     KEYB          KLIST                                                  <USERL02
     C                   KFLD                    WFKODE
     C                   KFLD                    WFIRMA
     C                   KFLD                    OKUSER
     C     KEYC          KLIST                                                  <USERL03
     C                   KFLD                    OKUSER
N24P  *
N24P C     KEYD          KLIST                                                  <USERL02
N24P C                   KFLD                    WFKODE
N24P C                   KFLD                    WFIRMA
N24P C                   KFLD                    NOK1SX
N24P C                   KFLD                    NOK2SX
N24P  *
     C     *LIKE         DEFINE    WFKODE        WWFKOD
     C     *LIKE         DEFINE    WFIRMA        WWFIRM
     C                   MOVE      '0'           HLPI12            1
     C                   MOVE      *BLANKS       WSOK              1
     C                   MOVE      *BLANKS       WKODE             2
     C                   MOVE      *ZEROS        A                 2 0
     C                   MOVE      *ZEROS        B                 3 0
      /EJECT
      *
      *----------------------------------------------------------------
      *   Info-ark
     C***                CALLP     VC021
      *
      *----------------------------------------------------------------
      *   DEFINERING OG FLYTTING TIL HJELPEFELTER
      *----------------------------------------------------------------
      *
     C     *DTAARA       DEFINE    *LDA          DATA1
     C                   IN        *DTAARA
     C                   MOVE      WUSER         OKUSER
     C                   MOVE      WFKODE        WWFKOD
     C                   MOVE      WFIRMA        WWFIRM
SY   C                   SETOFF                                       0306
SY   C                   SETOFF                                       0405
      /EJECT
      *
      *        HENT GAMMELT BIBLIOTEK VED VALG AV NYTT FIRMA
      *
     C     WSTART        IFEQ      *BLANKS
     C     KEYA          CHAIN     SYSAL01                            66
     C                   MOVE      BIBLIO        GMLBIB
     C                   MOVE      BIBLEN        GMLEND
     C                   MOVE      ELI2SA        GML2SA
     C                   MOVE      ELI3SA        GML3SA
     C                   MOVE      ELI4SA        GML4SA
     C                   MOVE      ELI5SA        GML5SA
N55  C                   MOVE      ELI6SA        GML6SA
N55  C                   MOVE      ELI7SA        GML7SA
N55  C                   MOVE      ELI8SA        GML8SA
N55  C                   MOVE      ELI9SA        GML9SA
     C                   ENDIF
      *
      *        SJEKK OM DATOFORMAT ER GYLDIG
      *
     C     WDATOF        IFEQ      2
     C                   MOVE      *ON           *IN70
     C                   MOVE      *ON           *IN89
     C                   ENDIF
      *
      *        SJEKK OM BRUKER ER REGISTRERT
      *
     C     KEYC          SETLL     USERL03                                60
     C     *IN60         IFEQ      *OFF
     C                   MOVE      *ON           *IN69
     C                   MOVE      *ON           *IN89
     C                   ENDIF
      *
      *        SJEKK OM BRUKER ER REGISTRERT FOR FLERE FIRMA
      *
     C     *IN60         IFEQ      *ON
     C     *IN68         DOWEQ     *OFF
     C     KEYC          READE     USERL03                                68
     C     *IN68         IFEQ      *OFF
     C                   ADD       1             X                 2 0
     C                   ENDIF
     C                   ENDDO
     C                   ENDIF
     C     *IN89         IFEQ      *OFF
     C     X             ANDEQ     1
     C                   MOVE      FKODE         WFKODE
     C                   MOVE      FIRMA         WFIRMA
     C                   GOTO      DATA
     C                   ENDIF
      *
     C     LDAUTO        IFEQ      '¤¤¤'
     C                   OPEN      TPMER5BF                             78
     C                   CLEAR                   *IN78
     C                   MOVE      *ON           *IN76
     C                   READ      TPMER5BF                             7878
     C     *IN78         IFEQ      *ON
     C                   CLEAR                   *IN76
     C                   CLOSE     TPMER5BF                             78
     C                   ENDIF                                                  <*IN78
     C                   ENDIF                                                  <LDAUTO
      *
      *        VIS HEADING BILDE
      *
     C     START         TAG
      *
     C                   MOVE      *OFF          *IN88
     C                   WRITE     VR005B01
      *
      *        VIS BILDE FOR VALG AV FIRMA
      *
     C     WRUTIN        IFEQ      'A'
     C                   MOVE      *ON           *IN30
     C                   MOVE      '1'           IN301
     C                   ENDIF
      *
     C     *IN89         IFEQ      *ON
N24  C                   WRITE     VR005B02
     C                   EXFMT     VR005B02
     C                   MOVE      *ON           *IN03
     C                   ENDIF
     C     *IN05         IFEQ      *ON
     C                   MOVE      *ON           *IN03
     C                   ENDIF
     C     *IN03         IFEQ      *ON
     C                   GOTO      DATA2
     C                   ENDIF
      *
      *        VIS BILDE FOR VALG AV FIRMA
      *
     C     *IN88         DOWEQ     *OFF
     C     VISB02        TAG
      *
      * Hvis fil for auto er OPEN, les denne.
      *
     C     *IN76         IFEQ      *ON
     C     *IN78         DOWEQ     *OFF
     C     R5BID         IFEQ      'A10'
     C                   SELECT
     C     R5BSEQ        WHENEQ    1
     C                   MOVEL     R5BDTA        WFKODE
     C     R5BSEQ        WHENEQ    2
     C                   MOVEL     R5BDTA        WFIRMA
     C                   ENDSL
     C                   DELETE    TPMER5BF
     C                   ENDIF                                                  <R5BID
     C                   READ      TPMER5BF                               78
     C                   ENDDO                                                  <*IN78
     C                   WRITE     VR005B02
     C     *IN78         IFEQ      *ON
     C                   CLEAR                   *IN76
     C                   CLOSE     TPMER5BF
     C                   ENDIF                                                  <*IN78
      *
     C                   ELSE                                                   <*IN76
     C                   WRITE     VR005B01
     C                   EXFMT     VR005B02
     C                   ENDIF                                                  <*IN76
     C                   MOVE      *OFF          *IN89
     C                   MOVE      *OFF          *IN67
     C                   MOVEA     '00'          *IN(71)
      *
      *        AVBRYT PROGRAMMET
      *
     C     *IN03         IFEQ      *ON
     C                   MOVE      WWFKOD        WFKODE
     C                   MOVE      WWFIRM        WFIRMA
     C                   GOTO      DATA2
     C                   ENDIF
      *
      *        SJEKK OM F4 ER TASTET PÅ FIRMAGRUPPE
      *
     C     *IN04         IFEQ      *ON
     C                   Z-ADD     CURLIN        CURLI1            5 0
     C     CURLI1        DIV       256           LINJE             2 0
     C     LINJE         MULT      256           CURLI2            5 0
     C     CURLI1        SUB       CURLI2        POSISJ            3 0
     C     LINJE         IFEQ      7
     C                   MOVE      'G'           WSOK
     C                   Z-ADD     8             A
     C                   Z-ADD     33            B
     C                   CALL      'VR900'                                      <Søk på firmakode
     C     *IN12         PARM      *IN12         HLPI12
     C     WSOK          PARM      WSOK          WSOK
     C     WKODE         PARM      WKODE         WKODE
     C     A             PARM      A             A
     C     B             PARM      B             B
     C     HLPI12        IFEQ      '0'
     C                   MOVE      WKODE         WFKODE
     C                   ENDIF
     C                   GOTO      VISB02
     C                   ELSE
      *
      *        SJEKK OM F4 ER TASTET PÅ FIRMAKODE
      *
     C     LINJE         IFEQ      9
     C     WFKODE        IFNE      *BLANKS
     C     WFKODE        CHAIN     SYSAL01                            71
     C     *IN71         CABEQ     *ON           VISB02
     C                   MOVE      BIBLDA        WDATLB           10
     C                   CALL      'VC010'
     C                   PARM      WDATLB        WDATLB
     C                   MOVE      *ON           *IN72
     C                   ELSE
     C                   MOVE      *ON           *IN71
     C                   GOTO      VISB02
     C                   ENDIF
     C                   MOVE      *BLANKS       WSOK
     C                   MOVE      WFKODE        WKODE
     C                   Z-ADD     10            A
     C                   Z-ADD     30            B
     C                   CALL      'VR900'                                      <Søk på firmakode
     C     *IN12         PARM      *IN12         HLPI12
     C     WSOK          PARM      WSOK          WSOK
     C     WKODE         PARM      WKODE         WKODE
     C     A             PARM      A             A
     C     B             PARM      B             B
     C     HLPI12        IFEQ      '0'
     C                   MOVE      WKODE         WFIRMA
     C                   ENDIF
     C     *IN72         IFEQ      *ON
     C                   CALL      'VC010A'
     C                   PARM      WDATLB        WDATLB
     C                   ENDIF
     C                   GOTO      VISB02
     C                   ELSE
     C                   GOTO      VISB02
     C                   ENDIF
     C                   ENDIF
     C                   ENDIF
      *
      *        ANNULLER REGISTRERINGEN
      *
     C     *IN05         IFEQ      *ON
     C                   MOVE      *BLANKS       WFKODE
     C                   MOVE      *BLANKS       WFIRMA
     C                   ELSE
      *
      *        SJEKK FIRMABEGREP OG BRUKER
      *
     C     KEYB          CHAIN     USERL02                            67
     C     *IN67         IFEQ      *ON
     C                   MOVE      *ON           *IN89
     C                   ELSE
     C                   MOVE      *ON           *IN88
     C                   ENDIF
     C                   ENDIF
     C                   ENDDO
      *
      *        AVSLUTT PROGRAMMET
      *
      *
     C     DATA          TAG
      *
      *        LES INN OPPLYSNINGER FOR LOCAL DATA AREA
      *
     C     KEYA          CHAIN     SYSAL01                            66
      *
     C     WSKID         CHAIN     SKIDL01                            64
      *
     C     *IN66         IFEQ      *OFF
     C                   MOVE      FIRMPL        WFIRMP
     C                   MOVEL     BIBL1         LDBIBL
     C                   ENDIF
     C                   MOVE      SKTYPE        WSKTYP
     C                   Z-ADD     1             WANTKO
     C                   Z-ADD     48            WLENGD
     C                   MOVE      *BLANKS       WSAVE
     C                   MOVE      *BLANKS       WHOLD
     C     TEG1SK        IFNE      *BLANKS
     C                   MOVE      TEG1SK        WTEGN
     C                   ELSE
     C     TEG2SK        IFNE      *BLANKS
     C                   MOVE      TEG2SK        WTEGN
     C                   ELSE
     C     TEG3SK        IFNE      *BLANKS
     C                   MOVE      TEG3SK        WTEGN
     C                   ELSE
     C     TEG4SK        IFNE      *BLANKS
     C                   MOVE      TEG4SK        WTEGN
     C                   ENDIF                                                  <TEG4SK
     C                   ENDIF                                                  <TEG3SK
     C                   ENDIF                                                  <TEG2SK
     C                   ENDIF                                                  <TEG1SK
     C     BIBLDA        IFNE      *BLANKS
     C                   MOVE      BIBLDA        WBIBLD
     C                   ELSE
     C                   MOVE      BIBLIO        WBIBLD
     C                   ENDIF
     C                   MOVE      ALLOK         WALLOK
     C     FIRMPL        IFEQ      *ZEROS
     C                   MOVE      'N'           WALLOK
     C                   ENDIF
N24P  *
N24P  * LEGGER INN OPPLYSN. ANG. ARKIV/SCANNING I LDA'EN.
N24P  *        POS. 223 = ARKIV
N24P  *        POS. 224 = SCANNING
N24P  *
N24P C                   MOVE      'D'           NOK1SX
N24P C                   MOVE      'ARKIV '      NOK2SX
N24P C     KEYD          CHAIN     SYSXL01                            63
N24P  *
N24P C     *IN63         IFEQ      *ON
N24P C                   MOVE      *BLANKS       DAT1SX
N24P C                   ENDIF
N24P  *
N24P C                   MOVEL     WARKSC        FLTALD
      *
     C                   MOVE      *BLANKS       SKRILD
     C                   MOVEL     'STANDARD'    SKRILD
     C                   MOVE      *BLANKS       RUTNLD
     C                   MOVEL     'STANDARD'    RUTNLD
     C                   MOVE      *BLANKS       JOBQLD
     C                   MOVEL     'QBATCH'      JOBQLD
     C                   MOVE      *BLANKS       HJOBLD
     C                   OUT       *DTAARA
      *
      *        GÅ TIL PROGRAM FOR REGISTRERING AV SKRIVEROPPLYSNINGER
      *
SY   C*                  CALL      'VR001'
SY   C                   CALL      'VR001N'
     C                   PARM      '9'           WIN01             1
     C                   PARM                    WIN03
      *
      *        WIN01 = 1 ANGIR AT SKRIVEROPPLYSNINGER IKKE ER REGISTRERT
      *
     C     WIN01         CABEQ     '1'           START
      *
      *        FLYTT VALGTE VERDIER TIL STANDARD I LDA
      *
     C                   IN        *DTAARA
      *
     C     WIN03         IFNE      '1'
     C                   MOVE      WWSKID        WSKID
     C                   MOVE      WWSKKO        WSKKO
     C                   MOVE      WWANTK        WANTKO
     C                   MOVE      WWTEGN        WTEGN
     C                   MOVE      WWLENG        WLENGD
N24   *
N24  C     WWOVFL        IFLE      *ZEROS
N24  C     WWOVFL        ORGT      WLENGD
N24  C     WLENGD        SUB       6             WWOVFL
N24   *
N24  C     WWOVFL        IFLE      *ZEROS
N24  C                   Z-ADD     WLENGD        WWOVFL
N24  C                   ENDIF                                                  <WWOVFL
N24   *
N24  C                   ENDIF                                                  <WWOVFL...
N24   *
     C                   MOVE      WWSAVE        WSAVE
     C                   MOVE      WWHOLD        WHOLD
     C                   ENDIF
      *
     C     DATA2         TAG
     C     *IN03         IFEQ      *ON
     C                   MOVE      *ON           WIN03
     C                   ENDIF
     C     *IN76         IFEQ      *ON
     C                   CLOSE     TPMER5BF                             78
     C                   ENDIF                                                  <*IN76
     C                   MOVEL     BIBL1         LDBIBL
     C                   MOVE      *ON           *INLR
     C/EJECT
      *     FEILRUTINE DERSOM SKJERMEN FALLER UT
      *
     C     FEILSR        BEGSR
     C                   OPEN      FEILLOGG
     C                   MOVEL     ARRC(1)       ZMSG1
     C                   MOVEL     ARRC(2)       ZMSG2
     C                   WRITE     FEIL
      * LUKK FEILLOGG, EVT. ANDRE BRUKER KONTROLLERTE FILER, OG
      * ALLE RPG KONTROLLERTE FILER
     C                   CLOSE     FEILLOGG
     C                   CLOSE     VR005D01
      * UTFØR KALL TIL PROGRAM VC006 RETT FRA RPG.
     C                   Z-ADD     15            LEN              15 5
     C                   MOVEL     ARRC(3)       KOMM             60
     C                   CALL      'QCMDEXC'
     C                   PARM                    KOMM
     C                   PARM                    LEN
     C                   ENDSR
      /EJECT
      *
**  COPYRIGHT LOAD
PÅ GRUNN AV BRUDD MED SKJERM ER OPPSTARTINGSPROGRAMM
AVBRUTT. SJEKK HVORFOR SKJERMEN ER UTE AV FUNKSJON.
CALL PGM(VC006)
5797-AWD IBM NORWAY 1988  5797-AWD COPYRIGHT IBM CORP. 1988
