     A          R GSS039PA
     A            KFMT1         75      1  1
     A            KFMT2         75      1  1
     A N52        STRI03        70      2  1
     A N52        STRI01        70      2 20
     A                                  2 45'STOCK REPORT (DANGEROUS GOODS)'
     A                                  4  2'SHIPPER:'
     A            KNAVN         30      4 16
     A            FINM          30      4 60
     A            ADR1          30      5 16
     A            FIAD1         30      5 60
     A            ADR2          30      6 16
     A            FIAD2         30      6 60
     A            POSTNR         4  0   7 16
     A            ADR3          24      7 24
     A            FIAD3         30      7 60
     A                                  9  2'MASTER:'
     A            CBNAB         30      9 16
     A                                  9 60'DATE:'
     A                                  9 66DATE EDTCDE(Y)
     A                                  9 77'PAGE:'
     A            XSIDE          3  0   9 84
      *
     A          R GSS039PA2
     A N52        STRI02        70     11  1
     A                                 11  2'-------------------------'
     A                                 11 27'-------------------------'
     A                                 11 52'-------------------------'
     A                                 11 77'-------------------------'
     A                                 11102'------------------'
     A                                 12  2'SUPPLIER'
     A                                 12 32'|P. ORDER NO.'
     A                                 12 53'|DATE'
     A                                 12 62'|CONTENTS'
     A                                 12 83'|PCS'
     A                                 12 89'|WEIGHT'
     A                                 12 97'|LOCATION'
     A                                 13  2'-------------------------'
     A                                 13 27'-------------------------'
     A                                 13 52'-------------------------'
     A                                 13 77'-------------------------'
     A                                 13102'------------------'
      *
     A          R GSS039PB                  SPACEB(1)
     A                                     4'UN:'
N01  A       N88  FGFA11         7         8
N01  A       N88  FGFA12         4        16SPACEA(1)
N01  A        88  DGUNNR         4S 0      8
N01  A        88  DGPSN         60        14SPACEA(1)
     A            ORSNM         30A        2
     A            XCONO1        20A       33
     A            XDTL           6  0     54EDTWRD('  .  .  ')
     A            XDSC20        20A       63
     A            ORCLI          5  0     84EDTCDE(Z)
     A            ORWT           7  1     89EDTCDE(3)
     A            XLOK          20A       98
      *
     A          R GSS039PC                  SPACEB(1)
     A  40        XSTATU        15A        2
     A  40        XTNO          12A       18
     A            XCON          20A       33
     A            GDDSC         55A       63
      *
     A          R GSS039PD                  SPACEB(2)
     A                                     2'STORED AT'
     A                                      HIGHLIGHT
     A            XLCNV         30A       12HIGHLIGHT
     A                                      SPACEA(1)
      *
     A          R GSS039PE                  SPACEB(1)
     A            GDDSC         55A       63
