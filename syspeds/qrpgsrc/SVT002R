********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 CB  PTF10 RECORDTYPE 'A' TILFØYD
********************************************************************
     FEDIRE1K   UF   E           K DISK
     FSVTAR1    UF A E           K DISK
     FSVTAV     UF A E           K DISK
     FSVTKD     UF A E           K DISK
     FSVTLK     UF A E           K DISK
     FSVTVK     UF A E           K DISK
     FSVTVS     UF A E           K DISK
      *
     D TABIN           S              1    DIM(29) CTDATA PERRCD(1)
     D TABUT           S              1    DIM(29) ALT(TABIN)
      *____________________
      * DS for input string
      *""""""""""""""""""""
     D                 DS
     D  RDATA                  1   1024
     D  TIN                    1    265    DIM(265)
     D  SVTA_TYPE              1      1
     D  SVTA_RT               10     12
     D                 DS
     D  T200                   1    200
     D  TB200                  1    200    DIM(200)
N01   *____________________
N01   * POSTTYPE = A
N01   *""""""""""""""""""""
N01  D                 DS
N01  D  SVTX_POST              1    265
N01  D  SVTX_TYPEA             1      1
N01  D  SVTX_ST                2      2
N01  D  SVTX_IE                3      3
N01  D  SVTX_VATA              4     13
N01  D  SVTX_DTS              14     21  0
N01  D  SVTX_DTE              22     29
N01  D* DUMMY Langt ut i record for å få blank
N01  D SVTX_MTI        S              6
N01  D SVTX_RG         S              4
N01  D SVTX_ACT        S              1
N01  D SVTX_AC         S              3
N01  D SVTX_ON         S              6
N01  D SVTX_TCC        S              1
N01  D SVTX_SEKV       S              3S 0
      *____________________
      * POSTTYPE = B
      *""""""""""""""""""""
     D                 DS
     D  SVTB_POST              1    265
     D  SVTB_TYPE              1      1
     D  SVTB_ST                2      2
     D  SVTB_IE                3      3
     D  SVTB_VATA              4     13
     D  SVTB_MTI              14     19
     D  SVTB_RG               20     23
     D  SVTB_ACT              24     24
     D  SVTB_AC               25     27
     D  SVTB_ON               28     33
     D  SVTB_TCC              34     34
     D  SVTB_DTS              35     42  0
     D  SVTB_SEKV             43     45  0
     D  SVTB_BESL             46     46  0
     D  SVTB_DTE              47     54
     D  SVTB_RGID             55     62
     D  SVTB_OJI              63     67
     D  SVTB_OJP              68     71
     D  SVTB_JRI              72     79
     D  SVTB_DTQ              80     87
     D  SVTB_TKD              88     89
     D  SVTB_SAT              90     99  3
     D  SVTB_VAL             100    102
     D  SVTB_MUC             103    105
     D  SVTB_MUQ             106    106
     D  SVTB_VILK            107    107  0
     D  SVTB_CTC             108    109
     D  SVTB_SN              110    112
     D  SVTB_CAMO            113    122  3
     D  SVTB_CVAL            123    125
     D  SVTB_CMU             126    128
     D  SVTB_CMUC            129    129
     D  SVTB_ACTC            130    132
     D  SVTB_CTCI            133    133
     D  SVTB_CRI             134    136
     D  SVTB_RGEX            137    140
     D  SVTB_FTI             141    143
     D  SVTB_FI              144    148
     D  SVTB_SDTS            149    156
     D  SVTB_SDTE            157    164
      *____________________
      * POSTTYPE = C
      *""""""""""""""""""""
     D                 DS
     D  SVTC_POST              1    265
     D  SVTC_TYPE              1      1
     D  SVTC_ST                2      2
     D  SVTC_LK                3      4
     D  SVTC_RG                7     10
     D  SVTC_DTS              11     18  0
     D  SVTC_DTE              19     26
     D  SVTC_SDTS             27     34
     D  SVTC_SDTE             35     42
      *____________________
      * POSTTYPE = D
      *""""""""""""""""""""
     D                 DS
     D  SVTD_POST              1    265
     D  SVTD_TYPE              1      1
     D  SVTD_ST                2      2
     D  SVTD_VATA              3     12
     D  SVTD_VATA2             5     12
     D  SVTD_VATA3             7     12
     D  SVTD_PL               13     14
     D  SVTD_LC               15     16
     D  SVTD_DTS              17     24  0
     D  SVTD_SEKV             25     27
     D  SVTD_DTE              28     35
     D  SVTD_QI               36     37
     D  SVTD_TXT              38    237
      *____________________
      * POSTTYPE = H
      *""""""""""""""""""""
     D                 DS
     D  SVTH_POST              1    265
     D  SVTH_TYPE              1      1
     D  SVTH_ST                2      2
     D  SVTH_KD                3      5
     D  SVTH_DTS               6     13  0
     D  SVTH_DTE              14     21
     D  SVTH_KRS              22     31  6
     D                 DS
     D  SVTO_POST              1    265
     D  SVTO_TYPE              1      1
     D  SVTO_ST                2      2
     D  SVTO_VATA              3     12
     D  SVTO_ACT              13     13
     D  SVTO_AC               14     16
     D  SVTO_REGI             17     20
     D  SVTO_MTI              21     23
     D  SVTO_KD               24     26
     D  SVTO_DTS              27     34  0
     D  SVTO_LOP              35     36
     D  SVTO_SEKV             37     39  0
     D  SVTO_DTE              40     47
     D  SVTO_SAT              48     57  4
     D  SVTO_VAL              58     60
     D  SVTO_ENH              61     63
     D  SVTO_MUQ              64     64
     D  SVTO_UR               65     65
     D  SVTO_DESC             66    265
     D                 DS
     D  SVTR_POST              1    265
     D  SVTR_TYPE              1      1
     D  SVTR_ST                2      2
     D  SVTR_IE                3      3
     D  SVTR_TYP               4      6
     D  SVTR_KD                7     16
     D  SVTR_LC               17     18
     D  SVTR_DTS              19     26  0
     D  SVTR_SEKV             27     29
     D  SVTR_DTE              30     37
     D  SVTR_MTSI             38     38
     D  SVTR_KB               39     58
     D  SVTR_LB               59    158
     D                 DS
     D  SVTT_POST              1    265
     D  SVTT_TYPE              1      1
     D  SVTT_ST                2      2
     D  SVTT_KD                3      5
     D  SVTT_DTS               6     13  0
     D  SVTT_DTE              14     21
     D  SVTT_OMR              22     28  0
     D  SVTT_KRS              29     38  6
      *//////////////
      *  Hodelogikk /
      *//////////////
     C                   EXSR      SRA
     C                   EXSR      SRB
     C                   MOVE      '1'           *INLR
      *////////////////////////////////////////////////////////////
      *  SRB                                                  SRB /
      *////////////////////////////////////////////////////////////
     C     SRB           BEGSR
     C     *IN51         DOWEQ     '0'
      *
      * UPCASE
     C     1             DO        265           XN                3 0
     C     TIN(XN)       LOOKUP    TABIN         TABUT                    33
     C                   IF        *IN33
     C                   MOVE      TABUT         TIN(XN)
     C                   ENDIF
     C                   ENDDO
      *
     C                   SELECT
N01  C                   WHEN      XPOSTTYPE = 'A'
N01  C                   EXSR      SRPA
     C                   WHEN      XPOSTTYPE = 'B'
     C                   EXSR      SRPB
     C                   WHEN      XPOSTTYPE = 'C'
     C                   EXSR      SRPC
     C                   WHEN      XPOSTTYPE = 'D'
     C                   EXSR      SRPD
     C                   WHEN      XPOSTTYPE = 'H'
     C*                  EXSR      SRPH
     C                   WHEN      XPOSTTYPE = 'O'
     C                   EXSR      SRPO
     C                   WHEN      XPOSTTYPE = 'R'
     C                   EXSR      SRPR
     C                   WHEN      XPOSTTYPE = 'T'
     C                   EXSR      SRPT
     C                   ENDSL
     C                   DELETE    EDIRER
N01  C     WMBR          READE     EDIRE1K                                51
     C  N51              ENDDO
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Initiering                                           SRA /
      *////////////////////////////////////////////////////////////
     C     SRA           BEGSR
      *________________
      * Input parameter
      *""""""""""""""""
     C     *ENTRY        PLIST
     C                   PARM                    WMBR             10
     C     TARK          KLIST
     C                   KFLD                    SVTB_IE
     C                   KFLD                    SVTB_VATA
     C                   KFLD                    SVTB_MTI
     C                   KFLD                    SVTB_RG
     C                   KFLD                    SVTB_ACT
     C                   KFLD                    SVTB_AC
     C                   KFLD                    SVTB_ON
     C                   KFLD                    SVTB_TCC
     C                   KFLD                    SVTB_SEKV
     C     TARKDEL       KLIST
     C                   KFLD                    SVTB_IE
     C                   KFLD                    SVTB_VATA
     C                   KFLD                    SVTB_MTI
     C                   KFLD                    SVTB_RG
     C                   KFLD                    SVTB_ACT
     C                   KFLD                    SVTB_AC
     C                   KFLD                    SVTB_ON
     C                   KFLD                    SVTB_TCC
     C                   KFLD                    SVTB_SEKV
     C                   KFLD                    SVTB_DTS
N01  C     TARKDELX      KLIST
N01  C                   KFLD                    SVTX_IE
N01  C                   KFLD                    SVTX_VATA
N01  C                   KFLD                    SVTX_MTI
N01  C                   KFLD                    SVTX_RG
N01  C                   KFLD                    SVTX_ACT
N01  C                   KFLD                    SVTX_AC
N01  C                   KFLD                    SVTX_ON
N01  C                   KFLD                    SVTX_TCC
N01  C                   KFLD                    SVTX_SEKV
N01  C                   KFLD                    SVTX_DTS
N01  C     TARKX         KLIST
N01  C                   KFLD                    SVTX_IE
N01  C                   KFLD                    SVTX_VATA
N01  C                   KFLD                    SVTX_MTI
N01  C                   KFLD                    SVTX_RG
N01  C                   KFLD                    SVTX_ACT
N01  C                   KFLD                    SVTX_AC
N01  C                   KFLD                    SVTX_ON
N01  C                   KFLD                    SVTX_TCC
N01  C                   KFLD                    SVTX_SEKV
     C     TLKK          KLIST
     C                   KFLD                    SVTC_LK
     C                   KFLD                    SVTC_RG
     C     TVKK          KLIST
     C                   KFLD                    SVTH_KD
     C                   KFLD                    SVTH_DTS
     C     TAVK          KLIST
     C                   KFLD                    SVTO_VATA
     C                   KFLD                    SVTO_REGI
     C                   KFLD                    SVTO_KD
     C                   KFLD                    SVTO_DTS
     C     TKDK          KLIST
     C                   KFLD                    SVTR_IE
     C                   KFLD                    SVTR_TYP
     C                   KFLD                    SVTR_KD
     C                   KFLD                    SVTR_DTS
     C     TVKKT         KLIST
     C                   KFLD                    SVTT_KD
     C                   KFLD                    SVTT_DTS
      *
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVE      'J'           XINIT             1
      *                                                                         Test character
      * Test vilken type fil
     C     WMBR          SETLL     EDIRE1K
     C     WMBR          READE     EDIRE1K                                51
     C                   SELECT
N01  C                   WHEN      ((SVTA_RT = '_KA') OR
N01  C                              (SVTA_RT = '_DA'))
N01  C                   MOVE      'A'           XPOSTTYPE         1
     C                   WHEN      ((SVTA_RT = '_KB') OR
     C                              (SVTA_RT = '_DB'))
     C                   MOVE      'B'           XPOSTTYPE         1
     C                   WHEN      ((SVTA_RT = '_KC') OR
     C                              (SVTA_RT = '_DC'))
     C                   MOVE      'C'           XPOSTTYPE         1
     C                   WHEN      ((SVTA_RT = '_KD') OR
     C                              (SVTA_RT = '_DD'))
     C                   MOVE      'D'           XPOSTTYPE         1
     C                   WHEN      ((SVTA_RT = '_KH') OR
     C                              (SVTA_RT = '_DH'))
     C                   MOVE      'H'           XPOSTTYPE         1
     C                   WHEN      ((SVTA_RT = '_KO') OR
     C                              (SVTA_RT = '_DO'))
     C                   MOVE      'O'           XPOSTTYPE         1
     C                   WHEN      ((SVTA_RT = '_KR') OR
     C                              (SVTA_RT = '_DR'))
     C                   MOVE      'R'           XPOSTTYPE         1
     C                   WHEN      ((SVTA_RT = '_KT') OR
     C                              (SVTA_RT = '_DT'))
     C                   MOVE      'T'           XPOSTTYPE         1
     C                   ENDSL
     C     WMBR          READE     EDIRE1K                                51
     C                   ENDSR
N01   *////////////////////////////////////////////////////////////
N01   *  SRPA                                                SRPA /
N01   *////////////////////////////////////////////////////////////
N01  C     SRPA          BEGSR
N01   *
N01  C     SVTA_TYPE     IFEQ      'A'
N01  C                   EVAL      SVTX_POST = RDATA
N01  C*    SVTX_IE       CABNE     'E'           SLSRPA
N01   *
N01  C     SVTX_DTE      IFNE      *BLANKS
N01  C     SVTX_DTE      ANDLT     WDT
N01  C     SVTX_ST       IFEQ      'D'
N01  C     SVTX_ST       OREQ      'U'
N01  C     TARKDELX      CHAIN     SVTAR1                             33
N01  C  N33              DELETE    SVTARR
N01  C                   GOTO      SLSRPA
N01  C                   ENDIF
N01  C                   ENDIF
N01   *
N01  C     SVTX_DTE      IFEQ      *BLANKS
N01  C     SVTX_DTE      ORGT      WDT
N01   *
N01  C     TARKX         SETLL     SVTAR1
N01  C     TARKX         READE     SVTAR1                                 33
      *
     C                   IF        *IN33 = '1'
     C     SVTX_ST       CABEQ     'D'           SLSRPA
     C                   EVAL      SVTA_IE = SVTX_IE
     C                   EVAL      SVTA_VATA = SVTX_VATA
     C                   EVAL      SVTA_MTI = SVTX_MTI
     C                   EVAL      SVTA_RG = SVTX_RG
     C                   EVAL      SVTA_ACT = SVTX_ACT
     C                   EVAL      SVTA_AC = SVTX_AC
     C                   EVAL      SVTA_ON = SVTX_ON
     C                   EVAL      SVTA_TCC = SVTX_TCC
     C                   EVAL      SVTA_SEKV = SVTX_SEKV
     C                   ELSE
     C     SVTX_ST       IFEQ      'D'
     C     SVTX_DTS      IFEQ      SVTA_DTS
     C                   DELETE    SVTARR
     C                   ELSE
     C                   UPDATE    SVTARR
     C                   ENDIF
     C                   GOTO      SLSRPA
     C                   ENDIF
     C     SVTX_DTS      IFLT      SVTA_DTS
     C                   UPDATE    SVTARR
     C                   GOTO      SLSRPA
     C                   ENDIF
     C                   ENDIF
      *
     C                   EVAL      SVTA_DTS = SVTX_DTS
     C                   EVAL      SVTA_BESL = *ZEROS
     C     SVTX_DTE      IFEQ      *BLANKS
     C                   EVAL      SVTA_DTE = 99999999
     C                   ELSE
     C                   MOVE      SVTX_DTE      SVTA_DTE
     C                   ENDIF
     C                   EVAL      SVTA_RGID = *BLANKS
     C                   EVAL      SVTA_OJI = *BLANKS
     C                   EVAL      SVTA_OJP = *BLANKS
     C                   EVAL      SVTA_JRI = *BLANKS
     C                   EVAL      SVTA_DTQ = *BLANKS
     C                   EVAL      SVTA_TKD = *BLANKS
     C                   EVAL      SVTA_SAT = *ZEROS
     C                   EVAL      SVTA_VAL = *BLANKS
     C                   EVAL      SVTA_MUC = *BLANKS
     C                   EVAL      SVTA_MUQ = *BLANKS
     C                   EVAL      SVTA_VILK = *ZEROS
     C                   EVAL      SVTA_CTC = *BLANKS
     C                   EVAL      SVTA_SN = *BLANKS
     C                   EVAL      SVTA_CAMO = *ZEROS
     C                   EVAL      SVTA_CVAL = *BLANKS
     C                   EVAL      SVTA_CMU = *BLANKS
     C                   EVAL      SVTA_CMUC = *BLANKS
     C                   EVAL      SVTA_ACTC = *BLANKS
     C                   EVAL      SVTA_CTCI= *BLANKS
     C                   EVAL      SVTA_CRI = *BLANKS
     C                   EVAL      SVTA_RGEX = *BLANKS
     C                   EVAL      SVTA_FTI = *BLANKS
     C                   EVAL      SVTA_FI = *BLANKS
     C                   MOVE      *ZEROS        SVTA_SDTS
     C                   MOVE      99999999      SVTA_SDTE
      *
     C                   IF        *IN33 = '1'
     C                   WRITE     SVTARR
     C                   ELSE
     C                   UPDATE    SVTARR
     C                   ENDIF
      *
     C                   ENDIF
     C                   ENDIF
      *
     C     SLSRPA        ENDSR
      *////////////////////////////////////////////////////////////
      *  SRPB                                                SRPB /
      *////////////////////////////////////////////////////////////
     C     SRPB          BEGSR
      *
     C     SVTA_TYPE     IFEQ      'B'
     C                   EVAL      SVTB_POST = RDATA
      *
     C     SVTB_DTE      IFNE      *BLANKS
     C     SVTB_DTE      ANDLT     WDT
     C     SVTB_ST       IFEQ      'D'
     C     SVTB_ST       OREQ      'U'
     C     TARKDEL       CHAIN     SVTAR1                             33
     C  N33              DELETE    SVTARR
     C                   GOTO      SLSRPB
     C                   ENDIF
     C                   ENDIF
      *
     C     SVTB_DTE      IFEQ      *BLANKS
     C     SVTB_DTE      ORGT      WDT
      *
     C     TARK          SETLL     SVTAR1
     C     TARK          READE     SVTAR1                                 33
      *
     C                   IF        *IN33 = '1'
     C     SVTB_ST       CABEQ     'D'           SLSRPB
     C                   EVAL      SVTA_IE = SVTB_IE
     C                   EVAL      SVTA_VATA = SVTB_VATA
     C                   EVAL      SVTA_MTI = SVTB_MTI
     C                   EVAL      SVTA_RG = SVTB_RG
     C                   EVAL      SVTA_ACT = SVTB_ACT
     C                   EVAL      SVTA_AC = SVTB_AC
     C                   EVAL      SVTA_ON = SVTB_ON
     C                   EVAL      SVTA_TCC = SVTB_TCC
     C                   EVAL      SVTA_SEKV = SVTB_SEKV
     C                   ELSE
     C     SVTB_ST       IFEQ      'D'
     C     SVTB_DTS      IFEQ      SVTA_DTS
     C                   DELETE    SVTARR
     C                   ELSE
     C                   UPDATE    SVTARR
     C                   ENDIF
     C                   GOTO      SLSRPB
     C                   ENDIF
     C     SVTB_DTS      IFLT      SVTA_DTS
     C                   UPDATE    SVTARR
     C                   GOTO      SLSRPB
     C                   ENDIF
     C                   ENDIF
      *
     C                   EVAL      SVTA_DTS = SVTB_DTS
     C                   EVAL      SVTA_BESL = SVTB_BESL
     C     SVTB_DTE      IFEQ      *BLANKS
     C                   EVAL      SVTA_DTE = 99999999
     C                   ELSE
     C                   MOVE      SVTB_DTE      SVTA_DTE
     C                   ENDIF
     C                   EVAL      SVTA_RGID = SVTB_RGID
     C                   EVAL      SVTA_OJI = SVTB_OJI
     C                   EVAL      SVTA_OJP = SVTB_OJP
     C                   EVAL      SVTA_JRI = SVTB_JRI
     C                   EVAL      SVTA_DTQ = SVTB_DTQ
     C                   EVAL      SVTA_TKD = SVTB_TKD
     C                   EVAL      SVTA_SAT = SVTB_SAT
     C                   EVAL      SVTA_VAL = SVTB_VAL
     C                   EVAL      SVTA_MUC = SVTB_MUC
     C                   EVAL      SVTA_MUQ = SVTB_MUQ
     C                   EVAL      SVTA_VILK = SVTB_VILK
     C                   EVAL      SVTA_CTC = SVTB_CTC
     C                   EVAL      SVTA_SN = SVTB_SN
     C                   EVAL      SVTA_CAMO = SVTB_CAMO
     C                   EVAL      SVTA_CVAL = SVTB_CVAL
     C                   EVAL      SVTA_CMU = SVTB_CMU
     C                   EVAL      SVTA_CMUC = SVTB_CMUC
     C                   EVAL      SVTA_ACTC = SVTB_ACTC
     C                   EVAL      SVTA_CTCI= SVTB_CTCI
     C                   EVAL      SVTA_CRI = SVTB_CRI
     C                   EVAL      SVTA_RGEX = SVTB_RGEX
     C                   EVAL      SVTA_FTI = SVTB_FTI
     C                   EVAL      SVTA_FI = SVTB_FI
     C     SVTB_SDTS     IFEQ      *BLANKS
     C                   MOVE      *ZEROS        SVTA_SDTS
     C                   ELSE
     C                   MOVE      SVTB_SDTS     SVTA_SDTS
     C                   ENDIF
     C     SVTB_SDTE     IFEQ      *BLANKS
     C     SVTA_SDTS     IFEQ      0
     C                   MOVE      *ZEROS        SVTA_SDTE
     C                   ELSE
     C                   MOVE      99999999      SVTA_SDTE
     C                   ENDIF
     C                   ELSE
     C                   MOVE      SVTB_SDTE     SVTA_SDTE
     C                   ENDIF
      *
     C                   IF        *IN33 = '1'
     C                   WRITE     SVTARR
     C                   ELSE
     C                   UPDATE    SVTARR
     C                   ENDIF
      *
     C                   ENDIF
     C                   ENDIF
      *
     C     SLSRPB        ENDSR
      *////////////////////////////////////////////////////////////
      *  SRPC                                                SRPC /
      *////////////////////////////////////////////////////////////
     C     SRPC          BEGSR
      *
     C                   EVAL      SVTC_POST = RDATA
      *
     C     SVTC_DTE      IFNE      *BLANKS
     C     SVTC_DTE      ANDLT     WDT
     C     SVTC_ST       ANDEQ     'D'
     C     TLKK          CHAIN     SVTLK                              33
     C  N33              DELETE    SVTLKR
     C                   GOTO      SLSRPC
     C                   ENDIF
      *
     C     SVTC_DTE      IFNE      *BLANKS
     C     SVTC_DTE      ANDLT     WDT
     C     SVTC_SDTE     ORNE      *BLANKS
     C     SVTC_SDTE     ANDLT     WDT
     C     SVTC_ST       IFEQ      'D'
     C     TLKK          CHAIN     SVTLK                              33
     C  N33              DELETE    SVTLKR
     C                   ENDIF
     C                   GOTO      SLSRPC
     C                   ENDIF
      *
     C     TLKK          CHAIN     SVTLK                              33
      *
     C                   IF        *IN33 = '1'
     C                   CLEAR                   SVTLKR
     C                   EVAL      SVLK_LK = SVTC_LK
     C                   EVAL      SVLK_RG = SVTC_RG
     C                   EVAL      SVLK_DTS = SVTC_DTS
     C                   ELSE
     C     SVTC_DTS      IFLT      SVLK_DTS
     C                   EVAL      SVLK_DTS = SVTC_DTS
     C                   ENDIF
     C                   ENDIF
      *
     C     SVTC_DTE      IFEQ      *BLANKS
     C                   EVAL      SVLK_DTE = 99999999
     C                   ELSE
     C                   MOVE      SVTC_DTE      SVLK_DTE
     C                   ENDIF
      *
     C     SVTC_SDTS     IFEQ      *BLANKS
     C                   MOVE      *ZEROS        SVLK_SDTS
     C                   ELSE
     C                   MOVE      SVTC_SDTS     SVLK_SDTS
     C                   ENDIF
      *
     C     SVTC_SDTE     IFEQ      *BLANKS
     C     SVLK_SDTS     IFEQ      0
     C                   MOVE      *ZEROS        SVLK_SDTE
     C                   ELSE
     C                   MOVE      99999999      SVLK_SDTE
     C                   ENDIF
     C                   ELSE
     C                   MOVE      SVTC_SDTE     SVLK_SDTE
     C                   ENDIF
      *
     C                   IF        *IN33 = '1'
     C                   WRITE     SVTLKR
     C                   ELSE
     C                   UPDATE    SVTLKR
     C                   ENDIF
      *
     C     SLSRPC        ENDSR
      *////////////////////////////////////////////////////////////
      *  SRPD                                                SRPD /
      *////////////////////////////////////////////////////////////
     C     SRPD          BEGSR
      *
     C                   EVAL      SVTD_POST = RDATA
      *
     C     SVTD_DTE      IFNE      *BLANKS
     C     SVTD_DTE      ANDLT     WDT
     C     SVTD_ST       ANDEQ     'D'
     C     SVTD_VATA     CHAIN     SVTVS                              33
     C  N33              DELETE    SVTVSR
     C                   GOTO      SLSRPD
     C                   ENDIF
      *
     C     SVTD_DTE      IFNE      *BLANKS
     C     SVTD_DTE      ANDLT     WDT
     C     SVTD_VATA2    OREQ      *ZEROS
     C     SVTD_ST       IFEQ      'D'
     C     SVTD_VATA     CHAIN     SVTVS                              33
     C                   DELETE    SVTVSR
     C                   ENDIF
     C                   GOTO      SLSRPD
     C                   ENDIF
      *
     C     SVTD_VATA     CHAIN     SVTVS                              33
      *
     C                   IF        *IN33 = '1'
     C                   CLEAR                   SVTVSR
     C                   EVAL      SVVS_VATA = SVTD_VATA
     C     SVTD_VATA3    IFEQ      *ZEROS
     C                   MOVE      *ZEROS        SVVS_VATAK
     C                   ELSE
     C                   MOVEL     SVVS_VATA     SVVS_VATAK
     C                   ENDIF
     C                   EVAL      SVVS_DTS = SVTD_DTS
     C                   ELSE
     C     SVTD_DTS      IFLT      SVVS_DTS
     C                   EVAL      SVVS_DTS = SVTD_DTS
     C                   ENDIF
     C                   ENDIF
      *
     C     SVTD_DTE      IFEQ      *BLANKS
     C                   EVAL      SVVS_DTE = 99999999
     C                   ELSE
     C                   MOVE      SVTD_DTE      SVVS_DTE
     C                   ENDIF
      *
     C                   MOVE      SVTD_TXT      T200
     C     1             DO        200           XN                3 0
     C     TB200(XN)     IFNE      '-'
     C     TB200(XN)     ANDNE     ' '
     C                   LEAVE
     C                   ENDIF
     C                   ENDDO
     C     XN            IFLT      200
     C                   MOVE      *BLANKS       SVTD_TXT
     C                   MOVEA     TB200(XN)     SVTD_TXT
     C                   ENDIF
      *
     C     SVTD_LC       IFEQ      'EN'
     C     SVVS_TXTE     IFEQ      *BLANKS
     C     SVTD_PL       OREQ      '10'
     C                   MOVEL     SVTD_TXT      SVVS_TXTE
     C                   ELSE
     C                   CAT       SVTD_TXT:1    SVVS_TXTE
     C                   ENDIF
     C                   ELSE
     C     SVVS_TXTS     IFEQ      *BLANKS
     C     SVTD_PL       OREQ      '10'
     C                   MOVEL     SVTD_TXT      SVVS_TXTS
     C                   ELSE
     C                   CAT       SVTD_TXT:1    SVVS_TXTS
     C                   ENDIF
     C                   MOVEL     SVVS_TXTS     SVVS_TXTK
     C                   ENDIF
      *
     C                   IF        *IN33 = '1'
     C                   WRITE     SVTVSR
     C                   ELSE
     C                   UPDATE    SVTVSR
     C                   ENDIF
      *
     C     SLSRPD        ENDSR
      *////////////////////////////////////////////////////////////
      *  SRPH                                                SRPH /
      *////////////////////////////////////////////////////////////
     C     SRPH          BEGSR
      *
     C                   EVAL      SVTH_POST = RDATA
      *
     C     SVTH_DTE      IFNE      *BLANKS
     C     SVTH_DTE      ANDLT     WDT
     C     SVTH_ST       ANDEQ     'D'
     C     TVKK          CHAIN     SVTVK                              33
     C  N33              DELETE    SVTVKR
     C                   GOTO      SLSRPH
     C                   ENDIF
      *
     C     SVTH_DTE      IFEQ      *BLANKS
     C     SVTH_DTE      ORGT      WDT
      *
     C     TVKK          CHAIN     SVTVK                              33
     C                   IF        *IN33 = '1'
     C                   EVAL      SVVK_KD = SVTH_KD
     C                   EVAL      SVVK_KRS = SVTH_KRS
     C                   EVAL      SVVK_DTS = SVTH_DTS
     C     SVTH_DTE      IFEQ      *BLANKS
     C                   EVAL      SVVK_DTE = 99999999
     C                   ELSE
     C                   MOVE      SVTH_DTE      SVVK_DTE
     C                   ENDIF
     C                   WRITE     SVTVKR
     C                   ENDIF
      *
     C                   ENDIF
      *
     C     SLSRPH        ENDSR
      *////////////////////////////////////////////////////////////
      *  SRPO                                                SRPO /
      *////////////////////////////////////////////////////////////
     C     SRPO          BEGSR
      *
     C                   EVAL      SVTO_POST = RDATA
      *
     C     SVTO_DTE      IFNE      *BLANKS
     C     SVTO_DTE      ANDLT     WDT
     C     SVTO_ST       ANDEQ     'D'
     C     TAVK          CHAIN     SVTAV                              33
     C  N33              DELETE    SVTAVR
     C                   GOTO      SLSRPO
     C                   ENDIF
      *
     C     SVTO_DTE      IFEQ      *BLANKS
     C     SVTO_DTE      ORGT      WDT
      *
     C     TAVK          CHAIN     SVTAV                              33
     C  N33SVTO_ST       IFEQ      'D'
     C                   DELETE    SVTAVR
     C                   GOTO      SLSRPO
     C                   ENDIF
      *
     C                   IF        *IN33 = '1'
     C                   EVAL      SVAV_VATA = SVTO_VATA
     C                   EVAL      SVAV_REGI = SVTO_REGI
     C                   EVAL      SVAV_KD = SVTO_KD
     C                   EVAL      SVAV_DTS = SVTO_DTS
     C                   ENDIF
      *
     C     SVTO_DTE      IFEQ      *BLANKS
     C                   EVAL      SVAV_DTE = 99999999
     C                   ELSE
     C                   MOVE      SVTO_DTE      SVAV_DTE
     C                   ENDIF
     C                   EVAL      SVAV_SAT = SVTO_SAT
     C                   EVAL      SVAV_VAL = SVTO_VAL
     C                   EVAL      SVAV_ENH = SVTO_ENH
     C                   IF        *IN33 = '1'
     C                   WRITE     SVTAVR
     C                   ELSE
     C                   UPDATE    SVTAVR
     C                   ENDIF
      *
     C                   ENDIF
      *
     C     SLSRPO        ENDSR
      *////////////////////////////////////////////////////////////
      *  SRPR                                                SRPR /
      *////////////////////////////////////////////////////////////
     C     SRPR          BEGSR
      *
     C                   EVAL      SVTR_POST = RDATA
      * Ikke oppdater Extra mængdenhet, denne tas manuellt.
     C     SVTR_TYP      IFNE      'MDM'

     C     SVTR_DTE      IFNE      *BLANKS
     C     SVTR_DTE      ANDLT     WDT
     C     SVTR_ST       IFEQ      'D'
     C     SVTR_ST       OREQ      'U'
     C     TKDK          CHAIN     SVTKD                              33
     C  N33              DELETE    SVTKDR
     C                   GOTO      SLSRPR
     C                   ENDIF
     C                   ENDIF
      *
     C     SVTR_DTE      IFEQ      *BLANKS
     C     SVTR_DTE      ORGT      WDT
      *
     C     TKDK          CHAIN     SVTKD                              33
     C  N33SVTR_ST       IFEQ      'D'
     C                   DELETE    SVTKDR
     C                   GOTO      SLSRPR
     C                   ENDIF
      *
     C                   IF        *IN33 = '1'
     C                   CLEAR                   SVTKDR
     C                   EVAL      SVKD_IE = SVTR_IE
     C                   EVAL      SVKD_TYP = SVTR_TYP
     C                   EVAL      SVKD_KD = SVTR_KD
     C                   EVAL      SVKD_DTS = SVTR_DTS
     C                   ENDIF
     C     SVTR_DTE      IFEQ      *BLANKS
     C                   EVAL      SVKD_DTE = 99999999
     C                   ELSE
     C                   MOVE      SVTR_DTE      SVKD_DTE
     C                   ENDIF
      *
     C     SVTR_LC       IFEQ      'EN'
     C     SVTR_KB       IFEQ      *BLANKS
     C                   MOVEL     SVTR_LB       SVKD_KBE
     C                   ELSE
     C                   EVAL      SVKD_KBE = SVTR_KB
     C                   ENDIF
     C                   EVAL      SVKD_LBE = SVTR_LB
     C                   ELSE
     C     SVTR_KB       IFEQ      *BLANKS
     C                   MOVEL     SVTR_LB       SVKD_KBS
     C                   ELSE
     C                   EVAL      SVKD_KBS = SVTR_KB
     C                   ENDIF
     C                   EVAL      SVKD_LBS = SVTR_LB
     C                   ENDIF
      *
     C                   IF        *IN33 = '1'
     C                   WRITE     SVTKDR
     C                   ELSE
     C                   UPDATE    SVTKDR
     C                   ENDIF
      *
     C                   ENDIF

     C                   ENDIF
      *
     C     SLSRPR        ENDSR
      *////////////////////////////////////////////////////////////
      *  SRPT                                                SRPT /
      *////////////////////////////////////////////////////////////
     C     SRPT          BEGSR
      *
     C                   EVAL      SVTT_POST = RDATA
      *
     C     SVTT_DTE      IFNE      *BLANKS
     C     SVTT_DTE      ANDLT     WDT
     C     TVKKT         CHAIN     SVTVK                              33
     C  N33              DELETE    SVTVKR
     C                   GOTO      SLSRPT
     C                   ENDIF
      *
     C     SVTT_DTE      IFEQ      *BLANKS
     C     SVTT_DTE      ORGT      WDT
      *
     C     TVKKT         CHAIN     SVTVK                              33
     C                   IF        *IN33 = '1'
     C                   EVAL      SVVK_KD = SVTT_KD
     C                   EVAL      SVVK_DTS = SVTT_DTS
     C                   ENDIF
     C                   EVAL      SVVK_OMR = SVTT_OMR
     C                   EVAL      SVVK_KRS = SVTT_KRS
     C     SVTT_DTE      IFEQ      *BLANKS
     C                   EVAL      SVVK_DTE = 99999999
     C                   ELSE
     C                   MOVE      SVTT_DTE      SVVK_DTE
     C                   ENDIF
     C                   IF        *IN33 = '1'
     C                   WRITE     SVTVKR
     C                   ELSE
     C                   UPDATE    SVTVKR
     C                   ENDIF
      *
     C                   EVAL      SVTT_DTS = SVTT_DTS - 1
     C     TVKKT         SETLL     SVTVK
     C     SVTT_KD       READPE    SVTVK                                  33
     C     *IN33         IFEQ      '0'
     C                   EVAL      SVVK_DTE = SVTT_DTS
     C                   UPDATE    SVTVKR
     C                   ENDIF
      *
     C                   ENDIF
      *
     C     SLSRPT        ENDSR
**
aA
bB
cC
dD
eE
fF
gG
hH
iI
jJ
kK
lL
mM
nN
oO
pP
qQ
rR
sS
tT
uU
vV
wW
xX
yY
zZ
æÆ
øØ
åÅ
