      ***********************************************************
      *** PROGRAM SKAL OPPDATERE W-FIL TIL PL.LISTE/LAGER     ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01-26                            ***
      *** LEDIGE INDIKATORER 27-32 35-99                      ***
      ***********************************************************
      *
     FLLWW162   UF   E           K DISK
     FLLLINE    UF   E           K DISK
     FLLVARE01  IF   E           K DISK
     FLLSERL01  UF A E           K DISK
     FLLSERL02  UF   E           K DISK    RENAME(LLSERR:LLSERR02)
     FLLAGJL01  UF   E           K DISK
     FLLAGJD01  UF A E           K DISK
     FLLAGJD02  IF   E           K DISK    RENAME(LLAGJDR:LLAGJDR02)
     F                                     PREFIX(AD2_)
     FLLAGJD07  UF   E           K DISK    RENAME(LLAGJDR:LLAGJDR07)
     F                                     PREFIX(AD7_)
     D MNI             S              7  0 DIM(100)
     D VNI             S              3  0 DIM(100)
     D NTF             S              7  0 DIM(100)
     D NTE             S              7  0 DIM(100)
     D NTJ             S              7  0 DIM(100)
     D MN2             S              7  0 DIM(100)
     D VN2             S              3  0 DIM(100)
     D NT2             S              7  0 DIM(100)
     D LC              S             25A   DIM(100)
      *
     C                   EXSR      INIT
      *
     C                   IF        UANR = *BLANKS
      * FRA WEB
     C     UMN           SETLL     LLLINE
     C     UMN           READE(N)  LLLINE                                 33
     C                   DOW       *IN33 = *OFF
     C                   MOVE      LLVN          XVNU              3 0
     C                   MOVE      LLANR         XANR             15
     C                   EXSR      OPPDAT
     C     UMN           READE(N)  LLLINE                                 33
     C                   ENDDO
     C                   ELSE
      * FRA SKJERMEN
     C                   MOVE      UVN           XVNU              3 0
     C                   MOVE      UANR          XANR             15
     C     LLLINEK       CHAIN(N)  LLLINE                             33
     C                   EXSR      OPPDAT
     C                   END
      *
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
      *
     C***********************************************
     C     OPPDAT        BEGSR
     C***********************************************
     C     LLVARE01K     CHAIN     LLVARE01                           33
     C                   SELECT
     C                   WHEN      *IN33
     C                   MOVE      'U'           XTYPE             1
     C                   WHEN      LVSN <> 'I'
     C                   MOVE      'U'           XTYPE
     C                   OTHER
     C                   MOVE      'I'           XTYPE
     C                   ENDSL
      *
     C                   IF        XTYPE = 'U'                                  1<-B
     C     LLWW162K2     SETLL     LLWW162
     C     LLWW162K2     READE(N)  LLWW162                                33
     C                   DOW       *IN33 = *OFF                                  2<-B
     C     LLSERL01K     CHAIN(N)  LLSERL01                           33
     C  N33              IF        SNTYPE = 'I'                                   3<-B
     C                   MOVE      SNTYPE        XTYPE
     C                   LEAVE
     C                   END                                                      3<-E
     C     LLWW162K2     READE(N)  LLWW162                                33
     C                   ENDDO                                                   2<-E
     C                   END                                                    1<-E
      * 1. NULLSTILL (MNU/VNU) I LLSERF
      * -------------------------------
     C     LLSERL02K     SETLL     LLSERL02
     C     LLSERL02K     READE     LLSERL02                               33
     C                   DOW       *IN33 = *OFF                                 1<-B
     C                   MOVE      *ZEROS        SNMNU
     C                   MOVE      *ZEROS        SNVNU
     C                   MOVE      *ZEROS        SNVNI
     C                   MOVE      'I'           SNST
     C                   UPDATE    LLSERR02
     C                   MOVE      SNTYPE        XTYPE
     C     LLSERL02K     READE     LLSERL02                               33
     C                   ENDDO                                                  1<-E
      *
      * 2. SUMMERE ANTALL SERIENR SOM ER SATT TIL FORESLÅTT LINJE I LLAGJD
      * ------------------------------------------------------------------
     C                   MOVE      *ZEROS        MNI
     C                   MOVE      *ZEROS        VNI
     C                   MOVE      *ZEROS        MN2
     C                   MOVE      *ZEROS        VN2
     C                   MOVE      *ZEROS        NTF
     C                   MOVE      *ZEROS        NTE
     C                   MOVE      *ZEROS        NTJ
     C                   MOVE      *ZEROS        XN03              3 0
     C     LLAGJD02K     SETLL     LLAGJD02
     C     LLAGJD02K     READE     LLAGJD02                               33
     C                   DOW       *IN33 = *OFF                                 1<-B
     C                   ADD       1             XN03
     C                   MOVE      AD2_VDMN      MNI(XN03)
     C                   MOVE      AD2_VDVN      VNI(XN03)
     C                   MOVE      AD2_VDMN      MN2(XN03)
     C                   MOVE      AD2_VDVN      VN2(XN03)
     C                   MOVE      AD2_VDLANT    NTF(XN03)
      * SUMMERE ANTALL LESTE SERIENR
     C     LLWW162K1     SETLL     LLWW162
     C     LLWW162K1     READE(N)  LLWW162                                33
     C                   DOW       *IN33 = *OFF                                  2<-B
     C                   ADD       1             NTE(XN03)
     C     LLWW162K1     READE(N)  LLWW162                                33
     C                   ENDDO                                                   2<-E
      * OPPDATE LINJENR PÅ ARBEIDSFIL
     C     LLWW162K1     SETLL     LLWW162
     C     LLWW162K1     READE     LLWW162                                33
     C                   DOW       *IN33 = *OFF                                  2<-B
     C                   EVAL      LW2VNI = AD2_VDVN
     C                   UPDATE    LLWW162R
     C     LLWW162K1     READE     LLWW162                                33
     C                   ENDDO                                                   2<-E
     C     LLAGJD02K     READE     LLAGJD02                               33
     C                   ENDDO                                                  1<-E
      *
      * 3. SUMMERE ALLE SOM IKKE TILHØRER FORESLÅTTE LINJER I LLAGJD
      * ------------------------------------------------------------
     C     LLWW162K2     SETLL     LLWW162
     C     LLWW162K2     READE(N)  LLWW162                                33
     C                   ADD       1             XN03
     C                   DOW       *IN33 = *OFF                                 1<-B
     C     1             DO        100           XN032                           2<-B
     C                   IF        ((MN2(XN032) = LW2MNI) AND
     C                              (VN2(XN032) = LW2VNI))
     C                   LEAVE
     C                   END
     C                   IF        MN2(XN032) = 0
     C                   LEAVE
     C                   END
     C                   ENDDO                                                   2<-E
     C                   IF        ((MN2(XN032) <> LW2MNI) OR
     C                              (VN2(XN032) <> LW2VNI))
     C                   MOVE      *ZEROS        LW2VNI
     C                   END
     C                   IF        LW2VNI = 0                                    2<-B
     C     XN03          DO        100           XN031                            3<-B
     C                   SELECT                                                    4<-B
     C                   WHEN      MNI(XN031) = 0
     C                   MOVE      LW2MNI        MNI(XN031)
     C                   ADD       1             NTE(XN031)
     C                   LEAVE
     C                   WHEN      MNI(XN031) = LW2MNI
     C                   ADD       1             NTE(XN031)
     C                   LEAVE
     C                   ENDSL                                                     4<-E
     C                   END                                                      3<-E
     C                   END                                                     2<-E
     C     LLWW162K2     READE(N)  LLWW162                                33
     C                   ENDDO                                                  1<-E
      *
      * 4. JUSTERE ANTALL I LLAGJD OG LLAGJF
      * ------------------------------------
     C     XTYPE         CABEQ     'U'           STOPD02
     C     1             DO        100           XN03                           1<-B
     C                   IF        VNI(XN03) > 0                                 2<-B
     C                   IF        NTF(XN03) <> NTE(XN03)                         3<-B
     C     XN03          ADD       1             XN031             3 0
     C     XN031         DO        99            XN032             3 0             4<-B
      *
     C     STOPD01       TAG
     C                   SELECT                                                     5<-B
     C                   WHEN      NTF(XN03) > NTE(XN03)
      * NÅ KOMMET TIL LINJE MED FÆRRE PLUKK ENN OPPRINGELIG FORESTLÅTT
     C                   IF        NTF(XN032) < NTE(XN032)                           6<-B
      * DEN NYE LINJEN MED FLERE PLUKK ENN OPPRINGELIG FORESLÅTT
      * JUSTERE BEGGE.
      *
      * TREKK 1 STYKK FRA LINJE MED FÆRRE PLUKK ENN OPPRINGELIG FORESTLÅTT
     C                   MOVE      MNI(XN03)     LW2MNI                         Ordne nøkkel
     C                   MOVE      VNI(XN03)     LW2VNI                         Ordne nøkkel
     C     LLAGJD01K     CHAIN     LLAGJD01                           33
     C                   SUB       1             VDLANT
     C                   SUB       1             VDSAE0
     C                   UPDATE    LLAGJDR
     C     LLAGJL01K2    CHAIN     LLAGJL01                           33
     C                   ADD       1             VASALD
     C                   ADD       1             VASAE0
     C                   UPDATE    LLAGJR
      * TREKK 1 STYKK FRA OPPRINGELIG FORESTLÅTT OG TREKK I JUSTERING
     C                   SUB       1             NTF(XN03)
     C                   SUB       1             NTJ(XN03)
      * ØK 1 STYKK TIL LINJE MED FLERE PLUKK ENNN OPPRINGELIG FORESTLÅTT
     C                   MOVE      MNI(XN032)    LW2MNI
     C                   MOVE      VNI(XN032)    LW2VNI
     C     LLAGJD01K     CHAIN     LLAGJD01                           33
     C  N33              ADD       1             VDLANT
     C  N33              ADD       1             VDSAE0
     C  N33              UPDATE    LLAGJDR
     C  N33LLAGJL01K2    CHAIN     LLAGJL01                           33
     C  N33              SUB       1             VASALD
     C  N33              SUB       1             VASAE0
     C  N33              UPDATE    LLAGJR
      *
      * JUSTERE DEN NYE LINJEN MED FLERE PLUKK ENN OPPRINGELIG FORSTÅTT
     C                   ADD       1             NTF(XN032)
     C                   ADD       1             NTJ(XN032)
      * SJEKK OM ØNSKET OG PLUKKET ER LIKE.
     C                   GOTO      STOPD01
     C                   END                                                         6<-E
      *
      * IKKE FINNES FLERE STYKK SOM KAN FLYTTES FRA LINJE MED FÆRRE PLUKK
      * TIL LINJE MED FLERE PLUKK. AVSLUTT DENNE LINJEN.
      *
     C                   WHEN      NTF(XN03) < NTE(XN03)
      * NÅ KOMMET TIL LINJE MED FLERE PLUKK ENNN OPPRINGELIG FORESTLÅTT
     C                   IF        NTF(XN032) > NTE(XN032)                           6<-B
      * DEN NYE LINJEN MED FÆRRE PLUKK ENN OPPRINGELIG FORESLÅTT
      * JUSTERE BEGGE.
      * ØK 1 STYKK TIL LINJE MED FLERE PLUKK ENNN OPPRINGELIG FORESTLÅTT
     C                   MOVE      MNI(XN03)     LW2MNI
     C                   MOVE      VNI(XN03)     LW2VNI
     C     LLAGJD01K     CHAIN     LLAGJD01                           33
     C                   ADD       1             VDLANT
     C                   ADD       1             VDSAE0
     C                   UPDATE    LLAGJDR
     C     LLAGJL01K2    CHAIN     LLAGJL01                           33
     C                   SUB       1             VASALD
     C                   SUB       1             VASAE0
     C                   UPDATE    LLAGJR
      * ØK 1 STYKK FRA OPPRINGELIG FORESTLÅTT OG ØK I JUSTERING
     C                   ADD       1             NTF(XN03)
     C                   ADD       1             NTJ(XN03)
      * TREKK 1 STYKK TIL LINJE MED FÆRRE PLUKK ENNN OPPRINGELIG FORESTLÅTT
     C                   MOVE      MNI(XN032)    LW2MNI
     C                   MOVE      VNI(XN032)    LW2VNI
     C     LLAGJD01K     CHAIN     LLAGJD01                           33
     C  N33              SUB       1             VDLANT
     C  N33              SUB       1             VDSAE0
     C  N33              UPDATE    LLAGJDR
     C  N33LLAGJL01K2    CHAIN     LLAGJL01                           33
     C  N33              SUB       1             VASALD
     C  N33              SUB       1             VASAE0
     C  N33              UPDATE    LLAGJR
      * JUSTERE DEN NYE LINJEN MED FÆRRE PLUKK ENN OPPRINGELIG FORSTÅTT
     C                   SUB       1             NTF(XN032)
     C                   SUB       1             NTJ(XN032)
     C                   GOTO      STOPD01
     C                   END                                                         6<-E
     C                   ENDSL                                                      5<-E
      *
     C                   ENDDO                                                     4<-E
      *
     C                   END                                                      3<-E
     C                   END                                                     2<-E
     C                   ENDDO                                                  1<-E
      *
      * 5. SLETT ALLE POSTER MED ANTALL=0 I LLAGJD
      * ------------------------------------------
     C     STOPD02       TAG
     C     1             DO        100           XN03                           1<-B
     C                   IF        VNI(XN03) = 0                                 2<-B
     C                   LEAVE
     C                   END                                                     2<-E
     C                   IF        ((NTF(XN03) = 0) AND (XTYPE = 'I'))           2<-B
     C                   MOVE      MNI(XN03)     LW2MNI
     C                   MOVE      VNI(XN03)     LW2VNI
     C     LLAGJD01K     CHAIN     LLAGJD01                           33
     C                   DELETE    LLAGJDR
     C                   END                                                     2<-E
     C                   ENDDO                                                  1<-E
      *
      * 5. OPPRETTE NYE POSTER I LLAGJD
      * -------------------------------
     C     XN03          DO        100           XN031                          1<-B
     C                   IF        MNI(XN031) = 0                                2<-B
     C                   LEAVE
     C                   END                                                     2<-E
     C                   CLEAR                   LLAGJDR
     C                   MOVE      MNI(XN031)    LW2MNI
     C     LLAGJD07K     SETLL     LLAGJD07
     C     LW2MNI        READPE(N) LLAGJD07                               33
     C                   IF        *IN33                                          3<-B
     C                   Z-ADD     1             VDVN
     C                   ELSE                                                     3
     C                   EVAL      VDVN = AD7_VDVN + 1
     C                   END                                                      3<-E
     C                   MOVE      UGRP          VDGRUP
     C                   MOVE      UPNR          VDKJED
     C                   MOVE      UMN           VDMNL
     C                   MOVE      XVNU          VDVNL
     C                   MOVE      LW2MNI        VDMN
     C                   MOVE      NTE(XN031)    VDLANT
     C                   MOVE      NTE(XN031)    VDSAE0
     C                   MOVE      99999999      VDLDAT
     C                   MOVE      XTEXT         VDTEXT
     C                   MOVE      UUSER         VDBUSE
     C                   MOVE      WDT           VDBDAT
     C                   MOVE      WTM           VDBKL
     C                   MOVE      'PLIST'       VDLTYP
     C                   WRITE     LLAGJDR
     C     LLAGJL01K     CHAIN     LLAGJL01                           33
     C                   SUB       NTE(XN031)    VASALD
     C                   SUB       NTE(XN031)    VASAE0
     C                   IF        VASALD < 0
     C                   Z-SUB     VASALD        XANT              7 0
     C                   MOVE      *ZEROS        VASALD
     C                   MOVE      *ZEROS        VASAE0
     C                   ELSE
     C                   Z-ADD     0             XANT
     C                   END
     C                   UPDATE    LLAGJR
     C                   IF        XANT > 0
     C                   EXSR      OPPDAT1
     C                   END
     C                   MOVE      VDVN          VNI(XN031)
     C                   MOVE      NTE(XN031)    NTJ(XN031)
     C                   ENDDO                                                  1<-E
      *
      * 6. OPPDATER PÅ LLWW162
      * ---------------------
     C     LLWW162K2     SETLL     LLWW162
     C     1             DO        100           XN03                           1<-B
     C                   IF        MNI(XN03) = 0                                 2<-B
     C                   LEAVE
     C                   END                                                     2<-E
     C                   IF        NTE(XN03) > 0                                 2<-B
     C     1             DO        NTE(XN03)                                      3<-B
     C     LLWW162K2     READE     LLWW162                                33
     C                   MOVE      VNI(XN03)     LW2VNI
     C                   UPDATE    LLWW162R
     C                   ENDDO                                                    3<-E
     C                   END                                                     2<-E
     C                   ENDDO                                                  1<-E
      *
      * 7. OPPDATER PÅ LLSERF
      * ---------------------
     C     LLWW162K2     SETLL     LLWW162
     C     LLWW162K2     READE(N)  LLWW162                                33
     C                   DOW       *IN33 = *OFF                                 1<-B
     C     LLSERL01K     CHAIN     LLSERL01                           33
     C                   MOVE      LW2MNU        SNMNU
     C                   MOVE      LW2VNU        SNVNU
     C                   MOVE      LW2VNI        SNVNI
     C                   MOVE      'P'           SNST
     C                   IF        *IN33
     C                   MOVE      UGRP          SNGRP
     C                   MOVE      UPNR          SNPNR
     C                   MOVE      XANR          SNANR
     C                   MOVE      LW2SN         SNSN
     C                   MOVE      'U'           SNTYPE
     C                   MOVE      LW2MNI        SNMNI
     C                   WRITE     LLSERR
     C                   ELSE
     C                   UPDATE    LLSERR
     C                   END
     C     LLWW162K2     READE(N)  LLWW162                                33
     C                   ENDDO                                                  1<-E
      *
      * 8. OPPDATER ANTALL LOKASJONER I LLLINE
      * --------------------------------------
     C     LLLINEK       CHAIN     LLLINE                             33
     C                   MOVE      *BLANKS       LLLOK
     C                   MOVE      *ZEROS        LLNTLK
     C     LLAGJD02K     SETLL     LLAGJD02
     C     LLAGJD02K     READE     LLAGJD02                               33
     C                   DOW       *IN33 = *OFF                                 1<-B
     C                   IF        LLLOK = *BLANKS                               2<-B
     C     LLAGJL01K2    CHAIN     LLAGJL01                           33
     C                   MOVE      VALLOK        LLLOK
     C                   END                                                     2<-E
     C                   ADD       1             LLNTLK
     C     LLAGJD02K     READE     LLAGJD02                               33
     C                   ENDDO                                                  1<-E
     C                   UPDATE    LLLINER
      *
     C                   ENDSR
      *
     C***********************************************
     C     OPPDAT1       BEGSR
     C***********************************************
      * GÅ GJENNOM ALLE POSTER PÅ SAMME INNLEGGING, OG SE OM EN ELLER FLERE
      * POSTER KAN BYTTES.
     C                   MOVE      LW2MNI        XMNI              7 0
     C     LLAGJL01K3    SETLL     LLAGJD01
     C     LLAGJL01K3    READE     LLAGJD01                               33
     C                   DOW       *IN33 = *OFF                                 1<-B
     C                   IF        ((VDLDAT = 99999999) AND (VDLTYP = 'PLIST'))  2<-B
     C                   SELECT                                                   3<-B
     C                   WHEN      VDLANT >= XANT
     C                   IF        VDLANT > XANT                                   4<-B
     C                   SUB       XANT          VDLANT
     C                   SUB       XANT          VDSAE0
     C                   UPDATE    LLAGJDR
     C                   ELSE                                                      4
     C                   DELETE    LLAGJDR
     C                   END                                                       4<-E
     C     1             DO        100           XN033             3 0             4<-B
     C                   IF        NTJ(XN033) < 0                                   5<-B
     C                   Z-SUB     NTJ(XN033)    XANT2             7 0
     C                   IF        XANT2 >= XANT                                     6<-B
     C                   Z-ADD     XANT          XANT10            7 0
     C                   EXSR      OPPDAT2
     C                   ADD       XANT          NTJ(XN033)
     C                   GOTO      SLOPPDAT1
     C                   ELSE                                                        6
     C                   Z-SUB     NTJ(XN033)    XANT10
     C                   EXSR      OPPDAT2
     C                   ADD       NTJ(XN033)    XANT
     C                   MOVE      *ZEROS        NTJ(XN033)
     C                   EXSR      OPPDAT2
     C                   END                                                         6<-E
     C                   END                                                        5<-E
     C                   ENDDO                                                     4<-E
     C                   OTHER
     C                   DELETE    LLAGJDR
     C                   MOVE      VDLANT        XANT3             7 0
     C                   SUB       VDLANT        XANT
     C     1             DO        100           XN033             3 0             4<-B
     C                   IF        NTJ(XN033) < 0                                   5<-B
     C                   Z-SUB     NTJ(XN033)    XANT2             7 0
     C                   IF        XANT2 >= XANT3                                    6<-B
     C                   MOVE      XANT3         XANT10
     C                   EXSR      OPPDAT2
     C                   ADD       XANT3         NTJ(XN033)
     C                   GOTO      SLOPPDAT11
     C                   ELSE                                                        6
     C                   Z-SUB     NTJ(XN033)    XANT10
     C                   EXSR      OPPDAT2
     C                   ADD       NTJ(XN033)    XANT3
     C                   MOVE      *ZEROS        NTJ(XN033)
     C                   END                                                         6<-E
     C                   END                                                        5<-E
     C                   ENDDO                                                     4<-E
     C                   ENDSL                                                    3<-E
     C                   ELSE                                                    2
     C                   UPDATE    LLAGJDR
     C                   END                                                     2<-E
     C     SLOPPDAT11    TAG
     C     LLAGJL01K3    READE     LLAGJD01                               33
     C                   ENDDO                                                  1<-E
      *
     C     SLOPPDAT1     TAG
     C                   ENDSR
      *
     C***********************************************
     C     OPPDAT2       BEGSR
     C***********************************************
     C                   MOVE      MNI(XN033)    LW2MNI
     C                   MOVE      *ZEROS        AD7_VDVN
     C     LW2MNI        SETLL     LLAGJD07
     C     LW2MNI        READE     LLAGJD07                               33
     C                   DOW       *IN33 = *OFF
     C                   IF        AD7_VDMNL = VDMNL
     C                   ADD       XANT10        AD7_VDLANT
     C                   ADD       XANT10        AD7_VDSAE0
     C                   UPDATE    LLAGJDR07
     C                   GOTO      STOPPDAT21
     C                   END
     C                   UPDATE    LLAGJDR07
     C     LW2MNI        READE     LLAGJD07                               33
     C                   ENDDO
      *
     C                   EVAL      VDVN = AD7_VDVN + 1
     C                   MOVE      LW2MNI        VDMN
     C                   MOVE      XANT10        VDLANT
     C                   MOVE      XANT10        VDSAE0
     C                   MOVE      UUSER         VDBUSE
     C                   MOVE      WDT           VDBDAT
     C                   MOVE      WTM           VDBKL
     C                   MOVE      'PLIST'       VDLTYP
     C                   WRITE     LLAGJDR
     C     STOPPDAT21    TAG
     C     LLAGJL01K     CHAIN     LLAGJL01                           33
     C                   SUB       XANT10        VASALD
     C                   SUB       XANT10        VASAE0
     C                   UPDATE    LLAGJR
     C                   EXSR      OPPDAT3
      *
     C                   ENDSR
      *
     C***********************************************
     C     OPPDAT3       BEGSR
     C***********************************************
     C     LLLINEK2      CHAIN     LLLINE                             33
     C                   MOVE      *BLANKS       LLLOK
     C                   MOVE      *ZEROS        LLNTLK
     C     LLAGJD02K3    SETLL     LLAGJD02
     C     LLAGJD02K3    READE     LLAGJD02                               33
     C                   DOW       *IN33 = *OFF                                 1<-B
     C                   IF        LLLOK = *BLANKS                               2<-B
     C     LLAGJL01K2    CHAIN     LLAGJL01                           33
     C                   MOVE      VALLOK        LLLOK
     C                   END                                                     2<-E
     C                   ADD       1             LLNTLK
     C     LLAGJD02K3    READE     LLAGJD02                               33
     C                   ENDDO                                                  1<-E
     C                   UPDATE    LLLINER
      *
     C                   ENDSR
      *
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     LLVARE01K     KLIST
     C                   KFLD                    UGRP
     C                   KFLD                    UPNR
     C                   KFLD                    XANR
     C     LLSERL01K     KLIST
     C                   KFLD                    UGRP
     C                   KFLD                    UPNR
     C                   KFLD                    XANR
     C                   KFLD                    LW2SN
     C     LLSERL02K     KLIST
     C                   KFLD                    UMN
     C                   KFLD                    XVNU
     C     LLLINEK       KLIST
     C                   KFLD                    UMN
     C                   KFLD                    XVNU
     C     LLLINEK2      KLIST
     C                   KFLD                    VDMNL
     C                   KFLD                    VDVNL
     C     LLWW162K1     KLIST
     C                   KFLD                    UMN
     C                   KFLD                    XVNU
     C                   KFLD                    AD2_VDMN
     C     LLWW162K2     KLIST
     C                   KFLD                    UMN
     C                   KFLD                    XVNU
     C     LLAGJL01K     KLIST
     C                   KFLD                    UGRP
     C                   KFLD                    UPNR
     C                   KFLD                    LW2MNI
     C     LLAGJL01K2    KLIST
     C                   KFLD                    UGRP
     C                   KFLD                    UPNR
     C                   KFLD                    AD2_VDMN
     C     LLAGJL01K3    KLIST
     C                   KFLD                    UGRP
     C                   KFLD                    UPNR
     C                   KFLD                    XMNI
     C     LLAGJD01K     KLIST
     C                   KFLD                    UGRP
     C                   KFLD                    UPNR
     C                   KFLD                    LW2MNI
     C                   KFLD                    LW2VNI
     C     LLAGJD02K     KLIST
     C                   KFLD                    UGRP
     C                   KFLD                    UPNR
     C                   KFLD                    UMN
     C                   KFLD                    XVNU
     C     LLAGJD02K2    KLIST
     C                   KFLD                    UGRP
     C                   KFLD                    UPNR
     C                   KFLD                    UMN
     C     LLAGJD02K3    KLIST
     C                   KFLD                    UGRP
     C                   KFLD                    UPNR
     C                   KFLD                    LLMN
     C                   KFLD                    LLVN
     C     LLAGJD07K     KLIST
     C                   KFLD                    LW2MNI
     C                   KFLD                    XDVN
     C                   MOVE      999           XDVN              3 0
      *
     C     *ENTRY        PLIST
     C                   PARM                    UUSER            10
     C                   PARM                    UGRP              8 0
     C                   PARM                    UPNR              8 0
     C                   PARM                    UANR             15
     C                   PARM                    UMN               7 0
     C                   PARM                    UVN               3 0
      *
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
      *
     C     LLAGJD02K2    SETLL     LLAGJD02
     C     LLAGJD02K2    READE     LLAGJD02                               33
     C                   MOVE      AD2_VDTEXT    XTEXT            30
      *
     C                   ENDSR
      *
