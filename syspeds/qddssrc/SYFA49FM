     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPDOC('STD' 'HELPNU' 'HLP400')
     A                                      HELP
     A          R BILDE01
     A                                      CF03(03 'Avslutt program')
     A                                      CF04(04 'SØK KUNDE')
     A                                  2 58MSGCON(22 DIV0047 SYMSGF)
     A                                      COLOR(TRQ)
     A                                  4 22MSGCON(33 DIV0742 SYMSGF)
     A                                      COLOR(WHT)
     A                                  9 22MSGCON(18 DIV0743 SYMSGF)
     A            WSKN           8  0B  9 41TEXT('KUNDENR')
     A                                      DSPATR(UL)
     A  99                                  DSPATR(PC)
     A  99                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                 23 22MSGCON(18 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 42MSGCON(18 DIV0636 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R SUBFIL                    SFL
     A            FTFRA          5A  O  5  2
     A            BFT1           5  0O  5  8EDTCDE(4)
     A            BFT2           5  0O  5 14EDTCDE(4)
     A            BFT3           5  0O  5 20EDTCDE(4)
     A            BFT4           5  0O  5 26EDTCDE(4)
     A            BFT5           5  0O  5 32EDTCDE(4)
     A            BFT6           5  0O  5 38EDTCDE(4)
     A            BFT7           5  0O  5 44EDTCDE(4)
     A            BFT8           5  0O  5 50EDTCDE(4)
     A            BFT9           5  0O  5 56EDTCDE(4)
     A            BFT10          5  0O  5 62EDTCDE(4)
     A            BFT11          5  0O  5 68EDTCDE(4)
     A            BFT12          5  0O  5 74EDTCDE(4)
     A            FTTIL          5A  O  6  2DSPATR(UL)
     A            FTFRAN         3A  O  6  9DSPATR(UL)
     A            FTOPDT         2A  O  6 14DSPATR(UL)
     A            FTSK           1A  O  6 18DSPATR(UL)
     A            FTKODE         1A  O  6 21DSPATR(UL)
     A            TXT1           4A  O  6 25
     A            FTMIN          5  0O  6 30DSPATR(UL)
     A                                      EDTCDE(4)
     A            TXT2           4A  O  6 39
     A            FTMAX1         5  0O  6 44DSPATR(UL)
     A                                      EDTCDE(4)
     A            TXT3           5A  O  6 53
     A            FTKOLL         1A  O  6 59DSPATR(UL)
     A            FTMM           1Y 0O  6 63EDTCDE(4)
     A                                      DSPATR(UL)
     A            FTVAL          3   O  6 65
     A                                      DSPATR(UL)
     A            FTLMOM         5Y 0O  6 69EDTCDE(4)
     A                                      DSPATR(UL)
     A            FTM3OM         5Y 0O  6 75EDTCDE(4)
     A                                      DSPATR(UL)
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0009)
     A                                      SFLPAG(0008)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A            ZRECNO         4S 0O  2 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1 10MSGCON(33 DIV0742 SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 58MSGCON(22 DIV0047 SYMSGF)
     A                                      COLOR(TRQ)
     A                                  3  2MSGCON(6 DIV0744 SYMSGF)
     A            WSKN           8  0O  3 10DSPATR(UL)
     A                                      COLOR(BLU)
     A                                      EDTCDE(4)
     A            KNAVN         30   O  3 19DSPATR(UL)
     A                                      COLOR(BLU)
     A            ADR1          30   O  3 50DSPATR(UL)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(78 DIV0755 SYMSGF)
     A                                      COLOR(WHT)
     A          R BUNN
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF03(03 'Avslutt')
     A                                      CF05(05 'Frisk')
     A                                      CF12(12 'FORRIGE')
     A                                      SLNO(22)
     A                                      CLRL(03)
     A                                      OVERLAY
     A                                  2  2MSGCON(18 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A                                  2 22MSGCON(18 DIV0021 SYMSGF)
     A                                      COLOR(BLU)
     A                                  2 42MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
      *
     A          R NYWRK
     A                                  1 10MSGCON(33 DIV0742 SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 58MSGCON(22 DIV0047 SYMSGF)
     A                                      COLOR(TRQ)
     A                                  3  2MSGCON(6 DIV0744 SYMSGF)
     A            WSKN           8  0O  3 10DSPATR(UL)
     A                                      COLOR(BLU)
     A                                      EDTCDE(4)
     A            KNAVN         30   O  3 19DSPATR(UL)
     A                                      COLOR(BLU)
     A            ADR1          30   O  3 50DSPATR(UL)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(78 DIV0755 SYMSGF)
     A                                      COLOR(WHT)
