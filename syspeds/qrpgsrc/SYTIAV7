      * HVIS TARIFFNR / FF / SEKV. NY I SADAVGE, SKRIV..
      *    %(PROSENT)/P(PROM.) FRA KODTS8
      *    HVIS SEKVENS 300 ER DET (P.T.) VAREGRUPPE B (LAKS FRA EU)
     H DATEDIT(*DMY)
     FSADSDNY   IP   E             DISK
     FSADAVGE   IF A E           K DISK
     FKODTS8    IF   E           K DISK
     FLISTE     O    F  132        PRINTER OFLIND(*INOF)
      *
     C     *IN99         IFEQ      *OFF
     C                   Z-ADD     1             SIDE              4 0
     C                   TIME                    TID               6 0
     C                   EXCEPT    PHEAD                                         1 PAGE
     C                   SETON                                        99
     C                   END
     C   OF              ADD       1             SIDE              4 0
     C   OF              EXCEPT    PHEAD
     C   OF              SETOFF                                       OF         OVERFLOW
      *
     C     KEY           KLIST
     C                   KFLD                    SDTNRF
     C                   KFLD                    SDKDAÆ
     C                   KFLD                    SDKDSÆ
     C                   KFLD                    SDDTF
      *
     C     KODS8K        KLIST
     C                   KFLD                    SDKDAÆ
     C                   KFLD                    SDKDSÆ
     C*
     C     SDKDAÆ        IFEQ      'FF'
     C     KEY           CHAIN     SADAVGE                            33
     C     *IN33         IFEQ      '1'
     C                   MOVE      'P'           KS8STY
     C     KODS8K        CHAIN     KODTS8                             35
     C                   MOVE      SDTNRF        AGTANR
     C                   MOVE      SDKDAÆ        AGAKD
     C                   MOVE      SDKDSÆ        AGSKV
     C                   MOVE      SDDTF         AGDTF
     C                   MOVE      SDDTT         AGDTT
     C     AGSKV         IFEQ      '300'
     C                   MOVE      'B'           AGKD
     C                   END
     C                   MOVE      KS8STY        AGPP
     C                   Z-ADD     SDBLSÆ        AGSATS
     C                   WRITE     SADAVGER
     C                   EXCEPT    DET
     C                   END
     C                   END
     OLISTE     E            PHEAD            02
     O                                           24 '**     HELT NYE     EKSP'
     O                                           44 'ORTAVGIFTER (FF) ** '
     O                                           86 'KJØRT DATO:'
     O                       UDATE         Y     96
     O                                          101 'TID:'
     O                       TID                110 '0  :  :  '
     O                                          120 'SIDE:'
     O                       SIDE          Z    125
     OLISTE     E            PHEAD            04
     O                                           24 '------------------------'
     O                       *PLACE              48
     O                       *PLACE              72
     O                       *PLACE              96
     O                       *PLACE             120
     O                                          132 '------------'
     OLISTE     E            PHEAD            05
     O                                           24 ' ENDR.KODE  TARIFFNR   '
     O                                              'AVG.KODE  SEKVENS       '
     O                                              ' NY. SATS               '
     OLISTE     E            PHEAD            06
     O                                           24 '------------------------'
     O                       *PLACE              48
     O                       *PLACE              72
     O                       *PLACE              96
     O                       *PLACE             120
     O                                          132 '------------'
     OLISTE     E            DET         1
     O                                            9 'NY:     '
     O                       AGTANR              19
     O                       AGAKD               27
     O                       AGSKV               39
     O                       AGKD                43
     O                       AGSATS        3     55
     O                       AGPP                57
