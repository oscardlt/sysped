********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 CB  Fjernet all gammel merking grunnet ny logikk PKI, ikke merket
11XXX * 02 CB  PTF11 Vis edifact fra *IFS med CR/LF
********************************************************************
     FSVI014D   CF   E             WORKSTN
     F                                     SFILE(SVI014DA:ZRECNO)
     F                                     SFILE(SVI014DE:ÆRECNO)
     FEDIM2     IF   E           K DISK
     FEDIM      IF   E           K DISK
     F                                     RENAME(EDIMR:EDIMR0)
     FTVINF     IF   E           K DISK
     FEDIS      IF   E           K DISK
N02  FEDISX     IF   E           K DISK
      *//////////////
      *  Hodelogikk /
      *//////////////
     C                   EXSR      SRA
     C                   EXSR      SRB
     C                   MOVE      '1'           *INLR
      *////////////////////////////////////////////////////////////
      *  Initiering                                           SRA /
      *////////////////////////////////////////////////////////////
     C     SRA           BEGSR
      *___________________
      * Definiere Workfelt
      *"""""""""""""""""""
     C     *LIKE         DEFINE    M0068A        W0068A
     C     *LIKE         DEFINE    M0068B        W0068B
     C     *LIKE         DEFINE    M0068C        W0068C
     C     *LIKE         DEFINE    FLN           WLN
     C     *LIKE         DEFINE    *IN30         WIN30
     C                   MOVE      *ZEROS        W0068A
     C                   MOVE      *ZEROS        W0068B
     C                   MOVE      *ZEROS        W0068C
     C                   MOVE      *ZEROS        WLN
      *__________________
      * Definiere Nøkkler
      *""""""""""""""""""
     C     KEYA          KLIST
     C                   KFLD                    WAVD
     C                   KFLD                    WTDN
     C                   KFLD                    W0068A
     C                   KFLD                    W0068B
     C                   KFLD                    W0068C
     C     KEYA1         KLIST
     C                   KFLD                    WAVD
     C                   KFLD                    WTDN
     C     KEYB          KLIST
     C                   KFLD                    MMN
     C                   KFLD                    WLN
     C     KEYC          KLIST
     C                   KFLD                    MSN
      *_________________
      * Input parametrer
      *"""""""""""""""""
     C     *ENTRY        PLIST
     C     WAVD          PARM                    WAVD
     C     WTDN          PARM                    WTDN
     C                   PARM                    UM1N07            3
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Bearbeide Formater                                   SRB /
      *////////////////////////////////////////////////////////////
     C     SRB           BEGSR
      *
     C     SRB1          TAG
      *______________
      * Tømme Subfile
      *""""""""""""""
     C                   MOVE      '1'           *IN93
     C                   WRITE     SVI014DB
     C                   MOVEA     '000'         *IN(93)
     C                   Z-ADD     *ZERO         ZRECNO
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     KEYA          SETLL     EDIM2
     C     SRB2          TAG
     C     KEYA1         READE     EDIM2                                  94
     C  N94              DO
     C     M1N07         IFNE      UM1N07
     C                   GOTO      SRB2
     C                   END
     C     MSR           COMP      'R'                                    27
     C                   ADD       1             ZRECNO
     C                   EXSR      SRBB
     C                   WRITE     SVI014DA
     C                   GOTO      SRB2
     C                   END
     C     ZRECNO        CABEQ     0             SRB9
     C                   Z-ADD     1             ZRECNO
      *_______________
      * Behandle bilde
      *"""""""""""""""
     C     SRB3          TAG
      *
     C                   MOVEA     '11'          *IN(91)
     C  N30              MOVE      'SY00000'     ZMSGNO
     C                   WRITE     SVI014DØ
     C                   WRITE     SVI014DC
     C                   EXFMT     SVI014DB
      *
     C     *IN03         IFEQ      '0'
     C     *IN12         ANDEQ     '0'
      *
     C                   MOVEA     '00'          *IN(91)
      *_____________
      * F5=Frisk opp
      *"""""""""""""
     C     *IN05         CABEQ     '1'           SRB1
      *_______
      * Valg ?
      *"""""""
     C                   MOVE      '0'           *IN52
     C                   MOVE      '0'           *IN30
     C                   MOVE      '0'           *IN95
     C     *IN52         DOWEQ     '0'
      *
     C                   READC     SVI014DA                               52
     C     *IN52         IFEQ      '0'
     C     ZOP           CASEQ     '5'           SRBE
     C     ZOP           CASEQ     '6'           SRBF
     C     ZOP           CASEQ     '7'           SRBG
     C     ZOP           CASEQ     '9'           SRBH
     C                   END
     C                   END
      *
     C  N30
     CANN52              END
      *
     C     *IN03         CABEQ     '1'           SRB9
     C     *IN95         CABEQ     '1'           SRB1
     C     *IN95         CABEQ     '0'           SRB3
     C                   END
      *
     C     SRB9          TAG
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Vise hele meldingsloggen.                           SRBE /
      *////////////////////////////////////////////////////////////
     C     SRBE          BEGSR
     C                   MOVE      '0'           *IN28
     C                   MOVE      'SY00000'     ZMSGNO
     C                   WRITE     SVI014DØ
     C     MMN           CHAIN     EDIM                               20
     C     SRBE1         TAG
     C                   MOVEA     '00'          *IN(25)
     C                   MOVE      WAVD          MAVD
     C                   MOVE      WTDN          MTDN
     C                   EXFMT     SVI014DD
      *_________________
      * Oppdatere SUBFIL
      *"""""""""""""""""
     C     SRBE9         TAG
     C                   MOVE      *BLANKS       ZOP
     C     MSR           COMP      'R'                                    27
     C                   UPDATE    SVI014DA
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Vise feilmeldinger                                  SRBF /
      *////////////////////////////////////////////////////////////
     C     SRBF          BEGSR
     C     SRBF1         TAG
     C                   MOVE      '1'           *IN30
      *______________
      * Tømme Subfile
      *""""""""""""""
     C                   MOVE      '1'           *IN83
     C                   MOVE      WAVD          MAVD
     C                   MOVE      WTDN          MTDN
     C                   WRITE     SVI014DF
     C                   MOVEA     '000'         *IN(83)
     C                   Z-ADD     *ZERO         ÆRECNO
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C                   MOVE      *ZEROS        WLN
     C     KEYB          SETLL     TVINF
     C     MMN           READE     TVINF                                  84
      *
     C     *IN84         DOWEQ     '0'
     C                   MOVE      '0'           *IN30
     C                   ADD       1             ÆRECNO
     C                   WRITE     SVI014DE
     C     MMN           READE     TVINF                                  84
     C  N84              END
     C                   Z-ADD     1             ÆRECNO
      *_______________
      * Ingen poster ?
      *"""""""""""""""
     C     *IN30         IFEQ      '1'
     C                   MOVE      'SY00110'     ZMSGNO
     C                   GOTO      SRBF9
     C                   END
      *_______________
      * Behandle bilde
      *"""""""""""""""
     C                   MOVEA     '11'          *IN(81)
     C  N30              MOVE      'SY00000'     ZMSGNO
     C                   WRITE     SVI014DØ
     C                   WRITE     SVI014DC
     C                   EXFMT     SVI014DF
     C     *IN05         CABEQ     '1'           SRBF1
      *_________________
      * Oppdatere SUBFIL
      *"""""""""""""""""
     C     SRBF9         TAG
     C  N30              MOVE      *BLANKS       ZOP
     C  N30MSR           COMP      'R'                                    27
     C  N30              UPDATE    SVI014DA
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Vise fysisk filkomponent                            SRBG /
      *////////////////////////////////////////////////////////////
     C     SRBG          BEGSR
      *___________________
      * Hente sendningsref
      *"""""""""""""""""""
     C     KEYC          CHAIN     EDIS                               20
      * ASCII
     C                   IF        SSR = 'S'
     C                   IF        SFILE = '*IFS'
     C                   EVAL      QCMDA = *BLANKS
     C                   EVAL      QCMDA = 'DSPF STMF(' + X'7D'
     C                             + %trimr(SIFS) + X'7D'
     C                             + ')'
     C                   EVAL      QCMDL = %len(%trim(QCMDA))
     C                   CALL      'QCMDEXC'                            98
     C                   PARM                    QCMDA
     C                   PARM                    QCMDL
     C                   GOTO      SRBG9
     C                   ENDIF
     C                   ENDIF
      *_______
      * EBCDIC
      *"""""""
     C                   MOVEL     SLIB          XSLIB             6
     C     XSLIB         IFEQ      'SYSPEDO'
     C                   MOVEL(P)  'DSPPFM FILE('QCMDA            50
     C     QCMDA         CAT       SLIB:0        QCMDA
     C     QCMDA         CAT       '/':0         QCMDA
     C     QCMDA         CAT       SFILE:0       QCMDA
     C     QCMDA         CAT       '_) MBR(':0   QCMDA
     C     QCMDA         CAT       SMBR:0        QCMDA
     C     QCMDA         CAT       ')':0         QCMDA
     C                   Z-ADD     50            QCMDL            15 5
     C                   CALL      'QCMDEXC'
     C                   PARM                    QCMDA
     C                   PARM                    QCMDL
     C                   ELSE
     C                   SELECT
     C     SFILE         WHENEQ    'EDIRE'
     C                   CALL      'EDI44R'
     C                   PARM      SLIB          SLIB
     C                   PARM      SFILE         SFILE
     C                   PARM      SMBR          SMBR
     C     SFILE         WHENEQ    'EDIRE1K'
     C                   CALL      'EDI44R1K'
     C                   PARM                    SLIB
     C                   PARM                    SFILE
     C                   PARM                    SMBR
     C     SFILE         WHENEQ    'EDISE'
     C                   CALL      'EDI44S'
     C                   PARM                    SLIB
     C                   PARM                    SFILE
     C                   PARM                    SMBR
     C     SFILE         WHENEQ    'EDISE1K'
     C                   CALL      'EDI44S1K'
     C                   PARM                    SLIB
     C                   PARM                    SFILE
     C                   PARM                    SMBR
     C                   ENDSL
     C                   END
      *_________________
      * Oppdatere SUBFIL
      *"""""""""""""""""
     C     SRBG9         TAG

     C  N30              MOVE      *BLANKS       ZOP
     C  N30MSR           COMP      'R'                                    27
     C  N30              UPDATE    SVI014DA
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Vise EDI-FACT                                       SRBH /
      *////////////////////////////////////////////////////////////
     C     SRBH          BEGSR
     C     KEYC          CHAIN     EDIS                               20
     C     *IN20         IFEQ      '0'
      * Finnes komponenten kvar ?
     C     SMBR          IFEQ      'REMOVED'
     C                   MOVE      'SPS0007'     ZMSGNO
     C                   MOVE      '1'           *IN30
     C                   GOTO      SLSRBH
     C                   END
      * Vise komponent.
     C                   MOVEL     SLIB          XSLIB             6
     C     XSLIB         IFEQ      'SYSPEDO'
     C                   CALL      'EDI41RX'
     C                   PARM                    SLIB
     C                   PARM                    SFILE
     C                   PARM                    SMBR
     C                   ELSE
     C                   SELECT
     C     SFILE         WHENEQ    'EDIRE'
     C                   CALL      'EDI41R'
     C                   PARM                    SLIB
     C                   PARM                    SFILE
     C                   PARM                    SMBR
     C     SFILE         WHENEQ    'EDISE'
     C                   CALL      'EDI41S'
     C                   PARM                    SLIB
     C                   PARM                    SFILE
     C                   PARM                    SMBR
N02  C     SFILE         WHENEQ    '*IFS'
N02  C     SIFS          IFNE      *BLANKS
N02  C                   MOVEL     SIFS          SXIFSOBJ
N02  C                   EVAL      *IN51=*OFF
N02  C                   ELSE
N02  C     SMBR          CHAIN     EDISX                              51
N02  C                   ENDIF
N02  C                   CALL      'EDI41SI'
N02  C                   PARM                    SXIFSOBJ
     C                   ENDSL
     C                   END
     C                   END
      *
     C                   MOVE      *BLANKS       ZOP
     C     MSR           COMP      'R'                                    27
     C                   UPDATE    SVI014DA
      *
     C     SLSRBH        ENDSR
      *////////////////////////////////////////////////////////////
      *  Hent ev. feiltext                                   SRBB /
      *////////////////////////////////////////////////////////////
     C     SRBB          BEGSR
     C                   EVAL      WTXT=*BLANKS
     C                   EVAL      WLN=1
     C     KEYB          CHAIN     TVINF                              84
     C     *IN84         IFEQ      '0'
     C                   MOVEL     F0078B        WTXT
     C     KEYB          READE     TVINF                                  84
     C     *IN84         IFEQ      '0'
     C                   CAT       'Fler linje':3WTXT
     C                   CAT       'r anvænd o':0WTXT
     C                   CAT       'ption: 6':0  WTXT
     C                   ENDIF
     C                   ENDIF
     C                   ENDSR
