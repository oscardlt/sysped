             PGM
             /*_____________________*/
             /* Deklarere Variabler */
             /*"""""""""""""""""""""*/
             DCL        VAR(&LIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&FIL) TYPE(*CHAR) LEN(10)
             DCL        VAR(&MBR) TYPE(*CHAR) LEN(10)
             DCL        VAR(&OK) TYPE(*LGL) LEN(1)
             DCL        VAR(&SSN) TYPE(*DEC) LEN(9 0)
             DCL        VAR(&SST) TYPE(*CHAR) LEN(1) VALUE(C)
             DCL        VAR(&NETW) TYPE(*CHAR) LEN(1) VALUE('0')
             DCL        VAR(&IIDS) TYPE(*CHAR) LEN(35)
             DCL        VAR(&IIDR) TYPE(*CHAR) LEN(35)
             DCL        VAR(&OKTCP) TYPE(*CHAR) LEN(2)
             DCL        VAR(&STRTIME) TYPE(*CHAR) LEN(6)
             DCL        VAR(&ENDTIME) TYPE(*CHAR) LEN(6)
             DCL        VAR(&QTIME) TYPE(*CHAR) LEN(6)
             DCL        VAR(&NBRCURRCD) TYPE(*DEC) LEN(10 0)
             DCL        VAR(&QRNAME) TYPE(*CHAR) LEN(10) +
                          VALUE('DKDTAQ')
             DCL        VAR(&QRLIB) TYPE(*CHAR) LEN(10) VALUE('*LIBL')
             DCL        VAR(&QRLEN) TYPE(*DEC) LEN(5 0) VALUE(1)
             DCL        VAR(&QRFIEL) TYPE(*CHAR) LEN(1)
             DCL        VAR(&QWAIT) TYPE(*DEC) LEN(5 0) VALUE(600)
             DCL        VAR(&QWAIT2) TYPE(*DEC) LEN(5 0) VALUE(45)
             /*__________________________________________________*/
             /* Denne Jobben kan bli startet *IMMED eller        */
             /* venter til et klokkeslett som finnes i firmans   */
             /* dataarea.                                        */
             /*""""""""""""""""""""""""""""""""""""""""""""""""""*/
             RTVDTAARA  DTAARA(EDIDTA (154 6)) RTNVAR(&STRTIME)
             IF         COND(&STRTIME *EQ *IMMED) THEN(GOTO +
                          CMDLBL(START))

             RTVSYSVAL  SYSVAL(QTIME) RTNVAR(&QTIME)
             IF         COND(&STRTIME *LE &QTIME) THEN(GOTO +
                          CMDLBL(START))

             DLYJOB     RSMTIME(&STRTIME)
 /*__________________________*/
 /* Skal polling avsluttes ? */
 /*""""""""""""""""""""""""""*/
 START:      RTVDTAARA  DTAARA(EDIDTA (160 6)) RTNVAR(&ENDTIME)
             IF         COND(&ENDTIME *NE *NONE) THEN(DO)
             RTVSYSVAL  SYSVAL(QTIME) RTNVAR(&QTIME)
             IF         COND(&ENDTIME *LT &QTIME) THEN(GOTO +
                          CMDLBL(END))
             ENDDO
             /*_______________*/
             /* Mottak av MSG */
             /*"""""""""""""""*/
             CALL       PGM(EDI07C0R) PARM(' ') /* Produksjon */
             CALL       PGM(EDI07C0R) PARM('1') /* Test */
             /*_______________*/
             /* Finn status A */
             /*"""""""""""""""*/
 START2:     CALL       PGM(EDI07R) PARM(&OK &LIB &FIL &MBR &SSN +
                          &NETW &IIDS &IIDR)
             IF         COND(&OK *EQ '1') THEN(DO)
             CALL       PGM(QRCVDTAQ) PARM(&QRNAME &QRLIB &QRLEN +
                          &QRFIEL &QWAIT)
             GOTO       CMDLBL(START)
             ENDDO
             /*______*/
             /* Send */
             /*""""""*/
             CALL       PGM(EDI07C0S) PARM(&IIDR &OKTCP &SSN)
             IF         COND(&OKTCP *EQ 'OK') THEN(CALL PGM(EDI06R) +
                          PARM(&SSN &SST &NBRCURRCD))
             IF         COND(&OKTCP *NE 'OK') THEN(CALL PGM(EDI05R) +
                          PARM(&SSN))
             CALL       PGM(EDI055R) PARM(&SSN)
             /*_______________*/
             /* Mottak av MSG */
             /*"""""""""""""""*/
             CALL       PGM(QRCVDTAQ) PARM(&QRNAME &QRLIB &QRLEN +
                          &QRFIEL &QWAIT2)
             CALL       PGM(EDI07C0R) PARM(' ') /* Produksjon */
             CALL       PGM(EDI07C0R) PARM('1') /* Test */

             GOTO       CMDLBL(START2)

 END:        ENDPGM
