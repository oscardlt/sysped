      *
      ************************************************************
      *** PROGRAM SKAL RETTE FRAKTBREV/TOLLPASSFRAKTBREV       ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER     ***
      *** LEDIGE INDIKATORER 01 02 04-26                       ***
      *** LEDIGE INDIKATORER 27-29 31-96                       ***
      ************************************************************
     FKODTA     IF   E           K DISK
     FHEADF     IF   E           K DISK
     FDOKUF     IF   E           K DISK
     FFIRM      IF   E           K DISK
     FSYRE10FM  CF   E             WORKSTN
     D                UDS
     D  UFIRM                  1      2
     D  UAVD                  15     18  0
     C*
     C     KODTAK        KLIST
     C                   KFLD                    YAUNI
     C                   KFLD                    WSAVD
     C     HEADFK        KLIST
     C                   KFLD                    WSAVD
     C                   KFLD                    WSOPD
     C     DOKUFK        KLIST
     C                   KFLD                    YRI
     C                   KFLD                    WSAVD
     C                   KFLD                    WSOPD
     C                   KFLD                    WSFBNR
     C*
     C                   MOVE      'A'           YAUNI             1
     C                   MOVE      'F'           YRI               1
     C                   MOVE      *BLANKS       XTRNV            30
     C                   MOVE      ' '           XKDME             1
     C                   MOVE      ' '           XKDE              1
     C                   MOVE      *ZEROS        XTOLL             2 0
     C                   MOVE      *BLANKS       WSMSG            62
     C*
     C                   Z-ADD     1             WSFBNR
     C                   MOVE      UAVD          WSAVD
     C     FIFIRM        SETLL     FIRM
     C                   READ      FIRM                                   33
     C*
     C     START         TAG
     C                   EXFMT     BILDE
     C                   SETOFF                                       30
     C*
     C   03              GOTO      SLUTT
     C*
     C     KODTAK        CHAIN     KODTA                              99
     C   99              SETON                                        30
     C   99              MOVE      'SPA0001'     MSGNR
     C   99              GOTO      START
     C     HEADFK        CHAIN     HEADF                              98
     C   98              SETON                                        30
     C   98              MOVE      'SPO0001'     MSGNR
     C   98              GOTO      START
     C     WSFBNR        IFEQ      *ZEROS
     C                   SETON                                        30
     C                   SETON                                        97
     C                   MOVE      'BBA0147'     MSGNR
     C                   GOTO      START
     C                   ENDIF
     C     DOKUFK        CHAIN     DOKUF                              44
     C                   SETOFF                                       97
     C   06
     CANN44              SETON                                        97
     C  N06
     CAN 44              SETON                                        97
     C   97              SETON                                        30
     C   97              MOVE      'SPF0004'     MSGNR
     C   97              GOTO      START
     C*
     C                   Z-ADD     9             UFLAGG            1 0
     C                   MOVE      *BLANKS       TEST              1
     C                   CALL      'SYFI10A'
     C                   PARM                    UFIRM
     C                   PARM                    WSAVD
     C                   PARM                    WSOPD
     C                   PARM                    WSFBNR
     C                   PARM                    KOAKNR
     C                   PARM                    XTRNV
     C                   PARM                    XKDME
     C                   PARM                    XKDE
     C                   PARM                    XTOLL
     C                   PARM                    FILFB
     C                   PARM                    FILTB
     C                   PARM                    WSMSG
     C                   PARM                    UFLAGG
     C                   PARM                    TEST
     C*
     C                   GOTO      START
     C*
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
     C*
