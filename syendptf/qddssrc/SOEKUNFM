     A*%%TS  SD  20171002  122708  JOVO        REL-V7R1M0  5770-WDS
*****A**************************************************************
     A* RETTINGER I DETTE PROGRAM:
PTFnrA*Sek Sig Vers Beskrivelse...........................
"""""A*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXXA* 01 YBC PTF10 VIS ORGANISASJONSNR
10XXXA* 02 JOV PTF10 ORG '000000000'->*blanks, Vis mailadr i kundebilde(DDS)
10XXXA* 03 YBC PTF10 LANDKODE ER MED PÅ SØKING
10XXXA* 06 sh  PTF10 utterligere litt cursorfix + tlf i subfil
10XXXA* 07 sh  PTF10 alltid cursor i kundnavn 1. gang
10XXXA* 10 JOV PTF10 LEDETEKSTER / FILTER PÅ TYPE
11058A* 11 CB  PTF11 SØK ALFA VIA F2 FOR MEST BRUKTE KUNDER ØVERST
*****A**************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3                  -
     A                                             27 132 *DS4)
     A                                      PRINT
     A                                      HLPTITLE('VEDLIKEHOLD AV KUNDERS LE-
     A                                      VERINGSADRESSER')
     A                                      HLPPNLGRP(HLPSCR SOEKUNPNL)
     A                                      ALTHELP
     A                                      HELP
      * "NOERASE" MÅ HA FOR Å IKKE TØMME BILDE SOM MAN KOMME FRA
     A          R NOERASE
     A                                      ASSUME
     A                                  1  2' '
      * MAN KUN DEFINNERE VINDU PÅ REKORDNIVÅ
     A          R WINDOWSZ
     A*                                     WINDOW(*DFT 16 74 *NOMSGLIN)
     A                                      WINDOW(*DFT 17 74 *NOMSGLIN)
     A                                      OVERLAY
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  5  2VALUES(' ' '1' '5' '8' 'S' 'P')
     A            KUNDNR    R        O  5  5REFFLD(KUNDR/KUNDNR *LIBL/CUNDF)
     A N70 42                               COLOR(WHT)
     A  70                                  COLOR(YLW)
     A                                      EDTCDE(4)
     A            VADRNA    R        H      REFFLD(VADRR/VADRNA *LIBL/VADR)
     A            KNAVN     R        O  5 14REFFLD(KUNDR/KNAVN *LIBL/CUNDF)
     A N70 42                               COLOR(WHT)
     A  70                                  COLOR(YLW)
     A            ADR1      R        H      REFFLD(KUNDR/ADR1 *LIBL/CUNDF)
     A            VADRN1    R        H      REFFLD(VADRR/VADRN1 *LIBL/VADR)
     A            WSADR1        28A  O  5 45
     A N70 42                               COLOR(WHT)
     A  70                                  COLOR(YLW)
     A            ADR2      R        H      REFFLD(KUNDR/ADR2 *LIBL/CUNDF)
     A            VADRN2    R        H      REFFLD(VADRR/VADRN2 *LIBL/VADR)
S01  A*           WSADR2    R        O  6 14REFFLD(KUNDR/ADR2 *LIBL/CUNDF)
S07  A*           SYFORE         9   B  6  4
N07  A            SYFORE         9   O  6  4
     A                                      DSPATR(UL)
     A  42                                  COLOR(WHT)
     A  85                                  COLOR(RED)
N01  A            XXTKRE         8Y 0O  6 14EDTCDE(4)
N01  A                                      DSPATR(UL)
N01  A  42                                  COLOR(WHT)
N01  A            SYLIKV    R        O  6 23REFFLD(KUNDR/SYLIKV *LIBL/CUNDF)
N01  A                                      DSPATR(UL)
N01  A  42                                  COLOR(WHT)
S06  A*           TFAXNR        15A  O  6 26
N06  A            TLF           15A  O  6 26
N01  A                                      DSPATR(UL)
N01  A  42                                  COLOR(WHT)
N10  A            SYLAND         2A  O  6 42
N10  A                                      DSPATR(UL)
N10  A  42                                  COLOR(WHT)
     A            POSTNR    R        H      REFFLD(KUNDR/POSTNR *LIBL/CUNDF)
S02  A*           WSPNR     R        O  6 45REFFLD(KUNDR/POSTNR *LIBL/CUNDF)
S02  A*                                     DSPATR(UL)
S02  A* 42                                  COLOR(WHT)
S02  A*                                     EDTCDE(4)
N02  A            WSPNRST       28A  O  6 45DSPATR(UL)
N02  A  42                                  COLOR(WHT)
     A            ADR3      R        H      REFFLD(KUNDR/ADR3 *LIBL/CUNDF)
     A            VADRN3    R        H      REFFLD(VADRR/VADRN3 *LIBL/VADR)
S02  A*           WSADR3        23A  O  6 50DSPATR(UL)
S02  A* 42                                  COLOR(WHT)
     A            AKTKOD         1A  H
     A            FIRMA          2A  H
     A            DKUND          1A  H
     A            KPERS         30A  H
S06  A*           TLF           15A  H
     A            SONAVN        10A  H
     A            VALKOD         3A  H
     A            SPRAAK         1A  H
     A            BANKG         15A  H
     A            POSTG         20A  H
     A            FMOT           8S 0H
     A            BETBET         2A  H
     A            BETMAT         1A  H
     A            SFAKT          1A  H
     A            KGRENS         7Y 0H
N01  A*           TFAXNR        15A  H
N06  A            TFAXNR        15A  H
     A            SYREGN         8S 0H
     A            SYKONT         7Y 0H
S01  A*           SYLIKV         1A  H
     A            SYOPDT         2A  H
     A            SYMINU         4S 2H
     A            SYUTLP         4S 2H
     A            SYRG          14A  H
     A            SYPOGE         9A  H
XXX  A            SYEPOS        70A  H
     A            SYSTAT         3A  H
S10  A*           SYLAND         2A  H
     A            SYSELG         3A  H
     A            SYIAT1         7S 0H
     A            SYIAT2         4S 0H
     A            SYCOTY         1A  H
     A            SYFR01         1A  H
     A            SYFR02         1A  H
     A            SYFR03         2A  H
     A            SYFR04         2A  H
     A            SYFR05         3A  H
     A            SYFR06         3A  H
     A            SYSALU        13Y 0H
     A            SFARKIV        1   O  5 74COLOR(RED)
     A*
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A*%%TS  SD  20171002  122708  JOVO        REL-V7R1M0  5770-WDS
     A  *DS3                                SFLSIZ(0006)
     A  *DS4                                SFLSIZ(0006)
     A  *DS3                                SFLPAG(0005)
     A  *DS4                                SFLPAG(0005)
     A  *DS3                                WINDOW(WINDOWSZ)
     A  *DS4                                WINDOW(WINDOWSZ)
     A  91N93N94                            ROLLUP(25)
     A                                      CF02(02 'Søk via mestbrukt')
     A                                      CA03(03 'Forrige')
     A                                      CA05(05 'Frisk')
     A                                      CA06(06 'LAGE NY')
     A                                      CA07(07 'EMAIL JN')
     A                                      CA09(09 'SYSPED VEDL.')
     A                                      CA12(12 'Forrige')
     A                                      CA10(10 'Frisøk')
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            ZRECNO         4S 0H      SFLRCDNBR
     A                                  1  2'SOEKKUN'
     A                                      COLOR(BLU)
     A                                  1 10MSGCON(050 DIV1694 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  2  2MSGCON(070 JOV1435 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
S10  A*                                 3  2MSGCON(070 SKU0060 *LIBL/SYMSGF)
     A                                  3  2MSGCON(050 JOV1528 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
NXX  A**                                3 54'Pn.Utland'
NXX  A**          W2PNRU         9A  I  3 64
     A            W2NR           1A  I  4  2DSPATR(UL)
     A                                      VALUES(' ' '1' '5')
     A            W2KUNR         8D 0I  4  5DSPATR(UL)
     A            W2ALFA        10A  I  4 14DSPATR(UL)
S03  A*                                 4 25MSGCON(012 DIV1698 *LIBL/SYMSGF)
S10  A*                                 4 25MSGCON(008 DIV1698 *LIBL/SYMSGF)
     A                                  4 25MSGCON(006 JOV1527 *LIBL/SYMSGF)
     A                                  4 41'LK'
     A            W2LAND         2A  I  4 44DSPATR(UL)
S10  A*                                 4 40MSGCON(010 SHU0117 *LIBL/SYMSGF)
     A                                  4 47MSGCON(006 SHU0117 *LIBL/SYMSGF)
     A            W2POST         4D  I  4 54DSPATR(UL)
     A                                  4 59'Fore'
     A            W2FORE        11A  I  4 64
     A                                  3 74'A'
     A                                      COLOR(WHT)
     A                                  4 32'Ty'
     A            W2TYPE         1A  I  4 35DSPATR(UL)
     A                                  4 37'A/F'
     A*
     A          R BUNN
     A*%%TS  SD  20100305  110904  JOVO        REL-V5R4M0  5722-WDS
     A  *DS3                                WINDOW(WINDOWSZ)
     A  *DS4                                WINDOW(WINDOWSZ)
     A                                      SLNO(15)
     A                                      CLRL(02)
     A                                      OVERLAY
     A  30        MSGTXT        70A  O  1  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A                                  2  1MSGCON(029 YBC0212 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  2 44MSGCON(018 JOV0998 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  2 63MSGCON(012 SHU1027 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A            F7TXT         12   O  2 31COLOR(BLU)
NXX  A                                  3  1MSGCON(040 JOV1751 *LIBL/SYMSGF)
NXX  A                                      COLOR(BLU)
     A*
     A          R NYWRK
     A*%%TS  SD  20101112  181553  JOVO        REL-V5R4M0  5722-WDS
     A  *DS3                                WINDOW(WINDOWSZ)
     A  *DS4                                WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A                                      CF02(02 'Søk via mestbrukt')
     A                                      CA03(03 'Forrige')
     A                                      CA05(05 'Frisk')
     A                                      CA06(06 'LAGE NY')
     A                                      CA10(10 'Frisøk')
     A                                      CA09(09 'SYSPED VEDL.')
     A                                      CA12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SOEKKUN'
     A                                      COLOR(BLU)
     A                                  1 10MSGCON(050 DIV1694 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  2  2MSGCON(070 JOV1435 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
S10  A*                                 3  2MSGCON(070 SKU0060 *LIBL/SYMSGF)
     A                                  3  2MSGCON(050 JOV1528 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
NXX  A**          W2PNRU         9A  I  3 64
NXX  A**                                3 54'Pn.Utland'
     A            W2NR           1A  I  4  2VALUES(' ' '1')
     A                                      DSPATR(UL)
     A            W2KUNR         8D 0I  4  5DSPATR(UL)
     A            W2ALFA        10A  I  4 14DSPATR(UL)
S03  A*                                 4 25MSGCON(012 DIV1698 *LIBL/SYMSGF)
S10  A*                                 4 25MSGCON(008 DIV1698 *LIBL/SYMSGF)
     A                                  4 25MSGCON(006 JOV1527 *LIBL/SYMSGF)
N10  A                                  4 32'Ty'
N10  A            W2TYPE         1A  I  4 35DSPATR(UL)
N10  A                                  4 37'A/F'
     A                                  4 41'LK'
     A            W2LAND         2A  I  4 44DSPATR(UL)
S10  A*                                 4 40MSGCON(010 SHU0117 *LIBL/SYMSGF)
     A                                  4 47MSGCON(006 SHU0117 *LIBL/SYMSGF)
     A            W2POST         4D  I  4 54DSPATR(UL)
     A                                  4 59'Fore'
     A            W2FORE        11A  I  4 64
     A N30                             15  2MSGCON(029 YBC0212 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        70A  O 15  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A                                 15 38MSGCON(018 JOV0998 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 15 57MSGCON(012 SHU1027 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
NXX  A                                 16  1MSGCON(040 JOV1751 *LIBL/SYMSGF)
NXX  A                                      COLOR(BLU)
     A*
     A          R BILDE01
     A*%%TS  SD  20100304  114109  JOVO        REL-V5R4M0  5722-WDS
     A                                      CA03(03 'Forrige')
     A                                      CA12(12 'Forrige')
     A                                  1  2'SYSPED/SOEKKUN'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(040 DIV1590 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 71TIME
     A                                      COLOR(BLU)
     A                                  2 71DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  3  3MSGCON(025 DIV1592 *LIBL/SYMSGF)
     A            KUNDNR         8Y 0O  3 32TEXT('Kundenummer')
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  5  3MSGCON(025 DIV1593 *LIBL/SYMSGF)
     A            KNAVN         30A  O  5 32TEXT('Kundenavn')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  7  3MSGCON(025 DIV1594 *LIBL/SYMSGF)
     A            KPERS         30A  O  7 32TEXT('Kontaktperson')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  8  3MSGCON(025 DIV1595 *LIBL/SYMSGF)
     A            ADR1          30A  O  8 32TEXT('Adresse 1')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  9  3MSGCON(025 DIV1596 *LIBL/SYMSGF)
     A            ADR2          30A  O  9 32TEXT('Adresse 2')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 10  3MSGCON(025 DIV1597 *LIBL/SYMSGF)
     A            POSTNR         4Y 0O 10 32TEXT('Postnummer')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 10 37'/'
     A            SYPOGE         9A  O 10 39DSPATR(UL)
     A                                      COLOR(WHT)
     A            ADR3          24A  O 10 51TEXT('Sted')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 11  3MSGCON(025 DIV2005 *LIBL/SYMSGF)
     A            SYLAND         2A  O 11 32DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 11 51MSGCON(016 SHU0116 *LIBL/SYMSGF)
     A                                 12  3MSGCON(025 DIV1599 *LIBL/SYMSGF)
     A            SONAVN        10A  O 12 32TEXT('Søkenavn')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            VADRNA    R        O 12 51REFFLD(VADRR/VADRNA *LIBL/VADR)
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 13  3MSGCON(025 DIV1600 *LIBL/SYMSGF)
     A            VALKOD         3A  O 13 32TEXT('Valutakode')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            VADRN1    R        O 13 51REFFLD(VADRR/VADRN1 *LIBL/VADR)
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 14  3MSGCON(025 DIV1601 *LIBL/SYMSGF)
     A            FMOT           8Y 0O 14 32TEXT('Fakturamottager v/kjede')
     A                                      EDTCDE(4)
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            VADRN2    R        O 14 51REFFLD(VADRR/VADRN2 *LIBL/VADR)
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 15  3MSGCON(025 DIV1598 *LIBL/SYMSGF)
     A            TLF           15A  O 15 32TEXT('Telefon')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            VADRN3    R        O 15 51REFFLD(VADRR/VADRN3 *LIBL/VADR)
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 16  3MSGCON(025 DIV3560 *LIBL/SYMSGF)
     A            TFAXNR        15A  O 16 32DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 17  3'eMail:'
     A            SYEPOS    R        O 17 11REFFLD(KUNDR/SYEPOS *LIBL/CUNDF)
     A                                      COLOR(WHT)
     A                                 18  3'Utleggsprov. minim/sats..'
     A            SYMINU         4Y 2O 18 32TEXT('MIN.UTLPRO')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            SYUTLP         4Y 2O 18 39TEXT('UTL.PROV')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 18 47MSGCON(035 DIV1612 *LIBL/SYMSGF)
     A                                 19  3MSGCON(025 DIV1604 *LIBL/SYMSGF)
     A            SYREGN         8Y 0O 19 32TEXT('REGNR')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 19 42MSGCON(025 DIV1605 *LIBL/SYMSGF)
     A            SYFR02         1A  O 19 69DSPATR(UL)
     A                                      COLOR(WHT)
     A            SYKONT    R        O 19 71REFFLD(KUNDR/SYKONT *LIBL/CUNDF)
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTWRD('     -  ')
     A                                 20  3MSGCON(025 DIV1606 *LIBL/SYMSGF)
     A            SYOPDT         2A  O 20 32TEXT('OPD.TYPE')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 20 42MSGCON(025 DIV1607 *LIBL/SYMSGF)
     A            SYLIKV         1A  O 20 70TEXT('LIKV.KODE')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 21  3MSGCON(025 SHU0024 *LIBL/SYMSGF)
     A            SFAKT          1A  O 21 32DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 21 42MSGCON(025 SHU0172 *LIBL/SYMSGF)
     A            SYFORE         9A  O 21 70DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 22  3MSGCON(025 SHU0139 *LIBL/SYMSGF)
     A            DKUND          1A  O 22 32DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 22 42MSGCON(025 JOV0265 *LIBL/SYMSGF)
     A            SYFR03         2A  O 22 70DSPATR(UL)
     A                                      COLOR(WHT)
