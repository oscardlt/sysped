      * L1-SYEDI For DESADV D.01B
      *
      * 1. Dette programmet blir kallt fra utskrift av pakkseddel i L1
      *    med parametre firmakode, kundenr, pakkseddelnr + årstall
      *
      * 2. Selgerskundenr er hardkodet og må rettes ved hver installasjon.
      *    (SøK på WUNDNR2)
      *
      * 3. Dette programmet legger ut til EDI-filer i SYEDI.
      *
      * 4. SYEDI lager automatisk EDIFACT utifra EDI-filene.
      *
     FDORDL24   IF   E           K DISK    PREFIX(Z_)
     FHORDL11   IF   E           K DISK    PREFIX(Y_)
     FKODML01   IF   E           K DISK    PREFIX(M_)
     FKODNL01   IF   E           K DISK    PREFIX(N_)
     FKOMML01   IF   E           K DISK    PREFIX(V_)
     FKUNDL01   IF   E           K DISK    PREFIX(X_)
     FVADRL01   IF   E           K DISK    PREFIX(Å_)
     FVAREL01   IF   E           K DISK    PREFIX(Q_)
     FECMSG     IF   E           K DISK
     FEDIC      UF   E             DISK
     FEUNH      O  A E             DISK
     FEDIM      O  A E             DISK
     FECNT01B   O  A E             DISK
     FECPS01B   O  A E             DISK
     FEDTM01B   O  A E             DISK
     FEFTX01B   O  A E             DISK
     FEIMD01B   O  A E             DISK
     FELIN01B   O  A E             DISK
     FELOC01B   O  A E             DISK
     FENAD01B   O  A E             DISK
     FEPIA01B   O  A E             DISK
     FEQTY01B   O  A E             DISK
     FERFF01B   O  A E             DISK
     FETOD01B   O  A E             DISK
     FEUNS      O  A E             DISK
      *___________________________
      * Arrays for string handling
      *"""""""""""""""""""""""""""
     D                 DS
     D  WTIME                  1     14  0
     D  WTID                   1      6  0
     D  WDD                    7      8  0
     D  WMM                    9     10  0
     D  WÅÅ                   11     14  0
     D                 DS
     D  WDATO                  1      8  0
     D  WÅ                     1      4  0
     D  WM                     5      6  0
     D  WD                     7      8  0
     D                 DS
     D  VLEDAT                 1      8
     D  Z_VLEAAR               1      4  0
     D  Z_VLEMND               5      6  0
     D  Z_VLEDAG               7      8  0
      *____________________________________
      * DS for leftjustify numericel fields
      *""""""""""""""""""""""""""""""""""""
     D                 DS
     D  ØN153                  1     15  3
     D  ØA153H                 1     12
     D  ØA153X                 5     12
     D  ØA153D                13     15
     D                 DS
     D  ØA12                   1     12
     D  Ø12                    1     12
     D                                     DIM(12)
      */////////////
      * MAIN LOGIC /
      */////////////
     C                   EXSR      SRINIT
     C                   EXSR      SRA
     C                   MOVE      '1'           *INLR
      *////////////////////////////////////////////////////////////
      * SR                                                  CNT00 /
      *////////////////////////////////////////////////////////////
     C     CNT00         BEGSR
     C                   Z-ADD     1             WREP0
     C                   CLEAR                   CNTR
     C                   Z-ADD     WMN           CMN
     C                   Z-ADD     0             CGN
     C                   Z-ADD     WREP0         CREP0
     C                   MOVEL     '2'           C6069
     C                   Z-ADD     XANTL         ØN153
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           C6066
     C                   WRITE     CNTR

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  CPS10 /
      *////////////////////////////////////////////////////////////
     C     CPS10         BEGSR
     C                   Z-ADD     1             WREP0
     C                   CLEAR                   CPSR
     C                   Z-ADD     WMN           CMN
     C                   Z-ADD     10            CGN
     C                   Z-ADD     WREP0         CREP0
     C                   MOVEL     '1'           C7164
     C                   WRITE     CPSR

     C                   EXSR      FTX10
     C                   EXSR      LIN17

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  DTM00 /
      *////////////////////////////////////////////////////////////
     C     DTM00         BEGSR
     C                   Z-ADD     1             WREP0
     C                   CLEAR                   DTMR
     C                   Z-ADD     WMN           DMN
     C                   Z-ADD     0             DGN
     C                   Z-ADD     WREP0         DREP0
      *_____________
      * Meldingsdato
      *"""""""""""""
     C                   MOVEL     '137'         D2005
     C                   MOVEL     WDATO         XAN14            14
     C                   MOVE      WTID          XAN14
     C                   MOVEL     XAN14         D2380
     C                   MOVEL     '203'         D2379
     C                   WRITE     DTMR
      *_____________
      * Leveingsdato
      *"""""""""""""
     C                   ADD       1             DREP0
     C                   MOVEL     '137'         D2005
     C                   MOVE      *BLANKS       D2380
     C                   MOVEL(P)  VLEDAT        D2380
     C                   MOVEL     '102'         D2379
     C                   WRITE     DTMR

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * EDIM0                                                EDIM0/
      *////////////////////////////////////////////////////////////
     C     EDIM0         BEGSR
     C                   CLEAR                   EDIMR
     C                   MOVEL     ECIIDI        M0004
     C                   MOVEL     ECIIDC        M0010
     C                   MOVE      '1'           M0035
     C                   MOVEL     U0062A        M0062
     C                   MOVEL     U0065         M0065
     C                   MOVEL     U1004         M0068
     C                   MOVEL     U1001         M1001
     C                   MOVEL     U1004         M1004
     C                   Z-ADD     WMN           MMN
     C                   MOVE      'S'           MSR
     C                   MOVEL     WDATO         MDT
     C                   MOVEL     WTID          MTM
     C                   WRITE     EDIMR

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  FTX10 /
      *////////////////////////////////////////////////////////////
     C     FTX10         BEGSR
     C                   Z-ADD     0             WREP1
      *___________________________
      * Merke og nr fra leverandør
      *"""""""""""""""""""""""""""
     C     KOMML01K      SETLL     KOMML01
     C     KOMML01K      READE     KOMML01                                52
     C                   DOW       *IN52 = *OFF
     C                   IF        ((V_UTSKDP = 'J') AND (V_OTBLNR = 0))
     C                   ADD       1             WREP1
     C                   CLEAR                   FTXR
     C                   Z-ADD     WMN           FMN
     C                   Z-ADD     10            FGN
     C                   Z-ADD     WREP0         FREP0
     C                   Z-ADD     WREP1         FREP1
     C                   MOVEL     'AAI'         F4451
     C                   MOVEL     V_OTBKOM      F4440A
     C                   WRITE     FTXR
     C                   ENDIF
     C     KOMML01K      READE     KOMML01                                52
     C  N52              ENDDO

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  FTX17 /
      *////////////////////////////////////////////////////////////
     C     FTX17         BEGSR
      *_______________________
      * Merking og nr på linje
      *"""""""""""""""""""""""
     C                   IF        Z_MERK <> *BLANKS
     C                   Z-ADD     1             WREP2
     C                   CLEAR                   FTXR
     C                   Z-ADD     WMN           FMN
     C                   Z-ADD     17            FGN
     C                   Z-ADD     WREP0         FREP0
     C                   Z-ADD     WREP1         FREP1
     C                   Z-ADD     WREP2         FREP2
     C                   MOVEL     'SUR'         F4451
     C                   MOVEL     Z_MERK        F4440A
     C                   WRITE     FTXR
     C                   ENDIF

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  IMD17 /
      *////////////////////////////////////////////////////////////
     C     IMD17         BEGSR
      *________________
      * Varebeskrivelse
      *""""""""""""""""
     C                   Z-ADD     1             WREP2
     C                   CLEAR                   IMDR
     C                   Z-ADD     WMN           IMN
     C                   Z-ADD     17            IGN
     C                   Z-ADD     WREP0         IREP0
     C                   Z-ADD     WREP1         IREP1
     C                   Z-ADD     WREP2         IREP2
     C                   MOVEL     'F'           I7077
     C     KOMML01K2     SETLL     KOMML01
     C     KOMML01K2     READE     KOMML01                                52
     C                   IF        *IN52 = *OFF
     C                   MOVEL     V_OTBKOM      XAN30            30
     C                   MOVEL     XAN30         I7008A
     C                   MOVE      V_OTBKOM      XAN30
     C                   MOVEL     XAN30         I7008B
     C                   ELSE
     C                   MOVEL     Q_VARTE1      I7008A
     C                   MOVEL     Q_VARTE2      I7008B
     C                   ENDIF
     C                   WRITE     IMDR

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  LIN17 /
      *////////////////////////////////////////////////////////////
     C     LIN17         BEGSR
     C                   Z-ADD     0             WREP1

     C     *IN53         DOWEQ     *OFF
     C                   ADD       1             WREP1
     C                   CLEAR                   LINR
     C                   Z-ADD     WMN           LMN
     C                   Z-ADD     17            LGN
     C                   Z-ADD     WREP0         LREP0
     C                   Z-ADD     WREP1         LREP1
     C                   Z-ADD     WREP1         ØN153
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           L1082A
     C                   WRITE     LINR

     C     VAREL01K      CHAIN     VAREL01                            52

     C                   EXSR      PIA17
     C                   EXSR      IMD17
     C                   EXSR      QTY17
     C                   EXSR      FTX17
     C                   EXSR      RFF18
     C                   EXSR      LOC20

     C     DORKEY        READE     DORDL24                                53
     C  N53HORKEY        CHAIN     HORDL11                            52
     C  N53              ENDDO

     C                   MOVE      WREP1         XANTL             5 0

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  LOC20 /
      *////////////////////////////////////////////////////////////
     C     LOC20         BEGSR
      *______________
      * lagerposisjon
      *""""""""""""""
     C                   IF        Z_LPLASS <> *BLANKS
     C                   Z-ADD     1             WREP2
     C                   CLEAR                   LOCR
     C                   Z-ADD     WMN           LMN
     C                   Z-ADD     20            LGN
     C                   Z-ADD     WREP0         LREP0
     C                   Z-ADD     WREP1         LREP1
     C                   Z-ADD     WREP2         LREP2
     C                   MOVEL     'ZZZ'         L3227
     C                   MOVEL     Z_LPLASS      L3224
     C                   WRITE     LOCR
     C                   ENDIF

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  NAD02 /
      *////////////////////////////////////////////////////////////
     C     NAD02         BEGSR
      *_______
      * Kjøper
      *"""""""
     C                   Z-ADD     1             WREP0
     C                   CLEAR                   NADR
     C                   Z-ADD     WMN           NMN
     C                   Z-ADD     2             NGN
     C                   Z-ADD     WREP0         NREP0

     C                   MOVEL     'BY'          N3035
     C     KUNKEY        CHAIN     KUNDL01                            52
     C                   Z-ADD     WUNDNR        ØN153
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           N3039
     C                   MOVEL     '9'           N3055
     C                   MOVEL     X_KNAVN       N3124A
     C                   MOVEL     X_ADR1        N3124B
     C                   MOVEL     X_ADR2        N3124C
     C                   MOVEL     X_ADR3        N3164
     C                   IF        X_POSTNR = 0
     C                   MOVEL     X_POSTNU      N3251
     C                   ELSE
     C                   MOVEL     X_POSTNR      N3251
     C                   ENDIF
     C                   MOVEL     X_LAND        N3207
     C                   WRITE     NADR
      *___________________________________
      * Mottaker (hvis forskjellig fra BY)
      *"""""""""""""""""""""""""""""""""""
      * Hent vareadresse
     C     VADKEY        CHAIN     VADRL01                            52
     C     *IN52         IFEQ      *OFF
     C     Å_VADRNA      IFNE      X_KNAVN
     C     Å_VADRN1      ORNE      X_ADR1
     C     Å_VADRN3      ORNE      X_ADR3
     C     Å_VPOSTN      ORNE      X_POSTNR
     C     Å_POSTVA      ORNE      X_POSTNU

     C                   ADD       1             WREP0
     C                   CLEAR                   NADR
     C                   Z-ADD     WMN           NMN
     C                   Z-ADD     2             NGN
     C                   Z-ADD     WREP0         NREP0

     C                   MOVEL     'DP'          N3035
     C                   MOVEL     Å_VADRNA      N3124A
     C                   MOVEL     Å_VADRN1      N3124B
     C                   MOVEL     Å_VADRN2      N3124C
     C                   MOVEL     Å_VADRN3      N3164
     C                   IF        Å_VPOSTN = 0
     C                   MOVEL     Å_POSTVA      N3251
     C                   ELSE
     C                   MOVEL     Å_VPOSTN      N3251
     C                   ENDIF
     C                   MOVEL     Å_LANDVA      N3207
     C                   WRITE     NADR

     C                   ENDIF
     C                   ENDIF
      *_______
      * Selger
      *"""""""
     C                   ADD       1             WREP0
     C                   CLEAR                   NADR
     C                   Z-ADD     WMN           NMN
     C                   Z-ADD     2             NGN
     C                   Z-ADD     WREP0         NREP0

     C                   MOVEL     'SU'          N3035
     C     KUNKEY2       CHAIN     KUNDL01                            52
     C                   Z-ADD     WUNDNR2       ØN153
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           N3039
     C                   MOVEL     '9'           N3055
     C                   MOVEL     X_KNAVN       N3124A
     C                   MOVEL     X_ADR1        N3124B
     C                   MOVEL     X_ADR2        N3124C
     C                   MOVEL     X_ADR3        N3164
     C                   MOVEL     X_POSTNR      N3251
     C                   MOVEL     X_LAND        N3207
     C                   WRITE     NADR
      *____________
      * Transportør
      *""""""""""""
     C                   IF        Y_LEVMAT <> *BLANKS
     C                   ADD       1             WREP0
     C                   CLEAR                   NADR
     C                   Z-ADD     WMN           NMN
     C                   Z-ADD     2             NGN
     C                   Z-ADD     WREP0         NREP0

     C     KODNL01K      CHAIN     KODNL01                            52
     C                   MOVEL     'CA'          N3035
     C                   MOVEL     N_KODTXT      N3124A
     C                   WRITE     NADR
     C                   ENDIF
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  PIA17 /
      *////////////////////////////////////////////////////////////
     C     PIA17         BEGSR
      *___________________
      * Leverandørs varenr
      *"""""""""""""""""""
     C                   Z-ADD     1             WREP2
     C                   CLEAR                   PIAR
     C                   Z-ADD     WMN           PMN
     C                   Z-ADD     17            PGN
     C                   Z-ADD     WREP0         PREP0
     C                   Z-ADD     WREP1         PREP1
     C                   Z-ADD     WREP2         PREP2
     C                   MOVEL     '5'           P4347
     C                   MOVEL     Z_VARENR      P7140A
     C                   MOVEL     'SA'          P7143A
     C                   WRITE     PIAR
      *_______
      * EAN-nr
      *"""""""
     C                   IF        Q_EANNR <> 0
     C                   ADD       1             WREP2
     C                   CLEAR                   PIAR
     C                   Z-ADD     WMN           PMN
     C                   Z-ADD     17            PGN
     C                   Z-ADD     WREP0         PREP0
     C                   Z-ADD     WREP1         PREP1
     C                   Z-ADD     WREP2         PREP2
     C                   MOVEL     '5'           P4347
     C                   MOVEL     Q_EANNR       P7140A
     C                   MOVEL     'EN'          P7143A
     C                   WRITE     PIAR
     C                   ENDIF

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  QTY17 /
      *////////////////////////////////////////////////////////////
     C     QTY17         BEGSR
      *______________
      * Antall levert
      *""""""""""""""
     C                   Z-ADD     1             WREP2
     C                   CLEAR                   QTYR
     C                   Z-ADD     WMN           QMN
     C                   Z-ADD     17            QGN
     C                   Z-ADD     WREP0         QREP0
     C                   Z-ADD     WREP1         QREP1
     C                   Z-ADD     WREP2         QREP2
     C                   MOVEL     '12'          Q6063
     C                   Z-ADD     Z_LEVERT      ØN153
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           Q6060
     C                   MOVEL     'PCE'         Q6411
     C                   WRITE     QTYR
      *_______________
      * Antall bestilt
      *"""""""""""""""
     C                   ADD       1             WREP2
     C                   CLEAR                   QTYR
     C                   Z-ADD     WMN           QMN
     C                   Z-ADD     17            QGN
     C                   Z-ADD     WREP0         QREP0
     C                   Z-ADD     WREP1         QREP1
     C                   Z-ADD     WREP2         QREP2
     C                   MOVEL     '21'          Q6063
     C                   Z-ADD     Z_BESTIL      ØN153
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           Q6060
     C                   MOVEL     'PCE'         Q6411
     C                   WRITE     QTYR

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  RFF18 /
      *////////////////////////////////////////////////////////////
     C     RFF18         BEGSR
     C                   Z-ADD     0             WREP2
      *______________________
      * Kjøpers bestillingsnr
      *""""""""""""""""""""""
     C                   IF        Y_KUNREF <> *BLANKS
     C                   ADD       1             WREP2
     C                   CLEAR                   RFFR
     C                   Z-ADD     WMN           RMN
     C                   Z-ADD     18            RGN
     C                   Z-ADD     WREP0         RREP0
     C                   Z-ADD     WREP1         RREP1
     C                   Z-ADD     WREP2         RREP2
     C                   MOVEL     'ON'          R1153
     C                   MOVEL     Y_KUNREF      R1154
     C                   WRITE     RFFR
     C                   ENDIF
      *___________________________
      * Selgers ordrenr og linjenr
      *"""""""""""""""""""""""""""
     C                   ADD       1             WREP2
     C                   CLEAR                   RFFR
     C                   Z-ADD     WMN           RMN
     C                   Z-ADD     18            RGN
     C                   Z-ADD     WREP0         RREP0
     C                   Z-ADD     WREP1         RREP1
     C                   Z-ADD     WREP2         RREP2
     C                   MOVEL     'VN'          R1153
     C                   Z-ADD     Z_ORDNR       ØN153
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           R1154
     C                   Z-ADD     Z_LNRORD      ØN153
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           R1156
     C                   WRITE     RFFR

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR for init                                        SRINIT /
      *////////////////////////////////////////////////////////////
     C     SRINIT        BEGSR
     C     *ENTRY        PLIST
     C                   PARM                    UFIRMA            2
     C                   PARM                    UKN               8
     C                   PARM                    UAAR              4
     C                   PARM                    UPAKKSN           7
      * HENTE DAGENS DATO OG TID
     C                   TIME                    WTIME
     C     WÅÅ           IFGT      31
     C                   Z-ADD     WÅÅ           WÅ
     C                   Z-ADD     WDD           WD
     C                   ELSE
     C                   Z-ADD     WDD           WÅ
     C                   Z-ADD     WÅÅ           WD
     C                   ENDIF
     C                   Z-ADD     WMM           WM
      * INIT AV ARBEIDSFELT
     C     *LIKE         DEFINE    DREP0         WREP0
     C     *LIKE         DEFINE    DREP1         WREP1
     C     *LIKE         DEFINE    DREP2         WREP2
     C     *LIKE         DEFINE    ECSR          WCSR
     C     *LIKE         DEFINE    EC0065        WC0065
     C     *LIKE         DEFINE    Z_PAKAAR      WAKAAR
     C     *LIKE         DEFINE    Z_PAKKSN      WAKKSN
     C     *LIKE         DEFINE    Z_OTBKOD      WTBKOD
     C     *LIKE         DEFINE    Z_KUNDNR      WUNDNR
     C     *LIKE         DEFINE    Z_KUNDNR      WUNDNR2
     C     *LIKE         DEFINE    UMN           WMN
     C                   MOVE      UKN           WUNDNR
     C                   Z-ADD     3             WUNDNR2
     C                   MOVE      'S'           WCSR
     C                   MOVEL     'DESADV'      WC0065
     C                   MOVEL     'O'           WTBKOD
     C                   MOVE      UAAR          WAKAAR
     C                   MOVE      UPAKKSN       WAKKSN
     C                   Z-ADD     0             ØX                2 0
     C                   MOVE      *BLANKS       ØAN              12
     C                   Z-ADD     0             W_ØN153          15 3
      * DEF AV KEYS
     C     ECMKEY        KLIST
     C                   KFLD                    UFIRMA
     C                   KFLD                    WUNDNR
     C                   KFLD                    WCSR
     C                   KFLD                    WC0065
     C     HORKEY        KLIST
     C                   KFLD                    UFIRMA
     C                   KFLD                    WTBKOD
     C                   KFLD                    Z_ORDAAR
     C                   KFLD                    Z_ORDNR
     C     DORKEY        KLIST
     C                   KFLD                    UFIRMA
     C                   KFLD                    WTBKOD
     C                   KFLD                    WAKAAR
     C                   KFLD                    WAKKSN
     C     KUNKEY        KLIST
     C                   KFLD                    UFIRMA
     C                   KFLD                    WUNDNR
     C     KUNKEY2       KLIST
     C                   KFLD                    UFIRMA
     C                   KFLD                    WUNDNR2
     C     VADKEY        KLIST
     C                   KFLD                    UFIRMA
     C                   KFLD                    Z_KUNDNR
     C                   KFLD                    Y_VADRNR
     C     VAREL01K      KLIST
     C                   KFLD                    UFIRMA
     C                   KFLD                    Z_VARENR
     C     KODML01K      KLIST
     C                   KFLD                    UFIRMA
     C                   KFLD                    Y_LEVBET
     C     KODNL01K      KLIST
     C                   KFLD                    UFIRMA
     C                   KFLD                    Y_LEVMAT
     C     KOMML01K      KLIST
     C                   KFLD                    UFIRMA
     C                   KFLD                    Z_OTBKOD
     C                   KFLD                    Z_ORDAAR
     C                   KFLD                    Z_ORDNR
     C     KOMML01K2     KLIST
     C                   KFLD                    UFIRMA
     C                   KFLD                    Z_OTBKOD
     C                   KFLD                    Z_ORDAAR
     C                   KFLD                    Z_ORDNR
     C                   KFLD                    Z_LNRORD
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                    SRA /
      *////////////////////////////////////////////////////////////
     C     SRA           BEGSR
      *________________________
      * Hente partID og release
      *""""""""""""""""""""""""
     C     ECMKEY        CHAIN     ECMSG                              53
     C                   IF        *IN53 = *OFF
      *_______________________
      * Hente ordreinformasjon
      *"""""""""""""""""""""""
     C     DORKEY        SETLL     DORDL24
     C     DORKEY        READE     DORDL24                                53
     C                   IF        *IN53 = *OFF
      *________________________
      * Retreive message number
      *""""""""""""""""""""""""
     C     1             CHAIN     EDIC                               52
     C                   ADD       1             CMN
     C                   UPDATE    EDICR
     C                   Z-ADD     CMN           WMN
     C                   MOVE      '1'           *IN41
      *_________________
      * EDIFACT Detaljer
      *"""""""""""""""""
     C                   EXSR      SRB
     C                   ENDIF
     C                   ENDIF
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                    SRB /
      *////////////////////////////////////////////////////////////
     C     SRB           BEGSR
     C     HORKEY        CHAIN     HORDL11                            52
      *_____________
      * ADD SEGMENTS
      *"""""""""""""
     C                   EXSR      DTM00
     C                   EXSR      NAD02
     C                   EXSR      TOD05
     C                   EXSR      CPS10
     C                   EXSR      CNT00
      *_____________
      * WRITE TO UNH
      *"""""""""""""
     C                   EXSR      UNH00
      *______________
      * WRITE TO EDIM
      *""""""""""""""
     C                   EXSR      EDIM0
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Leftjustify num.fields                              SRNE /
      *////////////////////////////////////////////////////////////
     C     SRNE          BEGSR
      * Remove neg sign.
     C                   IF        ØN153 < 0
     C                   Z-SUB     ØN153         ØN153
     C                   ENDIF
      * Move correct value into alpha 12-field
     C                   MOVE      *ZEROS        ØA12
     C     ØA153D        IFEQ      '000'
     C                   MOVE      ØA153H        ØA12
     C                   ELSE
     C                   MOVEL     ØA153X        ØA12
     C                   MOVE      ',   '        ØA12
     C                   MOVE      ØA153D        ØA12
     C                   END
      * search from left and find first sign over 0
     C     1             DO        11            ØX
     C     Ø12(ØX)       IFNE      '0'
     C                   GOTO      SRNE1
     C                   END
     C                   END
     C     SRNE1         TAG
      * Leftjustify to a new field
     C     ØA153D        IFNE      '000'
     C     ØA153X        IFEQ      '00000000'
     C                   SUB       1             ØX
     C                   ENDIF
     C     Ø12(12)       IFEQ      '0'
     C                   MOVE      ' '           Ø12(12)
     C     Ø12(11)       IFEQ      '0'
     C                   MOVE      ' '           Ø12(11)
     C                   ENDIF
     C                   ENDIF
     C                   ENDIF
     C                   MOVE      *BLANKS       ØAN
     C                   MOVEA     Ø12(ØX)       ØAN

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  TOD05 /
      *////////////////////////////////////////////////////////////
     C     TOD05         BEGSR
      *____________________
      * Leveringsbetingelse
      *""""""""""""""""""""
     C                   Z-ADD     1             WREP0
     C                   CLEAR                   TODR
     C                   Z-ADD     WMN           TMN
     C                   Z-ADD     5             TGN
     C                   Z-ADD     WREP0         TREP0
     C     KODML01K      CHAIN     KODML01                            52
     C                   MOVEL     Y_LEVBET      T4053
     C                   MOVEL     M_KODTXT      T4052A
     C                   WRITE     TODR

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * UNH00                                               UNH00 /
      *////////////////////////////////////////////////////////////
     C     UNH00         BEGSR
     C                   CLEAR                   UNHR
     C                   Z-ADD     WMN           UMN
     C                   MOVEL     WMN           U0062A
     C                   MOVEL     EC0065        U0065
     C                   MOVEL     'D'           U0052
     C                   MOVEL     '01B'         U0054
     C                   MOVEL     'UN'          U0051
     C                   MOVEL     'EAN007'      U0057
     C                   MOVEL     '231'         U1001
     C                   Z-ADD     WAKKSN        ØN153
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           U1004
     C                   MOVEL     U0062A        U0062B
     C                   WRITE     UNHR

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  UNS00 /
      *////////////////////////////////////////////////////////////
     C     UNS00         BEGSR
     C                   Z-ADD     1             WREP0
     C                   CLEAR                   UNSR
     C                   Z-ADD     WMN           UMN
     C                   Z-ADD     50            UGN
     C                   Z-ADD     WREP0         UREP0
     C                   MOVEL     'S'           U0081
     C                   WRITE     UNSR
     C                   ENDSR
