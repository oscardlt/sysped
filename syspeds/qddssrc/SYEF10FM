     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPPNLGRP(HLPSCR SYEF10PNL)
     A                                      HLPTITLE('OPPDRAGSREGISTRERING FLYF-
     A                                      RAKT EKSPORT')
     A                                      HELP
     A                                      ALTHELP
     A          R BILDE01
     A                                      CA12(12 'Avslutt program')
     A                                      CF05(05 'Gå direkte til faktura')
     A                                      CF07(07 'SPØRRE OPPDRAG')
     A                                      CF09(09 'Bytt signatur')
     A          H                           HLPPNLGRP(HLPSCR SYEF10PNL)
     A                                      HLPARA(01 001 24 080)
     A                                  1  2'SYSPED/SYEF10'
     A                                      COLOR(BLU)
     A                                  1 20MSGCON(045 DIV0596 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 67DATE
     A                                      EDTCDE(Y)
     A                                  2 20MSGCON(045 DIV0609 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  2 67TIME
     A                                  9  2MSGCON(018 DIV0599 *LIBL/SYMSGF)
     A            WSSG           3A  B  9 23TEXT('Signatur')
     A                                      DSPATR(UL)
     A  99                                  DSPATR(PC)
     A  99                                  DSPATR(RI)
     A                                 11  2MSGCON(018 DIV0600 *LIBL/SYMSGF)
     A            WSAVD          4Y 0B 11 23TEXT('Avdeling')
     A                                      DSPATR(UL)
     A  98                                  DSPATR(PC)
     A  98                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                 13  2MSGCON(018 DIV0601 *LIBL/SYMSGF)
     A            WSOPD          7Y 0B 13 23TEXT('Oppdragsnummer')
     A                                      DSPATR(UL)
     A N96                                  DSPATR(PC)
     A  97                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                 13 39MSGCON(020 DIV0604 *LIBL/SYMSGF)
     A                                 15  2MSGCON(018 DIV0602 *LIBL/SYMSGF)
     A            HEAVD          4Y 0B 15 23TEXT('Avdeling')
     A                                      DSPATR(UL)
     A  96                                  DSPATR(PC)
     A  96                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A            HEOPD          7Y 0B 15 31TEXT('Oppdragsnummer')
     A                                      DSPATR(UL)
     A  96                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                 15 39MSGCON(020 DIV0036 *LIBL/SYMSGF)
     A                                 18  2MSGCON(018 DIV0603 *LIBL/SYMSGF)
     A            WSOPDG         7Y 0O 18 23COLOR(WHT)
     A                                      EDTCDE(4)
     A            WSMSG         62A  O 20  2COLOR(RED)
     A                                      DSPATR(BL)
     A                                 23  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(018 DIV0605 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 42MSGCON(018 DIV0606 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 62MSGCON(018 DIV0033 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                             24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A          R BILDE02
     A*%%TS  SD  20120305  090727  SVEIN       REL-V7R1M0  5770-WDS
     A N31                                  CF02(02 'Samlast')
     A N31                                  CF03(03 'Avslutt registrering av op-
     A                                      pdrag')
     A                                      CF04(04 'SØKING')
     A N31                                  CF05(05 'Gå til flyfraktbrev (ikke -
     A                                      oppdater gammel flyfr.b)')
     A N31                                  CF06(06 'Beregner kubikk')
     A                                      CF07(07 'SPØRRING PÅ OPPDRAG')
     A N31                                  CF09(09 'Tildeler AWBnr')
     A N31                                  CF10(10 'Fjerne fra tur')
     A                                      CF11(11 'DUP-OPPDRAG')
     A                                      CA12(12 'Avbryt registrering av opp-
     A                                      drag')
     A                                      CF14(14 'Vise opplysning')
     A                                      CF17(17 'FLY.RATE-BRUTTO')
     A                                      CF24(24 'FLERE TASTER')
     A                                      CF18(18 'Freetext')
     A                                      CF19(19 'FLY.RATE-NETTO')
     A                                      CF15(15 'FrI SØK')
     A                                      CF23(23 'Ruteforespørsel')
     A                                      CF22(22 'sende FACT booking')
     A  27                                  ALARM
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A          H                           HLPPNLGRP(SYEF10010 SYEF10PNL)
     A                                      HLPARA(02 001 02 080)
     A          H                           HLPPNLGRP(SYEF10010 SYEF10PNL)
     A                                      HLPARA(03 001 03 024)
     A          H                           HLPPNLGRP(SYEF10015 SYEF10PNL)
     A                                      HLPARA(03 025 03 080)
     A          H                           HLPPNLGRP(SYEF10020 SYEF10PNL)
     A                                      HLPARA(04 001 05 080)
     A          H                           HLPPNLGRP(SYEF10025 SYEF10PNL)
     A                                      HLPARA(06 001 06 080)
     A          H                           HLPPNLGRP(SYEF10027 SYEF10PNL)
     A                                      HLPARA(07 001 07 080)
     A          H                           HLPPNLGRP(SYEF10030 SYEF10PNL)
     A                                      HLPARA(08 001 08 017)
     A          H                           HLPPNLGRP(SYEF10035 SYEF10PNL)
     A                                      HLPARA(08 018 08 029)
     A          H                           HLPPNLGRP(SYEF10040 SYEF10PNL)
     A                                      HLPARA(08 030 08 080)
     A          H                           HLPPNLGRP(SYEF10045 SYEF10PNL)
     A                                      HLPARA(09 001 10 080)
     A          H                           HLPPNLGRP(SYEF10050 SYEF10PNL)
     A                                      HLPARA(12 001 14 080)
     A          H                           HLPPNLGRP(SYEF10055 SYEF10PNL)
     A                                      HLPARA(17 001 17 032)
     A          H                           HLPPNLGRP(SYEF10060 SYEF10PNL)
     A                                      HLPARA(17 033 17 080)
     A          H                           HLPPNLGRP(SYEF10065 SYEF10PNL)
     A                                      HLPARA(20 001 20 080)
     A          H                           HLPPNLGRP(SYEF10070 SYEF10PNL)
     A                                      HLPARA(21 001 21 080)
     A          H                           HLPPNLGRP(SYEF10075 SYEF10PNL)
     A                                      HLPARA(22 028 22 080)
     A          H                           HLPPNLGRP(SYEF10999 SYEF10PNL)
     A                                      HLPARA(23 001 24 080)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGLN1         7A  P
     A            MSGLN2         7A  P
     A            MSGLN3         7A  P
     A            MSGLN4         7A  P
     A            MSGNR          7A  P
     A                                  1  2'SYSPED/SYEF10'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(040 DIV0123 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 61DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(GRN)
     A                                  1 73TIME
     A                                      COLOR(GRN)
     A                                  2  2MSGCON(005 DIV0062 *LIBL/SYMSGF)
     A            WSDT           6Y 0B  2  8DSPATR(UL)
     A  96                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  2 16MSGCON(003 SHU1169 *LIBL/SYMSGF)
     A            WSKDPL         1A  B  2 20TEXT('Planleggingskode')
     A                                      DSPATR(UL)
     A  73                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                  2 23MSGCON(004 DIV0071 *LIBL/SYMSGF)
     A            WSAVD          4Y 0B  2 28TEXT('Avdeling')
     A N58                                  DSPATR(PR)
     A N58                                  COLOR(WHT)
     A  58                                  COLOR(YLW)
     A                                      EDTCDE(4)
     A  58                              2 33MSGCON(005 YBC0035 *LIBL/SYMSGF)
     A                                      DSPATR(RI)
     A                                      COLOR(YLW)
     A N58        WSOPD          7Y 0O  2 33TEXT('Oppdragsnummer')
     A N29                                  DSPATR(BL)
     A N29                                  COLOR(RED)
     A  29                                  COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  2 41MSGCON(003 DIV0072 *LIBL/SYMSGF)
     A            WSSG           3A  B  2 45TEXT('Signatur')
     A                                      COLOR(YLW)
     A                                  2 49MSGCON(004 DIV0073 *LIBL/SYMSGF)
     A            WSSTD          4Y 0B  2 54TEXT('Kostnadssted')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  2 61MSGCON(004 DIV0117 *LIBL/SYMSGF)
     A            WSPRO          8Y 0O  2 66TEXT('Prosjektnummer')
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  3  2MSGCON(004 DIV0079 *LIBL/SYMSGF)
     A            WSSDF          5A  B  3  8TEXT('Frakt fra')
     A                                      DSPATR(UL)
     A  95                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                  3 14MSGCON(004 DIV0080 *LIBL/SYMSGF)
     A            WSSDT          5A  B  3 19TEXT('Frakt til')
     A                                      DSPATR(UL)
     A  94                                  DSPATR(RI)
     A N52                                  COLOR(YLW)
     A  52                                  COLOR(RED)
     A  52                                  DSPATR(BL)
     A                                  3 25MSGCON(004 DIV0082 *LIBL/SYMSGF)
     A            WSLKA          2A  B  3 30TEXT('Landkode')
     A                                      DSPATR(UL)
     A  93                                  DSPATR(RI)
     A N51                                  COLOR(YLW)
     A  51                                  COLOR(RED)
     A  51                                  DSPATR(BL)
     A                                  3 33MSGCON(006 DIV0124 *LIBL/SYMSGF)
     A            WSSDL         20A  B  3 40TEXT('Lossested')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A                                  3 61MSGCON(004 DIV0078 *LIBL/SYMSGF)
     A            WSMAWB        11S 0O  3 66COLOR(WHT)
     A                                  4  2MSGCON(004 DIV0118 *LIBL/SYMSGF)
     A            WSKNF          3Y 0B  4  8DSPATR(UL)
     A  69                                  DSPATR(RI)
     A N50                                  COLOR(YLW)
     A  50                                  COLOR(RED)
     A  50                                  DSPATR(BL)
     A                                      EDTCDE(4)
     A            KFSNAV        30A  O  4 17COLOR(WHT)
     A                                  4 48MSGCON(006 YBC1822 *LIBL/SYMSGF)
     A            TRBCD          5   O  4 55COLOR(WHT)
     A                                  4 61MSGCON(004 DIV0081 *LIBL/SYMSGF)
     A            WSHAWB         9A  O  4 66TEXT('House Airway Billno')
     A                                      COLOR(WHT)
     A                                  5  2MSGCON(005 DIV0119 *LIBL/SYMSGF)
     A            WSKNA          8Y 0B  5  8TEXT('Kundenr Agent')
     A                                      DSPATR(UL)
     A  70                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A  56                                  DSPATR(BL)
     A  56                                  COLOR(RED)
     A N56                                  COLOR(YLW)
     A            WSNAA         30A  O  5 17TEXT('Navn Agent')
     A  56                                  DSPATR(BL)
     A  56                                  COLOR(RED)
     A N56                                  COLOR(WHT)
     A                                  5 61MSGCON(004 DIV0120 *LIBL/SYMSGF)
     A            WSSAML         6A  O  5 66TEXT('Referanse Agent')
     A                                      COLOR(WHT)
     A                                  6  2MSGCON(005 DIV3654 *LIBL/SYMSGF)
     A            WSAWBK         1A  B  6  8COLOR(YLW)
     A            WSAWBN        11A  B  6 10COLOR(YLW)
     A            WSAWSE         1A  B  6 24COLOR(YLW)
     A                                  6 26MSGCON(004 DIV0588 *LIBL/SYMSGF)
     A            WSRFA         15A  B  6 31COLOR(YLW)
     A                                  7  2MSGCON(005 SHU0636 *LIBL/SYMSGF)
     A            WSPROD         1A  B  7  8COLOR(YLW)
     A                                  7 12MSGCON(005 SHU0316 *LIBL/SYMSGF)
     A            WSKDAK         1A  B  7 18COLOR(YLW)
     A            WSKNO          8Y 0B  7 20TEXT('Kundenr OPD.GIV')
     A                                      DSPATR(UL)
     A  72                                  DSPATR(RI)
     A  53                                  DSPATR(BL)
     A  53                                  COLOR(RED)
     A N53                                  COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSNAO         30A  O  7 29TEXT('NAVN OPD.GIV')
     A  53                                  DSPATR(BL)
     A  53                                  COLOR(RED)
     A N53                                  COLOR(WHT)
     A                                  8  2MSGCON(004 DIV0090 *LIBL/SYMSGF)
     A            WSKNS          8Y 0B  8  8TEXT('Kundenr Selger')
     A                                      DSPATR(UL)
     A  67
     AO 68                                  DSPATR(RI)
     A  34                                  DSPATR(PR)
     A                                      EDTCDE(4)
     A  55                                  COLOR(RED)
     A N55                                  COLOR(YLW)
     A  55                                  DSPATR(BL)
     A                                  8 18MSGCON(003 DIV0091 *LIBL/SYMSGF)
     A            WSANS          1Y 0B  8 22TEXT('Avtalenr Selger')
     A                                      DSPATR(UL)
     A  34                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  8 24MSGCON(002 DIV0092 *LIBL/SYMSGF)
     A            WSKDFS         1A  B  8 27TEXT('Fakturakode Selger')
     A                                      DSPATR(UL)
     A  34                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'X' 'B' 'E')
     A                                  8 29MSGCON(003 DIV0093 *LIBL/SYMSGF)
     A            WSVALS         3A  B  8 33TEXT('Valutakode Selger')
     A                                      DSPATR(UL)
     A  66                                  DSPATR(RI)
     A  34                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                  8 37MSGCON(004 DIV0094 *LIBL/SYMSGF)
     A            WSKNSF         8Y 0B  8 42TEXT('Kundenr Selg.fakm')
     A                                      DSPATR(UL)
     A  65                                  DSPATR(RI)
     A  34                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSNASF        30A  O  8 51TEXT('Navn Selg.fakm')
     A                                      DSPATR(UL)
     A  99                                  DSPATR(BL)
     A  99                                  COLOR(RED)
     A N99                                  COLOR(WHT)
     A                                  9  2MSGCON(004 DIV0095 *LIBL/SYMSGF)
     A            WSNAS         30A  B  9  8TEXT('Navn Selger')
     A                                      DSPATR(UL)
     A  67                                  DSPATR(RI)
     A  34                                  DSPATR(PR)
     A  55                                  DSPATR(BL)
     A  55                                  COLOR(RED)
     A N55                                  COLOR(YLW)
     A            WSADS1        30A  B  9 42TEXT('Adresse-1 Selger')
     A                                      DSPATR(UL)
     A  34                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A            WSADS2        30A  B 10  8TEXT('Adresse-3 Selger')
     A                                      DSPATR(UL)
     A  34                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A            WSADS3        30A  B 10 42TEXT('Adresse-2 Selger')
     A                                      DSPATR(UL)
     A  34                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A            WSLKS          2A  B 10 74COLOR(YLW)
     A  83                                  DSPATR(RI)
     A            WSEDIINF      15A  O 11  2COLOR(RED)
     A                                 12  2MSGCON(004 DIV0096 *LIBL/SYMSGF)
     A            WSKNK          8Y 0B 12  8TEXT('Kundenr Kjøper')
     A                                      DSPATR(UL)
     A  62
     AO 61                                  DSPATR(RI)
     A  32                                  DSPATR(PR)
     A                                      EDTCDE(4)
     A  54                                  DSPATR(BL)
     A  54                                  COLOR(RED)
     A N54                                  COLOR(YLW)
     A                                 12 18MSGCON(003 DIV0091 *LIBL/SYMSGF)
     A            WSANK          1Y 0B 12 22TEXT('Avtalenr Kjøper')
     A                                      DSPATR(UL)
     A  32                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 12 24MSGCON(002 DIV0092 *LIBL/SYMSGF)
     A            WSKDFK         1A  B 12 27TEXT('Fakturakode Kjøper')
     A                                      DSPATR(UL)
     A  32                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'X' 'B' 'E')
     A                                 12 29MSGCON(003 DIV0093 *LIBL/SYMSGF)
     A            WSVALK         3A  B 12 33TEXT('Valutakode Kjøper')
     A                                      DSPATR(UL)
     A  60                                  DSPATR(RI)
     A  32                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                 12 37MSGCON(004 DIV0094 *LIBL/SYMSGF)
     A            WSKNKF         8Y 0B 12 42TEXT('Kundenr Kjøp.fakm')
     A                                      DSPATR(UL)
     A  59                                  DSPATR(RI)
     A  32                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSNAKF        30A  O 12 51TEXT('Navn Kjøp.fakm')
     A                                      DSPATR(UL)
     A  98                                  DSPATR(BL)
     A  98                                  COLOR(RED)
     A N98                                  COLOR(WHT)
     A                                 13  2MSGCON(004 DIV0097 *LIBL/SYMSGF)
     A            WSNAK         30A  B 13  8TEXT('Navn Kjøper')
     A                                      DSPATR(UL)
     A  62                                  DSPATR(RI)
     A  32                                  DSPATR(PR)
     A  54                                  DSPATR(BL)
     A  54                                  COLOR(RED)
     A N54                                  COLOR(YLW)
     A            WSADK1        30A  B 13 42TEXT('Adresse-1 Kjøper')
     A                                      DSPATR(UL)
     A  32                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A            WSADK2        30A  B 14  8TEXT('Adresse-2 Kjøper')
     A                                      DSPATR(UL)
     A  32                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A            WSADK3        30A  B 14 42TEXT('Adresse-3 Kjøper')
     A                                      DSPATR(UL)
     A  32                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A            WSLKK          2A  B 14 74COLOR(YLW)
     A  84                                  DSPATR(RI)
     A                                 16  2MSGCON(075 DIV0125 *LIBL/SYMSGF)
     A            WSOT           2A  B 17  7TEXT('Oppdragstype')
     A                                      DSPATR(UL)
     A  89                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSFR           3A  B 17 18TEXT('Frankatur')
     A                                      DSPATR(UL)
     A  88                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSNT           7Y 0B 17 25TEXT('Antall')
     A                                      DSPATR(UL)
     A  87                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSVS1         25A  B 17 33TEXT('Vareslag-1')
     A                                      DSPATR(UL)
     A  86                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSVKT          9Y 0B 17 59TEXT('Vekt')
     A                                      DSPATR(UL)
     A  85                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSM3           7Y 3B 17 69TEXT('Kubikk')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSVS2         25A  B 18 33TEXT('Vareslag-2')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 19  2MSGCON(010 SHU0514 *LIBL/SYMSGF)
     A            WSGM1         15A  B 19 13COLOR(YLW)
     A            WSGM2         15A  B 19 30COLOR(YLW)
     A            WSAVD0         4Y 0O 19 62TEXT('Avdeling')
     A N58                                  COLOR(WHT)
     A  58                                  COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 19 67'/'
     A N58                                  COLOR(WHT)
     A  58                                  COLOR(YLW)
     A            WSOPD0         7Y 0O 19 69TEXT('OPPDRAGSNR')
     A N58                                  COLOR(WHT)
     A  58                                  COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 20  2MSGCON(018 DIV0122 *LIBL/SYMSGF)
     A            WSTSS          1A  B 20 36DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      VALUES('J' 'N')
     A                                 20 54MSGCON(005 DIV0100 *LIBL/SYMSGF)
     A            WSKDL         15A  B 20 60TEXT('Lagerkode')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSPK3          1A  B 21  2TEXT('Printerkode-3')
     A                                      DSPATR(UL)
     A  82                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'J' 'N' 'P')
     A            WSPK6          1A  B 21  6TEXT('Printerkode-4')
     A                                      DSPATR(UL)
     A  81                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'J' 'N' 'P')
     A            WSPK8          1A  B 21 10TEXT('Printerkode-5')
     A                                      DSPATR(UL)
     A  80                                  DSPATR(RI)
     A                                      VALUES(' ' 'J' 'N' 'P')
     A                                      COLOR(YLW)
     A            WSPK1          1A  B 21 14DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSPK7          1A  B 21 17DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSMSG         62A  O 21 19COLOR(RED)
     A                                      DSPATR(BL)
     A                                 22  2MSGCON(009 DIV0607 *LIBL/SYMSGF)
     A            WSOPDG         7Y 0O 22 12DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 22 28MSGCON(009 DIV0610 *LIBL/SYMSGF)
     A            KARTXT        31A  O 22 38TEXT('FRITEKST')
     A                                      COLOR(WHT)
     A            MSGL1         18A  O 23  2MSGID(&MSGLN1 SYMSGF)
     A                                      COLOR(BLU)
     A            MSGL2         18A  O 23 22MSGID(&MSGLN2 SYMSGF)
     A                                      COLOR(BLU)
     A            MSGL3         18A  O 23 42MSGID(&MSGLN3 SYMSGF)
     A                                      COLOR(BLU)
     A            MSGL4         18A  O 23 62MSGID(&MSGLN4 SYMSGF)
     A                                      COLOR(BLU)
     A            MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A                                  6 22'T'
     A                                 20 22MSGCON(013 SHU1300 *LIBL/SYMSGF)
     A                                 19 54MSGCON(007 SHU1301 *LIBL/SYMSGF)
