      *********************************************************************
      *  SPØRRING PÅ LAGERPOSTER - LOGISTIKK-SYSTEM
      * PROGRAM FOR Å ANGI SØKEUTVALG/FILTER
      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/SLAGSC0 MODULE(*LIBL/SLAGSC0
CRTPGM*        *LIBL/CHECKUSR *LIBL/INCGI) ACTGRP(CGI) AUT(*USE)
      *
      *********************************************************************
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 JOV PTF10 avvikende kundenr dersom User er intern
********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
      *--------------------------------------------------------------------
     FFIRM      if   e           k disk    usropn
     FCUNDC01   if   e           k disk    usropn
     FBRIDF     UF   E           K DISK    USROPN
     FKUFAST    if   e           k disk    usropn
     FLLGrpK05  if   e           k disk    USROPN
     FBRPARF    if   e           k disk    usropn
      *--------------------------------------------------------------------
      * Prototype definitions and standard system API error structure
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      *--------------------------------------------------------------------
      * Variables common to CGI programs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
      * Variables received from the input string
      *--------------------------------------------------------------------
     D lng             s              2
     D UsrSpcName      s             10
     D USER            s             10
     D firma           s              2
      * Variables received from the input string
      * and converted to numeric
     D zeros           s             15a   inz('000000000000000')
     D l1              s              3s 0
     D l2              s              3s 0
     D PGM             C                   CONST('SYSPED/CHECKUSR')
      *--------------------------------------------------------------------
      * Other variables
      *--------------------------------------------------------------------
     D Lib             s             10
     D Fn              s             10
     D Mbr             s             10
     D VG              S              5    DIM(10)
     D VE              S              5    DIM(10)
     D                 DS
     D  PAVRDA                 1     50
     D  VV                     1     50    DIM(10)
      *--------------------------------------------------------------------
      * Prolog common to CGI programs
      * -Receive input from the remote browser
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prolog3
      *=====================================================================******
      * MAIN PROCESS LINE
      *=====================================================================******
      * Get program start time for calculating execution time
     C                   time                    timedata1
      * Parse input string
     C                   exsr      Parse
      *
     C                   EXSR      INIT
     C                   CALLB     PGM
     C                   PARM                    BIBRID
     C                   PARM                    UsrSpcName
     C                   PARM                    UINN              1
     C                   IF        UINN = 'N'
     C                   GOTO      SLUTT
     C                   END
     C     USER          CHAIN     BRIDF                              50
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVE      WDT           BIDTF
     C                   MOVE      WTM           BITIDF
     C                   UPDATE    BRIDFR
      * Convert numeric input
     C                   exsr      CvtNumInp
      * Set output skeleton html member name
     C                   exsr      SetSkl
      * Read skeleton output html, etc.
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
      * Retrieve environment variables
     C                   exsr      RtvEnvVar
      *------------------
      * Write sections of HTML.
      *
      * 1-Section /$top
     C                   exsr      SetTop
     C                   callp     wrtsection('top')
N01  C                   IF        (XINTERN = 'J')
N01  C                   callp     wrtsection('AvvKnr')
N01  C                   END
N01  C                   callp     wrtsection('Del2')
      * 2-Contact persons
     C                   exsr      DspCper
      * 3-End html
     C                   exsr      SetEnd
     C     SLUTT         TAG
     C                   callp     wrtsection('endhtml')
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      *
     C                   close     FIRM
     C                   close     CUNDC01
     C                   close     KUFAST
     C                   close     LLGrpK05
     C                   close     BRPARF
     C                   eval      *inlr = *on
      *
     C                   return
      *=====================================================================******
      * Parse input
      *=====================================================================******
     C     Parse         begsr
     C                   eval      lng    = zhbgetvar('lng')
     C                   eval      USER   = zhbgetvar('USER')
     C                   EVAL      UsrSpcName  = zhbgetvar('UsrSpcName')
      * Convert to uppercase
     C                   eval      lng = uppify(lng)
      *
     C                   endsr
      *=====================================================================******
      * Convert numeric inputs
      *=====================================================================******
     C     CvtNumInp     begsr
      *
     C                   endsr
      *=====================================================================******
      *  Display contact persons to select from
      *=====================================================================******
     C     DspCper       begsr
     C                   move      FIFIRM        CFIRMA
     C                   move      BIKUNR        CCOMPN
      *
     C                   MOVE      *BLANKS       VG
     C                   MOVE      ' '           XVGNIVÅ           1
      * VAREGRUPPER (BRUKER-NIVÅ)
     C                   EVAL      PABRID = USER
     C                   EVAL      PAKUNR = 0
     C                   EVAL      PAID   = 'LLVG'
     C     BRPARFK       CHAIN     BRPARF                             40
     C                   IF        *IN40 = *OFF
     C                   MOVEA     VV            VG
     C                   MOVE      'B'           XVGNIVÅ           1
     C                   ELSE
      * VAREGRUPPER (KUNDE-NIVÅ)
     C                   EVAL      PABRID = '*KUNDFT*'
     C                   MOVE      BIFIRM        PABRID
     C                   EVAL      PAKUNR = BIKUNR
     C     BRPARFK       CHAIN     BRPARF                             40
     C                   IF        *IN40 = *OFF
     C                   MOVEA     VV            VG
     C                   MOVE      'K'           XVGNIVÅ           1
     C                   ELSE
      * VAREGRUPPER (FIRMA-NIVÅ)
     C                   EVAL      PAKUNR = 0
     C     BRPARFK       CHAIN     BRPARF                             40
     C                   IF        *IN40 = *OFF
     C                   MOVEA     VV            VG
     C                   MOVE      'F'           XVGNIVÅ           1
     C                   END
     C                   END
     C                   END
      *
      * VISE VAREGRUPPER
     C                   EVAL      KFTYP = 'VG'
      *
     C                   IF        VV(1) = *BLANKS
      * VIS ALLE GRUPPER TIL KUNDER
     C                   MOVE      BIKUNR        KFTYP
     C     KFTYP         SETLL     KUFAST
     C     KFTYP         READE     KUFAST                                 33
     C  N33              IF        KFKOD = ' DEFN'
     C     KFTYP         READE     KUFAST                                 33
     C                   END
     C                   DOW       *IN33 = *OFF
     C                   callp     updhtmlvar('VGKOD':KfKod)
     C                   callp     updhtmlvar('VGTXT':KfTXT)
     C                   callp     wrtsection('typsltrowVg')
     C     KFTYP         READE     KUFAST                                 33
     C                   ENDDO
     C                   ELSE
      *
     C                   SELECT
     C                   WHEN      ((XVGNIVÅ = 'B') OR (XVGNIVÅ = 'K'))
     C                   MOVE      BIKUNR        KFTYP
     C                   WHEN      XVGNIVÅ = 'F'
     C                   CAT       '*':0         KFTYP
     C                   CAT       BIFIRM:0      KFTYP
     C                   CAT       '*':0         KFTYP
     C                   ENDSL
      * HENT VAREGRUPPETEKST
     C     1             DO        10            XN                3 0
     C                   IF        VG(XN) <> *BLANKS
     C                   MOVE      VG(XN)        KFKOD
     C     KUFASTK       CHAIN     KUFAST                             33
     C   33              EVAL      KFTXT = 'ER *UKJENT'
     C                   callp     updhtmlvar('VGKOD':KfKod)
     C                   callp     updhtmlvar('VGTXT':KfTXT)
     C                   callp     wrtsection('typsltrowVg')
     C                   END
     C                   enddo
      *
     C                   END
      *
     C                   callp     wrtsection('typsltendVg')
      *
     C     CundcKey      setll     CUNDCR
     C     CundcKey      reade     CUNDCR                                 40
     C     *in40         doweq     '0'
     C                   exsr      SetSltRow
     C                   callp     wrtsection('typsltrow')
     C     CundcKey      reade     CUNDCR                                 40
     C                   enddo
      *
     C                   callp     wrtsection('typsltend')
      *
     C     BIEIER        IFEQ      *BLANKS
     C                   callp     updhtmlvar('EiKOD':' ')
     C                   callp     updhtmlvar('EiTXT':'ALL')
     C                   callp     wrtsection('typsltrowEi')
     C                   MOVE      'J'           XVAEIEROK
     C                   ELSE
     C                   MOVE      'N'           XVAEIEROK         1
     C                   END
      *
     C                   MOVE      *BLANKS       VE
     C                   MOVE      ' '           XVENIVÅ           1
      * VAREEIER (BRUKER-NIVÅ)
     C                   EVAL      PABRID = USER
     C                   EVAL      PAKUNR = 0
     C                   EVAL      PAID   = 'LLVE'
     C     BRPARFK       CHAIN     BRPARF                             40
     C                   IF        *IN40 = *OFF
     C                   MOVEA     VV            VE
     C                   MOVE      'B'           XVENIVÅ           1
     C                   ELSE
      * VAREEIER (KUNDE-NIVÅ)
     C                   EVAL      PABRID = '*KUNDFT*'
     C                   MOVE      BIFIRM        PABRID
     C                   EVAL      PAKUNR = BIKUNR
     C     BRPARFK       CHAIN     BRPARF                             40
     C                   IF        *IN40 = *OFF
     C                   MOVEA     VV            VE
     C                   MOVE      'K'           XVENIVÅ           1
     C                   ELSE
      * VAREEIER (FIRMA-NIVÅ)
     C                   EVAL      PAKUNR = 0
     C     BRPARFK       CHAIN     BRPARF                             40
     C                   IF        *IN40 = *OFF
     C                   MOVEA     VV            VE
     C                   MOVE      'F'           XVENIVÅ           1
     C                   END
     C                   END
     C                   END
      *
      * VISE VAREEIER
     C                   EVAL      KFTYP = 'VE'
      *
     C                   IF        VV(1) = *BLANKS
      * VIS ALLE EIER TIL KUNDER
     C                   MOVE      BIKUNR        KFTYP
     C     KFTYP         SETLL     KUFAST
     C     KFTYP         READE     KUFAST                                 33
     C  N33              IF        KFKOD = ' DEFN'
     C     KFTYP         READE     KUFAST                                 33
     C                   END
     C                   DOW       *IN33 = *OFF
     C                   callp     updhtmlvar('EIKOD':KfKod)
     C                   callp     updhtmlvar('EITXT':KfTXT)
     C                   callp     wrtsection('typsltrowEI')
     C     KFTYP         READE     KUFAST                                 33
     C                   ENDDO
     C                   ELSE
      *
     C                   SELECT
     C                   WHEN      ((XVENIVÅ = 'B') OR (XVENIVÅ = 'K'))
     C                   MOVE      BIKUNR        KFTYP
     C                   WHEN      XVENIVÅ = 'F'
     C                   CAT       '*':0         KFTYP
     C                   CAT       BIFIRM:0      KFTYP
     C                   CAT       '*':0         KFTYP
     C                   ENDSL
      * HENT VAREEIERTEKST
     C     1             DO        10            XN                3 0
     C                   IF        VE(XN) <> *BLANKS
     C                   MOVE      VE(XN)        KFKOD
     C     KUFASTK       CHAIN     KUFAST                             33
     C   33              EVAL      KFTXT = 'ER *UKJENT'
     C                   callp     updhtmlvar('EIKOD':KfKod)
     C                   callp     updhtmlvar('EITXT':KfTXT)
     C                   callp     wrtsection('typsltrowEI')
     C                   END
     C                   ENDDO
      *
     C                   END
      *
     C     XVAEIEROK     IFEQ      'N'
     C                   callp     updhtmlvar('EiKOD':BIEIER)
     C                   callp     updhtmlvar('EiTXT':'*UKJENT')
     C                   callp     wrtsection('typsltrowEi')
     C                   END
      *
     C                   callp     wrtsection('typsltendEi')
      *
     C                   endsr
      *=====================================================================******
      *  Set variable data for section /$top
      *=====================================================================******
     C     SetTop        begsr
      *------------------
      * Repeat for the next invocation
      * 1)national language
     C                   callp     updhtmlvar('LNG':lng)
      * 2)firma/kunde
     C                   callp     updhtmlvar('FIRMA':fifirm)
BODY C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updhtmlvar('USER':USER)
     C                   callp     updHtmlVar('UsrSpcName':UsrSpcName)
     C                   MOVE      BIKUNR        XXKUNR            8
     C                   callp     updhtmlvar('KUNR':XXKUNR)
     C     BIKUNR        chain     LLGrpK05                           33
     C                   callp     updhtmlvar('WSGRP':
     C                             %trim(%editc(GKGRP:'Z')))
     C                   callp     updhtmlvar('WSPNR':
     C                             %trim(%editc(GKPNR:'Z')))
      *------------------
      * <BODY> variables
      * Texts color
     C                   callp     updhtmlvar('COLOR':'black')
     C                   callp     updhtmlvar('TXTCOLOR':'black')
     C                   callp     updhtmlvar('BOLD':' ')
     C                   callp     updhtmlvar('BGCOLOR':BIFARG)
     C                   callp     updhtmlvar('LINK':'0000FF')
     C                   callp     updhtmlvar('VLINK':'FF0000')
     C                   callp     updhtmlvar('ALINK':'00FF00')
N01  C                   callp     updHtmlVar('AVVKNR':' ')
     C                   endsr
      *=====================================================================******
      *  Set variable data for section /$typsltrow
      *=====================================================================******
     C     SetSltRow     begsr
     C                   callp     updhtmlvar('BTTYPE1':CConta)
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$endhtml
      *=====================================================================******
     C     SetEnd        begsr
      * Server protocol
     C                   callp     updhtmlvar('PROTOCOL':S_Protocol)
      * Compute run time
     C                   time                    timedata2
     C     timedata2     subdur    timedata1     ms:*ms
     C                   eval      sec = ms / 1000000
     C                   callp     updhtmlvar('runtime':
     C                             %trim(%editw(sec:'     0 .   ')))
      *
     C                   endsr
      *=====================================================================******
      * Set html output skeleton member before its read into memory
      *=====================================================================******
     C     SetSkl        begsr
      *------------------
      * Set html output skeleton member
     C                   eval      lng = uppify(lng)
      * "HTMLSRC  " er dft = NORSK, ved å trykke på FLAGGET i inng.
      * kan dette endre stil "HTMLSRCXX" avh. av støttede språk
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC' + lng
     C                   eval      Mbr = 'SLAGSC0'
     C                   CAT       XSPRÅK:0      MBR
      *------------------
     C                   endsr
      *=====================================================================******
      *  Retrieve environment variables
      *=====================================================================******
     C     RtvEnvVar     begsr
      * Use getenvp to get this server's protocol and name
     C                   eval      S_Protocol =getenv('SERVER_PROTOCOL':
     C                             qusec)
      *
     C                   endsr
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
     C                   OPEN      BRIDF
     C     USER          CHAIN(N)  BRIDF                              50
     C     BILIBL        ifeq      *blanks
     C                   CALLB     'INCGI'
     C                   PARM                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
N01   * hent Parametre som går igjen i mange prog:
N01   *      Odd/Even/Rowhead-farger,Logobilde,Språk,Intern/Ekstern bruker,  mm
N01  c                   call      'ESGETPAR'
N01  c                   parm                    BIBRID
N01  c                   parm                    BIFIRM
N01  c                   parm                    BIKUNR
N01  c                   parm                    BIKNG
N01  c                   parm                    RowHead          10
N01  c                   parm                    Odd              10
N01  c                   parm                    Even             10
N01  c                   parm                    LogoImg          50
N01  c                   parm                    XSPRÅK            2
N01  c                   parm                    XINTERN           1
N01  c                   parm                    ParmDS1        1024
N01  c                   parm                    ParmDS2        1024
N01  c                   parm                    ParmDS3        1024
     C                   open      FIRM                                 50
     C                   open      CUNDC01                              50
     C                   open      KUFAST
     C                   open      LLGrpK05                             50
     C                   open      BRPARF                               50
      *
     c     CundcKey      klist
     c                   kfld                    CFIRMA
     c                   kfld                    CCOMPN
     c     BRPARFK       klist
     c                   kfld                    PABRID
     c                   kfld                    PAKUNR
     c                   kfld                    PAID
     C     KUFASTK       klist
     C                   kfld                    KFTYP
     C                   kfld                    KFKOD
      *
     C                   MOVE      BIBRID        PABRID
     C                   MOVE      *ZEROS        PAKUNR
     C                   MOVEL     'SPRÅK'       PAID
     C     BRPARFK       chain     BRPARF                             33
     C                   IF        (*IN33 OR (PAVRDA <> 'EN'))
     C                   MOVE      *BLANKS       XSPRÅK            2
     C                   ELSE
     C                   MOVE      'EN'          XSPRÅK            2
     C                   END
      *
     C     *loval        setll     FIRM
     C                   read      FIRM
      *
     C                   endsr
