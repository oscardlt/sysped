     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HELP
     A                                      ALTHELP(CA01)
      * "NOERASE" MÅ HA FOR Å IKKE TØMME BILDE SOM MAN KOMME FRA
     A          R NOERASE
     A                                      ASSUME
     A                                  1  2' '
      * MAN KUN DEFINNERE VINDU PÅ REKORDNIVÅ
     A          R WINDOWSZ
     A                                      WINDOW(*DFT 18 65 *NOMSGLIN)
     A                                      OVERLAY
      *
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  5  2VALUES(' ' '1' '2' '4' '9')
     A  27                                  DSPATR(PR)
     A            TVLI2          3  0H
     A            NVEH           2A  O  5  5
     A            NVNT           5  0O  5  8EDTCDE(4)
     A            NVNTEH         5  0O  5 14EDTCDE(4)
     A            NVMN          42A  O  5 20
     A            NVMNSK         2A  O  5 63
     A            TVEH           2A  O  6  5
     A            TVNT           5  0O  6  8EDTCDE(4)
     A            TVNTEH         5  0O  6 14EDTCDE(4)
     A            TVMN          42A  O  6 20
     A            TVMNSK         2A  O  6 63
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0006)
     A                                      SFLPAG(0005)
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A                                      CA03(03 'Forrige')
     A N27                                  CF04(04 'Søking')
     A                                      CA05(05 'Frisk')
     A                                      CA12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            ZRECNO         4S 0O  2 41SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SVX075R'
     A                                      COLOR(BLU)
     A                                  1 10'Underhåll kolli '
     A                                      COLOR(WHT)
     A N27                              2  2'1=Ny  2=Ændra 4=Radera 9=Ångra'
     A                                      COLOR(BLU)
     A                                  3  2'Op Enhet/kolli    Mærke & nr.     -
     A                                                                 SK'
     A                                      COLOR(WHT)
     A            W2NR           1A  I  4  2VALUES(' ' '1')
     A  27                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A            W2EH           2A  B  4  5DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  90                                  DSPATR(RI)
     A  90                                  DSPATR(PC)
     A            W2NT           5Y 0B  4  8DSPATR(UL)
     A  27                                  DSPATR(PR)
     A                                      EDTCDE(4)
     A            W2NTEH         5Y 0B  4 14DSPATR(UL)
     A  27                                  DSPATR(PR)
     A                                      EDTCDE(4)
     A            W2MN          35A  B  4 20DSPATR(UL)
     A  27                                  DSPATR(PR)
     A            W2MNSK         2A  B  4 63DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  89                                  DSPATR(RI)
     A  89                                  DSPATR(PC)
      *
     A          R BUNN
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A N27                             17  2'F3/12=Avbryt F4=Søk F5=Uppdatera'
     A                                      COLOR(BLU)
     A  27                             17  2'F3/12=Avbryt F5=Uppdatera'
     A                                      COLOR(BLU)
     A  30        MSGTXT        30A  O 18  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R VARELI
     A                                      WINDOW(WINDOWSZ)
     A                                      CF04(04 'Søk')
     A                                      CA12(12 'Forrige')
     A                                      CA15(15 'Flere')
     A                                      SLNO(15)
     A                                      CLRL(04)
     A                                      OVERLAY
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'   Enhet/kolli    Mærke & nr.     -
     A                                                                 SK'
     A                                      COLOR(WHT)
     A            WSEH           2A  B  2  5DSPATR(UL)
     A  90                                  DSPATR(RI)
     A  90
     AON30                                  DSPATR(PC)
     A            WSNT           5Y 0B  2  8DSPATR(UL)
     A                                      EDTCDE(4)
     A            WSNTEH         5Y 0B  2 14DSPATR(UL)
     A                                      EDTCDE(4)
     A            WSMN          35A  B  2 20DSPATR(UL)
     A            WSMNSK         2A  B  2 63DSPATR(UL)
     A  89                                  DSPATR(RI)
     A  89                                  DSPATR(PC)
     A            TVEH           2A  O  3  5
     A            TVNT           5Y 0O  3  8EDTCDE(4)
     A            TVNTEH         5Y 0O  3 14EDTCDE(4)
     A            TVMN          42A  O  3 20
     A            TVMNSK         2A  O  3 63
     A N30                              4  2'F4=Søk   F12=Avbryt   F15=Tillægg'
     A                                      COLOR(BLU)
     A  30        MSGTXT        30A  O  4  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R NYWRK
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A                                      CA03(03 'Forrige')
     A N27                                  CF04(04 'Søking')
     A                                      CA05(05 'Frisk')
     A                                      CA12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SVX075R'
     A                                      COLOR(BLU)
     A                                  1 10'Underhåll kolli '
     A                                      COLOR(WHT)
     A N27                              2  2'1=Ny '
     A                                      COLOR(BLU)
     A                                  3  2'Op Enhet/kolli    Mærke & nr.     -
     A                                                                 SK'
     A                                      COLOR(WHT)
     A            W2NR           1A  I  4  2VALUES(' ' '1')
     A                                      DSPATR(UL)
     A  27                                  DSPATR(PR)
     A            W2EH           2A  B  4  5DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  90                                  DSPATR(RI)
     A  90                                  DSPATR(PC)
     A            W2NT           5Y 0B  4  8DSPATR(UL)
     A  27                                  DSPATR(PR)
     A                                      EDTCDE(4)
     A            W2NTEH         5Y 0B  4 14DSPATR(UL)
     A  27                                  DSPATR(PR)
     A                                      EDTCDE(4)
     A            W2MN          35A  B  4 20DSPATR(UL)
     A  27                                  DSPATR(PR)
     A            W2MNSK         2A  B  4 63DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  89                                  DSPATR(RI)
     A  89                                  DSPATR(PC)
     A N30N27                          18  2'F3/12=Avbryt F4=Søk F5=Uppdatera'
     A                                      COLOR(BLU)
     A N30 27                          18  2'F3/12=Avbryt F5=Uppdatera'
     A                                      COLOR(BLU)
