      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/TJSYFRA1 MODULE(*LIBL/TJSYFRA1 *LIBL/INCGI)
CRTPGM*        ACTGRP(*NEW) AUT(*USE)
      *
      *********************************************************************
      /copy SYSPEDS/qrpgsrcpy,hspecs
      /copy SYSPEDS/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FSTED2D    IF   E           K DISK    USROPN
     FUSRDFT    IF   E           K DISK    USROPN
     FNOPNR     IF   E           K DISK    USROPN
     FFIRINL    IF   E           K DISK    USROPN
     FCUTRAN    IF   E           K DISK    USROPN
     FSIDUPA    UF A E           K DISK    USROPN
     FSIDUPB    UF A E           K DISK    USROPN
     FSIDUPC    UF A E           K DISK    USROPN
     FKODTV     IF   E           K DISK    USROPN
     FKODTA     IF   E           K DISK    USROPN
     FKODTGE    IF   E           K DISK    USROPN
     FKODTG2    IF   E           K DISK    USROPN
     FKODSAM    IF   E           K DISK    USROPN
     FBRPARF    IF   E           K DISK    USROPN
     FCUNDL01   IF   E           K DISK    USROPN
     FVALUL01   IF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy SYSPEDS/qrpgsrcpy,prototypeb
      /copy SYSPEDS/qrpgsrcpy,usec
      /copy SYSPEDS/qrpgsrcpy,variables3
      * Sammenslåing av F-linjer baserer seg på SAMME varekode/tekst ELLER sammensl.kode/tekst
     D FLI             S             85    DIM(100)
      * Client input variables
     D OddEven         S             10
     D WBOPD           S              7  0
     D WSOT            S              2
     D WSTOTN          S             13  2
     D FRA             S              5
     D TIL             S              5
     D ANT             S              5
     D VKT             S              5
     D M3              S              5
     D LM              S              5
     D NumBig          S             19  9
     D VARME           S              1
     D FARLIG          S              1
     D LENGDE          S              1
     D PROD            S              1
     D AVDG            S             40
     D FRDG            S             20
     D TILTX           S             35
     D user            S             10
     D AVVKNR          S              8
     D AVVKNN          S              8  0
     D AVVKNTXT        S            200
     D TJEN            S             50
     D OVERSKR         S            150
     D MinFr           S             50
     D OljValTxt       S             50
     D OljValBel       S              9  2
     D SeIntChecked    S             10
     D SeIntern        S              1
     D SortSam         S              1
     D MKOD            S              1
     D VTXT            S             45
     D XN01            S              3  0
     D MaxXN           S              3  0
     D StrXN           S              3  0
     D Mbr             S             10
     D FRABOOKING      S              1
     D JSONstring      S          32000
     D Error           S            150
     D AntLest         S              9  0

     D Spaces          S             12a   inz('&nbsp;&nbsp;')
     D                 DS
     D  ZSDS                   1    429
     D  ZJOB                 244    253
     D  ZUSER                254    263

     D                 DS
     D  ST2UNI                 1      9
     D  AVGDG                  1      7
     D  AV                     1      7
     D                                     DIM(7)
     D  FRADG                  8      9
     D                 DS
     D  DGNAVN                 1     14    inz('MaTiOnToFrLøSø')
     D  DGN                    1     14
     D                                     DIM(7)

     D                 DS
     D  FAVT                   1     20
     D    FAVT1                1      1
     D    FAVT2_20             2     20
     D                 DS
     D  GMDS                   1     85
     D    GM_SORT              1      3
     D    GM_SAMM              5      5
     D    GM_VT                7     47
     D    GM_MK               49     49
     D      GMDS_A             1     49
     D    GM_VAL              51     53
     D    GM_BELV             55     67  2
     D    GM_BELN             69     81  2
     D    GM_VK               83     85
     D                 DS
     D  NYDS                   1     85
     D    NY_SORT              1      3
     D    NY_SAMM              5      5
     D    NY_VT                7     47
     D    FAKDM               49     49
     D      NYDS_A             1     49
     D    FAVAL               51     53
     D    FABELV              55     67  2
     D    FABELN              69     81  2
     D    FAVK                83     85
     D                 DS
     D  HXHEAD                 1    256
     D  XXUR                   1      1
     D  XXFR                   2      4
     D  XXKDPL                 5      5
     D  XXLKA                  6      7
     D  XXSDF                  8     12
     D  XXTRI                 13     14
     D  XXSDT                 15     19
     D  XXLKS                 20     21
     D  XXSDFF                22     26
     D  XXLKK                 27     28
     D  XXSDVT                29     33
     D  XXVKT                 34     42  0
     D  XXFBV                 43     51  0
     D  XXM3                  52     58  3
     D  XXLM                  59     62  2
     D  XXNT                  63     69  0
     D  XXVERV                70     72
     D  XXVERB                73     75  2

     D                 DS
     D  HXHEADRet              1    256
     D  XXFBVRet              43     51  0

     D                 DS
     D  WSAVD                  1      7
     D  WSMOD                  1      1
     D  WSMOD2                 2      2
     D  WSSK                   3      3
     D  WBAVD                  4      7  0

      *get input-string
      /copy syspeds/qrpgsrcpy,prolog3

     C                   EXSR      Parse

     C                   EXSR      INIT
     C  N50              EXSR      SetAll

     C                   CALLP     gethtmlifs(
     C                             '/syspeddev/html/JSON.html':
     C                             '/CGITAG/')
     C                   CALLP     wrtsection('top')

      /free
        JSONstring='{'
        +'¬user¬ : ¬'+%trim(USER)+'¬, '
        +'¬xkunfrakt¬ : ¬'+%trim(XKUNFRAKT)+'¬, '
        +'¬fralk¬ : ¬'+%trim(XXLKA)+'¬, '
        +'¬fra¬ : ¬'+%trim(XXSDF)+'¬, '
        +'¬til¬ : ¬'+%trim(XXSDT)+'¬, '
        +'¬tillk¬ : ¬'+%trim(XXTRI)+'¬, '
        +'¬tilpn¬ : ¬'+%trim(XXSDT)+'¬, '
        +'¬ant¬ : ¬'+%trim(%editc(XXNT:'Z'))+'¬, '
        +'¬vkt¬ : ¬'+%trim(%editc(XXVKT:'Z'))+'¬, '
        +'¬m3¬ : ¬'+%trim(%editc(XXM3:'4'))+'¬, '
        +'¬lm¬ : ¬'+%trim(%editc(XXLM:'4'))+'¬, '
        +'¬prod¬ : ¬'+%trim(XXKDPL)+'¬, '
        +'¬varme¬ : ¬'+%trim(VARME)+'¬, '
        +'¬farlig¬ : ¬'+%trim(FARLIG)+'¬, '
        +'¬lengde¬ : ¬'+%trim(LENGDE)+'¬, '
        +'¬netto¬ : ¬'+%trim(%editc(NETTO:'J'))+'¬, '
        +'¬brutto¬ : ¬'+%trim(%editc(BRUTTO:'J'))+'¬, '
        +'¬oljvaltxt¬ : ¬'+%trim(OLJVALTXT)+'¬, '
        +'¬avdg¬ : ¬'+%trim(AVDG)+'¬, '
        +'¬frdg¬ : ¬'+%trim(FRDG)+'¬, '
        +'¬minfr¬ : ¬'+%trim(MINFR)+'¬, '
        +'¬wsavd¬ : ¬'+%trim(WSAVD)+'¬, '
        +'¬kunde¬ : ¬'+%trim(%editc(BIKUNR:'Z'))+'¬, '
        +'¬avvknr¬ : ¬'+%trim(%editc(AVVKNN:'Z'))+'¬, '
        +'¬avvkntxt¬ : ¬'+%trim(AVVKNTXT)+'¬, '
        +'¬fbv¬ : ¬'+%trim(%editc(HEFBV:'Z'))+'¬, '
        +'¬overskr¬ : ¬'+%trim(OVERSKR)+'¬, '
        +'¬tjen¬ : ¬'+%trim(TJEN)+'¬, '
        +'¬heot¬ : ¬'+%trim(HEOT)+'¬, '
        +'¬hefr¬ : ¬'+%trim(HEFR)+'¬, '
        +'¬errMsg¬ : ¬'+%trim(Error)+'¬, ';
      /end-free

     C                   CALL      'JSONFIX'
     C                   PARM                    JSONstring
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   CALLP     wrtsection('JSONlin')

     C                   EVAL      JSONstring ='"pricecalclist" : ['
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   CALLP     wrtsection('JSONlin')
     C     Error         IFEQ      *blanks
      * KUN  fraktpris .....
     C                   IF        ((XKUNFRAKT = 'J') OR (XKUNFRAKT = 'j'))
     C                   ELSE
      * FULL beregning .....
     C                   EXSR      HENTFAKT                                     syfi10x/lag linjer
     C                   EXSR      VISFAKT                                      skriv linjer
     C                   END

     C                   ENDIF
      * Skriv JSON-Liste/Array  Avslutning
     C                   EVAL      JSONstring =']'
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   CALLP     wrtsection('JSONlin')
      * Skriv JSON-string Avsluttning
     C                   EVAL      JSONstring ='}'
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   CALLP     wrtsection('JSONlin')
      * Quit
     C                   EXSR      Exit
      *=====================================================================
      * Parse input / cvt to numeric
      *=====================================================================
     C     Parse         BEGSR
     C                   CLEAR                   hxhead
      * Parse variables from QUERY_STRING environment variable
     C                   EVAL      XXLKA   = zhbgetvar('FRALK')
     C                   EVAL      XXLKA   = uppify(XXLKA)
     C                   EVAL      XXSDF   = zhbgetvar('FRA')
     C                   EVAL      XXSDF   = uppify(XXSDF)
     C                   EVAL      XXTRI   = zhbgetvar('TILLK')
     C                   EVAL      XXTRI   = uppify(XXTRI)
     C                   EVAL      XXSDT   = zhbgetvar('TIL')
     C                   EVAL      XXSDT   = uppify(XXSDT)
     C     XXLKA         IFEQ      *blanks
     C                   MOVE      'NO'          XXLKA
     C                   END
     C     XXTRI         IFEQ      *blanks
     C                   MOVE      'NO'          XXTRI
     C                   END
     C                   EVAL      ANT     = zhbgetvar('ANT')
     C                   EVAL      XXNT    = c2n2(ANT)
     C                   EVAL      VKT     = zhbgetvar('VKT')
     C                   EVAL      XXVKT   = c2n2(VKT)
     C                   EVAL      M3      = zhbgetvar('M3')
     C                   EVAL      XXM3    = *zeros
     C                   EVAL      NumBig  = c2n2(M3)
     C     NumBig        IFLE      9999                                         XXM3 = 7,3
     C                   EVAL      XXm3    = NumBig
     C                   END
     C                   EVAL      LM      = zhbgetvar('LM')
     C                   EVAL      XXLM    = *zeros
     C                   EVAL      NumBig  = c2n2(LM)
     C     NumBig        IFLE      99                                           XXLM = 4,2
     C                   EVAL      XXLM    = NumBig
     C                   END
     C                   EVAL      XXKDPL  = zhbgetvar('PROD')
     C                   EVAL      XXFR    = zhbgetvar('FRAN')
     C                   EVAL      WSAVD   = zhbgetvar('WSAVD')
      * 4 siste av wsavd er avdeling -WBAVD -sikre at det er numerisk
     C                   MOVE      wsavd         Tstnum4           4
     C                   EVAL      WBAVD   = c2n2(Tstnum4)
     C                   EVAL      WSOT    = zhbgetvar('WSOT')
     C                   EVAL      FARLIG  = zhbgetvar('FARLIG')
     C                   EVAL      VARME   = zhbgetvar('VARME')
     C                   EVAL      LENGDE  = zhbgetvar('LENGDE')
     C                   EVAL      AVVKNR  = zhbgetvar('AVVKNR')
     C                   EVAL      AVVKNN  = c2n2(AVVKNR)
     C                   EVAL      FRABOOKING = zhbgetvar('FRABOOKING')
     C                   EVAL      user = zhbgetvar('user')
      * Sorter/sammenslå er defauly men intern bruker KAN velge "SeIntern" - får da alle linjer
      *    ved KLIKK på checkboks kommer verdi Y/blank,
      *    ellesr kommer verdi fra siste valg via "SeIntern" som checked / blank(N)
     C     SeIntChecked  IFEQ      'checked'
     C                   EVAL      SeIntern = 'Y'
     C                   ELSE
     C                   EVAL      SeIntern = zhbgetvar('SeIntern')
     C                   END
     C     SeIntern      IFNE      'Y'
     C                   MOVE      'J'           SortSam
     C                   END
      * legg System Data Structure i DS
     C                   MOVE      PSDSDATA      Zsds
     C                   ENDSR
      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     EXIT          BEGSR
     C                   CLOSE     BRIDF
     C  N50              CLOSE     Sted2d
     C  N50              CLOSE     USRDFT
     C  N50              CLOSE     NOPNR
     C  N50              CLOSE     FIRINL
     C  N50              CLOSE     CUTRAN
     C  N50              CLOSE     SIDUPA
     C  N50              CLOSE     SIDUPB
     C  N50              CLOSE     SIDUPC
     C  N50              CLOSE     KODTV
     C  N50              CLOSE     KODTA
     C  N50              CLOSE     KODTGE
     C  N50              CLOSE     KODTG2
     C  N50              CLOSE     KODSAM
     C  N50              CLOSE     CUNDL01
     C  N50              CLOSE     VALUL01
     C                   CALLP     wrtsection('*fini')
     C                   EVAL      *INLR = *ON
     C                   RETURN
     C                   ENDSR
      *=====================================================================
      *  Set variable data for section /$all
      *=====================================================================
     C     SetAll        BEGSR

     C                   MOVE      BIKUNR        XXKN              8 0
     C     AVVKNN        IFNE      *zeros
     C                   MOVE      AVVKNN        XXKN
     C                   ENDIF
     C     CUNDL01K      CHAIN     CUNDL01                            33        avt.kund på BIKUNR?
     C  N33              IF        AKNRKU <> 0
     C                   MOVE      AKNRKU        XXKN
     C                   ENDIF

      * ved "Kun fraktkalk" eller ved Tr-modul beregnes frakten i forkant
     C     WSMOD         IFNE      ' '
     C     WSMOD         ANDNE     'H'
     C                   GOTO      fullberegn
     C                   END
      * Beregne frakt...
      * 1. beregne ren brutto
     C                   MOVE      *ZEROS        UUKN
     C                   EXSR      BRT
     C                   MOVE      UUVAL         XXVALBRT          3
     C                   Z-ADD(H)  UUBELØ        BRUTTO           13 0
      * 2. beregne ren avtale
     C                   Z-ADD     XXKN          UUKN              8 0
     C                   EXSR      AVT
     C                   MOVE      UUVAL         XXVALNET          3
     C                   Z-ADD(H)  UUBELØ        NETTO            13 0
      * 3 Hvis ren avtale ikke funnet prøv %% av brutto
     C     NETTO         IFEQ      *ZEROS
     C                   Z-ADD     XXKN          UUKN              8 0
     C                   EXSR      BRT
     C                   MOVE      UUVAL         XXVALNET          3
     C                   Z-ADD(H)  UUBELØ        NETTO            13 0
     C                   END
      * 25 % påslag for farlig gods / varmegods?
     C     XXLKA         IFEQ      'NO'
     C     XXTRI         ANDEQ     'NO'
     C                   MOVE      IFFARP        XXFARP            4 3
     C                   MOVE      IFVARP        XXVARP            4 3
     C                   MOVE      IFLENP        XXLENP            4 3
     C                   MOVE      IFFARP        XXFARPB           3 1
     C                   MOVE      IFVARP        XXVARPB           3 1
     C                   MOVE      IFLENP        XXLENPB           3 1
     C     XXKN          CHAIN     CUTRAN                             33
     C     *in33         IFEQ      *OFF
     C     CTIKFA        IFEQ      'X'
     C                   MOVE      *ZEROS        XXFARP
     C                   ENDIF
     C     CTIKVA        IFEQ      'X'
     C                   MOVE      *ZEROS        XXVARP
     C                   ENDIF
     C     CTIKLG        IFEQ      'X'
     C                   MOVE      *ZEROS        XXLENP
     C                   ELSE
     C     CTAVLP        IFNE      *zeros
     C                   MOVE      CTAVLP        XXLENP
     C                   ENDIF
     C                   ENDIF
     C                   ENDIF
     C     NETTO         MULT(H)   XXFARP        NETT25Fa         13 0
     C     BRUTTO        MULT(H)   XXFARPB       BRUTT25Fa        13 0
     C     NETTO         MULT(H)   XXVARP        NETT25Va         13 0
     C     BRUTTO        MULT(H)   XXVARPB       BRUTT25Va        13 0
     C     NETTO         MULT(H)   XXLENP        NETT25Le         13 0
     C     BRUTTO        MULT(H)   XXLENPB       BRUTT25Le        13 0
      * Farlig gods
     C     FARLIG        IFEQ      'Y'
     C     XXFARP        ANDNE     *ZEROS
     C                   ADD       NETT25Fa      NETTO
     C                   ADD       BRUTT25Fa     BRUTTO
     C                   END
      * Varmegods
     C     VARME         IFEQ      'Y'
     C     XXVARP        ANDNE     *ZEROS
     C                   ADD       NETT25Va      NETTO
     C                   ADD       BRUTT25Va     BRUTTO
     C                   END
      * Lengdegods
     C     LENGDE        IFEQ      'Y'
     C     XXLENP        ANDNE     *ZEROS
     C                   ADD       NETT25Le      NETTO
     C                   ADD       BRUTT25Le     BRUTTO
     C                   ENDIF
      * Minstefrakt kunde  (Ingen egen avtale-> lik brutto)
     C                   EVAL      MinFr=*blanks
     C     IFFJKO        IFNE      ' '
     C                   MOVEL     USONE         TstFjK            1
     C     TstFjK        IFEQ      IFFJKO
     C                   MOVE      CTAFMF        CTAVFM                         Kunde avv min fjern
     C                   MOVE      CTIFMF        CTIKFM                         Kunde ikke min fjer
     C     CTAVFM        IFEQ      *ZEROS
     C                   Z-ADD     IFMIFF        CTAVFM                         Firma Minste fjern.
     C                   END
     C                   END
     C                   END
     C     CTAVFM        IFEQ      *ZEROS
     C                   Z-ADD     IFMIFR        CTAVFM
     C                   END
     C     CTAVFM        IFNE      *zeros
     C     CTIKFM        ANDNE     'X'
     C     XXKDPL        ANDEQ     ' '
     C     NETTO         ANDGT     *zeros
     C     NETTO         ANDLT     CTAVFM
     C                   Z-ADD     CTAVFM        NETTO
     C                   EVAL      MinFr='Minimumsfrakt:  ' +
     C                             %trim(%editc(NETTO:'Z')) +
     C                             spaces+spaces
     C                   END
      * Minstefrakt brutto
     C     IFMIFR        IFNE      *zeros
     C     XXKDPL        ANDEQ     ' '
     C     BRUTTO        ANDGT     *zeros
     C     BRUTTO        ANDLT     IFMIFR
     C                   Z-ADD     IFMIFR        BRUTTO
     C                   ENDIF

     C                   END
      *olje/valuta-tillegg utfra felt i oppdragsbildet  (parametersettes, også HEUR ??)
     C                   IF        ((XKUNFRAKT = 'J') or  (XKUNFRAKT = 'j'))
     C                   MOVE      'J'           VisOljVal         1
     C                   ENDIF
     C                   CLEAR                   OljValTxt
     C                   CLEAR                   OljValBel
     C     VisOljVal     IFEQ      'J'
     C                   MOVE      'H'           UUR
     C                   MOVE      *ZEROS        UVALP
     C                   CALL      'SYFA24B'
     C                   PARM                    UUR               1
     C                   PARM                    UAVDOP            4 0
     C                   PARM      XXKN          UKNOP             8 0
     C                   PARM                    WSVALT            3            Geb.kode
     C                   PARM                    UVALP             3 1          %-sats
     C     UVALP         IFNE      *Zeros
     C                   MOVE      WSVALT        FAVK
     C     KGEKey        CHAIN     KODTGE                             33
     C     *in33         IFEQ      '0'
     C                   EVAL      OljValBel = (Netto*Uvalp/100) + 0.5
     C                   MOVE      00            OljValBel
     C                   EVAL      Netto = Netto + OljValbel
     C                   EVAL      OljValTxt = '(Inkl '
     C                             + %trim(%editc(UVALP:'2'))+' % '
     C                             + %trim(KGENOT)+'.  ( '
     C                             + %trim(%editc(OljValBel:'J'))+' ) )'
     C                   END
     C                   END
     C                   END
      * Tilsteds NAVN, Avgangsdg(1-7) + framføringstid(8-9) i ZIP-kode(9)
     C                   MOVE      *BLANKS       ST2NVN
     C                   MOVE      *BLANKS       AVDG
     C                   MOVE      *BLANKS       FRDG
      * ved ren innlandsfrakt KAN NOPNR være brkt
     C     XXLKA         IFEQ      'NO'
     C     XXTRI         ANDEQ     'NO'
     C                   MOVEL     XXSDT         TSTPNR            4
     C     TSTPNR        IFGE      '0000'
     C     TSTPNR        ANDLE     '9999'
     C     zuser         CHAIN     usrdft                             33
     C                   MOVE      TSTPNR        NOPONR
     C     InnlKey       CHAIN     NOPNR                              33
     C     *IN33         IFEQ      *OFF
     C                   MOVEL     NONAVN        ST2NVN
     C                   MOVE      NOAVGD        AVGDG
     C                   MOVE      NOFRFT        FRADG
     C     NOFRFT        IFEQ      *zeros
     C                   MOVE      *blanks       FRADG
     C                   ELSE
     C     NOFRFT        IFLT      10
     C                   MOVEL     ' '           FRADG
     C                   END
     C                   END
     C                   GOTO      PnrOk
     C                   END
     C                   END
     C                   END

     C     FullBeregn    TAG

     C     TILKEY        CHAIN     STED2D                             33
     C     *IN33         IFEQ      *OFF
     C     PnrOk         TAG                                                    navn/dgr fra NOPNR
     C     AVGDG         IFNE      *BLANKS
     C                   EXSR      KONVDG
     C                   END
     C     FRADG         IFNE      *BLANKS
      *                  EVAL      FRDG = 'Ant. framf.dg = '+FRADG
     C                   EVAL      FRDG = FRADG
     C                   END
     C                   END
     C                   EVAL      TILTX = XXTRI + XXSDT + ST2NVN
     C                   IF        XXFBVRet = 0
     C                   Z-ADD     XXVKT         HEFBV
     C                   ELSE
     C                   Z-ADD     XXFBVRet      HEFBV
     C                   ENDIF
      * Ved full beregning - lag oppdrag i SIDUPB (ovverride HEADF..)
      * (må lages FØR write øvre del av skjermbildet for å få med F.vekt mm)
     C                   IF        ((XKUNFRAKT <> 'J') AND (XKUNFRAKT <>'j'))
     C                   EXSR      SiDupOpd
     C                   END
     C     SeIntern      IFEQ      'Y'
     C                   EVAL      SeIntChecked = 'checked'
     C                   END
     C     AVVKNN        IFNE      *zeros
      *                  EVAL      AVVKNTXT = 'AvvikENDe kundenr '
      *                            + %trim(%editc(AVVKNN:'Z'))
     C                   EVAL      AVVKNTXT = %trim(%editc(AVVKNN:'Z'))
     C                   END
      * klargjøre HTMLvariable
      *
     C                   ENDSR
      *
      ***********************************************************************
     C     SiDupOpd      BEGSR
      ***********************************************************************
     C                   MOVE      'J'           POSTAL            1
     C     KodtaKey      CHAIN     KODTA                              52
     C  N52              MOVE      KOAPOS        POSTAL
     C     WBAVD         CHAIN     SIDUPC                             52
     C     *IN52         IFEQ      '0'
     C                   MOVE      TEOPDN        WBOPD
     C                   ADD       1             TEOPDN
     C                   UPDATE    TELLR
     C                   ELSE
     C                   Z-ADD     WBAVD         TEAVD
     C                   Z-ADD     1             TEOPDN
     C                   MOVE      TEOPDN        WBOPD
     C                   ADD       1             TEOPDN
     C                   WRITE     TELLR
     C                   END
      * fjern evt gamle..
     C     SIDUPAK       CHAIN     SIDUPA                             33
     C  N33              DELETE    HEADFR
     C     SIDUPAK       SETLL     SIDUPB
     C     SIDUPAK       READE     SIDUPB                                 33
     C                   DOW       *IN33 = *OFF
     C                   DELETE    FAKTR
     C     SIDUPAK       READE     SIDUPB                                 33
     C                   ENDDO

     C                   EVAL      HEAVD  = WBAVD
     C                   EVAL      HEOPD  = WBOPD
     C                   MOVE      XXTRI         HELKK
     C                   EVAL      HELKS  = XXLKA                               FRA
     C                   EVAL      HELKK  = XXTRI                               TIL
     C                   SELECT
     C     WSMOD         WHENEQ    'H'
     C                   EVAL      HELKA  = XXLKA
     C                   EVAL      HETRI  = XXTRI
     C     WSMOD         WHENEQ    'A'
     C     WSMOD         OREQ      'C'
     C     WSMOD         OREQ      'E'
     C     WSMOD         OREQ      'F'
     C                   EVAL      HELKA  = XXLKA
     C                   OTHER
     C                   EVAL      HELKA  = XXTRI
     C                   ENDSL
     C                   EVAL      HEUR   = WSMOD
     C                   EVAL      HESG   = 'WEB'
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVE      WDT           HEDTR
     C                   MOVE      WDT           HEDTOP
     C                   MOVE      XXSDF         HESDF
     C                   MOVE      XXSDT         HESDT
     C     HEUR          IFNE      'C'
     C     HEUR          ANDNE     'D'
     C     HEUR          ANDNE     'E'
     C     HEUR          ANDNE     'F'
     C     HEUR          ANDNE     'G'
     C     HEUR          ANDNE     'H'
     C     POSTAL        ANDEQ     'J'
      * til ER alt CHAINet
     C                   MOVE      ST2KO2        HESDT
     C     FRAKEY        CHAIN     STED2D                             33
     C                   MOVE      ST2KO2        HESDF
     C                   MOVE      XXSDF         HESDFF
     C                   MOVE      XXSDT         HESDVT
     C                   END

     C                   IF        WSSK = 'S'
     C                   MOVE      XXKN          HEKNS
     C                   MOVE      XXKN          HEKNSF
     C                   MOVE      ' '           HEKDFS
     C                   MOVE      UUVAL         HEVALS
     C                   MOVE      *BLANKS       HENAS
     C                   MOVE      'X'           HEKDFK
     C                   ELSE
     C                   MOVE      XXKN          HEKNK
     C                   MOVE      XXKN          HEKNKF
     C                   MOVE      ' '           HEKDFK
     C                   MOVE      UUVAL         HEVALK
     C                   MOVE      *BLANKS       HENAK
     C                   MOVE      'X'           HEKDFS
     C                   END

     C                   EVAL      HEKDPL = XXKDPL
     C                   MOVE      WSOT          HEOT
     C                   MOVE      XXFR          HEFR
     C                   MOVE      XXNT          HENT
     C                   MOVE      XXVKT         HEVKT
     C                   MOVE      XXLM          HELM
     C                   MOVE      XXM3          HEM3
     C     KODTVK        CHAIN     KODTV                              33
     C     HELM          MULT      KOVLKG        XFBV1             9 0
     C     HEM3          MULT      KOVKKG        XFBV2             9 0
     C     HEVKT         IFGE      XFBV1
     C                   Z-ADD     HEVKT         HEFBV
     C                   ELSE
     C                   Z-ADD     XFBV1         HEFBV
     C                   END
     C     HEFBV         IFLT      XFBV2
     C                   Z-ADD     XFBV2         HEFBV
     C                   END
     C     XXFBVRet      IFNE      *zeros
     C                   Z-ADD     XXFBVRet      HEFBV
     C                   END
     C                   MOVE      XXVALBRT      TRFRAV
     C                   if        XXVALNET<> *blanks
     C                   MOVE      XXVALNET      TRFRAV
     C                   ENDIF
     C                   IF        NETTO = 0
     C                   Z-ADD     BRUTTO        TRFRAB
     C                   ELSE
     C                   Z-ADD     NETTO         TRFRAB
     C                   END

     C                   CALL      'SYFA24B'
     C                   PARM                    HEUR
     C                   PARM      WBAVD         UAVD              4 0
     C                   PARM                    XXKN
     C                   PARM                    HEVALT
     C                   PARM                    HEVALP

     C                   WRITE     HEADFR

      *NETTO ulik 0 KUN ved TR-modul.. - i så fall legg ut evt fakt på Linje 0 og val/oil på 1
     C                   IF        TRFRAB<> 0
     C                   EVAL      FAAVD  = WBAVD
     C                   EVAL      FAOPD  = WBOPD
     C                   MOVE      *ZEROS        FALI
     C                   EVAL      FASK   = WSSK
     C                   EVAL      FAVK   = 'FRA'
     C                   EVAL      FAVAL  = TRFRAV
     C                   MOVE      XXKN          FAKUNR
     C                   EVAL      FAKDM  = 'J'
     C                   IF        NETTO <> 0
     C                   if        XSPRÅK<> 'EN'
     C                   EVAL      FAVT   = '(Netto/Avtale)'
     C                   ELSE
     C                   EVAL      FAVT   = '(Net/Agreement)'
     C                   ENDIF
     C                   ELSE
     C                   if        XSPRÅK<> 'EN'
     C                   EVAL      FAVT   = '(STD./IKKE avtale!)'
     C                   ELSE
     C                   EVAL      FAVT   = '(STD./NO agreement!)'
     C                   ENDIF
     C                   ENDIF
     C                   EVAL      FABELV = TRFRAB
     C                   Z-ADD     100           VALKU1
     C                   Z-ADD     100           OMRFAK
     C     VALKEY        CHAIN     VALUL01
      *          Dirty avrunding burde bruft firm-verdier (se DECI i syfi10X)
     C                   EVAL(H)   FABELN = (FABELV*VALKU1/OMRFAK)+0.50
     C                   MOVE      00            FABELN
     C                   WRITE     FAKTR
      * olj/val-tillegg HER kun når frakt alt er gitt (TR-modul)
     C                   IF        HEVALT <> *BLANKS
     C                   ADD       1             FALI
     C                   EVAL      FAVK   = HEVALT
     C                   EVAL      FAVT   = *BLANKS
     C                   EVAL      FABELV = NETTO * HEVALP
     C                   EVAL      FABELN = (FABELV*VALKU1/OMRFAK)+0.50
     C                   MOVE      00            FABELN
     C                   WRITE     FAKTR
     C                   END
     C                   END
      * kunne med fordel være mottatt som parameter...
     C                   select
     C     HEUR          wheneq    'A'
     C                   EVAL      TJEN = 'Land import'
     C     HEUR          wheneq    'B'
     C                   EVAL      TJEN = 'Land eksport'
     C     HEUR          wheneq    'C'
     C                   EVAL      TJEN = 'Fly import'
     C     HEUR          wheneq    'D'
     C                   EVAL      TJEN = 'Fly eksport'
     C     HEUR          wheneq    'E'
     C                   EVAL      TJEN = 'Fly innland'
     C     HEUR          wheneq    'F'
     C                   EVAL      TJEN = 'Sjø import'
     C     HEUR          wheneq    'G'
     C                   EVAL      TJEN = 'Sjø eksport'
     C     HEUR          wheneq    'H'
     C                   EVAL      TJEN = 'Transp. innland'
     C     WSMOD2        IFEQ      'I'
     C                   EVAL      TJEN = 'Transp. import'
     C                   ELSE
     C     WSMOD2        IFEQ      'E'
     C                   EVAL      TJEN = 'Transp. eksport'
     C                   END
     C                   END
     C                   ENDsl
     C                   EVAL      TJEN=%trim(TJEN)+', avd '
     C                             +%trim(%editc(WBAVD:'Z'))

     C                   ENDSR
      *=====================================================================******
      *  HENT FAKTURA
      *=====================================================================******
     C     HENTFAKT      BEGSR

     C                   MOVE      'NY'          UNYGML            2
     C                   MOVE      *BLANKS       UMELD             1
     C                   Z-ADD     9             UFLAGG            1 0
     C                   Z-ADD     0             UKFRA             7 0
     C                   MOVE      'FR'          WFRUNI            2
     C                   MOVE      'S'           UKODE             1
     C                   MOVE      '*SECUR'      USECUR
     C                   CALL      'SYFI10X'
     C                   PARM                    BIFIRM                         FIRMAKODE
     C                   PARM                    HEAVD                          AVDELING
     C                   PARM                    HEOPD                          OPDNR
     C                   PARM                    UNYGML                         NY=N GML=G
     C                   PARM                    KO2UTL            1            UTL PROV
     C                   PARM                    XKUTL             4 2          UTLP SATS
     C                   PARM                    XSUTL             4 2          UTLP SATS
     C                   PARM                    XKUMIN            2 0          UTLP MIN
     C                   PARM                    XSUMIN            2 0          UTLP MIN
     C                   PARM                    KO2MVK            1            MVA KJØPER
     C                   PARM                    KO2MVS            1            MVA SELGER
     C                   PARM                    UKFRA                          =0
     C                   PARM                    KOVAVR            1            OVERST.AVRUN
     C                   PARM                    UMSG             62            DS
     C                   PARM                    UMELD                          =*BLANK
     C                   PARM                    UFLAGG                         =0
     C                   PARM                    UKODE             1
     C     USECUR        PARM      USECUR        USECUR            6            =*SECUR

     C                   ENDSR
      *=====================================================================******
      *  VIS FAKTURA
      *=====================================================================******
     C     VISFAKT       BEGSR

     C     SIDUPAK       CHAIN     SIDUPA                             33
     C  N33              DELETE    HEADFR
      * skriv ut
     C     SIDUPAK       SETLL     SIDUPB
     C     SIDUPAK       READE     SIDUPB                                 33
     C                   DOW       *IN33 = *OFF
     C                   DELETE    FAKTR
      * klargjøring av linje flyttet ut i egen SR Setrow
      * odde/partalls-linjer i alternativ skyggelegging/bakgrunnsfarge
     C     SortSam       IFEQ      'J'
     C                   EXSR      SortSamSR
     C                   ELSE
     C                   EXSR      Setrow
     C                   END
     C     SIDUPAK       READE     SIDUPB                                 33
     C                   ENDDO
      * Hent sortert/sammenslått faktura fra array...
     C     SortSam       IFEQ      'J'
     C                   SORTA     FLI
     C                   EVAL      strXn = 100 - MaxXn + 1                      5 lin: elem 96->
     C     strXN         DO        100           XN01
     C     FLI(XN01)     IFNE      *BLANKS
     C                   MOVE      FLI(XN01)     NYDS
     C                   EXSR      Setrow
     C                   ENDIF
     C                   ENDDO
     C                   ENDIF
     C                   ENDSR
      ***********************************************************
     C     SortSamSR     BEGSR
      ***********************************************************
      * sort/sammenslå
      * kriterium for sammenslåing  - IKKE FRITEKST
      *            A)  SAMME GEBYRKODE       / FRITEKST  / momskode,
      *  ELLER     B)  SAMME SAMMENSNL.KODE  / FRITEKST  / momskode,
      * SAMMENSLÅTTE LINJER KOMMER FØRST
      * DERETTER LINJER  MED SAM-KODE MEN SOM IKKES SLÅS SAMMEN PGA FRITEKST
      * DERETTER ANDRE LINJER BASERT PÅ SORT-KODE
      *
      * a) klargjør noen variable
      * varetekst
     C     KG2Key        CHAIN     KODTG2                             33
     C     KGEKey        CHAIN     KODTGE                             33
     C                   IF        ((FAVK <> 'FRA') AND (FAVT = 'NETTO FRAKT'))
     C                   MOVE      *BLANKS       FAVT
     C                   END
     C     XSPRÅK        IFEQ      'EN'
     C                   EVAL      NY_VT = KGEENT+' '+FAVT
     C                   ELSE
     C                   EVAL      NY_VT = KGENOT+' '+FAVT
     C                   END
     C     FAVT1         IFEQ      'X'
     C                   EVAL      NY_VT =  FAVT2_20
     C                   END
      * Sort-nr/samkod(+evt tekst derfra.)
     C                   MOVE      KG2SRT        NY_SORT
     C                   MOVE      FACD11        NY_SAMM
     C     NY_SAMM       IFNE      *BLANKS
     C     FAVT          IFNE      *BLANKS
     C                   MOVE      '  1'         NY_SORT
     C                   MOVE      *BLANKS       NY_SAMM
     C                   ELSE
     C                   MOVE      '  2'         NY_SORT
     C     NY_SAMM       CHAIN     KODSAM                             33
     C  N33XSPRÅK        IFEQ      'EN'
     C                   EVAL      NY_VT = SAENGE
     C                   ELSE
     C                   EVAL      NY_VT = SANORS
     C                   END
     C                   END
     C                   END
      * b) slå sammen eller legg i første ledige
     C     1             DO        100           XN01
     C     FLI(XN01)     IFNE      *BLANKS
     C                   MOVE      FLI(XN01)     GMDS
     C     NYDS_A        IFEQ      GMDS_A
     C                   ADD       FABELV        GM_BELV
     C                   ADD       FABELN        GM_BELN
     C                   MOVE      *blanks       GM_VK                          sammenslått
     C                   MOVE      GMDS          FLI(XN01)
     C                   LEAVE
     C                   END
     C                   ELSE
     C                   MOVE      NYDS          FLI(XN01)
     C                   Z-ADD     XN01          MaxXN
     C                   LEAVE
     C                   END
     C                   ENDDO
     C                   ENDSR
      ********************************************************************
     C     SetRow        BEGSR
      ********************************************************************
      * Ved kundeview-sortert/sammenslått så vises avvikENDe tekst.
     C     SortSam       IFEQ      'J'
     C                   EVAL      VTXT = NY_VT
     C                   ELSE
     C     KGEKey        CHAIN     KODTGE                             33
     C     XSPRÅK        IFEQ      'EN'
     C   33              EVAL      KGEENT = '*Unknown'
     C                   EVAL      VTXT = KGEENT + FAVT
     C                   ELSE
     C   33              EVAL      KGENOT = '*Ukjent'
     C                   EVAL      VTXT = KGENOT + FAVT
     C                   END
     C                   END
     C     XINTERN       IFNE      'J'
     C                   MOVE      *BLANKS       FAVK
     C                   ELSE
     C     SortSam       IFNE      'J'
     C     FACD11        ANDNE     ' '
     C                   EVAL      VTXT = %trim(VTXT)+'  ('+FACD11+')'
     C                   END
     C                   END
     C                   ADD       FABELN        WSTOTN
     C                   Add       1             AntLest
      /free
       if AntLest=1;
          JSONstring=*blanks;
          ELSE;
          JSONstring=', ';
          ENDIF;
       JSONstring=%trim(JSONstring)+'{'
          +'¬wsgkd¬ : ¬'+%trim(FAVK)+'¬, '
          +'¬wsgtxt¬ : ¬'+%trim(VTXT)+'¬, '
          +'¬wsval¬ : ¬'+%trim(FAVAL)+'¬, '
          +'¬wsbel¬ : ¬'+%trim(%editc(FABELV:'J'))+'¬, '
          +'¬wsbeln¬ : ¬'+%trim(%editc(FABELN:'J'))+'¬, '
          +'¬wsmva¬ : ¬'+%trim(FAKDM)+'¬, '
          +'¬wstotn¬ : ¬'+%trim(%editc(WSTOTN:'J'))+'¬}';
      /end-free
     C                   CALL      'JSONFIX'
     C                   PARM                    JSONstring
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   CALLP     wrtsection('JSONlin')

     C                   ENDSR
      *=====================================================================******
      *  Konverter dagnr 123.. til Ma Ti On ....
      *=====================================================================******
     C     KONVDG        BEGSR

     C                   EVAL      AVDG = 'Avg.dg: '
     C     1             DO        7             NR                1 0
     C     AV(NR)        IFGE      '1'
     C     AV(NR)        ANDLE     '7'
     C                   MOVE      AV(NR)        NN                1 0
     C                   CAT       DGN(NN):1     AVDG
     C                   END
     C                   END

     C                   ENDSR
      *=====================================================================******
      *  BRUTTO rate
      *=====================================================================******
     C     BRT           BEGSR
     C                   MOVE      *ZEROS        XXFBV
     C                   MOVE      *ZEROS        NAVD
     C                   MOVE      *ZEROS        N5
     C                   MOVE      *ZEROS        UUBELØ           13 2
     C                   MOVE      *BLANKS       USONE
     C                   MOVE      *BLANKS       UUVAL
     C                   MOVE      *ZEROS        URABAT
     C                   MOVE      'FRA'         UGEB
     C                   MOVE      HXHEAD        UHEAD           256
     C                   CALL      'SYGE13'
     C                   PARM                    UGEB
     C                   PARM                    NAVD
     C                   PARM                    NOPD
     C                   PARM                    UUVAL
     C                   PARM                    UUBELØ
     C                   PARM                    USONE             5
     C                   PARM                    URABAT            5 2
     C                   PARM                    N5                5 0
     C                   PARM                    N5                5 0
     C                   PARM                    N5                5 0
     C                   PARM                    N5                5 0
     C                   PARM                    UUKN
     C                   PARM                    UHEAD
     C                   PARM                    atdum             1
     C                   MOVE      UHEAD         HXHEADRet

     C                   ENDSR
      *
      *=====================================================================******
      *  AVTALE
      *=====================================================================******
     C     AVT           BEGSR
     C                   MOVE      *BLANKS       UUVAL             3
     C                   MOVE      'FRA'         UGEB              3
     C                   MOVE      'FRA'         UGEBKO            3
     C                   MOVE      *ZEROS        UUBELØ           13 2
     C                   MOVE      HXHEAD        UHEAD           256
     C                   CALL      'SYGE14'
     C                   PARM                    UGEBKO
     C                   PARM                    UGEB
     C                   PARM                    NAVD
     C                   PARM                    NOPD
     C                   PARM                    UUKN
     C                   PARM                    UUVAL
     C                   PARM                    UUBELØ
     C                   PARM                    UAVNR             9 0
     C                   PARM                    UAVMSG           50
     C                   PARM                    USKA
     C                   PARM                    UHEAD
     C                   MOVE      UHEAD         HXHEADRet

     C                   ENDSR
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          BEGSR
     C                   OPEN      BRIDF
     C     USER          CHAIN     BRIDF                              50
      * En "standardvariant" libl: CALL bound (INCGI=*MODULE) med parameter.
     C     BILIBL        IFEQ      *blanks
     C                   CALLB     'INCGI'
     C                   PARM                    BISTDL
     C                   ELSE
      * Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   CALL      BILIBL
     C                   END
      * hent Parametre som går igjen i mange prog:
      *      Odd/Even/Rowhead-farger,Logobilde,Språk,Intern/Ekstern bruker,  mm
     C                   CALL      'ESGETPAR'
     C                   PARM                    BIBRID
     C                   PARM                    BIFIRM
     C                   PARM                    BIKUNR
     C                   PARM                    BIKNG
     C                   PARM                    RowHead          10
     C                   PARM                    Odd              10
     C                   PARM                    Even             10
     C                   PARM                    LogoImg          50
     C                   PARM                    XSPRÅK            2
     C                   PARM                    XINTERN           1
     C                   PARM                    PARMDS1        1024
     C                   PARM                    PARMDS2        1024
     C                   PARM                    PARMDS3        1024
     C   50              EVAL      Error = 'Invalid userID'

     C  N50              OPEN      Sted2d
     C  N50              OPEN      USRDFT
     C  N50              OPEN      NOPNR
     C  N50              OPEN      FIRINL
     C  N50              OPEN      CUTRAN
     C  N50              OPEN      KODTV
     C  N50              OPEN      KODTA
     C  N50              OPEN      KODTGE
     C  N50              OPEN      KODTG2
     C  N50              OPEN      KODSAM
     C  N50              OPEN      BRPARF
     C  N50              OPEN      SIDUPA
     C  N50              OPEN      SIDUPB
     C  N50              OPEN      SIDUPC
     C  N50              OPEN      CUNDL01
     C  N50              OPEN      VALUL01
      * key for Sted2d
     C     FRAKEY        KLIST
     C                   KFLD                    XXLKA
     C                   KFLD                    XXSDF
     C     TILKEY        KLIST
     C                   KFLD                    XXTRI
     C                   KFLD                    XXSDT
     C     SIDUPAK       KLIST
     C                   KFLD                    WBAVD
     C                   KFLD                    WBOPD
     C     InnlKey       KLIST
     C                   KFLD                    ZZTERM
     C                   KFLD                    NOPONR
     C     KODTVK        KLIST
     C                   KFLD                    KOVUNI
     C                   KFLD                    HEAVD
     C     KodtaKey      KLIST
     C                   KFLD                    KOAUNI
     C                   KFLD                    WBAVD
     C     KGEKey        KLIST
     C                   KFLD                    KGEUNI
     C                   KFLD                    FAVK
     C     KG2Key        KLIST
     C                   KFLD                    KG2UNI
     C                   KFLD                    FAVK
     C     BRPARFK       KLIST
     C                   KFLD                    PABRID
     C                   KFLD                    PAKUNR
     C                   KFLD                    PAID
     C     CUNDL01K      KLIST
     C                   KFLD                    BIFIRM
     C                   KFLD                    XXKN
     C     VALKEY        KLIST
     C                   KFLD                    BIFIRM
     C                   KFLD                    FAVAL
     C  N50*LOVAL        SETLL     FIRINL
     C  N50              READ      FIRINL                                 33
      * Konstanter postnrbasert KALKULASJON
     C                   MOVE      'H'           XXUR
     C                   MOVE      *BLANKS       XXLKS
     C                   MOVE      *BLANKS       XXSDFF
     C                   MOVE      *BLANKS       XXLKK
     C                   MOVE      *BLANKS       XXSDVT
     C                   MOVE      *BLANKS       XXVERV
     C                   MOVE      *ZEROS        XXVERB
     C                   MOVE      *ZEROS        N5                5 0
     C                   MOVE      *ZEROS        NAVD              4 0
     C                   MOVE      *ZEROS        NOPD              7 0
     C                   MOVE      *ZEROS        uavnr
     C                   MOVE      *ZEROS        XXFBVRet
     C                   MOVE      'S'           USKA              1
     C                   MOVE      'V'           KOVUNI
     C                   MOVE      'A'           KOAUNI
     C                   MOVE      'GE'          KGEUNI
     C                   MOVE      'G2'          KG2UNI
     C                   EVAL      PABRID = USER
     C                   MOVEL     'KUNFR'       PAID
     C  N50BRPARFK       CHAIN     BRPARF                             33
     C                   IF        *IN33
     C                   EVAL      PABRID = '*KUNDFT*' + BIFIRM
     C                   EVAL      PAKUNR = BIKUNR
     C  N50BRPARFK       CHAIN     BRPARF                             33
     C                   IF        *IN33
     C                   EVAL      PABRID = '*KUNDFT*' + BIFIRM
     C                   EVAL      PAKUNR = BIKUNR
     C  N50BRPARFK       CHAIN     BRPARF                             33
     C                   IF        *IN33
     C                   EVAL      PAKUNR = BIKNG
     C  N50BRPARFK       CHAIN     BRPARF                             33
     C                   IF        *IN33
     C                   EVAL      PAKUNR = 0
     C  N50BRPARFK       CHAIN     BRPARF                             33
     C                   ENDIF
     C                   ENDIF
     C                   ENDIF
     C                   ENDIF
     C                   IF        *IN33
     C                   MOVE      'J'           XKUNFRAKT         1
     C                   ELSE
     C                   MOVEL     PAVRDA        XKUNFRAKT
     C                   ENDIF
     C                   IF        ((XKUNFRAKT = 'J') OR (XKUNFRAKT = 'j'))
     C                   MOVE      *ZEROS        WBAVD
     C                   ENDIF
      * HENT SPRÅKKODE
     C                   MOVEL     'SPRÅK'       PAID
     C  N50BrParFK       CHAIN     BRPARF                             33
     C                   IF        (*IN33 OR (PAVRDA <> 'EN'))
     C                   MOVE      *BLANKS       XSPRÅK            2
     C                   ELSE
     C                   MOVE      'EN'          XSPRÅK            2
     C                   ENDIF
     C                   ENDSR
