     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPDOC('STD' 'HELPNU' 'HLP400')
     A                                      HELP
      *
     A          R SUBFIL                    SFL
     A            ISFRDA         8S 0H      TEXT('DATO')
     A            WSNR           1A  I  7  2
     A  99                                  DSPATR(RI)
     A                                      VALUES('5' '4' '3' '2' '6' '9' ' ' -
     A                                      'A' 'E')
     A            WSST           1A  H
     A            WSSLUT         1A  H
     A            WSFN           7Y 0H
     A            WSSTUN         8Y 0H
     A            WSSAVD         4Y 0O  7  4EDTCDE(4)
     A            WSSOPD         7Y 0O  7  9EDTCDE(4)
     A            WSSLOP         3Y 0O  7 17EDTCDE(4)
     A            WSSSAK         7Y 0O  7 21EDTCDE(4)
     A  41                                  COLOR(YLW)
     A N41                                  COLOR(RED)
     A            WSSAVA         5A  O  7 29
     A            WSSPFO         5A  O  7 35
     A            WSSÅRS        30A  O  7 41
     A            WSSREG         6Y 0O  7 72EDTCDE(Y)
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0016)
     A                                      SFLPAG(0015)
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A                                  1  2'SYSPED/SYIS10O'
     A                                      COLOR(BLU)
     A            ZRECNO         4S 0O  1 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1 19MSGCON(040 SHU1094 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  2 65TIME
     A                                      COLOR(BLU)
     A                                  3  2MSGCON(007 TBR0017 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 13MSGCON(009 TBR0018 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 25MSGCON(009 TBR0071 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(060 SHU1097 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  6  1'Op Avd. Oppd.nr Lop Saksnr. Art   -
     A                                      Part  Årsak                        -
     A                                        Reg.dato'
     A                                      COLOR(WHT)
      *
     A          R BUNN
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF03(03 'Avslutt')
     A                                      CF05(05)
     A                                      CF12(12)
     A                                      CF08(08)
     A                                      SLNO(22)
     A                                      CLRL(03)
     A                                      OVERLAY
     A                                  2  2MSGCON(010 TBR0013 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  2 22'F5=Frisk'
     A                                      COLOR(BLU)
     A                                  2 42MSGCON(010 DIV0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  3  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R SOK
     A                                      CF03(03 'Avslutt')
     A                                      CF12(12)
     A                                      SLNO(22)
     A                                      CLRL(03)
     A                                      OVERLAY
     A                                  2  2MSGCON(015 SHU1095 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            WSDATO         6Y 0I  2 18DSPATR(PC)
     A                                  2 26MSGCON(015 SHU1096 *LIBL/SYMSGF)
