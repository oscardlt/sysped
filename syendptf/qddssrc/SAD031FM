     A*%%TS  SD  20181024  112637  SVEIN       REL-V7R1M0  5770-WDS
*****A**************************************************************
     A* RETTINGER I DETTE PROGRAM:
PTFnrA*Sek Sig Vers Beskrivelse...........................
"""""A*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
P7XXXA* 01 YBC PTF7 BEVARE REFERANSE
xxxxxA*xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
11XXXA* 02 YBC PTF11 SLETT OPSJON
12237 * 03 YBC PTF12 UTVIDE KURS FRA 6,3 TIL 7,3
*****A**************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3                  -
     A                                             27 132 *DS4)
     A                                      PRINT
     A                                      HELP
     A                                      ALTHELP(CA01)
      * "NOERASE" MÅ HA FOR Å IKKE TØMME BILDE SOM MAN KOMME FRA
     A          R NOERASE
     A                                      ASSUME
     A                                  1  2' '
      * MAN KUN DEFINNERE VINDU PÅ REKORDNIVÅ
     A          R WINDOWSZ
     A                                      WINDOW(*DFT 15 72 *NOMSGLIN)
     A                                      OVERLAY
     A*
S02  A*           WSNR           1A  I  6  2VALUES(' ' '1')
S02  A*           WSNR           1A  I  6  2VALUES(' ' '1')
S02  A*           WSNR           1A  I  6  2VALUES(' ' '1')
     A          R SUBFIL                    SFL
     A*%%TS  SD  20181024  112637  SVEIN       REL-V7R1M0  5770-WDS
S02  A*           WSNR           1A  I  6  2VALUES(' ' '1')
     A            WSNR           1A  I  6  2VALUES(' ' '1' '4' 'W')
     A            ZFUNIK         9S 0H
     A            ZFREFF        17A  H
     A            ZFTXT         17A  O  6  5
     A N88                                  DSPATR(RI)
     A            ZFDT           8Y 0H
     A            WSDT           6Y 0O  6 23EDTCDE(Y)
     A            ZFBL28        12Y 2O  6 32EDTCDE(4)
     A            ZFVK28         3A  O  6 46
S01  A*           ZFKR28         6Y 3O  6 50EDTCDE(4)
S01  A*           NAVN12        12   O  6 58
N01  A            ZFKR28         7Y 3O  6 50EDTCDE(4)
N01  A            NAVN12        12   O  6 59
     A*
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A*%%TS  SD  20180823  085203  SVEIN       REL-V7R1M0  5770-WDS
     A  *DS3                                SFLSIZ(0007)
     A  *DS4                                SFLSIZ(0007)
     A  *DS3                                SFLPAG(0006)
     A  *DS4                                SFLPAG(0006)
     A  *DS3                                WINDOW(WINDOWSZ)
     A  *DS4                                WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A                                      CA05(05 'Frisk')
     A                                      CA12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            ZRECNO         4S 0O  2 56SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SAD031'
     A                                      COLOR(BLU)
     A                                  1 10'Plukk fakturanr'
     A                                      COLOR(WHT)
S02  A*                                 3  2'Velg opsjon;   1=Plukk'
     A                                  3  2'Velg opsjon   1=Plukk   4=Slett W=-
     A                                      se faktura i windows'
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(055 DIV1830 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            W2NR           1A  I  5  2VALUES(' ' '1')
     A            W2TXT         17A  I  5  5
     A                                  2 34'W ved reverse'
     A                                      COLOR(BLU)
      *
     A          R BUNN
     A                                      WINDOW(WINDOWSZ)
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A N30                             12  2'F5=Frisk   F12=Forrige'
     A                                      COLOR(BLU)
     A  30        MSGTXT        58A  O 12  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R NYWRK
     A                                      WINDOW(WINDOWSZ)
     A                                      CA05(05 'Frisk')
     A                                      CA12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SAD031'
     A                                      COLOR(BLU)
     A                                  1 10'Plukk fakturanr'
     A                                      COLOR(WHT)
S02  A*                                 3  2'Velg opsjon;   1=Plukk'
N02  A                                  3  2'Velg opsjon   1=Plukk   4=Slett'
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(55 DIV1830 SYMSGF)
     A                                      COLOR(WHT)
     A            W2NR           1A  I  5  2VALUES(' ' '1')
     A            W2TXT         17A  I  5  5
     A N30                             12  2'F5=Frisk   F12=Forrige'
     A                                      COLOR(BLU)
     A  30        MSGTXT        58A  O 12  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
