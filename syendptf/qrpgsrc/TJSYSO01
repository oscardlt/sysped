      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/TJSYSO01 MODULE(*LIBL/TJSYSO01 *LIBL/INCGI)
CRTPGM*        ACTGRP(*NEW) AUT(*USE)
      *
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
11XXX * 01 JOV PTF11 VIS FAKURA SUM - parameterstyrt
11XXX *              OBS var: ACTGRP(CGI)  endret til: ACTGRP(*NEW)
      *%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
      * 02 JOV PTF12 søk også på WSRFK= mottakers ref = Head35
      * 03 JOV PTF12 POD-dato henger igjen
12166 * 04 JOV PTF12 Kundegruppe utvidet fra 30 til 100 poster
12166 * 05 JOV PTF12 Param SGFSU=Vis fakt-Sum kan sette på FirmaNivå (USER overstyrer,begge veier)
      * 06 JOV PTF12 Inntill 5 uliek cutom-(kundespesial)-kolonner i lista
      * 07 JOV PTF12 Hvis HEDTMO = blank sjekk T&T/POD (kopi fra utkj.dup ??)
********************************************************************
      /copy SYSPEDS/qrpgsrcpy,hspecs
      /copy SYSPEDS/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRPARF    IF   E           K DISK    USROPN
     FFAIN      IF   E           K DISK    USROPN
N01  FFAKT      IF   E           K DISK    USROPN
     FHEADF     IF   E           K DISK    USROPN
     FHEADDT    IF   E           K DISK    USROPN
     F                                     RENAME(HEADFR:HEADFRDT)
     FHEAD14    IF   E           K DISK    USROPN
     F                                     RENAME(HEADFR:HEADFR14)
     FHEAD17    IF   E           K DISK    USROPN
     F                                     RENAME(HEADFR:HEADFR17)
     FHEAD18    IF   E           K DISK    USROPN
     F                                     RENAME(HEADFR:HEADFR18)
     FHEAD19    IF   E           K DISK    USROPN
     F                                     RENAME(HEADFR:HEADFR19)
     FHEAD29    IF   E           K DISK    USROPN
     F                                     RENAME(HEADFR:HEADFR29)
     FHEAD30    IF   E           K DISK    USROPN
     F                                     RENAME(HEADFR:HEADFR30)
     FHEAD31    IF   E           K DISK    USROPN
     F                                     RENAME(HEADFR:HEADFR31)
     FHEAD32    IF   E           K DISK    USROPN
     F                                     RENAME(HEADFR:HEADFR32)
N02  FHEAD35    IF   E           K DISK    USROPN
N02  F                                     RENAME(HEADFR:HEADFR35)
     FBILLOFLG  IF   E           K DISK    USROPN
     F                                     RENAME(BILLOFR:BILLOFRG)
     FBLJOINLF  IF   E           K DISK    USROPN
     FFRISOL01  IF   E           K DISK    USROPN
     FFRISOL03  IF   E           K DISK    USROPN
     F                                     RENAME(FRISOKR:FRISOKR3)
     FFRISOL04  IF   E           K DISK    USROPN
     F                                     RENAME(FRISOKR:FRISOKR4)
     FDOKUFM1   IF   E           K DISK    USROPN
     FBRIDF     IF   E           K DISK    USROPN
     FCUNDL01   IF   E           K DISK    USROPN
     FGSSCGL02  IF   E           K DISK    USROPN
     FFREETXT2  IF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy SYSPEDS/qrpgsrcpy,prototypeb
      /copy SYSPEDS/qrpgsrcpy,usec
      /copy SYSPEDS/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
      * Variables received from the input string
     D WSUSER          S             10
     D WSNA            S             30
     D WSKN            S              8
S04  D*KN              S              8  0 DIM(30)
N04  D KN              S              8  0 DIM(100)
     D WSAVD           S              4
     D WSOPD           S              7
     D WSGN            S             15
     D WSAWBN          S             15
     D WSFN            S              7
     D WSDTF           S              8
     D WSDTT           S              8
     D WSDTTN          S              8  0
     D WSKNA           S              8
     D WSDTKNA         S              9
     D WSRFA           S             35
N02  D WSRFK           S             35
     D WSPRO           S              8
     D WSHAWB          S              9
     D WSDTHAWB        S              8
     D WSOT            S              2
     D WSDTOT          S              8
     D WSCONT          S             17
     D WSFRI1          S              3
     D WSFRI2          S             35
     D WSDTFS          S              8
     D WSDTFST         S              8
     D WSSAML          S              6
     D WSMRK1          S             35
     D LK_SDF          S              8
     D LK_SDT          S              8
     D WSBLNR          S             16
     D WSBLCN          S             17
     D lstbrd          s              1
     D GrpNr           s              8
     D VALG01          S             50
     D VALG02          S             50
     D VALG03          S             50
     D VALG04          S             50
     D VALG05          S             50
     D VALG06          S             50
     D VALG07          S             50
     D VALG08          S             50
     D VALG09          S             50
     D VALG10          S             50
     D VALG11          S             50
     D VALG12          S             50
     D VALG13          S             50
     D VALG14          S             50
     D VALG15          S             50
     D VALG16          S             50
     D VALG99          S             50
     D ANTSND          s              5  0
     D MaxSN           s              5
     D MaxSND          s              5  0
     D ANTRCD          s              5  0
     D MaxRC           s              5
     D MaxRCD          s              5  0
N01  D FakSum          s             13  2
     D Error           S            150
     D JSONstring      s          32000
     D                 DS
     D  BLMN                   1     17
     D  BVMN                   1     17
     D                 DS
     D  BLAVD                  1      4  0
     D  BVAVD                  1      4  0
     D                 DS
     D  BLOPD                  1      7  0
     D  BVOPD                  1      7  0
     D                 DS
     D  HEAVD                  1      4S 0
     D  HEAVDA                 1      4
     D                 DS
     D  HEOPD                  1      7S 0
     D  HEOPDA                 1      7
      * Variables received from the input string
      * and converted to numeric
     D zeros           s             15a   inz('000000000000000')
     D NIER            s             35A   INZ('9999999999999999999999999999999-
     D                                     9999')
     D l1              s              3s 0
     D l2              s              3s 0
      * Number of records matching search criteria
     DRecordCnt        s              5S 0
      *
     D Lib             s             10
     D Fn              s             10
     D Mbr             s             10

      *get input-string
      /copy syspeds/qrpgsrcpy,prolog3
      * Parse input
     C                   exsr      Parse
      * Open files etc
     C                   EXSR      INIT

      *
     c                   callp     gethtmlifs(
     C                             '/syspeddev/html/JSON.html':
     C                             '/CGITAG/')
     C                   callp     wrtsection('top')
      *
      * ............................................................................................
      * skriv JSON-Object start..(alltid '{' + x ant param. m/komma mellom (IKKE komma etter siste))
      * ............................................................................................
      *
      /free
        JSONstring='{'
        +'¬user¬ : ¬'+%trim(WSUSER)+'¬, '
        +'¬wsna¬ : ¬'+%trim(WSNA)+'¬, '
        +'¬knavn¬ : ¬'+%trim(KNAVN)+'¬, '
        +'¬kunde¬ : ¬'+%trim(%editc(BIKUNR:'Z'))+'¬, '
N01     +'¬visFaktSum¬ : ¬'+%trim(XvisFaktSum)+'¬, '
N06     +'¬custFld1Name¬ : ¬Test1¬, '
N06     +'¬custFld2Name¬ : ¬Test2¬, '
N06     +'¬custFld3Name¬ : ¬¬, '
N06     +'¬custFld4Name¬ : ¬¬, '
N06     +'¬custFld5Name¬ : ¬¬, '
        +'¬errMsg¬ : ¬'+%trim(Error)+'¬, ';
      /end-free
      * JSONfix escaper " i tekst,  for deretter å bytte ¬->"
     c                   call      'JSONFIX'
     c                   parm                    JSONstring
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      *
      * ............................................................................................
      * Skriv JSON-LISTE Start (en liste er EN parameter(fra [ til   )
      * ............................................................................................
     c                   eval      JSONstring ='"qryoppdrag" : ['
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      *
      * Søk oppdrag
      *
     C                   EXSR      HENTOPD

     C     SLUTT         TAG
     C                   EXSR      EXIT
      *=====================================================================******
      * Parse input
      *=====================================================================******
     C     PARSE         BEGSR
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   EVAL      WSUSER  = zhbgetvar('USER')
     C                   EVAL      WSNA    = zhbgetvar('KNAVN')
     C                   EVAL      WSKN    = zhbgetvar('WSKN')
     C                   EVAL      WSAVD   = zhbgetvar('WSAVD')
     C                   EVAL      WSOPD   = zhbgetvar('WSOPD')
     C                   EVAL      WSPRO   = zhbgetvar('WSPRO')
     C                   EVAL      WSFN    = zhbgetvar('WSFN')
     C                   EVAL      WSGN    = zhbgetvar('WSGN')
     C                   eval      WSGN    = uppify(WSGN)
     C                   EVAL      WSAWBN  = zhbgetvar('WSAWBN')
     C                   eval      WSAWBN  = uppify(WSAWBN)
     C                   eval      WSAWBN  = '    ' + %TRIM(WSAWBN)
     C                   EVAL      WSDTF   = zhbgetvar('WSDTF')
     C                   EVAL      WSKNA   = zhbgetvar('WSKNA')
     C                   EVAL      WSDTKNA = zhbgetvar('WSDTKNA')
     C                   EVAL      WSRFA   = zhbgetvar('WSRFA')
     C                   eval      WSRFA   = uppify(WSRFA)
N02  C                   EVAL      WSRFK   = zhbgetvar('WSRFK')
N02  C                   eval      WSRFK   = uppify(WSRFK)
     C                   EVAL      WSPRO   = zhbgetvar('WSPRO')
     C                   EVAL      WSHAWB  = zhbgetvar('WSHAWB')
     C                   eval      WSHAWB  = uppify(WSHAWB)
     C                   EVAL      WSDTHAWB= zhbgetvar('WSDTHAWB')
     C                   IF        WSHAWB <> *BLANKS
     C                   EVAL      WSDTF   = *BLANKS
     C                   END
     C                   EVAL      WSOT    = zhbgetvar('WSOT')
     C                   EVAL      WSDTOT  = zhbgetvar('WSDTOT')
     C                   IF        ((WSOT <> *BLANKS) AND (WSDTOT = *BLANKS))
     C                   EVAL      WSDTOT = WDT
     C                   END
     C                   IF        WSOT   <> *BLANKS
     C                   EVAL      WSDTF   = *BLANKS
     C                   END
     C                   EVAL      WSCONT  = zhbgetvar('WSCONT')
     C                   eval      WSCONT  = uppify(WSCONT)
     C                   EVAL      WSFRI1  = zhbgetvar('FSCD')
     C                   IF        WSFRI2 <> *BLANKS
     C                   EVAL      WSDTF   = *BLANKS
     C                   END
     C                   EVAL      WSFRI2  = zhbgetvar('WSFRI2')
     C                   eval      WSFRI2  = uppify(WSFRI2)
     C                   IF        WSFRI1  = 'IFB'
     C                   MOVEL     WSFRI2        XAN03             3
     C                   IF        XAN03   = '401'
     C                   MOVE      WSFRI2        XAN32            32
     C                   EVAL      WSFRI2  = XAN32
     C                   END
     C                   END
     C                   EVAL      WSDTFS  = zhbgetvar('WSDTFS')
     C                   EVAL      WSDTFST = zhbgetvar('WSDTFST')
     C                   EVAL      WSSAML  = zhbgetvar('WSSAML')
     C                   eval      WSSAML  = uppify(WSSAML)
     C                   EVAL      WSMRK1  = zhbgetvar('WSMRK1')
     C                   eval      WSMRK1  = uppify(WSMRK1)
      * 20 lang med foranstilt 00 =EAN/GS1, stripp bort AI=00 (lagres ikke i DOKUFM)
     c                   if        %subst(WSMRK1:1:2)='00' and
     c                             %subst(WSMRK1:20:1)<>' ' and
     c                             %subst(WSMRK1:21:1)=' '
     C                   eval      WSMRK1  = %subst(WSMRK1:3:18)
     c                   endif
     C                   EVAL      WSBLNR  = zhbgetvar('WSBLNR')
     C                   eval      WSBLNR  = uppify(WSBLNR)
     C                   EVAL      WSBLCN  = zhbgetvar('WSBLCN')
     C                   eval      WSBLCN  = uppify(WSBLCN)
     C                   MOVE      *ZEROS        XSKN              8 0
     C                   EVAL      XSKN  = c2n2(WSKN)
     C                   MOVE      *ZEROS        XSAVD             4 0
     C                   EVAL      XSAVD = c2n2(WSAVD)
     C                   MOVE      *ZEROS        XSOPD             7 0
     C                   EVAL      XSOPD = c2n2(WSOPD)
     C                   MOVE      *ZEROS        XSPRO             8 0
     C                   EVAL      XSPRO = c2n2(WSPRO)
     C                   MOVE      *ZEROS        XSFN              7 0
     C                   EVAL      XSFN = c2n2(WSFN)
     C                   EVAL      WSGNMAX = WSGN
     C                   CAT       NIER:0        WSGNMAX          15
     C                   EVAL      WSAWBNMAX = WSAWBN
     C                   CAT       NIER:0        WSAWBNMAX        15
     C                   MOVE      *ZEROS        XSDTF             8 0
     C                   EVAL      XSDTF  = c2n2(WSDTF)
     C                   EVAL      XSDTFMAX = XSDTF
     C                   ADD       10000         XSDTFMAX          8 0
     C                   EVAL      WSDTT   = zhbgetvar('WSDTT')
     C                   eval      wsdttn  = c2n2(WSDTT)
     C     wsdttn        IFNE      *zeros
     C                   EVAL      XSDTFMAX = wsdttn
     c                   end
     C                   MOVE      *ZEROS        XSKNA             8 0
     C                   EVAL      XSKNA = c2n2(WSKNA)
     C                   MOVE      *ZEROS        XSDTKNA           8 0
     C                   EVAL      XSDTKNA  = c2n2(WSDTKNA)
     C                   EVAL      WSRFAMAX = WSRFA
     C                   CAT       NIER:0        WSRFAMAX         35
N02  C                   EVAL      WSRFKMAX = WSRFK
N02  C                   CAT       NIER:0        WSRFKMAX         35
     C                   MOVE      *ZEROS        XSPRO             8 0
     C                   EVAL      XSPRO = c2n2(WSPRO)
     C                   EVAL      WSHAWBMAX = WSHAWB
     C                   CAT       NIER:0        WSHAWBMAX         9
     C                   MOVE      *ZEROS        XSDTHAWB          8 0
     C                   EVAL      XSDTHAWB = c2n2(WSDTHAWB)
     C                   MOVE      *ZEROS        XSDTOT            8 0
     C                   EVAL      XSDTOT   = c2n2(WSDTOT)
     C                   EVAL      WSCONTMAX = WSCONT
     C                   CAT       NIER:0        WSCONTMAX        17
     C                   EVAL      WSFRI2MAX = WSFRI2
     C                   CAT       NIER:0        WSFRI2MAX        35
     C                   MOVE      *ZEROS        XSDTFS            8 0
     C                   EVAL      XSDTFS = c2n2(WSDTFS)
     C                   MOVE      *ZEROS        XSDTFST           8 0
     C                   EVAL      XSDTFST = c2n2(WSDTFST)
     C                   EVAL      WSMRK1MAX = WSMRK1
     C                   CAT       NIER:0        WSMRK1MAX        35
     C                   EVAL      WSBLNRMAX = WSBLNR
     C                   CAT       NIER:0        WSBLNRMAX        16
     C                   EVAL      WSBLCNMAX = WSBLCN
     C                   CAT       NIER:0        WSBLCNMAX        17

     C                   endsr
      *=====================================================================******
      * HENT OPPDRAG
      *=====================================================================******
     C     HENTOPD       BEGSR

     C                   Move      *zeros        AntSnd
     C                   Move      *zeros        AntRcd
     C     MaxSnd        IFEQ      *ZEROS
     C                   Z-add     1000          MaxSnd
     C                   END
     C     MaxRcd        IFEQ      *ZEROS
     C                   Z-add     50000         MaxRcd
     C                   END

     C                   SELECT
     C                   WHEN      XSAVD <> 0
     C     HEADFK        SETLL     HEADF
     C                   WHEN      XSFN <> 0
     C     XSFN          SETLL     FAIN
     C                   WHEN      WSGN <> *BLANKS
     C     WSGN          SETLL     HEAD18
     C                   WHEN      WSAWBN <> *BLANKS
     C     WSAWBN        SETLL     HEAD18
     C                   WHEN      XSKNA <> 0
     C     HEAD29K       SETLL     HEAD29
     C                   WHEN      WSRFA <> *BLANKS
     C     WSRFA         SETLL     HEAD17
N02  C                   WHEN      WSRFK <> *BLANKS
N02  C     WSRFK         SETLL     HEAD35
     C                   WHEN      XSPRO <> 0
     C     XSPRO         SETLL     HEAD14
     C                   WHEN      WSHAWB <> *BLANKS
     C     HEAD31K       SETLL     HEAD31
     C                   WHEN      WSOT   <> *BLANKS
     C     HEAD30K       SETLL     HEAD30
     C                   WHEN      WSCONT <> *BLANKS
     C     WSCONT        SETLL     HEAD32
     C                   WHEN      WSFRI1 <> *BLANKS
     C     FRISOL01K     SETLL     FRISOL01
     C                   WHEN      WSFRI2 <> *BLANKS
     C     WSFRI2        SETLL     FRISOL04
     C                   WHEN      WSSAML <> *BLANKS
     C     WSSAML        SETLL     HEAD19
     C                   WHEN      WSMRK1 <> *BLANKS
     C     WSMRK1        SETLL     DOKUFM1
     C                   WHEN      WSBLNR <> *BLANKS
     C     WSBLNR        SETLL     BILLOFLG
     C                   WHEN      WSBLCN <> *BLANKS
     C     WSBLCN        SETLL     BLJOINLF
     C                   WHEN      XSDTF <> 0
     C     XSDTF         SETLL     HEADDT
     C                   ENDSL

     C     STHENTOPD     TAG
     C                   SELECT
     C                   WHEN      XSAVD <> 0
     C     HEADFK        READE     HEADF                                  50
     C                   WHEN      XSFN <> 0
     C     XSFN          READE     FAIN                                   50
     C  N50              IF        WSFRI1 <> *BLANKS
     C     FRISOL03K     CHAIN     FRISOL03                           33
     C   33              GOTO      STHENTOPD
     C                   END
     C  N50HEADFK2       CHAIN     HEADF                              50
     C                   WHEN      WSGN <> *BLANKS
     C                   READ      HEAD18                                 50
     C  N50WSGNMAX       COMP      HEGN                                 50
     C                   WHEN      WSAWBN <> *BLANKS
     C                   READ      HEAD18                                 50
     C  N50WSAWBNMAX     COMP      HEGN                                 50
     C                   WHEN      XSKNA <> 0
     C     XSKNA         READE     HEAD29                                 50
     C                   WHEN      WSRFA <> *BLANKS
     C                   READ      HEAD17                                 50
     C  N50WSRFAMAX      COMP      HERFA                                50
N02  C                   WHEN      WSRFK <> *BLANKS
N02  C                   READ      HEAD35                                 50
N02  C  N50WSRFKMAX      COMP      HERFK                                50
     C                   WHEN      XSPRO <> 0
     C     XSPRO         READE     HEAD14                                 50
     C                   WHEN      WSHAWB <> *BLANKS
     C                   READ      HEAD31                                 50
     C  N50WSHAWBMAX     COMP      HEHAWB                               50
     C                   WHEN      WSOT   <> *BLANKS
     C     WSOT          READE     HEAD30                                 50
     C                   WHEN      WSCONT <> *BLANKS
     C                   READ      HEAD32                                 50
     C  N50WSCONTMAX     COMP      HETRCN                               50
     C                   WHEN      WSFRI1 <> *BLANKS
     C     WSFRI1        READE     FRISOL01                               50
     C   50              GOTO      SLHENTOPD
     C     WSFRI2MAX     CABLT     FSSOK         SLHENTOPD
     C     HEADFK3       CHAIN     HEADF                              50
     C   50              GOTO      STHENTOPD
     C     XSDTFS        CABGT     HEDTOP        STHENTOPD
     C                   IF        XSDTFST <> 0
     C     XSDTFST       CABLT     HEDTOP        STHENTOPD
     C                   END
     C                   WHEN      WSFRI2 <> *BLANKS
     C                   READ      FRISOL04                               50
     C   50              GOTO      SLHENTOPD
     C     WSFRI2MAX     CABLT     FSSOK         SLHENTOPD
     C     HEADFK3       CHAIN     HEADF                              50
     C   50              GOTO      STHENTOPD
     C     XSDTFS        CABGT     HEDTOP        STHENTOPD
     C                   IF        XSDTFST <> 0
     C     XSDTFST       CABLT     HEDTOP        STHENTOPD
     C                   END
     C                   WHEN      WSSAML <> *BLANKS
     C     WSSAML        READE     HEAD19                                 50
     C                   WHEN      WSMRK1 <> *BLANKS
     C                   READ      DOKUFM1                                50
     C  N50WSMRK1MAX     COMP      FMMRK1                               50
     C  N50HEADFK4       CHAIN     HEADF                              50
     C                   WHEN      WSBLNR <> *BLANKS
     C                   READ      BILLOFLG                               50
     C  N50WSBLNRMAX     COMP      BLBLNR                               50
     C                   IF        *IN50 = *OFF
     C     HEADFKBL      CHAIN     HEADF                              33
     C     *IN33         CABEQ     *ON           STHENTOPD
     C                   END
     C                   WHEN      WSBLCN <> *BLANKS
     C                   READ      BLJOINLF                               50
     C  N50WSBLCNMAX     COMP      BLMN                                 50
     C                   IF        *IN50 = *OFF
     C     HEADFKBL      CHAIN     HEADF                              33
     C     *IN33         CABEQ     *ON           STHENTOPD
     C                   END
     C                   WHEN      XSDTF <> 0
     C                   READ      HEADDT                                 50
     C  N50XSDTFMAX      COMP      HEDTOP                               50
     C                   OTHER
     C                   MOVE      '1'           *IN50
     C                   ENDSL

     C                   IF        *IN50 = *OFF
     C                   Add       1             AntRcd
     C     AntRcd        CabGe     MaxRcd        SLHENTOPD
     C     HEST          CABEQ     'S'           STHENTOPD
     C     TRSLAG        CABEQ     'FF'          STHENTOPD
     C     TRSLAG        CABEQ     'VF'          STHENTOPD
     C                   IF        XINTERN <> 'J'
     C                   MOVE      'N'           XOK               1
     C                   IF        XKUNEGEN = 'J'
     C                   IF        HEKNSF = BIKUNR OR HEKNKF = BIKUNR
     C                   MOVE      'J'           XOK
     C                   END
     C                   ELSE
s04  C*    1             DO        30            XN2
n04  C     1             DO        100           XN2
     C                   IF        KN(XN2) = 0
     C                   LEAVE
     C                   END
     C     HEKNA         IFNE      KN(XN2)
     C     TRKNFA        ANDNE     KN(XN2)
     C     HEKNS         ANDNE     KN(XN2)
     C     HEKNSF        ANDNE     KN(XN2)
     C     HEKNK         ANDNE     KN(XN2)
     C     HEKNKF        ANDNE     KN(XN2)
     C                   ELSE
     C                   MOVE      'J'           XOK
     C                   LEAVE
     C                   END
     C                   ENDDO
     C                   END
     C     XOK           CABEQ     'N'           STHENTOPD
     C                   END
     C                   END
      * Post er lest som skal medtas..
     C                   IF        *IN50 = *OFF
     C                   Add       1             AntSnd
     C     AntSnd        CabGe     MaxSnd        SLHENTOPD
     C                   EXSR      SKRIV
     C                   GOTO      STHENTOPD
     C                   END

     C     SLHENTOPD     TAG
      * ............................................................................................
      * Skriv JSON-Liste/Array  Avslutning
      * ............................................................................................
     c                   eval      JSONstring =']'
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      * ............................................................................................
      * Skriv JSON-string Avsluttning
      * ............................................................................................
     c                   eval      JSONstring ='}'
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
     C                   ENDSR

      *=====================================================================******
      *  Skriv Json-strenger
      *=====================================================================******
     C     SKRIV         BEGSR

     C     FREETXT2K     SETLL     FREETXT2
     C     FREETXT2K     READE     FREETXT2                               33
     C                   IF        *IN33 = *OFF
     C                   callp     updHTMLvar('COLOR':'<font COLOR=RED>')
     C                   END

     c                   eval      LK_SDF = HESDF
     c                   eval      LK_SDT = HESDT
     c     HEUR          IFEQ      'H'
     c                   eval      LK_SDF = HELKA + ' ' + HESDF
     c                   eval      LK_SDT = HETRI + ' ' + HESDT
     c                   ENDIF

     C                   EVAL      XWSREF=*BLANKS
     C                   SELECT
     C                   WHEN      WSFN <> *BLANKS
     C                   MOVE      FIFN          XFIFN             7
     C                   CAT       XFIFN:0       XWSREF           48
     C                   WHEN      WSRFA <> *BLANKS
     C                   CAT       HERFA:0       XWSREF
N02  C                   WHEN      WSRFK <> *BLANKS
N02  C                   CAT       HERFK:0       XWSREF
     C                   WHEN      WSHAWB <> *BLANKS
     C                   CAT       HEHAWB:0      XWSREF
     C                   WHEN      WSOT   <> *BLANKS
     C                   CAT       HEOT:0        XWSREF
     C                   WHEN      WSCONT <> *BLANKS
     C                   CAT       HETRCN:0      XWSREF
     C                   WHEN      WSFRI1 <> *BLANKS
     C                   CAT       FSSOK:0       XWSREF
     C                   WHEN      WSFRI2 <> *BLANKS
     C                   CAT       FSSOK:0       XWSREF
     C                   WHEN      WSMRK1 <> *BLANKS
     C                   CAT       FMMRK1:0      XWSREF
     C                   OTHER
     C                   CAT       HERFA:0       XWSREF
     C                   ENDSL

N03  C                   MOVE      *zeros        PODDATO           8 0
     c     HEDTMO        IFNE      *ZEROS
     c     HEDTMO        ANDNE     *BLANKS
     C                   MOVE      HEDTMO        PODDATO           8 0
N07  c                   ELSE
N07  C                   CALL      'SYGE46'
N07  C                   PARM      HEAVD         xxAVD             4 0
N07  C                   PARM      HEOPD         xxOPD             7 0
N07  C                   PARM      *zeros        xxFBNR            3 0
N07  C                   PARM      'POD'         xxACTI            3
N07  C                   PARM      'F'           Type              1
N07  C                   PARM      *zeros        xxDATE            8 0
N07  C                   PARM      *zeros        xxTIME            6 0
N07  C                   PARM      *zeros        antall            5 0
N07  c                   if        xxDate <> *zeros
N07  C                   MOVE      xxDate        PODDATO
N07  c                   endif
     c                   ENDIF
      *
     C                   SELECT
     C                   WHEN      WSBLNR <> *BLANKS
     C                   MOVEL     BLBLNR        XHEKDPL          17
     C                   WHEN      WSBLCN <> *BLANKS
     C                   MOVEL     BLMN          XHEKDPL
     C                   OTHER
     C                   MOVEL     HEKDPL        XHEKDPL
     C                   ENDSL
N01  C                   move      *zeros        FakSum
N01  C                   IF        XvisFaktSum = 'J'
N01  c     FakKey        setll     FAKT
N01  c     FakKey        reade     FAKT                                   33
N01  c     *IN33         doweq     '0'
N01  c                   if        FAKDA<>'K' and FAKDA<>'K' and FAOPKO<>'D'
N01  c                             and FAKUNR = BIKUNR
N01  c                   eval      FakSum = Faksum + FABELN
N01  c                   endif
N01  c     FakKey        reade     FAKT                                   33
N01  C                   enddo
N01  C                   ENDIF

      /free
       if AntSnd=1;
          JSONstring=*blanks;
          else;
          JSONstring=', ';
          endif;
       JSONstring=%trim(JSONstring)+'{'
          +'¬heavd¬ : ¬'+%trim(HEAVDA)+'¬, '
          +'¬heopd¬ : ¬'+%trim(HEOPDA)+'¬, '
          +'¬hedtop¬ : ¬'+%trim(%editc(HEDTOP:'Z'))+'¬, '
          +'¬hegn¬ : ¬'+%trim(HEGN)+'¬, '
          +'¬henas¬ : ¬'+%trim(HENAS)+'¬, '
          +'¬henak¬ : ¬'+%trim(HENAK)+'¬, '
          +'¬hefr¬ : ¬'+%trim(HEFR)+'¬, '
          +'¬hent¬ : ¬'+%trim(%editc(HENT:'Z'))+'¬, '
          +'¬hevkt¬ : ¬'+%trim(%editc(HEVKT:'Z'))+'¬, '
          +'¬hem3¬ : ¬'+%trim(%editc(HEM3:'4'))+'¬, '
          +'¬helm¬ : ¬'+%trim(%editc(HELM:'4'))+'¬, '
          +'¬hesdf¬ : ¬'+%trim(LK_SDF)+'¬, '
          +'¬hesdt¬ : ¬'+%trim(LK_SDT)+'¬, '
          +'¬hepro¬ : ¬'+%trim(%editc(HEPRO:'Z'))+'¬, '
          +'¬xwsref¬ : ¬'+%trim(XWSREF)+'¬, '
          +'¬poddato¬ : ¬'+%trim(%editc(PODDATO:'Z'))+'¬, '
N01       +'¬faktsum¬ : ¬'+%trim(%editc(FAKSUM:'M'))+'¬, '
N06       +'¬custFld1Value¬ : ¬¬, '
N06       +'¬custFld2Value¬ : ¬¬, '
N06       +'¬custFld3Value¬ : ¬¬, '
N06       +'¬custFld4Value¬ : ¬¬, '
N06       +'¬custFld5Value¬ : ¬¬, '
          +'¬hekdpl¬ : ¬'+%trim(XHEKDPL)+'¬}';
      /end-free
     C                   call      'JSONFIX'
     C                   parm                    JSONstring
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')

     C                   endsr
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
     C                   OPEN      BRIDF
     C     WSUSER        CHAIN     BRIDF                              50
      * En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
      * Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
     C   50              eval      Error = 'Invalid userID'
OddEv * hent Parametre som går igjen i mange prog:
OddEv *      Odd/Even/Rowhead-farger,Logobilde,Språk,Intern/Ekstern bruker,  mm
OddEvc                   call      'ESGETPAR'
OddEvc                   parm                    BIBRID
OddEvc                   parm                    BIFIRM
OddEvc                   parm                    BIKUNR
OddEvc                   parm                    BIKNG
OddEvc                   parm                    RowHead          10
OddEvc                   parm                    Odd              10
OddEvc                   parm                    Even             10
OddEvc                   parm                    LogoImg          50
OddEvc                   parm                    XSPRÅK            2
OddEvc                   parm                    XINTERN           1
OddEvc                   parm                    ParmDS1        1024
OddEvc                   parm                    ParmDS2        1024
OddEvc                   parm                    ParmDS3        1024
     C                   open      FAIN                                 50
N01  C                   open      FAKT                                 50
     C                   open      HEADF                                50
     C                   open      HEADDT                               50
     C                   open      HEAD14                               50
     C                   open      HEAD17                               50
     C                   open      HEAD18                               50
     C                   open      HEAD19                               50
     C                   open      HEAD29                               50
     C                   open      HEAD30                               50
     C                   open      HEAD31                               50
     C                   open      HEAD32                               50
N02  C                   open      HEAD35                               50
     C                   open      BILLOFLG                             50
     C                   open      BLJOINLF                             50
     C                   open      FRISOL01                             50
     C                   open      FRISOL03                             50
     C                   open      FRISOL04                             50
     C                   open      DOKUFM1                              50
     C                   open      GSSCGL02                             50
     C                   open      FREETXT2                             50
     C                   open      BRPARF                               50
     C                   open      CUNDL01                              50

     C     KunKey        klist
     C                   kfld                    BIFIRM
     C                   kfld                    BIKUNR
     C     HEADFK        klist
     C                   kfld                    XSAVD
     C                   kfld                    XSOPD
     C     HEADFK2       klist
     C                   kfld                    FIAVD
     C                   kfld                    FIOPD
     C     HEADFK3       klist
     C                   kfld                    FSAVD
     C                   kfld                    FSOPD
     C     HEADFK4       klist
     C                   kfld                    FMAVD
     C                   kfld                    FMOPD
     C     HEADFKBL      klist
     C                   kfld                    BLAVD
     C                   kfld                    BLOPD
     C     HEAD29K       klist
     C                   kfld                    XSKNA
     C                   kfld                    XSDTKNA
     C     HEAD31K       klist
     C                   kfld                    WSHAWB
     C                   kfld                    XSDTHAWB
     C     HEAD30K       klist
     C                   kfld                    WSOT
     C                   kfld                    XSDTOT
     C     FREETXT2K     klist
     C                   kfld                    HEAVD
     C                   kfld                    HEOPD
     C                   kfld                    FRTKOD
     C                   MOVE      '1'           FRTKOD
N01  C     FAKKEY        klist
N01  C                   kfld                    HEAVD
N01  C                   kfld                    HEOPD
     C     FRISOL01K     klist
     C                   kfld                    WSFRI1
     C                   kfld                    WSFRI2
     C                   kfld                    XSDTFS
     C     FRISOL03K     KLIST
     C                   KFLD                    FIAVD
     C                   KFLD                    FIOPD
     C                   KFLD                    WSFRI2
     C                   KFLD                    WSFRI1

     C                   MOVE      *ZEROS        FIFN

     C     BRPARFK       klist
S05  C*                  kfld                    PABRID
N05  C                   kfld                    WSUSER
     C                   kfld                    PAKUNR
     C                   kfld                    PAID

S05  C*                  EVAL      PABRID = WSUSER

N05  C     FIPARFK       klist
N05  C                   kfld                    FIUSER           10
N05  C                   kfld                    PAKUNR
N05  C                   kfld                    PAID
N05  c                   Eval      FIUSER = '*KUNDFT*'+BIFIRM

     C                   MOVEL     'SPRÅK'       PAID
     C     BRPARFK       chain     BRPARF                             33
     C                   IF        (*IN33 OR (PAVRDA <> 'EN'))
     C                   MOVE      *BLANKS       XSPRÅK            2
     C                   ELSE
     C                   MOVE      'EN'          XSPRÅK            2
     C                   END
N01   * vis fakturasum?
N05   * basis er Firmanivå J el N
N01  C                   MOVEL     'SGFSU'       PAID
S05  C*    BRPARFK       chain     BRPARF                             33
N05  C     FIPARFK       chain     BRPARF                             33
N01  C                   IF        (*IN33 OR
N01  C                             ((PAVRDA <> 'j') AND
N01  C                              (PAVRDA <> 'J')))
N01  C                   MOVE      'N'           XVisFaktSum       1
N01  C                   ELSE
N01  C                   MOVE      'J'           XVisFaktSum
N01  C                   END

N05   * brukernivå overstyurer firma (i begge retninger)
N05  C     BRPARFK       chain     BRPARF
N05  C                   IF        %found
N05  C                   MOVE      'N'           XVisFaktSum       1
N05  C                   IF        PAVRDA = 'j' OR
N05  C                             PAVRDA = 'J'
N05  C                   MOVE      'J'           XVisFaktSum
N05  C                   END
N05  C                   END

     C                   MOVEL     'PODMR'       PAID
     C     BRPARFK       chain     BRPARF                             33
     C                   IF        (*IN33 OR
     C                             ((PAVRDA <> 'j') AND
     C                              (PAVRDA <> 'J')))
     C                   MOVE      'N'           XPODMR            1
     C                   ELSE
     C                   MOVE      'J'           XPODMR            1
     C                   END

     C                   MOVEL     'HEKUN'       PAID
     C     BRPARFK       chain     BRPARF                             33
     C                   IF        (*IN33 OR
     C                             ((PAVRDA <> 'j') AND
     C                              (PAVRDA <> 'J')))
     C                   MOVE      'N'           XKUNEGEN          1
     C                   ELSE
     C                   MOVE      'J'           XKUNEGEN
     C                   END

     C                   MOVE      *ZEROS        KN
     C                   MOVE      *ZEROS        XN2               3 0
     C                   IF        XSKN = 0
     C                   MOVEL     'KUNGR'       PAID
     C     BRPARFK       chain     BRPARF                             33
     C                   IF        (*IN33 OR (PAVRDA = *BLANKS))
     C                   MOVE      BIKUNR        KN(1)
     C                   ELSE
     C                   MOVEL     PAVRDA        CGCG
     C     CGCG          SETLL     GSSCGL02
     C     CGCG          READE     GSSCGL02                               33
     C                   DOW       *IN33 = *OFF
     C                   ADD       1             XN2
     C                   MOVE      CGCNO         KN(XN2)
     C     CGCG          READE     GSSCGL02                               33
     C                   ENDDO
     C                   END
     C                   ELSE
     C                   MOVE      XSKN          KN(1)
     C                   END
     c     KunKey        chain     cundl01                            33

     C                   endsr
      *=====================================================================******
      *  AVSLUTT
      *=====================================================================******
     C     EXIT          begsr
     C                   close     BRPARF
     C                   close     BRIDF
     C                   close     FAIN
n01  C                   close     FAKT
     C                   close     HEADF
     C                   close     HEADDT
     C                   close     HEAD14
     C                   close     HEAD17
     C                   close     HEAD18
     C                   close     HEAD19
     C                   close     HEAD29
     C                   close     HEAD30
     C                   close     HEAD31
     C                   close     HEAD32
N02  C                   close     HEAD35
     C                   close     BILLOFLG
     C                   close     BLJOINLF
     C                   close     FRISOL01
     C                   close     FRISOL03
     C                   close     FRISOL04
     C                   close     DOKUFM1
     C                   close     GSSCGL02
     C                   close     FREETXT2
     C                   close     CUNDL01
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   MOVE      *ON           *INLR
     C                   return
     C                   endsr
