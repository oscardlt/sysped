     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPDOC('SYFA5201' 'SYFA52' 'SY400LO-
     A                                      /MAINT')
     A                                      HELP
      *
     A          R SUBFIL                    SFL
     A            ARSTAT         1A  H
     A            ARLINK        40A  H
     A            WSNR           1A  B  6  3VALUES(' ' '2' '4')
     A            ARTYPE         2A  O  6  7
     A            ARTXT         15   O  6 10
     A            ARAVD          4S 0O  6 26
     A            AROPD          7S 0O  6 31
E02  A            ARUNDE        10A  O  6 39
N01  A  77                                  DSPATR(HI)
E02  A            ARUSER        10A  O  6 50
E02  A            ARDATE         8S 0O  6 61
E02  A            ARTIME         6S 0O  6 70
     A            ARKUND         8  0O  7  2
     A            KNAVN         30   O  7 11
     A            ARRFK         35   O  7 42COLOR(WHT)
     A            WSLINW        76A  O  8  3DSPATR(UL)
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0005)
     A                                      SFLPAG(0004)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A            ZRECNO         4S 0O  3 75SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYSPED/ARC019'
     A                                      COLOR(BLU)
     A                                  1 17MSGCON(050 SHU1016 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  2 70TIME
     A                                      COLOR(BLU)
     A                                  2  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3  2MSGCON(030 DIV0017 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(077 SHU1017 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  5  2MSGCON(060 SHU1018 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
      *
     A          R BUNN
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF03(03 'EXIT')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  3  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 42MSGCON(018 DIV0022 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 62MSGCON(018 DIV0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  98        MSG205        78A  O  4  2MSGID(DIV 0012 SYMSGF)
     A                                      COLOR(WHT)
     A N98                              4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
      *
     A          R VARELI
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF12(12 'Forrige')
     A                                      SLNO(20)
     A                                      CLRL(05)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2MSGCON(077 SHU1017 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  2  2MSGCON(060 SHU1018 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            WSTYPE         2A  B  3  7
     A  71                                  DSPATR(RI)
     A            ARTXT         15A  O  3 10
     A            WSAVD          4Y 0B  3 26EDTCDE(4)
     A  70                                  DSPATR(RI)
     A            WSOPD          7Y 0B  3 31EDTCDE(4)
     A  70                                  DSPATR(RI)
     A            WSUNDE        10A  B  3 39
     A            ARUSER        10A  O  3 50
     A            ARDATE         8S 0O  3 61
     A            ARTIME         6S 0O  3 70
     A            WSKUND         8Y 0B  4  2EDTCDE(4)
     A  72                                  DSPATR(RI)
     A            KNAVN         30A  O  4 11
     A            WSRFK         35A  B  4 42CHECK(LC)
     A  30        MSGTXT        78A  O  5  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A N30                              5  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
      *
     A          R VAREL2
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            UUSTAT         1A  B  2  2
     A            W2AVD          4S 0I  2  4
     A            W2OPD          7S 0I  2 10
     A            W2UNDE        10A  B  2 20
     A                                  2 32MSGCON(045 SHU1019 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  3  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 32'SS:(SHIPS)L1:(LØSNING1) T:(TUR) K:-
     A                                      (KUNDE)'
     A                                  4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
      *
     A          R VAREL3
     A                                      CF12(12 'Forrige')
     A                                      SLNO(20)
     A                                      CLRL(05)
     A                                      OVERLAY
     A                                  1  2MSGCON(077 SHU1017 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  2  7MSGCON(060 SHU1018 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            ARTYPE         2A  O  3  7COLOR(WHT)
     A            ARTXT         15A  O  3 10COLOR(WHT)
     A            ARAVD          4Y 0O  3 26COLOR(WHT)
     A            AROPD          7Y 0O  3 31COLOR(WHT)
     A            ARUNDE        10A  O  3 39COLOR(WHT)
     A            ARUSER        10A  O  3 50COLOR(WHT)
     A            ARDATE         8S 0O  3 61COLOR(WHT)
     A            ARTIME         6S 0O  3 70COLOR(WHT)
     A            ARKUND         8Y 0O  4  7COLOR(WHT)
     A            KNAVN         30A  O  4 16COLOR(WHT)
     A                                  5  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                  5 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
      *
     A          R NYWRK
     A                                      CF03(03 'EXIT')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                  1  2'SYSPED/ARC019'
     A                                      COLOR(BLU)
     A                                  1 17MSGCON(050 SHU1016 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  2 70TIME
     A                                      COLOR(BLU)
     A                                  2  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3  2MSGCON(030 DIV0017 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(077 SHU1017 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  5  7MSGCON(060 SHU1018 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 42MSGCON(018 DIV0022 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 62MSGCON(018 DIV0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  98        MSG205        78A    24  2MSGID(DIV 0012 SYMSGF)
     A                                      COLOR(WHT)
     A N98                             24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
