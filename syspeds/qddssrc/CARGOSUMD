     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      HLPDOC('SYKS020010' 'SYKS02' 'SY400-
     A                                      LO/TRANS')
     A                                      HELP
     A                                      ALTHELP
      * "NOERASE" MÅ HA FOR Å IKKE TØMME BILDE SOM MAN KOMME FRA
     A          R NOERASE
     A                                      ASSUME
     A                                  1  2' '
     A          R WINDOW1
     A                                      WINDOW(*DFT 12 50 *NOMSGLIN)
     A                                      CF12(12 'Avslutt')
     A                                      BLINK
     A                                  1  2'CARGOSUM'
     A                                      COLOR(BLU)
     A                                  1 13'Diff oppdrag/cargoscan'
     A                                      COLOR(WHT)
     A                                  2  2'Snd:'
     A            HXSNDN        20   O  2  7COLOR(WHT)
     A                                  2 28'Avd/opd:'
     A            HEAVD          4  0O  2 37COLOR(WHT)
     A                                  2 42'/'
     A            HEOPD          7  0O  2 44COLOR(WHT)
     A                                  3  2'Fraktvekt avviker mer enn'
     A            AVVIKPROS      3Y 1O  3 28EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  3 33'% mellom oppdrag'
     A                                  4  2'og fraktvekt basert på vekt/M3 i c-
     A                                      argoscan-kolli'
     A                                  5  3'      Ant kll Pll      Vekt  Volum-
     A                                       M3 Fraktvekt'
     A                                      COLOR(WHT)
     A                                  6  2'Oppdr:'
     A                                      COLOR(WHT)
     A            HENT           7Y 0O  6  9EDTCDE(4)
     A            COGNTP         3Y 0O  6 17EDTCDE(4)
     A            HEVKT          9Y 0O  6 21EDTCDE(4)
     A            HEM3           7Y 3O  6 32EDTCDE(4)
     A            HEFBV          9Y 0O  6 41EDTCDE(4)
     A                                  7  2'C.Scan:'
     A                                      COLOR(WHT)
     A            COMNT          5Y 0O  7 11EDTCDE(4)
     A            COMNTP         3Y 0O  7 17EDTCDE(4)
     A            COMVKT         7Y 2O  7 25EDTCDE(4)
     A            COMM3          7Y 5O  7 34EDTCDE(4)
     A            COMFBV         5Y 0O  7 45EDTCDE(4)
     A                                  8 30'Pros avvik:'
     A                                      COLOR(WHT)
     A            COAVVP         6Y 1O  8 42EDTCDE(J)
     A                                      COLOR(WHT)
     A N50                              9  2'Tast enter for å oppdatere oppdrag-
     A                                      et med verdier'
     A                                      COLOR(TRQ)
     A  50                              9  2'ALLE KOLLI ER IKKE GÅTT GJENNOM CA-
     A                                      RGOSCAN!!     '
     A                                      COLOR(TRQ)
     A N50                             10  2'fra cargoscan. Fraktbeløp må godkj-
     A                                      ennes på ny.  '
     A  50                             10  2'Enter=OK IKKE SETT PÅ TUR (vent ti-
     A                                      l komplett...)'
     A N50                             11  2'Gå i så fall inn på oppdraget/rebe-
     A                                      regn/godkjenn.'
     A  50                             11  2'F12=Ignorer,SETT PÅ TUR,vkt mm bli-
     A                                      r IKKE justert'
     A N50        SKUTXT        20A  O 12  2COLOR(TRQ)
     A  50                             12  2'Enter=Vent/IKKE på tur F12=PÅ TUR,-
     A                                      ignorer varsel'
     A                                      COLOR(BLU)
     A N50                             12 23'Enter=Oppdater F12=Ikke opp.'
     A                                      COLOR(BLU)
