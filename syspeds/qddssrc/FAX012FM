     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPDOC('STD' 'HELPNU' 'HLP400')
     A                                      HELP
     A                                      ALTHELP
      *
     A          R BILDE01
     A                                      CF03(03 'Avslutt program')
     A                                      CF10(10 'lage dokument')
     A                                      CF04(04 'søking på kunder')
     A                                      CF11(11 'sendbeskjed')
     A                                      CF06(06 'Rediger dokument')
     A                                      CF08(08 'Fri part')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  3'SYSPED/FAX012'
     A                                      COLOR(BLU)
     A                                  1 21'SYSPED      FAX-UTILITY'
     A                                      COLOR(WHT)
     A                                  1 62DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(GRN)
     A                                  1 73TIME
     A                                      COLOR(GRN)
     A                                  2 21MSGCON(040 SHU0203 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  6  3MSGCON(025 SHU0166 *LIBL/SYMSGF)
     A            KUNDNR         8Y 0B  6 30EDTCDE(4)
     A                                      DSPATR(UL)
     A            KNAVN         30A  B  6 40
     A  25                                  DSPATR(PR)
     A                                  7  3MSGCON(025 SHU0167 *LIBL/SYMSGF)
     A            FAXNR         15A  B  7 30
     A  71                                  DSPATR(PC)
     A                                      DSPATR(UL)
     A                                  7 46MSGCON(012 SHU0261 *LIBL/SYMSGF)
     A            WSLAND         2A  B  7 59
     A                                  7 62'='
     A            FAXRET         6A  O  7 64
     A                                  9  3MSGCON(025 SHU0202 *LIBL/SYMSGF)
     A            MAPPE         20A  B  9 30
     A                                 10  3MSGCON(025 SHU0201 *LIBL/SYMSGF)
     A            DOKNA         12A  B 10 30
     A  73                                  DSPATR(RI)
     A                                 12  3MSGCON(025 SHU0173 *LIBL/SYMSGF)
     A            WSTIME         6Y 0B 12 30DSPATR(UL)
     A                                      EDTCDE(4)
     A                                 13  3MSGCON(025 SHU0174 *LIBL/SYMSGF)
     A            WSDATE         6Y 0B 13 30DSPATR(UL)
     A                                      EDTCDE(4)
     A                                 15  3MSGCON(025 SHU0343 *LIBL/SYMSGF)
     A            WSCVP          1   B 15 30
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 21MSGCON(018 DIV0032 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 40MSGCON(018 SHU0168 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
