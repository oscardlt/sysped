      *
      ***********************************************************
      *** PROGRAM SKAL SKRIVE 1 SIDES FAX                     ***
      *****************************************************************
      *
     FFAXLOG02  IF   E           K DISK
     FKODTLK    IF   E           K DISK
     FCUNDL01   IF   E           K DISK
     FKODTA     IF   E           K DISK
     FFAXQLOG   IF   E           K DISK
     FFAXSEN    IF   E           K DISK
     FHODEFAX   IF   E           K DISK
     FFAXMANP   UF   E           K DISK
     FFAX022P   O    E             PRINTER OFLIND(*IN01)
     FFAX022C   O    E             PRINTER OFLIND(*IN02)
     D                 DS
     D  FINAVN                 1     40
     D  FINA30                 1     30
     D                 DS
     D  ARX                    1      2  0
     D  MNX                    3      4  0
     D  DGX                    5      6  0
     D  DATOX                  1      6  0
     D                 DS
     D  DG                     1      2  0
     D  MN                     3      4  0
     D  AR                     5      6  0
     D  DATO                   1      6  0
     D                 DS
     D  LI13                   1     60
     D  LI14                  61    120
     D  LI15                 121    180
     D  LI16                 181    240
     D  LI17                 241    300
     D  LI18                 301    360
     D  LI19                 361    420
     D  LI20                 421    480
     D  LI21                 481    540
     D  LI22                 541    600
     D  LI23                 601    660
     D  LI24                 661    720
     D  TSID2A                 1    250
     D  TSID2B               251    500
     D  TSID2C               501    720
     D                UDS
     D  UFIRM                  1      2
     D  UAVD                  15     18
     D  USG                   19     21
     I*
     C                   EXSR      INIT
     C*
     C     *ENTRY        PLIST
     C                   PARM                    REFNRA            9
     C                   PARM                    UJOBNR            6
     C                   PARM                    COPPRT            1
     C*
     C     KODTAK        CHAIN     KODTA                              34
     C     *IN34         IFEQ      *OFF
     C     CUNDK1        CHAIN     CUNDL01                            35
     C     *IN35         IFEQ      *OFF
     C     POSTNR        IFNE      *ZEROS
     C                   MOVEL     POSTNR        WPOSTN
     C                   ELSE
     C                   MOVEL     SYPOGE        WPOSTN
     C                   ENDIF                                                  <- POSTNR
     C     WPOSTN        CAT       ADR3:1        PADR3            35
     C     KODLK1        CHAIN     KODTLK                             36
     C     *IN36         IFEQ      *OFF
     C                   MOVEL     KLKNVN        COUNTR           30
     C                   ENDIF                                                  <- *IN36
     C                   ENDIF                                                  <- *IN35
     C                   ENDIF                                                  <- *IN34
     C*
     C                   MOVE      UYEAR         AR
     C                   MOVE      UMONTH        MN
     C                   MOVE      UDAY          DG
     C                   MOVE      REFNRA        REFNR             9 0
     C                   MOVE      KNAVN         KNAVNQ           30
     C*
     C* KOPIPRINT
     C*
     C     COPPRT        IFEQ      'J'
     C     REFNR         CHAIN(N)  FAXMANP                            33
     C                   MOVE      PUDATE        DATOX
     C                   MOVE      ARX           AR
     C                   MOVE      MNX           MN
     C                   MOVE      DGX           DG
     C     JOBNR         CHAIN     FAXLOG02                           44
     C     LOFAXN        COMP      'MAIL'                                 88
     C     LOGKEY        CHAIN     FAXQLOG                            44
     C  N44QFSU          IFNE      'S'
     C                   MOVE      '* NOT '      QFDATE
     C                   MOVE      'SENDT*'      QFTIME
     C                   END
     C* EVENTUELL HODEAVVIK ANNET SPRÅK
     C     KUNDNR        IFNE      *ZEROS
     C     KUKEY         CHAIN     CUNDL01                            44
     C                   END
     C                   MOVE      SPRAAK        UNIKH             1
     C     HODEK         CHAIN     HODEFAX                            44
     C     KNAVNQ        IFNE      *BLANKS
     C                   MOVE      *BLANKS       FINAVN
     C                   MOVEL     KNAVNQ        FINAVN
     C                   END
     C*
     C  N33              WRITE     CVRPAC
     C*
     C* FAXPRINT
     C*
     C                   ELSE
     C     REFNR         CHAIN     FAXMANP                            33
     C     JOBNR         IFEQ      *BLANKS
     C  N33              MOVE      UJOBNR        JOBNR
     C                   END
     C  N33              UPDATE    FAXMANR
     C* EVENTUELL HODEAVVIK ANNET SPRÅK
     C     KUNDNR        IFNE      *ZEROS
     C     KUKEY         CHAIN     CUNDL01                            33
     C                   END
     C                   MOVE      SPRAAK        UNIKH             1
     C     HODEK         CHAIN     HODEFAX                            33
     C     KNAVNQ        IFNE      *BLANKS
     C                   MOVE      *BLANKS       FINAVN
     C                   MOVEL     KNAVNQ        FINAVN
     C                   END
     C*
     C                   WRITE     CVRPAG
     C     TSID2A        IFEQ      *BLANKS
     C     TSID2B        ANDEQ     *BLANKS
     C     TSID2C        ANDEQ     *BLANKS
     C                   WRITE     CVRPA1A
     C                   ELSE
     C                   WRITE     CVRPA1B
     C                   END
     C                   WRITE     CVRPA2
     C                   END
     C                   SETON                                        LR
     C                   RETURN
     C***********************************************
     C     INIT          BEGSR
     C*---------------------------------------------*
     C     *LIKE         DEFINE    KOAUNI        KOUNIK
     C     *LIKE         DEFINE    SYPOGE        WPOSTN
     C     *LIKE         DEFINE    KLKUNI        KLUNIK
     C     *LIKE         DEFINE    KOAAVD        NAVD
     C*
     C                   MOVEL     'A'           KOUNIK
     C                   MOVEL     'LK '         KLUNIK
     C                   MOVEL     UAVD          NAVD
     C*
     C* KODETEKST - AVD.
     C*
     C     KODTAK        KLIST
     C                   KFLD                    KOUNIK
     C                   KFLD                    NAVD
     C*
     C* HODE TEKST
     C*
     C     HODEK         KLIST
     C                   KFLD                    UNIKH
     C                   KFLD                    NAVD
     C                   MOVE      ' '           UNIKH             1
     C*
     C*
     C* KUNDEFIL
     C*
     C     CUNDK1        KLIST
     C                   KFLD                    KOAFIR
     C                   KFLD                    KOAKNR
     C*
     C     KUKEY         KLIST
     C                   KFLD                    UFIRM
     C                   KFLD                    KUNDNR
     C*
     C* LANDKODE
     C*
     C     KODLK1        KLIST
     C                   KFLD                    KLUNIK
     C                   KFLD                    SYLAND
     C*
     C* SENDINGSLOGG
     C*
     C     LOGKEY        KLIST
     C                   KFLD                    JOBNR
     C                   KFLD                    FASTKO            2
     C                   MOVE      '00'          FASTKO
      *
     C     UFIRM         SETLL     FAXSEN
     C                   READ      FAXSEN                                 33
      *
     C                   ENDSR
