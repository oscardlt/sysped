      *********************************************************************
      * PROGRAM BEHANDLER INPUT FRA SYMN1 OG VISER LISTE
      *
CRTPG+*  CRTPGM SYSPED/ESOP11 MODULE(*LIBL/ESOP11 *LIBL/ESOP13
CRTPGM*        *LIBL/CHECKUSR *LIBL/INCGI) ACTGRP(CGI) AUT(*USE)
      *
      *********************************************************************
      * MAIN PROGRAM FLOW
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
     FZEADL05   UF   E           K DISK    USROPN
     Fzreetxt1  UF A E           K DISK    USROPN
     Fzrisol01  UF   E           K DISK    USROPN
     Fzokufm1   UF A E           K DISK    USROPN
     FZTRACKF   O  A E             DISK    USROPN
     FCUNDL01   IF   E           K DISK    USROPN
     FBRIDF     UF   E           K DISK    USROPN
     FBRPARF    IF   E           K DISK    USROPN
     FFIRM      if   e           k disk    usropn
     FWRKCGI    O  A E             DISK    usropn
      *--------------------------------------------------------------------
      * Prototype defintions and standard system API error structure
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      *--------------------------------------------------------------------
      * Variables common to CGI programs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
      * Variables received from the input string
     D OddEven         s             10
     D lng             s              2
     D BckGnd          s             10
     D UsrSpcName      s             10
     D WSUSER          S             10
     D WSNA            S             30
     D WSREFF          S             17
     D WSDOCS          S            500
     D XXSEND          S             10
     D XXslett         S              1
     D AVVKNR          S              8
     D AVVKNRN         S              8  0
     D ErrTxt          S            500
     D GrpNr           s              8
     D Sndnr           S             20
     D ANTSND          s              5  0
     D MaxSN           s              5
     D MaxSND          s              5  0
     D ANTRCD          s              5  0
     D MaxRC           s              5
     D MaxRCD          s              5  0
     D A               S              1    DIM(15)
     D PGM             C                   CONST('SYSPED/CHECKUSR')
     D TITLE           C                   CONST('Arbeid med ordre')
     D TITLEEN         C                   CONST('Work with orders')
     D IfsMultIndicators...
     d                 ds
     D  NoErrors                       n
     D  NameTooLong                    n
     D  NotAccessible                  n
     D  NoFilesUsable                  n
     D  DupSections                    n
     D  FileIsEmpty                    n
      *Array for fri-tekster
     D                 DS
     D  TEXT                   1   6300
     D  TX                     1   6300    DIM(6300)
     D                 DS
     D  TXT1                   1   6300
     D  TX1                    1   6300    DIM(90)
     D                 DS
     D  TXT2                   1   6300
     D  TX2                    1   6300    DIM(90)
     D                 DS
     D  TXT3                   1   6300
     D  TX3                    1   6300    DIM(90)
     D                 DS
     D  FTX                    1     70
     D  FT                     1     70    DIM(70)
     D Wpos            s              4  0
     D Gmpos           s              4  0
     D BLF             s              4  0
     D Tst700          s            700
      * Definere  hex 0D / 25 (CRLF)
     D Hex0D           c                   x'0D'
     D Hex25           c                   x'25'
      *
     D ItemCount       s             10i 0
     D i               s             10i 0
     D                 DS
     D  PAVRDA                 1     50
     D  PAVRDTxt               6     50
     D                 DS
     D  DataDS                 1    220
     D  WDsANT                 1      4  0
     D  WDsVKT                 5     10  0
     D  WDsM3                 11     17  3
     D  WDsLM                 18     21  2
     D  WDsREF                22     36
     D  WDsMNavn              37     66
     D  WDsMAdr1              67     96
     D  WDsMPnSt              97    126
     D  WDsSndnr             127    146
      *--------------------------------------------------------------------
      * Prolog common to CGI programs
      * -Receive input from the remote browser
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prolog3
      *=====================================================================******
      * MAIN PROCESS LINE
      *=====================================================================******
      * Get program start time for calculating execution time
     C                   time                    timedata1
      * Parse input
     C                   exsr      Parse
      *
     C                   EXSR      INIT
     C                   CALLB     PGM
     C                   PARM                    BIBRID
     C                   PARM                    UsrSpcName
     C                   PARM                    UINN              1
     C                   IF        UINN = 'N'
     C                   GOTO      SLUTT
     C                   END
      * dato/tid siste vellykkede operasjon.
     C     WSUSER        CHAIN     BRIDF                              50
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVE      WDT           BIDTF
     C                   MOVE      WTM           BITIDF
     C                   UPDATE    BRIDFR
     C                   MOVE      BIBESK        WINT              3
      *
      * ER DET BEDT OM PRINT AV HENTELISTE..(submit med checked i xx poster)
     C                   eval      ItemCount = ZhbGetVarCnt('HLIST')
     C     ItemCount     IFGT      *ZEROS
     C                   EXSR      HENTELISTE
     C     ErrTxt        CABEQ     *blanks       Slutt
     C                   END
      *
      * WSREFF skal være UNIK (innen åpne poster for en bruker/kunde)
      * STATUS-endring (send eller slett)
     C                   IF        WSREFF <> *BLANKS
     C                   IF        AVVKNRN <> 0
     C                   EVAL      BIKUNR=AVVKNRN
     C                   ENDIF
     C     Zead5Key      CHAIN     ZEADL05                            33
     C     *IN33         IFEQ      '0'
     C                   IF        XXSEND = 'SEND'
     C                   EXSR      SndOppd
     C                   ELSE
     C                   IF        XXSLETT = 'Y'
     C                   MOVE      'S'           HEST
     C                   END
     C                   UPDATE    HEADFR
     C                   END
     C                   END
     C                   END
      *
      * Set output skeleton html member name
     C                   exsr      SetSkl
      *------------------                                                   ******
      * Write HTML sections
      *
      * 1-Section /$top
     C                   exsr      SetTop
     C                   callp     wrtsection('stdtop')
     C                   callp     wrtsection('top')
      * 4-Sections /$tablerow
      *
     C                   EXSR      HENTORDRE
      *
     C                   callp     updHTMLvar('ANTSND':
     C                             %trim(%editc(ANTSND:'3')))
     C                   callp     updHTMLvar('MaxSnd':
     C                             %trim(%editc(MaxSnd:'3')))
     C                   callp     updHTMLvar('ANTRCD':
     C                             %trim(%editc(ANTRCD:'3')))
     C                   callp     updHTMLvar('MaxRcd':
     C                             %trim(%editc(MaxRcd:'3')))
     C                   callp     updhtmlvar('runtime':
     C                             %trim(%editw(sec:'     0 .   ')))
     C                   callp     wrtsection('bot')
      *
      * 5a-Section  type oppdrag ved New..
     C                   exsr      CrtNewTyp
     C                   callp     wrtsection('stdbot')
      * 5b-Section  Bytt kunde
     C     WINT          IFEQ      '*I*'
     C                   callp     wrtsection('byttku')
     C                   ENDIF
      *
      * 6-Section /$endhtml
     C     endhtml       tag
     C                   exsr      SetEnd
     C     SLUTT         tag
     C                   callp     wrtsection('endhtml')
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
     C                   EXSR      EXIT
     C                   return
      *=====================================================================******
      * Send oppdrag inn til basen
      *=====================================================================******
     C     SndOppd       begsr
      *
     C                   MOVE      'N'           XDIREKT           1
     C                   IF        ((HEUR <> ' ') AND (HEAVD <> 0))
     C                   MOVE      'HEDIR'       PAID
     C     BrParKey      CHAIN     BRPARF                             33
     C   33KundKey       chain     BRPARF                             33
     C   33GrupKey       chain     BRPARF                             33
     C   33FirmKey       chain     BRPARF                             33
     C  N33              IF        ((PAVRDA = 'J') OR (PAVRDA = 'j'))
     C                   MOVE      'J'           XDIREKT
     C                   END
     C                   END
      *
     C                   MOVE      'WEB'         HESG
     C                   MOVEL     HEREFF        HERFA
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTD               6
     C                   MOVE      WDT           HEDTR
     C                   MOVE      ' '           HEST
     C                   MOVE      ' '           HE01
      * Hent Fritekst til samlet streng max 700 lang
     C                   MOVE      *ZEROS        XN1               3 0
     C                   MOVE      *ZEROS        XN2               3 0
     C                   MOVE      *ZEROS        XN3               3 0
     C                   MOVE      *BLANKS       TEXT
     C                   MOVE      *BLANKS       TXT1
     C                   MOVE      *BLANKS       TXT2
     C                   MOVE      *BLANKS       TXT3
     C     HEUNIK        SETLL     Zreetxt1
     C     HEUNIK        Reade     Zreetxt1                               33
     C     *in33         doweq     '0'
     c     frtkod        IFEQ      'R'
     C                   ADD       1             XN1
     C                   MOVE      FRTTXT        TX1(XN1)
     c                   end
     c     frtkod        IFEQ      'G'
     C                   ADD       1             XN2
     C                   MOVE      FRTTXT        TX2(XN2)
     c                   end
     C     frtkod        IFEQ      ' '
     C                   ADD       1             XN3
     C                   MOVE      FRTTXT        TX3(XN3)
     C                   end
     c     frtkod        IFNE      'M'
     c     frtkod        andne     'T'
     c                   delete    Freetxtr
     C                   end
     C     HEUNIK        Reade     Zreetxt1                               33
     c                   end
      *
     c                   movel     Hex0D         CrLf              2
     c                   move      Hex25         CrLf
     c                   move      HEUNIK        FRUNIK
     c                   move      HEREFF        FRREFF
     c                   move      *zeros        FRTLI
     c                   move      HEDTOP        FRTDT
      * Melding til mottaker....
     C                   IF        TXT1 <> *BLANKS
     c                   move      'R'           FRTKOD
     C                   MOVE      TXT1          TEXT
     c                   exsr      SkrivFrt
     c                   end
      * Melding til Transportør.
     C                   IF        TXT2 <> *BLANKS
     c                   move      'G'           FRTKOD
     C                   MOVE      TXT2          TEXT
     c                   exsr      SkrivFrt
     c                   end
      * Melding til Transportør.
     C                   IF        TXT3 <> *BLANKS
     C                   move      ' '           FRTKOD
     C                   MOVE      TXT3          TEXT
     C                   exsr      SkrivFrt
     C                   end
      * fix REFF i ZOKUFM
     C     HEUNIK        SETLL     Zokufm1
     C     HEUNIK        Reade     Zokufm1                                33
     C     *in33         doweq     '0'
     c                   move      HEREFF        FMREFF
     c                   update    DOKUFMR
     C     HEUNIK        Reade     Zokufm1                                33
     c                   end
      *
      * fix REFF i Zrisok
     C     HEUNIK        SETLL     zrisol01
     C     HEUNIK        Reade     zrisol01                               33
     C     *in33         doweq     '0'
     c                   move      HEDTOP        FSDTOP
     c                   move      HEREFF        FSREFF
     c                   update    FRISOKR
     C     FSKODE        IFEQ      'IFB'
     c                   eval      Sndnr  = '401' + FSSOK
     C                   end
     C     HEUNIK        Reade     zrisol01                               33
     c                   end
      *
     C                   UPDATE    Headfr
      *
     C                   EXSR      OPDLOG
      *
     C                   MOVE      HEUNIK        UUNIK             9
      *
      * E-POST TIL mailadresse registrert i bildet (XREETXT)
      * hvis til innland -utsett denne til oppdr.nr er tildelt (utføres i ESOP11D)
      * (burde kankje utsette på alle dersom XDIREKT = 'J' - men staretr enkelt)
     c                   if        HEAVD<>9999 and HEAVD<>9998 and HEAVD<>9997
     C                   MOVE      '*FTXT'       UTYPE             5
     C                   CALL      'ESOP11C'
     C                   PARM                    UUNIK
     C                   PARM                    UTYPE
     c                   endif
      *
      * E-POST TIL INTERBRUKER
     C                   MOVE      'HEUMI'       UTYPE             5
     C                   CALL      'ESOP11C'
     C                   PARM                    UUNIK
     C                   PARM                    UTYPE
      * E-POST TIL intern avhenger av oppdragstype
     C                   MOVE      'HEUMO'       UTYPE             5
     C                   CALL      'ESOP11C'
     C                   PARM                    UUNIK
     C                   PARM                    UTYPE
      *
      * E-POST TIL WEB-RBRUKER (KUNDE SOM REGISTRERER OPPDRAG)
     C                   MOVE      'HEUMS'       UTYPE             5
     C                   CALL      'ESOP11C'
     C                   PARM                    UUNIK
     C                   PARM                    UTYPE
      *
     C                   IF        XDIREKT = 'J'
     C                   CALL      'ESOP11D'
     C                   PARM                    HEREFF
     C                   PARM                    HEUNIK
     C                   END
      *
     c                   endsr
      *=====================================================================******
      * Skriv freetekst
      *=====================================================================******
     C     SkrivFrt      begsr
     C                   z-add     1             GMPOS
     C                   z-add     1             WPOS
      * Neste linje er fram til neste CR/LF eller max 70 tegn
     c     NextCRLF      Tag
     C     CRLF          SCAN      TEXT:GMPOS    WPOS                     51
      * fant ikke flere CRLF, resten blank? Hopp ut / ellers lagre 70+70.
     C     *IN51         IFEQ      *OFF
     c                   MOVE      *BLANKS       TST700
     c                   movea     TX(GMpos)     TST700
     C     TST700        CABEQ     *BLANKS       UTSKRIV
     c                   z-add     9999          Wpos
     c                   end
      *
      * Lagre uansett fra og med start.sted
     C                   MOVEA     TX(GMPOS)     FT
      * Finn lengde på siste streng(max 70) - blank resten (fra lengde +1)
     C                   EVAL      BLF = WPOS - GMPOS + 1
     C     BLF           IFGT      70
      *(51 OFF. skal IKKE hoppe over CRLF før neste linje.)
     C                   MOVE      *OFF          *IN51
     C                   eval      wpos = GMPOS +70
     c                   end
     C     BLF           IFge      1
     C     BLF           andle     70
     C                   MOVEA     *BLANKS       FT(BLF)
     C                   END
     c                   add       1             FRTLI
     c                   Movea     FT            FRTTXT
     c                   write     freetxtr
      *
      * Finn neste start-posisjon (HVIS CRLF - hopp pver)
     C                   Z-add     WPOS          GMPOS
     C     *IN51         IFEQ      *ON
     C                   ADD       2             GMPOS
     C                   END
     C     GMPOS         CABLE     700           NEXTCRLF
     C
      *
     c     UtSKRIV       endsr
      *
      *=====================================================================******
      * Parse input
      *=====================================================================******
     C     Parse         begsr
     C                   eval      lng     = zhbgetvar('lng')
     C                   eval      BckGnd  = zhbgetvar('bckgnd')
     C                   EVAL      WSUSER  = zhbgetvar('USER')
     C                   EVAL      UsrSpcName  = zhbgetvar('UsrSpcName')
      *
     C                   EVAL      WSREFF  = zhbgetvar('HEREFF')
     C                   EVAL      XXSEND  = zhbgetvar('XXSEND')
     C                   EVAL      XXslett = zhbgetvar('XXslett')
     C                   eval      AVVKNR  = zhbgetvar('AVVKNR')
     C                   eval      AVVKNRN = c2n2(AVVKNR)
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$top
      *  (search criteria)
      *=====================================================================******
     C     SetTop        begsr
      * Repeat national language for the next invocation
     C                   callp     updhtmlvar('LNG':lng:
     C                             InitHTMLVars)
     C                   callp     updHTMLvar('ROWHEAD':RowHead)
     C                   callp     updHTMLvar('LogoImg':LogoImg)
      * Color for text, background, links, visited links, active links
     C                   callp     updhtmlvar('TXTCOLOR':'black')
     C                   callp     updhtmlvar('BOLD':' ')
     C                   callp     updhtmlvar('BGCOLOR':BIFARG)
     C                   callp     updhtmlvar('LINK':'0000FF')
     C                   callp     updhtmlvar('VLINK':'FF0000')
     C                   callp     updhtmlvar('ALINK':'00FF00')
      *
     C                   IF        SPRAAK = 'EN'
     C                   callp     updhtmlvar('TITLE':TITLEEN)
     C                   ELSE
     C                   callp     updhtmlvar('TITLE':TITLE)
     C                   END
      * KUNDE
     c     AvvKnrN       ifne      *zeros
     c                   z-add     AvvKnrN       BIKUNR
     c                   end
     C     CUNDL01K      CHAIN     CUNDL01                            33
     C                   callp     updhtmlvar('WSNA':KNAVN)
     C                   callp     updhtmlvar('USER':WSUSER)
BODY C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updHtmlVar('UsrSpcName':UsrSpcName)
     C                   callp     updHTMLvar('sdato':
     C                             %trim(%editw(BIDTV:'    .  .  ')))
     C                   callp     updHTMLvar('stid':
     C                             %trim(%editw(BITIDV:'  .  .  ')))
     C                   callp     updhtmlvar('AVVKNR':
     C                             %trim(%editc(AVVKNRN:'Z')))
     C                   callp     updhtmlvar('LABELTYP':LabelTyp)
     C                   callp     updhtmlvar('ERRTXT':ERRTXT)
      *
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$tablerow (table row)
      *=====================================================================******
     C     SetTabRow     begsr
      *
     C                   callp     updHTMLvar('ODDEVEN':OddEven)
     C                   MOVE      HEUNIK        HEUNIKA           9
     C                   callp     updhtmlvar('HEUNIK':HEUNIKA)
     C                   callp     updhtmlvar('HEREFF':HEREFF)
     C                   callp     updhtmlvar('HEDTOP':
     C                             %trim(%editw(HEDTOP:'    .  .  ')))
     C                   callp     updhtmlvar('HEGN':HEGN)
     C                   callp     updhtmlvar('HENAS':HENAS)
     C                   callp     updhtmlvar('HENAK':HENAK)
     C                   callp     updhtmlvar('HEFR':HEFR)
     C                   callp     updhtmlvar('HENT':
     C                             %trim(%editc(HENT:'Z')))
     C                   callp     updhtmlvar('HEVKT':
     C                             %trim(%editc(HEVKT:'Z')))
     C                   callp     updhtmlvar('HEM3':
     C                             %trim(%editc(HEM3:'4')))
     C                   callp     updhtmlvar('HESDF':HESDF)
     C                   callp     updhtmlvar('HESDT':HESDT)
     C                   callp     updhtmlvar('FRALK':'')
     C                   callp     updhtmlvar('TILLK':'')
     C                   select
     C     HEUR          wheneq    'H'
     C                   callp     updhtmlvar('FRALK':HELKA)
     C                   callp     updhtmlvar('TILLK':HETRI)
     C     HEUR          wheneq    'A'
     C     HEUR          oreq      'B'
     C                   callp     updhtmlvar('FRALK':HELKS)
     C                   callp     updhtmlvar('TILLK':HELKK)
     c                   endsl
     C                   callp     updhtmlvar('HERFA':HERFA)
     C                   callp     updhtmlvar('XXOK':'SEND')
     C                   callp     updhtmlvar('HEPK1':HEPK1)
     C                   callp     updhtmlvar('HEPK2':HEPK2)
     C                   callp     updhtmlvar('HEPK3':HEPK3)
     C                   callp     updhtmlvar('HEPK4':HEPK4)
     C                   callp     updhtmlvar('HEPK5':HEPK5)
      *
     C                   MOVE      HEUNIK        XAN09             9
     C                   EVAL      WSDOCS = '<a href="#" OnClick="PrtFbr('      Fraktbrev
     C                             + WAPSTR + XAN09 + WAPSTR
     C                             + '); return false;">F.br&nbsp'
     C                             + HEPK1 + '</a> '
     C                             + '<a href="#" OnClick="PrtTPFbr('           TP fraktbrev
     C                             + WAPSTR + XAN09 + WAPSTR
     C                             + '); return false;">TP.F.br&nbsp'
     C                             + HEPK1 + '</a> '
     C                             + '<a href="#" OnClick="PrtCmr('             CMR fraktbrev
     C                             + WAPSTR + XAN09 + WAPSTR
     C                             + '); return false;">Cmr&nbsp'
     C                             + HEPK2 + '</a> '
     C                             + '<a href="#" OnClick="PrtML('              Merkelapp
     C                             + WAPSTR + XAN09 + WAPSTR
     C                             + '); return false;">Merk&nbsp'
     C                             + HEPK3 + '</a> '
     C                             + '<a href="#" OnClick="PrtBook('            Booking
     C                             + WAPSTR + XAN09 + WAPSTR
     C                             + '); return false;">Booking</a> '
     C                   callp     updhtmlvar('WSDOCS':WSDOCS)
      *
     C                   endsr
      *=====================================================================******
      *  Pulldown-boks for å velge type ved ved Lag ny..                    )
      *=====================================================================******
     C     CrtNewTyp     begsr
     C                   callp     wrtsection('TypSltStr')
     C                   MOVE      'HEUR '       PAID
     C     BrParKey      setll     BRPARF
     C     BrParKey      reade     BRPARF                                 40
     C     *in40         doweq     '0'
     c                   z-add     PAVRDN        XXAVD             4 0
     c                   Move      XXAVD         XXAVDA            4
     c                   Move      *blanks       SlTy             54
     c                   eval      Slty = XXAVDA + PAVRDA
     C                   callp     updhtmlvar('SLTY':SLTY)
     C                   callp     updhtmlvar('SLTYTXT':PAVRDTxt)
     C                   callp     wrtsection('TypSltRow')
     C     BrParKey      reade     BRPARF                                 40
     C                   enddo
      *
     C                   callp     wrtsection('TypSltEnd')
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$endhtml
      *=====================================================================******
     C     SetEnd        begsr
      * Compute run time
     C                   time                    timedata2
     C     timedata2     subdur    timedata1     ms:*ms
     C                   eval      sec = ms / 1000000
     C                   callp     updhtmlvar('runtime':
     C                             %trim(%editw(sec:'     0 .   ')))
      *
     C                   endsr
      *=====================================================================******
      * Set html output skeleton member before its read into memory
      *=====================================================================******
     C     SetSkl        begsr
      *------------------
      * Set html output skeleton member
cccccC                   eval      IfsMultIndicators = gethtmlifsmult(
cccccC                             '/syspeddev/html/Header/eSpedTop.html -
cccccC                             /syspeddev/html/ESOP11.html -
cccccC                             /syspeddev/html/Footer/eSpedBot.html':
cccccC                             '/CGITAG/')
      *
     C                   endsr
      *=====================================================================******
      * HENT ORDRE
      *=====================================================================******
     C     HENTORDRE     BEGSR
      *
     C                   Move      *zeros        AntSnd
     C                   Move      *zeros        AntRcd
     C     MaxSnd        IFEQ      *ZEROS
     C                   Z-add     1000          MaxSnd
     C                   END
     C     MaxRcd        IFEQ      *ZEROS
     C                   Z-add     5000          MaxRcd
     C                   END
      *
     C     XKNGIVER      SETLL     ZEADL05
     C     XKNGIVER      READE(N)  ZEADL05                                50
     C     *in50         doweq     '0'
     C                   IF        (((XKNGIVER <> BIKUNR) AND
     C                               (HEKNS <> BIKUNR)) AND
     C                               (HEKNK <> BIKUNR))
     C                   GOTO      XLESNESTE
     C                   END
     C                   IF        HESG = *BLANKS OR HESG = 'WEB'
     C                   Add       1             AntRcd
     C     AntRcd        CabGe     MaxRcd        SLHENTOPD
     C                   exsr      SetTabRow
     C                   callp     wrtsection('row')
     C                   IF        HEST = 'E'
     C                   callp     wrtsection('row1')
     C                   ELSE
     C                   callp     wrtsection('row2')
     C                   END
     C                   callp     wrtsection('row3')
     C                   Add       1             AntSnd
     C     AntSnd        CabGe     MaxSnd        SLHENTOPD
     C                   END
     C     XLESNESTE     TAG
     C     XKNGIVER      READE(N)  ZEADL05                                50
     C                   ENDDO                                                  *hival
      *
     C     SLHENTOPD     ENDSR
      *
      *////////////////////////////////////////////////////////////
     C     OPDLOG        BEGSR
      *////////////////////////////////////////////////////////////
      *
     C                   MOVE      HEAVD         TTAVD
     C                   MOVE      HEOPD         TTOPD
     C                   MOVE      WDT           TTDATE
     C                   MOVE      WTM           TTTIME
     C                   MOVE      WDT           TTDATL
     C                   MOVE      WTM           TTTIML
     C                   MOVE      'ED2'         TTACTI
     C                   MOVE      WSUSER        TTUSER
     C                   EVAL      TTTEXT = 'Incoming EDI recieved'
     C                   EVAL      TTTEXL = 'Inkommende EDI mottatt'
     C                   MOVE      'WEB'         TTEDEV
     C                   MOVE      HEUNIK        TTUNIK
     C                   MOVE      HEREFF        TTREFF
     C                   WRITE     TRACKFR
      *
     C                   ENDSR
      *
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
     C                   OPEN      BRIDF
     C     WSUSER        CHAIN(N)  BRIDF                              50
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
OddEv * hent Parametre som går igjen i mange prog:
OddEv *      Odd/Even/Rowhead-farger,Logobilde,Språk,Intern/Ekstern bruker,  mm
OddEvc                   call      'ESGETPAR'
OddEvc                   parm                    BIBRID
OddEvc                   parm                    BIFIRM
OddEvc                   parm                    BIKUNR
OddEvc                   parm                    BIKNG
OddEvc                   parm                    RowHead          10
OddEvc                   parm                    Odd              10
OddEvc                   parm                    Even             10
OddEvc                   parm                    LogoImg          50
OddEvc                   parm                    XSPRÅK            2
OddEvc                   parm                    XINTERN           1
OddEvc                   parm                    ParmDS1        1024
OddEvc                   parm                    ParmDS2        1024
OddEvc                   parm                    ParmDS3        1024
OddEv *
     C                   open      ZEADL05                              50
     C                   open      zreetxt1
     C                   open      zokufm1
     C                   open      zrisol01
     C                   OPEN      ZTRACKF
     C                   open      BRPARF                               50
     C                   OPEN      CUNDL01
     C                   OPEN      FIRM
     C                   OPEN      WRKCGI
      *
     C     CUNDL01K      klist
     C                   kfld                    BIFIRM
     C                   kfld                    BIKUNR
     C     ZEAD5KEY      klist
     C                   kfld                    XKNGIVER
     C                   kfld                    WSREFF
     c     FirmKey       klist
     c                   kfld                    DFTUSER          10
     c                   kfld                    NULLKU            8 0
     c                   kfld                    PAID
     c                   Eval      DFTUSER = '*KUNDFT*'+BIFIRM
     c     GrupKey       klist
     c                   kfld                    DFTUSER
     c                   kfld                    BIKNG
     c                   kfld                    PAID
     c     KundKey       klist
     c                   kfld                    DFTUSER
     c                   kfld                    BIKUNR
     c                   kfld                    PAID
     C     BrParKey      klist
     C                   kfld                    PABRID
     C                   kfld                    PAKUNR
     C                   kfld                    PAID
      *
     C                   BITON     '123457'      WAPSTR            1
     C                   BITOFF    '06'          WAPSTR
     C                   MOVE      BIBRID        PABRID
     C                   MOVE      *zeros        PAKUNR
      * HENT ARBEIDGIVER
     C                   MOVEL     'HEKN1'       PAID
     C     BrParKey      Chain     BRPARF                             33
     C   33              MOVE      BIKUNR        XKNGIVER          8 0
     C  N33              EVAL      XKNGIVER = PAVRDN
      * HENT SPRÅKKODE
     C                   MOVEL     'SPRÅK'       PAID
     C     BrParKey      Chain     BRPARF                             33
     C                   IF        (*IN33 OR (PAVRDA <> 'EN'))
     C                   MOVE      *BLANKS       XSPRÅK            2
     C                   ELSE
     C                   MOVE      'EN'          XSPRÅK            2
     C                   END
      * Hent labeltye (zebra/eltron) dersom det er behandling av tur
     c                   Move      *blanks       LabelTyp          1
     C                   MOVE      'LABTY'       PAID
     C     BrParKey      Chain     BRPARF                             33
     c  n33              MoveL     PAVRDA        LabelTyp
     c   33              MoveL     'Z'           LabelTyp
     C                   eval      ErrTxt     = *blanks
      *
     C                   endsr
      *=====================================================================******
      *  AVSLUTT
      *=====================================================================******
     C     EXIT          begsr
     C                   close     BRIDF
     C                   close     ZEADL05
     C                   close     zreetxt1
     C                   close     zokufm1
     C                   close     zrisol01
     C                   close     ZTRACKF
     C                   close     BRPARF
     C                   close     CUNDL01
     C                   CLOSE     FIRM
     C                   CLOSE     WRKCGI
     C                   eval      *inlr = *on
      *
     C                   endsr
      *
      *=====================================================================******
      *  PRINT HENTELISTE
      *=====================================================================******
     C     HENTELISTE    begsr
     C     AVVKNRN       IFNE      0
     C                   EVAL      BIKUNR=AVVKNRN
     C                   ENDIF
      *
      * SJEKK AT ALLE VARSLEDE DOKUMENT ER SKREVET UT..
     C                   eval      i = 1
     C     i             DOUGT     ItemCount
     C                   eval      WSREFF  = ZhbGetVar('HLIST':i)
     C     Zead5Key      CHAIN(N)  ZEADL05                            33
     C     *IN33         IFEQ      *OFF
     C     HEPK1         IFEQ      'Y'
     C     HEPK2         OREQ      'Y'
     C     HEPK3         OREQ      'Y'
     C                   LEAVE
     c                   END
     c                   END
     C                   eval      i = i +1
     C                   ENDDO
      * Hvis i ikke er blitt større enn Itemcount så er det feil..
     C     i             IFLE      ItemCount
     C                   eval      ErrTxt     = 'En eller flere av sendingene '+
     C                             'har uutskrevne dokumenter (Y) Disse må ' +
     C                             'printes (eller kode endres) før henteli' +
     C                             'ste kan skrives.'
     c                   goto      UtHenteL
     c                   END
      *
      * OK - KJØR
      * Oppdater hver post / skriv henteliste
      * Hent random integer, WINTEG er del 1 av unik key i workfile
     c                   eval      WINTEG   = random(1:999999)
     C                   eval      i = 1
     C     i             DOUGT     ItemCount
     C                   eval      WSREFF  = ZhbGetVar('HLIST':i)
     C     Zead5Key      CHAIN     ZEADL05                            33
     C     *IN33         IFEQ      *OFF
      * 1. Send oppdraget
     C                   Exsr      SndOppd
      * 2. Skriv til wrkfile for henteliste
      * (sndnr ligger også som eget felt i DS- sort-key kan enkelt endres..)
     c                   eval      wkey2   = sndnr
     c                   eval      WDsANT    = HENT
     c                   eval      WDsVKT    = HEVKT
     c                   eval      WDsM3     = HEM3
     c                   eval      WDsLM     = HELM
     c                   eval      WDsREF    = HERFA
     c                   eval      WDsMNavn  = HENAK
     c                   eval      WDsMAdr1  = HEADK1
     c                   eval      WDsMPnSt  = HEADK3
     c                   eval      WDsSndnr  = Sndnr
     c                   eval      WData     = DataDS
     c                   write     WRKCGIR
     c                   END
      *
     C                   eval      i = i +1
     C                   ENDDO
      *
      * SKRIV LISTE
     C     CUNDL01K      CHAIN     CUNDL01                            33
     C     BIFIRM        CHAIN     FIRM
     C                   EVAL      FIRMANAV = FIFT
     C                   callb     'ESOP13'
     c                   parm                    BIBRID
     c                   parm                    UsrSpcName
     c                   parm                    BIBESK
     c                   parm                    FIRMANAV         30
     c                   parm                    WSBIL            10
     c                   parm                    KNAVN
     c                   parm                    ADR1
     c                   parm                    POSTNR
     c                   parm                    ADR3
     c                   parm                    LISTENR          20
     c                   parm                    WINTEG
      *
      *
     C     UtHenteL      TAG
      * Param WSREF blankes slik at resten av prog blir ren refresh
     c                   move      *blanks       WSREFF
     C                   endsr
      *
