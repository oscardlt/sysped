     H DFTACTGRP(*NO)
     FHLBFILT   UF   E           K DISK
     FGSSBXF    UF   E           K DISK
     FGSSDGF    UF   E           K DISK
     FGSSFGF    UF   E           K DISK
     FGSSGDF    UF   E           K DISK
     FGSSKAF    UF   E           K DISK
     FGSSLGF    UF   E           K DISK
     FGSSLGTF   UF   E           K DISK
     FGSSOLF    UF   E           K DISK
     FGSSORF    UF   E           K DISK
     FGSSOVF    UF   E           K DISK
     FGSSOWF    UF   E           K DISK
     FGSSPAF    UF   E           K DISK
     FGSSPOF    UF   E           K DISK
     FGSSRFF    UF   E           K DISK
     FGSSTRF    UF   E           K DISK
     FGSSTWF    UF   E           K DISK
     FARKIVL17  UF   E           K DISK    RENAME(ARKIVR:ARKIVR17)
     FARKIVL19  UF   E           K DISK
      *
     FARKTXT    IF   E           K DISK
     FARKEXT    IF   E           K DISK
     FFIRMARC   IF   E           K DISK
     FFIRM      IF   E           K DISK
      *

     d                 ds
     d  x_arunde               1     10
     d  x_arunde1              1      3
     d  x_arunde2              4     10
     d                 ds
     d  orono                  1      7  0
     d  orono_a                1      7
     d                 ds
     d  trtno                  1     11  0
     d  trtno_avd              1      4  0
     d  trtno_opd              5     11  0
     d                 ds
     d  ARAVD                  1      4s 0
     d  UAVD                   1      4a
     d                 ds
     d  AROPD                  1      7s 0
     d  UOPD                   1      7a

     d xdtf            S              8  0
     d xdtp            S              8  0
     d WhereC          s            512a
     d FirstW          s              1A
     d cQuote          C                   const('''')
     d SqlStm          s            512a
     d myrows0         s             10i 0
     d myrows          s             10i 0
     d myrows2         s             10i 0
     D XPOS            S              3  0
     D Error           S            150
     d X               S             10  0
     d X2              S             10  0
     D XSLUTT          S              1
     D ufil            S            256A
     D utyp            S              5A
     D uok             S              1A
      *  Prototype for 'SYREO01C3'
     d SYREO01C3       pr                  extpgm('SYREO01C3')
     d ufil_                               like(ufil)
      *  Prototype for 'SYREO01R3'
     d SYREO01R3       pr                  extpgm('SYREO01R3')
     d uavd_                               like(uavd)
     d uopd_                               like(uopd)
     d utyp_                               like(utyp)
     d uok_                                like(uok)
      *  Prototype for syreo17r
     d syreo17r        pr
     d udtf_                               like(udtf)
     d udtp_                               like(udtp)
      *  *ENTRY Interface for Main Procedure
     d syreo17r        pi
     d udtf                           8
     d udtp                           8
      *
     d MyPoDS0         ds                  Qualified  dim(1)
     d  FIFIRM                        2A
     d MyPoDS          ds                  Qualified  dim(10000)
     d  ORONO                         7S 0
     d  ORST                          1A
     d  ORBCD                         5A
     d  ORDTR                         8S 0
     d  ORDTL                         8S 0
     d MyPoDS2         ds                  Qualified  dim(10000)
     d  TRTNO                        11S 0
     d  TRST                          1A
     d  TRDTL                         8S 0
      *
      /free

         exsr init;

         dow xslutt <> 'J';
           exsr srsso;
         enddo;

         xslutt = 'N';
         dow xslutt <> 'J';
           exsr srsst;
         enddo;

         *inlr = *on;
         return;

       //=====================================================================
       // Ordre
       //=====================================================================
       begsr srsso;

       clear MyPoDS;
       SqlStm = 'Select ORONO, ORST, ORBCD, ORDTR, ORDTL ' +
                'from GSSORF ' +
                'Where ORDTL <> 0 and ORDTL <= ' +
                %trim(udtf);
       Exec SQL PREPARE DynSel from :SqlStm;
       EXEC SQL DECLARE  A33  cursor for DynSel;
       Exec SQL OPEN A33;
       Myrows = %elem(MyPoDS);
       exec sql FETCH A33   for :MyRows rows
                  INTO :MyPoDs;
       Exec SQL CLOSE A33;

       if myrows > 0;
         for x = 1 to myrows;
           if mypods(x).orst = *blanks;
             xslutt = 'J';
             leave;
           endif;

           orono  = mypods(x).orono;
           orst   = mypods(x).orst;
           orbcd  = mypods(x).orbcd;
           ordtr  = mypods(x).ordtr;
           ordtl  = mypods(x).ordtl;

           if orst = '6' or orst = 'D';

             exec sql
             delete from gssdgf
             where dgono = :orono
             with NC;

             exec sql
             delete from gssfgf
             where fgono = :orono
             with NC;

             exec sql
             delete from gssgdf
             where gdono = :orono
             with NC;

             exec sql
             delete from gsskaf
             where kaono = :orono
             with NC;

             exec sql
             delete from gsslgf
             where lgono = :orono
             with NC;

             exec sql
             delete from gssolf
             where olono = :orono
             with NC;

             exec sql
             delete from gssolf
             where olono = :orono
             with NC;

             exec sql
             delete from gssovf
             where ovono = :orono
             with NC;

             exec sql
             delete from gssowf
             where owono = :orono
             with NC;

             exec sql
             delete from gsspaf
             where paono = :orono
             with NC;

             exec sql
             delete from gsspof
             where poono = :orono
             with NC;

             exec sql
             delete from gssrff
             where rfono = :orono
             with NC;

             exec sql
             delete from hlbfilt
             where orono = :orono
             with NC;

             // slett arkiv
             x_arunde2 = orono_a;
             setll x_arunde arkivl19;
             reade x_arunde arkivl19;
             dow not %eof;
               chain (fifirm) firmarc;
               chain (artype) arktxt;
               if %found and arklag <> *blanks;
                 chain (fifirm:arklag) arkext;
               endif;
               if arbhis <> *blanks;
               // Bane for historikk
                 arcane = arbhis;
               endif;
               xpos = %scan ('.' : arlink : 1);
               if xpos = 0;
                 ufil = '/' + %TRIM(ARCANE) + '/' + %TRIM(ARLINK) + '.pdf';
               else;
                 ufil = '/' + %TRIM(ARCANE) + '/' + %TRIM(ARLINK);
               endif;
               syreo01c3 (ufil);

               delete arkivr;
               reade x_arunde arkivl19;
             enddo;

             chain orono gssorf;
             if %found;
               delete gssorfr;
             endif;

           endif;

         endfor;
       endif;

       endsr;

       //=====================================================================
       // Trip
       //=====================================================================
       begsr srsst;

       clear MyPoDS2;
       SqlStm = 'Select TRTNO, TRST, TRDTL ' +
                'from GSSTRF ' +
                'Where TRDTL <> 0 and TRDTL <= ' +
                %trim(udtf);
       Exec SQL PREPARE DynSel2 from :SqlStm;
       EXEC SQL DECLARE  A34  cursor for DynSel2;
       Exec SQL OPEN A34;
       Myrows2 = %elem(MyPoDS2);
       exec sql FETCH A34   for :MyRows2 rows
                  INTO :MyPoDs2;
       Exec SQL CLOSE A34;

       if myrows2 > 0;
         for x = 1 to myrows2;
           if mypods2(x).trst = *blanks;
             xslutt = 'J';
             leave;
           endif;

           trtno  = mypods2(x).trtno;
           trst   = mypods2(x).trst;
           trdtl  = mypods2(x).trdtl;

           if trst = '6' or trst = 'D';

             exec sql
             delete from gsstwf
             where twtno = :trtno
             with NC;

             exec sql
             delete from gsslgtf
             where tltno = :trtno
             with NC;

             exec sql
             delete from hlbfilt
             where ortno = :trtno
             with NC;

             // slett arkiv
             aravd = trtno_avd;
             aropd = trtno_opd;
             // Sjekk om oppdrag ligger i andre modul. Ved N sletter KUN oppdrag.
             syreo01r3 (uavd: uopd: utyp: uok);
             setll (aravd:aropd) arkivl17;
             reade (aravd:aropd) arkivl17;
             dow not %eof;
               if uok = 'J';
                 chain (fifirm) firmarc;
                 chain (artype) arktxt;
                 if %found and arklag <> *blanks;
                   chain (fifirm:arklag) arkext;
                 endif;
                 if arbhis <> *blanks;
                 // Bane for historikk
                   arcane = arbhis;
                 endif;
                 xpos = %scan ('.' : arlink : 1);
                 if xpos = 0;
                   ufil = '/' + %TRIM(ARCANE) + '/' + %TRIM(ARLINK) + '.pdf';
                 else;
                   ufil = '/' + %TRIM(ARCANE) + '/' + %TRIM(ARLINK);
                 endif;
                 syreo01c3 (ufil);

                 delete arkivr17;
               else;
                 update arkivr17;
               endif;
               reade (aravd:aropd) arkivl17;
             enddo;

             chain trtno gsstrf;
             if %found;
               delete gsstrfr;
             endif;

           endif;

         endfor;
       endif;

       endsr;

      //***********************************************
       begsr init;
      //***********************************************
       xdtf = %float(udtf);
       xdtp = %float(udtp);
       x_arunde1 = 'SS:';
       utyp = 'OPD';

       SqlStm = 'Select * ' +
                'from FIRM ' +
                'FOR READ ONLY ';
       Exec SQL PREPARE DynSel0 from :SqlStm;
       EXEC SQL DECLARE  A30  cursor for DynSel0;
       Exec SQL OPEN A30;
       Myrows0 = %elem(MyPoDS0);
       exec sql FETCH A30   for :MyRows0 rows
                  INTO :MyPoDs0;
       Exec SQL CLOSE A30;
       FIFIRM = mypods0(1).fifirm;

       endsr;

      /end-free
