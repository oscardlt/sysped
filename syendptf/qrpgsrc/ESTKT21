      *  Obs ! Kompiler med tillat error 20  GENLVL(20)    (decedit angitt 2 ganger-siste ignoreres)
      * BASIS = STSW04RM
      *********************************************************************
      *  After compiling this RPG MODULE,
      *  create the related program with the following command:
      *
CRTPG+*  CRTPGM SYSPED/ESTKT21 MODULE(*LIBL/ESTKT21 *LIBL/INCGI)
CRTPGM*         ACTGRP(*NEW) AUT(*USE)
      *
      *
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 JOV PTF12 UncompleteInn-tag innført
10XXX * 02 JOV PTF12 Location parametr innført
********************************************************************
      *
     H decedit('0.')
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
      *--------------------------------------------------------------------
      * Data base files
      *--------------------------------------------------------------------
     FBRIDF     if   e           k disk    usropn
     Fstsopd3   if   e           K DISK    usropn
     Fstscli    if   e           K DISK    usropn
     Fheadf     if   e           k disk    usropn
N03  FTRACKL02  IF   E           K DISK    USROPN
N03  FFREETXT2  IF   E           K DISK    USROPN
Nxx  FDOKUFN    IF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Prototype defintions and standard system API error structure
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      *--------------------------------------------------------------------
      * Variables common to CGI programs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
      * Variables received from the input string
     DwsPro            s              8  0
     DTN1              s              8
     DwsPro1           s              8  0
     DTN2              s              8
     DwsPro2           s              8  0
     DTN3              s              8
     DwsPro3           s              8  0
     DBILN             s             10
     D AntLest         s              5  0
     D quit            s              4
     D UserId          s             10
     D User            s             10
N01  D VERSJ           s             10
     D TERM            s             20
     D TERM5           s              5
N02  D Loc             s              5
      * andre variabler
     D Fn              s             10
     D Lib             s             10
     D Mbr             s             10
     D GmAvOp          s             10
     D antclk          s              5  0
     D antclm          s              5  0
     D CliFinnes       s              1
     D JSONstring      s          32000
N02  D ErrMsg          S            150
N02  D InfoMsg         S            150
N02  D SuccessMsg      S            150
      *
     D                 DS
     D  HXSNDN                 1     20
     D  Sndnr1                 1     10
     D  Sndnr2                11     20
     D                 DS
     D  Henas                  1     30
     D  xxnas                  1      6
     D                 DS
     D  Henak                  1     30
     D  xxnak                  1     10
     D                 DS
     D  Headk3                 1     30
     D  xxadk3                 1     10
     D                 DS
     D  XDT                    1      8  0
     D  XDTÅÅ                  1      4  0
     D  XDTMM                  5      6  0
     D  XDTDD                  7      8  0
     D                 DS
     D  XTIME                  1     14  0
     D  XTID                   1      4  0
     D  XDD                    7      8  0
     D  XMM                    9     10  0
     D  XÅÅ                   11     14  0
      *
     D Spaces          s             12a   inz('&nbsp;&nbsp;')
      *--------------------------------------------------------------------
      * Prolog common to CGI programs
      * -Receive input from the remote browser
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prolog3
      *=====================================================================******
      * MAIN PROCESS LINE
      *=====================================================================******
      * Parse input string
     C                   eval      TN1      = zhbgetvar('TN1   ')
     C                   eval      TN2      = zhbgetvar('TN2   ')
     C                   eval      TN3      = zhbgetvar('TN3   ')
     C                   eval      WSPRO    = c2n2(TN1   )
     C                   eval      WSPRO1   = c2n2(TN1   )
     C                   eval      WSPRO2   = c2n2(TN2   )
     C                   eval      WSPRO3   = c2n2(TN3   )
     C                   eval      TERM     = zhbgetvar('TERM')
     C                   eval      TERM5    = TERM
N02  C                   eval      LOC      = zhbgetvar('LOC')
N02   * Lokasjon xxINT (eks DRINT) = Inn Torg Drammen og skal overstyre type/term i STS-filer
N02  C                   if        %subst(LOC:3:3)='INT'
N02  C                   eval      TERM5    = LOC
N02  C                   endif
     C                   eval      BILN   = zhbgetvar('BILN')
     C                   eval      userid = zhbgetvar('user')
N01   * versjonsnr av klientprogramvare
N01  C                   eval      VERSJ = zhbgetvar('V')
      * Set libl/open files
     C                   exsr      Init
      * Set output skeleton html member name
     c                   callp     gethtmlifs(
     C                             '/syspeddev/html/JSON.html':'/CGITAG/')
     C                   callp     wrtsection('top')
      *
      * Hent/vis sendinger
     C                   exsr      HSEND
      *
      *
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      *
     C                   close     BRIDF
     C                   close     stsopd3
     C                   close     stscli
     C                   close     headf
N03  C                   close     TRACKL02
N03  C                   close     FREETXT2
Nxx  C                   close     DOKUFN
     C                   eval      *inlr = *on
     C                   RETURN
      *
     C***********************************************
     C     HSEND         BEGSR
     C***********************************************
      /free
        JSONstring='{'
        +'¬user¬ : ¬'+%trim(USERID)+'¬, '
        +'¬tn1¬ : ¬'+%trim(TN1)+'¬, '
        +'¬tn2¬ : ¬'+%trim(TN2)+'¬, '
        +'¬tn3¬ : ¬'+%trim(TN3)+'¬, '
        +'¬term¬ : ¬'+%trim(TERM)+'¬, '
        +'¬term5¬ : ¬'+%trim(TERM5)+'¬, '
        +'¬orderList¬ : [';
      /end-free
     c                   call      'JSONFIX'
     c                   parm                    JSONstring
     C*                  callp     updHTMLvar('JSONstring':JSONstring)
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')
      *
      * tur 1
     C                   exsr      HSEND2
      * + tur 2
     c     wspro2        ifne      *zeros
     c                   move      wspro2        wspro
     C                   exsr      HSEND2
     c                   end
      * + tur 3
     c     wspro3        ifne      *zeros
     c                   move      wspro3        wspro
     C                   exsr      HSEND2
     c                   end
     c                   move      wspro1        wspro
      *
     C                   callp     wrtsection('tablebot')
     c                   eval      jsonstring =']'
      /free
        JSONstring='], '
        +'¬errMsg¬ : ¬'+%trim(ErrMsg)+'¬, '
        +'¬infoMsg¬ : ¬'+%trim(InfoMsg)+'¬, '
        +'¬successMsg¬ : ¬'+%trim(SuccessMsg)+'¬}';
      /end-free
     c                   call      'JSONFIX'
     c                   parm                    JSONstring
     C*                  callp     updHTMLvar('JSONstring':JSONstring)
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')
      *
     C                   ENDSR
      *
     C***********************************************
     C     HSEND2        BEGSR
     C***********************************************
      *
     C     TurKey        SETLL     stsopd3
     C     TurKey        READE     stsopd3                                94
     C     *IN94         DOWEQ     '0'
     c     heakey        chain     headf                              33
     C                   if        Term = *blanks and                           UTgående
     C                             hepro <> sopro                               tur MÅ stemme
     C                   goto      NEXT
     C                   end
N04  c     HXSNDN        ifeq      *blanks
N04  c                   move      HEAVD         HEAVDA            4
N04  c                   move      HEOPD         HEOPDA            7
N04  c                   eval      HXSNDN = HEAVDA+'_'+HEOPDA
N04  c                   endif
      * ant. clid lest
Nxxx c                   move      *blanks       XXLocO            5
     c                   Move      *zeros        antclk
     c                   Move      *zeros        antclm
     C                   Move      'N'           clifinnes
     C     cliKey        SETLL     stscli
     C     cliKey        READE     stscli                                 34
     C  N34              Move      'J'           clifinnes
     C     *IN34         DOWEQ     '0'
     c     Scstat        ifne      ' '
     c                   add       1             antclk
Nxx  c                   else
Nxx  c                   exsr      GetLoc
     c                   end
     C     cliKey        READE     stscli                                 34
     C  N34              ENDDO
      *
     c     CliFinnes     ifeq      'J'
     C     HENT          sub       antclk        antclm
     c     Antclm        iflt      *zeros
     C                   move      *zeros        antclm
     c                   end
     c                   end
      *
N02  c                   move      'false'       URGKOD            5
N03  C                   MOVE      'URG'         XXACTI
N03  c     TTKey         Chain     TRACKl02                           33
N03  c                   if        *in33=*off and %subst(TTTEXT:1:8)='*URGENT*'
N02  c                   move      'true '       URGKOD
N02  c                   endif
N02  c                   move      'false'       TRAMSG            5
     C                   MOVE      'G'           XXSKKO
N03  c     FREETK        Chain     FREETXT2                           33
N03  c                   if        *in33=*off
N02  c                   move      'true '       TRAMSG
N02  c                   endif
TOTEN * Ved utgående - Hvis ikke ankommet terminal (rett) vis info forran navn..
TOTENc                   move      'false'       uncompIn          5
TOTENC                   if        BIFIRM = 'TT'
TOTENC                   if        Term = *blanks
TOTENC                             and TstTerm <> *blanks
TOTENC                   MOVE      'TEI'         XXACTI
TOTENc     TTkey         setll     TRACKL02
TOTENc     TTkey         reade     TRACKL02
TOTENc                   dow       not %eof (TRACKL02)
TOTENc     TstTerm       scan      TTTEXT                                 33
TOTENc                   if        *in33 = *on
TOTENc                   leave
TOTENc                   endif
TOTENc     TTkey         reade     TRACKL02
TOTENc                   enddo
TOTEN * hvis eof betyr det at det IOKKE var TEI på aktuell term
TOTENc                   if        %eof (TRACKL02)
TOTENC                   eval      HENAK = '(*)'+HENAK
TOTENc                   move      'true '       uncompIn          5
TOTENC                   endif
TOTENC                   endif
TOTENC                   endif
      * Ved utgående vis opppdragets reelle turner / ved inngående vis løpenummr det ligger på
     c                   move      HEPRO         TURNR             8 0
     c                   if        TERM <> *blanks
     c                   move      WSPRO1        TURNR             8 0
     c                   endif
      *
      /free
          // Lægg ut Json stræng
          // Skriv en JSON-Array-linje pr sending - komma før hvert nytt element
            AntLest = AntLest + 1;
            if AntLest=1;
              JSONstring=*blanks;
            else;
              JSONstring=', ';
            endif;
            JSONstring=%trim(JSONstring)+'{'
              +'¬turnr¬ : ¬'+%trim(%editc(TURNR:'X'))+'¬, '
              +'¬heavd¬ : ¬'+%trim(%editc(HEAVD:'3'))+'¬, '
              +'¬heopd¬ : ¬'+%trim(%editc(HEOPD:'3'))+'¬, '
              +'¬hxsndn¬ : ¬'+%trim(hxsndn)+'¬, '
              +'¬henas¬ : ¬'+%trim(henas)+'¬, '
              +'¬heads1¬ : ¬'+%trim(heads1)+'¬, '
              +'¬heads3¬ : ¬'+%trim(heads3)+'¬, '
              +'¬henak¬ : ¬'+%trim(henak)+'¬, '
              +'¬headk1¬ : ¬'+%trim(headk1)+'¬, '
              +'¬headk3¬ : ¬'+%trim(headk3)+'¬, '
              +'¬urgent¬ : '+%trim(urgkod)+', '
N01           +'¬uncompleteIn¬ : '+%trim(uncompIn)+', '
              +'¬transpmsg¬ : '+%trim(traMsg)+', '
Nxx           +'¬spesLoc¬ : ¬'+%trim(XXLocO)+'¬, '
              +'¬hent¬ : '+%trim(%editc(HENT:'3'))+','
              +'¬hevkt¬ : '+%trim(%editc(HEVKT:'3'))+','
              +'¬hem3¬ : '+%trim(%editc(HEM3:'L'))+','
              +'¬helm¬ : '+%trim(%editc(HELMLA:'L'))+','
              +'¬antcll¬ : '+%trim(%editc(HENT:'3'))+','
              +'¬sostat¬ : ¬'+%trim(SOSTAT)+'¬, '
              +'¬antcllk¬ : '+%trim(%editc(AntClk:'3'))+','
              +'¬antcllm¬ : '+%trim(%editc(AntClm:'3'))
              +'}';
      /end-free
     c                   call      'JSONFIX'
     c                   parm                    JSONstring
     C*                  callp     updHTMLvar('JSONstring':JSONstring)
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')

     C     Next          tag
     C     TurKey        READE     stsopd3                                94
     C  N94              ENDDO
      *
     C                   ENDSR
      *
      *=====================================================================******
      * Set Keys mm
      *=====================================================================******
     C     init          begsr
      *=====================================================================******
      *
     C                   open      BRIDF
     C     UserID        CHAIN     BRIDF                              30
      * --------------------------------------
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C* (bedre ressursmessig). MODUL må da være med comp. av dette pgm!!
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
     C                   open      stsopd3
     C                   open      stscli
     C                   open      headf
N03  C                   open      TRACKL02
N03  C                   open      FREETXT2
Nxx  C                   open      DOKUFN
      *
      *  keys mm
     C     TURKEY        KLIST
     C                   KFLD                    TERM5
     C                   KFLD                    WSPRO
     C     CLIKEY        KLIST
     C                   KFLD                    sotype
     C                   KFLD                    sopro
     C                   KFLD                    soavd
     C                   KFLD                    soopd
     C     HEAKEY        KLIST
     C                   KFLD                    soavd
     C                   KFLD                    soopd
N03  C     TTKey         KLIST
N03  C                   KFLD                    HEAVD
N03  C                   KFLD                    HEOPD
N03  C                   KFLD                    XXFBNR            3 0
N03  C                   KFLD                    XXACTI            3
     C     FREETK        KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C                   KFLD                    XXSKKO            1
TOTEN * Ved utgående - sett Tstterm basert på turnr
TOTENC                   if        BIFIRM = 'TT'
TOTENC                   if        Term = *blanks
TOTENC                   if        WSPRO>=80000000 and WSPRO>=89999999
TOTENC                   move      'STOKKE'      TstTerm           6
TOTENC                   else
TOTENC                   move      'RAUFOS'      TstTerm           6
TOTENC                   endif
TOTENC                   endif
TOTENC                   endif
     C                   endsr
Nxx  C*****************************************************************
Nxx  C     GetLoc        BEGSR
Nxx  C*****************************************************************
Nxx   * Ligger kolliet på spesifik lokasjon  / er kolli på samme oppdrag Mixed??
Nxx  c                   eval      FNCLID = %subst(SCCLID:3)
Nxx   * finn siste DOKUFN / location
Nxx   * (her burde også ligg test på om det er på samme TERMINAL)
Nxx  c                   move      *blanks       XXLocD            5
Nxx  c     FNCLID        setll     DOKUFN
Nxx  c     FNCLID        reade     DOKUFN                                 33
Nxx  C     *in33         doweq     '0'
Nxx   * ....+... 10...+... 20...+... 30
Nxx   * Lagt på lokasjon UTE01
Nxx  C                   if        FNSTAT = 'L'
Nxx  c                   eval      XXLocD = %subst(FNTEXT:18:5)
Nxx  C                   endif
Nxx  c     FNCLID        reade     DOKUFN                                 33
Nxx  C                   enddo
Nxx   *
Nxx  c                   if        XXLocO=*blanks
Nxx  c                   eval      XXLocO=XXLocD
Nxx  c                   else
Nxx  c                   if        XXLocO<>XXLocD
Nxx  c                   eval      XXLocO='*MIX*'
Nxx  c                   endif
Nxx  c                   endif
Nxx   *
Nxx  C                   ENDSR
