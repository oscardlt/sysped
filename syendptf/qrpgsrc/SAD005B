********************************************************************
     ** RETTINGER I DETTE PROGRAM:
PTFnr *Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 YBC PTF10 OPSJON 8 = FULL MODULS
10XXX * 02 YBC PTF10 ØREJUSTERING PÅ FAKTURASUM
10XXX * 03 SH  PTF10 sikrer mot feil i SI27
10262 * 04 YBC PTF10 SATS ER UTVIDERT TIL 7.2
10XXX * 05 YBC PTF10 UTVIDER TIL 27X132, INGEN MERKING
10286 * 06 YBC PTF10 VEKT ER ENDRET FRA 5.0 TIL 12.3
      *              VAREBESKRIVELSE ER ØKT FRA 15A TIL 30A
11174 * 07 YBC PTF11 TILLEGGSLINJER MED SAMME POST
11174 * 08 SH  PTF11 artikkelnummer se skjermbildet
xxxxxx*xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
12XXX * 09 YBC PTF12 IKKE TA MED SLETTET LINJER
12053 * 10 YBC PTF12 VIS VAREBESKRIVELSE
12XXX * 11 YBC PTF12 VISE FLERE AVGIFTER
12108 * 12 YBC PTF12 Prosedyre på linje-nivå
12200 * 13 YBC PTF12 CONTAINER ER I EGEN FIL
12282 * 14 YBC PTF12 4 SIFFER PROSEDYRE OG 2 SIFFER TRANSAKSJONSTYPE
********************************************************************
      *
      ***********************************************************
      *** PROGRAM SKAL SPØRRE PÅ TOLLDEK. (SAD-IMPORT) DEL-2  ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      ***********************************************************
      *
     FFIRM      IF   E           K DISK
     FHEADF     IF   E           K DISK
     FSADH      IF   E           K DISK
     FSADV      IF   E           K DISK
     FSADV2     IF   E           K DISK    RENAME(SADVR:SADV2R)
     FSADV4     IF   E           K DISK    RENAME(SADVR:SADV4R)
     FSADVJ     IF   E           K DISK
N13  FSADVCN    IF   E           K DISK
     FKODTVA    IF   E           K DISK
     FSAD005FB  CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D                 DS
s03  D* SI27                   1     27
n03  D  SI27B                  1     27
     D  SIBELC                 1     11  2
     D  SIRAB                 12     15  2
     D  SIRABX                12     15
N10  D                 DS
N10  D  SVVT01                 1     30
S11  D* SVVT0128               1     28
N11  D  SVVT0116               1     16
     D*
     C*
     C                   EXSR      INIT
     C*
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C  N31
     CANN32SADV2K        SETLL     SADV2
     C   31
     CANN32SADV4K        SETLL     SADV4
     C   32SADV1K        SETLL     SADV
     C     START1        TAG
     C  N31
     CANN32UAVD          READE     SADV2                                  94
     C   31
     CANN32UAVD          READE     SADV4                                  94
     C   32UAVD          READE     SADV                                   94
     C  N94UTDN          COMP      SVTDN                              9494
     C   94              MOVE      *BLANKS       WSNR
      *
     C     START2        TAG
      *
     C  N94              DO
     C                   DO        17
     C                   ADD       1             ZRECNO
     C                   EXSR      FLYVAR
N11  C                   EVAL      WSAVG = %TRIM(SVAKD2) + ' ' + %TRIM(SVAKD3)
N11  C                             + ' ' + %TRIM(SVAKD4) + ' ' + %TRIM(SVAKD5)
     C                   WRITE     SUBFIL
     C  N31
     CANN32UAVD          READE     SADV2                                  94
     C   31
     CANN32UAVD          READE     SADV4                                  94
     C   32UAVD          READE     SADV                                   94
     C  N94UTDN          COMP      SVTDN                              9494
     C  N94              MOVE      SVLI          XLIFF             5 0
     C  N94              END
     C                   END
     C*
     C                   Z-ADD     ZRECNO        YRECNO            5 0
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     2             POSNBR
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYVARE
     C                   ELSE
     C                   WRITE     SUBCTL
     C                   EXFMT     BUNN
     C                   END
     C*
     C***
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C                   Z-ADD     WSSUBN        XRECNO            4 0
     C     XRECNO        IFEQ      0
     C                   Z-ADD     ZRECNO        XRECNO            4 0
     C                   END
     C***
     C*
     C                   SETOFF                                       9830
     C*
     C*** F1  ---> BYTTE AVDELINGER
     C   01              Z-ADD     1             UFLAGG
     C   01              GOTO      SLUTT
     C*** F3  ---> AVSLUTTE SPØRRING
     C   03              Z-ADD     3             UFLAGG
     C   03              GOTO      SLUTT
     C*** F11 ---> SE PÅ OPPDRAG
     C   11              DO
     C                   EXSR      SEOPPD
     C                   GOTO      START3
     C                   END
     C*** F12 ---> AVBRYT OG GÅ TILBAKE TIL DEL-1
     C   12              Z-ADD     9             UFLAGG
     C   12              GOTO      SLUTT
     C*** F5  ---> VISE STARTEN AV SUBFIL
     C   05              GOTO      START
     C*** F8  ---> VELGE SORTERINGSMÅTE OG START POST FOR SUBFIL
     C   08              GOTO      START7
     C*** ROLL UP ---> BLA TIL NESTE SIDE
     C   25              DO
     C                   Z-ADD     YRECNO        ZRECNO
     C  N94SADFFK        CHAIN     SADV                               33
     C                   SETOFF                                       25
     C                   GOTO      START2
     C                   END
     C*
     C*** SJEKKE OM BRUKER HAR VALGT OPSJONER. HIVS SÅ,T UTFØRE DISSE.
     C     ZRECNO        IFNE      0
     C                   EXSR      VLHENT
     C                   END
     C*** 98 PÅ ---> BRUKER HAR IKKE VALGT OPSJON. VISE SAMME POSTER
     C***            I SUBFIL DVS. SAMME RECORDNR.
     C   98XRECNO        IFNE      0
     C                   Z-ADD     XRECNO        ZRECNO
     C                   END
     C                   GOTO      START3
     C*
     C*** VELGE SORTERINGSMÅTE OG START POST FOR SUBFIL
     C*** 31 PÅ    ---> SORTERE PÅ TARIFFNR. + LANDKODE     (SADV2)
     C*** 32 PÅ    ---> SORTERE PÅ REGISTRERINGS REKKEFØLGE (SADV)
     C*** 31,32 AV ---> SORTERE PÅ UTSKRIFTS REKKEFØLGE     (SADV4)
     C     START7        TAG
     C                   MOVE      *ZEROS        W3VNT
     C                   EXFMT     VAREL2
     C   12              GOTO      START3
     C     W3LI          COMP      0                                  3232
     C  N32              DO
     C     W3VNT         COMP      0                                      31
     C   31W3LK          COMP      *BLANKS                                31
     C                   END
     C   32              SETOFF                                       31
     C                   GOTO      START
     C*
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
     C*
     C***********************************************
     C     SEOPPD        BEGSR
     C***********************************************
     C*** SJEKKE OM OPPDRAG FINNES. HVIS IKKE VISE FEILMELDING.
     C*** ELLERS VISER OPPDRAGET.
     C     HEADFK        CHAIN     HEADF                              30
     C  N30              DO
     C                   Z-ADD     0             UFLAGG
     C                   CALL      'SYFM21A'
     C                   PARM                    FIFIRM
     C                   PARM                    UAVD
     C                   PARM                    UTDN
     C                   PARM                    UFLAGG
     C     UFLAGG        IFEQ      0
     C                   CALL      'SYFM21B'
     C                   PARM                    FIFIRM
     C                   PARM                    UAVD
     C                   PARM                    UTDN
     C                   PARM                    UFLAGG
     C                   END
     C                   END
     C*
     C   30              MOVE      'SPO0032'     MSGNR
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     FLYVAR        BEGSR
     C***********************************************
     C*** PGA. AT DET IKKE ER NOK PLASS TIL Å VISE ALLE VIKTIGE OPPL.
     C*** PÅ EN LINJE PÅ SKJERMEN, VISER BARE 5 SIFRE PÅ BETTO VEKT
     C*** 3 SIFRE PÅ MENGDE, 5 SIFRE PÅ AVGIFT BELØP OG REGISTRERINGS
     C*** REKKEFØLGE ELLER UTSKRIFT REKKEFEØLGE.
     C*** 27 PÅ ---> TILLEGGS LINJE.
     C     SVVKTB        COMP      0                                      27
     C  N32              MOVE      SVLN          WXLN
     C   32              MOVE      SVLI          WXLN
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     VLHENT        BEGSR
     C***********************************************
     C*** SJEKKE OM BRUKER HAR VALGT NOEN OPSJONER. HVIS IKKE, SETTER
     C*** IND. 98 PÅ OG GIR FEILMELDING TIL BRUKER.
     C*** OPSJON 5 ---> VISE DETALJER AV POST.
     C*** LINNBR, POSNBR ---> RETUR POSISJON AV CURSOR
     C                   SETOFF                                       919299
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C   98              DO
     C   99              SETOFF                                       98
     C   98              MOVE      'DIV0012'     MSGNR
     C   98              SETON                                            30
     C                   Z-ADD     1             LINNBR
     C                   GOTO      SLHENT
     C                   END
     C     ZRECNO        DIV       17            LINNBR
     C                   MVR                     XLIN              2 0
     C     XLIN          IFEQ      0
     C                   Z-ADD     17            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
N01  C     WSNR          IFEQ      '8'
N07  C     WSNR          OREQ      '5'
N01  C                   MOVE      ' '           WSNR
N01  C     SVVKTB        IFEQ      0
N01  C                   SETON                                        2738
N01  C                   ELSE
N01  C                   SETOFF                                       2738
N01  C                   END
N11  C                   EVAL      WSAVG = %TRIM(SVAKD2) + ' ' + %TRIM(SVAKD3)
N11  C                             + ' ' + %TRIM(SVAKD4) + ' ' + %TRIM(SVAKD5)
N01  C                   UPDATE    SUBFIL
N01  C                   EXSR      FULMOD
N01  C   12              GOTO      SLHENT
N01  C                   END
     C                   END
     C*
     C     SLHENT        TAG
     C                   ADD       5             LINNBR
     C                   Z-ADD     2             POSNBR
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     FINSUM        BEGSR
     C***********************************************
     C*** SUMMERE TOLLVERDIER PÅ HVER ENKEL POST OG REGNE DIFFERANSE.
     C*** OMREGNE FAKTURASUM TIL NOK FOR Å VISE PÅ SKJERMEN.
     C     SIBEL7        SUB       SIBEL6        W2AVGA
     C     W2AVGA        SUB       SIBEL5        W2AVGA
     C                   MOVE      *ZEROS        W2VSUM
     C     SADVK         SETLL     SADV
     C                   SETOFF                                       33
     C*
     C     STFS          TAG
     C     UAVD          READE     SADV                                   33
     C  N33UTDN          COMP      SVTDN                              3333
     C  N33              DO
N09  C                   IF        SVPREÆ <> 'S'
     C                   ADD       SVBELT        W2VSUM
N09  C                   END
     C                   GOTO      STFS
     C                   END
     C*
     C     SIBEL3        SUB       W2VSUM        W2DIFF
     C*
     C     SIMI          IFNE      'I'
     C     SIBELR        IFNE      0
     C     KODV2K        CHAIN     KODTVA                             33
     C     SIBELR        MULT      SIVKU         XWORK            16 5
     C   33SIVALR        IFEQ      'IEP'
     C                   Z-ADD     1             KVAOMR
     C                   ELSE
     C                   Z-ADD     100           KVAOMR
     C                   END
S02  C*    XWORK         DIV       KVAOMR        UFAKNO
N02  C     XWORK         DIV(H)    KVAOMR        UFAKNO
     C                   Z-ADD     SIBELS        WSBEL4
     C                   ELSE
     C                   Z-ADD     SIBEL4        WSBEL4
     C     SIBEL3        IFNE      0
     C     KODVAK        CHAIN     KODTVA                             33
     C     SIBEL3        MULT      SIVKU         XWORK            16 5
     C   33SIVAL3        IFEQ      'IEP'
     C                   Z-ADD     1             KVAOMR
     C                   ELSE
     C                   Z-ADD     100           KVAOMR
     C                   END
S02  C*    XWORK         DIV       KVAOMR        UFAKNO
N02  C     XWORK         DIV(H)    KVAOMR        UFAKNO
     C                   END
     C                   END
     C                   END
     C*
     C                   ENDSR
     C*
N01   ***********************************************
N01  C     FULMOD        BEGSR
N01   ***********************************************
     C                   EVAL      XLI = SVLI
N01  C     SADVK         CHAIN     SADV                               33
N13   *
N13  C     SADVK         CHAIN     SADVCN                             33
N13  C   33              MOVE      *BLANKS       X_SVCNR          17
N13  C  N33              EVAL      X_SVCNR = SVCNR
N13  C  N33SADVK         READE     SADVCN                                 33
N13  C  N33              EVAL      *IN36 = *ON
N13  C   33              EVAL      *IN36 = *OFF
N13  C                   EVAL      SVCNR = X_SVCNR
N01   *
N01  C     SADVK         CHAIN     SADVJ                              33
N01  C     *IN33         IFEQ      '1'
N01  C                   MOVE      SIVAL3        SJVAL
N01  C                   MOVE      *ZEROS        SJBEL
N01  C                   MOVE      *ZEROS        SJPROS
N01  C                   MOVE      *ZEROS        SJBELN
N01  C                   END
N01   *
N01  C     SLHENTSV      TAG
N01  C                   EXFMT     BILDE
N13  C                   IF        *IN18
N13  C                   MOVE      '0'           UTYPECN
N13  C                   CALL      'SAD068'
N13  C                   PARM      SVAVD         UAVDCN            4 0
N13  C                   PARM      SVTDN         UTDNCN            7 0
N13  C                   PARM      SVLI          ULICN             5 0
N13  C                   PARM                    UCNR             17
N13  C                   PARM                    UTYPECN           1
N13  C                   GOTO      SLHENTSV
N13  C                   END
N01   *
N01  C                   ENDSR
N01   *
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     HEADFK        KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    UTDN
     C     SADHK         KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    UTDN
     C     SADVK         KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    UTDN
     C                   KFLD                    XLI
     C     SADFFK        KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    UTDN
     C                   KFLD                    XLIFF
     C     SADV1K        KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    UTDN
     C                   KFLD                    W3LI
     C     SADV2K        KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    UTDN
     C                   KFLD                    W3VNT
     C                   KFLD                    W3LK
     C                   KFLD                    XTN
     C                   KFLD                    XPRE
     C                   KFLD                    XAS
     C                   KFLD                    XPVA
     C                   KFLD                    XMFR
     C                   KFLD                    XVF
N12  C                   KFLD                    XDP
N14  C                   KFLD                    XDP2
     C     SADV4K        KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    UTDN
     C                   KFLD                    W3LN
     C     SADVJK        KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    UTDN
     C                   KFLD                    SVLI
     C     KODVAK        KLIST
     C                   KFLD                    XVAUNI
     C                   KFLD                    SIVAL3
     C     KODV2K        KLIST
     C                   KFLD                    XVAUNI
     C                   KFLD                    SIVALR
     C*
     C     *ENTRY        PLIST
     C                   PARM                    UAVD              4 0
     C                   PARM                    UTDN              7 0
     C                   PARM                    UFLAGG            1 0
     C*
N01  C     *LIKE         DEFINE    SVVKTB        XSVVKTB
N01  C     *LIKE         DEFINE    SVVKTN        XSVVKTN
N01  C     *LIKE         DEFINE    SVNTM         XSVNTM
N01  C     *LIKE         DEFINE    SVBELT        XSVBELT
     C                   MOVE      *ZEROS        XLI               5 0
     C                   MOVE      ' '           XTN               1
     C                   MOVE      ' '           XPRE              1
S04  C*                  MOVE      *ZEROS        XAS               5 2
N04  C                   MOVE      *ZEROS        XAS               7 2
     C                   MOVE      ' '           XPVA              1
     C                   MOVE      ' '           XMFR              1
     C                   MOVE      ' '           XVF               1
N12  C                   MOVE      *ZEROS        XDP               2 0
N14  C                   MOVE      *ZEROS        XDP2              2 0
     C                   MOVE      'VA'          XVAUNI            2
     C*
     C                   MOVE      *ZEROS        SVLI
     C                   MOVE      *ZEROS        SIBELC
     C*
     C     FIFIRM        SETLL     FIRM
     C                   READ      FIRM                                   33
     C     SADHK         CHAIN     SADH                               33
n03  C     SI27          IFEQ      *BLANKS
n03  C                   MOVE      *ZEROS        SI27B
n03  C                   ELSE
n03  C                   MOVE      SI27          SI27B
n03  C                   END
     C                   EXSR      FINSUM
     C*
     C                   ENDSR
     C*
