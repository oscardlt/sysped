     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPDOC('STD' 'HELPNU' 'HLP400')
     A                                      HELP
     A                                      ALTHELP
     A          R BILDE
     A                                      CF03(03 'Avslutt')
     A                                  1  2'SYSPED/SYFAFB'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(040 DIV1402 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 62DATE
     A                                      EDTCDE(Y)
     A                                  1 72TIME
     A                                  2 19MSGCON(040 DIV1548 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            WSKOPI         8A  O  3 19COLOR(WHT)
     A N70                              5  2MSGCON(024 TRO0161 *LIBL/SYMSGF)
     A N70        WSPO           1A  B  5 29DSPATR(UL)
     A  70                                  DSPATR(PR)
     A                                      VALUES('J' 'N' 'T')
     A N70                              5 48'(J/N)'
     A                                  7  2MSGCON(024 DIV1497 *LIBL/SYMSGF)
     A            WSAVD          4Y 0B  7 29DSPATR(UL)
     A                                      EDTCDE(4)
     A  99                                  DSPATR(RI)
     A                                  9  2MSGCON(024 DIV1498 *LIBL/SYMSGF)
     A            WSSG           3A  B  9 29DSPATR(UL)
     A  98                                  DSPATR(RI)
     A                                  9 48MSGCON(010 DIV1513 *LIBL/SYMSGF)
V10  A*N70                             11  2MSGCON(024 DIV1499 *LIBL/SYMSGF)
V10  A N70                             11  2MSGCON(024 YBC1956 *LIBL/SYMSGF)
     A            WSPRO          8Y 0B 11 29DSPATR(UL)
     A                                      EDTCDE(4)
     A  70                                  DSPATR(PR)
     A  70                                  DSPATR(ND)
     A N70                             11 48MSGCON(010 DIV1518 *LIBL/SYMSGF)
V10  A N70                             12  2MSGCON(024 YBC1957 *LIBL/SYMSGF)
V10  A            WSOPD          7Y 0B 12 29DSPATR(UL)
V10  A                                      EDTCDE(4)
     A  70                                  DSPATR(PR)
     A  70                                  DSPATR(ND)
V10  A N70                             12 48MSGCON(010 DIV1518 *LIBL/SYMSGF)
     A N70                             13  2MSGCON(024 DIV1500 *LIBL/SYMSGF)
     A            WSGN          15A  B 13 29DSPATR(UL)
     A  70                                  DSPATR(PR)
     A  70                                  DSPATR(ND)
     A N70                             15  2MSGCON(024 DIV0257 *LIBL/SYMSGF)
     A N70        WSVB           1A  B 15 29DSPATR(UL)
     A                                      VALUES('K' 'J' 'N')
     A  70                                  DSPATR(PR)
     A N70                             15 48MSGCON(024 DIV1549 *LIBL/SYMSGF)
     A N70                             17  2MSGCON(024 DIV1509 *LIBL/SYMSGF)
     A            WSJBKØ         1A  B 17 29DSPATR(UL)
     A                                      VALUES(' ' 'J' 'N')
     A  70                                  DSPATR(PR)
     A  70                                  DSPATR(ND)
     A N70                             17 48MSGCON(010 DIV0259 *LIBL/SYMSGF)
     A                                 18  2MSGCON(024 DIV1510 *LIBL/SYMSGF)
     A            WSPRT1        10A  B 18 29DSPATR(UL)
     A                                 18 48MSGCON(018 DIV1550 *LIBL/SYMSGF)
     A                                 19  2MSGCON(024 DIV1510 *LIBL/SYMSGF)
     A            WSPRT2        10A  B 19 29DSPATR(UL)
     A                                 19 48MSGCON(018 DIV1551 *LIBL/SYMSGF)
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 48MSGCON(015 TBR0255 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                             24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A                                  6 34'(T=Alle på gitt tur uavhengig av a-
     A                                      vd/signatur)'
