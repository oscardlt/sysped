      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/TDKG004R MODULE(*LIBL/TDKG004R *LIBL/INCGI)
CRTPGM*        ACTGRP(CGI) AUT(*USE)
      *
      *********************************************************************
      /copy SYSPEDS/qrpgsrcpy,hspecs
      /copy SYSPEDS/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FDKXKODL01 IF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy SYSPEDS/qrpgsrcpy,prototypeb
      /copy SYSPEDS/qrpgsrcpy,usec
      /copy SYSPEDS/qrpgsrcpy,variables3
      *
      * Varible for
     D WSUSER          S             10
     D WSONAVN         S             35
     D WKOD            S             10
     D WAVG            S              1
     D WANK            S              1
     D WTRS            S              1
     D STOPP           S             35
     D JSONstring      s          32000
     D Error           S            150
     D AntLest         S              9  0
     d pos             S              5U 0

      *get input-string
      /copy syspeds/qrpgsrcpy,prolog3

      * Parse input
     C                   exsr      Parse
      * Open files etc
     C                   EXSR      INIT
      *
     c                   callp     gethtmlifs(
     C                             '/syspeddev/html/JSON.html':
     C                             '/CGITAG/')
     C                   callp     wrtsection('top')
      *
      * ............................................................................................
      * skriv JSON-Object start..(alltid '{' + x ant param. m/komma mellom (IKKE komma etter siste))
      * ............................................................................................
      *
      /free
        JSONstring='{'
        +'¬user¬ : ¬'+%trim(WSUSER)+'¬, '
        +'¬sonavn¬ : ¬'+%trim(WSONAVN)+'¬, '
        +'¬kod¬ : ¬'+%trim(WKOD)+'¬, '
        +'¬avg¬ : ¬'+%trim(WAVG)+'¬, '
        +'¬ank¬ : ¬'+%trim(WANK)+'¬, '
        +'¬trs¬ : ¬'+%trim(WTRS)+'¬, '
        +'¬errMsg¬ : ¬'+%trim(Error)+'¬, ';
      /end-free
      * JSONfix escaper " i tekst,  for deretter å bytte ¬->"
     c                   call      'JSONFIX'
     c                   parm                    JSONstring
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      *
      * ............................................................................................
      * Skriv JSON-LISTE Start (en liste er EN parameter(fra [ til   )
      * ............................................................................................
     c                   eval      JSONstring ='"customslist" : ['
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      *
      * Hæmta kontor
      *
     c     Error         ifeq      *blanks
     C     WTKUNIK       SETLL     DKXKODL01
     C     WTKUNIK       READE     DKXKODL01                              55
     C     *IN55         DOWEQ     *OFF

     C                   EVAL      POS=1
      /free
           if wkod <> *blanks;
             pos=0;
             pos = %scan (%trim(WKOD) : TKKODE);
           endif;
      /end-free
     C     POS           IFEQ      *ZEROS
     C                   GOTO      NEXT
     C                   ENDIF
     C                   EVAL      POS=1
      /free
           if wsonavn <> *blanks;
             pos=0;
             pos = %scan (%trim(WSONAVN) : TKTXTN);
           endif;
      /end-free
     C     POS           IFEQ      *ZEROS
     C                   GOTO      NEXT
     C                   ENDIF
     C                   IF        WAVG='J'
     C     TKAVG         CABNE     'J'           NEXT
     C                   ENDIF
     C                   IF        WANK='J'
     C     TKANK         CABNE     'J'           NEXT
     C                   ENDIF
     C                   IF        WTRS='J'
     C     TKTRS         CABNE     'J'           NEXT
     C                   ENDIF
     C                   Add       1             AntLest
      * ............................................................................................
      * Skriv en JSON-Array-linje pr menypunkt - komma før hvert nytt element
      * ............................................................................................
      /free
       if AntLest=1;
          JSONstring=*blanks;
          else;
          JSONstring=', ';
          endif;
       JSONstring=%trim(JSONstring)+'{'
          +'¬tkkode¬ : ¬'+%trim(TKKODE)+'¬, '
          +'¬tktxtn¬ : ¬'+%trim(TKTXTN)+'¬, '
          +'¬tkavg¬ : ¬'+%trim(TKAVG)+'¬, '
          +'¬tkank¬ : ¬'+%trim(TKANK)+'¬, '
          +'¬tktrs¬ : ¬'+%trim(TKTRS)+'¬}';
      /end-free
     C                   call      'JSONFIX'
     C                   parm                    JSONstring
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')

     C     NEXT          TAG
     C     WTKUNIK       READE     DKXKODL01                              55
     C                   ENDDO
     C     UTLES         TAG
     c                   endif
      * ............................................................................................
      * Skriv JSON-Liste/Array  Avslutning
      * ............................................................................................
     c                   eval      JSONstring =']'
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      * ............................................................................................
      * Skriv JSON-string Avsluttning
      * ............................................................................................
     c                   eval      JSONstring ='}'
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      *
      * Quit
     C                   exsr      Exit


      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Lukk fil
     C                   CLOSE     BRIDF
     C  N50              CLOSE     DKXKODL01

      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   MOVE      *ON           *INLR
     C                   return
     C                   endsr
      *
      *********************************************************
     C     Parse         Begsr
      *********************************************************
      * Get input
     C                   EVAL      WSUSER  = zhbgetvar('USER')
     C                   EVAL      WSONAVN = zhbgetvar('SONAVN')
     C                   EVAL      WKOD = zhbgetvar('KOD')
     C                   EVAL      WAVG = zhbgetvar('AVG')
     C                   EVAL      WANK = zhbgetvar('ANK')
     C                   EVAL      WTRS = zhbgetvar('TRS')
     C
     c                   endsr
      *
      *********************************************************
     C     INIT          Begsr
      *********************************************************
     C                   OPEN      BRIDF
      * Test innlogging
     C     WSUSER        CHAIN     BRIDF                              50
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C                   call      BILIBL
     C                   end
      *
     C     KEY1          KLIST
     C                   KFLD                    WTKUNIK           3
     C                   KFLD                    WKOD
     C     KEY2          KLIST
     C                   KFLD                    WTKUNIK
     C                   KFLD                    WSONAVN
     C                   EVAL      WTKUNIK='106'
      *
     C   50              eval      Error = 'Invalid userID'
     C  N50              OPEN      DKXKODL01

     c                   endsr
      *
