********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr *Sek Sig Vers Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
P6XXX * 01 YBC PTF6 SALDO ER ØKT FRA 5.0 TIL 7.0
P8058 * 02 YBC PTF8 BRUKER ARBEIDSFIL
P8XXX * 03 YBC PTF8 PLUKK VED UTGÅENDE
xxxxxx*xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
11XXX * 04 YBC PTF11 F8 FOR SØKING
********************************************************************
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HELP
     A                                      ALTHELP
      *
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  5  2VALUES(' ' '1' '4')
S02  A*           VDMN           7  0H
S02  A*           VDVN           3  0H
S02  A*           VALOK1         5A  O  5  5
S02  A*           VALOK2        20A  O  5 11
N02  A            WS2MNI         7  0H
N02  A            WS2VNI         3  0H
N02  A            WS2LC1         5A  O  5  5
N02  A            WS2LC2        20A  O  5 11
     A            WSSN          35A  B  5 32
     A  28                                  DSPATR(PR)
S02  A*           SNSN          35A  H
N02  A            WS2SN         35A  H
     A            WSPLUK         3A  O  5 70
S03  A* 27                                  DSPATR(ND)
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      SFLSIZ(0016)
     A                                      SFLPAG(0015)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
N02  A                                      CA03(03 'Oppdater')
     A                                      CA05(05 'Frisk')
N04  A                                      CA08(08 'Søk')
     A                                      CA12(12 'AVSLUTT')
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/LL016R'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(30 YBC1135 SYMSGF)
     A                                      DSPATR(HI)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                  1 70TIME
     A                                  2  2MSGCON(11 YBC1136 SYMSGF)
     A            VAANR         15A  O  2 14COLOR(BLU)
     A                                  2 32MSGCON(13 YBC1137 SYMSGF)
E01  A            UANT           7  0O  2 46COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  2 56MSGCON(15 YBC1138 SYMSGF)
E01  A            WSANT          7  0O  2 72COLOR(YLW)
     A                                      EDTCDE(4)
     A            ZRECNO         4S 0O  2 76SFLRCDNBR
     A                                      DSPATR(ND)
S03  A*N27                              3  2MSGCON(40 YBC1139 SYMSGF)
N03  A                                  3  2MSGCON(40 YBC1139 SYMSGF)
     A                                      COLOR(BLU)
S03  A* 27                              3  2MSGCON(40 YBC1140 SYMSGF)
S03  A*                                     COLOR(BLU)
     A                                  4  2MSGCON(65 YBC1141 SYMSGF)
     A                                      DSPATR(HI)
S03  A*N27                              4 70MSGCON(10 YBC1142 SYMSGF)
N03  A                                  4 70MSGCON(10 YBC1142 SYMSGF)
     A                                      DSPATR(HI)
      *
     A          R BUNN
     A                                 23  2MSGCON(40 YBC1143 SYMSGF)
     A                                      COLOR(BLU)
N04  A                                 23 44MSGCON(10 YBC2016 SYMSGF)
N04  A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                             24 40'(C) COPYRIGHT  SYSTEMA A/S  2001  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
N04   *
N04  A          R VAREL2
N04  A                                      CA12(12 'Forrige')
N04  A                                      SLNO(21)
N04  A                                      CLRL(04)
N04  A                                      OVERLAY
N04  A                                  2  2'Start fra serienr ->'
N04  A            W2SN          35A  I  2 24DSPATR(UL)
N04  A                                      DSPATR(PC)
N04  A                                  3  2'F12=Forrige'
N04  A                                      COLOR(BLU)
