      *********************************************************************
      * TRANSP.PLANLEGGING   - Opprette tur
      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/ss140 MODULE(*LIBL/ss140
CRTPGM*        *LIBL/CHECKUSR *LIBL/INCGI) ACTGRP(CGI) AUT(*USE)
      *
      *********************************************************************
      * MAIN PROGRAM FLOW
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
     FBRIDF     UF   E           K DISK    USROPN
     FBRPARF    IF   E           K DISK    USROPN
     FGSSTRF    UF   E           K DISK    USROPN
     FHEADF     UF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Prototype defintions and standard system API error structure
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      *--------------------------------------------------------------------
      * Variables common to CGI programs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
      * Variables received from the input string
     D lng             s              2
     D UsrSpcName      s             10
     D WSUSER          S             10
     D WSTUR           S             11
     D WSTUR2          S             11  0
     D WSDT            S             10
     D WSTM            S              5
     D PGM             C                   CONST('SYSPED/CHECKUSR')
     D                 DS
     D WSDT2                   1      8  0
     D WSDTÅÅ                  1      4  0
     D WSDTMM                  5      6  0
     D WSDTDD                  7      8  0
     D                 DS
     D WSTM2                   1      4  0
     D WSTMH                   1      2  0
     D WSTMM                   3      4  0
     D                 DS
     D TRTNO                   1     11  0
     D TRAVD                   1      4  0
     D TROPD                   5     11  0
     D FMELDNR         S              2
     D FEILMELDING     S             60
     D FEIL01          C                   CONST('Given date is invalid. -
     D                                      (format is HHYYMMDD)')
     D FEIL02          C                   CONST('Given time is invalid.')
      *
     D Lib             s             10
     D Fn              s             10
     D Mbr             s             10
      *
     D Spaces          s             12a   inz('&nbsp;&nbsp;')
      *
      *--------------------------------------------------------------------
      * Prolog common to CGI programs
      * -Receive input from the remote browser
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prolog3
      *=====================================================================******
      * MAIN PROCESS LINE
      *=====================================================================******
      * Parse input
     C                   exsr      Parse
      *
     C                   EXSR      INIT
     C                   CALLB     PGM
     C                   PARM                    BIBRID
     C                   PARM                    UsrSpcName
     C                   PARM                    UINN              1
     C                   IF        UINN = 'N'
     C                   GOTO      SLUTT
     C                   END
     C     WSUSER        CHAIN     BRIDF                              50
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVE      WDT           BIDTF
     C                   MOVE      WTM           BITIDF
     C                   UPDATE    BRIDFR
      *
     C                   IF        WSDT2 = 0
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVEL     WDT           WSDT
     C                   MOVEL     WDT           WSDT2
     C                   ELSE
     C                   exsr      tstfeil
     C                   end
      *
      * Set output skeleton html member name
     C                   exsr      SetSkl
      * Read skeleton output html, etc.
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
      *
      * 1-Section /$top
     C                   exsr      SetTop
     C                   callp     updhtmlvar('FEILMELDING':
     C                             FEILMELDING)
     C                   callp     updhtmlvar('FMELDNR':FMELDNR)
     C                   callp     wrtsection('top')
      *
     C     SLUTT         TAG
     C                   EXSR      EXIT
     C                   return
      *=====================================================================******
      * Test skjermbilde
      *=====================================================================******
     C     TstFeil       begsr
      * TEST DATO
     C                   MOVE      WSDTDD        UDD               2 0
     C                   MOVE      WSDTMM        UMM               2 0
     C                   MOVE      WSDTÅÅ        UÅÅ               2 0
     C                   CALL      'TESTDATO'
     C                   PARM                    UDD
     C                   PARM                    UMM
     C                   PARM                    UÅÅ
     C                   PARM                    UFLAGG            1 0
     C     UFLAGG        IFEQ      1
     C                   MOVEL     FEIL01        FEILMELDING
     C                   MOVE      '01'          FMELDNR
     C                   GOTO      UtTest
     C                   ENDIF
      *
     C                   IF        WSTMH > 23
     C                   MOVEL     FEIL02        FEILMELDING
     C                   MOVE      '02'          FMELDNR
     C                   GOTO      UtTest
     C                   ENDIF
      *
     C                   IF        WSTMM > 59
     C                   MOVEL     FEIL02        FEILMELDING
     C                   MOVE      '02'          FMELDNR
     C                   GOTO      UtTest
     C                   ENDIF
      * OPPDATER TRIP
     C     WSTUR2        CHAIN     GSSTRF                             33
     C                   IF        *IN33 = *OFF
     C                   EVAL      TRFT4 = 'Recieve date:'
     C                   CAT       WSDT:1        TRFT4
     C                   CAT       'Time:':5     TRFT4
     C                   CAT       WSTM:1        TRFT4
     C                   UPDATE    GSSTRFR
     C     HEADFK        CHAIN     HEADF                              33
     C                   IF        *IN33 = *OFF
     C                   MOVEL     WSUSER        HESGM
     C                   MOVE      WSDT2         HEDTMO
     C                   MOVE      WSTM2         HEKLMO
     C                   UPDATE    HEADFR
     C                   END
      *
     C     BrParKey      Chain     BRPARF                             33
     C                   IF        *IN33 = *OFF
     C                   MOVE      'IN '         LocInOut          3
     C                   MOVEL     PAVRDA        LocCode           5
     C                   MOVE      WSTUR2        UTUR             11
     C                   CALL      'STS113'
     C                   PARM                    UTUR
     C                   PARM                    LocInOut
     C                   PARM                    LocCode
     C                   PARM                    WSUSER
     C                   END
      *
     C                   MOVE      'Y'           AVSLUTT
     C                   END
      *
     C     UtTest        endsr
      *=====================================================================******
      * Parse input
      *=====================================================================******
     C     Parse         begsr
     C                   eval      lng     = zhbgetvar('lng')
     C                   eval      WSUSER  = zhbgetvar('USER')
     C                   EVAL      UsrSpcName  = zhbgetvar('UsrSpcName')
     C                   eval      WSTUR   = zhbgetvar('TUR')
     C                   eval      WSTUR2  = c2n2(WSTUR)
     C                   eval      WSDT    = zhbgetvar('WSDT')
     C                   eval      WSDT2   = c2n2(WSDT)
     C                   eval      WSTM    = zhbgetvar('WSTM')
     C                   eval      WSTM2   = c2n2(WSTM)
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$top
      *  (search criteria)
      *=====================================================================******
     C     SetTop        begsr
      * Repeat national language for the next invocation
     C                   callp     updhtmlvar('LNG':lng:
     C                             InitHTMLVars)
      * Color for text, background, links, visited links, active links
     C                   callp     updhtmlvar('TXTCOLOR':'black')
     C                   callp     updhtmlvar('BOLD':' ')
     C                   callp     updhtmlvar('BGCOLOR':BIFARG)
     C                   callp     updhtmlvar('LINK':'0000FF')
     C                   callp     updhtmlvar('VLINK':'FF0000')
     C                   callp     updhtmlvar('ALINK':'00FF00')
     C                   callp     updhtmlvar('TUR':WSTUR)
     C                   callp     updHTMLvar('WSDT':
     C                             %trim(%editw(WSDT2:'    .  .  ')))
     C                   callp     updHTMLvar('WSTM':
     C                             %trim(%editw(WSTM2:'  :  ')))
BODY C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updHtmlVar('USER':WSUSER)
     C                   callp     updHtmlVar('UsrSpcName':UsrSpcName)
     C                   callp     updHTMLvar('sdato':
     C                             %trim(%editw(BIDTV:'    .  .  ')))
     C                   callp     updHTMLvar('stid':
     C                             %trim(%editw(BITIDV:'  .  .  ')))
     C                   callp     updHtmlVar('AVSLUTT':AVSLUTT)
      *
     C                   endsr
      *=====================================================================******
      * Set html output skeleton member before its read into memory
      *=====================================================================******
     C     SetSkl        begsr
      *------------------
      * Set html output skeleton member
     C                   eval      lng = uppify(lng)
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC' + lng
     C                   eval      Mbr = 'SS140'
      *
     C                   endsr
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
     C                   OPEN      BRIDF
     C     WSUSER        CHAIN(N)  BRIDF                              50
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
     C                   OPEN      GSSTRF
     C                   OPEN      HEADF
     C                   OPEN      BRPARF
      *
     C     HEADFK        klist
     C                   kfld                    TRAVD
     C                   kfld                    TROPD
     C     BrParKey      klist
     C                   kfld                    PABRID
     C                   kfld                    PAKUNR
     C                   kfld                    PAID
      *
     C                   MOVE      BIBRID        PABRID
     C                   MOVE      *ZEROS        PAKUNR
     C                   MOVE      'SSFAB'       PAID
      *
     C                   MOVE      'N'           AVSLUTT           1
      *
     C                   endsr
      *=====================================================================******
      *  AVSLUTT
      *=====================================================================******
     C     EXIT          begsr
     C                   callp     wrtsection('endhtml')
     C                   callp     wrtsection('*fini')
     C                   close     BRIDF
     C                   close     GSSTRF
     C                   close     HEADF
     C                   CLOSE     BRPARF
     C                   eval      *inlr = *on
      *
     C                   endsr
