     FSYPA10FM  CF   E             WORKSTN
     FLISTE     O    F   80        PRINTER OFLIND(*INOF)
     FPALLDL09  IP   E           K DISK
     FFIRM      IF   E           K DISK
     D                 DS
     D  INNDAT                 1      8  0
     D  DAG                    1      2  0
     D  MND                    3      4  0
     D  AAR                    5      8  0
     D                 DS
     D  INNDAS                 1      8  0
     D  AARS                   1      4  0
     D  MNDS                   5      6  0
     D  DAGS                   7      8  0
     D                 DS
     D  DATO1                  1      8  0
     D  DATO1A                 1      4  0
     D  DATO1M                 5      6  0
     D  DATO1D                 7      8  0
     D                 DS
     D  DATO2                  1      8  0
     D  DATO2A                 1      4  0
     D  DATO2M                 5      6  0
     D  DATO2D                 7      8  0
     D                 DS
     D  XULTID                 1     14  0
     D  XULTM                  1      6  0
     D  UDAG                   7      8  0
     D  UMND                   9     10  0
     D  UAAR                  11     14  0
     IPALLDR        01
     I              PAPNVN                      PAPNVN        L1
     I              PAPPNR                      PAPPNR        L1
     C  N99              EXSR      INIT
     C  N99              SETON                                        99
     C     PASTAT        IFNE      'D'
     C     PASTAT        ANDNE     'B'
     C     PAKVDT        IFLE      INNDAS
     C                   ADD       PAANT         ANT               8 0
     C                   ADD       PAANT         TOT               8 0
     C     PAKVDT        IFLT      DATO2
     C                   ADD       PAANT         ANT3              8 0
     C                   ADD       PAANT         TOT3              8 0
     C                   ELSE
     C     PAKVDT        IFLT      DATO1
     C                   ADD       PAANT         ANT2              8 0
     C                   ADD       PAANT         TOT2              8 0
     C                   ELSE
     C                   ADD       PAANT         ANT1              8 0
     C                   ADD       PAANT         TOT1              8 0
     C                   END
     C                   END
     C                   END
     C                   END
     CL1 OF              DO
     CL1                 EXCEPT    PHEAD
     CL1                 END
     CL1                 EXCEPT    PDET
     CLR                 EXCEPT    PTOT
     C************************************************************
     C     INIT          BEGSR
     C************************************************************
     C                   TIME                    XULTID
     C                   MOVE      *ZEROS        INNDAT
     C                   MOVE      *ZEROS        INNDAS
     C     START         TAG
     C                   EXFMT     BILDE
     C   03              DO
     C                   SETON                                        LR
     C                   RETURN
     C                   END
     C     INNDAT        IFEQ      0
     C                   GOTO      START
     C                   END
     C     *LOVAL        SETLL     FIRM
     C                   READ      FIRM                                   33
     C                   MOVEL     FIFT          FIFT35           35
     C                   EXCEPT    PHEAD
     C*
     C                   MOVE      AAR           AARS
     C                   MOVE      MND           MNDS
     C                   MOVE      DAG           DAGS
     C*
     C                   MOVE      AAR           DATO1A
     C                   MOVE      AAR           DATO2A
     C                   MOVE      MND           DATO1M
     C                   MOVE      MND           DATO2M
     C                   MOVE      DAG           DATO1D
     C                   MOVE      DAG           DATO2D
     C*
     C* FIKSER DATO1
     C*
     C     MND           IFGT      3
     C                   SUB       3             DATO1M
     C                   ELSE
     C                   SUB       1             DATO1A
     C     MND           IFEQ      3
     C                   Z-ADD     12            DATO1M
     C                   ELSE
     C     MND           IFEQ      2
     C                   Z-ADD     11            DATO1M
     C                   ELSE
     C                   Z-ADD     10            DATO1M
     C                   END
     C                   END
     C                   END
     C*
     C* FIKSER DATO2
     C*
     C     MND           IFGT      6
     C                   SUB       6             DATO2M
     C                   ELSE
     C                   SUB       1             DATO2A
     C     MND           IFEQ      6
     C                   Z-ADD     12            DATO2M
     C                   ELSE
     C     MND           IFEQ      5
     C                   Z-ADD     11            DATO2M
     C                   ELSE
     C     MND           IFEQ      4
     C                   Z-ADD     10            DATO2M
     C                   ELSE
     C     MND           IFEQ      3
     C                   Z-ADD     9             DATO2M
     C                   ELSE
     C     MND           IFEQ      2
     C                   Z-ADD     8             DATO2M
     C                   ELSE
     C                   Z-ADD     7             DATO2M
     C                   END
     C                   END
     C                   END
     C                   END
     C                   END
     C                   END
     C                   ENDSR
     OLISTE     E            PHEAD            03
     O                       FIFT35              36
      * ØVRRIGE FELT FLYTTET 10 OPP
     O                                           50 'UTSKRIFTSDATO:'
     O                       UDAG                53
     O                                           54 '-'
     O                       UMND                56
     O                                           57 '-'
     O                       UAAR                61
     O                                           70 'SIDE:'
     O                       PAGE          Z     76
     OLISTE     E            PHEAD            04
     O                                           42 'PALLESALDO PR:'
     O                                           50 'DATO:'
     O                       DAG                 53
     O                                           54 '-'
     O                       MND                 56
     O                                           57 '-'
     O                       AAR                 61
     OLISTE     E            PHEAD            05
     O                                           25 '------------------------'
     O                                           49 '------------------------'
     O                                           73 '------------------------'
     OLISTE     E            PHEAD            06
     O                                            9 'KUNDENR'
     O                                           14 'NAVN'
     O                                           50 'SALDO'
     O                                           59 '3 MND'
     O                                           68 '6 MND'
     O                                           77 'ELDRE'
     OLISTE     E            PHEAD          1 07
     O                                           25 '------------------------'
     O                                           49 '------------------------'
     O                                           73 '------------------------'
     OLISTE     E            PDET           1
     O                       PAPPNR        Z      9
     O                       PAPNVN              40
     O                       ANT           LB    50
     O                       ANT1          LB    59
     O                       ANT2          LB    68
     O                       ANT3          LB    77
     OLISTE     E            PTOT              1
     O                                           25 '* TOTALER FOR LISTE *   '
     O                       TOT           LB    50
     O                       TOT1          LB    59
     O                       TOT2          LB    68
     O                       TOT3          LB    77
