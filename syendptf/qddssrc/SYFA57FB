      * RETTINGER I DETTE PROGRAM:
PTFnr *Sek Sig Vers Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
P12XX * 01 TH  PTF12 NY HELPFUNKSJON  KUN DDS
********************************************************************
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      CF03(03 'Exit')
N01  A                                      HLPPNLGRP(HLPSCR SYFA57BPNL)
N01  A                                      HLPTITLE('VEDLIKEHOLD AV KUNDES SAM-
N01  A                                      LEFAKTURAPROFIL')
     A                                      ALTHELP
     A                                      HELP
     A          R NOREC
     A                                      TEXT('Select')
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A                                      CF12(12)
     A                                      CF06(06 'LAG NY')
     A                                      CF07(07 'LES CUNDL07')
     A                                      CF08(08 'LES CUSAPR')
     A                                      CF09(09 'LES KUN AVD.KODE')
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/SYFA57B'
     A                                      COLOR(BLU)
     A                                  1 20MSGCON(045 JOV0444 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 73DATE
     A                                      EDTCDE(Y)
     A  41                              2 20MSGCON(045 JOV0459 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A  42                              2 20MSGCON(045 JOV0460 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A  43                              2 20MSGCON(045 JOV0461 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  2 73TIME
     A                                  3  2MSGCON(040 DIV2055 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(078 JOV0467 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  1MSGCON(078 JOV0445 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            TOPOPT         1A  B  6  2VALUES(' ' '1' '2' '3' '4' '5' '9')
     A            WSKN      R        B  6  5REFFLD(CUSAPRR/CPKN *LIBL/CUSAPR)
     A                                      EDTCDE(4)
     A  51                                  DSPATR(PR)
     A  51                                  COLOR(WHT)
     A N41                             22 21MSGCON(018 JOV0462 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N42                             22 41MSGCON(018 JOV0463 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N43                             22 61MSGCON(018 JOV0464 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23  2MSGCON(010 DIV2056 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 17MSGCON(018 DIV0022 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A          R SUBFIL                    SFL
     A                                      TEXT('SUBFILE')
     A            WSNR           1A  B  7  2VALUES(' ' '2' '3' '4' '5' '9')
     A            KUNDNR    R        O  7  5REFFLD(CUSAPRR/CPKN *LIBL/CUSAPR)
     A                                      EDTCDE(4)
     A            KNAVN         30   O  7 14
     A            SFAKT     R        O  7 45REFFLD(KUNDR/SFAKT *LIBL/CUNDF)
     A            XXSTD          4A  O  7 48
     A            XXSP      R        O  7 53REFFLD(CUSAPRR/CPSP *LIBL/CUSAPR)
     A            XXSPAK    R        O  7 56REFFLD(CUSAPRR/CPKO *LIBL/CUSAPR)
     A            XXLAY         11A  O  7 58
     A            XXKO      R        O  7 70REFFLD(CUSAPRR/CPKO *LIBL/CUSAPR)
     A            XXKOAK    R        O  7 73REFFLD(CUSAPRR/CPKO *LIBL/CUSAPR)
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0016)
     A                                      SFLPAG(0015)
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      TEXT('SUBCTL')
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                      CF05(05 'Forny')
     A                                      CF06(06 'LAG NY')
     A                                      CF07(07 'LES CUNDL07')
     A                                      CF08(08 'LES CUSAPR')
     A                                      CF09(09 'LES KUN AVD.KODE')
     A                                      CF12(12 'Annuller')
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/SYFA57B'
     A                                      COLOR(BLU)
     A                                  1 20MSGCON(045 JOV0444 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 73DATE
     A                                      EDTCDE(Y)
     A  41                              2 20MSGCON(045 JOV0459 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A  42                              2 20MSGCON(045 JOV0460 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A  43                              2 20MSGCON(045 JOV0461 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  2 73TIME
     A                                  3  2MSGCON(040 DIV2057 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A            ZRECNO         4S 0O  3 68SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  4  2MSGCON(078 JOV0467 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  1MSGCON(078 JOV0445 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            TOPOPT         1A  B  6  2VALUES(' ' '1' '2' '3' '4' '5' '9')
     A            WSKN      R        B  6  5REFFLD(CUSAPRR/CPKN *LIBL/CUSAPR)
     A                                      EDTCDE(4)
     A  51                                  DSPATR(PR)
     A  51                                  COLOR(WHT)
     A          R BOTREC
     A                                      TEXT('INFO')
     A                                      OVERLAY
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 21MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 41MSGCON(018 DIV0022 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 61MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N41                             22 21MSGCON(018 JOV0462 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N42                             22 41MSGCON(018 JOV0463 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N43                             22 61MSGCON(018 JOV0464 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A          R BLANKREC                  TEXT('Message')
     A                                      OVERLAY
     A            ZMSGTX        79A  O 24  2MSGID(&ZMSGNO SYMSGF)
     A N30                                  COLOR(WHT)
     A  30                                  COLOR(RED)
     A            ZMSGNO         7A  P
     A          R CHGREC
     A                                      TEXT('INFO')
     A                                      CF04(04 'SØK KUNDE')
     A                                      CF12(12 'Annuller')
     A                                      CF08(08 'Spesifikasjon')
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            ZMSGNO         7A  P
     A                                  1  2'SYSPED/SYFA57B'
     A                                      COLOR(BLU)
     A                                  1 20MSGCON(045 JOV0444 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 73DATE
     A                                      EDTCDE(Y)
     A                                  2 73TIME
     A                                  3  2MSGCON(008 JOV0446 *LIBL/SYMSGF)
     A            CPKN      R        B  3 11REFFLD(CUSAPRR/CPKN *LIBL/CUSAPR)
     A  56
     AON55                                  DSPATR(PR)
     A                                      EDTCDE(4)
     A  56
     AON55                                  COLOR(WHT)
     A            KNAVN         30A  B  3 20
     A  56
     AON55                                  DSPATR(PR)
     A  56
     AON55                                  COLOR(WHT)
     A            ADR1          30A  O  3 51COLOR(WHT)
     A            ADR2          30A  O  4 20COLOR(WHT)
     A            POSTNR    R        O  4 51REFFLD(KUNDR/POSTNR *LIBL/CUNDF)
     A                                      COLOR(WHT)
     A            ADR3      R        O  4 56REFFLD(KUNDR/ADR3 *LIBL/CUNDF)
     A                                      COLOR(WHT)
     A                                  6  2MSGCON(025 JOV0447 *LIBL/SYMSGF)
     A            WSKO           1A  B  6 28
     A  56                                  DSPATR(PR)
     A  56                                  COLOR(WHT)
     A                                      VALUES(' ' 'N' 'S' 'O' 'B')
     A                                  6 31MSGCON(045 JOV0448 *LIBL/SYMSGF)
     A                                  7 31MSGCON(045 JOV0449 *LIBL/SYMSGF)
     A                                  8  2MSGCON(025 JOV0450 *LIBL/SYMSGF)
     A            WSKOAK         1Y 0B  8 28EDTCDE(4)
     A  56                                  DSPATR(PR)
     A  56                                  COLOR(WHT)
     A                                  9  2MSGCON(025 JOV0451 *LIBL/SYMSGF)
     A            WSKOHE        28A  B  9 28
     A  56                                  DSPATR(PR)
     A  56                                  COLOR(WHT)
     A                                  9 58MSGCON(023 JOV0556 *LIBL/SYMSGF)
     A                                 11  2MSGCON(025 JOV0452 *LIBL/SYMSGF)
     A            WSHEN1        50A  B 11 28
     A  56                                  DSPATR(PR)
     A  56                                  COLOR(WHT)
     A            WSHEN2        50A  B 12 28
     A  56                                  DSPATR(PR)
     A  56                                  COLOR(WHT)
     A                                 13 28MSGCON(050 JOV0453 *LIBL/SYMSGF)
     A                                 14  2MSGCON(025 JOV0568 *LIBL/SYMSGF)
     A            WSFS01    R        B 14 28REFFLD(CUSAPRR/CPFS01 *LIBL/CUSAPR)
     A  56                                  DSPATR(PR)
     A  56                                  COLOR(WHT)
     A            WSFS02    R        B 14 32REFFLD(CUSAPRR/CPFS02 *LIBL/CUSAPR)
     A  56                                  DSPATR(PR)
     A  56                                  COLOR(WHT)
     A            WSFS03    R        B 14 36REFFLD(CUSAPRR/CPFS03 *LIBL/CUSAPR)
     A  56                                  DSPATR(PR)
     A  56                                  COLOR(WHT)
     A            WSFS04    R        B 14 40REFFLD(CUSAPRR/CPFS04 *LIBL/CUSAPR)
     A  56                                  DSPATR(PR)
     A  56                                  COLOR(WHT)
     A            WSFS05    R        B 14 44REFFLD(CUSAPRR/CPFS05 *LIBL/CUSAPR)
     A  56                                  DSPATR(PR)
     A  56                                  COLOR(WHT)
     A                                 15  2MSGCON(025 JOV0545 *LIBL/SYMSGF)
     A            WSSORT    R        B 15 28REFFLD(CUSAPRR/CPSORT *LIBL/CUSAPR)
     A  56                                  DSPATR(PR)
     A  56                                  COLOR(WHT)
     A            WSSONA        30A  O 15 30
     A                                 16  2MSGCON(025 JOV0454 *LIBL/SYMSGF)
     A            WSSP      R        B 16 28REFFLD(CUSAPRR/CPSP *LIBL/CUSAPR)
     A  56                                  DSPATR(PR)
     A  56                                  COLOR(WHT)
     A                                      VALUES(' ' 'J' 'N' 'Y')
     A                                 16 30MSGCON(005 JOV0455 *LIBL/SYMSGF)
     A                                 16 37MSGCON(025 JOV0456 *LIBL/SYMSGF)
     A            WSSPAK    R        B 16 64REFFLD(CUSAPRR/CPSPAK *LIBL/CUSAPR)
     A  56                                  DSPATR(PR)
     A  56                                  COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 17  2MSGCON(075 JOV0457 *LIBL/SYMSGF)
     A            WSSPH1    R        B 18  2REFFLD(CUSAPRR/CPSPH1 *LIBL/CUSAPR)
     A  56                                  DSPATR(PR)
     A  56                                  COLOR(WHT)
     A            WSSPH2    R        B 19  2REFFLD(CUSAPRR/CPSPH2 *LIBL/CUSAPR)
     A  56                                  DSPATR(PR)
     A  56                                  COLOR(WHT)
     A            WSSPH3    R        B 20  2REFFLD(CUSAPRR/CPSPH3 *LIBL/CUSAPR)
     A  56                                  DSPATR(PR)
     A  56                                  COLOR(WHT)
     A                                 21  2MSGCON(025 JOV0533 *LIBL/SYMSGF)
     A            WSFALI         1A  B 21 28TEXT('J/N skriv fakt.linjer')
     A  56                                  DSPATR(PR)
     A  56                                  COLOR(WHT)
     A                                      VALUES(' ' 'J' 'N' 'Y' 'I')
     A                                 21 30'J/N/I'
     A                                 21 37MSGCON(025 JOV0567 *LIBL/SYMSGF)
     A            WSSPSU         1A  B 21 64
     A  56                                  DSPATR(PR)
     A  56                                  COLOR(WHT)
     A                                      VALUES(' ' 'J' 'N' 'Y')
     A                                 21 66MSGCON(005 JOV0455 *LIBL/SYMSGF)
     A                                 23  2MSGCON(018 DIV0032 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 23MSGCON(018 JOV0503 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 62MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A            ZMSGTX        79A  O 24  2MSGID(&ZMSGNO SYMSGF)
     A                                      COLOR(WHT)
