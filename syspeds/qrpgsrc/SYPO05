********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
11XXX * 01 SH  PTF11 Kan grense og tabell 2
********************************************************************
      *
      *
      ***********************************************************
      *** PROGRAM FOR INTERVALLSTYRING AV POSTNUMMER          ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01 02 07 09-11 13-24
      *** LEDIGE INDIKATORER 27-29 31 32 34-39 41-85 95-97
      ***********************************************************
      *
     FSTED2D    IF   E           K DISK
     FKODTLK    IF   E           K DISK
     FSONESL02  IF   E           K DISK
     FBASES     UF A E           K DISK
     FSYPO05FM  CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
      *
     C                   EXSR      INIT
      *
     C     START0        TAG
     C                   EXFMT     BILDE01
     C                   SETOFF                                       30
     C   03              GOTO      SLUTT
     C   04              DO
     C                   CALL      'SYFI10L'
     C                   PARM                    BASLKF
     C                   PARM                    USPED
     C                   END
     C     KODLKK        CHAIN     KODTLK                             30
     C   30              MOVE      'SPL0001'     MSGNR
     C   30              GOTO      START0
      *
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
     C                   MOVE      W2SPNF        YASPNF            5
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     PRISK3        SETLL     BASES
     C     START1        TAG
     C     PRIKU         READE(N)  BASES                                  94
     C   94              MOVE      *BLANKS       BASPNF
     C   94              MOVE      *BLANKS       WSNR
      *
     C     START2        TAG
      *
     C  N94              DO
     C                   DO        16
     C                   ADD       1             ZRECNO
     C                   WRITE     SUBFIL
     C     PRIKU         READE(N)  BASES                                  94
     C  N94              MOVE      BASPNF        YASPNF
     C  N94              END
     C                   END
     C*
     C                   Z-ADD     ZRECNO        YRECNO            4 0
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYVARE
     C                   ELSE
     C                   WRITE     SUBCTL
     C                   EXFMT     BUNN
     C                   END
     C                   SETOFF                                       9830
     C*
     C*** AVSLUTT
     C   03              GOTO      SLUTT
     C*** FRISK OPP
     C   05              GOTO      START
     C*** LAGE NY
     C   06              DO
     C                   MOVEA     '0000'        *IN(87)
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     8             POSNBR
     C                   GOTO      START5
     C                   END
     C*** SØK FRA
     C   08              GOTO      START6
     C*
     C   12              GOTO      START0
     C*
     C*** BLA VIDERE
     C   25              DO
     C                   Z-ADD     YRECNO        ZRECNO
     C                   SETOFF                                       25
     C* N94      PRISK3    SETLLBASES
     C  N94              READP(N)  BASES                                  94
     C  N94              READ(N)   BASES                                  94
     C                   GOTO      START2
     C                   END
     C*
     C     ZRECNO        IFEQ      0
     C                   GOTO      START3
     C                   ELSE
     C                   EXSR      HENT
     C   98              GOTO      START3
     C                   END
     C*
     C*** SLETT VARE
     C     WSNR          IFEQ      '4'
     C                   MOVE      ' '           WSNR
     C                   EXFMT     VAREL3
     C  N12              DO
     C     PRISK2        CHAIN     BASES                              33
     C  N33              DELETE    BASESR
     C                   EXSR      MOVB1
     C                   MOVE      '*DEL*'       BASPNF
     C                   END
     C                   UPDATE    SUBFIL
     C                   GOTO      START3
     C                   END
     C*
     C*** LAGE/ENDRE
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     37            POSNBR
     C     START5        TAG
     C                   EXFMT     VARELI
     C                   MOVEA     '0000'        *IN(87)
     C                   SETOFF                                       30
      *
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
      *
     C   04              DO
     C                   EXSR      SØKDTA
     C                   GOTO      START5
     C                   END
      *
     C   12              DO
     C   40              UPDATE    SUBFIL
     C                   SETOFF                                       40
     C                   EXSR      MOVB2
     C                   GOTO      START3
     C                   END
     C                   EXSR      TESTB1
     C   30              GOTO      START5
     C     NY            IFEQ      'J'
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     8             POSNBR
     C                   GOTO      START5
     C                   END
     C                   GOTO      START3
     C*
     C*** SØK FRA
     C     START6        TAG
     C                   EXFMT     VAREL2
     C   03              GOTO      SLUTT
     C                   GOTO      START
     C*
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
     C*
     C***********************************************
     C     TESTB1        BEGSR
     C***********************************************
     C  N40              DO
     C     STEDK1        CHAIN     STED2D                             88
     C     *IN88         IFEQ      '1'
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     8             POSNBR
     C                   MOVE      'SHU0042'     MSGNR
     C                   MOVE      '1'           *IN30
     C                   GOTO      SLTB1
     C                   END
      *
     C     KODLKT        CHAIN     KODTLK                             87
     C     *IN87         IFEQ      '1'
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     23            POSNBR
     C                   MOVE      'SPL0001'     MSGNR
     C                   MOVE      '1'           *IN30
     C                   GOTO      SLTB1
     C                   END
      *
     C     STEDK2        CHAIN     STED2D                             89
     C     *IN89         IFEQ      '1'
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     26            POSNBR
     C                   MOVE      'SHU0042'     MSGNR
     C                   MOVE      '1'           *IN30
     C                   GOTO      SLTB1
     C                   END
      *
     C     PRISK         CHAIN(N)  BASES                              33
     C     *IN33         IFEQ      '0'
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     8             POSNBR
     C                   MOVE      'YBC0347'     MSGNR
     C                   MOVE      '1'           *IN88
     C                   MOVE      '1'           *IN30
     C                   GOTO      SLTB1
     C                   END
     C                   END
      *
     C     WSSSOT        SETLL     SONESL02
     C     WSSSOT        READE     SONESL02                               90
     C     *IN90         IFEQ      '1'
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     37            POSNBR
     C                   MOVE      'YBC0346'     MSGNR
     C                   MOVE      '1'           *IN30
     C                   GOTO      SLTB1
     C                   END
      *
N01  C     WSSSOL        SETLL     SONESL02
N01  C     WSSSOL        READE     SONESL02                               86
N01  C     *IN86         IFEQ      '1'
N01  C                   Z-ADD     22            LINNBR
N01  C                   Z-ADD     61            POSNBR
N01  C                   MOVE      'YBC0346'     MSGNR
N01  C                   MOVE      '1'           *IN30
N01  C                   GOTO      SLTB1
N01  C                   END
      *
     C     PRISK         CHAIN     BASES                              33
     C  N33
     CANN40              GOTO      SLTB1
     C                   EXSR      MOVWPR
     C  N33              UPDATE    BASESR
     C   33              WRITE     BASESR
     C   40              UPDATE    SUBFIL
     C  N40              ADD       1             YRECNO
     C  N40              Z-ADD     YRECNO        ZRECNO
     C  N40              WRITE     SUBFIL
     C                   WRITE     SUBCTL
     C   40              MOVE      'N'           NY                1
     C  N40              MOVE      'J'           NY
     C                   SETOFF                                       40
     C                   EXSR      MOVB2
     C*
     C     SLTB1         ENDSR
     C*
     C***********************************************
     C     HENT          BEGSR
     C***********************************************
     C                   MOVEA     '00'          *IN(91)
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C  N98WSNR          IFEQ      '2'
     C     BASPNF        COMP      '*DEL*'                                98
     C   98              GOTO      SLHENT
     C                   EXSR      MOVPRW
     C                   MOVE      ' '           WSNR
     C                   SETOFF                                       908988
     C                   SETON                                        40
     C                   GOTO      SLHENT
     C                   END
     C  N98WSNR          IFEQ      '4'
     C     BASPNF        COMP      '*DEL*'                                98
     C                   GOTO      SLHENT
     C                   END
     C                   END
     C*
     C     SLHENT        TAG
     C   98              DO
     C                   MOVE      'DIV0012'     MSGNR
     C                   SETON                                            30
     C                   END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVWPR        BEGSR
     C***********************************************
     C                   MOVE      WSSPNF        BASPNF
     C                   MOVE      WSSLKT        BASLKT
     C                   MOVE      WSSPNT        BASPNT
     C                   MOVE      WSSSOT        BASSOT
N01  C                   MOVE      WSSLMT        BASLMT
N01  C                   MOVE      WSSSOL        BASSOL
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVPRW        BEGSR
     C***********************************************
     C                   MOVE      BASPNF        WSSPNF
     C                   MOVE      BASLKT        WSSLKT
     C                   MOVE      BASPNT        WSSPNT
     C                   MOVE      BASSOT        WSSSOT
N01  C                   MOVE      BASLMT        WSSLMT
N01  C                   MOVE      BASSOL        WSSSOL
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVB1         BEGSR
     C***********************************************
     C                   MOVE      *BLANKS       BASPNF
     C                   MOVE      *BLANKS       BASLKT
     C                   MOVE      *BLANKS       BASPNT
     C                   MOVE      *BLANKS       BASSOT
N01  C                   MOVE      *ZEROS        BASLMT
N01  C                   MOVE      *BLANKS       BASSOL
     C                   ENDSR
     C*
     C***********************************************
     C     MOVB2         BEGSR
     C***********************************************
     C                   MOVE      *BLANKS       WSSPNF
     C                   MOVE      *BLANKS       WSSLKT
     C                   MOVE      *BLANKS       WSSPNT
     C                   MOVE      *BLANKS       WSSSOT
N01  C                   MOVE      *ZEROS        WSSLMT
N01  C                   MOVE      *BLANKS       WSSSOL
     C                   ENDSR
     C*
     C***********************************************
     C     SØKDTA        BEGSR
     C***********************************************
     C  N40LINNBR        IFEQ      22
     C     POSNBR        ANDGE     8
     C     POSNBR        ANDLE     12
     C                   MOVE      *BLANKS       UNVN
     C                   CALL      'SYFI10F'
     C                   PARM                    WSSPNF
     C                   PARM                    UNVN             30
     C                   PARM                    BASLKF
     C                   Z-ADD     23            POSNBR
     C                   GOTO      SLSØK
     C                   END
      *
     C  N40LINNBR        IFEQ      22
     C     POSNBR        ANDGE     23
     C     POSNBR        ANDLE     24
     C                   CALL      'SYFI10L'
     C                   PARM                    WSSLKT
     C                   PARM                    USPED
     C                   Z-ADD     26            POSNBR
     C                   GOTO      SLSØK
     C                   END
      *
     C  N40LINNBR        IFEQ      22
     C     POSNBR        ANDGE     26
     C     POSNBR        ANDLE     30
     C                   MOVE      *BLANKS       UNVN
     C                   CALL      'SYFI10F'
     C                   PARM                    WSSPNT
     C                   PARM                    UNVN             30
     C                   PARM                    WSSLKT
     C                   Z-ADD     37            POSNBR
     C                   GOTO      SLSØK
     C                   END
      *
     C     LINNBR        IFEQ      22
     C     POSNBR        ANDGE     37
     C     POSNBR        ANDLE     41
     C                   CALL      'SYPO02X'
     C                   PARM                    WSSSOT
     C                   GOTO      SLSØK
     C                   END
     C*
     C                   MOVE      'DIV1037'     MSGNR
     C                   SETON                                        30
     C*
     C     SLSØK         ENDSR
     C*
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     KODLKK        KLIST
     C                   KFLD                    LKUNIK
     C                   KFLD                    BASLKF
     C     KODLKT        KLIST
     C                   KFLD                    LKUNIK
     C                   KFLD                    WSSLKT
     C     PRIKU         KLIST
     C                   KFLD                    BASLKF
     C     PRISK         KLIST
     C                   KFLD                    BASLKF
     C                   KFLD                    WSSPNF
     C                   KFLD                    WSSLKT
     C                   KFLD                    WSSPNT
     C     PRISK1        KLIST
     C                   KFLD                    BASLKF
     C                   KFLD                    W2SPNF
     C     PRISK2        KLIST
     C                   KFLD                    BASLKF
     C                   KFLD                    BASPNF
     C                   KFLD                    BASLKT
     C                   KFLD                    BASPNT
     C     PRISK3        KLIST
     C                   KFLD                    BASLKF
     C                   KFLD                    YASPNF
     C     STEDK1        KLIST
     C                   KFLD                    BASLKF
     C                   KFLD                    WSSPNF
     C     STEDK2        KLIST
     C                   KFLD                    WSSLKT
     C                   KFLD                    WSSPNT
     C*
     C                   MOVE      'J'           USPED             1
     C                   MOVE      'LK '         LKUNIK            3
     C*
     C                   ENDSR
     C*
