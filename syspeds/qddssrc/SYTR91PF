*****A**************************************************************
     A* RETTINGER I DETTE PROGRAM:
PTFnrA*Sek Sig Vers Beskrivelse...........................
"""""A*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10261 * 01 YBC PTF10 VEKT/KUBIKK/KOLLI I TURER ER ENDRET TIL 9.0, 7.3 OG 7.0
*****A**************************************************************
     A*****************************************************************
     A*   UTSKRIFT AV RUNDTURER           (CPI = 15)
     A* ANTALL LINJE=72,  ANTALL TEGN PR. LINJE=120  OVERFLOW=72      *
     A*****************************************************************
     A          R PHEAD
     A                                  2  2MSGCON(119 JOV0003 SYMSGF)
     A                                      HIGHLIGHT
     A                                  3  2MSGCON(119 SHU0002 SYMSGF)
     A                                      HIGHLIGHT
     A            TURUND         8S 0   3 50EDTCDE(Z)
     A                                      HIGHLIGHT
     A            PAGE           2  0   3106EDTCDE(Z)
     A                                  4  2MSGCON(119 JOV0003 SYMSGF)
     A                                      HIGHLIGHT
     A                                  4 78MSGCON(15 JOV0005 SYMSGF)
     A            IDAGX          6S 0   4100EDTCDE(Y)
     A            TUNAT         30A     5 11
     A                                      HIGHLIGHT
     A                                  5 78MSGCON(15 DIV2254 SYMSGF)
     A            TUKNT2         8S 0   5100EDTCDE(Z)
     A                                  6 11MSGCON(9 DIV4021 SYMSGF)
     A            TUBILN        10A     6 24
     A                                  6 41MSGCON(11 DIV4043 SYMSGF)
     A            TUHENG        10A     6 52
     A                                  7  1'----------------------------------'
     A                                  7 35'----------------------------------'
     A                                  7 69'----------------------------------'
     A                                  7103'-----'
     A          R PDET                      SPACEB(1)
     A  49        WSAVD          4S 0O    16EDTCDE(Z)
     A  49                                16'_____________'
     A  49        WSPRO          8S 0O    21EDTCDE(Z)
     A  49        WSTXTX        65   O    32
     A N49        WSTXT         65   O    16
     A          R PDETH1                    SPACEB(1)
     A                                     1MSGCON(50 SHU0003 SYMSGF)
     A          R PDETH2                    SPACEB(1)
     A                                     1MSGCON(50 SHU0004 SYMSGF)
     A          R PDETH3                    SPACEB(1)
     A                                     1MSGCON(50 SHU0005 SYMSGF)
     A          R PDETH4                    SPACEB(1)
     A                                     1MSGCON(50 SHU0006 SYMSGF)
     A          R PDETH5                    SPACEB(1)
     A                                     1MSGCON(80 SHU0007 SYMSGF)
     A          R PDETH6                    SPACEB(1)
     A                                     1MSGCON(80 JOV0040 SYMSGF)
      *_______________________________
      * Format for AVREGNINGSDATA.
      *"""""""""""""""""""""""""""""""
     A          R ADET                      SPACEB(2)
S01  A*                                    2MSGCON(119 JOV0004 SYMSGF)
N01  A                                     2MSGCON(119 YBC2000 SYMSGF)
     A                                      HIGHLIGHT
     A                                     2'________________________'
     A                                    26'________________________'
     A                                    50'________________________'
     A                                    74'________________________'
     A                                    98'______________________'
     A* NY LINJE FOR I % AV BRUTTO.....
     A                                      SPACEB(1)
     A                                     2' '
     A            TURUND         8  0      1EDTCDE(Z)
     A                                      HIGHLIGHT
     A            TUAVD          4  0     10EDTCDE(Z)
     A                                      HIGHLIGHT
     A            TUPRO          8  0     15EDTCDE(Z)
     A                                      HIGHLIGHT
     A            TUDATO         6  0     24EDTWRD('  .  .  ')
     A                                      HIGHLIGHT
     A            TUSDF         20        33
     A                                      HIGHLIGHT
     A            TUSDT         20        54
     A                                      HIGHLIGHT
     A            TUBILN        10        76
     A                                      HIGHLIGHT
     A            TUHENG        10        86
     A                                      HIGHLIGHT
S01  A*           TUTVKT         5  0     96EDTCDE(3)
N01  A            TUTVKT         9  0     96EDTCDE(3)
     A                                      HIGHLIGHT
S01  A*N86        TUTLM          4  2    102EDTWRD(' 0,  ')
N01  A N86        TUTLM          4  2    106EDTWRD(' 0,  ')
     A                                      HIGHLIGHT
S01  A*N87        TUTM3          5  2    108EDTWRD('  0,  ')
N01  A N87        TUTM3          7  3    112EDTWRD('   0,   ')
     A                                      HIGHLIGHT
     A          R ADETST                    SPACEB(1)
     A                                     2MSGCON(15 JOV0006 SYMSGF)
     A                                      HIGHLIGHT
     A  84                                20MSGCON(18 DIV2400 SYMSGF)
     A                                      HIGHLIGHT
     A N84 89                             20MSGCON(35 JOV0013 SYMSGF)
     A                                      HIGHLIGHT
     A N84N89                             20MSGCON(35 JOV0014 SYMSGF)
     A                                      HIGHLIGHT
     A          R ADET2H                    SPACEB(1)
     A                                     2MSGCON(119 JOV0008 SYMSGF)
     A                                      HIGHLIGHT
     A          R ADET2                     SPACEB(1)
     A  51N61                              2'Avtalt/estim(A)'
     A  52N62                              2MSGCON(15 DIV2387 SYMSGF)
     A  53N63                              2MSGCON(15 DIV2388 SYMSGF)
     A  54N64                              2MSGCON(15 DIV2389 SYMSGF)
     A  55N65                              2MSGCON(15 DIV2390 SYMSGF)
     A  56N66                              2MSGCON(15 DIV2391 SYMSGF)
     A  57N67                              2MSGCON(15 DIV2392 SYMSGF)
     A            TVTXT         35        20
     A            TVOAVD         4  0     55EDTCDE(Z)
     A                                    59'/'
     A            TVOPD          7  0     60EDTCDE(Z)
     A  55
     AO 56        TVBNRB         7  0     68EDTCDE(Z)
     A  52        TVBLLB        11  2     76EDTCDE(J)
     A  52        TVPRO          5  2     91EDTWRD('   .  ')
     A            TVBLL         11  2     97EDTCDE(J)
     A          R DETTOT                    SPACEB(1)
     A                                     2MSGCON(18 JOV0042 SYMSGF)
     A                                      HIGHLIGHT
     A                                    20MSGCON(10 JOV0045 SYMSGF)
     A                                      HIGHLIGHT
     A            XBRT           9  2     29EDTCDE(J)
     A                                      HIGHLIGHT
     A                                    42MSGCON(10 JOV0043 SYMSGF)
     A                                      HIGHLIGHT
     A            XTREK          9  2     51EDTCDE(J)
     A                                      HIGHLIGHT
     A                                    65MSGCON(10 JOV0046 SYMSGF)
     A                                      HIGHLIGHT
     A            XPROV          9  2     74EDTCDE(J)
     A                                      HIGHLIGHT
     A                                    88MSGCON(15 JOV0047 SYMSGF)
     A                                      HIGHLIGHT
     A            THTOT         11  2    104EDTCDE(J)
     A                                      HIGHLIGHT
     A  34                               120'*'
     A                                      HIGHLIGHT
     A* NY LINJE FOR I % AV BRUTTO.....
     A                                     2' '
     A                                      SPACEB(1)
     A                                    65MSGCON(15 JOV0048 SYMSGF)
     A                                      HIGHLIGHT
     A            XPROS          5  2     80EDTCDE(J)
     A                                      HIGHLIGHT
     A                                    87'%'
     A                                      HIGHLIGHT
     A          R TOTA                      SPACEB(2)
     A                                     2MSGCON(16 DIV1682 SYMSGF)
     A            XMVAGR        13  2     22EDTCDE(J)
     A                                    41MSGCON(20 DIV1683 SYMSGF)
     A            PRITAX         4  2     54EDTCDE(J)
     A            XVAL           3        99
     A            XMOMS         13  2    101EDTCDE(J)
      *_________________________________________________
      * Format for faktura-total. without Norwegian giro.
      *""""""""""""""""""""""""""""""""""""""""""""""""""
     A          R TOTC                      SPACEB(2)
     A                                     2MSGCON(18 JOV0044 SYMSGF)
     A                                      HIGHLIGHT
     A                                    20MSGCON(15 JOV0045 SYMSGF)
     A                                      HIGHLIGHT
     A            XTBRT          9  2     29EDTCDE(J)
     A                                      HIGHLIGHT
     A                                    42MSGCON(10 JOV0043 SYMSGF)
     A                                      HIGHLIGHT
     A            XTTREK         9  2     51EDTCDE(J)
     A                                      HIGHLIGHT
     A                                    65MSGCON(15 JOV0046 SYMSGF)
     A                                      HIGHLIGHT
     A            XTPROV         9  2     74EDTCDE(J)
     A                                      HIGHLIGHT
     A                                    88MSGCON(15 JOV0047 SYMSGF)
     A                                      HIGHLIGHT
     A            XBRUTT        13  2    101EDTCDE(J)
     A                                      HIGHLIGHT
     A                                     2' '
     A                                      SPACEB(1)
     A                                    65MSGCON(15 JOV0048 SYMSGF)
     A                                      HIGHLIGHT
     A            XTPROS         5  2     80EDTCDE(J)
     A                                      HIGHLIGHT
     A                                    87'%'
     A                                      HIGHLIGHT
