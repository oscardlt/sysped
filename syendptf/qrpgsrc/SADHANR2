********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
12XXX * 01 YBC PTF12 Test om edi-oppdrag
********************************************************************
     h dftactgrp(*no)
     fsadhd     if   e           k disk
     fsadv      if   e           k disk
     fsadkap    if   e           k disk
     fedim10    if   e           k disk
     ftvinf     if   e           k disk
     ffrisok    if   e           k disk
     fsadhan    o  a e             disk
     f                                     prefix(W_)
     d iso1            s               d   datfmt(*iso)
     d iso2            s               d   datfmt(*iso)
     d jul             s               d   datfmt(*jul)
     d wok             s              1
N01  d x_svexr03       s                   like(svexr03)
     d                 ds
     d svvnt                          8  0
     d  svvnt2                        2    overlay(svvnt)
     d                 ds
     d f0078a                        70
     d  f0078ax                       3    overlay(f0078a)
     ‚*  Prototype for sadhanr2
     d sadhanr2        pr
     d udtfra_                             like(udtfra)
     d udttil_                             like(udttil)
     d ukns_                               like(ukns)
     d uknk_                               like(uknk)
     ‚*  *ENTRY Interface for Main Procedure
     d sadhanr2        pi
     d udtfra                         8S 0
     d udttil                         8S 0
     d ukns                           8S 0
     d uknk                           8S 0

      /free
       setll (udtfra) sadhd;
       read  sadhd;
       dow not %eof and sidt <= udttil;

         if sitdn > 0;
           if sist = 'P';

           wok = ' ';
           if ukns > 0;
             if ukns <> sikns;
               wok = 'N';
             endif;
           endif;
           if uknk > 0;
             if uknk <> siknk;
               wok = 'N';
             endif;
           endif;

           if wok = ' ';

             clear sadhanr;

               w_wie='I';
               w_siavd=siavd;
               w_sitdn=sitdn;
               w_sisg=sisg;
               w_sidt=sidt;
               w_sikns=sikns;
               w_sign=sign;
               w_siknk=siknk;
               w_sidtg=sidtg;
               w_silka=silka;
               exsr antvp;
               exsr merk;
               w_wsvexr03=svexr03;
               monitor;
                 jul = %date(%subst(sign: 3: 2)+'/' + %subst(sign: 10: 3):*jul);
                 on-error;
               endmon;
                 iso1 =  jul;
               monitor;
                 iso2 =  %date(%subst(sidtg: 1: 4) + '-' + %subst(sidtg: 5: 2) +
                         '-' + %subst(sidtg: 7: 2):*iso);
                 on-error;
               endmon;
               monitor;
               w_wantdag = %diff(iso2:iso1:*d);
                 on-error;
               endmon;
                 chain ( siavd : sitdn : '*CR') frisok;
                   if %found;
               w_wextref = fssok;
                   endif;

             write sadhanr;

           endif;
           endif;
         endif;

           read  sadhd;
       enddo;

       *inlr = *on;

        //________________________//
        // Tell antall vareposter //
        //""""""""""""""""""""""""//
          begsr antvp;
N01         x_svexr03 = *blanks;
N01         w_wsvexr03 = *blanks;
            setll (siavd : sitdn) sadv;
            reade (siavd : sitdn) sadv;
            dow not %eof;

              w_wvpreg=w_wvpreg+1;

              if svln > w_wvpoff;
                w_wvpoff=svln;
              endif;

S01         //if svexr03 <> *blanks;
N01           if (svexr03 <> *blanks or svexr02 <> *blanks)
N01              and w_wsvexr03 = *blanks;
N01             if svexr03 = *blanks;
N01               x_svexr03 = svexr02;
N01             else;
N01               x_svexr03 = svexr03;
N01             endif;
                w_wsvexr03 = 'EDI';
              endif;

              chain (svvnt2) sadkap;
                if %found;
                  select;
                    when sadkap02 = 'I    ';
                     w_wai = w_wai + 1;
                    when sadkap02 = 'II   ';
                     w_waii    = w_waii    + 1;
                    when sadkap02 = 'III  ';
                     w_WAIII   = w_WAIII   + 1;
                    when sadkap02 = 'IV   ';
                     w_WAIV    = w_WAIV    + 1;
                    when sadkap02 = 'V    ';
                     w_WAV     = w_WAV     + 1;
                    when sadkap02 = 'VI   ';
                     w_WAVI    = w_WAVI    + 1;
                    when sadkap02 = 'VII  ';
                     w_WAVII   = w_WAVII   + 1;
                    when sadkap02 = 'VIII ';
                     w_WAVIII  = w_WAVIII  + 1;
                    when sadkap02 = 'IX   ';
                     w_WAIX    = w_WAIX    + 1;
                    when sadkap02 = 'X    ';
                     w_WAX     = w_WAX     + 1;
                    when sadkap02 = 'XI   ';
                     w_WAXI    = w_WAXI    + 1;
                    when sadkap02 = 'XII  ';
                     w_WAXII   = w_WAXII   + 1;
                    when sadkap02 = 'XIII ';
                     w_WAXIII  = w_WAXIII  + 1;
                    when sadkap02 = 'XIV  ';
                     w_WAXIV   = w_WAXIV   + 1;
                    when sadkap02 = 'XV   ';
                     w_WAXV    = w_WAXV    + 1;
                    when sadkap02 = 'XVI  ';
                     w_WAXVI   = w_WAXVI   + 1;
                    when sadkap02 = 'XVII ';
                     w_WAXVII  = w_WAXVII  + 1;
                    when sadkap02 = 'XVIII';
                     w_WAXVIII = w_WAXVIII + 1;
                    when sadkap02 = 'XVIX ';
                     w_WAXVIX  = w_WAXVIX  + 1;
                    when sadkap02 = 'XX   ';
                     w_WAXX    = w_WAXX    + 1;
                    when sadkap02 = 'XXI  ';
                     w_WAXXI   = w_WAXXI   + 1;
                  endsl;

              endif;

            reade (siavd : sitdn) sadv;
            enddo;
N01         svexr03 = x_svexr03;
          endsr;
        //_______________________//
        // Finn förste merknaden //
        //"""""""""""""""""""""""//
          begsr merk;
            setll (siavd : sitdn) edim10;
            reade (siavd : sitdn) edim10;
            dow not %eof and w_wmerk = *blanks;
              if m0065 = 'CUSRES';
              if msr   = 'R';
              if m1n07 = 'DFI' or m1n07 = 'DME';
                exsr merk2;
              endif;
              endif;
              endif;
            reade (siavd : sitdn) edim10;
            enddo;
            if w_wmerk=*blanks;
              w_wmerk='OK';
            endif;
          endsr;
        //_______________________//
        // Finn förste merknaden //
        //"""""""""""""""""""""""//
          begsr merk2;
            setll (mmn) tvinf;
            reade (mmn) tvinf;
            dow not %eof and w_wmerk = *blanks;
              if f4815 = 'NE' or f4815 = 'PP';
                if f0078a <> '999';
                  if f0078ax <> 'VAR';
                    if f0078ax <> '950';
                      if f0078ax <> '972';
                        if f0077 <> *blanks;
                          w_wmerk = f4815 + f0077;
                        else;
                          w_wmerk = f4815 + f0078ax;
                        endif;
                      endif;
                    endif;
                  endif;
                endif;
              endif;
            reade (mmn) tvinf;
            enddo;
          endsr;
      /end-free
