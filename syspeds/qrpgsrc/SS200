      *********************************************************************
      * Arbeid med plukkliste
      * PROGRAM BEHANDLER INPUT FRA SYMN1
      *
CRTPG+*  CRTPGM SYSPED/SS200 MODULE(*LIBL/SS200
CRTPGM*        *LIBL/CHECKUSR *LIBL/INCGI) ACTGRP(CGI) AUT(*USE)
      *
      *
      *********************************************************************
      * MAIN PROGRAM FLOW
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
     FCUNDL01   IF   E           K DISK    USROPN
     FWSSORF    UF   E           K DISK    USROPN
     FBRPARF    IF   E           K DISK    USROPN
     FBRIDF     UF   E           K DISK    USROPN
     FCDBOATLF  IF   E           K DISK    USROPN
     FGSSCGF    IF   E           K DISK    USROPN
     FGSSORL29  IF   E           K DISK    USROPN
     Fsted2     IF   E           K DISK    USROPN
     FBATGR     IF   E           K DISK    USROPN
     FBATGRLF   IF   E           K DISK    USROPN
     F                                     RENAME(BATGRR:BATGR2)
      *--------------------------------------------------------------------
      * Prototype defintions and standard system API error structure
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      *--------------------------------------------------------------------
      * Variables common to CGI programs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
      * Variables received from the input string
OddEvD OddEven         s             10
     D IKKEOK          s              1a
     D wsboagr         s              7
     D lng             s              2
     D BckGnd          s             10
     D lstbrd          s              1
     D UsrSpcName      s             10
     D WSUSER          S             10
     D PGM             C                   CONST('SYSPED/CHECKUSR')
      * Variables received from the input string
      * and converted to numeric
     D zeros           s             15a   inz('000000000000000')
      * Number of records matching search criteria
     DRecordCnt        s              5S 0
      *
     D Lib             s             10
     D Fn              s             10
     D Mbr             s             10
      *
     D Spaces          s             12a   inz('&nbsp;&nbsp;')
      *
      *--------------------------------------------------------------------
      * Prolog common to CGI programs
      * -Receive input from the remote browser
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prolog3
      *=====================================================================******
      * MAIN PROCESS LINE
      *=====================================================================******
      * Get program start time for calculating execution time
     C                   time                    timedata1
      * Parse input
     C                   exsr      Parse
      *
     C                   EXSR      INIT
     C                   CALLB     PGM
     C                   PARM                    BIBRID
     C                   PARM                    UsrSpcName
     C                   PARM                    UINN              1
     C                   IF        UINN = 'N'
     C                   GOTO      SLUTT
     C                   END
     C     WSUSER        CHAIN     BRIDF                              50
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVE      WDT           BIDTF
     C                   MOVE      WTM           BITIDF
     C                   UPDATE    BRIDFR
      * Set output skeleton html member name
     C                   exsr      SetSkl
      * Read skeleton output html, etc.
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
      * Retrieve environment variables
     C                   exsr      RtvEnvVar
      *------------------                                                   ******
      * Write HTML sections
      *
      * 1-Section /$top
     C                   exsr      SetTop
     C                   callp     wrtsection('top')
      *
     C                   exsr      SetFound
      *
      * skjekker om det finnes grupper
      *
     C                   IF        WSBOAGR = *BLANKS
     C                   EXSR      GRUPPETST
     C                   END
      *
      * hvis grupper finnes skrives FORM = FORMG
      *
     C                   IF        ((GRUJN = 'J') OR (WSBOAGR <> *BLANKS))
     C                   callp     wrtsection('foundg')
     C                   EXSR      GRUPPE
     C                   END
      *
      * lager sectbuttom for båtkodene
      *
     C                   callp     wrtsection('found')
     C                   EXSR      HENTBKD
     C                   EXSR      LOCSR
     C                   callp     wrtsection('bunn')
     C                   EXSR      TØMWFIL
      * 6-Section /$endhtml
     C                   exsr      SetEnd
     C     SLUTT         TAG
     C                   callp     wrtsection('endhtml')
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
     C                   EXSR      EXIT
      *
     C                   return
      *
      *=====================================================================******
      * Parse input
      *=====================================================================******
     C     Parse         begsr
     C                   eval      IKKEOK     = zhbgetvar('IKKEOK    ')
     C                   eval      wsboagr   = zhbgetvar('wsboagr')
     C                   eval      wsboagr = uppify(wsboagr)
     C                   eval      lng     = zhbgetvar('lng')
     C                   eval      BckGnd  = zhbgetvar('bckgnd')
     C                   eval      lstbrd  = zhbgetvar('lstbrd')
     C                   eval      WSUSER  = zhbgetvar('USER')
     C                   EVAL      UsrSpcName  = zhbgetvar('UsrSpcName')
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$top
      *  (search criteria)
      *=====================================================================******
     C     SetTop        begsr
      * Repeat national language for the next invocation
     C                   callp     updhtmlvar('LNG':lng:
     C                             InitHTMLVars)
      * Color for text, background, links, visited links, active links
     C                   callp     updhtmlvar('TXTCOLOR':'black')
     C                   callp     updhtmlvar('BOLD':' ')
     C                   callp     updhtmlvar('BGCOLOR':BIFARG)
     C                   callp     updhtmlvar('LINK':'0000FF')
     C                   callp     updhtmlvar('VLINK':'FF0000')
     C                   callp     updhtmlvar('ALINK':'00FF00')
      * KUNDE
     C                   callp     updhtmlvar('WSNA':KNAVN)
OddEvC                   callp     updHTMLvar('ROWHEAD':RowHead)
OddEvC                   callp     updHTMLvar('LogoImg':LogoImg)
BODY C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updHtmlVar('USER':WSUSER)
     C                   callp     updHtmlVar('UsrSpcName':UsrSpcName)
     C                   callp     updHTMLvar('sdato':
     C                             %trim(%editw(BIDTV:'    .  .  ')))
     C                   callp     updHTMLvar('stid':
     C                             %trim(%editw(BITIDV:'  .  .  ')))
     C                   callp     updHtmlVar('wsboagr':wsboagr)
      *
     C                   endsr
      *
      *=====================================================================
      * Set html output variables for section /$found (display a boat)
      *=====================================================================
     C     SetFound      begsr
      *
      * Sett inn i HTMLvariabler
     C                   callp     updHtmlVar('USER':WSUSER)
     C                   callp     updHtmlVar('UsrSpcName':UsrSpcName)
     C                   callp     updhtmlvar('BIKUNR':
     C                             %trim(%editc(BIKUNR:'Z')))
     C                   callp     updHtmlVar('KNAVN':KNAVN)
      *
     C                   endsr
      *=====================================================================
      * Read output skeleton html member into memory
      *=====================================================================
      ***********************************************************************
     C     HENTBKD       begsr
      ***********************************************************************
     C     grukey        klist
     C                   kfld                    wsboagr
     C                   kfld                    CBKDB
     C     grukey2       klist
     C                   kfld                    CBKDB
      *
     C                   callp     updhtmlvar('CBNAB':'ALL')
     C                   callp     updhtmlvar('CBKDB':'ALL')
     C                   callp     wrtsection('typsltrow')
      *
     C     *LOVAL        SETLL     CDBOATLF
     C                   READ      CDBOATLF                               40
     C     *IN40         DOWEQ     '0'
     C                   IF        CBLK <> '  '
     C                   SELECT
     C                   WHEN      XAGRP <> *BLANKS
     C                   IF        XAGRP = '**'
     C                   callp     updhtmlvar('CBNAB':CBNAB)
     C                   callp     updhtmlvar('CBKDB':CBKDB)
     C                   callp     wrtsection('typsltrow')
     C                   ELSE
     C                   END
     C                   WHEN      ((CBKN = BIKUNR) AND (CBLK <> '  '))
     C     wsboagr       ifne      *blanks
     C     grukey        chain     batgr                              33
     C     *in33         ifeq      '0'
     C                   callp     updhtmlvar('CBNAB':CBNAB)
     C                   callp     updhtmlvar('CBKDB':CBKDB)
     C                   callp     wrtsection('typsltrow')
     C                   end
     C                   else
     C                   callp     updhtmlvar('CBNAB':CBNAB)
     C                   callp     updhtmlvar('CBKDB':CBKDB)
     C                   callp     wrtsection('typsltrow')
     C                   end
     C                   WHEN      ((CBKN <> BIKUNR) AND (wsboagr <> '       '))
     C                   move      'N'           kuok              1
     C                   exsr      tstku2
     C     kuok          ifeq      'J'
     C                   callp     updhtmlvar('CBNAB':CBNAB)
     C                   callp     updhtmlvar('CBKDB':CBKDB)
     C                   callp     wrtsection('typsltrow')
     C                   end
     C                   WHEN      XCG <> *BLANKS
     C     CBKN          CHAIN     GSSCGF                             33
     C  N33              IF        ((XCG = CGCG) AND (CBLK <> '  '))
     C                   callp     updhtmlvar('CBNAB':CBNAB)
     C                   callp     updhtmlvar('CBKDB':CBKDB)
     C                   callp     wrtsection('typsltrow')
     C                   END
     C                   ENDSL
     C                   END
     C                   READ      CDBOATLF                               40
     C  N40              ENDDO
      *
     C                   callp     wrtsection('typsltend')
      *
     C                   ENDSR
      *
      ****************************************************************************
     C     locsr         begsr
      ****************************************************************************
      * Selection box for Locations
     C                   callp     wrtsection('locstart')
     C                   IF        XALOC = *BLANKS
     C                   callp     updhtmlvar('LOCKOD':'ALL')
     C                   callp     updhtmlvar('LOCNAV':'ALL')
     C                   callp     wrtsection('LOCsltrow')
     C     GSSORL29K     SETGT     GSSORL29
     C     BIKUNR        READE     GSSORL29                               40
     C     *IN40         DOWEQ     '0'
     C     ORLC          chain     sted2                              33
     C                   eval      LOCNAV = ORLC
     C  n33              CAT       '-':1         LOCNAV           40
     C  n33              CAT       st2nvn:1      LOCNAV
     C                   callp     updhtmlvar('LOCKOD':ORLC)
     C                   callp     updhtmlvar('LOCNAV':LOCNAV)
     C                   callp     wrtsection('LOCsltrow')
     C     GSSORL29K     SETGT     GSSORL29
     C     BIKUNR        READE     GSSORL29                               40
     C                   END
      *
     C                   ELSE
      *
     C                   callp     updhtmlvar('LOCKOD':XALOC)
     C                   callp     updhtmlvar('LOCNAV':XALOC2)
     C                   callp     wrtsection('LOCsltrow')
     C                   END
      *
     C                   callp     wrtsection('LOCsltend')
      *
     C                   endsr
      *
      *=====================================================================
      * TØM ARBEIDSFIL
      *=====================================================================
     C     TØMWFIL       begsr
      *
     C     WSUSER        SETLL     WSSORF
     C     WSUSER        READE     WSSORF                                 40
     C     *IN40         DOWEQ     '0'
     C                   DELETE    WSSORFR
     C     WSUSER        READE     WSSORF                                 40
     C  N40              ENDDO
      *
     C                   endsr
      *
      *=====================================================================******
      *  Set output variables for section /$endhtml
      *=====================================================================******
     C     SetEnd        begsr
      * Server protocol
     C                   callp     updhtmlvar('PROTOCOL':S_Protocol)
      * Compute run time
     C                   time                    timedata2
     C     timedata2     subdur    timedata1     ms:*ms
     C                   eval      sec = ms / 1000000
     C                   callp     updhtmlvar('runtime':
     C                             %trim(%editw(sec:'     0 .   ')))
      *
     C                   endsr
      *=====================================================================******
      * Set html output skeleton member before its read into memory
      *=====================================================================******
     C     SetSkl        begsr
      *------------------
      * Set html output skeleton member
     C                   eval      lng = uppify(lng)
      * "HTMLSRC  " er dft = NORSK, ved å trykke på FLAGGET i inng.
      * kan dette endre stil "HTMLSRCXX" avh. av støttede språk
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC' + lng
     C                   eval      Mbr = 'SS200'
      *------------------
     C                   endsr
      *=====================================================================******
      *  Retrieve environment variables
      *=====================================================================******
     C     RtvEnvVar     begsr
      * Use getenvp to get this server's protocol and name
     C                   eval      S_Protocol =getenv('SERVER_PROTOCOL':
     C                             qusec)
     C                   eval      S_Name     =getenv('SERVER_NAME':
     C                             qusec)
      *
     C                   endsr
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
     C                   OPEN      BRIDF
     C     WSUSER        CHAIN(N)  BRIDF                              50
      * En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   CALLB     'INCGI'
     C                   PARM                    BISTDL
     C                   else
      * Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
OddEv * hent Parametre som går igjen i mange prog:
OddEv *      Odd/Even/Rowhead-farger,Logobilde,Språk,Intern/Ekstern bruker,  mm
OddEvc                   call      'ESGETPAR'
OddEvc                   parm                    BIBRID
OddEvc                   parm                    BIFIRM
OddEvc                   parm                    BIKUNR
OddEvc                   parm                    BIKNG
OddEvc                   parm                    RowHead          10
OddEvc                   parm                    Odd              10
OddEvc                   parm                    Even             10
OddEvc                   parm                    LogoImg          50
OddEvc                   parm                    XSPRÅK            2
OddEvc                   parm                    XINTERN           1
OddEvc                   parm                    ParmDS1        1024
OddEvc                   parm                    ParmDS2        1024
OddEvc                   parm                    ParmDS3        1024
     C                   open      BRPARF                               50
     C                   open      CUNDL01                              50
     C                   open      CDBOATLF                             50
     C                   open      BATGR                                50
     C                   open      BATGRLF                              50
     C                   open      WSSORF                               50
     C                   open      GSSCGF                               50
     C                   open      GSSORL29                             50
     C                   open      sted2                                50
      *
     C     GSSORL29K     klist
     C                   kfld                    BIKUNR
     C                   kfld                    ORLC
     C     CUND1K        klist
     C                   kfld                    BIFIRM
     C                   kfld                    BIKUNR
     C     CUND1K        CHAIN     CUNDL01                            50
     C                   IF        BIKNG <> 0
     C     BIKNG         CHAIN     GSSCGF                             50
     C  N50              MOVE      CGCG          XCG               2
     C                   END
      *
     C                   MOVE      BIBRID        PABRID
     C                   MOVE      *zeros        PAKUNR
     C                   MOVE      'SSAGT'       PAID
     C     BrParKey      CHAIN     BRPARF                             33
     C                   IF        *IN33 = *OFF
     C                   MOVE      '**'          XAGRP             2
     C                   MOVE      'SSKGR'       PAID
     C     BrParKey      CHAIN     BRPARF                             33
     C  N33              MOVEL     PAVRDA        XAGRP
     C                   MOVE      'SSLOC'       PAID
     C     BrParKey      CHAIN     BRPARF                             33
     C  N33              MOVEL     PAVRDA        XALOC             3
     C  N33              MOVE      PAVRDA        XALOC2           45
     C                   END
      *
     C                   endsr
      *=====================================================================******
      *  AVSLUTT
      *=====================================================================******
     C     EXIT          begsr
     C                   close     BRIDF
     C                   close     CUNDL01
     C                   close     BRPARF                               50
     C                   close     CDBOATLF
     C                   close     BATGR
     C                   close     BATGRLF
     C                   close     WSSORF
     C                   close     GSSCGF
     C                   close     GSSORL29
     C                   close     sted2
     C                   eval      *inlr = *on
      *
     C                   endsr
      ********************************************************************
     C     GRUPPE        begsr
      ********************************************************************
     C                   setoff                                       40
     C     BATGRKEY      klist
     C                   kfld                    wsboagr
     c                   move      wsboagr       wsboagrf          7
     C                   callp     updhtmlvar('wsboagr':wsboagr)
     C                   callp     updhtmlvar('wsboagrn':'           ')
     C                   callp     wrtsection('typsltrowgr')
     c                   eval      wsboagr=*blanks
     c     wsboagrf      ifne      *blanks
     C                   callp     updhtmlvar('wsboagr':wsboagr)
     C                   callp     updhtmlvar('wsboagrn':'           ')
     C                   callp     wrtsection('typsltrowgr')
     c                   end
      *
     C     BATGRKEY      SETLL     BATGR
     C     *IN40         DOWEQ     '0'
     C                   READ      BATGR                                  40
     C     *in40         ifeq      '0'
     C     bagrup        andne     wsboagrf
     C     BAkunr        ifeq      bikunr
     C                   callp     updhtmlvar('wsboagr':bagrup)
     C                   callp     updhtmlvar('wsboagrn':banavn)
     C                   callp     wrtsection('typsltrowgr')
     C     BAGRUP        SETGT     BATGR
     C                   else
     C                   exsr      kutst
     C                   END
     C                   END
     C  N40              ENDDO
     C                   move      wsboagrf      wsboagr
     C                   callp     updHtmlVar('wsboagr':wsboagr)
      *
     C                   callp     wrtsection('typsltendgr')
      *
     C                   ENDSR
      *
      ********************************************************************
     C     GRUPPETST     begsr
      ********************************************************************
      * skjekker først om kunde har grupper
     C                   setoff                                       40
     C                   move      'N'           grujn             1
     C     BATGRKEY      SETLL     BATGR
     C     *IN40         DOWEQ     '0'
     C                   READ      BATGR                                  40
     C     *in40         ifeq      '0'
     C     BAkunr        ifeq      bikunr
     C                   move      'J'           grujn
     C                   end
     C     BAGRUP        SETGT     BATGR
     C                   end
     C  N40              ENDDO
      *
     C                   ENDSR
      *
      *******************************************************************
     C     kutst         begsr
      *******************************************************************
     c                   setoff                                       34
     C     BrParKey      klist
     C                   kfld                    PABRID
     C                   kfld                    PAKUNR
     C                   kfld                    PAID
     C                   MOVE      BIBRID        PABRID
     C                   MOVE      *zeros        PAKUNR
     C
     C                   MOVE      'SSCUS'       PAID
     C     BrParKey      setll     BRPARF
     C     *in34         doweq     '0'
     C     BrParKey      reade     BRPARF                                 34
     C  n34pavrdn        ifeq      bakunr
     C                   callp     updhtmlvar('wsboagr':bagrup)
     C                   callp     updhtmlvar('wsboagrn':banavn)
     C                   callp     wrtsection('typsltrowgr')
     C                   goto      utku
     C                   end
     C                   end
     C     utku          tag
     C     BAGRUP        SETGT     BATGR
      *
     C                   ENDSR
      *
      *******************************************************************
     C     tstku2        begsr
      *******************************************************************
     c                   setoff                                       3435
     C                   MOVE      BIBRID        PABRID
     C                   MOVE      *zeros        PAKUNR
     C
     C                   MOVE      'SSCUS'       PAID
     C     BrParKey      setll     BRPARF
     C     *in34         doweq     '0'
     C     BrParKey      reade     BRPARF                                 34
     C  N34pavrdn        ifeq      cbkn
     C     wsboagr       ifne      *blanks
     C     grukey        chain     batgr                              35
     C                   ELSE
     C     GRUKEY2       CHAIN     BATGRLF                            35
     C                   end
     C  n35              move      'J'           kuok
     C  n35              goto      utku2
     C                   end
     C                   end
     C     utku2         tag
      *
     C                   endsr
      *
