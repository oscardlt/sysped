********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 SH  PTF10 MC over verdi          .
********************************************************************
      *
OBS!! * Det er "IKKE lov" å bruke kode E (ENDRING) i margen.
      *        Skal da heller ha en linnje med SXX og en med NXX
      * Signatur for den ansvarlige for PTF-en angis KUN i headingen
      *        Hvis en annen "retter en PTF" - men så lite at det ikke
      *        logges spesielt i PTF/Heading - sett sign på linjenivå
      *EKSEMPEL:
      *
      ***********************************************************
      *** PROGRAM SKAL VEDLIKEHOLDE FLYSELSKAPER              ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01 02 07 09-11 13-24             ***
      *** LEDIGE INDIKATORER 27-29 31 32 34-39 41-86 95-97 99 ***
      ***********************************************************
      *
     FKODTFS    UF A E           K DISK
     FKODTFSLF  IF   E           K DISK
     F                                     RENAME(KODTFSR:KODTFS2R)
     FFIRM      IF   E           K DISK
     FCUNDL01   IF   E           K DISK
     FSYFA38FM  CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     D                 DS
     D  KFSNAV                 1     30
     D  XDLT                   1      7
     D                 DS
     D  KFSKOM                 1      2  2
     D  KFSKO2                 1      2  0
     C*
     C                   EXSR      INIT
     C*
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     KODFSL        SETLL     KODTFSLF
     C     START1        TAG
     C                   READ      KODTFSLF                               94
     C   94              MOVE      *BLANKS       WSNR
      *
     C     START2        TAG
      *
     C  N94              DO
     C                   DO        17
     C                   ADD       1             ZRECNO
     C                   WRITE     SUBFIL
     C                   READ      KODTFSLF                               94
     C  N94              END
     C                   END
     C*
     C                   Z-ADD     ZRECNO        YRECNO            4 0
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYVARE
     C                   ELSE
     C                   WRITE     SUBCTL
     C                   EXFMT     BUNN
     C                   END
     C                   SETOFF                                       9830
     C*
     C*** AVSLUTT
     C   03              GOTO      SLUTT
     C*** FRISK OPP
     C   05              GOTO      START
     C*** LAGE NY
     C   06              GOTO      START5
     C*** SØK FRA
     C   08              GOTO      START6
     C*
     C*** BLA VIDERE
     C   25              DO
     C                   Z-ADD     YRECNO        ZRECNO
     C                   SETOFF                                       25
     C                   GOTO      START2
     C                   END
     C*
     C     ZRECNO        IFEQ      0
     C                   GOTO      START3
     C                   ELSE
     C                   EXSR      HENT
     C   98              GOTO      START3
     C                   END
     C*
     C*** SLETT
     C     WSNR          IFEQ      '4'
     C                   MOVE      ' '           WSNR
     C                   EXFMT     VAREL3
     C  N12              DO
     C     KODFS2        CHAIN     KODTFS                             33
     C  N33              DELETE    KODTFSR
     C                   EXSR      MOVB1
     C                   MOVE      '*DELETE'     XDLT
     C                   END
     C                   UPDATE    SUBFIL
     C                   GOTO      START3
     C                   END
     C*
     C*
     C*** LAGE/ENDRE
     C     START5        TAG
     C                   EXFMT     VARELI
     C                   MOVEA     '0000'        *IN(87)
     C                   SETOFF                                       30
     C   04              DO
     C                   EXSR      SØKKUN
     C                   GOTO      START5
     C                   END
     C   12              DO
     C   40              UPDATE    SUBFIL
     C                   SETOFF                                       40
     C                   EXSR      MOVB2
     C                   GOTO      START3
     C                   END
     C                   EXSR      TESTB1
     C   30              GOTO      START5
     C                   GOTO      START3
     C*
     C*** SØK FRA
     C     START6        TAG
     C                   EXFMT     VAREL2
     C   03              GOTO      SLUTT
     C                   GOTO      START
     C*
     C*
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
     C*
     C***********************************************
     C     TESTB1        BEGSR
     C***********************************************
     C*
     C* TEST FLYSELSKAPSNR
     C  N40              DO
     C     WSFNR         COMP      *ZEROS                                 90
     C   90              MOVE      'SPF0023'     MSGNR
     C   90              SETON                                        30
     C                   END
     C*
     C* TEST OM FLYSELSKAPSNAVN ER TASTET
     C     WSNAV         COMP      *BLANKS                                89
     C   89
     CANN30              MOVE      'SPF0024'     MSGNR
     C   89              SETON                                        30
     C*
     C* TEST OM FLYSELSKAPS INTERNASJONAL KODE
     C     WSKD2         COMP      *BLANKS                                88
     C   88
     CANN30              MOVE      'SPF0025'     MSGNR
     C   88              SETON                                        30
     C*
     C* TEST OM FRAKTKODE ER RIKTIG
     C     KUND1K        CHAIN     CUNDL01                            87
     C   87
     CANN30              MOVE      'SPK0001'     MSGNR
     C   87              SETON                                        30
     C*
     C   30              GOTO      SLTB1
     C*
     C     KODFSK        CHAIN     KODTFS                             33
     C  N33
     CANN40              GOTO      SLTB1
     C                   EXSR      MOVWFS
     C  N33              UPDATE    KODTFSR
     C   33              WRITE     KODTFSR
     C   40              UPDATE    SUBFIL
     C  N40              ADD       1             YRECNO
     C  N40              Z-ADD     YRECNO        ZRECNO
     C  N40              WRITE     SUBFIL
     C                   WRITE     SUBCTL
     C                   SETOFF                                       40
     C                   EXSR      MOVB2
     C*
     C     SLTB1         ENDSR
     C*
     C***********************************************
     C     HENT          BEGSR
     C***********************************************
     C                   MOVEA     '00'          *IN(91)
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C  N98WSNR          IFEQ      '2'
     C     XDLT          COMP      '*DELETE'                              98
     C   98              GOTO      SLHENT
     C                   EXSR      MOVFSW
     C                   MOVE      ' '           WSNR
     C                   MOVEA     '0000'        *IN(87)
     C                   SETON                                        40
     C                   GOTO      SLHENT
     C                   END
     C  N98WSNR          IFEQ      'S'
     C                   CALL      'SYEF06'
     C                   PARM                    KFSFNR
     C                   MOVE      ' '           WSNR
     C                   UPDATE    SUBFIL
     C                   END
     C  N98WSNR          IFEQ      '4'
     C     XDLT          COMP      '*DELETE'                              98
     C                   GOTO      SLHENT
     C                   END
     C                   END
     C*
     C     SLHENT        TAG
     C   98              DO
     C                   MOVE      'DIV0012'     MSGNR
     C                   SETON                                            30
     C                   END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVWFS        BEGSR
     C***********************************************
     C                   MOVE      XUNI          KFSUNI
     C                   MOVE      WSFNR         KFSFNR
     C                   MOVE      WSNAV         KFSNAV
     C                   MOVE      WSKD2         KFSKD2
     C                   MOVE      WSAC          KFSAC
     C                   MOVE      WSKNR         KFSKNR
     C                   MOVE      WSMC          KFSMC
N01  C                   MOVE      WSMCO         KFSMCO
N01  C                   MOVE      WSMC2         KFSMC2
     C                   MOVE      WSJAT         KFSJAT
     C     WSKOM         DIV       100           KFSKOM
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVFSW        BEGSR
     C***********************************************
     C                   MOVE      KFSFNR        WSFNR
     C                   MOVE      KFSNAV        WSNAV
     C                   MOVE      KFSKD2        WSKD2
     C                   MOVE      KFSAC         WSAC
     C                   MOVE      KFSKNR        WSKNR
     C                   MOVE      KFSMC         WSMC
N01  C                   MOVE      KFSMCO        WSMCO
N01  C                   MOVE      KFSMC2        WSMC2
     C                   MOVE      KFSJAT        WSJAT
     C     KFSKOM        MULT      100           WSKOM
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVB1         BEGSR
     C***********************************************
     C                   MOVE      *ZEROS        KFSFNR
     C                   MOVE      *BLANKS       KFSNAV
     C                   MOVE      *BLANKS       KFSKD2
     C                   MOVE      *BLANKS       KFSAC
     C                   MOVE      *ZEROS        KFSKNR
     C                   MOVE      *ZEROS        KFSMC
N01  C                   MOVE      *zeros        KFSMCO
N01  C                   MOVE      *zeros        KFSMC2
     C                   MOVE      *BLANKS       KFSJAT
     C                   MOVE      *ZEROS        KFSKOM
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVB2         BEGSR
     C***********************************************
     C                   MOVE      *ZEROS        WSFNR
     C                   MOVE      *BLANKS       WSNAV
     C                   MOVE      *BLANKS       WSKD2
     C                   MOVE      *BLANKS       WSAC
     C                   MOVE      *ZEROS        WSKNR
     C                   MOVE      *ZEROS        WSMC
N01  C                   MOVE      *zeros        WSMCO
N01  C                   MOVE      *zeros        WSMC2
     C                   MOVE      *BLANKS       WSJAT
     C                   MOVE      *ZEROS        WSKOM
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     KODFSK        KLIST
     C                   KFLD                    XUNI
     C                   KFLD                    WSFNR
     C     KODFS2        KLIST
     C                   KFLD                    XUNI
     C                   KFLD                    KFSFNR
     C     KODFSL        KLIST
     C                   KFLD                    W2NAV
     C     KUND1K        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    WSKNR
     C*
     C*
     C                   MOVE      'FS'          XUNI              2
     C*
     C*
     C     FIFIRM        SETLL     FIRM
     C                   READ      FIRM                                   33
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     SØKKUN        BEGSR
     C***********************************************
     C                   MOVE      *BLANKS       USØKKN           30
     C                   CALL      'SOEKKUN'
     C                   PARM                    WSKNR
     C                   PARM                    FIFIRM
     C                   PARM                    USØKKN           30
     C*
     C                   ENDSR
     C*
