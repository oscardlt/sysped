      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/TJGE004 MODULE(*LIBL/TJGE004 *LIBL/INCGI)
CRTPGM*        ACTGRP(*NEW) AUT(*USE)
      *
      *********************************************************************
      /copy SYSPEDS/qrpgsrcpy,hspecs
      /copy SYSPEDS/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     UF   E           K DISK    USROPN
     FHEADF     IF   E           K DISK    USROPN
     FSADIUF    IF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy SYSPEDS/qrpgsrcpy,prototypeb
      /copy SYSPEDS/qrpgsrcpy,usec
      /copy SYSPEDS/qrpgsrcpy,variables3
      *
      * Varible for
     D WSUSER          S             10
     D AVD             S              4
     D OPD             S              7
     D JSONstring      s          32000
     D Error           S            150

      *get input-string
      /copy syspeds/qrpgsrcpy,prolog3

      * Parse input
     C                   exsr      Parse
      * Open files etc
     C                   EXSR      INIT
      *
     c                   callp     gethtmlifs(
     C                             '/syspeddev/html/JSON.html':
     C                             '/CGITAG/')
     C                   callp     wrtsection('top')
      *
     c                   if        Error = *blanks
     C     Key           chain     Headf                              33
     c   33              eval      Error = 'Invalid Order number ('
     c                             + %trim(%editc(HEAVD:'3'))+'/'
     c                             + %trim(%editc(HEOPD:'3'))+')'
     C     Key           chain     SADIUF                             33
     C                   endif
      *
      * ............................................................................................
      * skriv JSON-Object start..(alltid '{' + x ant param. m/komma mellom (IKKE komma etter siste))
      * ............................................................................................
      *
      /free
        JSONstring='{'
        +'¬user¬ : ¬'+%trim(WSUSER)+'¬, '
        +'¬errMsg¬ : ¬'+%trim(Error)+'¬ ';
      /end-free
      * JSONfix escaper " i tekst,  for deretter å bytte ¬->"
     c                   call      'JSONFIX'
     c                   parm                    JSONstring
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      *
     c     Error         cabne     *blanks       AvslJSON
      *
      * ved å starte med komma på hver ny blir det lette (enn å passe på IKKE komma etter siste)
      /free
        JSONstring=
         ',¬avd¬ : ¬'           +%trim(%editc(HEAVD:'3'))+'¬'
        +',¬opd¬ : ¬'           +%trim(%editc(HEOPD:'3'))+'¬'
        +',¬sign¬ : ¬'          +%trim(HESG)+'¬'
        +',¬tur¬ : ¬'           +%trim(%editc(HEPRO:'3'))+'¬'
        +',¬godsNr¬ : ¬'        +%trim(HEGN)+'¬'
        +',¬pos¬ : ¬'           +%trim(HEPOS1)+'¬'
        +',¬uPos¬ : ¬'          +%trim(%editc(HEPOS2:'Z'))+'¬'
        +',¬opdRef¬ : ¬'        +%trim(HERFA)+'¬'
        +',¬hawb¬ : ¬'          +%trim(HEHAWB)+'¬'
        +',¬contNo¬ : ¬'        +%trim(HETRCN)+'¬'
        +',¬avsKnr¬ : ¬'        +%trim(%editc(HEKNS:'3'))+'¬'
        +',¬avsNavn¬ : ¬'       +%trim(HENAS)+'¬'
        +',¬avsAdr1¬ : ¬'       +%trim(HEADS1)+'¬'
        +',¬avsAdr2¬ : ¬'       +%trim(HEADS2)+'¬'
        +',¬avsAdr3¬ : ¬'       +%trim(HEADS3)+'¬'
        +',¬motKnr¬ : ¬'        +%trim(%editc(HEKNK:'3'))+'¬'
        +',¬motNavn¬ : ¬'       +%trim(HENAK)+'¬'
        +',¬motAdr1¬ : ¬'       +%trim(HEADK1)+'¬'
        +',¬motAdr2¬ : ¬'       +%trim(HEADK2)+'¬'
        +',¬motAdr3¬ : ¬'       +%trim(HEADK3)+'¬'
        +',¬antall¬ : ¬'        +%trim(%editc(HENT:'3'))+'¬'
        +',¬vareslag¬ : ¬'      +%trim(HEVS1)+'¬'
        +',¬vekt¬ : ¬'          +%trim(%editc(HEVKT:'3'))+'¬'
        +',¬m3¬ : ¬'            +%trim(%editc(HEM3:'3'))+'¬'
        +',¬lm¬ : ¬'            +%trim(%editc(HELM:'3'))+'¬'
        +',¬godsmerke¬ : ¬'     +%trim(HEGM1)+'¬'
        +',¬sadMerkn1¬ : ¬'     +%trim(SAIME1)+'¬'
        +',¬sadMerkn2¬ : ¬'     +%trim(SAIME2)+'¬'
        +',¬sadMerkn3¬ : ¬'     +%trim(SAIME3)+'¬'
        +',¬sadMerkn4¬ : ¬'     +%trim(SAIME4)+'¬'
        +',¬sadMerkn5¬ : ¬'     +%trim(SAIME5)+'¬'
        +',¬sadMerkn6¬ : ¬'     +%trim(SAIME6)+'¬'
        +',¬sadMerkn7¬ : ¬'     +%trim(SAIME7)+'¬'
        +',¬sadMerkn8¬ : ¬'     +%trim(SAIME8)+'¬'
        +',¬sadMerkn9¬ : ¬'     +%trim(SAIME9)+'¬'
        +',¬sadMerkn10¬ : ¬'    +%trim(SAIME10)+'¬';
      /end-free
     C                   call      'JSONFIX'
     C                   parm                    JSONstring
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      *
      * ............................................................................................
      * Skriv JSON-string Avsluttning
      * ............................................................................................
     c     AvslJSON      tag
     c                   eval      JSONstring ='}'
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      *
      * Quit
     C                   exsr      Exit


      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Lukk fil
     C                   CLOSE     BRIDF
     C  N50              CLOSE     HEADF
     C  N50              CLOSE     SADIUF

      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   MOVE      *ON           *INLR
     C                   return
     C                   endsr
      *
      *********************************************************
     C     Parse         Begsr
      *********************************************************
      * Get input
     C                   EVAL      WSUSER  = zhbgetvar('USER')
     C                   EVAL      AVD  = zhbgetvar('AVD')
     C                   EVAL      HEAVD  = c2n2(AVD)
     C                   EVAL      OPD  = zhbgetvar('OPD')
     C                   EVAL      HEOPD = c2n2(OPD)
     C
     c                   endsr
      *
      *********************************************************
     C     INIT          Begsr
      *********************************************************
     C                   OPEN      BRIDF
      * Test innlogging
     C     WSUSER        CHAIN     BRIDF                              50
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C                   call      BILIBL
     C                   end
      *
     C     KEY           KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
      *
     C   50              eval      Error = 'Invalid userID'
     C  N50              OPEN      HEADF
     C  N50              OPEN      SADIUF

     c                   endsr
      *
