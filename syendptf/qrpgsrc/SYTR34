      *
      ***********************************************************
      *** PROGRAM FOR ARBEID MED ÅPNE HENTEOPPDRAG            ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      ***********************************************************
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 JOV PTF10 tildel 402-sendingsnr fra 'normal serie' - kuttet i FIRINL
      *%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
      * 02 JOV PTF12 Avdtildeling fra NOPNR - innhentet til std fra Ramberg
      * 03 JOV PTF12 test av terminal også mot NOPNR
      * 04 JOV PTF12 FYSISK SLETT også av kolli
********************************************************************
      *
     FHENOP     UF A E           K DISK
     FHENOP01   IF   E           K DISK
     F                                     RENAME(HENOPR:HENOPR1)
     FHENOP03   IF   E           K DISK
     F                                     RENAME(HENOPR:HENOPR3)
     FHENOS     UF   E           K DISK
     FHENOK     UF   E           K DISK
     FFIRINL    UF   E           K DISK
     FFIRM      IF   E           K DISK
     FCUNDL01   IF   E           K DISK
     FVADR      IF   E           K DISK
     FUNITL01   IF   E           K DISK
     FKODTA     IF   E           K DISK
     FKODTRI    IF   E           K DISK
     FUSRDFT    IF   E           K DISK
N02  FNOPNR     IF   E           K DISK
     FTURER14   IF   E           K DISK
     FSYTR34FM  CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     F                                     INFDS(FBAREA)
      *___________________________
      * Finner username
      *"""""""""""""""""""""""""""
     D                SDS
     D  ZJOB                 244    253
     D  ZUSER                254    263
     D  ZJOBNR               264    269  0
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D                 DS
     D  HONAS                  1     30
     D  XDLT                   1      7
     D                 DS
     D  HODTÅ                  1      4  0
     D  HODTM                  5      6  0
     D  HODTD                  7      8  0
     D  HODT                   1      8  0
     D                 DS
     D  WSDTD                  1      2  0
     D  WSDTM                  3      4  0
     D  WSDTÅ                  5      6  0
     D  WSDT                   1      6  0
     D                 DS
     D  UFN17A                 1      2
     D  UFN17B                 3      7
     D  UFN17C                 8     17
     D  UFN17                  1     17
     D                UDS
     D  UFIRM                  1      2
     D  UAVD                  15     18
     D  USG                   19     21
      *
     C                   EXSR      INIT
      *
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     AVDLES        COMP      *ZEROS                                 27
     C     *IN27         IFEQ      '1'
     C     *LOVAL        SETLL     HENOP01
     C                   ELSE
     C     LESKEY        SETLL     HENOP03
     C                   END
     C     START1        TAG
     C     *IN27         IFEQ      '1'
     C                   READ      HENOP01                                94
     C                   ELSE
     C     LESKEY        READE     HENOP03                                94
     C                   END
      *
     C     START2        TAG
      *
     C     *IN94         DOWEQ     '0'
     C     HOPNRT        IFEQ      ZZTERM
     C                   MOVEL     HODTD         HODT4
     C                   MOVE      HODTM         HODT4
     C                   ADD       1             ZRECNO
     C     HOEDSN        COMP      *ZEROS                             5555
     C     HOMOPD        COMP      9999999                                56
     C                   MOVE      HOPNRT        XXPNRT            5
     C                   MOVE      XXPNRT        HONAS
     C                   WRITE     SUBFIL
     C                   END
     C     *IN27         IFEQ      '1'
     C                   READ      HENOP01                                94
     C                   ELSE
     C     LESKEY        READE     HENOP03                                94
     C                   END
     C  N94              ENDDO
      *
     C                   Z-ADD     ZRECNO        YRECNO            4 0
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     2             POSNBR
     C     ZRECNO        IFGT      0
     C                   Z-ADD     1             ZRECNO
     C                   END
      *
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   Z-ADD     2             LINNBR
     C                   Z-ADD     14            POSNBR
     C  N30              DO
     C                   SETON                                        30
     C                   MOVE      'SHU0697'     MSGNR
     C                   END
     C                   EXFMT     NYVARE
     C                   ELSE
     C                   WRITE     BUNN
     C                   EXFMT     SUBCTL
     C                   END
      *
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C                   Z-ADD     WSSUBN        XRECNO            4 0
      *
     C                   SETOFF                                       9998
     C                   SETOFF                                       30
      *** AVSLUTT
     C   03              GOTO      SLUTT
     C   12              GOTO      SLUTT
      *
      * SØK AVDELING
      *
     C   04              DO
     C                   CALL      'SYFA14B'
     C                   PARM                    AVDLES
     C                   END
      *
      * ENDRET AVDELING I HEADING
      *
     C     AVDLES        IFNE      AVDLGM
     C                   MOVE      AVDLES        AVDLGM
     C     KODTA2        CHAIN     KODTA                              85
     C     *IN85         IFEQ      '1'
     C                   MOVE      'SPA0001'     MSGNR
     C                   Z-ADD     2             LINNBR
     C                   Z-ADD     14            POSNBR
     C                   SETON                                        30
     C                   GOTO      START3
     C                   END
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     2             POSNBR
     C                   GOTO      START
     C                   END
      *** FRISK OPP
     C   05              GOTO      START
      *** LAGE NY
     C     START4        TAG
     C   06              DO
     C                   EXSR      MOVB2
     C                   GOTO      START5
     C                   END
      *
      *** BLA VIDERE
     C   25              DO
     C                   Z-ADD     YRECNO        ZRECNO
     C                   SETOFF                                       25
     C                   GOTO      START2
     C                   END
      *
     C     ZRECNO        IFNE      0
     C                   EXSR      HENT
     C     REFRES        IFEQ      'J'
     C                   GOTO      START
     C                   END
     C                   END
     C   98XRECNO        IFNE      0
     C                   Z-ADD     XRECNO        ZRECNO
     C                   END
     C                   GOTO      START3
      *
     C     START5        TAG
     C                   MOVE      '0'           *IN40
     C                   EXSR      KDTFMT
     C                   EXFMT     VARELI
     C                   EXSR      KDTFMT
     C                   MOVEA     '00000000'    *IN(80)
     C                   MOVEA     '000'         *IN(88)
     C                   MOVE      '0'           *IN30
      *
     C     *IN04         IFEQ      '1'
     C                   EXSR      SØK
     C                   GOTO      START5
     C                   END
      *
     C     *IN02         IFEQ      '1'
     C                   EXSR      GETSND
     C                   GOTO      START5
     C                   END
      *
     C     *IN09         IFEQ      '1'
     C     WSDAVD        IFEQ      *ZEROS
     C                   SETON                                        8530
     C                   MOVE      'JOV1200'     MSGNR
     C                   Z-ADD     21            LINNBR
     C                   Z-ADD     49            POSNBR
     C                   GOTO      START5
     C                   END
     C     WSBILN        IFNE      *BLANKS
     C     WSPRO         ANDEQ     *ZEROS
     C                   EXSR      AUTTUR
     C     TUROK         CABEQ     'J'           START5
     C                   END
     C                   EXSR      SØKTUR
     C                   GOTO      START5
     C                   END
      *
     C     *IN12         IFEQ      '1'
     C                   EXSR      MOVB2
     C                   ELSE
     C                   EXSR      TESTB
     C     *IN30         CABEQ     '1'           START5
      * VED NY - OVER TIL VARELINJER
     C                   MOVE      HOLNR         ULNR              7 0
     C                   move      'N'           UAUTO             1
     C                   CALL      'SYTR34B'
     C                   PARM                    ULNR
     C                   PARM                    UAUTO
      * DEN NYE KJØRT HELT FERDIG... REFRESH
     C     ULNR          CHAIN(N)  HENOP                              33
     C     *IN33         IFEQ      '0'
     C     HOST          ANDGT     'C'
     C                   GOTO      START
     C                   END
      * ELLERS SKRIV TIL SUBFIL..
     C                   MOVE      HOPNRT        XXPNRT            5
     C                   MOVE      XXPNRT        HONAS
     C                   ADD       1             YRECNO
     C                   Z-ADD     YRECNO        ZRECNO
     C                   WRITE     SUBFIL
     C                   WRITE     SUBCTL
     C                   END
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     2             POSNBR
     C                   GOTO      START3
      *
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
      *
     C***********************************************
     C     SØK           BEGSR
     C***********************************************
      *
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
      *
      * SØK BILNR
     C     LINNBR        IFEQ      17
     C     POSNBR        ANDGE     54
     C     POSNBR        ANDLE     63
     C                   CALL      'SYTR19'
     C                   PARM                    WSBILN
     C                   Z-ADD     17            LINNBR
     C                   Z-ADD     71            POSNBR
     C                   GOTO      SLTSØK
     C                   END
      * SØK TUR
     C     LINNBR        IFEQ      17
     C     POSNBR        ANDGE     71
     C     WSDAVD        IFEQ      *ZEROS
     C                   SETON                                        8530
     C                   MOVE      'JOV1200'     MSGNR
     C                   Z-ADD     21            LINNBR
     C                   Z-ADD     49            POSNBR
     C                   END
     C                   EXSR      SØKTUR
     C                   GOTO      SLTSØK
     C                   END
      * KUNDE
     C     LINNBR        IFEQ      18
     C                   CALL      'SOEKKUN'
     C                   PARM                    WSKNS
     C                   PARM                    FIFIRM
     C                   PARM                    WSNAS
     C     WSKNS         IFNE      *ZEROS
     C     KUNKEY        CHAIN     CUNDL01                            86
     C     *IN86         IFEQ      '0'
     C                   EXSR      GETNAV
     C                   END
     C                   END
     C                   GOTO      SLTSØK
     C                   END
      *
      *
     C     SLTSØK        ENDSR
      *
     C***********************************************
     C     GETNAV        BEGSR
     C***********************************************
      *
     C     VADRKE        CHAIN     VADR                               33
     C     *IN33         IFEQ      '0'
      * HENT NAVN IHT VAREADRESSE
     C                   MOVE      VADRNA        WSNAS
     C                   MOVE      VADRN1        WSADS1
     C                   MOVEL     VADRN3        TMPPNR            4
     C                   TESTN                   TMPPNR               34
     C     *IN34         IFEQ      '1'
     C                   MOVE      TMPPNR        WSPNRS                          POSTNR
     C                   MOVE      VADRN3        TMPAD3           25
     C                   MOVEL     TMPAD3        WSSTDS                          STED
     C                   ELSE
     C                   MOVE      *ZEROS        WSPNRS
     C                   MOVEL     VADRN3        WSSTDS
     C                   END
     C                   ELSE
      * HENT NAVN IHT KUNDEREGISTER
     C                   MOVEL     KNAVN         WSNAS
     C                   MOVEL     ADR1          WSADS1
     C                   MOVE      POSTNR        WSPNRS
     C                   MOVEL     ADR3          WSSTDS
     C                   END
     C                   ENDSR
      *
     C***********************************************
     C     TESTB         BEGSR
     C***********************************************
     C     WSSNDN        IFEQ      '*'
     C                   EXSR      GETSND
     C                   END
     C     WSSNDN        IFEQ      *BLANKS
     C                   MOVE      '1'           *IN82
     C                   MOVE      '1'           *IN30
     C                   MOVE      'JOV1209'     MSGNR
     C                   Z-ADD     17            LINNBR
     C                   Z-ADD     28            POSNBR
     C                   END
      * BILNR MÅ VÆRE GYLDIG I.H.T REGISTER
     C     WSBILN        CHAIN     UNITL01                            83
     C     *IN83         IFEQ      '1'
     C     *IN30         ANDEQ     '0'
     C                   MOVE      '1'           *IN83
     C                   MOVE      '1'           *IN30
     C                   MOVE      'JOV1176'     MSGNR
     C                   Z-ADD     17            LINNBR
     C                   Z-ADD     54            POSNBR
     C                   END
      * TURNR.
     C     WSPRO         IFNE      *ZEROS
     C     WSPRO         CHAIN     TURER14                            84
     C  N84              MOVE      TUBILN        WSBILN
     C  N84TUST          COMP      ' '                                84
     C     *IN84         IFEQ      '1'
     C     *IN30         ANDEQ     '0'
     C                   MOVE      'JOV1160'     MSGNR
     C                   Z-ADD     17            LINNBR
     C                   Z-ADD     71            POSNBR
     C                   SETON                                        30
     C                   END
     C                   END
      * KUNDE
      * .. AUTOSØK??
     C     WSKNS         IFEQ      *ZEROS
     C     WSNAS         ANDNE     *BLANKS
     C                   CALL      'SOEKKUN'
     C                   PARM                    WSKNS
     C                   PARM                    FIFIRM
     C                   PARM                    WSNAS
     C     WSKNS         IFNE      *ZEROS
     C                   MOVE      *BLANKS       WSNAS
     C                   END
     C                   END
      * TEST NR..
     C     KUNKEY        CHAIN     CUNDL01                            86
     C     *IN86         IFEQ      '1'
     C     *IN30         IFEQ      '0'
     C                   MOVE      '1'           *IN30
     C                   MOVE      'SPK0001'     MSGNR
     C                   Z-ADD     18            LINNBR
     C                   Z-ADD     9             POSNBR
     C                   END
     C                   ELSE
     C     WSNAS         IFEQ      *BLANKS
     C                   EXSR      GETNAV
     C                   END
     C                   END
      *
      * POSTNR AVSENDER
     C     WSPNRS        COMP      *ZEROS                                 89
     C     *IN89         IFEQ      '1'
     C     *IN30         ANDEQ     '0'
     C                   MOVE      'SPS0012'     MSGNR
     C                   Z-ADD     19            LINNBR
     C                   Z-ADD     49            POSNBR
     C                   SETON                                        30
     C                   END
      *
      *
      * DATO
     C                   MOVE      WSDTÅ         UÅÅ               2 0
     C                   MOVE      WSDTM         UMM               2 0
     C                   MOVE      WSDTD         UDD               2 0
     C                   CALL      'TESTDATO'
     C                   PARM                    UDD
     C                   PARM                    UMM
     C                   PARM                    UÅÅ
     C                   PARM                    UFLAGG            1 0
     C     UFLAGG        COMP      1                                      87
     C     *IN87         IFEQ      '1'
     C     *IN30         ANDEQ     '0'
     C                   MOVE      'SPD0001'     MSGNR
     C                   Z-ADD     21            LINNBR
     C                   Z-ADD     9             POSNBR
     C                   SETON                                        30
     C                   END
      *
      * POSTNR TERMINAL
     C     WSPNRT        CHAIN     KODTRI                             88
N03  C   88WSPNRT        CHAIN     NOPNR                              88
     C     *IN88         IFEQ      '1'
     C     *IN30         ANDEQ     '0'
S03  C*                  MOVE      'JOV1161'     MSGNR
N03  C                   MOVE      'JOV1764'     MSGNR
     C                   Z-ADD     21            LINNBR
     C                   Z-ADD     35            POSNBR
     C                   SETON                                        30
     C                   END
      *
      * FORESLÅ AVDELING FOR HENTE-OPPDRAG
      * HENTE-OPD (EGET FELT I FIL) ER FRA KUNDENS POSTNR TIL TERMINAL
      * DET ER EN EGEN TABELL PR TERMINAL (WSPNRT DEL 1 AV KEY)
     C     WSDAVD        IFEQ      *ZEROS
     C     WSPNRS        ANDNE     *ZEROS
     C     WSPNRT        ANDNE     *ZEROS
N02  C     KODRIK        CHAIN     NOPNR                              33
N02  C     *IN33         IFEQ      *OFF
N02  C     NOAVDU        ANDNE     *ZEROS
N02  C                   MOVE      NOAVDU        RIAVDU
N02  C                   MOVE      NOAVDI        RIAVDI
N02  C                   MOVE      NOAVDH        RIAVDH
N02  C                   goto      AvdOK
N02  C                   END
     C     KODRIK        SETGT     KODTRI
     C     WSPNRT        READPE    KODTRI                                 33
     C  N33WSPNRS        IFGE      RIPNRF
     C     WSPNRS        ANDLE     RIPNRT
N02  C     AvdOk         Tag
     C                   MOVE      RIAVDH        WSDAVD
     C                   ENDIF
     C                   END
      *
      * AVD-NR FOR DUP-OPPDRAGENE
     C     KODTAK        CHAIN     KODTA                              85
     C     *IN85         IFEQ      '1'
     C     *IN30         ANDEQ     '0'
     C                   MOVE      'SPA0001'     MSGNR
     C                   Z-ADD     21            LINNBR
     C                   Z-ADD     49            POSNBR
     C                   SETON                                        30
     C                   END
      *
      * INGEN ANDRE FEIL, ENNÅ IKKE GITT TURNR..
     C     *IN30         IFEQ      '0'
     C     WSPRO         IFEQ      *ZEROS
     C                   MOVE      'JOV1201'     MSGNR
     C                   Z-ADD     17            LINNBR
     C                   Z-ADD     71            POSNBR
     C                   SETON                                        3084
     C                   END
     C                   END
      *
      * HOPP UT VED FEIL
     C     *IN30         CABEQ     '1'           SLTB
      *
      *** OPPDATERE PÅ REGISTER OG SUBFIL
     C     WSLNR         IFNE      *ZEROS
     C     WSLNR         CHAIN     HENOP                              44
     C                   ELSE
     C                   EXSR      GETLNR
     C                   SETON                                        44
     C                   END
      *
     C                   EXSR      MOVWTK
      *
     C     *IN44         IFEQ      '0'
     C                   UPDATE    HENOPR
     C*                    UPDATSUBFIL
     C                   ELSE
     C                   WRITE     HENOPR
     C                   END
     C                   EXSR      MOVB2
      *
     C     SLTB          ENDSR
     C***********************************************
     C     AUTTUR        BEGSR
     C***********************************************
      * BILNR MÅ VÆRE GYLDIG I.H.T REGISTER
     C     WSBILN        CHAIN     UNITL01                            83
     C     *IN83         IFEQ      '1'
     C                   MOVE      '1'           *IN83
     C                   MOVE      '1'           *IN30
     C                   MOVE      'JOV1176'     MSGNR
     C                   Z-ADD     17            LINNBR
     C                   Z-ADD     54            POSNBR
     C                   MOVE      'J'           TUROK                           =IKKE I SØK
     C                   GOTO      UTATUR
     C                   END
      *
     C                   MOVE      WSBILN        XBILN            10
     C                   MOVE      WSDAVD        XAVD              4 0
     C                   MOVE      *ZEROS        XPRO              8 0
     C                   MOVE      *ZEROS        XANT              3 0
     C                   CALL      'SYTR88H'
     C                   PARM                    XAVD
     C                   PARM                    XBILN
     C                   PARM                    XPRO
     C                   PARM                    XANT
     C     XPRO          IFEQ      *ZEROS
     C                   MOVE      'N'           TUROK             1
     C                   ELSE
     C                   MOVE      'J'           TUROK
     C     XPRO          CHAIN     TURER14                            33
     C     *IN33         IFEQ      '0'
     C                   MOVE      XPRO          WSPRO
     C                   MOVEL     TUBILN        WSBILN
     C                   Z-ADD     18            LINNBR
     C                   Z-ADD     9             POSNBR
     C                   END
     C                   END
      *
     C     UTATUR        ENDSR
     C***********************************************
     C     SØKTUR        BEGSR
     C***********************************************
      *
     C                   MOVE      WSDAVD        XAVD              4 0
     C                   MOVE      *ZEROS        XOPD              7 0
     C                   MOVE      *ZEROS        XPRO              8 0
     C                   MOVE      *ZEROS        XRUND             8 0
     C                   MOVE      'P'           USPØR             1
     C                   CALL      'SYTR88'
     C                   PARM                    XPRO
     C                   PARM                    XRUND
     C                   PARM                    XAVD
     C                   PARM                    XOPD
     C                   PARM                    USPØR
     C                   PARM                    UFLAGG
     C     XPRO          IFNE      *ZEROS
     C     XPRO          CHAIN     TURER14                            33
     C     *IN33         IFEQ      '0'
     C                   MOVE      XPRO          WSPRO
     C                   MOVEL     TUBILN        WSBILN
     C                   Z-ADD     18            LINNBR
     C                   Z-ADD     9             POSNBR
     C                   END
     C                   END
      *
     C                   ENDSR
     C***********************************************
     C     HENT          BEGSR
     C***********************************************
     C                   MOVE      'N'           REFRES            1
     C                   Z-ADD     0             LINNBR
     C                   SETOFF                                       919299
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C   98              DO
     C   99              SETOFF                                       98
     C                   Z-ADD     1             LINNBR
     C                   GOTO      SLHENT
     C                   END
      *
     C     ZRECNO        DIV       15            LINNBR
     C                   MVR                     XLIN              2 0
     C     XLIN          IFEQ      0
     C                   Z-ADD     15            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
      *
     C     WSNR          IFEQ      '2'
     C                   MOVE      ' '           WSNR
     C     XDLT          COMP      '*DELETE'                              98
     C   98              GOTO      SLHENT
     C                   EXSR      CHGTK
     C     HOEDSN        COMP      *ZEROS                             5555
     C     HOMOPD        COMP      9999999                                56
     C                   MOVE      HOPNRT        XXPNRT            5
     C                   MOVE      XXPNRT        HONAS
     C                   UPDATE    SUBFIL
     C   12              GOTO      SLHENT
     C                   END
      *
     C     WSNR          IFEQ      '4'
     C                   MOVE      ' '           WSNR
     C     XDLT          COMP      '*DELETE'                              98
     C   98              GOTO      SLHENT
     C                   EXSR      DLTTK
     C   12              GOTO      SLHENT
     C                   END
      *
     C     WSNR          IFEQ      '7'
     C                   MOVE      ' '           WSNR
     C     XDLT          COMP      '*DELETE'                              98
     C   98              GOTO      SLHENT
     C                   SETON                                        99
     C                   MOVE      HOLNR         ULNR              7 0
     C                   move      'N'           UAUTO
     C                   CALL      'SYTR34B'
     C                   PARM                    ULNR
     C                   PARM                    UAUTO
     C     ULNR          CHAIN(N)  HENOP                              33
     C     *IN33         IFEQ      '0'
     C     HOST          ANDGT     'C'
     C                   MOVE      'J'           REFRES
     C                   END
     C     HOEDSN        COMP      *ZEROS                             5555
     C     HOMOPD        COMP      9999999                                56
     C                   MOVE      HOPNRT        XXPNRT            5
     C                   MOVE      XXPNRT        HONAS
     C                   UPDATE    SUBFIL
     C                   END
      *
     C                   END
      *
     C     SLHENT        TAG
     C                   ADD       4             LINNBR
     C                   Z-ADD     2             POSNBR
     C                   SETOFF                                       99
     C   98              DO
     C                   MOVE      'DIV0012'     MSGNR
     C                   SETON                                            30
     C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     CHGTK         BEGSR
     C***********************************************
     C     HOLNR         CHAIN(N)  HENOP                              44
     C                   EXSR      MOVTKW
     C                   MOVE      '1'           *IN40
     C                   Z-ADD     9             POSNBR
     C     STCHG         TAG
     C                   EXSR      KDTFMT
     C                   EXFMT     VARELI
     C                   EXSR      KDTFMT
     C                   MOVEA     '00000000'    *IN(80)
     C                   MOVEA     '000'         *IN(88)
     C                   MOVE      '0'           *IN30
      *
     C   12              DO
     C                   EXSR      MOVB2
     C                   END
     C     *IN04         IFEQ      '1'
     C                   EXSR      SØK
     C                   GOTO      STCHG
     C                   END
     C     *IN02         IFEQ      '1'
     C                   EXSR      GETSND
     C                   GOTO      STCHG
     C                   END
     C     *IN09         IFEQ      '1'
     C     WSDAVD        IFEQ      *ZEROS
     C                   SETON                                        8530
     C                   MOVE      'JOV1200'     MSGNR
     C                   Z-ADD     21            LINNBR
     C                   Z-ADD     49            POSNBR
     C                   GOTO      STCHG
     C                   END
     C     WSBILN        IFNE      *BLANKS
     C     WSPRO         ANDEQ     *ZEROS
     C                   EXSR      AUTTUR
     C     TUROK         CABEQ     'J'           STCHG
     C                   END
     C                   EXSR      SØKTUR
     C                   GOTO      STCHG
     C                   END
     C  N12              EXSR      TESTB
     C  N12
     CAN 30              GOTO      STCHG
      * VED F10- OVER TIL VARELINJER
     C     *IN10         IFEQ      '1'
     C                   MOVE      HOLNR         ULNR              7 0
     C                   move      'N'           UAUTO
     C                   CALL      'SYTR34B'
     C                   PARM                    ULNR
     C                   PARM                    UAUTO
     C     ULNR          CHAIN(N)  HENOP                              33
     C     *IN33         IFEQ      '0'
     C     HOST          ANDGT     'C'
     C                   MOVE      'J'           REFRES
     C                   END
     C                   END
      *
     C     XLIN          IFEQ      0
     C                   Z-ADD     15            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     DLTTK         BEGSR
     C***********************************************
     C                   SETON                                        99
     C     HOLNR         CHAIN(N)  HENOP                              33
     C                   EXFMT     VAREL3
     C  N12              DO
     C     HOLNR         CHAIN     HENOP                              33
     C  N33              DELETE    HENOPR
     C     HOLNR         SETLL     HENOS
     C     HOLNR         READE     HENOS                                  33
     C     *IN33         DOWEQ     '0'
     C                   DELETE    HENOSR
E04  C* Slett  også Kolli-ID..
     C     HENOSK        SETLL     HENOK
     C     HENOSK        READE     HENOK                                  33
     C     *IN33         DOWEQ     '0'
S04  C*                  MOVEL     '*D*'         HKID
s04  C*                  UPDATE    HENOKR
N04  C                   delete    HENOKR
     C     HENOSK        READE     HENOK                                  33
     C                   END
     C     HOLNR         READE     HENOS                                  33
     C                   END
     C                   CLEAR                   HENOPR
     C                   MOVE      '*DELETE'     XDLT
     C                   END
     C     XLIN          IFEQ      0
     C                   Z-ADD     15            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C     HOEDSN        COMP      *ZEROS                             5555
     C     HOMOPD        COMP      9999999                                56
     C                   MOVE      HOPNRT        XXPNRT            5
     C                   MOVE      XXPNRT        HONAS
     C                   UPDATE    SUBFIL
      *
     C                   ENDSR
      *
     C***********************************************
     C     MOVWTK        BEGSR
     C***********************************************
     C   44              CLEAR                   HENOPR
     C                   MOVE      WSLNR         HOLNR
     C                   MOVE      WSSNDN        HOSNDN
     C                   MOVE      WSPRO         HOPRO
     C                   MOVE      WSBILN        HOBILN
     C                   MOVE      WSKNS         HOKNS
     C                   MOVE      WSNAS         HONAS
     C                   MOVE      WSADS1        HOADS1
     C                   MOVE      WSPNRS        HOPNRS
     C                   MOVE      WSSTDS        HOSTDS
     C                   MOVE      WSDTD         HODTD
     C                   MOVE      WSDTM         HODTM
     C     WSDTÅ         IFGT      50
     C     WSDTÅ         ADD       1900          HODTÅ
     C                   ELSE
     C     WSDTÅ         ADD       2000          HODTÅ
     C                   END
     C                   MOVE      WSPNRT        HOPNRT
     C                   MOVE      WSDAVD        HODAVD
      * SUBFIL
     C                   MOVEL     HODTD         HODT4
     C                   MOVE      HODTM         HODT4
      *
     C                   ENDSR
      *
     C***********************************************
     C     MOVTKW        BEGSR
     C***********************************************
     C                   SETON                                        99
     C                   MOVE      HOLNR         WSLNR
     C                   MOVE      HOSNDN        WSSNDN
     C                   MOVE      HOPRO         WSPRO
     C                   MOVE      HOBILN        WSBILN
     C                   MOVE      HOKNS         WSKNS
     C                   MOVE      HONAS         WSNAS
     C                   MOVE      HOADS1        WSADS1
     C                   MOVE      HOPNRS        WSPNRS
     C                   MOVE      HOSTDS        WSSTDS
     C                   MOVE      HODTD         WSDTD
     C                   MOVE      HODTM         WSDTM
     C                   MOVE      HODTÅ         WSDTÅ
     C                   MOVE      HOPNRT        WSPNRT
     C                   MOVE      HODAVD        WSDAVD
     C                   MOVEL     WSSNDN        TSTSND            5
     C     TSTSND        IFEQ      '40270'
     C                   Z-ADD     18            LINNBR
     C                   Z-ADD     9             POSNBR
     C                   ELSE
     C                   Z-ADD     17            LINNBR
     C                   Z-ADD     28            POSNBR
     C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     MOVB2         BEGSR
     C***********************************************
     C                   Z-ADD     17            LINNBR
     C                   Z-ADD     28            POSNBR
     C                   MOVE      *ZEROS        WSLNR
     C                   MOVE      *BLANKS       WSSNDN
     C                   MOVE      *ZEROS        WSPRO
     C                   MOVE      *BLANKS       WSBILN
     C                   MOVE      *BLANKS       WSKNS
     C                   MOVE      *BLANKS       WSNAS
     C                   MOVE      *BLANKS       WSADS1
     C                   MOVE      *ZEROS        WSPNRS
     C                   MOVE      *BLANKS       WSSTDS
     C                   MOVE      UDAY          WSDTD
     C                   MOVE      UMONTH        WSDTM
     C                   MOVE      UYEAR         WSDTÅ
     C                   MOVE      ZZTERM        WSPNRT
     C                   MOVE      *ZEROS        WSDAVD
      *
     C                   ENDSR
     C***********************************************
     C     GETSND        BEGSR
     C***********************************************
     C*
     C                   MOVEL     WSSNDN        TSTSND            5
     C     TSTSND        IFEQ      '40270'
     C                   MOVE      '1'           *IN82
     C                   MOVE      '1'           *IN30
     C                   MOVE      'JOV1168'     MSGNR
     C                   Z-ADD     17            LINNBR
     C                   Z-ADD     28            POSNBR
     C                   GOTO      UTGSND
     C                   END
S01  C*
S01  C*                  MOVE      *ZEROS        FIRAVD            4 0
S01  C*    FIRAVD        CHAIN     FIRINL                             33
S01  C*    *IN33         IFEQ      '0'
S01  C*                  ADD       1             IF02NR
S01  C*                  UPDATE    FIRINLR
S01  C*                  MOVE      IF02LA        UFN17A
S01  C*                  MOVE      IF02LE        UFN17B
S01  C*                  MOVEL     IF02NR        UFN17C
S01  C*                  MOVE      *ZEROS        UAN22            22
S01  C*                  MOVE      UFN17         UAN22
S01  C*                  CALL      'SYGE08'
S01  C*                  PARM                    UAN22
S01  C*                  MOVE      UAN22         UFN17
S01  C*                  MOVEL     '402'         WSSNDN
S01  C*                  MOVE      UFN17         WSSNDN
S01  C*                  Z-ADD     17            LINNBR
S01  C*                  Z-ADD     54            POSNBR
S01  C*                  END
N01  C                   CALL      'SYGE15'
N01  C                   PARM                    WSSNDN
N01  C                   MOVEL     '402'         WSSNDN
      *
     C     UTGSND        ENDSR
      *
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
      *
     C                   TESTN                   UAVD                 33
     C  N33              MOVE      *ZEROS        UAVD
     C                   MOVE      *ZEROS        HODT
     C                   MOVE      *ZEROS        WSDT
      *
     C                   MOVE      *ZEROS        AVDLES            4 0
     C                   MOVE      *ZEROS        AVDLGM            4 0
     C     LESKEY        KLIST
     C                   KFLD                    AVDLES
      *
     C     KUNKEY        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    WSKNS
     C     VADRKE        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    WSKNS
     C                   KFLD                    A1
     C                   Z-ADD     1             A1                5 0
      *
     C     HENOSK        KLIST
     C                   KFLD                    HSLNR
     C                   KFLD                    HSLNR2
     C     KODTAK        KLIST
     C                   KFLD                    YYAUNI
     C                   KFLD                    WSDAVD            4 0
     C     KODTA2        KLIST
     C                   KFLD                    YYAUNI
     C                   KFLD                    AVDLES
     C                   MOVE      'A'           YYAUNI            1
     C     KODRIK        KLIST
     C                   KFLD                    WSPNRT
     C                   KFLD                    WSPNRS
     C     *LOVAL        SETLL     FIRM
     C                   READ      FIRM                                   33
     C     ZUSER         CHAIN     USRDFT                             33
     C                   ENDSR
      *
      *
     C***********************************************
     C     GETLNR        BEGSR
     C***********************************************
      * Hent neste løpenr
     C                   MOVE      *ZEROS        HOLNR
     C     9999999       SETGT     HENOP
     C                   READP(N)  HENOP                                  33
     C     HOLNR         ADD       1             WSLNR
      *
     C                   ENDSR
      *
     C***********************************************
     C     KDTFMT        BEGSR
     C***********************************************
     C                   MOVE      WSDTD         UDT1              2 0
     C                   MOVE      WSDTM         UDT2              2 0
     C                   MOVE      WSDTÅ         UDT3              2 0
     C                   CALL      'DATOFMT'
     C                   PARM                    UDT1
     C                   PARM                    UDT2
     C                   PARM                    UDT3
     C                   MOVE      UDT1          WSDTD
     C                   MOVE      UDT2          WSDTM
     C                   MOVE      UDT3          WSDTÅ
     C*
     C                   ENDSR
     C*
