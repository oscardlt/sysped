             PGM
             DCL        VAR(&IN01) TYPE(*LGL) LEN(1) VALUE('0') /* +
                          Hvis &in01 på, betyr det avslutt program +
                          fra valgbilde. */
             DCL        VAR(&IN03) TYPE(*LGL) LEN(1) /* Hvis &in03 +
                          på, skal losseliste  være stående. */
             DCL        VAR(&IN04) TYPE(*LGL) LEN(1) /* Hvis &in04 +
                          på, skal printetype være 4224. */
             DCL        VAR(&JBK) TYPE(*CHAR) LEN(1) /* Jobbkø. */
             DCL        VAR(&WS) TYPE(*CHAR) LEN(10) /* Navn til +
                          arbeidsstasjon. */
             DCL        VAR(&UNIK) TYPE(*CHAR) LEN(7)
             DCL        VAR(&JOBN) TYPE(*CHAR) LEN(6)
             DCL        VAR(&FAXJN) TYPE(*CHAR) LEN(1)
             DCL        VAR(&BRUDDA) TYPE(*CHAR) LEN(1)
             DCL        VAR(&FAXNR) TYPE(*CHAR) LEN(15)
             DCL        VAR(&AVD) TYPE(*CHAR) LEN(4)
             DCL        VAR(&TUR) TYPE(*CHAR) LEN(8)
             DCL        VAR(&AGNULL) TYPE(*CHAR) LEN(8) +
                          VALUE('00000000')
             DCL        VAR(&EDIJN) TYPE(*CHAR) LEN(1)
             DCL        VAR(&LISJN) TYPE(*CHAR) LEN(1)
             DCL        VAR(&BELJN) TYPE(*CHAR) LEN(1)
             DCL        VAR(&BELJN2) TYPE(*CHAR) LEN(1)
             DCL        VAR(&UDSFAX) TYPE(*CHAR) LEN(1) VALUE('N')
             DCL        VAR(&DADRJN) TYPE(*CHAR) LEN(1)
    /* */
    /* HENTE AVDELING, SIGNATUR OG FIRMAKODE */
             CALL       PGM(SYFA00) PARM(&IN01)
             IF         COND(&IN01 = '1') THEN(GOTO CMDLBL(SLUTT)) /* +
                          Hvis &IN01 på, avslutter program. */
 RTVUNIK:    RTVJOBA    NBR(&JOBN)
             CHGVAR     VAR(&UNIK) VALUE('W' *CAT &JOBN)
             RMVM       FILE(WRKAGL01) MBR(&UNIK)
             MONMSG     MSGID(CPF7310)
             RMVM       FILE(WRKAG) MBR(&UNIK)
             MONMSG     MSGID(CPF7310)
             ADDPFM     FILE(WRKAG) MBR(&UNIK)
             MONMSG     MSGID(CPF7306) EXEC(GOTO CMDLBL(RTVUNIK))
             ADDLFM     FILE(WRKAGL01) MBR(&UNIK) DTAMBRS((WRKAG +
                          (&UNIK)))
             MONMSG     MSGID(CPF7306) EXEC(GOTO CMDLBL(RTVUNIK))
             OVRDBF     FILE(WRKAG) TOFILE(WRKAG) MBR(&UNIK)
             OVRDBF     FILE(WRKAGL01) TOFILE(WRKAGL01) MBR(&UNIK)
    /* */
    /* HENTE OPPLYSNINGER OM HVA SLAGS UTSKRIFT MAN ØNSKER */
             CALL       PGM(SYFAGO) PARM(&JBK &IN03 &IN04 &IN01 +
                          &FAXJN &FAXNR &AVD &TUR &BRUDDA &EDIJN +
                          &LISJN &BELJN &BELJN2 &DADRJN)
             IF         COND(&IN01 = '1') THEN(GOTO CMDLBL(SLUTT)) /* +
                          Hvis &IN01 på, avslutter program. */
    /* */
    /* ----------------------------------------------------------- */
    /* SKRIVER UT GODSLISTE  EKSPORT LAND  PÅ PAPIR */
    /* ----------------------------------------------------------- */
             IF         COND(&LISJN = 'J') THEN(DO)
             IF         COND(&JBK = 'J') THEN(DO)
             SBMJOB     CMD(CALL PGM(SYFA55CLJ) PARM(&IN03 &IN04 +
                          &AVD &TUR &BELJN &DADRJN)) JOB(GODSLISTE) +
                          JOBD(*USRPRF) JOBQ(SYJOBQPRT)
                        ENDDO
             ELSE       CMD(DO)
             CALL       PGM(SYFA55CLJ) PARM(&IN03 &IN04 &AVD &TUR +
                          &BELJN &DADRJN)
                        ENDDO
                        ENDDO
    /* ----------------------------------------------------------- */
    /* SENDER EDIFACT  AV GODSLISTEN - IFCSUM ELLER IFTMIN (V/DIST)*/
    /* ----------------------------------------------------------- */
             IF         COND(&EDIJN = 'J') THEN(DO)
             IF         COND(&DADRJN = 'J') THEN(DO)
             CALL       PGM(E305R) PARM(&BRUDDA)
                        ENDDO
             ELSE       CMD(DO)
             CALL       PGM(E300R) PARM(&BRUDDA)
                        ENDDO
                        ENDDO
    /* ----------------------------------------------------------- */
    /* SENDER GODSLISTE PÅ FAX   */
    /* ----------------------------------------------------------- */
             IF         COND(&FAXJN = 'J') THEN(DO)
             IF         COND(&BRUDDA = 'J') THEN(DO)
             CALL       PGM(SYFA55A)   PARM(&AVD &TUR &BELJN)
             GOTO       CMDLBL(SLUTT)
                        ENDDO
             CALL       PGM(FAX055CLD) PARM(&AVD &TUR &BELJN2 &AGNULL)
                        ENDDO
    /* */
             RMVM       FILE(WRKAGL01) MBR(&UNIK)
             MONMSG     MSGID(CPF7310)
             RMVM       FILE(WRKAG) MBR(&UNIK)
             MONMSG     MSGID(CPF7310)
 SLUTT:      ENDPGM
