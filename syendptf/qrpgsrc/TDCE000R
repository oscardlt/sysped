      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/TDCE000R MODULE(*LIBL/TDCE000R *LIBL/INCGI)
CRTPGM*        ACTGRP(*NEW) AUT(*USE)
      *
      *********************************************************************
      /copy SYSPEDS/qrpgsrcpy,hspecs
      /copy SYSPEDS/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FFRISOK    IF   E           K DISK    USROPN
     FBRPARF    IF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy SYSPEDS/qrpgsrcpy,prototypeb
      /copy SYSPEDS/qrpgsrcpy,usec
      /copy SYSPEDS/qrpgsrcpy,variables3
     ‚*  Prototype for 'JSONFIX'
     d jsonfix         pr                  extpgm('JSONFIX')
     d jsonstring_                         like(jsonstring)
     ‚*  Prototype for 'INCGI'
     d incgi           pr                  extproc('INCGI')
     d bistdl_                             like(bistdl)
     ‚*  Prototype for variabel program
     D Varpgm          Pr                  ExtPgm(BILIBL)
     ‚*  Prototype for 'SYGE20'
     d syge20          pr                  extpgm('SYGE20')
     d ufunk_                              like(ufunk)
     d udat1_                              like(udat1)
     d udat2_                              like(udat2)
     d udgr_                               like(udgr)
     d WhereC          s            512a
     d FirstW          s              1A
     d cQuote          C                   const('''')
     d SqlStm          s            512a
     d myrows          s             10i 0
     D WSUSER          S             10
     D AVD             S              4
     D AVDN            S              4  0
     D SIGN            S              3
     D OPD             S              7
     D OPDN            S              7S 0
     D LRN             S             14
     D MRN             S             18
     D DATUM           S              8
     D DATUMN          S              8S 0
     D DATUMT          S              8
     D DATUMTN         S              8S 0
     D STATUS          S              1
     D FORENKL         S              1
     D MOTNAVN         S             35
     D BVIKT           S             12
     D BVIKTN          S             11S 3
     D JSONstring      s          32000
     D Error           S            150
     D AntLest         S              9  0
     D Jn043           S              1
     d X               S             10  0
     d hopp            S              1
     d wspl            S              1
     d udat1           s              8  0
     d udat2           s              8  0
     d udgr            s              5  0
     d ufunk           s              3
     d wpabrid         s                   like(pabrid)
     d wpakunr         s                   like(pakunr)
     d wpasort         s                   like(pasort)
     d MyPoDS          ds                  Qualified  dim(100)
     d  THAVD                         4S 0
     d  THTDN                         7S 0
     d  DKXH_0035                     1
     d  THTUID                       14
     d  THSG                          3
     d  THTRNR                       18
     d  THDT                          8S 0
     d  THST                          1
     d  THNTK                         7  0
     d  THVKB                        11S 3
     d  THENKL                        1
     d  THNAS                        30
     d  THNAK                        30
     d  THLSTL                        1
     d  THVERS                        1
      *get input-string
      /copy syspeds/qrpgsrcpy,prolog3

      /free
         exsr ParseSr;
         exsr init;

       gethtmlifs(
       '/syspeddev/html/JSON.html':
       '/CGITAG/');
       wrtsection('top');

       //...........................................................................................
       //skriv JSON-Object start (alltid '{' + x ant param. m/komma mellom (IKKE komma etter siste))
       //...........................................................................................

        JSONstring='{'
        +'¬user¬ : ¬'+%trim(WSUSER)+'¬, '
        +'¬avd¬ : ¬'+%trim(%editc(AVDN:'Z'))+'¬, '
        +'¬sign¬ : ¬'+%trim(SIGN)+'¬, '
        +'¬lrn¬ : ¬'+%trim(LRN)+'¬, '
        +'¬opd¬ : ¬'+%trim(OPD)+'¬, '
        +'¬mrn¬ : ¬'+%trim(MRN)+'¬, '
        +'¬datum¬ : ¬'+%trim(DATUM)+'¬, '
        +'¬datumt¬ : ¬'+%trim(DATUMT)+'¬, '
        +'¬status¬ : ¬'+%trim(STATUS)+'¬, '
        +'¬motNavn¬ : ¬'+%trim(MOTNAVN)+'¬, '
        +'¬bvikt¬ : ¬'+%trim(BVIKT)+'¬, '
        +'¬errMsg¬ : ¬'+%trim(Error)+'¬, ';

       //JSONfix escaper " i tekst,  for deretter å bytte ¬->"

       jsonfix ( jsonstring );

       updhtmlvar2('JSONstring'
                  :%addr(jsonstring)
                  :%len(jsonstring));
       wrtsection('JSONlin');

       //...........................................................................................
       // Skriv JSON-LISTE Start (en liste er EN parameter(fra [ til   )
       //..........................................................................................
       jsonstring ='"orderList" : [';

       updhtmlvar2('JSONstring'
                  :%addr(jsonstring)
                  :%len(jsonstring));
       wrtsection('JSONlin');

       if error = *blanks;
         exsr SelSr;
         if myrows > 0;
           exsr LesSr;
         endif;
       endif;
       // ..........................................................................................
       // Skriv JSON-Liste/Array  Avslutning
       // ..........................................................................................
       jsonstring =']';
       updhtmlvar2('JSONstring'
                  :%addr(jsonstring)
                  :%len(jsonstring));
       wrtsection('JSONlin');
       // ..........................................................................................
       // Skriv JSON-string Avsluttning
       // ..........................................................................................
       jsonstring ='}';
       updhtmlvar2('JSONstring'
                  :%addr(jsonstring)
                  :%len(jsonstring));
       wrtsection('JSONlin');
         exsr Exit;

       //-----------------------------------------------------------------
       // Select fra fil via SQL
       //-----------------------------------------------------------------
       begsr     SelSr;

       If    AVDN > 0;
         AVD = %editc(AVDN:'X');
         Exsr  AddAnd;
         Eval  WhereC = ' THAVD  = ' + AVD;
       EndIf;

       If    SIGN  <> *blanks;
         Exsr  AddAnd;
         Eval  WhereC =  %trim(WhereC)  +
                       ' THSG = ' + cQuote + SIGN + cQuote +' ';
       EndIf;

       If    OPDN > 0;
         OPD = %editc(OPDN:'X');
         Exsr  AddAnd;
         Eval  WhereC =  %trim(WhereC)  +
         ' THTDN  = ' + OPD;
       EndIf;

       If    LRN  <> *blanks;
         Exsr  AddAnd;
         Eval  WhereC =  %trim(WhereC)  +
         ' THTUID LIKE ' + cQuote + %trim(LRN) + '%' + cQuote;
        EndIf;

       If    MRN  <> *blanks;
         Exsr  AddAnd;
         Eval  WhereC =  %trim(WhereC)  +
         ' THTRNR LIKE ' + cQuote + %trim(MRN) + '%' + cQuote;
        EndIf;

       If    DATUMN > 0;
         DATUM = %editc(DATUMN:'X');
         Exsr  AddAnd;
         Eval  WhereC =  %trim(WhereC)  +
         ' THDT  >= ' + DATUM;
       EndIf;

       If    DATUMTN > 0;
         DATUMT = %editc(DATUMTN:'X');
         Exsr  AddAnd;
         Eval  WhereC =  %trim(WhereC)  +
         ' THDT  <= ' + DATUMT;
       EndIf;

       If    STATUS  <> *blanks and STATUS <> 'b';
         Exsr  AddAnd;
         Eval  WhereC =  %trim(WhereC)  +
         ' THST = ' + cQuote + STATUS + cQuote;
        EndIf;

       If    MOTNAVN  <> *blanks;
         Exsr  AddAnd;
         Eval  WhereC =  %trim(WhereC)  +
         ' THNAK LIKE ' + cQuote + '%' + %trim(MOTNAVN) + '%' + cQuote;
        EndIf;

       If    BVIKTN > 0;
         BVIKT = %editc(BVIKTN:'4');
         Exsr  AddAnd;
         Eval  WhereC =  %trim(WhereC)  +
         ' THVKB  = ' + BVIKT;
       EndIf;

         Eval      WhereC =  ' Where ' + %trim(WhereC);

         Eval       SqlStm = 'Select THAVD, THTDN, DKXH_0035, THTUID, ' +
                             'THSG, THTRNR, THDT, THST, ' +
                             'THNTK, THVKB, THENKL, ' +
                             'THNAS, THNAK, THLSTL, THVERS ' +
                             'from DKXH ' +
                              %trim(WhereC)  +
                             ' Order by '       +
                             'THDT desc ' +
                             'FOR READ ONLY ';
         Exec SQL PREPARE DynSel from :SqlStm;
         EXEC SQL DECLARE  A33  cursor for DynSel;
         Exec SQL OPEN A33;
         Myrows = %elem(MyPoDS);
         exec sql FETCH A33   for :MyRows rows
                  INTO :MyPoDs;
       endsr;
       //-----------------------------------------------------------------
       // Set inn "AND" mellom where settninger
       //-----------------------------------------------------------------
       begsr     AddAnd;
       If        FirstW =   'N';
         Eval      WhereC =  %trim(WhereC) + ' and ';
       EndIf;
         Eval      FirstW =   'N';
       endsr;
       //-----------------------------------------------------------------
       // hent input
       //-----------------------------------------------------------------
       begsr     ParseSr;
         wsuser  = zhbgetvar('USER');
         avd     = zhbgetvar('AVD');
         avdn    = c2n2(avd);
         sign    = zhbgetvar('SIGN');
         opd     = zhbgetvar('OPD');
         opdn    = c2n2(opd);
         lrn     = zhbgetvar('LRN');
         mrn     = zhbgetvar('MRN');
         datum   = zhbgetvar('DATUM');
         datumn  = c2n2(datum);
         datumt  = zhbgetvar('DATUMT');
         datumtn = c2n2(datumt);
         status  = zhbgetvar('STATUS');
         motnavn = zhbgetvar('MOTNAVN');
         bvikt   = zhbgetvar('BVIKT');
         bviktn  = c2n2(bvikt);
        endsr;
        // __________________________________________________
        // Læs array                                    LESSR
        // """"""""""""""""""""""""""""""""""""""""""""""""""
           begsr LesSr;
       for x = 1 to myrows;

       if mypods(x).thsg = *blanks;
         leave;
       endif;

       if mypods(x).THLSTL <> *blanks;
         wspl = 'J';
       else;
         wspl = 'N';
       endif;

           chain (mypods(x).THAVD: mypods(x).THTDN: '*CN') FRISOK;
           if not %found;
              fssok = *blanks;
           endif;

        // Lægg ut Json stræng
           AntLest = AntLest + 1;

        // Skriv en JSON-Array-linje pr menypunkt - komma før hvert nytt element

       if AntLest=1;
          JSONstring=*blanks;
          else;
          JSONstring=', ';
          endif;
       JSONstring=%trim(JSONstring)+'{'
          +'¬avd¬ : ¬'+%trim(%editc(mypods(x).THAVD:'Z'))+'¬, '
          +'¬opd¬ : ¬'+%trim(%editc(mypods(x).THTDN:'Z'))+'¬, '
          +'¬tst¬ : ¬'+%trim(mypods(x).DKXH_0035)+'¬, '
          +'¬lrnNr¬ : ¬'+%trim(mypods(x).THTUID)+'¬, '
          +'¬sign¬ : ¬'+%trim(mypods(x).THSG)+'¬, '
          +'¬mrnNr¬ : ¬'+%trim(mypods(x).THTRNR)+'¬, '
          +'¬datum¬ : ¬'+%trim(%editc(mypods(x).THDT:'Z'))+'¬, '
          +'¬status¬ : ¬'+%trim(mypods(x).THST)+'¬, '
          +'¬kolli¬ : ¬'+%trim(%editc(mypods(x).THNTK:'Z'))+'¬, '
          +'¬bruttoVikt¬ : ¬'+%trim(%editc(mypods(x).THVKB:'3'))+'¬, '
          +'¬forenklad¬ : ¬'+%trim(mypods(x).THENKL)+'¬, '
          +'¬elList¬ : ¬'+%trim(WSPL)+'¬, '
          +'¬eksternref¬ : ¬'+%trim(FSSOK)+'¬, '
          +'¬avsNavn¬ : ¬'+%trim(mypods(x).THNAS)+'¬, '
          +'¬motNavn¬ : ¬'+%trim(mypods(x).THNAK)+'¬, '
          +'¬thvers¬ : ¬'+%trim(mypods(x).THVERS)+'¬}';
         jsonfix (JSONstring);
       updhtmlvar2('JSONstring'
                  :%addr(jsonstring)
                  :%len(jsonstring));
         wrtsection ('JSONlin');

        endfor;

       endsr;
       //********************************************************
       begsr init;
       //********************************************************
         open bridf;
         // Test innlogging
         chain wsuser bridf;

         if %found;
           if bilibl = *blanks;
             incgi ( bistdl );
           else;

             Monitor;
                   Varpgm();
                 On-Error;
                   //
                 EndMon;

           endif;
         else;

           error = 'Invalid userID';
         endif;
         open frisok;
         open brparf;

         // Er dato= blank eller 0, sett  da dato 7 dager tilbake
         if opdn=*zeros and lrn=*blanks and mrn=*blanks and datumn=*zeros;

           wpabrid=wsuser;
           wpakunr=0;
           wpasort=0;
           chain (WPABRID: WPAKUNR: 'DFTDG': WPASORT) BRPARF;
           if not %found or pavrdn = 0;
              pavrdn=7;
           endif;

           ufunk = 'SUB';
           udat1 = datumn;
           udgr = pavrdn;
           syge20 ( ufunk : udat1 : udat2 : udgr );
           datumn=udat2;
         endif;


       endsr;
       //=====================================================================
       // Close output html and quit
       //=====================================================================
       begsr exit;
         // Lukk fil
         close bridf;
         close frisok;
         close brparf;

         wrtsection('*fini');
         *inlr = *on;
         return;
       endsr;
      /end-free
