      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/EPOMS15 MODULE(*LIBL/EPOMS15
CRTPGM*        *LIBL/CHECKUSR *LIBL/INCGI) ACTGRP(CGI) AUT(*USE)
      *
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
      * 01 JOV PTF10 Title sette i RPG
10XXX * 02 YBC PTF10 PLUKK MULIGHET
10XXX * 03 YBC PTF10 TEST FRA/TIL STEDSKODE PÅ LINJE-NIVÅ
xxxxxx*xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
11XXX * 04 YBC PTF11 VIS KOLLI-ID, POMS LITAUEN
11XXX * 05 YBC PTF11 VIS KUN EGNE IO FOR LEVERANDØR
********************************************************************
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     UF   E           K DISK    USROPN
     FEPOH01    IF   E           K DISK    USROPN
     FEPOL      IF   E           K DISK    USROPN
N04  FEPOK      UF   E           K DISK    USROPN
N04  FBRPARF    IF   E           K DISK    USROPN
N04  FVADR3     IF   E           K DISK    USROPN
N02  FHEADF     IF   E           K DISK    USROPN
      *********************************************************************
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      /copy syspeds/qrpgsrcpy,variables3
      * Indicators for GetHtmlIfsMult subprocedure
     D IfsMultIndicators...
     d                 ds
     D  NoErrors                       n
     D  NameTooLong                    n
     D  NotAccessible                  n
     D  NoFilesUsable                  n
     D  DupSections                    n
     D  FileIsEmpty                    n

      *
     D OddEven         s             10
     0* Client input variables
     D user            s             10
     D UsrSpcName      s             10
     D WSKN            S              8
     D BSKN            S              8  0
     D WSTUR           s              8
     D WBTUR           s              8  0
     D WSAVD           s              4
     D WBAVD           s              4  0
     D WSOPD           s              7
     D WBOPD           s              7  0
     D WSBES           s             10
N02  D WPSBES          s             10
N02  D WPSLIN          s              7
N02  D WPBLIN          s              7  0
N02  D WPSULIN         s              5
N02  D WPBULIN         s              5  0
N02  D WPSSTA3         s              5
     D XIKKEPL         s              1
     D KUNSUP          s             15    VARYING
     D KUNFRA          s              8
     D KUNTIL          s              8
     D VisAChecked     s             10
     D VisAlle         s              1
     D DATO            s              8  0
     D ZZVKt           s             11  2
     D ZZM3            s              4  0
     D ETDMIN          s              8
     D ETDMINn         s              8  0
     D ETDMAX          s              8
     D ETDMAXn         s              8  0
     D From            s              8
     D To              s              8
     D STATUS          S            100
     D REASON          S            100
     D STATUSL         S            100
     D REASONL         S            100
     D Title           S            100
     D PGM             C                   CONST('SYSPED/CHECKUSR')
      *
     D ANTSND          s              5  0
     D MaxSN           s              5
     D MaxSND          s              5  0
     D ANTRCD          s              5  0
     D MaxRC           s              5
     D MaxRCD          s              5  0
     D Spaces          s             12a   inz('&nbsp;&nbsp;')
     D ItemCount       s             10i 0
     D i               s             10i 0
     D UPC             C                   CONST('ABCDEFGHIJKLMNOPQRST-
     D                                     UVWXYZÆØÅ')
     D LO              C                   CONST('abcdefghijklmnopqrst-
     D                                     uvwxyzæøå')
     D                 DS
     D  FraDs                  1      8
     D   EHFLK                 1      2
     D   StrFra                3      3    inz('-')
     D   EHFSTE                4      8
     D                 DS
     D  TilDs                  1      8
     D   EHTLK                 1      2
     D   StrTil                3      3    inz('-')
     D   EHTSTE                4      8
     D                 DS
     D  TstFrTi                1      8
     D   TstStr                3      3
N03  D                 DS
N03  D  LFraDs                 1      8
N03  D   ELFLK                 1      2
N03  D   LStrFra               3      3    inz('-')
N03  D   EFFSTE                4      8
N03  D                 DS
N03  D  LTilDs                 1      8
N03  D   ELTLK                 1      2
N03  D   LStrTil               3      3    inz('-')
N03  D   ELTSTE                4      8
      *
      /copy syspeds/qrpgsrcpy,prolog3
      *
      * Get program start time for calculating execution time
     C                   time                    timedata1
      * Parse input / cvt to numeric
     C                   exsr      Parse
      * File open / keys
     C                   EXSR      INIT
      *
     C                   CALLB     PGM
     C                   PARM                    BIBRID
     C                   PARM                    UsrSpcName
     C                   PARM                    UINN              1
     C                   IF        UINN = 'N'
     C                   GOTO      SLUTT
     C                   END
      * dato/tid siste vellykkede operasjon.
     C     USER          CHAIN     BRIDF                              50
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVE      WDT           BIDTF
     C                   MOVE      WTM           BITIDF
     C                   UPDATE    BRIDFR
N02   *
N02  C                   IF        WPSBES <> *BLANKS
N02  C                   CALL      'EPOMS15A'
N02  C                   PARM                    WBAVD             4 0
N02  C                   PARM                    WBOPD             7 0
N02  C                   PARM                    BIBRID
N02  C                   PARM                    BSKN
N02  C                   PARM                    WPSBES
N02  C                   PARM                    WPBLIN
N02  C                   PARM                    WPBULIN
N02  C                   PARM                    WPSSTA3
N02  C                   PARM                    XFEILPLK          1
N02  C                   PARM                    XFEILPLKT       100
N02  C                   END
      * Read output skeleton html member into memory
     C                   exsr      LoadHtml
      * Set section variables
     C                   exsr      Settop
     C                   callp     wrtsection('StdtopTab')
     C                   callp     wrtsection('top')                            Programmets html
N04  C                   IF        XJOBBKL = 'J'
N04  C                   callp     wrtsection('topCl01')
N04  C                   ELSE
N04  C                   callp     wrtsection('topCl02')
N04  C                   END
N04  C                   callp     wrtsection('topCl03')
N04  C                   IF        XJOBBKL = 'J'
N04  C                   callp     wrtsection('topCl1')
N04  C                   ELSE
N04  C                   callp     wrtsection('topCl2')
N04  C                   END
N04  C                   callp     wrtsection('topCl3')
      *
      * HOVEDRUTINE
      *
      * Vis liste over registrete saker
      *
     C     MaxSnd        IFEQ      *ZEROS
     C                   Z-add     500           MaxSnd
     C                   END
     C     MaxRcd        IFEQ      *ZEROS
     C                   Z-add     2000          MaxRcd
     C                   END
      *
     C                   Move      *zeros        AntSnd
     C                   Move      *zeros        AntRcd
      *
     c     Key           setll     EPOH01
     c     Key           Reade     EPOH01                                 35
      *
     c     *IN35         doweq     '0'
N05  C                   IF        XJOBBKLL <> *BLANKS AND XJOBBKLL <> EHLEVN
N05  C                   GOTO      NextRec
N05  C                   END
     C                   Add       1             AntRcd
     C     AntRcd        CabGe     MaxRcd        Utles
      * div filter
     c     KUNSUP        ifne      *BLANKS
N01  C     LO:UPC        XLATE     EHLNAV        TSTLNAV          30
     C     KUNSUP        SCAN      TSTLNAV                                33
     c     *IN33         CaBEQ     *OFF          NextRec
     C                   end
N03  c                   MOVE      KUNFRA        XSTEDF1           8
N03  c                   MOVE      KUNTIL        XSTEDT1           8
N03  c                   MOVE      ' '           TSTSTRF           1
N03  c                   MOVE      ' '           TSTSTRT           1
njov c     Kunfra        ifne      *blanks
     c                   eval      TstFrTi=Kunfra
     c     TstStr        ifeq      '-'
S03  c*    Kunfra        cabne     FraDS         NextRec                        LK-Sted
N03  c                   MOVE      FRADS         XSTEDF2           8
N03  c                   EVAL      TSTSTRF = '-'
     c                   else
S03  c*    Kunfra        cabne     EHFSTE        NextRec                        Kun sted
N03  c                   EVAL      XSTEDF2 = EHFSTE
njov c                   endif
njov c                   endif
njov c     Kuntil        ifne      *blanks
     c                   eval      TstFrTi=Kuntil
     c     TstStr        ifeq      '-'
S03  c*    Kuntil        cabne     TilDS         NextRec                        LK-Sted
N03  c                   MOVE      TILDS         XSTEDT2           8
N03  c                   EVAL      TSTSTRT = '-'
     c                   else
S03  c*    Kuntil        cabne     EHTSTE        NextRec                        Kun sted
N03  c                   EVAL      XSTEDT2 = EHTSTE
njov c                   endif
njov c                   endif
N03  c                   EXSR      TESTSTD
N03  c     XVISOK        CABNE     'J'           NEXTREC
      * Ok - skriv
      * linjenivå
     C     EPOLK         SETLL     EPOL
     C     EPOLK         READE     EPOL                                   33
     C                   DOW       *IN33 = *OFF
     c                   exsr      TstLinExc
     c                   if        LinExclud='N'
     C                   exsr      Setrow
     C                   callp     wrtsection('row')
N02   *
N02  C                   IF        WBAVD = 0 OR ELSTA3 = 'R'
N02  C                   EVAL      XPCKUPH = 'N'
N02  C                   EVAL      XPCKUPL = 'N'
N02  C                   ELSE
N02   * TEST OM BESTILLING/LINJE KAN PLUKKES.
N02  C                   CALL      'EPOMS17'
N02  C                   PARM                    BSKN
N02  C                   PARM                    EHBES
N02  C                   PARM                    ELLIN
N02  C                   PARM                    ELULIN
N02  C                   PARM                    HESDF
N02  C                   PARM                    HESDT
N02  C                   PARM                    XPCKUPH           1
N02  C                   PARM                    XPCKUPL           1
N02  C                   END
N02   * VIS PLUKK-TEGN HVIS BESTILLING/LINJE KAN PLUKKES.
N02  C                   IF        XPCKUPH = 'J'
N02  C                   callp     wrtsection('rowPckH1')
N02  C                   ELSE
N02  C                   callp     wrtsection('rowPckH2')
N02  C                   END
N02  C                   callp     wrtsection('rowPckH3')
N02  C                   IF        XPCKUPL = 'J'
N02  C                   callp     wrtsection('rowPckL1')
N02  C                   ELSE
N02  C                   callp     wrtsection('rowPckL2')
N02  C                   END
N02  C                   callp     wrtsection('rowPckL3')
N02   *
     C                   Add       1             AntSnd
     C     AntSnd        CabGe     MaxSnd        Utles
     C                   endif
     C     EPOLK         READE     EPOL                                   33
     C                   ENDDO
      *
     c     NextRec       Tag
     c     Key           Reade     EPOH01                                 35
     c                   enddo
      *
      *
     C     UTLES         TAG
     C                   callp     updHTMLvar('ANTSND':
     C                             %trim(%editc(ANTSND:'3')))
     C                   callp     updHTMLvar('MaxSnd':
     C                             %trim(%editc(MaxSnd:'3')))
     C                   callp     updHTMLvar('ANTRCD':
     C                             %trim(%editc(ANTRCD:'3')))
     C                   callp     updHTMLvar('MaxRcd':
     C                             %trim(%editc(MaxRcd:'3')))
      * Quit
      * Compute run time
     C                   time                    timedata2
     C     timedata2     subdur    timedata1     ms:*ms
     C                   eval      sec = ms / 1000000
     C                   callp     updhtmlvar('runtime':
     C                             %trim(%editw(sec:'     0 .   ')))
      *
     C                   callp     wrtsection('bot')                            programmets Html
     C                   callp     wrtsection('botStd')                         Body/htmlavsl
      *
     C     SLUTT         TAG
     C                   exsr      Exit
      *
     C                   eval      *inlr = *on
     C                   return
      *
N03   *=====================================================================
N03   * TEST STED FRA OG TIL
N03   *=====================================================================
N03  C     TESTSTD       begsr
N03   *
N03   * Subrutine skal teste om PO skal vises og hele PO kan plukkes.
N03   * XVISOK  = J  -->  Vis PO.
N03   * XIKKEPL = J  -->  Kan ikke plukke hele PO.
N03   *
N03   * XSTEDF1 --> Oppgitt fra-sted fra WEB
N03   * XSTEDT1 --> Oppgitt til-sted fra WEB
N03   * XSTEDF2 --> Oppgitt fra-sted fra hode
N03   * XSTEDT2 --> Oppgitt til-sted fra hode
N03   *
N03  C                   MOVE      'N'           XVISOK            1
N03  C                   IF        BSKN = 21647
N03   * VOICE KAN IKKE PLIKKE HELE PO
N03  C                   EVAL      XIKKEPL = 'J'
N03  C                   ELSE
N03  C                   EVAL      XIKKEPL = 'N'
N03  C                   END
N03   * TEST PÅ HODE-NIVÅ
N03  C                   IF        (XSTEDF1 = *BLANKS AND XSTEDT1 = *BLANKS) OR
N03  C                             (XSTEDF1 = *BLANKS AND XSTEDT1 = XSTEDT2) OR
N03  C                             (XSTEDF1 = XSTEDF2 AND XSTEDT1 = *BLANKS) OR
N03  C                             (XSTEDF1 = XSTEDF2 AND XSTEDT1 = XSTEDT2)
N03   * Vis PO hvis ingen begresning av steder fra web-input ELLER
N03   *        hvis ingen begresning av fra-sted fra web-input og til-sted er lik ELLER
N03   *        hvis ingen begresning av til-sted fra web-input og fra-sted er lik ELLER
N03   *        hvis både fra-sted og til-sted er like.
N03  C                   MOVE      'J'           XVISOK
N03  C                   END
N03  C                   IF        HESDF <> EHFSTE OR HESDT <> EHTSTE
N03   * Ikke plukk hele PO når enten fra-sted eller til-sted er ulik fra turens.
N03  C                   MOVE      'J'           XIKKEPL
N03  C                   END
N03   *
N03   * TEST PÅ LINJE-NIVÅ
N03  C     EPOLK         SETLL     EPOL
N03  C     EPOLK         READE     EPOL                                   33
N03  C                   DOW       *IN33 = *OFF
N03  C                   IF        ELFSTE <> *BLANKS OR ELTSTE <> *BLANKS
N03   * Ikke plukk hele PO når enten avvikende fra-sted eller til-sted på linje-nivå.
N03  C                   MOVE      'J'           XIKKEPL
N03  C                   END
N03   *
N03  C                   SELECT
N03   *
N03  C                   WHEN      XSTEDF1 = *BLANKS AND XSTEDT1 = *BLANKS
N03   * Ingen begrensning fra web på fra/til sted. Benytt test på hode tidligere.
N03   *
N03  C                   WHEN      XSTEDF1 = *BLANKS AND XSTEDT1 <> *BLANKS
N03   * Begrensing av til-sted fra web. Test om linje skal vises.
N03   * Vis hvis til-sted på linje er lik med til-sted fra web. Ellers benytt test på hode-nivå.
N03  C                   IF        TstStrT = '-'
N03  C                   IF        XSTEDT1 = LTilDS
N03  C                   MOVE      'J'           XVISOK
N03  C                   END
N03  C                   ELSE
N03  C                   IF        XSTEDT1 = ELTSTE
N03  C                   MOVE      'J'           XVISOK
N03  C                   END
N03  C                   END
N03   *
N03  C                   WHEN      XSTEDF1 <> *BLANKS AND XSTEDT1 = *BLANKS
N03   * Begrensing av fra-sted fra web. Test om linje skal vises.
N03   * Vis hvis fra-sted på linje er lik med fra-sted fra web. Ellers benytt test på hode-nivå.
N03  C                   IF        TstStrF = '-'
N03  C                   IF        XSTEDF1 = LFraDS
N03  C                   MOVE      'J'           XVISOK
N03  C                   END
N03  C                   ELSE
N03  C                   IF        XSTEDF1 = ELFSTE
N03  C                   MOVE      'J'           XVISOK
N03  C                   END
N03  C                   END
N03   *
N03  C                   WHEN      XSTEDF1 <> *BLANKS AND XSTEDT1 <> *BLANKS
N03   * Begrensing av fra/til sted fra web. Test om linje skal vises.
N03   * Vis PO hvis fra/til sted i hode eller linje er lik fra/til sted fra web.
N03  C                   SELECT
N03  C                   WHEN      TstStrF = '-' AND TstStrT = '-'
N03   * Fra/til sted i web er med landkode.
N03  C                   IF        (XSTEDF1 = LFraDS OR XSTEDF1 = FraDS) AND
N03  C                             (XSTEDT1 = LTilDS OR XSTEDT1 = TilDS)
N03  C                   MOVE      'J'           XVISOK
N03  C                   END
N03  C                   WHEN      TstStrF = '-' AND TstStrT <> '-'
N03   * Fra-sted i web er med landkode. Til-sted er uten landkode
N03  C                   IF        (XSTEDF1 = LFraDS OR XSTEDF1 = FraDS) AND
N03  C                             (XSTEDT1 = ELTSTE OR XSTEDT1 = EHTSTE)
N03  C                   MOVE      'J'           XVISOK
N03  C                   END
N03  C                   WHEN      TstStrF <> '-' AND TstStrT <> '-'
N03   * Fra/til sted i web er uten landkode.
N03  C                   IF        XSTEDF1 = ELFSTE OR XSTEDT1 = ELTSTE
N03  C                   MOVE      'J'           XVISOK
N03  C                   END
N03  C                   WHEN      TstStrF <> '-' AND TstStrT = '-'
N03   * Til-sted i web er med landkode. Fra-sted er uten landkode
N03  C                   IF        XSTEDF1 = ELFSTE OR XSTEDT1 = LTilDS
N03  C                   MOVE      'J'           XVISOK
N03  C                   END
N03  C                   ENDSL
N03   *
N03  C                   ENDSL
N03   *
N03  C                   IF        XVISOK = 'J' AND XIKKEPL = 'J'
N03   * Kan vises og ikke plukk hele PO --> treng ikke å teste videre.
N03  C                   LEAVE
N03  C                   END
N03   *
N03  C     EPOLK         READE     EPOL                                   33
N03  C                   ENDDO
N03   *
N03  C                   endsr
      *
      **********************************************************
     C     TstLinExc     Begsr
      * startverdi på flagg=Som om linjen skal ekskluderes..
     C                   move      'Y'           LinExclud         1
     c     ELOPD         cabne     *zeros        UtLinExc
      *
     c     ETDminN       ifne      *zeros
     c     ELETD         cablt     ETDminN       UtLinExc
     c                   endif
     c     ETDmaxN       ifne      *zeros
     c     ELETD         cabgt     ETDmaxN       UtLinExc
     c                   endif
     C* kom gjennom alle tester - sett 'N' i Exclud-flagg
     C                   move      'N'           LinExclud         1
     C     UtLinExc      endsr
      *
      *=====================================================================
      * Parse input / cvt to numeric
      *=====================================================================
     C     Parse         begsr
      * Parse variables from QUERY_STRING environment variable
     C                   eval      MAXSN    = zhbgetvar('MaxSn')
     C                   eval      MaxSnd   = c2n2(MAXSN)
     C                   eval      MAXRC    = zhbgetvar('MaxRc')
     C                   eval      MaxRcd   = c2n2(MAXRC)
      * Userid.....
     C                   eval      user = zhbgetvar('user')
     C                   EVAL      UsrSpcName  = zhbgetvar('UsrSpcName')
     C                   eval      WSKN    = zhbgetvar('WSKN')
     C                   eval      BSKN    = c2n2(WSKN)
     C                   eval      WSTUR    = zhbgetvar('WSTUR')
     C                   EVAL      WBTUR    = c2n2(WSTUR)
     C                   eval      WSAVD    = zhbgetvar('WSAVD')
     C                   EVAL      WBAVD    = c2n2(WSAVD)
     C                   eval      WSOPD    = zhbgetvar('WSOPD')
     C                   EVAL      WBOPD    = c2n2(WSOPD)
     C                   eval      WSBES    = zhbgetvar('WSBES')
N02  C                   eval      WPSBES   = zhbgetvar('WPSBES')
N02  C                   eval      WPSLIN   = zhbgetvar('WPSLIN')
N02  C                   EVAL      WPBLIN   = c2n2(WPSLIN)
N02  C                   eval      WPSULIN  = zhbgetvar('WPSULIN')
N02  C                   EVAL      WPBULIN  = c2n2(WPSULIN)
N02  C                   eval      WPSSTA3  = zhbgetvar('WPSSTA3')
     C                   eval      KUNSUP  = zhbgetvar('KUNSUP')
N01  C     LO:UPC        XLATE     KUNSUP        KUNSUP
     C                   eval      KUNFRA   = zhbgetvar('KUNFRA')
N01  C     LO:UPC        XLATE     KUNFRA        KUNFRA
     C                   eval      KUNTIL   = zhbgetvar('KUNTIL')
N01  C     LO:UPC        XLATE     KUNTIL        KUNTIL
     C                   eval      ETDmin  = zhbgetvar('ETDmin')
     C                   eval      ETDminN = c2n2(ETDmin)
     C                   eval      ETDmax  = zhbgetvar('ETDmax')
     C                   eval      ETDmaxN = c2n2(ETDmax)
     c                   if        ETDminN<>0 and ETDmaxN=0
     c                   move      ETDmin        ETDmax
     c                   move      ETDminN       ETDmaxN
     c                   endif
      * Checkboks for Vis alle
      *    ved KLIKK på checkboks kommer verdi Y/blank,
      *    ellers kommer verdi fra siste valg via "VisAlle" som checked / blank(N)
     C     VisAchecked   Ifeq      'checked'
     C                   eval      VisAlle  = 'Y'
     C                   else
     C                   eval      VisAlle  = zhbgetvar('VisAlle')
     C                   end
     C                   endsr
      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   CLOSE     BRIDF
     C                   CLOSE     EPOH01
     C                   CLOSE     EPOL
N04  C                   CLOSE     EPOK
N04  C                   CLOSE     BRPARF
N04  C                   CLOSE     VADR3
N02  C                   CLOSE     HEADF
     C                   endsr
      *=====================================================================
      * Read output skeleton html member into memory
      *=====================================================================
     C     LoadHtml      begsr
cccccC                   eval      IfsMultIndicators = gethtmlifsmult(
cccccC                             '/syspeddev/html/eSpedTop.html -
cccccC                             /syspeddev/html/EPOMS15.html -
cccccC                             /syspeddev/html/eSpedBot.html':
cccccC                             '/CGITAG/')
     C                   endsr
      *=====================================================================
      *  Set variable data for section /$top
      *=====================================================================
     C     SetTop        begsr
      *
N01  c                   eval      Title='POMS Purchase Order Management System'
N01  C                   callp     updhtmlvar('TITLE':Title)
     c
     C                   callp     updHTMLvar('ROWHEAD':RowHead)
     C                   callp     updHTMLvar('LogoImg':LogoImg)
     C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updHTMLvar('USER':USER)
     C                   callp     updHtmlVar('UsrSpcName':UsrSpcName)
     C                   callp     updHtmlVar('WSKN':
     C                             %trim(%editc(BSKN:'Z')))
     C                   callp     updHTMLvar('BESK':BIBESK)
     C                   callp     updHtmlVar('KUNDE':
     C                             %trim(%editc(BIKUNR:'Z')))
     C                   callp     updHTMLvar('KNAVN':BIBESK)
     C                   callp     updHTMLvar('WSTUR':WSTUR)
     C                   callp     updHTMLvar('WSAVD':WSAVD)
     C                   callp     updHTMLvar('WSOPD':WSOPD)
     C                   callp     updHTMLvar('EHBES':EHBES)
     C                   callp     updHTMLvar('KUNSUP':KUNSUP)
     C                   callp     updHTMLvar('KUNFRA':KUNFRA)
     C                   callp     updHTMLvar('KUNTIL':KUNTIL)
     C                   callp     updHTMLvar('ETDmin':ETDmin)
     C                   callp     updHTMLvar('ETDmax':ETDmax)
      * Søkeparam
     C                   callp     updHTMLvar('VISAlle':VISAlle)
     C     VisAlle       Ifeq      'Y'
     C                   eval      VisAChecked = 'checked'
     C                   end
     C                   callp     updHTMLvar('VisAChecked':VisAChecked)
      *
     C                   endsr
      *=====================================================================
      *  Set variable data for section /$row
      *=====================================================================
     C     Setrow        begsr
      *
      * HEADER/PO LEVEL
     C                   callp     updHTMLvar('EHBES':EHBES)
     c                   z-add     EHDATO        DATO
     C                   callp     updHTMLvar('EHLNAV':EHLNAV)
     C                   callp     updHTMLvar('EHBLNR':EHBLNR)
     C                   callp     updHTMLvar('EHORIG':EHORIG)
     c*                  eval      From = EHFLK +'-'+EHFSTE
N03  C                   IF        ELFSTE = *BLANKS
     c                   eval      From = EHFSTE
N03  C                   ELSE
N03   * Vis avvikende fra-sted fra linje
N03  c                   eval      From = ELFSTE
N03  C                   END
     C                   callp     updHTMLvar('FROM':FROM)
     c*                  eval      To   = EHTLK +'-'+EHTSTE
N03  C                   IF        ELTSTE = *BLANKS
     c                   eval      To   = EHTSTE
N03  C                   ELSE
N03   * Vis avvikende til-sted fra linje
N03  c                   eval      To   = ELTSTE
N03  C                   END
     C                   callp     updHTMLvar('EHTRKO':EHTRKO)
     C                   callp     updHTMLvar('TO':TO)
     c                   z-add     EHVKT         ZZVKT
     C                   callp     updHTMLvar('EHVKT':
     C                             %trim(%editc(ZZVKT:'Z')))
     c                   z-add     EHM3          ZZM3
     C                   callp     updHTMLvar('EHM3':
     C                             %trim(%editc(ZZM3:'Z')))
     c                   select
     c     EHSTA         wheneq    ' '
     c     EHSTA         oreq      '0'
     c                   eval      STATUS = 'Pending'
     c     EHSTA         wheneq    '1'
N04  c                   IF        XJOBBKL = 'J'
N04  c                   eval      STATUS = 'Confirmed'
N04  c                   ELSE
     c                   eval      STATUS = 'Contacted'
N04  c                   END
     c     EHSTA         wheneq    '15'
     c                   eval      STATUS = 'Ready for stuffing'
     c     EHSTA         wheneq    '2'
N04  c                   IF        XJOBBKL = 'J'
N04  c                   eval      STATUS = 'Printed'
N04  c                   ELSE
     c                   eval      STATUS = 'Booked'
N04  c                   END
     c     EHSTA         wheneq    '3'
N04  c                   IF        XJOBBKL = 'J'
N04  c                   eval      STATUS = 'Colleted'
N04  c                   ELSE
     c                   eval      STATUS = 'Confirmed'
N04  c                   END
     c     EHSTA         wheneq    '4'
N04  c                   IF        XJOBBKL = 'J'
N04  c                   eval      STATUS = 'Received'
N04  c                   ELSE
     c                   eval      STATUS = 'Shipped'
N04  c                   END
     c     EHSTA         wheneq    '5'
N04  c                   IF        XJOBBKL = 'J'
N04  c                   eval      STATUS = 'Out terminal'
N04  c                   ELSE
     c                   eval      STATUS = 'Arrived'
N04  c                   END
     c     EHSTA         wheneq    '6'
     c                   eval      STATUS = '6*Undefined*'
     c                   endsl
     C                   callp     updHTMLvar('STATUS':STATUS)
      *
     c                   select
     c     EHREAS        wheneq    ' '
     c                   eval      REASON = *blanks
     c     EHREAS        wheneq    'xxx'
     c                   eval      REASON = EHREAS + ' xxxxxxxxxxxxx'
     c     EHREAS        wheneq    'AAAA'
     c                   eval      REASON = EHREAS + ' YYYYYYYYYYYYY'
     c     EHREAS        wheneq    'XYX..'
     c                   eval      REASON = EHREAS + ' ZZZZZZZZZZZZZ'
     c                   other
     c                   eval      REASON = EHREAS
     c                   endsl
     C                   callp     updHTMLvar('REASON':REASON)
      * LINE LEVEL
     C                   callp     updHTMLvar('ELLIN':
     C                             %trim(%editc(ELLIN:'Z')))
     C                   callp     updHTMLvar('ELULIN':
     C                             %trim(%editc(ELULIN:'Z')))
N02  C                   callp     updHTMLvar('ELSTA3':ELSTA3)
     C                   callp     updHTMLvar('ELART':ELART)
     C                   callp     updHTMLvar('ELARTL':ELARTL)
     C                   callp     updHTMLvar('ETD':
     C                             %trim(%editc(ELETD:'Z')))
     C                   callp     updHTMLvar('ELABES':ELABES)
N04  C                   callp     updHTMLvar('EHINV':EHINV)
N04  C                   EVAL      VAKURE = EHINV
N04  C     VADR3K        CHAIN     VADR3                              33
N04  C                   IF        *IN33
N04  C                   callp     updHTMLvar('VADRNA':' ')
N04  C                   ELSE
N04  C                   callp     updHTMLvar('VADRNA':VADRNA)
N04  C                   END
     C                   callp     updHTMLvar('ELBLNR':ELBLNR)
     C                   callp     updHTMLvar('ELORIG':ELORIG)
     C                   callp     updHTMLvar('ELNTSL':
     C                             %trim(%editc(ELNTSL:'Z')))
     c                   z-add(H)  ELNTKL        XXNTKL            6 0
     C                   callp     updHTMLvar('ELNTKL':
     C                             %trim(%editc(xxNTKL:'Z')))
     c                   z-add     ELVKTL        ZZVKT
     C                   callp     updHTMLvar('ELVKTL':
     C                             %trim(%editc(ZZVKT:'4')))
     C                   callp     updHTMLvar('ELM3L':
     C                             %trim(%editc(ELM3L:'4')))
     c                   select
     c     ELSTA         wheneq    ' '
     c     ELSTA         oreq      '0'
     c                   eval      STATUSL= 'Pending'
     c     ELSTA         wheneq    '1'
     c                   eval      STATUSL= 'Contacted'
     c     ELSTA         wheneq    '2'
     c                   eval      STATUSL= 'Booked'
     c     ELSTA         wheneq    '3'
     c                   eval      STATUSL= 'Confirmed'
     c     ELSTA         wheneq    '4'
     c                   eval      STATUSL= 'Shipped'
     c     ELSTA         wheneq    '5'
     c                   eval      STATUSL= 'Arrived'
     c     ELSTA         wheneq    '6'
     c                   eval      STATUSL= '6*Undefined*'
N04  C     ELSTA         wheneq    'A'
N04  C                   eval      STATUSL = 'Confirmed'
N04  C     ELSTA         wheneq    'B'
N04  C                   eval      STATUSL = 'Printed'
N04  C     ELSTA         wheneq    'C'
N04  C                   eval      STATUSL = 'Collected'
N04  C     ELSTA         wheneq    'D'
N04  C                   eval      STATUSL = 'Received'
N04  C     ELSTA         wheneq    'E'
N04  C                   eval      STATUSL = 'Out terminal'
N04  C     ELSTA         wheneq    'F'
N04  C                   eval      STATUSL = 'Deliveried'
     c                   endsl
     C                   callp     updHTMLvar('STATUSL':STATUSL)
      *
     c                   select
     c     ELREAS        wheneq    ' '
     c                   eval      REASONL= *blanks
     c     ELREAS        wheneq    'xxx'
     c                   eval      REASONL= ELREAS + ' xxxxxxxxxxxxx'
     c     ELREAS        wheneq    'AAAA'
     c                   eval      REASONL= ELREAS + ' YYYYYYYYYYYYY'
     c     ELREAS        wheneq    'XYX..'
     c                   eval      REASONL= ELREAS + ' ZZZZZZZZZZZZZ'
     c                   other
     c                   eval      REASONL= ELREAS
     c                   endsl
     C                   callp     updHTMLvar('REASONL':REASONL)
      * siste staus ikke sendt, indiker i liste ??
     c                   move      *blanks       HSTA2style      100
     c                   move      *blanks       LSTA2style      100
     C                   if        EHSTA2='S'
     c                   eval      HSTA2style = 'bgcolor="#E65F5F"'
     c                   endif
     C                   if        ELSTA2='S'
     c                   eval      LSTA2style = 'bgcolor="#E65F5F"'
     c                   endif
     C                   callp     updHTMLvar('HSTA2style':HSTA2style)
     C                   callp     updHTMLvar('LSTA2style':LSTA2style)
N04   *
N04  C                   MOVE      *BLANKS       KOLLINR        1024
N04  C                   IF        XJOBBKL = 'J'
N04  C     EPOKK         SETLL     EPOK
N04  C     EPOKK         READE     EPOK                                   33
N04  C                   DOW       *IN33 = *OFF
N04  C                   IF        EKDTPR <> 0
N04  C                   IF        KOLLINR = *BLANKS
N04  C                   EVAL      KOLLINR = EKMRK
N04  C                   ELSE
N04  C                   EVAL      KOLLINR = %TRIM(KOLLINR) + ' ' + %TRIM(EKMRK)
N04  C                   END
N04  C                   END
N04  C     EPOKK         READE     EPOK                                   33
N04  C                   ENDDO
N04  C                   END
N04  C                   callp     updHTMLvar('KOLLINR':KOLLINR)
      *
     C                   endsr
      *
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
     C                   OPEN      BRIDF
     C     USER          CHAIN(N)  BRIDF                              50
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
      *
     C                   OPEN      EPOH01
     C                   OPEN      EPOL
N04  C                   OPEN      EPOK
N04  C                   OPEN      BRPARF
N04  C                   OPEN      VADR3
N02  C                   OPEN      HEADF
      *
      * hent Parametre som går igjen i mange prog:
      *      Odd/Even/Rowhead-farger,Logobilde,Språk,Intern/Ekstern bruker,  mm
     c                   call      'ESGETPAR'
     c                   parm                    BIBRID
     c                   parm                    BIFIRM
     c                   parm                    BIKUNR
     c                   parm                    BIKNG
     c                   parm                    RowHead          10
     c                   parm                    Odd              10
     c                   parm                    Even             10
     c                   parm                    LogoImg          50
     c                   parm                    XSPRÅK            2
     c                   parm                    XINTERN           1
     c                   parm                    ParmDS1        1024
     c                   parm                    ParmDS2        1024
     c                   parm                    ParmDS3        1024
      *
     c     Key           klist
     C                   kfld                    BSKN
     c     EPOLK         klist
     C                   kfld                    BSKN
     c                   kfld                    EHBES
N04  C     EPOKK         klist
N04  C                   kfld                    BSKN
N04  C                   kfld                    EHBES
N04  C                   kfld                    ELLIN
N04  C     VADR3K        klist
N04  C                   kfld                    BIFIRM
N04  C                   kfld                    EHKUND
N04  C                   kfld                    VAKURE
N04  C     BrParKey      klist
N04  C                   kfld                    PABRID
N04  C                   kfld                    PAKUNR
N04  C                   kfld                    PAID
N02  C     HEADFK        klist
N02  C                   kfld                    WBAVD
N02  C                   kfld                    WBOPD
N02   *
N02  C     HEADFK        CHAIN     HEADF                              33
N04   *
N04  C                   MOVE      BIBRID        PABRID
N04  C                   MOVE      *zeros        PAKUNR
N04  C                   MOVE      'POMKL'       PAID
N04  C                   MOVE      'N'           XJOBBKL           1
N05  C                   MOVE      *BLANKS       XJOBBKLL         47
N04  C     BrParKey      chain     BRPARF                             33
N04  C  N33              IF        %SUBST(PAVRDA:1:1) = 'J' OR
N04  C                             %SUBST(PAVRDA:1:1) = 'j'
N04  C                   EVAL      XJOBBKL = 'J'
N05  C                   EVAL      XJOBBKLL = %SUBST(PAVRDA:3:47)
N04  C                   END
      *
     C                   endsr
      *
