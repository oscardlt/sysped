********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
12068 * 01 CB  PTF12 3 desimaler på fakturabeløp
12XXX * 02 CB  PTF12 Umulig kombinasjon
      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/TDKE017R MODULE(*LIBL/TDKE017R *LIBL/INCGI)
CRTPGM*        ACTGRP(*NEW) AUT(*USE)
      *
      *********************************************************************
      /copy SYSPEDS/qrpgsrcpy,hspecs
      /copy SYSPEDS/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FDKTVK     IF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy SYSPEDS/qrpgsrcpy,prototypeb
      /copy SYSPEDS/qrpgsrcpy,usec
      /copy SYSPEDS/qrpgsrcpy,variables3
      *
      * Varible for
     D WSUSER          S             10
     D DKEH_221        S              3
     D DKEH_221B       S              7  4
     D DKEH_221C       S              7  0
S01  D*DKEH_222        S             11  3
N01  D DKEH_222        S             18  3
     D DKEV_46         S             11  0
S01  D*DKEV_42         S             15  0
N01  D DKEV_42         S             18  3
     D A07             S              7
     D A11             S             11
N01  D A19             S             11
     D JSONstring      s          32000
     D Error           S            150
     D AntLest         S              9  0

      *get input-string
      /copy syspeds/qrpgsrcpy,prolog3

      * Parse input
     C                   exsr      Parse
      * Open files etc
     C                   EXSR      INIT
      *
     c                   callp     gethtmlifs(
     C                             '/syspeddev/html/JSON.html':
     C                             '/CGITAG/')
     C                   callp     wrtsection('top')
      *
      * ............................................................................................
      * skriv JSON-Object start..(alltid '{' + x ant param. m/komma mellom (IKKE komma etter siste))
      * ............................................................................................
      *
      /free
        JSONstring='{'
        +'¬user¬ : ¬'+%trim(WSUSER)+'¬, '
        +'¬errMsg¬ : ¬'+%trim(Error)+'¬, ';
      /end-free
      * JSONfix escaper " i tekst,  for deretter å bytte ¬->"
     c                   call      'JSONFIX'
     c                   parm                    JSONstring
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')
      *
      * ............................................................................................
      * Skriv JSON-LISTE Start (en liste er EN parameter(fra [ til   )
      * ............................................................................................
     C                   eval      JSONstring ='"statvaluecalc" : ['
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')
      *
      * Hæmta detta ena ærendet.
      *
     c     Error         ifeq      *blanks
     C                   EXSR      AVG
      * ............................................................................................
      * Skriv en JSON-Array-linje pr menypunkt - komma før hvert nytt element
      * ............................................................................................
      /free
          JSONstring=*blanks;
       JSONstring=%trim(JSONstring)+'{'
          +'¬dkev_46¬ : ¬'+%trim(%editc(DKEV_46:'4'))+'¬}';
      /end-free
     C                   call      'JSONFIX'
     C                   parm                    JSONstring
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')
      *
     c                   endif
     c                   eval      JSONstring =']'
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')
      * ............................................................................................
      * Skriv JSON-string Avsluttning
      * ............................................................................................
     c                   eval      JSONstring ='}'
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')
      *
      * Quit
     C                   exsr      Exit


      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Lukk fil
     C                   CLOSE     BRIDF
     C  N50              CLOSE     DKTVK

      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   MOVE      *ON           *INLR
     C                   endsr
      *
      *********************************************************
     C     Parse         Begsr
      *********************************************************
      * Get input
     C                   EVAL      WSUSER     =  zhbgetvar('USER')
     C                   EVAL      DKEH_221   =  zhbgetvar('DKEH_221 ')
     C                   EVAL      A07        =  zhbgetvar('DKEH_221B')
     C                   EVAL      DKEH_221B  =  c2n2(A07)
     C                   EVAL      A11        =  zhbgetvar('DKEH_221C')
     C                   EVAL      DKEH_221C  =  c2n2(A11)
S01  C*                  EVAL      A11        =  zhbgetvar('DKEH_222')
N01  C                   EVAL      A19        =  zhbgetvar('DKEH_222')
S01  C*                  EVAL      DKEH_222   =  c2n2(A11)
N01  C                   EVAL      DKEH_222   =  c2n2(A19)
     C                   EVAL      A11        =  zhbgetvar('DKEV_46')
     C                   EVAL      DKEV_46    =  c2n2(A11)
S01  C*                  EVAL      A11        =  zhbgetvar('DKEV_42')
N01  C                   EVAL      A19        =  zhbgetvar('DKEV_42')
S01  C*                  EVAL      DKEV_42    =  c2n2(A11)
N01  C                   EVAL      DKEV_42    =  c2n2(A19)
     c                   endsr
      *
      *********************************************************
     C     INIT          Begsr
      *********************************************************
     C                   OPEN      BRIDF                                50
     C     *IN50         CABEQ     *ON           OPENTAG
      * Test innlogging
     C     WSUSER        CHAIN     BRIDF                              50
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C                   call      BILIBL
     C                   end
      *
     C                   Z-ADD     0             WTN               2 0
     C                   Z-ADD     0             ZCIFTOT          18 4
     C     *LIKE         DEFINE    DKEH_222      ZVIH_222
     C     *LIKE         DEFINE    DKVK_KD       ZVVK_KD
     C     *LIKE         DEFINE    DKVK_DTS      ZVVK_DTS
     C     DKTVKKEY      KLIST
     C                   KFLD                    ZVVK_KD
     C                   KFLD                    ZVVK_DTS
      *
     C     OPENTAG       TAG
     C   50              eval      Error = 'Invalid userID'
     C  N50              OPEN      DKTVK
      *_________________________________
      * Hæmta dagens datum och klockslag
      *"""""""""""""""""""""""""""""""""
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVEL     WDT           XDATO             8 0

     c                   endsr
      *
      *_________________________________________________
      * Avgiftsberækning                             AVG
      *"""""""""""""""""""""""""""""""""""""""""""""""""
     C     AVG           BEGSR
      * Rækna ut totalt CIF værde
     C                   EXSR      SRCIFTOT
      * Starta med Statistiskt værde
     C                   EXSR      SRCIFDET
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Berækna CIF totalt                              SRCIFTOT /
      *////////////////////////////////////////////////////////////
     C     SRCIFTOT      BEGSR
      *__________________________________
      * Summera alla kostnader och avdrag
      *""""""""""""""""""""""""""""""""""
     C                   EVAL      ZVIH_222=*ZEROS
      * Fakturabelopp i Danska kronor
     C     DKEH_222      MULT      DKEH_221B     ZVIH_222
     C                   EVAL      ZVVK_KD=DKEH_221
     C                   MOVEL     WDT           ZVVK_DTS
     C     DKTVKKEY      SETGT     DKTVK
     C                   READP     DKTVK                                  64
     C                   IF        *IN64=*OFF
     C                   IF        DKVK_OMR > 0 and ZVIH_222 > 0
     C     ZVIH_222      DIV       DKVK_OMR      ZVIH_222
     C                   ENDIF
     C                   ENDIF
     C                   EVAL      ZCIFTOT=ZVIH_222
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Berækna CIF før varupost                        SRCIFDET /
      *////////////////////////////////////////////////////////////
     C     SRCIFDET      BEGSR
     C                   IF        DKEH_222  = ZCIFTOT
     C                   IF        DKEV_46   = *ZEROS
     C                   EVAL      DKEV_46   = DKEV_42
     C                   ENDIF
     C                   ELSE
N02  C                   EVAL      ZFAKTOR=1
     C                   IF        DKEH_222  > 0
     C                   IF        DKEV_42   > 0
N02  C     DKEV_42       IFLE      DKEH_222
     C     DKEV_42       DIV       DKEH_222      ZFAKTOR          18 8
N02  C                   ENDIF
     C                   ENDIF
     C                   ENDIF
     C                   IF        DKEV_46   = *ZEROS
     C     ZCIFTOT       MULT      ZFAKTOR       DKEV_46
     C                   ENDIF
     C                   ENDIF
     C                   ENDSR
