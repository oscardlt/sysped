     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      REF(*LIBL/HEADF)
     A                                      PRINT
     A                                      CF03(03 'Avslutt')
     A                                      HLPDOC('STD' 'HELPNU' 'HLP400')
     A                                      HELP
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  6  3VALUES(' ' '5')
     A            HEPOS1    R        H
     A            WSPOS1         4A  O  6  6
     A            HENAS     R   30   O  6 11
     A            HENAK     R   30   O  6 42
     A            HEOT      R    2   O  6 73
     A            HEFR      R    3   O  6 76
     A            HEAVD     R    4  0O  7  5EDTCDE(4)
     A                                      DSPATR(UL)
     A            HEOPD     R    7  0O  7 10EDTCDE(4)
     A                                      DSPATR(UL)
     A            HENT      R    7  0H
     A            W2NT           5  0O  7 18EDTCDE(4)
     A                                      DSPATR(UL)
     A            HEVKT     R    9  0H
     A            W2VKT          5  0O  7 24EDTCDE(4)
     A                                      DSPATR(UL)
     A            HEM3      R    7  3H
     A            W2M3           4  2O  7 30EDTCDE(4)
     A                                      DSPATR(UL)
     A            HELM      R    4  2O  7 36EDTCDE(4)
     A                                      DSPATR(UL)
     A            HEVS1     R   25   O  7 42DSPATR(UL)
     A            HESDF     R    5   O  7 68DSPATR(UL)
     A            HESDT     R    5   O  7 74DSPATR(UL)
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0008)
     A                                      SFLPAG(0007)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A  91N93                               CF05(05 'Frisk')
     A  91N93                               CF12(12 'Forrige')
     A            ZRECNO         4S 0O  2 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYSPED/SYFF53A'
     A                                      COLOR(BLU)
     A                                  1 55DATE
     A                                      EDTCDE(Y)
     A                                  1 65TIME
     A                                  1 20MSGCON(30 DIV1525 SYMSGF)
     A                                      DSPATR(HI)
     A                                  2  2MSGCON(12 DIV0050 SYMSGF)
     A            UAVD           4Y 0O  2 15EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  2 22MSGCON(12 DIV1527 SYMSGF)
     A            UPRO           8Y 0O  2 36EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  3  2MSGCON(30 DIV0016 SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 40MSGCON(15 DIV0048 SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(79 DIV1530 SYMSGF)
     A                                      DSPATR(HI)
     A                                  5  2MSGCON(79 DIV1531 SYMSGF)
     A                                      DSPATR(HI)
     A          R BUNN
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF05(05 'Frisk')
     A                                      CF12(12 'Forrige')
     A                                      SLNO(22)
     A                                      CLRL(03)
     A                                      OVERLAY
     A                                  2  2MSGCON(18 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A                                  2 22MSGCON(18 DIV0021 SYMSGF)
     A                                      COLOR(BLU)
     A                                  2 42MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A          R NYWRK
     A                                      CF05(05 'Frisk')
     A                                      CF12(12 'Forrige')
     A                                  1  2'SYSPED/SYFF53A'
     A                                      COLOR(BLU)
     A                                  1 54DATE
     A                                      EDTCDE(Y)
     A                                  1 65TIME
     A                                  1 20MSGCON(30 DIV1525 SYMSGF)
     A                                      DSPATR(HI)
     A                                  2  2MSGCON(12 DIV0050 SYMSGF)
     A            UAVD           4Y 0O  2 15EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  2 22MSGCON(12 DIV1527 SYMSGF)
     A            UPRO           8Y 0O  2 36EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  3  2MSGCON(30 DIV0016 SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 40MSGCON(15 DIV0048 SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(79 DIV1530 SYMSGF)
     A                                      DSPATR(HI)
     A                                  5  2MSGCON(79 DIV1531 SYMSGF)
     A                                      DSPATR(HI)
     A                                 23  2MSGCON(18 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(18 DIV0021 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 42MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A                                 24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
