*****A**************************************************************
     A* RETTINGER I DETTE PROGRAM:
PTFnrA*Sek Sig Vers Beskrivelse...........................
"""""A*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
P0511A* 01 JOV PTF5 F7=VEDL. FR.TILLEG (VAL/OLJE..)
P8068A* 04 TH  PTF8 NY HELPFUNKSJON  KUN DDS
*****A**************************************************************
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPPNLGRP(HLPSCR SYFA36PNL)
     A                                      HLPTITLE('VEDLIKEHOLD AV FRAKTRATER-
     A                                       LAND')
     A                                      HELP
     A                                      ALTHELP
      *
     A          R BILDE01
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF03(03 'Avslutt program')
     A                                      CF04(04 'Søk')
     A                                      CF12(12 'Forrige')
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1  2'SYSPED/SYKS80'
     A                                      COLOR(BLU)
     A                                  1 20'Automatisk kostnadsføring dagsoppg-
     A                                      jør'
     A                                      COLOR(WHT)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  2 70TIME
     A                                      COLOR(BLU)
     A                                  4 26'DATO 1'
     A                                  4 37'DATO 2'
     A                                  4 48'DATO 3'
     A                                  4 59'DATO 4'
     A                                  5  2'Tast inn fort.dato.:'
     A            TDAG1          2Y 0B  5 25EDTCDE(4)
     A  81                                  DSPATR(PC)
     A  81                                  DSPATR(RI)
     A            TMND1          2Y 0B  5 28EDTCDE(4)
     A  81                                  DSPATR(RI)
     A            TAAR1          2Y 0B  5 31EDTCDE(4)
     A  81                                  DSPATR(RI)
     A            TDAG2          2Y 0B  5 36EDTCDE(4)
     A  82                                  DSPATR(PC)
     A  82                                  DSPATR(RI)
     A            TMND2          2Y 0B  5 39EDTCDE(4)
     A  82                                  DSPATR(RI)
     A            TAAR2          2Y 0B  5 42EDTCDE(4)
     A  82                                  DSPATR(RI)
     A            TDAG3          2Y 0B  5 47EDTCDE(4)
     A  83                                  DSPATR(RI)
     A            TMND3          2Y 0B  5 50EDTCDE(4)
     A  83                                  DSPATR(RI)
     A            TAAR3          2Y 0B  5 53EDTCDE(4)
     A  83                                  DSPATR(RI)
     A            TDAG4          2Y 0B  5 58EDTCDE(4)
     A  84                                  DSPATR(RI)
     A            TMND4          2Y 0B  5 61EDTCDE(4)
     A  84                                  DSPATR(RI)
     A            TAAR4          2Y 0B  5 64EDTCDE(4)
     A  84                                  DSPATR(RI)
     A                                  6 26'DATO 5'
     A                                  6 37'DATO 6'
     A                                  6 48'DATO 7'
     A                                  6 59'DATO 8'
     A            TDAG5          2Y 0B  7 25EDTCDE(4)
     A  85                                  DSPATR(PC)
     A  85                                  DSPATR(RI)
     A            TMND5          2Y 0B  7 28EDTCDE(4)
     A  85                                  DSPATR(RI)
     A            TAAR5          2Y 0B  7 31EDTCDE(4)
     A  85                                  DSPATR(RI)
     A            TDAG6          2Y 0B  7 36EDTCDE(4)
     A  86                                  DSPATR(PC)
     A  86                                  DSPATR(RI)
     A            TMND6          2Y 0B  7 39EDTCDE(4)
     A  86                                  DSPATR(RI)
     A            TAAR6          2Y 0B  7 42EDTCDE(4)
     A  86                                  DSPATR(RI)
     A            TDAG7          2Y 0B  7 47EDTCDE(4)
     A  87                                  DSPATR(RI)
     A            TMND7          2Y 0B  7 50EDTCDE(4)
     A  87                                  DSPATR(RI)
     A            TAAR7          2Y 0B  7 53EDTCDE(4)
     A  87                                  DSPATR(RI)
     A            TDAG8          2Y 0B  7 58EDTCDE(4)
     A  88                                  DSPATR(RI)
     A            TMND8          2Y 0B  7 61EDTCDE(4)
     A  88                                  DSPATR(RI)
     A            TAAR8          2Y 0B  7 64EDTCDE(4)
     A  88                                  DSPATR(RI)
     A                                  9  2'Bilagsnummer.......:'
     A            BILNR          7Y 0B  9 25EDTCDE(4)
     A                                 11  2'Tast inn bilagsdato:'
     A            BDAG           2Y 0B 11 25EDTCDE(4)
     A  98                                  DSPATR(PC)
     A  98                                  DSPATR(RI)
     A            BMND           2Y 0B 11 28EDTCDE(4)
     A  98                                  DSPATR(RI)
     A            BAAR           2Y 0B 11 31EDTCDE(4)
     A  98                                  DSPATR(RI)
     A                                 11 36'(dag/mnd/aar)'
     A                                 13  2'Tast inn periode...:'
     A            PMND           2Y 0B 13 25EDTCDE(4)
     A  96                                  DSPATR(PC)
     A  96                                  DSPATR(RI)
     A            PAAR           2Y 0B 13 28EDTCDE(4)
     A  96                                  DSPATR(RI)
     A                                 13 32'(mnd/aar)'
     A                                 15  2'Tast inn leverandør:'
     A            LEVNR          8Y 0B 15 25EDTCDE(4)
     A  99                                  DSPATR(PC)
     A  99                                  DSPATR(RI)
     A                                 17  2'Fakturanummer......:'
     A            FAKTNR        13A  B 17 25
     A  95                                  DSPATR(PC)
     A  95                                  DSPATR(RI)
     A                                 19  2'Kid-referanse......:'
     A            KID           25A  B 19 25
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A                                  5 68'(dag/mnd/aar)'
