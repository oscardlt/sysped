      *********************************************************************
      * PROGRAM VISE AKTUELL SAKER
      *
CRTPG+*  CRTPGM SYSPED/EISO22 MODULE(*LIBL/EISO22
CRTPGM*        *LIBL/CHECKUSR *LIBL/INCGI) ACTGRP(CGI) AUT(*USE)
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 YBC PTF10 VIS FLERE INFORMASJONER
10XXX * 02 YBC PTF10 DIVERSE ENDRINGER ETTER MØTE 09.02.2012
********************************************************************
      *
      *********************************************************************
      * MAIN PROGRAM FLOW
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
     FBRIDF     UF   E           K DISK    USROPN
     FKODTLK    IF   E           K DISK    usropn
N01  FKALENDER  IF   E           K DISK    usropn
N01  FHEAD31    IF   E           K DISK    usropn
     FWRKCGISS  IF   E           K DISK    usropn
      *--------------------------------------------------------------------
      * Prototype defintions and standard system API error structure
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      *--------------------------------------------------------------------
      * Variables common to CGI programs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
N01  D TAB1            S              2    DIM(8) CTDATA PERRCD(1)
N01  D TAB2            S              1    DIM(8) ALT(TAB1)
     D OddEven         s             10
      * Variables received from the input string
     D lng             s              2
     D BckGnd          s             10
     D lstbrd          s              1
     D UsrSpcName      s             10
     D WSUSER          S             10
     D WSAAR           s              4
     D WSMNF           s              2
     D WSMNT           s              2
     D WINT            s              6
     D WSMOD           S              1
     D WSLK            S              2
N01  D WSBILN          S              9
N01  D WSTURN          S              8
N01  D WBTURN          S              8  0
N01  D WSATDUD         S              1
N01  D WSETAUD         S              1
N01  D WSATAUD         S              1
N01  D WSALL           S              1
N01  D WSDIRKJ         S              1
N02  D WSINNL          S              1
N02  D WWMOD           S              1
N02  D WWLK            S              2
     D PGM             C                   CONST('SYSPED/CHECKUSR')
      * Variables received from the input string
     D Lib             s             10
     D Fn              s             10
     D Mbr             s             10
     D                 DS
     D  XXKEY                  1    256
     D  XXMOD                  1      1
     D  XXLK                   2      3
     D  XXUNIK                 1      3
     D                 DS
     D  WKEY2                  1    256
     D  WXMOD                  1      1
     D  WXLK                   2      3
     D  WXUNIK                 1      3
     D  WXDT                   4     11  0
     D  WXBILN                12     20
N01  D  W1XBILN                2     10
N01  D  W1XDT                 11     18  0
N01  D  W1XLK                 19     20
N01  D  W1XMOD                21     21
     D                 DS
     D  WDATA                  1   1024
     D  WXDATO1                2      9  0
     D  WXDATO2               10     17  0
     D  WXTURNR               18     25  0
     D  WXANTOPD              26     32  0
     D  WXANTDIR              33     39  0
N01  D  WXATDDT               40     47  0
N01  D  WXLVKT                48     56  0
N02  D  WXLKD                 57     76    DIM(10)
N02  D  WXLVK                 77    166  0 DIM(10)
N02  D  WXLPR                167    246  0 DIM(10)
N01  D UC              C                   CONST('ABCDEFGHIJKLMNOPQRST-
N01  D                                     UVWXYZÆØÅ')
N01  D LC              C                   CONST('abcdefghijklmnopqrst-
N01  D                                     uvwxyzæøå')
      *
      *--------------------------------------------------------------------
      * Prolog common to CGI programs
      * -Receive input from the remote browser
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prolog3
      *=====================================================================******
      * MAIN PROCESS LINE
      *=====================================================================******
      * Get program start time for calculating execution time
     C                   time                    timedata1
      * Parse input
     C                   exsr      Parse
      *
     C                   EXSR      INIT
      *
      * Set output skeleton html member name
     C                   exsr      SetSkl
      * Read skeleton output html, etc.
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
      * Retrieve environment variables
     C                   exsr      RtvEnvVar
      *------------------                                                   ******
      * Write HTML sections
      *
      * 1-Section /$top
     C                   exsr      SetTop
     C                   callp     wrtsection('top')
      *
      * 3-Section /$tablestr
     C                   exsr      SetTabStr
     C                   callp     wrtsection('tablestr')
N01  C                   IF        WSALL = 'J'
N01  C                   callp     wrtsection('LT01')
N01  C                   END
N01  C                   callp     wrtsection('LT02')
N01  C                   IF        WSALL = 'J'
N01  C                   callp     wrtsection('LT03')
N01  C                   END
N01  C                   callp     wrtsection('LT04')
N01  C                   IF        WSALL = 'J'
N01  C                   callp     wrtsection('LT05')
N01  C                   END
N01  C                   callp     wrtsection('LT06')
      * 4-Sections /$tablerow
      *
     C     WRKCGIK       SETLL     WRKCGISS
     C     WINTEG        READE     WRKCGISS                               50
N01  C                   IF        WSALL = 'J'
N01  C  N50WXMOD         COMP      '*'                                5050
N01  C                   ELSE
     C  N50WXUNIK        COMP      XXUNIK                             5050
N01  C                   END
      *
     C                   z-add     0             bseq              5 0
     C                   DOW       *IN50 = *OFF
     C                   exsr      SetTabRow
N01  C                   IF        XIKKEVIS = 'N'
     C                   ADD       1             bseq
     C                   callp     wrtsection('tablerow')
N01  C                   IF        WSALL = 'J'
N01  C                   callp     wrtsection('tablerow1')
N01  C                   END
N01  C                   callp     wrtsection('tablerow2')
N01  C                   END
      *
     C     WINTEG        READE     WRKCGISS                               50
N01  C                   IF        WSALL = 'J'
N01  C  N50WXMOD         COMP      '*'                                5050
N01  C                   ELSE
     C  N50WXUNIK        COMP      XXUNIK                             5050
N01  C                   END
     C                   ENDDO                                                  *hival
      *
      * 5-Section /$tableend
     C                   callp     wrtsection('tableend')
      *
      * 5a-Section /$foundsome
     C                   exsr      SetFndSome
     C                   callp     wrtsection('foundsome2')
      * 6-Section /$endhtml
     C                   exsr      SetEnd
     C     SLUTT         tag
     C                   callp     wrtsection('endhtml')
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
     C                   EXSR      EXIT
     C                   return
      *
      *=====================================================================******
      * Parse input
      *=====================================================================******
     C     Parse         begsr
     C                   eval      lng     = zhbgetvar('lng')
     C                   eval      BckGnd  = zhbgetvar('bckgnd')
     C                   eval      lstbrd  = zhbgetvar('lstbrd')
     C                   EVAL      WSUSER  = zhbgetvar('USER')
     C                   EVAL      UsrSpcName  = zhbgetvar('UsrSpcName')
N02  C                   EVAL      WSINNL  = zhbgetvar('WSINNL')
      *
     C                   EVAL      WINT    = zhbgetvar('WINTEG')
     C                   EVAL      WINTEG  = c2n2(WINT)
     C                   EVAL      WSLK    = zhbgetvar('WSLK')
     C                   EVAL      WSMOD   = zhbgetvar('WSMOD')
     C                   EVAL      WSAAR   = zhbgetvar('WSAAR')
     C                   EVAL      WSMNF   = zhbgetvar('WSMNF')
     C                   EVAL      WSMNT   = zhbgetvar('WSMNT')
N01  C                   EVAL      WSBILN  = zhbgetvar('WSBILN')
N01  C     LC:UC         XLATE     WSBILN        WSBILN
N01  C                   EVAL      WSTURN  = zhbgetvar('WSTURN')
N01  C                   eval      WBTURN  = c2n2(WSTURN)
N01  C                   EVAL      WSATDUD = zhbgetvar('WSATDUD')
N01  C                   EVAL      WSETAUD = zhbgetvar('WSETAUD')
N01  C                   EVAL      WSATAUD = zhbgetvar('WSATAUD')
N01  C                   EVAL      WSALL   = zhbgetvar('WSALL')
N01  C                   EVAL      WSDIRKJ = zhbgetvar('WSDIRKJ')
      *
     C                   endsr
      *
      *=====================================================================******
      *  Set output variables for section /$top
      *  (search criteria)
      *=====================================================================******
     C     SetTop        begsr
      * Repeat national language for the next invocation
     C                   callp     updhtmlvar('LNG':lng:
     C                             InitHTMLVars)
OddEvC                   callp     updHTMLvar('ROWHEAD':RowHead)
OddEvC                   callp     updHTMLvar('LogoImg':LogoImg)
      * Color for text, background, links, visited links, active links
     C                   callp     updhtmlvar('TXTCOLOR':'black')
     C                   callp     updhtmlvar('BOLD':' ')
     C                   callp     updhtmlvar('BGCOLOR':BIFARG)
     C                   callp     updhtmlvar('LINK':'0000FF')
     C                   callp     updhtmlvar('VLINK':'FF0000')
     C                   callp     updhtmlvar('ALINK':'00FF00')
      * KUNDE
     C                   callp     updhtmlvar('WSNA':BIBESK)
BODY C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updhtmlvar('USER':WSUSER)
     C                   callp     updHtmlVar('UsrSpcName':UsrSpcName)
N02  C                   callp     updHTMLvar('WSINNL':WSINNL)
     C                   callp     updHTMLvar('sdato':
     C                             %trim(%editw(BIDTV:'    .  .  ')))
     C                   callp     updHTMLvar('stid':
     C                             %trim(%editw(BITIDV:'  .  .  ')))
      *
     C                   callp     updHTMLvar('WSAAR':WSAAR)
     C                   callp     updHTMLvar('WSMNF':WSMNF)
     C                   callp     updHTMLvar('WSMNT':WSMNT)
     C                   callp     updHTMLvar('WINTEG':
     C                             %trim(%editc(WINTEG:'Z')))
     C                   callp     updHTMLvar('WSMOD':WSMOD)
     C                   IF        WSMOD = 'I'
     C                   callp     updHTMLvar('WSMODTXT':'Import')
     C                   ELSE
     C                   callp     updHTMLvar('WSMODTXT':'Eksport')
     C                   END
     C                   callp     updHTMLvar('WSLK':WSLK)
     C     KODTLKK       CHAIN     KODTLK                             33
     C                   callp     updHTMLvar('WSLKTXT':KLKNVN)
N01  C                   callp     updHTMLvar('WSBILN':WSBILN)
N01  C                   callp     updHTMLvar('WSTURN':WSTURN)
N01  C                   callp     updHTMLvar('WSATDUD':WSATDUD)
N01  C                   callp     updHTMLvar('WSETAUD':WSETAUD)
N01  C                   callp     updHTMLvar('WSATAUD':WSATAUD)
N01  C                   callp     updHTMLvar('WSALL':WSALL)
N01  C                   callp     updHTMLvar('WSDIRKJ':WSDIRKJ)
      *
     C                   endsr
      *
      *=====================================================================******
      *  Set output variables for section /$tablestr (table start)
      *=====================================================================******
     C     SetTabStr     begsr
     C     lstbrd        ifeq      'Y'
     C                   callp     updhtmlvar('LB':'0')
     C                   else
     C                   callp     updhtmlvar('LB':'1')
     C                   endif
      *
     C                   endsr
      *
      *=====================================================================******
      *  Set output variables for section /$tablerow (table row)
      *=====================================================================******
     C     SetTabRow     begsr
N01  C                   MOVE      'N'           XIKKEVIS          1
N01  C                   IF        WSBILN <> *BLANKS AND WSBILN <> W1XBILN
N01  C                   EVAL      XIKKEVIS = 'J'
N01  C                   GOTO      SLTABROW
N01  C                   END
N01  C                   IF        WBTURN <> 0 AND WBTURN <> WXTURNR
N01  C                   EVAL      XIKKEVIS = 'J'
N01  C                   GOTO      SLTABROW
N01  C                   END
N01  C     WXDATO1       CHAIN     KALENDER                           33
N01  C   33              MOVE      *BLANKS       KAUDG
N01  C     KAUDG         LOOKUP    TAB1          TAB2                     33
N01  C                   MOVE      TAB2          XUKEDAG           1
N01  C                   IF        XUKEDAG <> WSETAUD AND WSETAUD <> ' '
N01  C                   EVAL      XIKKEVIS = 'J'
N01  C                   GOTO      SLTABROW
N01  C                   END
N01  C                   callp     updhtmlvar('WXETAUG':XUKEDAG)
N01  C     WXDATO2       CHAIN     KALENDER                           33
N01  C   33              MOVE      *BLANKS       KAUDG
N01  C     KAUDG         LOOKUP    TAB1          TAB2                     33
N01  C                   MOVE      TAB2          XUKEDAG           1
N01  C                   IF        XUKEDAG <> WSATAUD AND WSATAUD <> ' '
N01  C                   EVAL      XIKKEVIS = 'J'
N01  C                   GOTO      SLTABROW
N01  C                   END
N01  C                   callp     updhtmlvar('WXATAUG':XUKEDAG)
N01  C                   callp     updhtmlvar('WXATDDT':
N01  C                             %trim(%editw(WXATDDT:'    .  .  ')))
N01  C     WXATDDT       CHAIN     KALENDER                           33
N01  C   33              MOVE      *BLANKS       KAUDG
N01  C     KAUDG         LOOKUP    TAB1          TAB2                     33
N01  C                   MOVE      TAB2          XUKEDAG           1
N01  C                   IF        XUKEDAG <> WSATDUD AND WSATDUD <> ' '
N01  C                   EVAL      XIKKEVIS = 'J'
N01  C                   GOTO      SLTABROW
N01  C                   END
N01  C                   callp     updhtmlvar('WXATDUG':XUKEDAG)
N01  C                   callp     updhtmlvar('W1XLK':W1XLK)
N01  C                   callp     updhtmlvar('W1XMOD':W1XMOD)
N01   *
      * odde/partalls-linjer i alternativ skyggelegging/bakgrunnsfarge
     c     OddEven       IFEQ      ODD
     c                   eval      OddEven = EVEN
     c                   else
     c                   eval      OddEven = ODD
     c                   end
     C                   callp     updHTMLvar('ODDEVEN':OddEven)
     c* Sequence of this Recd (numeric, converted to char)
     C                   move      bseq          bseqchar          5
     C                   callp     updhtmlvar('SEQ':bseqchar)
     C                   SELECT
     C                   WHEN      WXDATO2 = 99999999
     C                   callp     updhtmlvar('XXFARGE':'BGCOLOR="#FF6427"')
     C                   WHEN      WXDATO1 < WXDATO2
     C                   callp     updhtmlvar('XXFARGE':'BGCOLOR="#FFD700"')
     C                   OTHER
     C                   callp     updhtmlvar('XXFARGE':'BGCOLOR="#00FF7F"')
     C                   ENDSL
N01  C                   IF        WSALL = 'J'
N01  C                   callp     updhtmlvar('WXBILN':W1XBILN)
N01  C                   ELSE
     C                   callp     updhtmlvar('WXBILN':WXBILN)
N01  C                   END
     C                   callp     updhtmlvar('WXTN':
     C                             %trim(%editc(WXTURNR:'Z')))
     C                   callp     updhtmlvar('WXDATO1':
     C                             %trim(%editw(WXDATO1:'    .  .  ')))
     C                   callp     updhtmlvar('WXDATO2':
     C                             %trim(%editw(WXDATO2:'    .  .  ')))
N02   *
N01  C                   Z-ADD     0             WXTVKT            9 0
N01  C                   IF        WSALL = 'J'
N01  C                   EVAL      HEDTOP = W1XDT
N01  C                   EVAL      HEHAWB = W1XBILN
N02  C                   EVAL      WWLK   = W1XLK
N02  C                   EVAL      WWMOD  = W1XMOD
N01  C                   ELSE
N01  C                   EVAL      HEDTOP = WXDT
N01  C                   EVAL      HEHAWB = WXBILN
N02  C                   EVAL      WWLK   = WXLK
N02  C                   EVAL      WWMOD  = WXMOD
N01  C                   END
N02  C                   MOVE      *BLANKS       WXLKD
N02  C                   MOVE      *ZEROS        WXLVK
N02  C                   MOVE      *ZEROS        WXLPR
N02   *
N01  C     HEAD31K       SETLL     HEAD31
N01  C     HEAD31K       READE     HEAD31                                 33
N01  C                   DOW       *IN33 = *OFF
N02  C                   IF        HEST <> 'S'
S02  C*                  IF        HESDL <> 'DIREKTEKJØRT' OR WSDIRKJ <> 'N'
N02  C                   IF        WSINNL = 'J'                                 INNLAND
N02  C                   IF        HELKA <> 'NO' OR HETRI <> 'NO'               IKKE INNLAND OPPD
N02  C                   GOTO      LESNESTE1
N02  C                   END
N02  C                   ELSE                                                   IKKE INNLAND
N02  C                   IF        HELKA = 'NO' AND HETRI = 'NO'                INNLAND OPPDRAG
N02  C                   GOTO      LESNESTE1
N02  C                   END
N02  C                   END
N02  C                   SELECT
N02  C                   WHEN      (WWMOD = 'I' AND HEUR = 'A') OR
N02  C                             (WWMOD = 'I' AND HEUR = 'C') OR
N02  C                             (WWMOD = 'I' AND HEUR = 'F')
N02  C                   WHEN      WWMOD = 'I' AND HEUR = 'H' AND HELKA <> 'NO'
N02  C                   WHEN      (WWMOD = 'E' AND HEUR = 'B') OR
N02  C                             (WWMOD = 'E' AND HEUR = 'D') OR
N02  C                             (WWMOD = 'E' AND HEUR = 'G')
N02  C                   WHEN      WWMOD = 'E' AND HEUR = 'H' AND HELKA = 'NO'
N02  C                   OTHER
N02  C                   GOTO      LESNESTE1
N02  C                   ENDSL
N02  C     'DIREKT'      SCAN      HESDL                                  33
N02  C                   IF        *IN33 = *OFF OR WSDIRKJ <> 'N'
N02  C                   IF        HELKA = WWLK
N02  C                   EVAL      UJN = 'N'
N02  C                   ELSE
N02  C                   EVAL      UPRO = HEPRO
N02  C                   EVAL      ULK  = HELKA
N02  C                   EXSR      SRSJEKK
N02  C                   END
N02  C                   IF        UJN = 'N'
N01  C                   ADD       HEVKT         WXTVKT
N02   * SUMMER PR LAND
N02  C     1             DO        10            XN                3 0
N02  C                   SELECT
N02  C                   WHEN      WXLKD(XN) = '  '
N02  C                   EVAL      WXLKD(XN) = HELKA
N02  C                   EVAL      WXLVK(XN) = HEVKT
N02  C                   LEAVE
N02  C                   WHEN      WXLKD(XN) = HELKA
N02  C                   EVAL      WXLVK(XN) = WXLVK(XN) + HEVKT
N02  C                   LEAVE
N02  C                   ENDSL
N02  C                   ENDDO
N02   * LAGRE TURNR
N02  C                   IF        HEPRO <> WXTURNR
N02  C     1             DO        10            XN                3 0
N02  C                   SELECT
N02  C                   WHEN      WXLPR(XN) = 0
N02  C                   EVAL      WXLPR(XN) = HEPRO
N02  C                   LEAVE
N02  C                   WHEN      WXLPR(XN) = HEPRO
N02  C                   LEAVE
N02  C                   ENDSL
N02  C                   ENDDO
N02  C                   END
N02   *
N02  C                   END
N01  C                   END
N02  C                   END
N02  C     LESNESTE1     TAG
N01  C     HEAD31K       READE     HEAD31                                 33
N01  C                   ENDDO
N02   *
N02   * FINN SAMME BIL OG NESTE DAG
N02  C                   IF        HEHAWB <> ' '
N02  C                   EVAL      UPRO = WXTURNR
N02  C                   IF        WSALL = 'J'
N02  C                   EVAL      ULK   = W1XLK
N02  C                   ELSE
N02  C                   EVAL      ULK   = WXLK
N02  C                   END
N02  C                   EXSR      SRSJEKK
N02   *
N02  C                   IF        UJN = 'N'
N02  C                   ADD       1             HEDTOP
N02  C     HEAD31K       SETLL     HEAD31
N02  C     HEAD31K       READE     HEAD31                                 33
N02  C                   DOW       *IN33 = *OFF
N02  C                   IF        HEST <> 'S'
N02  C                   IF        WXTURNR = HEPRO
N02  C                   IF        WSINNL = 'J'                                 INNLAND
N02  C                   IF        HELKA <> 'NO' OR HETRI <> 'NO'               IKKE INNLAND OPPD
N02  C                   GOTO      LESNESTE2
N02  C                   END
N02  C                   ELSE                                                   IKKE INNLAND
N02  C                   IF        HELKA = 'NO' AND HETRI = 'NO'                INNLAND OPPDRAG
N02  C                   GOTO      LESNESTE2
N02  C                   END
N02  C                   END
N02  C     'DIREKT'      SCAN      HESDL                                  33
N02  C                   IF        *IN33 = *OFF OR WSDIRKJ <> 'N'
N02  C                   ADD       HEVKT         WXTVKT
N02   * SUMMER PR LAND
N02  C     1             DO        10            XN                3 0
N02  C                   SELECT
N02  C                   WHEN      WXLKD(XN) = '  '
N02  C                   EVAL      WXLKD(XN) = HELKA
N02  C                   EVAL      WXLVK(XN) = HEVKT
N02  C                   LEAVE
N02  C                   WHEN      WXLKD(XN) = HELKA
N02  C                   EVAL      WXLVK(XN) = WXLVK(XN) + HEVKT
N02  C                   LEAVE
N02  C                   ENDSL
N02  C                   ENDDO
N02   *
N02  C                   END
N02  C                   END
N02  C                   END
N02  C     LESNESTE2     TAG
N02  C     HEAD31K       READE     HEAD31                                 33
N02  C                   ENDDO
N02  C                   SUB       1             HEDTOP
N02   *
N02  C                   END
N02  C                   END
N02   *
N01  C                   callp     updhtmlvar('WXTVKT':
N01  C                             %trim(%editc(WXTVKT:'Z')))
N01  C                   callp     updhtmlvar('WXLVKT':
N01  C                             %trim(%editc(WXLVKT:'Z')))
N01  C                   IF        WXTVKT = 0
N01  C                   Z-ADD     0             WXPVKT            5 2
N02  C                   Z-ADD     0             WWPVKT            5 2
N01  C                   ELSE
N01  C                   EVAL(H)   WXPVKT = WXLVKT / WXTVKT * 100
N02  C                   MOVE      WXPVKT        WWPVKTM           5 2
N02  C     1             DO        10            XN
N02  C                   IF        WXLKD(XN) = '  '
N02  C                   LEAVE
N02  C                   END
N02  C                   EVAL(H)   WWPVKT = WXLVK(XN) / WXTVKT * 100
N02  C                   IF        WWPVKT > WWPVKTM
N02  C                   EVAL      WWLK = WXLKD(XN)
N02  C                   EVAL      WWPVKTM = WWPVKT
N02  C                   END
N02  C                   ENDDO
N01  C                   END
N01  C                   callp     updhtmlvar('WXPVKT':
N01  C                             %trim(%editc(WXPVKT:'3')))
N02  C                   SELECT
N02  C                   WHEN      WSALL = 'J' AND WWLK = W1XLK
N02  C                   EVAL      WWLK = *BLANKS
N02  C                   WHEN      WSALL <> 'J' AND WWLK = WXLK
N02  C                   EVAL      WWLK = *BLANKS
N02  C                   ENDSL
N02  C                   callp     updhtmlvar('WWLK':WWLK)
N02  C                   MOVE      *BLANKS       XANDREPRO       100
N02  C                   IF        HEHAWB <> *BLANKS
N02  C     1             DO        10            XN
N02  C                   IF        WXLPR(XN) = 0
N02  C                   LEAVE
N02  C                   END
N02  C                   MOVE      WXLPR(XN)     XAN08             8
N02  C                   IF        XANDREPRO = *BLANKS
N02  C                   EVAL      XANDREPRO = %TRIM(%EDITC(WXLPR(XN):'Z'))
N02  C                   ELSE
N02  C                   EVAL      XANDREPRO = %TRIM(XANDREPRO) + ','
N02  C                             + %TRIM(%EDITC(WXLPR(XN):'Z'))
N02  C                   END
N02  C                   ENDDO
N02  C                   END
N02  C                   callp     updhtmlvar('WWPRO':XANDREPRO)
     C                   SELECT
     C                   WHEN      WXANTDIR = 0
     C                   callp     updhtmlvar('WXDIR':'NEI')
     C                   WHEN      WXANTDIR = WXANTOPD
     C                   callp     updhtmlvar('WXDIR':'JA')
     C                   OTHER
     C                   callp     updhtmlvar('WXDIR':'DEL')
     C                   END
      *
N01  C     SLTABROW      TAG
     C                   endsr
      *
N02   *=====================================================================******
N02   *  SJEKK OPPDRAG SKAL MED
N02   *=====================================================================******
N02  C     SRSJEKK       BEGSR
N02  C                   IF        WSALL = 'J'
N02  C                   EVAL      UMOD  = W1XMOD
N02  C                   EVAL      UDT   = W1XDT
N02  C                   EVAL      UBILN = W1XBILN
N02  C                   ELSE
N02  C                   EVAL      UMOD  = WXMOD
N02  C                   EVAL      UDT   = WXDT
N02  C                   EVAL      UBILN = WXBILN
N02  C                   END
N02   * SJKK OM SAMME TUR PÅ DAGEN FØR
N02  C                   CALL      'EISO22A'
N02  C                   PARM      WINTEG        UINTEG            6 0
N02  C                   PARM                    UPRO              8 0
N02  C                   PARM                    WSALL
N02  C                   PARM                    UMOD              1
N02  C                   PARM                    ULK               2
N02  C                   PARM                    UDT               8 0
N02  C                   PARM                    UBILN             9
N02  C                   PARM                    UJN               1
N02   *
N02  C                   ENDSR
N02   *
      *=====================================================================******
      *  Set output variables for section /$foundsome (some Recds were found)
      *=====================================================================******
     C     SetFndSome    begsr
      * Repeat background color
     C                   callp     updhtmlvar('BCKGND':bckgnd)
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$endhtml
      *=====================================================================******
     C     SetEnd        begsr
      * Server protocol
     C                   callp     updhtmlvar('PROTOCOL':S_Protocol)
      * Compute run time
     C                   time                    timedata2
     C     timedata2     subdur    timedata1     ms:*ms
     C                   eval      sec = ms / 1000000
     C                   callp     updhtmlvar('runtime':
     C                             %trim(%editw(sec:'     0 .   ')))
      *
     C                   endsr
      *=====================================================================******
      * Set html output skeleton member before its read into memory
      *=====================================================================******
     C     SetSkl        begsr
      *------------------
      * Set html output skeleton member
     C                   eval      lng = uppify(lng)
      * "HTMLSRC  " er dft = NORSK, ved å trykke på FLAGGET i inng.
      * kan dette endre stil "HTMLSRCXX" avh. av støttede språk
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC' + lng
     C                   eval      Mbr = 'EISO22'
     C*                  CAT       XSPRÅK:0      MBR
      *
     C                   endsr
      *=====================================================================******
      *  Retrieve environment variables
      *=====================================================================******
     C     RtvEnvVar     begsr
      * Use getenvp to get this server's protocol and name
     C                   eval      S_Protocol =getenv('SERVER_PROTOCOL':
     C                             qusec)
     C                   eval      S_Name     =getenv('SERVER_NAME':
     C                             qusec)
      *
     C                   endsr
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
     C                   OPEN      BRIDF
     C     WSUSER        CHAIN(N)  BRIDF                              50
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
      *
     C                   EXSR      TSTUSER
      *
OddEv * hent Parametre som går igjen i mange prog:
OddEv *      Odd/Even/Rowhead-farger,Logobilde,Språk,Intern/Ekstern bruker,  mm
OddEvc                   call      'ESGETPAR'
OddEvc                   parm                    BIBRID
OddEvc                   parm                    BIFIRM
OddEvc                   parm                    BIKUNR
OddEvc                   parm                    BIKNG
OddEvc                   parm                    RowHead          10
OddEvc                   parm                    Odd              10
OddEvc                   parm                    Even             10
OddEvc                   parm                    LogoImg          50
OddEvc                   parm                    XSPRÅK            2
OddEvc                   parm                    XINTERN           1
OddEvc                   parm                    ParmDS1        1024
OddEvc                   parm                    ParmDS2        1024
OddEvc                   parm                    ParmDS3        1024
      *
     C                   OPEN      KODTLK
N01  C                   OPEN      KALENDER
N01  C                   OPEN      HEAD31
     C                   OPEN      WRKCGISS
      *
     C     WRKCGIK       klist
     C                   kfld                    WINTEG
     C                   kfld                    XXKEY
     C     KODTLKK       klist
     C                   kfld                    KLKUNI
     C                   kfld                    WSLK
N01  C     HEAD31K       klist
N01  C                   kfld                    HEHAWB
N01  C                   kfld                    HEDTOP
      *
     C                   MOVE      *BLANKS       XXKEY
N01  C                   IF        WSALL = 'J'
N01  C                   MOVE      '*'           XXMOD
N01  C                   ELSE
     C                   MOVE      WSMOD         XXMOD
     C                   MOVE      WSLK          XXLK
N01  C                   END
     C                   MOVE      'LK '         KLKUNI
      *
     C                   endsr
      *
      *////////////////////////////////////////////////////////////
     C     TSTUSER       BEGSR
      *////////////////////////////////////////////////////////////
      /copy syspeds/qrpgsrcpy,tstuser
      *
     C                   endsr
      *
      *=====================================================================******
      *  AVSLUTT
      *=====================================================================******
     C     EXIT          begsr
     C                   close     BRIDF
     C                   close     WRKCGISS
     C                   close     KODTLK
N01  C                   close     KALENDER
N01  C                   close     HEAD31
     C                   eval      *inlr = *on
      *
     C                   endsr
      *
**

Ma1
Ti2
On3
To4
Fr5
Lø6
Sø7
