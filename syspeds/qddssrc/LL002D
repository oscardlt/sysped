*****A**************************************************************
     A* RETTINGER I DETTE PROGRAM:
PTFnrA*Sek Sig Vers Beskrivelse...........................
"""""A*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
PXXXXA* 01 YBC PTF5 FAST LAGERLOKASJON FOR VARE
%%%%%A*%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
PXXXXA* 02 YBC PTF6 KRAV PÅ SERIENR
PXXXXA* 03 YBC PTF6 FLERE NYE FELTER, MYE ENDRINGER OG INGEN MERKING
PXXXXA* 04 YBC PTF6 FORHOLDSTALL STYKK-EKSE FLYTTES TIL VENSTRE
P6XXXA* 05 YBC PTF6 NY FIL FOR LEVERANDØRS KUNDEREF.
P6XXXA* 06 YBC PTF6 NY FELT FOR VAREEIER
P6XXXA* 07 SH  PTF8 FRISØK F10
P8068A* 09 TH  PTF8 NY HELPFUNKSJON  KUN DDS
*****A**************************************************************
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
N09  A                                      HLPPNLGRP(HLPSCR LL002PNL)
N09  A                                      HLPTITLE('SØK PÅ VAREKODER')
     A                                      HELP
     A                                      ALTHELP(CA01)
      * "NOERASE" MÅ HA FOR Å IKKE TØMME BILDE SOM MAN KOMME FRA
     A          R NOERASE
     A                                      ASSUME
     A                                  1  2' '
      * MAN KUN DEFINNERE VINDU PÅ REKORDNIVÅ
     A          R WINDOWSZ
S06  A*                                     WINDOW(*DFT 10 67 *NOMSGLIN)
N06  A                                      WINDOW(*DFT 10 73 *NOMSGLIN)
     A                                      OVERLAY
      *
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  5  2VALUES(' ' '1' '5')
     A            LVANR         15A  O  5  5
     A            LVVB30        30A  O  5 21
     A            LVVB          35A  H
     A            LVANRL        15A  O  5 52
N06  A            LVEIER         5A  O  5 68
     A            LVTXT         40A  H
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A                                      SFLSIZ(0006)
     A                                      SFLPAG(0005)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A                                      CA03(03 'Forrige')
     A                                      CA05(05 'Frisk')
     A                                      CA06(06 'LAGE NY')
N07  A                                      CF10(10 'FRISØK')
     A                                      CA12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            ZRECNO         4S 0O  2 28SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'LL002R'
     A                                      COLOR(BLU)
     A                                  1 10MSGCON(20 YBC0917 SYMSGF)
     A                                      COLOR(WHT)
     A                                  2  2MSGCON(35 YBC0918 SYMSGF)
     A                                      COLOR(BLU)
     A                                  3  2MSGCON(65 YBC0919 SYMSGF)
     A                                      COLOR(WHT)
     A            W2NR           1A  I  4  2VALUES(' ' '1')
     A                                      DSPATR(UL)
     A            W2ANR         15A  B  4  5DSPATR(UL)
     A            W2VB30        30A  B  4 21DSPATR(UL)
     A            W2ANRL        15A  B  4 52DSPATR(UL)
      *
     A          R BUNN
     A                                      WINDOW(WINDOWSZ)
     A                                      SLNO(10)
     A                                      CLRL(01)
     A                                      OVERLAY
S07  A*N30                              1  2MSGCON(045 YBC0920 *LIBL/SYMSGF)
N07  A N30                              1  2MSGCON(045 SHU1028 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        65A  O  1  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R NYWRK
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A                                      CA03(03 'Forrige')
     A                                      CA05(05 'Frisk')
     A                                      CA06(06 'LAGE NY')
N07  A                                      CF10(10 'FRISØK')
     A                                      CA12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'LL002R'
     A                                      COLOR(BLU)
     A                                  1 10MSGCON(20 YBC0917 SYMSGF)
     A                                      COLOR(WHT)
     A                                  2  2MSGCON(20 YBC0917 SYMSGF)
     A                                      COLOR(BLU)
     A                                  3  2MSGCON(65 YBC0919 SYMSGF)
     A                                      COLOR(WHT)
     A            W2NR           1A  I  4  2VALUES(' ' '1')
     A                                      DSPATR(UL)
     A            W2ANR         15A  B  4  5DSPATR(UL)
     A            W2VB30        30A  B  4 21DSPATR(UL)
     A            W2ANRL        15A  B  4 52DSPATR(UL)
S07  A*                                10  2MSGCON(45 YBC0920 SYMSGF)
N07  A                                 10  2MSGCON(45 SHU1028 SYMSGF)
     A                                      COLOR(BLU)
      *
     A          R VISART
S01  A*                                     WINDOW(*DFT 14 70 *NOMSGLIN)
N01  A                                      WINDOW(*DFT 17 70 *NOMSGLIN)
     A                                      CA12(12 'Forrige')
     A                                      OVERLAY
S09  A*         H                           HLPDOC('LL00201' 'LL002' 'SY400LO/-
S09  A*                                     LOGI')
N09  A          H                           HLPPNLGRP(LL00201 LL002PNL)
     A                                      HLPARA(01 001 16 067)
     A                                  1  2'SYSPED/LL002R'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(20 YBC0922 SYMSGF)
     A                                      DSPATR(HI)
     A                                  2  2MSGCON(12 YBC0923 SYMSGF)
     A            LVANR         15A  O  2 15DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  2 32MSGCON(12 YBC0924 SYMSGF)
     A            LVANRL        15A  O  2 45DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  3  2MSGCON(12 YBC0925 SYMSGF)
     A            LVVB          35A  O  3 15DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  4  2MSGCON(12 YBC0926 SYMSGF)
     A            LVLNR         17A  O  4 15DSPATR(UL)
     A                                      DSPATR(HI)
S05  A*           KUALFA        10A  O  4 34DSPATR(UL)
N05  A            XADRNA        10A  O  4 34DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  5  2MSGCON(12 YBC0927 SYMSGF)
     A            LVANS         30A  O  5 15DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  6  2MSGCON(12 YBC0928 SYMSGF)
     A            LVAG01         5A  O  6 15DSPATR(UL)
     A                                      DSPATR(HI)
     A            LVAG02         5A  O  6 21DSPATR(UL)
     A                                      DSPATR(HI)
     A            LVAG03         5A  O  6 27DSPATR(UL)
     A                                      DSPATR(HI)
     A            LVAG04         5A  O  6 33DSPATR(UL)
     A                                      DSPATR(HI)
     A            LVAG05         5A  O  6 39DSPATR(UL)
     A                                      DSPATR(HI)
N10  A                                  6 46MSGCON(10 YBC1714 SYMSGF)
N10  A            LVEIER         5A  O  6 57DSPATR(UL)
     A                                  7 15MSGCON(04 YBC0929 SYMSGF)
     A                                      COLOR(WHT)
     A                                  7 35MSGCON(13 YBC0930 SYMSGF)
     A                                      COLOR(WHT)
     A                                  8 15MSGCON(52 YBC0931 SYMSGF)
     A                                      COLOR(WHT)
     A                                  9  2MSGCON(12 YBC0932 SYMSGF)
     A            LVEH0          3A  O  9 15DSPATR(UL)
     A                                      DSPATR(HI)
     A            LVEH0T        15A  O  9 20DSPATR(UL)
     A                                      DSPATR(HI)
     A            LVVKT0         7Y 2O  9 51DSPATR(UL)
     A                                      DSPATR(HI)
     A                                      EDTCDE(4)
     A            LVKUB0         7Y 4O  9 60DSPATR(UL)
     A                                      DSPATR(HI)
     A                                      EDTCDE(4)
     A                                 10  2MSGCON(12 YBC0933 SYMSGF)
     A            LVEH1          3A  O 10 15DSPATR(UL)
     A                                      DSPATR(HI)
     A            LVEH1T        15A  O 10 20DSPATR(UL)
     A                                      DSPATR(HI)
S04  A*           LVAN01         5Y 0O 10 45DSPATR(UL)
N04  A            LVAN01         5Y 0O 10 37DSPATR(UL)
     A                                      DSPATR(HI)
     A                                      EDTCDE(4)
N04  A            LVAN00         5Y 0O 10 45DSPATR(UL)
N04  A                                      DSPATR(HI)
N04  A                                      EDTCDE(4)
     A            LVVKT1         7Y 2O 10 51DSPATR(UL)
     A                                      DSPATR(HI)
     A                                      EDTCDE(4)
     A            LVKUB1         7Y 4O 10 60DSPATR(UL)
     A                                      DSPATR(HI)
     A                                      EDTCDE(4)
     A                                 11  2MSGCON(12 YBC0934 SYMSGF)
     A            LVEH2          3A  O 11 15DSPATR(UL)
     A                                      DSPATR(HI)
     A            LVEH2T        15A  O 11 20DSPATR(UL)
     A                                      DSPATR(HI)
     A            LVAN12         5Y 0O 11 37DSPATR(UL)
     A                                      DSPATR(HI)
     A                                      EDTCDE(4)
     A            LVAN02         5Y 0O 11 45DSPATR(UL)
     A                                      DSPATR(HI)
     A                                      EDTCDE(4)
     A            LVVKT2         7Y 2O 11 51DSPATR(UL)
     A                                      DSPATR(HI)
     A                                      EDTCDE(4)
     A            LVKUB2         7Y 4O 11 60DSPATR(UL)
     A                                      DSPATR(HI)
     A                                      EDTCDE(4)
     A                                 12  2MSGCON(12 YBC0935 SYMSGF)
     A            LVEH3          3A  O 12 15DSPATR(UL)
     A                                      DSPATR(HI)
     A            LVEH3T        15A  O 12 20DSPATR(UL)
     A                                      DSPATR(HI)
     A            LVAN23         5Y 0O 12 37DSPATR(UL)
     A                                      DSPATR(HI)
     A                                      EDTCDE(4)
     A            LVAN03         5Y 0O 12 45DSPATR(UL)
     A                                      DSPATR(HI)
     A                                      EDTCDE(4)
     A            LVVKT3         7Y 2O 12 51DSPATR(UL)
     A                                      DSPATR(HI)
     A                                      EDTCDE(4)
     A            LVKUB3         7Y 4O 12 60DSPATR(UL)
     A                                      DSPATR(HI)
     A                                      EDTCDE(4)
     A                                 13  2MSGCON(12 YBC0936 SYMSGF)
     A            LVMIN1         1Y 0O 13 15DSPATR(UL)
     A                                      DSPATR(HI)
     A            LVMEH          3A  O 13 17DSPATR(HI)
     A                                      DSPATR(UL)
     A                                 13 30MSGCON(06 YBC0937 SYMSGF)
     A            LVSALD         7Y 0O 13 37DSPATR(UL)
     A                                      DSPATR(HI)
     A                                      EDTCDE(4)
     A                                 13 47MSGCON(10 YBC0938 SYMSGF)
     A            LVMSTK         5Y 0O 13 58DSPATR(UL)
     A                                      DSPATR(HI)
     A                                      EDTCDE(4)
     A                                 14  2MSGCON(12 YBC0939 SYMSGF)
     A            LVBATC         1A  O 14 15DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 14 18MSGCON(12 YBC0940 SYMSGF)
     A            LVDTU          1A  O 14 31DSPATR(UL)
     A                                      DSPATR(HI)
N02  A                                 14 34MSGCON(12 YBC0941 SYMSGF)
N02  A            LVSN           1A  O 14 47DSPATR(UL)
N02  A                                      DSPATR(HI)
     A                                 15  2MSGCON(12 YBC0942 SYMSGF)
     A            LVTXT         40A  O 15 15DSPATR(UL)
     A                                      DSPATR(HI)
N01  A                                 16  2MSGCON(12 YBC0943 SYMSGF)
N01  A            LVLOK1         5A  O 16 15DSPATR(UL)
N01  A                                      DSPATR(HI)
N01  A            LVLOK2        20A  O 16 21DSPATR(UL)
N01  A                                      DSPATR(HI)
     A                                 17  2MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
