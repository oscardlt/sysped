********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10222 * 01 YBC PTF10 NYE FELTER FOR VARSLING
********************************************************************
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
      *
     A          R B10
     A                                      CF03(03 'EXIT')
     A                                      CF12(12 'CANCEL')
     A                                      CF04(04 'SØK')
     A                                      SLNO(20)
     A                                      CLRL(05)
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            WMODE          7A  O  1  2MSGID(&WMODNR SYMSGF)
     A                                      COLOR(WHT)
     A                                  2  6'Ptype Tekstbeskrivelse            -
     A                                              Farge      Dag Farge '
     A            TVKODE         5A  B  3  6
     A  40                                  DSPATR(PR)
     A N40                                  DSPATR(PC)
     A  40                                  COLOR(WHT)
     A            TVTEXT        35A  B  3 12
     A  40                                  DSPATR(PC)
     A            TVFARG        10A  B  3 48
     A            TVANDG         3Y 0B  3 59EDTCDE(Z)
     A            TVADFA        10A  B  3 63
     A                                  4  2'F3/F12=Avbryt   F4=Søk'
     A                                      COLOR(BLU)
     A  30                              5  2'FEIL I INNTASTEDE DATA'
     A                                      COLOR(WHT)
     A            WMODNR         7A  P
      *
     A          R B20
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  3'SYSPED/SYISKDT'
     A                                      COLOR(BLU)
     A                                  1 24MSGCON(030 TBR0100 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 57'Dato:'
     A                                      COLOR(BLU)
     A                                  1 63DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  2 31'Posteringstype'
     A                                      COLOR(WHT)
     A                                  2 57'Kl. :'
     A                                      COLOR(BLU)
     A                                  2 63TIME
     A                                      COLOR(BLU)
     A                                  4  3'Velg funksjon, trykk Enter'
     A                                      COLOR(BLU)
     A                                  4 40'  2=Endre  4=Slette'
     A                                      COLOR(BLU)
     A                                  6  3'Valg'
     A                                      COLOR(WHT)
     A                                  6  6'Ptype Tekstbeskrivelse            -
     A                                              Farge      Dag Farge '
     A                                      COLOR(WHT)
     A            FVKODE         5A  B  7  6
     A                                 10 10'(Ingen poster i filen)   '
     A  30                             24  2'FEIL I INNTASTEDE DATA'
     A                                      COLOR(WHT)
      *
     A          R B30
     A                                      CF03(03 'EXIT')
     A                                      CF12(12 'CANCEL')
     A                                      SLNO(20)
     A                                      CLRL(05)
     A                                      OVERLAY
     A            WMODNR         7A  P
     A            WMODE          7A  O  1  2MSGID(&WMODNR SYMSGF)
     A                                      COLOR(WHT)
     A                                  2  6'Ptype Tekstbeskrivelse            -
     A                                              Farge      Dag Farge '
     A            TVKODE         5A  O  2  6DSPATR(PC)
     A                                      COLOR(WHT)
     A            TVTEXT        35A  O  2 12COLOR(WHT)
     A            TVFARG        10A  O  2 48COLOR(WHT)
     A            TVANDG         3Y 0O  3 59EDTCDE(Z)
     A            TVADFA        10A  O  3 63
     A                                  4  2MSGCON(010 TBR0013 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 32MSGCON(030 TBR0110 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  4 70MSGCON(010 TBR0015 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
      *
     A          R B40
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF03(03 'Avslutt')
     A N31                                  CF05(05 'Forny')
     A                                      CF06(06 'Ny linje')
     A                                      CF10(10 'LEDETEKST')
     A  31                                  CF12(12 'Avbryt')
     A                                      SLNO(23)
     A                                      CLRL(02)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2MSGCON(010 TBR0013 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  1 17MSGCON(010 TBR0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  1 30MSGCON(010 TBR0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  1 45'F10=Ledetekst'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  2  2TEXT('FEIL I INNTASTEDE DATA')
      *
     A          R H50                       SFL
     A            SFRRN         10S 0H
     A            WSNR           1A  I  8  3VALUES(' ' '2' '4')
     A            SVKODE         5   O  8  6
     A            SVTEXT        35A  O  8 12
     A            SVFARG        10A  O  8 48
     A            SVANDG         3Y 0O  8 59EDTCDE(Z)
     A            SVADFA        10A  O  8 63
      *
     A          R K60                       SFLCTL(H50)
     A                                      SFLSIZ(2000)
     A                                      SFLPAG(0013)
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A                                  1  3'SYSPED/SYISKDT'
     A                                      COLOR(BLU)
     A                                  1 24MSGCON(030 TBR0100 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 57'Dato:'
     A                                      COLOR(BLU)
     A                                  1 63DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  2 31'Posteringstype'
     A                                      COLOR(WHT)
     A                                  2 57'Kl. :'
     A                                      COLOR(BLU)
     A                                  2 63TIME
     A                                      COLOR(BLU)
     A            ZRECNO         4S 0O  2 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  4  3'Velg funksjon, trykk Enter'
     A                                      COLOR(BLU)
     A                                  4 40'  2=Endre  4=Slette'
     A                                      COLOR(BLU)
     A                                  6  2'Ops'
     A                                      COLOR(WHT)
     A                                  6  6'Ptype Tekstbeskrivelse            -
     A                                              Farge      Dag Farge '
     A                                      COLOR(WHT)
     A            FVKODE         5A  B  7  6
