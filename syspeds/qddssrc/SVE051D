     A*%%TS  SD  20141208  102938  CB          REL-V7R1M0  5770-WDS
     A*%%EC
     A                                      DSPSIZ(27 132 *DS4)
     A                                      PRINT
     A                                      ALTHELP
     A                                      HELP
      *
     A          R SVE051DA                  SFL
     A            ZOP            1A  I  8  2VALUES(' ' '2' '4' '5')
     A            SVEA_SYAV      4Y 0O  8  5EDTCDE(4)
     A            SVEA_SYOP      7Y 0O  8 12EDTCDE(4)
     A            SVEA_OMEO     17A  O  8 22
     A            SVEA_OMTY      1A  O  8 42
     A            SVEA_0035      1A  O  8 48
     A*
     A          R SVE051DB                  SFLCTL(SVE051DA)
     A*%%TS  SD  20141208  102226  CB          REL-V7R1M0  5770-WDS
     A                                      SFLSIZ(0014)
     A                                      SFLPAG(0013)
     A  91N93N94                            ROLLUP(25 'Rollup')
     A  91N93N94                            ROLLDOWN(26 'Rolldown')
     A                                      CF03(03 'Gå ut')
     A                                      CF04(04 'Søk')
     A                                      CF05(05 'Refresh')
     A                                      CF06(06 'NY')
     A                                      CF12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/SVE051'
     A                                      COLOR(BLU)
     A                                  1 16'DA'
     A                                      COLOR(BLU)
     A                                  1 55'Underhåll av TDS Export'
     A                                      COLOR(WHT)
     A                                  1116DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  1125TIME
     A                                      COLOR(BLU)
     A                                  2 56'Avdelingsupplysningar'
     A                                      COLOR(WHT)
     A            ZRECNO         4S 0O  2119SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  4  2'Vælj option eller tryck Enter.'
     A                                      COLOR(BLU)
     A                                  5  4'2=Ændra   4=Radera'
     A                                      COLOR(BLU)
     A                                  7  5'Avd'
     A                                      COLOR(WHT)
     A                                  7 11'Ærendenr'
     A                                      COLOR(WHT)
     A                                  7 22'Eori'
     A                                      COLOR(WHT)
     A                                  7 42'Typ'
     A                                      COLOR(WHT)
     A                                  7 48'Test'
     A                                      COLOR(WHT)
     A*
     A          R SVE051DC
     A*%%TS  SD  20141208  101030  CB          REL-V7R1M0  5770-WDS
     A                                      OVERLAY
     A                                 26  2'F3=Avsluta'
     A                                      COLOR(BLU)
     A                                 26 15'F4=Søk'
     A                                      COLOR(BLU)
     A                                 26 24'F6=Skapa'
     A                                      COLOR(BLU)
     A                                 26 35'F12=Avbryt'
     A                                      COLOR(BLU)
     A*
     A          R SVE051DD
     A*%%TS  SD  20141208  102938  CB          REL-V7R1M0  5770-WDS
     A                                      OVERLAY
     A                                      CF03(03 'Gå ut')
     A                                      CF04(04 'Søk')
     A                                      CF12(12 'Forrige')
     A            ZVEA_SYAV      4Y 0B 22  5EDTCDE(4)
     A            ZVEA_SYOP      7Y 0B 22 12EDTCDE(4)
     A            ZVEA_OMEO     17A  B 22 22
     A            ZVEA_OMTY      1A  B 22 42
     A            ZVEA_0035      1A  B 22 48
     A                                 22 50'(1=Test)'
     A                                 23 12'Handl:'
     A            ZVEA_OMHA     35A  B 23 19
     A                                 23 57'Tel:'
     A            ZVEA_OMTL     25A  B 23 62DSPATR(PC)
     A                                 26  2'F3=Avsluta'
     A                                      COLOR(BLU)
     A                                 26 15'F4=Søk'
     A                                      COLOR(BLU)
     A                                 26 24'F12=Avbryt'
     A                                      COLOR(BLU)
      *
     A          R SVE051DE
     A                                 23  2' '
     A                                 24  2' '
      *
     A          R SVE051DØ
     A                                      OVERLAY
     A            ZMSGNO         7A  P
     A            ZMSGTX        79   O 27  2MSGID(&ZMSGNO SYMSGF)
     A                                      COLOR(WHT)
