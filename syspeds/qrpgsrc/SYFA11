********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
11XXX * 01 SH  PTF11 enhet innført         ..
11XXX * 02 SH  PTF11 generelle gebyr  F20
11XXX * 03 SH  PTF12 justeringsverdi kommet
********************************************************************
      *
      ***********************************************************
      *** PROGRAM SKAL VEDLIKEHOLDE GEBYRS PRISAVT. (BRUTTO)  ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01 02 07 09-11 13-24             ***
      *** LEDIGE INDIKATORER 27-29 31 32 34-40 45-97          ***
      ***********************************************************
      *
     FPRIK1D2   IF   E           K DISK
     FKODTGE    IF   E           K DISK
     FKODTFR    IF   E           K DISK
     FKODTOTY   IF   E           K DISK
     FSYFA11FM  CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     C*
     C                   EXSR      INIT
     C*
     C     BG1           TAG
     C     START         TAG
     C                   MOVE      '1'           *IN43
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(43)
     C                   Z-ADD     *ZEROS        ZRECNO
     C                   EXSR      MOVBY
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     PRIK1K        SETLL     PRIK1D2
     C     START1        TAG
     C     YKNR          READE     PRIK1D2                                44
N02  C  N44GEN           IFEQ      'J'
N02  C     PKOT01        ANDNE     'GE'
N02  C                   SETON                                        44
N02  C                   END
      *
     C     START2        TAG
      *
     C  N44              DO
     C                   DO        15
     C                   ADD       1             ZRECNO
     C     KODGEK        CHAIN     KODTGE                             33
     C   33              MOVE      *BLANKS       KGENOT
     C     PKAVTT        COMP      'P'                                    50
     C   50              Z-ADD     PKBELØ        XXPROS
     C                   WRITE     SUBFIL
     C     YKNR          READE     PRIK1D2                                44
N02  C  N44GEN           IFEQ      'J'
N02  C     PKOT01        ANDNE     'GE'
N02  C                   SETON                                        44
N02  C                   END
     C  N44              EXSR      MOVPY
     C  N44              END
     C                   END
     C                   Z-ADD     ZRECNO        YRECNO            5 0
     C*
     C     START3        TAG
     C                   MOVEA     '11'          *IN(41)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYWRK
     C                   ELSE
     C                   WRITE     SUBCTL
     C                   EXFMT     BUNN
     C                   END
     C                   SETOFF                                       30
     C*
     C*** EXIT
     C   03              GOTO      SLUTT
     C*
     C*** REFRESH
     C   05              GOTO      START
     C*
     C*** CREATE
     C   06              DO
     C                   EXSR      CRTPR
     C                   GOTO      START3
     C                   END
     C*** SEARCH
     C   08              GOTO      START6
     C*** CANCEL
     C   12              GOTO      BG1
     C*
N02  C*** F20
N02  C     *IN20         ifeq      '1'
N02  c     gen           ifeq      'J'
N02  c                   move      'N'           gen               1
N02  C                   SETON                                        60
N02  c                   else
N02  c                   move      'J'           gen
N02  C                   SETOFF                                       60
N02  C                   move      'GE'          W2OT01
N02  C                   MOVE      'GEN'         W2FR01
N02  C                   GOTO      START
N02  C                   END
N02  C                   END
     C*
     C*** ROLL
     C   25              DO
     C                   SETOFF                                       2544
     C                   Z-ADD     YRECNO        ZRECNO
     C*** READ FIRST RECORD FROM FILE THAT IS NOT AT SUBFILE
     C     PRIK3K        SETLL     PRIK1D2
     C     YKNR          READE     PRIK1D2                                44
N02  C  N44GEN           IFEQ      'J'
N02  C     PKOT01        ANDNE     'GE'
N02  C                   SETON                                        44
N02  C                   END
     C                   GOTO      START2
     C                   END
     C*
     C     ZRECNO        IFNE      0
     C                   EXSR      HENT
     C                   END
     C                   GOTO      START3
     C*
     C*** SEARCH
     C     START6        TAG
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     19            POSNBR
     C                   EXFMT     VAREL2
     C   12              GOTO      START3
     C                   GOTO      START
     C*
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
     C*
     C***********************************************
     C     TESTB1        BEGSR
     C***********************************************
     C     KODFRK        CHAIN     KODTFR                             99
     C   99              DO
     C                   MOVE      'SPF0010'     MSGNR
     C                   SETON                                        30
     C                   Z-ADD     9             LINNBR
     C                   Z-ADD     20            POSNBR
     C                   END
     C     KODOTK        CHAIN     KODTOTY                            98
     C   98              DO
     C  N30              MOVE      'SPO0007'     MSGNR
     C  N30              Z-ADD     11            LINNBR
     C  N30              Z-ADD     20            POSNBR
     C                   SETON                                        30
     C                   END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     HENT          BEGSR
     C***********************************************
     C                   MOVEA     '00'          *IN(41)
     C     *IN30         DOWEQ     '0'
     C                   READC     SUBFIL                                 30
     C  N30WSNR          IFEQ      '2'
     C     PKGEBY        COMP      '*DL'                                  30
     C  N30              EXSR      CHGPR                                        CHANGE
     C                   GOTO      SLHENT
     C                   END
     C  N30WSNR          IFEQ      '3'
     C     PKGEBY        COMP      '*DL'                                  30
     C  N30              EXSR      CPYPR                                        COPY
     C                   GOTO      SLHENT
     C                   END
     C  N30WSNR          IFEQ      '4'
     C     PKGEBY        COMP      '*DL'                                  30
     C  N30              EXSR      DELPR                                        DELETE
     C                   GOTO      SLHENT
     C                   END
     C                   END
     C*
     C     SLHENT        TAG
     C   30              MOVE      'DIV0012'     MSGNR
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     DELPR         BEGSR
     C***********************************************
     C                   Z-ADD     1             UFLAGG            1 0
     C                   CALL      'SYFA11A'
     C                   PARM                    PKAVTN
     C                   PARM                    PKGEBY
     C                   PARM                    PKTNR
     C                   PARM                    PKMVA
N01  C                   PARM                    PKKOLL
     C                   PARM                    PKSAMM
N03  C                   PARM                    PKTEGN
N03  C                   PARM                    PKJUST
     C                   PARM                    PKFVAL
     C                   PARM                    PKLNR
     C                   PARM                    PKFR01
     C                   PARM                    PKOT01
     C                   PARM                    UFLAGG
     C*
     C                   MOVE      ' '           WSNR
     C     UFLAGG        IFNE      1
     C                   MOVE      *BLANKS       PKAVTN
     C                   MOVE      '*DL'         PKGEBY
     C                   MOVE      *BLANKS       KGENOT
     C                   MOVE      *BLANKS       PKTNR
     C                   MOVE      *ZEROS        PKLNR
     C                   MOVE      *BLANKS       PKMVA
N01  C                   MOVE      *BLANKS       PKKOLL
     C                   MOVE      *BLANKS       PKSAMM
     C                   END
     C                   UPDATE    SUBFIL
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     CHGPR         BEGSR
     C***********************************************
     C                   Z-ADD     2             UFLAGG            1 0
     C                   CALL      'SYFA11A'
     C                   PARM                    PKAVTN
     C                   PARM                    PKGEBY
     C                   PARM                    PKTNR
     C                   PARM                    PKMVA
N01  C                   PARM                    PKKOLL
     C                   PARM                    PKSAMM
     C                   PARM                    PKFVAL
     C                   PARM                    PKLNR
     C                   PARM                    PKFR01
     C                   PARM                    PKOT01
     C                   PARM                    UFLAGG
     C*
     C                   MOVE      ' '           WSNR
     C                   UPDATE    SUBFIL
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     CPYPR         BEGSR
     C***********************************************
     C                   MOVE      ' '           WSNR
     C                   UPDATE    SUBFIL
     C                   Z-ADD     3             UFLAGG            1 0
     C                   CALL      'SYFA11A'
     C                   PARM                    PKAVTN
     C                   PARM                    PKGEBY
     C                   PARM                    PKTNR
     C                   PARM                    PKMVA
N01  C                   PARM                    PKKOLL
     C                   PARM                    PKSAMM
     C                   PARM                    PKFVAL
     C                   PARM                    PKLNR
     C                   PARM                    PKFR01
     C                   PARM                    PKOT01
     C                   PARM                    UFLAGG
     C*
     C     UFLAGG        IFNE      1
     C                   ADD       1             YRECNO
     C                   Z-ADD     YRECNO        ZRECNO
     C     KODGEK        CHAIN     KODTGE                             33
     C   33              MOVE      *BLANKS       KGENOT
     C                   WRITE     SUBFIL
     C                   END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     CRTPR         BEGSR
     C***********************************************
     C                   Z-ADD     9             UFLAGG            1 0
     C                   MOVE      *BLANKS       PKOT01
     C                   MOVE      *BLANKS       PKFR01
N02  C     GEN           IFEQ      'J'
N02  C                   MOVE      'GE'          PKOT01
N02  C                   MOVE      'GEN'         PKFR01
N02  C                   END
     C                   CALL      'SYFA11A'
     C                   PARM                    PKAVTN
     C                   PARM                    PKGEBY
     C                   PARM                    PKTNR
     C                   PARM                    PKMVA
n01  C                   PARM                    PKKOLL
     C                   PARM                    PKSAMM
     C                   PARM                    PKFVAL
     C                   PARM                    PKLNR
     C                   PARM                    PKFR01
     C                   PARM                    PKOT01
     C                   PARM                    UFLAGG
     C*
     C     UFLAGG        IFNE      1
     C                   ADD       1             YRECNO
     C                   Z-ADD     YRECNO        ZRECNO
     C     KODGEK        CHAIN     KODTGE                             33
     C   33              MOVE      *BLANKS       KGENOT
     C                   WRITE     SUBFIL
     C                   END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVBY         BEGSR
     C***********************************************
     C                   MOVE      W2OT01        YOT01
     C                   MOVE      W2FR01        YFR01
     C                   MOVE      W2LNR         YLNR
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVPY         BEGSR
     C***********************************************
     C                   MOVE      PKOT01        YOT01
     C                   MOVE      PKFR01        YFR01
     C                   MOVE      PKAVTN        YAVTN
     C                   MOVE      PKGEBY        YGEBY
     C                   MOVE      PKLNR         YLNR
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     SØKDTA        BEGSR
     C***********************************************
     C     LINNBR        IFEQ      9
     C     POSNBR        ANDGE     20
     C     POSNBR        ANDLE     22
     C                   MOVE      'J'           USPED             1
     C                   CALL      'SYFI10G'
     C                   PARM                    WSFR01
     C                   PARM                    USPED
     C                   Z-ADD     11            LINNBR
     C                   GOTO      SLSØK
     C                   END
     C*
     C     LINNBR        IFEQ      11
     C     POSNBR        ANDGE     20
     C     POSNBR        ANDLE     21
     C                   CALL      'SYFI10C'
     C                   PARM                    WSOT01
     C                   Z-ADD     9             LINNBR
     C                   GOTO      SLSØK
     C                   END
     C*
     C                   MOVE      'DIV1037'     MSGNR
     C                   SETON                                        30
     C*
     C     SLSØK         ENDSR
     C*
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     PRIK1K        KLIST
     C                   KFLD                    YKNR
     C                   KFLD                    W2OT01
     C                   KFLD                    W2FR01
     C     PRIK2K        KLIST
     C                   KFLD                    YKNR
     C                   KFLD                    WSOT01
     C                   KFLD                    WSFR01
     C                   KFLD                    PKAVTN
     C                   KFLD                    PKGEBY
     C                   KFLD                    PKLNR
     C     PRIK3K        KLIST
     C                   KFLD                    YKNR
     C                   KFLD                    YOT01
     C                   KFLD                    YFR01
     C                   KFLD                    YAVTN
     C                   KFLD                    YGEBY
     C                   KFLD                    YLNR
     C     KODGEK        KLIST
     C                   KFLD                    YGEUNI
     C                   KFLD                    PKGEBY
     C     KODOTK        KLIST
     C                   KFLD                    YOTUNI
     C                   KFLD                    WSOT01
     C     KODFRK        KLIST
     C                   KFLD                    YFRUNI
     C                   KFLD                    WSFR01
     C*
     C                   MOVE      '    ALLE'    YKNR              8
     C                   MOVE      ' '           YAVTN             1
     C                   MOVE      '   '         YGEBY             3
     C                   MOVE      '  '          YOT01             2
     C                   MOVE      '   '         YFR01             3
     C                   MOVE      *ZEROS        W2LNR             4 0
     C                   MOVE      *ZEROS        YLNR              4 0
     C                   MOVE      'GE'          YGEUNI            2
     C                   MOVE      'OTY'         YOTUNI            3
     C                   MOVE      'FR'          YFRUNI            2
     C*
N02  c                   move      'N'           gen               1
N02  C                   SETON                                        60
     C                   ENDSR
     C*
