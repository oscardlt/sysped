             PGM        PARM(&FILE &LIBRARY &MEMBER)
             DCL        VAR(&FILE) TYPE(*CHAR) LEN(10) /* File    */
             DCL        VAR(&LIBRARY) TYPE(*CHAR) LEN(10) /* Library */
             DCL        VAR(&MEMBER) TYPE(*CHAR) LEN(10) /* Member  */
             DCL        VAR(&AVD) TYPE(*DEC) LEN(4 0) /* Avdeling */
             DCL        VAR(&MRKP) TYPE(*CHAR) LEN(10) /* Printer +
                          Device for Merknader */
             DCL        VAR(&OK) TYPE(*CHAR) LEN(1) /* Status fra +
                          program, N=Nei(Ikke OK), J=Ja(OK) */
             DCL        VAR(&OKPRT) TYPE(*CHAR) LEN(1) /* Printerdata+
                          finnes for avdelingen J/N         */
             DCL        VAR(&WS) TYPE(*CHAR) LEN(10) VALUE(TVINR) /* +
                          "Skjerm ID"= Membernavn arbeidsfil*/
             DCL        VAR(&USER) TYPE(*CHAR) LEN(10)
           /*______________________________________________*/
           /* Konvertere meldinger.                        */
           /* - Oppdatere transitt register med en unik    */
           /*   status for: Mottatt men ikke utskrevet.    */
           /* - Hvis det er noe feil med datane skrives    */
           /*   det ut en feillogg.                        */
           /*""""""""""""""""""""""""""""""""""""""""""""""*/
             CALL       PGM(SVX154R) PARM(&LIBRARY &FILE &MEMBER &WS +
                          &OK)
             IF         COND(&OK *EQ N) THEN(GOTO CMDLBL(END))
           /*_________________________*/
           /* Skriv ut lossetillatelse*/
           /*"""""""""""""""""""""""""*/
             RTVJOBA    USER(&USER)
             CALL       PGM(SVX157R) PARM(&USER &WS)
           /*______________________________________________*/
           /* Hente avdelingsopplysninger for å overstyre  */
           /* printerfiler. Ingen med A/B/Å i staus-OK=N   */
           /*""""""""""""""""""""""""""""""""""""""""""""""*/
 GETAVD:     CALL       PGM(SVX155R) PARM(&AVD &MRKP &WS &OK &OKPRT)
             IF         COND(&OK *EQ N) THEN(GOTO CMDLBL(END))

             IF         COND(&OKPRT *EQ N) THEN(CHGVAR VAR(&MRKP) +
                          VALUE(*SYSVAL))

             OVRPRTF    FILE(TVINME) PAGESIZE(72 80) OVRFLW(66) +
                          OUTQ(&MRKP) SECURE(*YES)
           /*_________________________*/
           /* Skriv ut Merknader (ME) */
           /*"""""""""""""""""""""""""*/
             CALL       PGM(SVX156R) PARM(&AVD &WS)
             GOTO       CMDLBL(GETAVD)
 END:        ENDPGM
