      *
      ***********************************************************
      *** PROGRAM SKAL ARBEIDE MED FØLSOMME VARER             ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01 02 06-11 13-24
      *** LEDIGE INDIKATORER 28 29 31 32 34-88 95-97
      ***********************************************************
      *
     FSVXVFS    UF A E           K DISK
     FSVXFKDF   IF   E           K DISK
     FSVX021D   CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
      *
     C                   EXSR      INIT
      *
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
     C     UFV           IFNE      *BLANKS
     C                   MOVE      UFV           TVFV
     C                   MOVE      UFVNT         TVFVNT
     C                   ADD       1             ZRECNO
     C                   WRITE     SUBFIL
     C                   END
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     SVVFSK        SETLL     SVXVFS
     C     SVVFSK        READE(N)  SVXVFS                                 94
      *
     C     *IN94         DOWEQ     '0'
     C                   ADD       1             ZRECNO
     C                   WRITE     SUBFIL
     C     SVVFSK        READE(N)  SVXVFS                                 94
     C  N94              END
      *
     C                   Z-ADD     ZRECNO        YRECNO            5 0
     C     ZRECNO        IFGT      0
     C                   Z-ADD     1             ZRECNO
     C                   END
     C                   Z-ADD     4             LINNBR
     C                   Z-ADD     2             POSNBR
      *
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYWRK
     C                   ELSE
     C                   WRITE     BUNN
     C                   EXFMT     SUBCTL
     C                   END
      *
     C                   SETOFF                                       9830
     C                   MOVEA     '00'          *IN(89)
     C   03
     COR 12              GOTO      SLUTT
      *** FRISK OPP
     C   05              DO
     C  N27              EXSR      OPDFIL
     C                   GOTO      START
     C                   END
      *
     C     W2NR          IFEQ      '1'
     C                   EXSR      CRTFS
     C                   GOTO      START3
     C                   END
      *
     C     ZRECNO        IFNE      0
     C                   EXSR      HENT
     C                   END
     C                   GOTO      START3
      *
     C     SLUTT         TAG
     C  N27              EXSR      OPDFIL
     C                   SETON                                        LR
     C                   RETURN
      *
     C***********************************************
     C     OPDFIL        BEGSR
     C***********************************************
     C     SVVFSK        SETLL     SVXVFS
     C     SVVFSK        READE     SVXVFS                                 33
     C     *IN33         DOWEQ     '0'
     C                   DELETE    SVXVFSR
     C     SVVFSK        READE     SVXVFS                                 33
     C  N33              ENDDO
      *
     C                   MOVE      *BLANKS       UFV
     C                   MOVE      *ZEROS        UFVNT
     C     YRECNO        IFGT      0
     C     1             CHAIN     SUBFIL                             33
     C     TVFV          IFNE      '*D'
     C                   MOVE      TVFV          UFV
     C                   MOVE      TVFVNT        UFVNT
     C                   END
     C     2             DO        YRECNO        ZRECNO
     C     ZRECNO        CHAIN     SUBFIL                             33
     C     TVFV          IFNE      '*D'
     C     UFV           IFEQ      *BLANKS
     C                   MOVE      TVFV          UFV
     C                   MOVE      TVFVNT        UFVNT
     C                   ELSE
     C                   WRITE     SVXVFSR
     C                   END
     C                   END
     C                   ENDDO
     C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     HENT          BEGSR
     C***********************************************
     C                   MOVEA     '00'          *IN(91)
     C                   MOVEA     '00'          *IN(98)
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C   98              DO
     C   99              MOVE      '0'           *IN98
     C                   GOTO      SLHENT
     C                   END
     C  N98WSNR          IFEQ      '2'
     C     TVFV          COMP      '*D'                                   98
     C   98              GOTO      SLHENT
     C                   EXSR      CHGFS
     C                   END
     C  N98WSNR          IFEQ      '4'
     C     TVFV          COMP      '*D'                                   98
     C   98              GOTO      SLHENT
     C                   EXSR      DLTFS
     C                   END
     C                   END
      *
     C     SLHENT        TAG
     C   98              DO
     C                   MOVE      'DIV0012'     MSGNR
     C                   SETON                                            30
     C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     CRTFS         BEGSR
     C***********************************************
      *
     C     W2FVNT        COMP      0                                      89
     C     *IN89         IFEQ      '1'
     C                   MOVE      'SVX0817'     MSGNR
     C                   MOVE      '1'           *IN30
     C                   GOTO      SLCRT
     C                   END
      *
     C                   MOVE      W2FV          TVFV
     C                   MOVE      W2FVNT        TVFVNT
     C                   ADD       1             YRECNO
     C                   MOVE      YRECNO        ZRECNO
     C                   WRITE     SUBFIL
     C                   WRITE     SUBCTL
     C                   MOVE      *BLANKS       W2NR
     C                   MOVE      *ZEROS        W2FVNT
      *
     C     SLCRT         ENDSR
      *
     C***********************************************
     C     CHGFS         BEGSR
     C***********************************************
     C                   MOVE      '1'           *IN99
     C                   MOVE      TVFV          WSFV
     C                   MOVE      TVFVNT        WSFVNT
      *
     C     STCHG         TAG
     C                   EXFMT     VARELI
     C                   MOVEA     '00'          *IN(89)
     C                   MOVE      '0'           *IN30
      *
     C   12              GOTO      SLCHG
      *
     C     WSFV          COMP      *BLANKS                                90
     C     *IN90         IFEQ      '1'
     C                   MOVE      'SVX0827'     MSGNR
     C                   MOVE      '1'           *IN30
     C                   GOTO      STCHG
     C                   END
      *
     C     WSFVNT        COMP      0                                      89
     C     *IN89         IFEQ      '1'
     C                   MOVE      'SVX0817'     MSGNR
     C                   MOVE      '1'           *IN30
     C                   GOTO      STCHG
     C                   END
      *
     C                   MOVE      WSFV          TVFV
     C                   MOVE      WSFVNT        TVFVNT
      *
     C     SLCHG         TAG
     C                   MOVE      *BLANKS       WSNR
     C                   UPDATE    SUBFIL
      *
     C                   ENDSR
      *
     C***********************************************
     C     DLTFS         BEGSR
     C***********************************************
     C                   MOVE      '1'           *IN99
     C                   MOVE      *BLANKS       TVFV
     C                   MOVEL     '*D'          TVFV
     C                   MOVE      *ZEROS        TVFVNT
     C                   MOVE      *BLANKS       W2NR
     C                   UPDATE    SUBFIL
      *
     C                   ENDSR
      *
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     SVVFSK        KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    UTDN
     C                   KFLD                    ULI
      *
     C     *ENTRY        PLIST
     C                   PARM                    UAVD              4 0
     C                   PARM                    UTDN              7 0
     C                   PARM                    ULI               5 0
     C                   PARM                    UVNT6             6 0
     C                   PARM                    UFV               2
     C                   PARM                    UFVNT            11 3
     C     *IN27         PARM      *IN27         UIN27             1
      *
     C     UVNT6         CHAIN     SVXFKDF                            33
     C     *IN33         IFEQ      '1'
     C                   MOVE      '1'           *INLR
     C                   RETURN
     C                   END
     C                   MOVE      TFKODE        W2FV
     C                   MOVE      TFKODE        WSFV
     C                   WRITE     WINDOWSZ
     C                   MOVE      UAVD          TVAVD
     C                   MOVE      UTDN          TVTDN
     C                   MOVE      ULI           TVLI
      *
     C                   ENDSR
      *
