      *********************************************************************
      * SLETT MERKET SLETT-LINJER
      * PROGRAM BEHANDLER INPUT FRA SYL026
      *
CRTPG+*  CRTPGM SYSPED/SYL026 MODULE(*LIBL/SYL026 *LIBL/SYL023B
CRTPGM*        *LIBL/CHECKUSR *LIBL/INCGI) ACTGRP(CGI) AUT(*USE)
      *
      *
      *********************************************************************
      * MAIN PROGRAM FLOW
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
      * Boats for sale data base file (LF on BOATSALE by boat serial number)
     FLLAGJL01  UF   E           K DISK    USROPN
     FLLGRPK05  IF   e           k disk    USROPN
     FBRIDF     UF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Prototype definitions and standard system API error structure
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      * Variables common to CGI programs
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
      * VARIABLES SPECIFIC TO THIS PROGRAM
      *--------------------------------------------------------------------
      * Variables received from the input string
     D lng             s              2
     D UsrSpcName      s             10
     D Lib             s             10
     D ItemCount       s             10i 0
     D SernoC          s            107a
     D i               s             10i 0
     D Fn              s             10
     D Mbr             s             10
     D TB              S              1    DIM(15)

     D WSMRNR          S              5
     D WSMN            S              7
     D WSUSER          S             10
     D BWSMRNR         S              5  0
     D BWSMN           S              7  0
     D PGM             C                   CONST('SYSPED/CHECKUSR')
     D PGM2            C                   CONST('*LIBL/SYL023B')
     D                 DS
     D XXREF                   1    107
     D XXMN                    1      7
     D XXMRNR                  8     12
     D XXKJED                 13     20
     D XXKNV                  21     50
     D XXLNR                  51     67
     D XXLNA                  68     97
     D XXUSER                 98    107
      *
      /copy syspeds/qrpgsrcpy,prolog3
      *=====================================================================
      * MAIN PROCESSING LINE
      *=====================================================================
      * Parse input string
     C                   exsr      Parse
      * Initier
     C                   EXSR      INIT
     C                   CALLB     PGM
     C                   PARM                    BIBRID
     C                   PARM                    UsrSpcName
     C                   PARM                    UINN              1
     C                   IF        UINN = 'N'
     C                   GOTO      SLUTT
     C                   END
     C     WSUSER        CHAIN     BRIDF                              50
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVE      WDT           BIDTF
     C                   MOVE      WTM           BITIDF
     C                   UPDATE    BRIDFR
      * Slett plukklinje
     C                   exsr      SLETT
      *
     C                   EXSR      VISLINJE
      * Send output buffer and quit
     C     SLUTT         TAG
     C                   exsr      Exit
      *=====================================================================
      * Send output buffer and quit
      *=====================================================================
     C     Exit          begsr
     C                   CLOSE     LLAGJL01
     C                   close     LLGRPK05
     C                   CLOSE     BRIDF
     C                   eval      *inlr = *on
     C                   return
     C                   endsr
      *=====================================================================
      * Parse input string
      *=====================================================================
     C     Parse         begsr
     C                   eval      lng     = zhbgetvar('lng')
     C                   eval      WSUSER  = zhbgetvar('USER')
     C                   EVAL      UsrSpcName  = zhbgetvar('UsrSpcName')

     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$top
      *  (search criteria)
      *=====================================================================******
     C     SetTop        begsr
      * Repeat national language for the next invocation
     C                   callp     updhtmlvar('LNG':lng:
     C                             InitHTMLVars)
      * Color for text, background, links, visited links, active links
     C                   callp     updhtmlvar('TXTCOLOR':'black')
     C                   callp     updhtmlvar('BOLD':' ')
     C                   callp     updhtmlvar('BGCOLOR':BIFARG)
     C                   callp     updhtmlvar('LINK':'0000FF')
     C                   callp     updhtmlvar('VLINK':'FF0000')
     C                   callp     updhtmlvar('ALINK':'00FF00')
      *
     C                   endsr
      *=====================================================================
      *  Initier
      *=====================================================================
     C     INIT          begsr
      * Get program start time for calculating execution time
     C                   time                    timedata1
      *
     C                   OPEN      BRIDF
     C     WSUSER        CHAIN(N)  BRIDF                              50
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   CALLB     'INCGI'
     C                   PARM                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
     C                   open      LLAGJL01                             50
     C                   open      LLGRPK05                             50
     C     LLAGJL01K     klist
     C                   kfld                    GKGRP
     C                   kfld                    GKPNR
     C                   kfld                    BWSMN
     C     BIKUNR        chain     LLGrpK05                           33
      *
     C                   endsr
      *
      *=====================================================================
      *  Slett merket linje
      *=====================================================================
     C     SLETT         begsr
      * Retrieve number of boats to be shown
     C                   EVAL      ItemCount = ZhbGetVarCnt('serno')
      * Show the requested boats
     C                   IF        ItemCount = 0
     C                   eval      SernoC = zhbgetvar('xxref')
     C                   MOVE      SERNOC        XXREF
     C                   MOVE      XXMN          BWSMN
     C                   MOVE      XXMRNR        BWSMRNR
     C     LLAGJL01K     CHAIN     LLAGJL01                           33
     C  N33              DELETE    LLAGJR
     C                   ELSE
     C                   eval      i = 1
     C     i             DOUGT     ItemCount
     C                   eval      SernoC = ZhbGetVar('serno':i)
     C                   MOVE      SERNOC        XXREF
     C                   MOVE      XXMN          BWSMN
     C                   MOVE      XXMRNR        BWSMRNR
     C     LLAGJL01K     CHAIN     LLAGJL01                           33
     C  N33              DELETE    LLAGJR
     C                   eval      i = i +1
     C                   ENDDO
     C                   END
      *
     C                   endsr
      *=====================================================================
      * VIS LINJER
      *=====================================================================
     C     VISLINJE      begsr
      *
     C                   CALLB     PGM2
     C                   PARM                    BWSMRNR
     C                   PARM                    GKGRP             8 0
     C                   PARM                    BIKUNR
     C                   PARM                    WSUSER
     C                   PARM                    UsrSpcName
     C                   PARM                    BIFIRM
     C                   PARM                    BIFARG
     C                   PARM                    WSANR            15
     C                   PARM                    WSVB             35
     C                   PARM                    WSSALD            7
     C                   PARM                    WSOANT            7
     C                   PARM                    WSLOK1            5
     C                   PARM                    WSLOK2           20
     C                   PARM                    WSLLT             3
     C                   PARM                    FMELDNR           1
     C                   PARM                    FEILMELDING      80
      *
     C                   endsr
