      *
      ***********************************************************
      *** PROGRAM SKAL KONVERTER XML-FIL TIL SAD-IMPORT       ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01 02 04 07 09-11 13-24          ***
      *** LEDIGE INDIKATORER 27-29 31 32 34-39 41-88 95-97    ***
      ***********************************************************
      *
     FXMLRCVF   IF   E           K DISK
     FXMLRCVF01 IF   E           K DISK    RENAME(XMLRCVFR:XMLRCVFR01)
     FXMLRCVF02 IF   E           K DISK    RENAME(XMLRCVFR:XMLRCVFR02)
     FXMLRCVF03 IF   E           K DISK    RENAME(XMLRCVFR:XMLRCVFR03)
     FXMLRCAF   IF   E           K DISK
     FHEADF     UF A E           K DISK
     FDISP      O  A E           K DISK
     FFRISOK    UF A E           K DISK
     FFRISOL01  IF   E           K DISK    RENAME(FRISOKR:FRISOKR1)
     FENADSF    UF A E           K DISK
     FTELL      UF   E           K DISK
      *
     D                 DS
     D  XTXT                   1     15
     D  XT                     1     15    DIM(15)
     D                 DS
     D  XTAL                   1     15  3
     D  XTALH                  1     12  0
     D  XTALD1                13     13
     D  XTALD2                14     14
     D  XTALD3                15     15
     D                 DS
     D  XAN08                  1      8
     D  XAN081                 1      7
     D  XAN082                 8      8
     D                 DS
     D  HEAVD                  1      4  0
     D  DIAVD                  1      4  0
     D  FSAVD                  1      4  0
     D  NAVD                   1      4  0
     D  HEOPD                  5     11  0
     D  DIOPD                  5     11  0
     D  FSOPD                  5     11  0
     D  NOPD                   5     11  0
     D  HESG                  12     14
     D  DISIGN                12     14
     D  X14S                   1     14
     D                 DS
     D  HEDTOP                 1      8  0
     D  FSDTOP                 1      8  0
     D  HEDTOPA                1      4  0
     D  HEDTOPM                5      6  0
     D  HEDTOPD                7      8  0
     D                 DS
     D  HEVS1                  1     25
     D  HEVS122                4     25
      *
     C                   EXSR      INIT
      *
     C                   EXSR      SRXML1
      *
     C                   EVAL      *INLR = *ON
     C                   RETURN
      *
      ***********************************************
     C     SRXML1        BEGSR
      ***********************************************
      *
      * HENT MELDINGS REFERANSE
     C                   Z-ADD     1             X001_XRLEV1
     C     STSRXML1      TAG
     C                   ADD       1             X001_XRLEV1
     C                   Z-ADD     1             X001_XRLEV2
     C                   Z-ADD     1             X001_XRLEV3
     C                   Z-ADD     0             X001_XRLEV4
     C                   Z-ADD     0             X001_XRLEV5
     C                   Z-ADD     0             X001_XRLEV6
     C                   Z-ADD     0             X001_XRLEV7
     C                   Z-ADD     0             X001_XRLEV8
     C                   Z-ADD     0             X001_XRLEV9
     C     XMLKEY01      CHAIN     XMLRCAF                            33
     C     *IN33         CABEQ     *ON           SLSRXML1
     C                   DOW       *IN33 = *OFF
     C                   IF        XRATN = 'consignmentId'
     C                   EVAL      FSSOK  = XRATV
     C                   EXSR      SRXML2
     C                   GOTO      STSRXML1
     C                   END
     C     XMLKEY01      READE     XMLRCAF                                33
     C                   ENDDO
      *
     C     SLSRXML1      ENDSR
      *
      ***********************************************
     C     SRXML2        BEGSR
      ***********************************************
      *
     C                   EXSR      SRFINNAVD
      *
     C                   EVAL      X002_XRLEV1 = XRLEV1
     C                   EVAL      X002_XRLEV2 = XRLEV2
     C                   EVAL      X002_XRLEV3 = 1
     C                   EVAL      X002_XRLEV4 = 0
      *
     C     XMLKEY02      CHAIN     XMLRCVF                            33
     C                   DOW       *IN33 = *OFF                                 1<-B
     C                   SELECT                                                  2<-B
      * HENT INFOR. FRA <Consignment>
     C                   WHEN      XRNV = 'Consignment'                          2<-B
     C                   EXSR      SRXML301
      * HENT INFOR. FRA <CustomsClearance>
     C                   WHEN      XRNV = 'CustomsClearance'                     2<-B
     C                   EXSR      SRXML302
      * HENT INFOR. FRA <ConsignmentGoodsItemSet>
     C                   WHEN      XRNV = 'ConsignmentGoodsItemSet'              2
     C                   EXSR      SRXML303
      * HENT INFOR. FRA <Party>
     C                   WHEN      XRNV = 'Party'                                2
     C                   EXSR      SRXML304
      * HENT INFOR. FRA <TransportLeg>
     C                   WHEN      XRNV = 'TransportLeg'                         2
     C                   EXSR      SRXML305
      * HENT INFOR. FRA <TermsOfDelivery>
     C                   WHEN      XRNV = 'TermsOfDelivery'                      2
     C                   EXSR      SRXML306
      * HENT INFOR. FRA <Service>
     C                   WHEN      XRNV = 'Service'                              2
     C                   EXSR      SRXML307
      * HENT INFOR. FRA <DateAndTimes>
     C                   WHEN      XRNV = 'DateAndTimes'                         2
     C                   EXSR      SRXML308
     C                   ENDSL                                                   2<-E
     C                   ADD       1             X002_XRLEV3
     C     XMLKEY02      CHAIN     XMLRCVF                            33
     C                   ENDDO                                                  1<-E
      *
     C                   EXSR      SROPDHEA
      *
     C                   ENDSR
      *
      ***********************************************
     C     SRXML301      BEGSR
      ***********************************************
      *
     C                   EVAL      FSKODE = 'CMR'
     C     FRISOL01K     CHAIN     FRISOL01                           33
     C                   IF        *IN33
     C                   CLEAR                   HEADFR
     C                   CLEAR                   DISPR
     C                   CLEAR                   FRISOKR
     C                   MOVE      XXAVD         HEAVD
     C     HEAVD         CHAIN     TELL                               33
     C                   MOVE      TEOPDN        HEOPD
     C                   ADD       1             TEOPDN
     C                   UPDATE    TELLR
     C                   MOVE      WDT           HEDTR
     C                   EVAL      TRKNFA = 1
     C                   EVAL      HEKNA  = TRKNFA
     C                   MOVE      'J'           XNYOPD            1
     C                   ELSE
     C     HEADFK        SETLL     FRISOK
     C     HEADFK        READE     FRISOK                                 33
     C                   DOW       *IN33 = *OFF
     C                   DELETE    FRISOKR
     C     HEADFK        READE     FRISOK                                 33
     C                   ENDDO
     C                   EVAL      FSKODE = *BLANKS
     C                   EVAL      FSSOK  = *BLANKS
     C                   EVAL      FSDOKK = *BLANKS
     C     HEADFK        SETLL     ENADSF
     C     HEADFK        READE     ENADSF                                 33
     C                   DOW       *IN33 = *OFF
     C                   DELETE    NADSR
     C     HEADFK        READE     ENADSF                                 33
     C                   ENDDO
     C     HEADFK        CHAIN     HEADF                              33
     C                   MOVE      'N'           XNYOPD
     C                   END
      *
      * INFOR. FRA <CustomsClearance>
      *
     C                   EVAL      X003_XRLEV1 = XRLEV1
     C                   EVAL      X003_XRLEV2 = XRLEV2
     C                   EVAL      X003_XRLEV3 = XRLEV3
     C                   EVAL      X003_XRLEV4 = 1
     C                   EVAL      X003_XRLEV5 = 0
      *
     C     XMLKEY03      CHAIN     XMLRCVF01                          33
     C                   DOW       *IN33 = *OFF                                 1<-B
     C                   SELECT                                                  2<-B
     C                   WHEN      XRNV = 'TotalGrossWeight'                     2
     C                   MOVEL     XRVERD        XTXT
     C                   EXSR      KNVTAL
     C                   Z-ADD     XTAL          HEVKT
     C                   WHEN      XRNV = 'TotalVolume'                          2
     C                   MOVEL     XRVERD        XTXT
     C                   EXSR      KNVTAL
     C                   EVAL      HEM3 = XTAL / 1000
     C                   WHEN      XRNV = 'NumberOfPackages'                     2
     C                   MOVEL     XRVERD        XTXT
     C                   EXSR      KNVTAL
     C                   Z-ADD     XTAL          HENT
     C                   WHEN      XRNV = 'Reference'                            2
     C                   EXSR      SRXML301A
     C                   ENDSL                                                   2<-E
      *
     C                   EVAL      X003_XRLEV4 = X003_XRLEV4 + 1
     C     XMLKEY03      CHAIN     XMLRCVF01                          33
     C                   ENDDO                                                  1<-E
      *
     C                   ENDSR
      *
      ***********************************************
     C     SRXML301A     BEGSR
      ***********************************************
      *
     C                   EVAL      X003_XRLEV1 = XRLEV1
     C                   EVAL      X003_XRLEV2 = XRLEV2
     C                   EVAL      X003_XRLEV3 = XRLEV3
     C                   EVAL      X003_XRLEV4 = XRLEV4
      *
     C     XMLKEY031     CHAIN     XMLRCVF02                          33
     C                   DOW       *IN33 = *OFF                                 1<-B
     C                   SELECT                                                  2<-B
     C                   WHEN      XRNV = 'ReferenceNo'                          2
     C                   EVAL      FSSOK  = XRVERD
     C                   WHEN      XRNV = 'ReferenceType'                        2
     C                   EVAL      FSKODE = XRVERD
     C                   ENDSL                                                   2<-E
     C                   IF        ((FSSOK <> *BLANKS) AND (FSKODE <> *BLANKS))  2<-B
     C                   IF        FSKODE = 'C'                                   3<-B
     C                   EVAL      HERFA  = FSSOK
     C                   END                                                      3<-E
     C                   WRITE     FRISOKR
     C                   EVAL      FSSOK  = *BLANKS
     C                   EVAL      FSKODE = *BLANKS
     C                   END                                                     2<-E
     C     XMLKEY031     READE     XMLRCVF02                              33
     C                   ENDDO                                                  1<-E
      *
     C                   ENDSR
      *
      ***********************************************
     C     SRXML302      BEGSR
      ***********************************************
      *
      * INFOR. FRA <CustomsClearance>
      *
     C                   EVAL      X003_XRLEV1 = XRLEV1
     C                   EVAL      X003_XRLEV2 = XRLEV2
     C                   EVAL      X003_XRLEV3 = XRLEV3
     C                   EVAL      X003_XRLEV4 = 1
     C                   EVAL      X003_XRLEV5 = 0
      *
     C     XMLKEY03      CHAIN     XMLRCVF01                          33
     C                   DOW       *IN33 = *OFF                                 1<-B
     C                   SELECT                                                  2<-B
     C                   WHEN      XRNV = 'EntryNumber'                          2
     C                   EVAL      HEGN   = XRVERD
     C                   WHEN      XRNV = 'EntryPositionNumber'                  2
     C                   EVAL      HEPOS1 = XRVERD
     C                   ENDSL                                                   2<-E
      *
     C                   EVAL      X003_XRLEV4 = X003_XRLEV4 + 1
     C     XMLKEY03      CHAIN     XMLRCVF01                          33
     C                   ENDDO                                                  1<-E
      *
     C                   ENDSR
      *
      ***********************************************
     C     SRXML303      BEGSR
      ***********************************************
      * INFOR. FRA <ConsignmentGoodsItemSet>
      *
     C                   EVAL      X003_XRLEV1 = XRLEV1
     C                   EVAL      X003_XRLEV2 = XRLEV2
     C                   EVAL      X003_XRLEV3 = XRLEV3
     C                   EVAL      X003_XRLEV4 = 1
     C                   EVAL      X003_XRLEV5 = 0
      *
     C     XMLKEY03      CHAIN     XMLRCVF01                          33
     C                   DOW       *IN33 = *OFF                                 1<-B
     C                   SELECT                                                  2<-B
     C                   WHEN      XRNV = 'GoodsItem'                            2
     C                   EXSR      SRXML303A
     C                   ENDSL                                                   2<-E
      *
     C                   EVAL      X003_XRLEV4 = X003_XRLEV4 + 1
     C                   EVAL      X003_XRLEV5 = 0
     C     XMLKEY03      CHAIN     XMLRCVF01                          33
     C                   ENDDO                                                  1<-E
      *
     C                   ENDSR
      *
      ***********************************************
     C     SRXML303A     BEGSR
      ***********************************************
     C                   EVAL      HEVS1  = *BLANKS
     C                   EVAL      HEVS2  = *BLANKS
      *
     C                   EVAL      X003_XRLEV4 = XRLEV4
      *
     C     XMLKEY031     CHAIN     XMLRCVF02                          33
     C                   DOW       *IN33 = *OFF                                 1<-B
     C                   SELECT                                                  2<-B
     C                   WHEN      XRNV = 'GoodsDescription'                     2
     C                   SELECT                                                   3<-B
     C                   WHEN      HEVS1  = *BLANKS                               3
     C                   EVAL      HEVS1  = XRVERD
     C                   WHEN      HEVS122 = *BLANKS                              3
     C                   CAT       XRVERD:1      HEVS1
     C                   WHEN      HEVS2  = *BLANKS                               3
     C                   EVAL      HEVS2  = XRVERD
     C                   END                                                      3<-E
     C                   WHEN      XRNV = 'NumberOfPackages'                     2
     C                   WHEN      XRNV = 'PackageTypeCode'                      2
     C                   IF        HEVS1  = *BLANKS                               3<-B
     C                   EVAL      HEVS1  = XRVERD
     C                   ELSE                                                     3
     C                   EVAL      HEVS1  = %TRIM(XRVERD) + ' ' + %TRIM(HEVS1)
     C                   END                                                      3<-E
     C                   WHEN      XRNV = 'GrossWeight'                          2
     C                   ENDSL                                                   2<-E
      *
     C     XMLKEY031     READE     XMLRCVF02                              33
     C                   ENDDO                                                  1<-E
      *
     C                   ENDSR
      *
      ***********************************************
     C     SRXML304      BEGSR
      ***********************************************
     C                   EVAL      N3035  = *BLANKS
     C                   EVAL      N3039  = *BLANKS
     C                   EVAL      N1131  = *BLANKS
     C                   EVAL      N3055  = *BLANKS
     C                   EVAL      N3124A = *BLANKS
     C                   EVAL      N3124B = *BLANKS
     C                   EVAL      N3124C = *BLANKS
     C                   EVAL      N3124D = *BLANKS
     C                   EVAL      N3124E = *BLANKS
     C                   EVAL      N3036A = *BLANKS
     C                   EVAL      N3036B = *BLANKS
     C                   EVAL      N3036C = *BLANKS
     C                   EVAL      N3042A = *BLANKS
     C                   EVAL      N3042B = *BLANKS
     C                   EVAL      N3042C = *BLANKS
     C                   EVAL      N3164  = *BLANKS
     C                   EVAL      N3229  = *BLANKS
     C                   EVAL      N3251  = *BLANKS
     C                   EVAL      N3207  = *BLANKS
     C                   EVAL      NKN    = TRKNFA
      * INFOR. FRA <Party>
      *
     C                   EVAL      X003_XRLEV1 = XRLEV1
     C                   EVAL      X003_XRLEV2 = XRLEV2
     C                   EVAL      X003_XRLEV3 = XRLEV3
     C                   EVAL      X003_XRLEV4 = 0
     C                   EVAL      X003_XRLEV5 = 0
      *
      * HENT PARAMETER FRA <Party>
     C                   MOVE      *BLANKS       XAVSMOT           8
     C                   MOVE      *ZEROS        XKNR              8 0
     C     XMLKEY03      SETLL     XMLRCAF
     C     XMLKEY03      READE     XMLRCAF                                33
     C                   DOW       *IN33 = *OFF
     C                   SELECT
     C                   WHEN      ((XRATN='subClass') AND (XRATV='Consignor'))
     C                   MOVE      'AVSENDER'    XAVSMOT           8
     C                   EVAL      N3035  = 'CZ'
     C                   WHEN      ((XRATN='subClass') AND (XRATV='Consignee'))
     C                   MOVE      'MOTTAKER'    XAVSMOT           8
     C                   EVAL      N3035  = 'CN'
     C                   WHEN      ((XRATN='subClass') AND
     C                              (XRATV='TransportCompany'))
     C                   MOVE      'TR.PORTØ'    XAVSMOT           8
     C                   EVAL      N3035  = 'AF'
     C                   WHEN      ((XRATN='subClass') AND
     C                              (XRATV='DespatchParty'))
     C                   MOVE      'HENTEADR'    XAVSMOT           8
     C                   EVAL      N3035  = 'PW'
     C                   WHEN      ((XRATN='subClass') AND
     C                              (XRATV='DeliveryParty'))
     C                   MOVE      'LEVERADR'    XAVSMOT           8
     C                   EVAL      N3035  = 'DP'
     C                   WHEN      ((XRATN='subClass') AND
     C                              (XRATV='FreightPayer'))
     C                   MOVE      'FAKTMOTT'    XAVSMOT           8
     C                   EVAL      N3035  = 'PI'
     C                   WHEN      XRATN = 'partyId'
     C                   EVAL      N3039  = XRATV
     C                   IF        XAVSMOT <> 'TR.PORTØ'
     C                   MOVEL     XRATV         XTXT
     C                   EXSR      KNVTAL
     C                   Z-ADD     XTAL          XKNR              8 0
     C                   END
     C                   WHEN      XRATN = 'idType'
     C                   EVAL      N1131  = XRATV
     C                   ENDSL
     C     XMLKEY03      READE     XMLRCAF                                33
     C                   ENDDO
      *
     C                   SELECT
     C                   WHEN      XAVSMOT = 'AVSENDER'
     C                   MOVE      XKNR          HEKNS
     C                   WHEN      XAVSMOT = 'MOTTAKER'
     C                   MOVE      XKNR          HEKNK
     C                   WHEN      XAVSMOT = 'FAKTMOTT'
     C                   MOVE      XKNR          HEKNKF
     C                   ENDSL
      *
     C                   EVAL      X003_XRLEV4 = 1
     C                   EVAL      X003_XRLEV5 = 0
     C     XMLKEY03      CHAIN     XMLRCVF01                          33
     C                   DOW       *IN33 = *OFF                                 1<-B
     C                   SELECT                                                  2<-B
     C                   WHEN      XRNV = 'Name'                                 2
     C                   SELECT                                                   3<-B
     C                   WHEN      XAVSMOT = 'AVSENDER'                           3
     C                   EVAL      HENAS   = XRVERD
     C                   WHEN      XAVSMOT = 'MOTTAKER'                           3
     C                   EVAL      HENAK   = XRVERD
     C                   ENDSL                                                    3<-E
     C                   EVAL      N3036A  = XRVERD
     C                   WHEN      XRNV = 'Address'                              2
     C                   EXSR      SRXML304A
     C                   ENDSL                                                   2<-E
      *
     C                   EVAL      X003_XRLEV4 = X003_XRLEV4 + 1
     C     XMLKEY03      CHAIN     XMLRCVF01                          33
     C                   ENDDO                                                  1<-E
      *
     C                   IF        XAVSMOT <> *BLANKS
     C                   WRITE     NADSR
     C                   END
      *
     C                   ENDSR
      *
      ***********************************************
     C     SRXML304A     BEGSR
      ***********************************************
      *
     C                   EVAL      X003_XRLEV5 = 1
     C                   EVAL      X003_XRLEV6 = 0
      *
     C     XMLKEY031     CHAIN     XMLRCVF02                          33
     C                   DOW       *IN33 = *OFF                                 1<-B
     C                   SELECT                                                  2<-B
     C                   WHEN      XRNV = 'Street'                               2
     C                   SELECT                                                   3<-B
     C                   WHEN      XAVSMOT = 'AVSENDER'                           3
     C                   EVAL      HEADS1  = XRVERD
     C                   WHEN      XAVSMOT = 'MOTTAKER'                           3
     C                   EVAL      HEADK1  = XRVERD
     C                   ENDSL                                                    3<-E
     C                   EVAL      N3042A  = XRVERD
     C                   WHEN      XRNV = 'PostalCode'                           2
     C                   SELECT                                                   3<-B
     C                   WHEN      XAVSMOT = 'AVSENDER'                           3
     C                   EVAL      HEADS3  = XRVERD
     C                   EVAL      HESDF   = XRVERD
     C                   WHEN      XAVSMOT = 'MOTTAKER'                           3
     C                   EVAL      HEADK3  = XRVERD
     C                   EVAL      HESDT   = XRVERD
     C                   ENDSL                                                    3<-E
     C                   EVAL      N3251   = XRVERD
     C                   WHEN      XRNV = 'City'                                 2
     C                   SELECT                                                   3<-B
     C                   WHEN      XAVSMOT = 'AVSENDER'                           3
     C                   CAT       XRVERD:1      HEADS3
     C                   WHEN      XAVSMOT = 'MOTTAKER'                           3
     C                   CAT       XRVERD:1      HEADK3
     C                   ENDSL                                                    3<-E
     C                   EVAL      N3164   = XRVERD
     C                   WHEN      XRNV = 'CountryCode'                          2
     C                   EVAL      N3207   = XRVERD
     C                   SELECT                                                   3<-B
     C                   WHEN      XAVSMOT = 'AVSENDER'                           3
     C                   EVAL      HELKA   = XRVERD
     C                   WHEN      XAVSMOT = 'MOTTAKER'                           3
     C                   EVAL      HETRI   = XRVERD
     C                   ENDSL                                                    3<-E
     C                   ENDSL                                                   2<-E
      *
     C     XMLKEY031     READE     XMLRCVF01                              33
     C                   ENDDO                                                  1<-E
      *
     C                   ENDSR
      *
      ***********************************************
     C     SRXML305      BEGSR
      ***********************************************
      * INFOR. FRA <TransportLeg>
      *
     C                   EVAL      X003_XRLEV1 = XRLEV1
     C                   EVAL      X003_XRLEV2 = XRLEV2
     C                   EVAL      X003_XRLEV3 = XRLEV3
     C                   EVAL      X003_XRLEV4 = 1
     C                   EVAL      X003_XRLEV5 = 0
      *
     C     XMLKEY03      CHAIN     XMLRCVF01                          33
     C                   DOW       *IN33 = *OFF                                 1<-B
     C                   SELECT                                                  2<-B
     C                   WHEN      XRNV = 'TransportLegType'                     2
     C                   WHEN      XRNV = 'TransportLegId'                       2
     C                   WHEN      XRNV = 'Location'                             2
     C                   WHEN      XRNV = 'ModeOfTransport'                      2
     C                   MOVEL     XRVERD        HETRM
     C                   WHEN      XRNV = 'MeansOfTransport'                     2
     C                   MOVEL     XRVERD        HEHAWB
     C                   ENDSL
      *
     C                   EVAL      X003_XRLEV4 = X003_XRLEV4 + 1
     C     XMLKEY03      CHAIN     XMLRCVF01                          33
     C                   ENDDO                                                  1<-E
      *
     C                   ENDSR
      *
      ***********************************************
     C     SRXML306      BEGSR
      ***********************************************
      * INFOR. FRA <TermsOfDelivery>
      *
     C                   EVAL      X003_XRLEV1 = XRLEV1
     C                   EVAL      X003_XRLEV2 = XRLEV2
     C                   EVAL      X003_XRLEV3 = XRLEV3
     C                   EVAL      X003_XRLEV4 = 1
     C                   EVAL      X003_XRLEV5 = 0
      *
     C     XMLKEY03      CHAIN     XMLRCVF01                          33
     C                   DOW       *IN33 = *OFF                                 1<-B
     C                   SELECT                                                  2<-B
     C                   WHEN      XRNV = 'TODConditionCode'                     2
     C                   EVAL      HEFR   = XRVERD
     C     XMLKEY032     CHAIN     XMLRCAF                            33
     C  N33              IF        XRATN = 'codeListAgencyIdentifier'             3<-B
     C                   EVAL      HEFR   = XRATV
     C                   END                                                      3<-E
     C                   WHEN      XRNV = 'TODConditionCodeList'                 2
     C                   ENDSL                                                  2<-E
      *
     C                   EVAL      X003_XRLEV4 = X003_XRLEV4 + 1
     C     XMLKEY03      CHAIN     XMLRCVF01                          33
     C                   ENDDO                                                  1<-E
      *
     C                   ENDSR
      *
      ***********************************************
     C     SRXML307      BEGSR
      ***********************************************
      * INFOR. FRA <Service>
      *
     C                   ENDSR
      *
      ***********************************************
     C     SRXML308      BEGSR
      ***********************************************
      * INFOR. FRA <DateAndTimes>
      *
     C                   EVAL      X003_XRLEV1 = XRLEV1
     C                   EVAL      X003_XRLEV2 = XRLEV2
     C                   EVAL      X003_XRLEV3 = XRLEV3
     C                   EVAL      X003_XRLEV4 = 1
     C                   EVAL      X003_XRLEV5 = 0
      *
     C     XMLKEY03      CHAIN     XMLRCVF01                          33
     C                   DOW       *IN33 = *OFF                                 1<-B
     C                   SELECT                                                  2<-B
     C                   WHEN      XRNV = 'Year'                                 2
     C                   MOVEL     XRVERD        HEDTOPA
     C                   WHEN      XRNV = 'Month'                                2
     C                   MOVEL     XRVERD        HEDTOPM
     C                   WHEN      XRNV = 'Day'                                  2
     C                   MOVEL     XRVERD        HEDTOPD
     C                   ENDSL                                                  2<-E
      *
     C                   EVAL      X003_XRLEV4 = X003_XRLEV4 + 1
     C     XMLKEY03      CHAIN     XMLRCVF01                          33
     C                   ENDDO                                                  1<-E
      *
     C                   ENDSR
      *
      ***********************************************
     C     SROPDHEA      BEGSR
      ***********************************************
      *
     C                   IF        XNYOPD = 'J'
      *
     C                   IF        HESG = *BLANKS
     C                   MOVE      'AMT'         HESG
     C                   END
     C                   IF        HEDTOP = 0
     C                   MOVE      WDT           HEDTOP
     C                   END
     C                   MOVE      'G'           HEST
     C                   MOVE      'H'           HEUR
     C                   MOVE      'X'           HEKDFK
     C                   MOVE      'X'           HEKDFS
     C                   MOVE      'N'           TRMVA
     C                   MOVE      'N'           HEPK1
     C                   MOVE      'N'           HEPK2
     C                   MOVE      'N'           HEPK3
     C                   MOVE      'N'           HEPK4
     C                   MOVE      'N'           HEPK5
     C                   MOVE      'N'           HEPK6
     C                   MOVE      'N'           HEPK7
     C                   MOVE      'BC'          HEOT
     C                   WRITE     HEADFR
     C                   WRITE     DISPR
      *
     C                   ELSE
      *
     C                   UPDATE    HEADFR
      *
     C                   END
      *
     C                   ENDSR
      *
      ***********************************************
     C     SRFINNAVD     BEGSR
      ***********************************************
      *
     C                   Z-ADD     1             XXAVD             4 0
      *
     C                   ENDSR
      *
      ***********************************************
     C     KNVTAL        BEGSR
      ***********************************************
     C                   MOVE      *ZEROS        XTAL
     C     1             DO        15            XN                3 0          1<-B
     C                   IF        ((XT(XN) = *BLANKS) OR (XT(XN) = '.'))        2<-B
     C                   LEAVE
     C                   ENDIF                                                   2<-E
     C                   MULT      10            XTALH
     C                   MOVE      XT(XN)        XTALH
     C                   ENDDO                                                  1<-E
      *
     C                   IF        XT(XN) = '.'                                 1<-B
     C                   ADD       1             XN
     C                   MOVE      XT(XN)        XTALD1
     C                   ADD       1             XN
     C                   IF        XT(XN) <> ' '                                 2<-B
     C                   MOVE      XT(XN)        XTALD2
     C                   ADD       1             XN
     C                   IF        XT(XN) <> ' '                                  3<-B
     C                   MOVE      XT(XN)        XTALD3
     C                   END                                                      3<-E
     C                   END                                                     2<-E
     C                   END                                                    1<-E
      *
     C                   ENDSR
      *
      ***********************************************
     C     INIT          BEGSR
      ***********************************************
     C     *ENTRY        PLIST
     C                   PARM                    USN               9
      *
     C     HEADFK        KLIST
     C                   KFLD                    FSAVD
     C                   KFLD                    FSOPD
     C     FRISOL01K     KLIST
     C                   KFLD                    FSKODE
     C                   KFLD                    FSSOK
     C     XMLKEY01      KLIST
     C                   KFLD                    XRSN
     C                   KFLD                    X001_XRLEV1
     C                   KFLD                    X001_XRLEV2
     C                   KFLD                    X001_XRLEV3
     C                   KFLD                    X001_XRLEV4
     C                   KFLD                    X001_XRLEV5
     C                   KFLD                    X001_XRLEV6
     C                   KFLD                    X001_XRLEV7
     C                   KFLD                    X001_XRLEV8
     C                   KFLD                    X001_XRLEV9
     C     XMLKEY02      KLIST
     C                   KFLD                    XRSN
     C                   KFLD                    X002_XRLEV1
     C                   KFLD                    X002_XRLEV2
     C                   KFLD                    X002_XRLEV3
     C                   KFLD                    X002_XRLEV4
     C     XMLKEY03      KLIST
     C                   KFLD                    XRSN
     C                   KFLD                    X003_XRLEV1
     C                   KFLD                    X003_XRLEV2
     C                   KFLD                    X003_XRLEV3
     C                   KFLD                    X003_XRLEV4
     C                   KFLD                    X003_XRLEV5
     C     XMLKEY031     KLIST
     C                   KFLD                    XRSN
     C                   KFLD                    X003_XRLEV1
     C                   KFLD                    X003_XRLEV2
     C                   KFLD                    X003_XRLEV3
     C                   KFLD                    X003_XRLEV4
     C     XMLKEY032     KLIST
     C                   KFLD                    XRSN
     C                   KFLD                    X003_XRLEV1
     C                   KFLD                    X003_XRLEV2
     C                   KFLD                    X003_XRLEV3
     C                   KFLD                    X003_XRLEV4
     C                   KFLD                    X003_XRLEV5
     C                   KFLD                    X003_XRLEV6
     C     XMLKEY033     KLIST
     C                   KFLD                    XRSN
     C                   KFLD                    X003_XRLEV1
     C                   KFLD                    X003_XRLEV2
     C                   KFLD                    X003_XRLEV3
     C                   KFLD                    X003_XRLEV4
     C                   KFLD                    X003_XRLEV5
     C     XMLKEYAVD     KLIST
     C                   KFLD                    XRSN
     C                   KFLD                    XRLEV1
     C                   KFLD                    XRLEV2
     C                   KFLD                    XRLEV3
     C                   KFLD                    XRLEV4
     C                   KFLD                    XRLEV5
     C                   KFLD                    XRLEV6
      *
     C     *LIKE         DEFINE    XRLEV1        X001_XRLEV1
     C     *LIKE         DEFINE    XRLEV2        X001_XRLEV2
     C     *LIKE         DEFINE    XRLEV3        X001_XRLEV3
     C     *LIKE         DEFINE    XRLEV4        X001_XRLEV4
     C     *LIKE         DEFINE    XRLEV5        X001_XRLEV5
     C     *LIKE         DEFINE    XRLEV6        X001_XRLEV6
     C     *LIKE         DEFINE    XRLEV7        X001_XRLEV7
     C     *LIKE         DEFINE    XRLEV8        X001_XRLEV8
     C     *LIKE         DEFINE    XRLEV9        X001_XRLEV9
     C     *LIKE         DEFINE    XRLEV1        X002_XRLEV1
     C     *LIKE         DEFINE    XRLEV2        X002_XRLEV2
     C     *LIKE         DEFINE    XRLEV3        X002_XRLEV3
     C     *LIKE         DEFINE    XRLEV4        X002_XRLEV4
     C     *LIKE         DEFINE    XRLEV1        X003_XRLEV1
     C     *LIKE         DEFINE    XRLEV2        X003_XRLEV2
     C     *LIKE         DEFINE    XRLEV3        X003_XRLEV3
     C     *LIKE         DEFINE    XRLEV4        X003_XRLEV4
     C     *LIKE         DEFINE    XRLEV5        X003_XRLEV5
     C     *LIKE         DEFINE    XRLEV6        X003_XRLEV6
     C     *LIKE         DEFINE    XRNV          X001_XRNV
      *
     C                   MOVE      USN           XRSN
      *
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
      *
     C                   ENDSR
      *
