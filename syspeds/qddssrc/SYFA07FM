********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr *Sek Sig Vers Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
P8068 * 01 TH  PTF8 NY HELPFUNKSJON  KUN DDS
********************************************************************
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
S01  A*                                     HLPDOC('SYFA070010' 'SYFA07' 'SY400-
S01  A*                                     LO/MAINT')
N01  A                                      HLPPNLGRP(HLPSCR SYFA07PNL)
N01  A                                      HLPTITLE('VEDLIKEHOLD AV BRUTTO GEB-
N01  A                                      YR PRISTABELL')
     A                                      HELP
     A                                      ALTHELP
     A          R BILDE01
     A                                      CF03(03 'Avslutt program')
     A                                  1  2'SYSPED/SYFA07'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(50 DIV1478 SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                  2 70TIME
     A                                 10  2MSGCON(20 DIV1479 SYMSGF)
     A            WSTNR          1A  B 10 25DSPATR(UL)
     A  99                                  DSPATR(RI)
     A                                      COLOR(GRN)
     A                                 23  2MSGCON(18 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A  99                             24  2MSGCON(78 SPT0016 SYMSGF)
     A                                      COLOR(WHT)
     A N99                             24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  5  3VALUES(' ' '2' '4')
     A            PRGEBY         3A  O  5  7TEXT('GEBYRKODE')
     A            PRINT          5Y 0O  5 15TEXT('INTERVAL-TIL OG MED KG')
     A                                      EDTCDE(4)
     A            PRBELØ        10Y 3O  5 27TEXT('BELØP')
     A                                      EDTCDE(4)
     A            PRANT          5Y 0O  5 40TEXT('PRIS PR KG')
     A                                      EDTCDE(4)
     A            PRKOLL         1   O  5 54TEXT('ANNEN ENHET')
     A            PRTEGN         1A  O  5 62TEXT('+ / -')
     A            PRJUST        10Y 3O  5 67TEXT('JUSTERES MED KR')
     A                                      EDTCDE(4)
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0017)
     A                                      SFLPAG(0016)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
S01  A*         H                           HLPDOC('SYFA07010' 'SYFA07' 'SY400L-
S01  A*                                     O/MAINT')
N01  A          H                           HLPPNLGRP(SYFA07010 SYFA07PNL)
     A                                      HLPARA(01 001 20 080)
     A            ZRECNO         4S 0O  3 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYSPED/SYFA07'
     A                                      COLOR(BLU)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                  2 70TIME
     A                                  1 19MSGCON(50 DIV1478 SYMSGF)
     A                                      COLOR(WHT)
     A                                  2 19MSGCON(20 DIV1479 SYMSGF)
     A                                      COLOR(WHT)
     A            WSTNR          1   O  2 41COLOR(WHT)
     A                                  3  2MSGCON(30 DIV0016 SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 34MSGCON(30 DIV0017 SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(78 DIV1480 SYMSGF)
     A                                      COLOR(WHT)
     A          R BUNN
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF03(03 'Avslutt program')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                      CF12(12 'FORRIGE')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
S01  A*         H                           HLPDOC('SYFA07999' 'SYFA07' 'SY400L-
S01  A*                                     O/MAINT')
N01  A          H                           HLPPNLGRP(SYFA07999 SYFA07PNL)
     A                                      HLPARA(01 001 04 080)
     A                                  2  2MSGCON(18 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A                                  3  2MSGCON(18 DIV0021 SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 22MSGCON(18 DIV0022 SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 42MSGCON(18 DIV0023 SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 62MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A  98                              4  2MSGCON(78 DIV0012 SYMSGF)
     A                                      COLOR(WHT)
     A N98                              4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A          R VARELI
     A N40                                  CF04(04 'SØKING')
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
S01  A*         H                           HLPDOC('SYFA07015' 'SYFA07' 'SY400L-
S01  A*                                     O/MAINT')
N01  A          H                           HLPPNLGRP(SYFA07015 SYFA07PNL)
     A                                      HLPARA(02 001 02 026)
S01  A*         H                           HLPDOC('SYFA07020' 'SYFA07' 'SY400L-
S01  A*                                     O/MAINT')
N01  A          H                           HLPPNLGRP(SYFA07020 SYFA07PNL)
     A                                      HLPARA(02 027 02 039)
S01  A*         H                           HLPDOC('SYFA07025' 'SYFA07' 'SY400L-
S01  A*                                     O/MAINT')
N01  A          H                           HLPPNLGRP(SYFA07025 SYFA07PNL)
     A                                      HLPARA(02 040 02 053)
S01  A*         H                           HLPDOC('SYFA07030' 'SYFA07' 'SY400L-
S01  A*                                     O/MAINT')
N01  A          H                           HLPPNLGRP(SYFA07030 SYFA07PNL)
     A                                      HLPARA(02 054 02 061)
S01  A*         H                           HLPDOC('SYFA07035' 'SYFA07' 'SY400L-
S01  A*                                     O/MAINT')
N01  A          H                           HLPPNLGRP(SYFA07035 SYFA07PNL)
     A                                      HLPARA(02 062 02 077)
S01  A*         H                           HLPDOC('SYFA07040' 'SYFA07' 'SY400L-
S01  A*                                     O/MAINT')
N01  A          H                           HLPPNLGRP(SYFA07040 SYFA07PNL)
     A                                      HLPARA(02 067 02 080)
     A                                  1  5MSGCON(74 DIV1480 SYMSGF)
     A                                      COLOR(WHT)
     A            WSGEBY         3A  B  2  7TEXT('GEBYRKODE')
     A                                      DSPATR(UL)
     A  40                                  DSPATR(PR)
     A N40N30
     AO 90                                  DSPATR(PC)
     A  90                                  DSPATR(RI)
     A  40                                  COLOR(WHT)
     A N40                                  COLOR(GRN)
     A            WSINT          5Y 0B  2 15TEXT('INTERVAL-TIL OG MED KG')
     A                                      DSPATR(UL)
     A  40                                  DSPATR(PR)
     A  89                                  DSPATR(PC)
     A  89                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A  40                                  COLOR(WHT)
     A N40                                  COLOR(GRN)
     A            WSBELØ        10Y 3B  2 27TEXT('BELØP')
     A                                      DSPATR(UL)
     A  40                                  DSPATR(PC)
     A                                      EDTCDE(4)
     A                                      COLOR(GRN)
     A            WSANT          5Y 0B  2 40TEXT('PRIS PR KG')
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(GRN)
     A            WSKOLL         1A  B  2 54TEXT('ANNEN ENHET')
     A                                      DSPATR(UL)
     A                                      COLOR(GRN)
     A                                      VALUES(' ' 'X' 'B' 'L' 'D' '0' '1' -
     A                                      '2' '3' '4' '5' '6' '7' '8' '9' 'V'-
N02  A                                       'M' 'F' 'T' 'E' 'I')
S02  A*                                      'M' 'F')
     A            WSTEGN         1A  B  2 62TEXT('+ / -')
     A                                      DSPATR(UL)
     A                                      COLOR(GRN)
     A                                      VALUES(' ' '+' '-' 'M' 'N')
     A            WSJUST        10Y 3B  2 67TEXT('JUSTERES MED KR')
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(GRN)
     A                                  3  2MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  4  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                              4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A          R VAREL2
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
S01  A*         H                           HLPDOC('SYFA07999' 'SYFA07' 'SY400L-
S01  A*                                     O/MAINT')
N01  A          H                           HLPPNLGRP(SYFA07999 SYFA07PNL)
     A                                      HLPARA(01 001 02 080)
     A                                  2  2MSGCON(12 DIV0026 SYMSGF)
     A            W2GEBY         3A  I  2 16DSPATR(UL)
     A                                      DSPATR(PC)
     A            W2INT          5Y 0I  2 21DSPATR(UL)
     A                                  3  2MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A          R VAREL3
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
S01  A*         H                           HLPDOC('SYFA07999' 'SYFA07' 'SY400L-
S01  A*                                     O/MAINT')
N01  A          H                           HLPPNLGRP(SYFA07999 SYFA07PNL)
     A                                      HLPARA(01 001 02 080)
     A                                  1  5MSGCON(74 DIV1480 SYMSGF)
     A                                      COLOR(WHT)
     A            PRGEBY         3A  O  2  7TEXT('GEBYRKODE')
     A                                      DSPATR(UL)
     A                                      DSPATR(PC)
     A                                      COLOR(WHT)
     A            PRINT          5Y 0O  2 15TEXT('INTERVAL-TIL OG MED KG')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            PRBELØ        10Y 3O  2 27TEXT('BELØP')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            PRANT          5Y 0O  2 40TEXT('PRIS PR KG')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            PRKOLL         1A  O  2 54TEXT('ANNEN ENHET')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            PRTEGN         1A  O  2 62TEXT('+ / -')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            PRJUST        10Y 3O  2 67TEXT('JUSTERES MED KR')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  3  2MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A          R NYVARE
     A                                      CF03(03 'Avslutt program')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                      CF12(12 'FORRIGE')
S01  A*         H                           HLPDOC('SYFA07010' 'SYFA07' 'SY400L-
S01  A*                                     O/MAINT')
N01  A          H                           HLPPNLGRP(SYFA07010 SYFA07PNL)
     A                                      HLPARA(01 001 20 080)
     A                                  1  2'SYSPED/SYFA07'
     A                                      COLOR(BLU)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                  2 70TIME
     A                                  1 19MSGCON(50 DIV1478 SYMSGF)
     A                                      COLOR(WHT)
     A                                  2 19MSGCON(20 DIV1479 SYMSGF)
     A                                      COLOR(WHT)
     A            WSTNR          1   O  2 43COLOR(WHT)
     A                                  3  2MSGCON(30 DIV0016 SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 34MSGCON(30 DIV0017 SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(78 DIV1480 SYMSGF)
     A                                      COLOR(WHT)
     A                                 22  2MSGCON(18 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23  2MSGCON(18 DIV0021 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(18 DIV0022 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 42MSGCON(18 DIV0023 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 62MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A  98                             24  2MSGCON(78 DIV0012 SYMSGF)
     A                                      COLOR(WHT)
     A N98                             24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
