********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
12XXX * 01 sh  PTF12 kan legge inn egne gyldige verdier
********************************************************************
      ***********************************************************
      *** PROGRAM FOR Å VEDLIKEHOLDE/SØKE ETTER FRIE SØKEVEIER  ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01 02 04 07 09-11 13-24          ***
      *** LEDIGE INDIKATORER 27-29 31 32 34-39 41-87 95-96    ***
      ***********************************************************
      *
n01  FFIRM      IF   E           K DISK
     FKODFRI    UF A E           K DISK
     FKOFAST    IF   E           K DISK
     FSYFA65FM  CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D                 DS
     D  KFSOTX                 1     30
     D  KOSFN7                 1      7
     C*
     C                   EXSR      INIT
     C*
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
     C                   MOVE      W2SOKO        Y2SOKO            3
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     KODSF1        SETLL     KODFRI
     C     START1        TAG
     C                   READ      KODFRI                                 94
     C  N94              UPDATE    KODFRIR
      *
     C     START2        TAG
      *
     C  N94              DO
     C                   DO        14
     C                   ADD       1             ZRECNO
     C                   WRITE     SUBFIL
     C                   READ      KODFRI                                 94
     C  N94              UPDATE    KODFRIR
     C  N94              MOVE      KFSOKO        YFSOKO
     C  N94              END
     C                   END
     C*
     C                   Z-ADD     ZRECNO        YRECNO            5 0
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     3             POSNBR
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYWRK
     C                   ELSE
     C                   WRITE     SUBCTL
     C                   EXFMT     BUNN
     C                   END
     C*
     C***
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C                   Z-ADD     WSSUBN        XRECNO            4 0
     C     XRECNO        IFEQ      0
     C                   Z-ADD     ZRECNO        XRECNO            4 0
     C                   END
     C***
     C*
     C                   SETOFF                                       9899
     C*
     C*** AVSLUTT
     C   03              GOTO      SLUTT
     C*
     C*** FRISK OPP
     C   05              GOTO      START
     C*
     C*** LAGE NY
     C   06              DO
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     8             POSNBR
     C                   MOVE      *BLANKS       WSSOKO
     C                   GOTO      START5
     C                   END
     C*** SØK FRA
     C   08              GOTO      START6
     C*
     C*** BLA VIDERE
     C   25              DO
     C                   SETOFF                                       25
     C                   Z-ADD     YRECNO        ZRECNO
     C  N94KODSF3        SETLL     KODFRI
     C  N94              READ      KODFRI                                 94
     C  N94              UPDATE    KODFRIR
     C                   GOTO      START2
     C                   END
     C*
     C     ZRECNO        IFNE      0
     C                   EXSR      HENT
     C                   END
     C   98XRECNO        IFNE      0
     C                   Z-ADD     XRECNO        ZRECNO
     C                   END
     C                   GOTO      START3
     C*
     C*** LAGE NY
     C     START5        TAG
     C                   EXFMT     VARELI
     C                   SETOFF                                       309089
     C                   SETOFF                                       8887
     C*** SØKING KODER
     C   04              DO
     C                   EXSR      SØKDTA
      * linjer flyttet til SR
     C                   GOTO      START5
     C                   END
     C*
     C   12              EXSR      MOVB2
     C  N12              EXSR      TESTB1
     C  N12
     CAN 30              GOTO      START5
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     3             POSNBR
     C                   GOTO      START3
     C*
     C*** SØK FRA
     C     START6        TAG
     C                   EXFMT     VAREL2
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     3             POSNBR
     C   12              GOTO      START3
     C                   GOTO      START
     C*
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
     C*
     C***********************************************
     C     SØKDTA        BEGSR
     C***********************************************
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
      * Gyldig Dokkode
     c     POSNBR        IFLT      65
     C                   EVAL      XKFTYP  = 'FSDOKK'
     C                   MOVE      'N'           UUPDAT            1
     C                   MOVE      *BLANKS       XKFKOD            5
     C                   CALL      'SYFAKDA'
     C                   PARM                    XKFTYP           10
     C                   PARM                    UUPDAT
     C                   PARM                    XKFKOD
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     54            POSNBR
     C     XKFKOD        IFNE      *BLANKS
     C                   MOVEL     XKFKOD        WSSODK
     C                   END
      * Gyldig T&T-URL
     C                   ELSE
     C                   EVAL      XKFTYP  = 'TRACKPREFI'
     C                   MOVE      'N'           UUPDAT            1
     C                   MOVE      *BLANKS       XKFKOD            5
     C                   CALL      'SYFAKDA'
     C                   PARM                    XKFTYP
     C                   PARM                    UUPDAT
     C                   PARM                    XKFKOD
     C     XKFKOD        IFNE      *BLANKS
     C                   MOVEL     XKFKOD        WSSTTU
     C                   END
     C                   END
     c                   endsr
     C***********************************************
     C     TESTB1        BEGSR
     C***********************************************
     C  N40              DO
     C     WSSOKO        COMP      *BLANKS                                90
     C   90              MOVE      'SHU0136'     MSGNR
     C  N90WSSOKO        CHAIN     KODFRI                             33
     C  N90
     CANN33              DO
     C                   UPDATE    KODFRIR
     C                   SETON                                        90
     C                   MOVE      'SHU0137'     MSGNR
     C                   END
     C   90              Z-ADD     8             POSNBR
     C   90              SETON                                        30
     C                   END
     C*
     C     WSSOTX        COMP      *BLANKS                                89
     C   89
     CANN30              MOVE      'SHU0138'     MSGNR
     C   89
     CANN30              Z-ADD     20            POSNBR
     C   89              SETON                                        30
      * Gyldig Track&Trace kode..
     c     *IN30         Ifeq      *OFF
     c     WSSTTU        ANDNE     *BLANKS
     C     KOFASTK       CHAIN     KOFAST                             87
     c     *IN87         Ifeq      *ON
     C                   MOVE      'JOV1334'     MSGNR
     C                   Z-ADD     68            POSNBR
     C                   MOVE      *ON           *IN30
     c                   end
     c                   end
     C*
     C   30              GOTO      SLTB
     C*
     C*** OPPDATERE PÅ REGISTER OG SUBFIL
     C     WSSOKO        CHAIN     KODFRI                             33
     C                   EXSR      MOVWSF
     C  N33              UPDATE    KODFRIR
     C   33              WRITE     KODFRIR
     C   40              UPDATE    SUBFIL
     C  N40              ADD       1             YRECNO
     C  N40              Z-ADD     YRECNO        ZRECNO
     C  N40              WRITE     SUBFIL
     C                   WRITE     SUBCTL
     C                   EXSR      MOVB2
     C*
     C     SLTB          ENDSR
     C*
     C***********************************************
     C     HENT          BEGSR
     C***********************************************
     C                   Z-ADD     0             LINNBR
     C                   SETOFF                                       919299
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C   98              DO
     C   99              SETOFF                                       98
     C                   Z-ADD     1             LINNBR
     C                   GOTO      SLHENT
     C                   END
     C     ZRECNO        DIV       14            LINNBR
     C                   MVR                     XLIN              2 0
     C     XLIN          IFEQ      0
     C                   Z-ADD     14            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C  N98WSNR          IFEQ      '1'
     C  N97              SETON                                        98
     C   98              GOTO      SLHENT
     C                   MOVE      KFSOKO        UUKODE
     C                   SETON                                        LR
     C                   RETURN
     C                   END
     C     WSNR          IFEQ      '2'                                          ENDRE
     C                   MOVE      ' '           WSNR
     C     KOSFN7        COMP      '*DELETE'                              98
     C   98              GOTO      SLHENT
     C                   EXSR      CHGSF
     C   12              GOTO      SLHENT
     C                   END
n01  C     WSNR          IFEQ      '9'                                          ENDRE
     C                   MOVE      ' '           WSNR
     C     KOSFN7        COMP      '*DELETE'                              98
     C   98              GOTO      SLHENT
     C                   EXSR      GYLDIG
     C   12              GOTO      SLHENT
     C                   END
     C     WSNR          IFEQ      '4'                                          SLETTE
     C                   MOVE      ' '           WSNR
     C     KOSFN7        COMP      '*DELETE'                              98
     C   98              GOTO      SLHENT
     C                   EXSR      DLTSF
     C   12              GOTO      SLHENT
     C                   END
     C                   END
     C*
     C     SLHENT        TAG
     C                   ADD       5             LINNBR
     C                   Z-ADD     3             POSNBR
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     DLTSF         BEGSR
     C***********************************************
     C                   SETON                                        99
     C                   MOVE      KFSOKO        WSSOKO
     C                   EXFMT     VAREL3
     C  N12              DO
     C     WSSOKO        CHAIN     KODFRI                             33
     C  N33              DELETE    KODFRIR
     C                   EXSR      MOVB1
     C                   END
     C     XLIN          IFEQ      0
     C                   Z-ADD     14            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C                   UPDATE    SUBFIL
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     CHGSF         BEGSR
     C***********************************************
     C                   EXSR      MOVSFW
     C                   SETON                                        99
     C     STCHG         TAG
     C                   EXFMT     VARELI
     C                   SETOFF                                       309089
     C   04              DO
      * Sr opprettet / linjer her slettet
     C                   EXSR      SØKDTA
     C                   GOTO      STCHG
     C                   END
     C*
     C   12              DO
     C                   UPDATE    SUBFIL
     C                   EXSR      MOVB2
     C                   END
     C  N12              EXSR      TESTB1
     C  N12
     CAN 30              GOTO      STCHG
     C     XLIN          IFEQ      0
     C                   Z-ADD     14            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVB1         BEGSR
     C***********************************************
     C                   MOVE      *BLANKS       KFSOKO
     C                   MOVE      *BLANKS       KFSOTX
     C                   MOVEL     '*DELETE'     KOSFN7
     C                   MOVE      *BLANKS       KFSODK
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVB2         BEGSR
     C***********************************************
     C                   SETOFF                                       40
     C                   MOVE      *BLANKS       WSSOKO
     C                   MOVE      *BLANKS       WSSOTX
     C                   MOVE      *BLANKS       WSSODK
     C                   MOVE      *BLANKS       WSSTTU
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVSFW        BEGSR
     C***********************************************
     C                   MOVE      KFSOKO        WSSOKO
     C                   MOVE      KFSOTX        WSSOTX
     C                   MOVE      KFSODK        WSSODK
     C                   MOVE      KFSTTU        WSSTTU
     C                   SETON                                        40
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     20            POSNBR
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVWSF        BEGSR
     C***********************************************
     C   33              DO
     C                   MOVE      WSSOKO        KFSOKO
     C                   END
     C*
     C                   MOVE      WSSOTX        KFSOTX
     C                   MOVE      WSSODK        KFSODK
     C                   MOVE      WSSTTU        KFSTTU
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     *ENTRY        PLIST
     C                   PARM      UUKODE        UUKODE            3
     C                   PARM                    UUPLUK            1
     C     UUPLUK        COMP      'J'                                    97
     C     KODSF1        KLIST
     C                   KFLD                    W2SOKO
     C     KODSF2        KLIST
     C                   KFLD                    KFSOKO
     C     KODSF3        KLIST
     C                   KFLD                    YFSOKO            3
     C     KOFASTK       klist
     C                   kfld                    XKFTYP2          10
     C                   kfld                    WSSTTU
     C                   EVAL      XKFTYP2  = 'TRACKPREFI'
     C*
     C*
     C                   ENDSR
     C*
n01  C***********************************************
n01  C     GYLDIG        BEGSR
n01  C***********************************************
n01  C*
n01  c                   read      firm                                   33
n01  c                   MOVE      'V'           UMODE             1
n01  c                   z-add     0             cokunr            8 0
n01  c                   z-add     0             coavd             4 0
n01  C                   CALL      'VCUNDO2R'
n01  C                   PARM                    FIFIRM
n01  C                   PARM                    COKUNR
     C                   PARM                    COAVD
n01  C                   PARM                    KFSOKO
n01  C                   PARM                    DUM              15
n01  C                   PARM                    UMODE
n01  c                   move      *blanks       wsnr
n01  C                   UPDATE    SUBFIL
n01  c                   endsr
