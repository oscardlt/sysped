********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 SH  PTF10 flyttet username     ...
10XXX * 02 jov PTF10 se IFSfil via browser ..
10XXX * 03 TH  PTF10 Sette J både for utgående og inngående
10XXX * 04 YBC PTF10 LES IFS-FILER VIA LINKE I SIFS
10XXX * 05 SH  PTF10 hopp ut ved fler    enn   9998 records
10XXX * 06 YBC PTF10 LES IFS-FILER VIA LINKE I SIFS
10181 * 07 CB  PTF10 Rettelse/Tillegg for endring 6
10181 * 08 sh  PTF10 4 = slett
11XXX * 09 CB  PTF11 Vis edifact fra *IFS med CR/LF
11XXX * 10 SH  PTF11 Vis ifs også ved option 7
11XXX * 11 SH  PTF11 reset subfil
11XXX * 12 JOV PTF11 Ved langt filnavn og opt W - forkort pga maxlengde i PCO/SYGE39CL
11XXX * 13 CB  PTF11 Byttet avsender/mottakernavn ved S='+'
      *%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
12145 * 14 SH  PTF12 sende edi til seg selv av fil
12145 * 15 JOV PTF12 Søk på Utveksl.ref endret fra begynner på .... til Inneholder ...
12145 * 16 JOV PTF12 Vis også egen EDI-part i subfila
12145 * 17 sh  PTF12 Vise meldingstype og selektere
12220 * 18 YBC PTF12 Vise også pdf ved opsjon W
12227 * 19 YBC PTF12 Vise også Json ved opsjon W
12XXX * 20 YBC PTF12 Opsjon W ved sending of SIFS<>*blanks
12XXX * 21 SH  PTF12 https
********************************************************************
/* *********************************************************************   */
/*    *  * RETTINGER I DETTE PROGRAM:                                      */
/* ********************************************************************    */
     FEDI40D    CF   E             WORKSTN
     F                                     SFILE(EDI40DB:ZRECNO)
     F                                     INFDS(FBAREA)
     FEDIS1     IF   E           K DISK
     F                                     RENAME(EDISR:EDISR1)
     FEDIS      UF   E           K DISK
N17  FEDIM6     IF   E           K DISK
N17  FEUNH1     IF   E           K DISK
     FEDII      IF   E           K DISK
     FEDISS     O  A E             DISK
     FEDISX     IF   E           K DISK
N01  FFIRMARC   IF   E           K DISK
N01  FARUSERF   IF   E           K DISK
N15  D SOKVAR          s             35    varying
S15  D*ZA              S              1    DIM(14)
     D ZB              S              1    DIM(35)
N12   *
N12  D TMPFILE         S            256    VARYING
N12  D TMPLEN          S              3  0
N12   *
     D                 DS
     D  ZDTTM                  1     14  0
     D  ZDTT                   1      8  0
     D  ZTMT                   9     14  0
     D                 DS
     D  SDTTM                  1     14  0
     D  SDT                    1      8  0
     D  STM                    9     14  0
     D                 DS
     D  WDT                    1      8
     D  WÅ                     1      4  0
     D  WM                     5      6  0
     D  WD                     7      8  0
     D                 DS
     D  WTID                   1      6  0
     D  WTIDH                  1      2  0
     D  WTIDM                  3      4  0
     D  WTIDS                  5      6  0
      * KOPIER MEMBER TIL LØSNING-1 REMOTERING..
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
      * Program status data strukur
e01  D                SDS
n01  D   UserName            254    263
s01  D*  UserName            354    363
      *//////////////
      *  Hodelogikk /
      *//////////////
     C                   EXSR      SRA
     C                   EXSR      SRB
     C                   MOVE      '1'           *INLR
     C                   RETURN
      *////////////////////////////////////////////////////////////
      *  Initiering                                           SRA /
      *////////////////////////////////////////////////////////////
     C     SRA           BEGSR
      *___________________
      * Definiere Workfelt
      *"""""""""""""""""""
     C     *LIKE         DEFINE    *IN03         WIN03
     C     *LIKE         DEFINE    *IN30         WIN30
     C     *LIKE         DEFINE    INID          WINID
N16  C     *LIKE         DEFINE    INID          WEGID
     C     *LIKE         DEFINE    S0004         W0004
S15  C*    *LIKE         DEFINE    S0020         W0020
     C                   Z-ADD     0             ZAX               2 0
     C                   Z-ADD     0             WAX               2 0
      *__________________
      * Definiere Nøkkler
      *""""""""""""""""""
     C     KEYA          KLIST
     C                   KFLD                    ZDTF
     C                   KFLD                    ZTMF
      *_____________
      * Init av felt
      *"""""""""""""
     C* Hente dagens dato og klokkeslett.
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVEL     WDT           ZDTF
     C                   MOVEL     WDT           ZDTT
     C                   Z-ADD     000000        ZTMF
     C                   Z-ADD     235959        ZTMT
     C                   MOVE      'J'           ZJNS
S03  C*                  MOVE      'N'           ZJNR
N03  C                   MOVE      'J'           ZJNR
     C                   MOVEL     '*ALL'        Z0020
     C                   MOVEL     '*ALL'        Z0004
     C                   Z-ADD     0             WDTS              8 0
     C                   Z-ADD     0             WTMS              6 0
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     42            POSNBR
      *__________________
      * Output parametrer
      *""""""""""""""""""
     C     PLISTB        PLIST
     C                   PARM      SSN           SSN
     C                   PARM      S0020         S0020
     C                   PARM      S0036         S0036
     C                   PARM      SST           SST
     C                   PARM      WDTS          WDTS
     C                   PARM      WTMS          WTMS
     C     *IN03         PARM                    WIN03
     C     PLISTC        PLIST
     C                   PARM      SSN           SSN
     C                   PARM      SLIB          SLIB
     C                   PARM      SFILE         SFILE
     C                   PARM      SMBR          SMBR
     C                   PARM      INNETW        INNETW
     C                   PARM      WINID         WINID
     C     *IN30         PARM                    WIN30
     C     PLISTD        PLIST
     C                   PARM      SLIB          SLIB
     C                   PARM      SFILE         SFILE
     C                   PARM      SMBR          SMBR
     C     *IN30         PARM                    WIN30
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Bearbeide Formater                                   SRB /
      *////////////////////////////////////////////////////////////
     C     SRB           BEGSR
      *___________
      * Startbilde
      *"""""""""""
     C     SRB1          TAG
     C  N30              MOVE      'SY00000'     ZMSGNO
     C                   WRITE     EDI40DØ
     C                   EXFMT     EDI40DA
     C                   MOVEA     '0000000'     *IN(30)
     C     *IN03         CABEQ     '1'           SRB9
     C     *IN02         IFEQ      '1'
     C                   CALL      'EDI40R2'
     C                   GOTO      SRB1
     C                   END
      * Teste dato fra:
     C                   MOVEL     ZDTF          WDT
     C     WM            COMP      1                                    30
     C  N30WM            COMP      12                                 30
     C  N30WD            COMP      1                                    30
     C  N30WD            COMP      31                                 30
     C   30              MOVE      'SPD0001'     ZMSGNO
     C   30              MOVEA     '1100000'     *IN(30)
     C   30              Z-ADD     5             LINNBR
     C   30              GOTO      SRB1
      * Teste dato til:
     C                   MOVEL     ZDTT          WDT
     C     WM            COMP      1                                    30
     C  N30WM            COMP      12                                 30
     C  N30WD            COMP      1                                    30
     C  N30WD            COMP      31                                 30
     C   30              MOVE      'SPD0001'     ZMSGNO
     C   30              MOVEA     '1001000'     *IN(30)
     C   30              Z-ADD     8             LINNBR
     C   30              GOTO      SRB1
      * Fra dato større en til dato.
     C     ZDTF          IFGT      ZDTT
     C                   MOVE      'SPD0002'     ZMSGNO
     C                   MOVEA     '1101000'     *IN(30)
     C                   Z-ADD     5             LINNBR
     C                   GOTO      SRB1
     C                   END
      * Teste tid fra:
     C                   Z-ADD     ZTMF          WTID
     C  N30WTIDH         COMP      24                                 30
     C  N30WTIDM         COMP      60                                 30
     C  N30WTIDS         COMP      60                                 30
     C   30              MOVE      'SPK0006'     ZMSGNO
     C   30              MOVEA     '1010000'     *IN(30)
     C   30              Z-ADD     6             LINNBR
     C   30              GOTO      SRB1
      * Teste tid til:
     C                   Z-ADD     ZTMT          WTID
     C  N30WTIDH         COMP      24                                 30
     C  N30WTIDM         COMP      60                                 30
     C  N30WTIDS         COMP      60                                 30
     C   30              MOVE      'SPK0006'     ZMSGNO
     C   30              MOVEA     '1000100'     *IN(30)
     C   30              Z-ADD     9             LINNBR
     C   30              GOTO      SRB1
      * Klokkeslett fra får ikke vare større en klokkeslett til.
     C     ZDTF          IFEQ      ZDTT
     C     ZTMF          ANDGT     ZTMT
     C                   MOVE      'SPK0007'     ZMSGNO
     C                   MOVEA     '1010100'     *IN(30)
     C                   Z-ADD     6             LINNBR
     C                   GOTO      SRB1
     C                   END
      * En må sette J på minimum 1 av sendn/mottak.
     C     ZJNS          IFEQ      'N'
     C     ZJNR          ANDEQ     'N'
     C                   MOVE      'SPS0005'     ZMSGNO
     C                   MOVEA     '1000011'     *IN(30)
     C                   Z-ADD     11            LINNBR
     C                   GOTO      SRB1
     C                   END
      * Ordne stopper for søkebegrepp.
S15   *
S15  C*                  MOVEA     Z0020         ZA
S15  C*    ZA(1)         IFNE      '*'
S15  C*    ZA(1)         ANDNE     ' '
S15  C*                  Z-ADD     0             ZAX
S15  C*    1             DO        14            ZAX
S15  C*    15            SUB       ZAX           WAX
S15  C*    ZA(WAX)       CABNE     *BLANKS       SRB2
S15  C*                  MOVE      '9'           ZA(WAX)
S15  C*                  ENDDO
S15  C*                  ENDIF
S15   *
S15  C     SRB2          TAG
      *
     C                   MOVEA     Z0004         ZB
     C     ZB(1)         IFNE      '*'
     C     ZB(1)         ANDNE     ' '
     C                   Z-ADD     0             ZAX
     C     1             DO        35            ZAX
     C     36            SUB       ZAX           WAX
     C     ZB(WAX)       CABNE     *BLANKS       SRB3
     C                   MOVE      '9'           ZB(WAX)
     C                   ENDDO
     C                   ENDIF
      *
     C     SRB3          TAG
      *
     C     SRB4          TAG
      *
     C                   MOVEA     ZB            W0004
S15  C*                  MOVEA     ZA            W0020
      * Forespørsel Utvekslingsid ?
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C     *IN04         IFEQ      '1'
     C     LINNBR        ANDEQ     16
     C                   CALL      'EDI52R'
     C                   PARM                    Z0004
     C                   GOTO      SRB1
     C                   ENDIF
      *______________
      * Tømme Subfile
      *""""""""""""""
      *
     C                   MOVE      '1'           *IN93
     C                   WRITE     EDI40DC
     C                   MOVEA     '000'         *IN(93)
     C                   Z-ADD     *ZERO         ZRECNO
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     KEYA          SETLL     EDIS1
     C                   READ      EDIS1                                  94
     C  N94SDTTM         COMP      ZDTTM                              94
     C     *IN94         DOWEQ     '0'
     C                   MOVE      '0'           *IN21
     C     ZJNS          COMP      'N'                                    20
     C   20SSR           COMP      'S'                                    20
     C  N20ZJNR          COMP      'N'                                    21
     C  N20
     CAN 21SSR           COMP      'R'                                    21
      *
     C                   MOVE      '0'           *IN22
S15  C*    ZA(1)         IFNE      '*'
S15  C*    ZA(1)         ANDNE     ' '
S15  C*    S0020         IFLT      Z0020
S15  C*    S0020         ORGT      W0020
S15  C*                  MOVE      '1'           *IN22
S15  C*                  ENDIF
S15  C*                  ENDIF
N15   * Utveksl.ref - hvis oppgitt begrep test mot inneholder..  (i stedet for begynner på...)
N15  c                   if        Z0020<>*blanks and
N15  c                             %subst(Z0020:1:4)<>'*ALL'
N15  C                   eval      SOKVAR = %trim(Z0020)
N15  C     SOKVAR        SCAN      S0020                                  33
N15  C  N33              MOVE      '1'           *IN22
N15  C                   ENDIF
      *
     C                   MOVE      '0'           *IN23
     C     ZB(1)         IFNE      '*'
     C     ZB(1)         ANDNE     ' '
      *
     C     S0004         IFLT      Z0004
     C     S0004         ORGT      W0004
     C                   MOVE      '1'           *IN23
     C                   ENDIF
      *
     C     *IN23         IFEQ      '1'
     C     S0010         IFGE      Z0004
     C     S0010         ANDLE     W0004
     C                   MOVE      '0'           *IN23
     C                   ENDIF
     C                   ENDIF
      *
     C                   ENDIF
n17  c                   move      *blanks       M0065
N17  c     ssn           chain     edim6                              33
N17  c     ssn           chain     eunh1                              33
N17  c   33              move      *blanks       meldn
N17  c  N33              EVAL      MELDN = %trim(U0052)
N17  c                             + %trimr(U0054)
N17  c     zmeldt        ifne      *blanks
N17  c     zmeldt        andne     M0065
N17  C                   MOVE      '1'           *IN23
N17  c                   end
N17  c     zmeld2        ifne      *blanks
N17  c     zmeld2        andne     MELDN
N17  C                   MOVE      '1'           *IN23
N17  c                   end
      *
     C     *IN20         IFEQ      '0'
     C     *IN21         ANDEQ     '0'
     C     *IN22         ANDEQ     '0'
     C     *IN23         ANDEQ     '0'
     C                   ADD       1             ZRECNO
     C     SSR           IFEQ      'S'
N13  C     SSR           OREQ      '+'
     C                   MOVEL     S0010         WINID
N16  C                   MOVEL     S0004         WEGID
     C                   ELSE
     C                   MOVEL     S0004         WINID
N16  C                   MOVEL     S0010         WEGID
     C                   ENDIF
     C     WINID         CHAIN     EDII                               21
     C  N21              MOVEL     INNA          WSMOTP
     C   21              MOVE      *BLANKS       WSMOTP
s16  C*  21              MOVEL     S0004         WSMOTP
s16  C*  21              MOVE      S0010         WSMOTP
N16  C   21              MOVEL     WINID         WSMOTP
     C     WEGID         CHAIN     EDII                               21
     C  N21              MOVEL     INNA          WSEGEP
     C   21              MOVEL     WEGID         WSEGEP
N16  C   21              MOVEL     WEGID         WSMOTP
     C                   WRITE     EDI40DB
     C                   ENDIF
      *
     C                   READ      EDIS1                                  94
     C  N94SDTTM         COMP      ZDTTM                              94
n05  C  N94ZRECNO        COMP      9998                               94
     C  N94              END
     C     ZRECNO        IFEQ      0
     C                   MOVE      'SPR0001'     ZMSGNO
     C                   MOVE      '1'           *IN30
     C                   GOTO      SRB1
     C                   END
     C                   Z-ADD     1             ZRECNO
      *_______________
      * Behandle bilde
      *"""""""""""""""
     C     SRB5          TAG
      *
     C                   MOVEA     '11'          *IN(91)
     C  N30              MOVE      'SY00000'     ZMSGNO
     C                   WRITE     EDI40DØ
     C                   WRITE     EDI40DD
     C                   EXFMT     EDI40DC
      *
     C     *IN12         CABEQ     '1'           SRB1
     C     *IN03         IFEQ      '0'
      *
     C                   MOVEA     '00'          *IN(91)
      *_____________
      * F5=Frisk opp
      *"""""""""""""
     C     *IN05         CABEQ     '1'           SRB2
      *_______
      * Valg ?
      *"""""""
     C                   MOVE      '0'           *IN52
     C                   MOVE      '0'           *IN30
     C                   MOVE      '0'           *IN95
     C     *IN52         DOWEQ     '0'
      *
     C                   READC     EDI40DB                                52
     C     *IN52         IFEQ      '0'
     C     ZOP           CASEQ     '1'           SRBA
N08  C     ZOP           CASEQ     '4'           SRSL
     C     ZOP           CASEQ     '5'           SRBE
     C     ZOP           CASEQ     '6'           SRBF
     C     ZOP           CASEQ     '7'           SRBG
N14  C     ZOP           CASEQ     'E'           SRBG
     C     ZOP           CASEQ     '8'           SROC
N08  C     ZOP           CASEQ     'R'           SROC
     C     ZOP           CASEQ     'B'           SRAVR
     C     ZOP           CASEQ     '9'           SRBI
N14  C     ZOP           CASEQ     'F'           SRBI
     C     ZOP           CASEQ     'L'           SRBL
     C     ZOP           CASEQ     'I'           SRBIFS
N02  C     ZOP           CASEQ     'W'           SRBIFSW
     C     ZOP           CASEQ     'J'           SRBBAC
     C                   END
     C                   END
      *
     C  N30
     CANN52              END
      *
     C     *IN03         CABEQ     '1'           SRB9
     C     *IN95         CABEQ     '1'           SRB2
     C     *IN95         CABEQ     '0'           SRB5
     C                   END
      *
     C     SRB9          ENDSR
      *////////////////////////////////////////////////////////////
      *  Omsendning av sendning.                             SRBA /
      *////////////////////////////////////////////////////////////
     C     SRBA          BEGSR
      * Finnes komponenten kvar ?
     C     SMBR          IFEQ      'REMOVED'
     C                   MOVE      'SPS0007'     ZMSGNO
     C                   MOVE      '1'           *IN30
     C                   GOTO      SRBA9
     C                   END
      *__________
      * Omkjøring.
      *""""""""""
     C     SSR           IFEQ      'S'
     C     SSN           CHAIN     EDIS                               30        Sendning
     C     *IN30         IFEQ      '0'
     C                   MOVE      'A'           SST
     C                   UPDATE    EDISR
      * SKRIV POST TIL DETALJ LOGG
     C                   CLEAR                   EDISSR
     C                   Z-ADD     SSN           SSSN
     C                   MOVEL     WDT           SSDT
     C                   MOVEL     WTM           SSTM
     C                   MOVEL     SST           SSST
     C                   EVAL      SSTEXT='Resend is ordered by:'
     C     SSTEXT        CAT       UserName:1    SSTEXT
     C                   WRITE     EDISSR
     C                   ENDIF
     C                   ELSE
     C     SFILE         IFEQ      'EDIRE1K'
     C                   CALL      'EDI35R1K'
     C                   PARM                    SMBR
     C                   ELSE
     C                   CALL      'EDI35R'
     C                   PARM                    SMBR
     C                   ENDIF
     C                   END
     C     *IN30         IFEQ      '1'
     C                   MOVE      'SPS0009'     ZMSGNO
     C                   GOTO      SRBA9
     C                   END
      *_________________
      * Oppdatere SUBFIL
      *"""""""""""""""""
     C                   MOVE      *BLANKS       ZOP
     C                   UPDATE    EDI40DB
      *
     C     SRBA9         ENDSR
      *////////////////////////////////////////////////////////////
      *  Vise hele sendningen.                               SRBE /
      *////////////////////////////////////////////////////////////
     C     SRBE          BEGSR
N14  c                   eval      SNDNAME = *blanks
N14  c                   eval      RECNAME = *blanks
N14  c     S0004         CHAIN     EDII
N14  c                   if        %found
N14  c                   eval      SNDNAME = INNA
N14  c                   endif
N14  c     S0010         CHAIN     EDII
N14  c                   if        %found
N14  c                   eval      RECNAME = INNA
N14  c                   endif
     C                   MOVE      'SY00000'     ZMSGNO
     C                   EXFMT     EDI40DE
      *_________________
      * Oppdatere SUBFIL
      *"""""""""""""""""
     C                   MOVE      *BLANKS       ZOP
     C                   UPDATE    EDI40DB
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Vise alle meldinger i sendningen.                   SRBF /
      *////////////////////////////////////////////////////////////
     C     SRBF          BEGSR
      *________________________
      * Call på eksternt CL-pgm
      *""""""""""""""""""""""""
     C                   Z-ADD     SDT           WDTS
     C                   Z-ADD     STM           WTMS
     C                   MOVEL     S0004         W0004X            6
     C                   MOVEL     S0010         W0010X            6
     C     W0004X        IFEQ      'NO0001'
     C     W0010X        OREQ      'NO0001'
     C                   CALL      'TVI66R'      PLISTB
     C                   ELSE
     C                   CALL      'EDI66R'
     C                   PARM                    SSN
     C                   ENDIF
     C     *IN30         IFEQ      '1'
     C                   MOVE      'SPM0001'     ZMSGNO
     C                   GOTO      SRBF9
     C                   END
      *_________________
      * Oppdatere SUBFIL
      *"""""""""""""""""
     C                   MOVE      *BLANKS       ZOP
     C                   UPDATE    EDI40DB
      *
     C     SRBF9         ENDSR
      *////////////////////////////////////////////////////////////
      *  Vise fysisk komponent.                              SRBG /
      *////////////////////////////////////////////////////////////
     C     SRBG          BEGSR
      * Finnes komponenten kvar ?
     C     SMBR          IFEQ      'REMOVED'
     C                   MOVE      'SPS0007'     ZMSGNO
     C                   MOVE      '1'           *IN30
     C                   GOTO      SRBG9
     C                   END
      * Vise komponent.
     C                   MOVEL     SLIB          XSLIB             6
     C     XSLIB         IFEQ      'SYSPEDO'
     C                   MOVEL(P)  'DSPPFM FILE('QCMDA           512
     C     QCMDA         CAT       SLIB:0        QCMDA
     C     QCMDA         CAT       '/':0         QCMDA
     C     QCMDA         CAT       SFILE:0       QCMDA
     C     QCMDA         CAT       '_) MBR(':0   QCMDA
     C     QCMDA         CAT       SMBR:0        QCMDA
     C     QCMDA         CAT       ')':0         QCMDA
     C                   Z-ADD     50            QCMDL            15 5
     C                   CALL      'QCMDEXC'
     C                   PARM                    QCMDA
     C                   PARM                    QCMDL
     C                   ELSE
     C                   SELECT
     C     SFILE         WHENEQ    'EDIRE'
     C                   CALL      'EDI44R'
     C                   PARM                    SLIB
     C                   PARM                    SFILE
     C                   PARM                    SMBR
N14  C                   PARM                    ZOP
     C     SFILE         WHENEQ    'EDIRE1K'
     C                   CALL      'EDI44R1K'
     C                   PARM                    SLIB
     C                   PARM                    SFILE
     C                   PARM                    SMBR
N14  C                   PARM                    ZOP
     C     SFILE         WHENEQ    'EDISE'
     C                   CALL      'EDI44S'
     C                   PARM                    SLIB
     C                   PARM                    SFILE
     C                   PARM                    SMBR
N14  C                   PARM                    ZOP
     C     SFILE         WHENEQ    'EDISE1K'
     C                   CALL      'EDI44S1K'
     C                   PARM                    SLIB
     C                   PARM                    SFILE
     C                   PARM                    SMBR
N14  C                   PARM                    ZOP
N10  C     SFILE         WHENEQ    '*IFS'
N10  C                   EXSR      SRBIFS
N10  C                   GOTO      SRBG9
     C                   ENDSL
     C                   END
      *_________________
      * Oppdatere SUBFIL
      *"""""""""""""""""
     C                   MOVE      *BLANKS       ZOP
     C                   UPDATE    EDI40DB
      *
     C     SRBG9         ENDSR
      *////////////////////////////////////////////////////////////
      *  Vise fysisk komponent MED LINEFEED                  SRBI /
      *////////////////////////////////////////////////////////////
     C     SRBI          BEGSR
      * Finnes komponenten kvar ?
     C     SMBR          IFEQ      'REMOVED'
     C                   MOVE      'SPS0007'     ZMSGNO
     C                   MOVE      '1'           *IN30
     C                   GOTO      SRBI9
     C                   END
      * TEST OM XML-FILER
     C                   MOVEL     S0026         XAN04             4
     C                   IF        XAN04 = 'XML-'
     C                   CALL      'EDI41RXM'
     C                   PARM                    SLIB
     C                   PARM                    SFILE
     C                   PARM                    SMBR
     C                   ELSE
      * Vise komponent.
     C                   MOVEL     SLIB          XSLIB             6
     C     XSLIB         IFEQ      'SYSPEDO'
     C                   CALL      'EDI41RX'
     C                   PARM                    SLIB
     C                   PARM                    SFILE
     C                   PARM                    SMBR
     C                   ELSE
     C                   SELECT
     C     SFILE         WHENEQ    'EDIRE'
     C                   CALL      'EDI41R'
     C                   PARM                    SLIB
     C                   PARM                    SFILE
     C                   PARM                    SMBR
N14  C                   PARM                    ZOP
     C     SFILE         WHENEQ    'EDIRE1K'
     C                   CALL      'EDI41R1K'
     C                   PARM                    SLIB
     C                   PARM                    SFILE
     C                   PARM                    SMBR
N14  C                   PARM                    ZOP
     C     SFILE         WHENEQ    'EDISE'
     C                   CALL      'EDI41S'
     C                   PARM                    SLIB
     C                   PARM                    SFILE
     C                   PARM                    SMBR
N14  C                   PARM                    ZOP
     C     SFILE         WHENEQ    'EDISE1K'
     C                   CALL      'EDI41S1K'
     C                   PARM                    SLIB
     C                   PARM                    SFILE
     C                   PARM                    SMBR
N14  C                   PARM                    ZOP
N09  C     SFILE         WHENEQ    '*IFS'
N09  C     SIFS          IFNE      *BLANKS
N09  C                   MOVEL     SIFS          SXIFSOBJ
N09  C                   EVAL      *IN51=*OFF
N09  C                   ELSE
N09  C     SMBR          CHAIN     EDISX                              51
N09  C                   ENDIF
N09  C                   CALL      'EDI41SI'
N09  C                   PARM                    SXIFSOBJ
N14  C                   PARM                    ZOP
     C                   ENDSL
     C                   END
     C                   END
      *_________________
      * Oppdatere SUBFIL
      *"""""""""""""""""
     C                   MOVE      *BLANKS       ZOP
     C                   UPDATE    EDI40DB
      *
     C     SRBI9         ENDSR
      *////////////////////////////////////////////////////////////
      *  Overføre member til Løsning 1 OCR/REMITERING        SROC /
      *////////////////////////////////////////////////////////////
     C     SROC          BEGSR
      * Finnes komponenten kvar ?
     C     SMBR          IFEQ      'REMOVED'
     C                   MOVE      'SPS0007'     ZMSGNO
     C                   MOVE      '1'           *IN30
     C                   GOTO      SROC9
     C                   END
      *________________________
      * Er det OCR/Rem.dta?
      *""""""""""""""""""""""""
     C     S0026         IFNE      'REM'
     C     S0026         ANDNE     'OCR'
     C                   MOVE      *ON           *IN30
     C                   MOVE      'JOV0408'     ZMSGNO
     C                   GOTO      SROC9
     C                   END
      *________________________
N08   * Er den slettet/sendt før
N08   *""""""""""""""""""""""""
N08  C     ZOP           IFNE      'R'
N08  C     SST           IFEQ      'S'
N08  C     SST           OREQ      'C'
N08  C                   MOVE      *ON           *IN30
N08  C                   MOVE      'SHU1341'     ZMSGNO
N08  C                   GOTO      SROC9
N08  C                   END
N08  C                   END
N08   *________________________
      * Overfør fila
      *""""""""""""""""""""""""
      * OCR INNBETALING
     C     S0026         IFEQ      'OCR'
     C                   CALL      'EDIOCRCL'
     C                   PARM                    SMBR
     C                   PARM                    SLIB
     C                   MOVE      *ON           *IN30
     C                   MOVE      'JOV0410'     ZMSGNO
     C                   ELSE
      *
      * REMMITERING
      *
     C                   CALL      'EDIBNKCL'
     C                   PARM                    SMBR
     C                   PARM                    SLIB
     C                   MOVE      *ON           *IN30
     C                   MOVE      'JOV0409'     ZMSGNO
     C                   END
      *_______________ _
      * Oppdatere EDIS
      *""""""""""""""" "
     C     SSN           CHAIN     EDIS                               31        Sendning
     C     *IN31         IFEQ      '0'
     C                   MOVE      'C'           SST
     C                   UPDATE    EDISR
     C                   ENDIF
      *_________________
      * Oppdatere SUBFIL
      *"""""""""""""""""
n11  C     SROC9         tag
     C                   MOVE      *BLANKS       ZOP
     C                   UPDATE    EDI40DB
      *
s11  C*    SROC9         ENDSR
n11  C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  slette med status S                                      /
N08   *////////////////////////////////////////////////////////////
N08  C     SRSL          BEGSR
N08   * merk S
N08  C     SSN           CHAIN     EDIS                               31        Sendning
N08  C     *IN31         IFEQ      '0'
N08  C                   MOVE      'S'           SST
N08  C                   UPDATE    EDISR
N08  C                   ENDIF
N08   *_________________
N08   * Oppdatere SUBFIL
N08   *"""""""""""""""""
N08  C                   MOVE      *BLANKS       ZOP
N08  C                   UPDATE    EDI40DB
N08   *
N08  C                   ENDSR
N08   *////////////////////////////////////////////////////////////
      *  Overføre BANKAVSTEMNING TIL PCFIL                   SRAVR/
      *////////////////////////////////////////////////////////////
     C     SRAVR         BEGSR
      * Finnes komponenten kvar ?
     C     SMBR          IFEQ      'REMOVED'
     C                   MOVE      'SPS0007'     ZMSGNO
     C                   MOVE      '1'           *IN30
     C                   GOTO      SRAV9
     C                   END
      *________________________
      * Er det OCR/Rem.dta?
      *""""""""""""""""""""""""
     C     S0026         IFNE      'AVR'
     C                   MOVE      *ON           *IN30
     C                   MOVE      'JOV0408'     ZMSGNO
     C                   GOTO      SRAV9
     C                   END
      *________________________
      * Overfør fila
      *""""""""""""""""""""""""
     C                   CALL      'EDIBAV'      PLISTC
      *_______________ _
      * Oppdatere EDIS
      *""""""""""""""" "
     C     SSN           CHAIN     EDIS                               31        Sendning
     C     *IN31         IFEQ      '0'
     C                   MOVE      'C'           SST
     C                   UPDATE    EDISR
     C                   ENDIF
      *_________________
      * Oppdatere SUBFIL
      *"""""""""""""""""
     C                   MOVE      *BLANKS       ZOP
     C                   UPDATE    EDI40DB
      *
     C     SRAV9         ENDSR
      *////////////////////////////////////////////////////////////
      *  Vise loggdetaljer                                   SRBL /
      *////////////////////////////////////////////////////////////
     C     SRBL          BEGSR
      *_____________________
      * Call på eksternt pgm
      *"""""""""""""""""""""
     C                   CALL      'EDI42R'
     C                   PARM                    SSN
      *_________________
      * Oppdatere SUBFIL
      *"""""""""""""""""
     C                   MOVE      *BLANKS       ZOP
     C                   UPDATE    EDI40DB
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Vise fysisk komponent i backupfil                 SRBBAC /
      *////////////////////////////////////////////////////////////
     C     SRBBAC        BEGSR
      * Finnes komponenten kvar ?
     C     SMBR          IFEQ      'REMOVED'
     C                   MOVE      'SPS0007'     ZMSGNO
     C                   MOVE      '1'           *IN30
     C                   GOTO      SRBBAC9
     C                   END
      * HEnt komponentnavn fra x-ref
     C     SMBR          CHAIN     EDISX                              51
     C                   IF        *IN51=*OFF
     C     SXBACKM       IFNE      *BLANKS
      * Vise komponent.
     C                   MOVEL     SLIB          XSLIB             6
     C                   MOVEL(P)  'DSPPFM FILE('QCMDA
     C     QCMDA         CAT       SLIB:0        QCMDA
     C     QCMDA         CAT       '/':0         QCMDA
     C     SFILE         IFEQ      'EDIRE1K'
     C     QCMDA         CAT       'EDI.BACK1K':0QCMDA
     C                   ELSE
     C     QCMDA         CAT       'EDI.BACK':0  QCMDA
     C                   ENDIF
     C     QCMDA         CAT       ') MBR(':0    QCMDA
     C     QCMDA         CAT       SXBACKM:0     QCMDA
     C     QCMDA         CAT       ')':0         QCMDA
     C                   Z-ADD     50            QCMDL
     C                   CALL      'QCMDEXC'
     C                   PARM                    QCMDA
     C                   PARM                    QCMDL
     C                   ENDIF
     C                   ENDIF
      *_________________
      * Oppdatere SUBFIL
      *"""""""""""""""""
     C                   MOVE      *BLANKS       ZOP
     C                   UPDATE    EDI40DB
      *
     C     SRBBAC9       ENDSR
      *////////////////////////////////////////////////////////////
      *  Vise IFS fil                                      SRBIFS /
      *////////////////////////////////////////////////////////////
     C     SRBIFS        BEGSR
N07   * Endret fra her
      * Hent ifsnavn
     C     SIFS          IFNE      *BLANKS
     C                   MOVEL     SIFS          SXIFSOBJ
     C                   EVAL      *IN51=*OFF
     C                   ELSE
     C     SMBR          CHAIN     EDISX                              51
     C                   ENDIF
N07   * Endret til hit
N02  c  N51SXIFSOBJ      comp      *blanks                                51
     C                   IF        *IN51=*OFF
      * Vise fil
N14  c     ZOP           IFNE      'E'
     C                   EVAL      QCMDA = *BLANKS
     C                   EVAL      QCMDA = 'DSPF STMF(' + X'7D'
     C                             + %trimr(SXIFSOBJ) + X'7D'
     C                             + ')'
     C                   EVAL      QCMDL = %len(%trim(QCMDA))
     C                   CALL      'QCMDEXC'                            98
     C                   PARM                    QCMDA
     C                   PARM                    QCMDL
N14  C                   else
N14  c                   call      'SYGE55CLI'
N14  c                   parm                    SXIFSOBJ
N14  c                   endif
     C                   ENDIF
      *_________________
      * Oppdatere SUBFIL
      *"""""""""""""""""
     C                   MOVE      *BLANKS       ZOP
     C                   UPDATE    EDI40DB
      *
     C                   ENDSR
N02
N02   *////////////////////////////////////////////////////////////
N02   *  Vise IFS fil VIA WEB BROWSER                      SRBIFSW/
N02   *////////////////////////////////////////////////////////////
N02  C     SRBIFSW       BEGSR
N02   * KNYTTER HELE FUNKSJONEN TIL INTERN IP I ARKIVSYSTEM
N02   * HEnt ifsnavn fra x-ref
N04  C                   IF        SMBR = *BLANKS AND SIFS <> *BLANKS
N20  C                             OR SSR = 'S' AND SIFS <> *BLANKS
N04  C                   EVAL      SXIFSOBJ = %SUBST(SIFS:2:255)
N04  C                   EVAL      *IN51 = *OFF
N04  C                   ELSE
N02  C     SMBR          CHAIN     EDISX                              51
N04  C                   END
N02  c  N51SXIFSOBJ      comp      *blanks                                51
N02  C  N51*LOVAL        setll     FIRMARC
N02  c  N51              call      'SYGE35CL'
N02  c                   parm      SXIFSOBJ      UIfsObj         256
N02  c                   parm                    UIN51             1
     c  N51UIN51         comp      '1'                                    51
N02  c  N51              READ      firmarc                                51
N02  C                   IF        *IN51=*OFF
N02   * inetern/esketrn overstyrt på usernivå??
N02  c     UserName      chain     aruserf                            51
N02   * Fila må over i temporært område som ligger innenfor http-config
N02   * lag det om det ikke finnes
N02  C                   EVAL      QCMDA = *BLANKS
N02  C                   EVAL      QCMDA = 'MD DIR(' + X'7D'
N02  C                             + '/syspeddev/temp/' + X'7D'
N02  C                             + ')'
N02  C                   EVAL      QCMDL = %len(%trim(QCMDA))
N02  C                   CALL      'QCMDEXC'                            98
N02  C                   PARM                    QCMDA
N02  C                   PARM                    QCMDL
N02   * FINN SISTE SLASH + RENT FILNAVN
N02  C                   MOVE      *BLANKS       IFSFILE         256
N02  C                   Z-ADD     1             X                 3 0
N02  C     NextSlash     TAG
N02  C     '/'           SCAN      SXIFSOBJ:X    Y                 3 0    33
N02  C   33Y             ADD       1             X
N02  C   33              GOTO      NextSlash
s12  C*                  EVAL      IFSFILE = '/syspeddev/temp/'
s12  C*                            + %SUBST(SXIFSOBJ:X)
N12   * Om originalt filnavn er mer enn 30 - vis kun 30 siste (pga lendebegr. i PCO/SYGE39)
N12   * max lengde i PCO = 123, 44=innpakking (under), Innledende url usikker .... lengde
N12   * RunDll32.exe url.dll,FileProtocolHandler "<urn inn her ...>"
N12  C                   EVAL      TMPFILE = %SUBST(SXIFSOBJ:X)
N12  c                   EVAL      TMPLEN = %len(%trim(TMPFILE))
N12  c                   if        TMPLEN > 30
N12  C                   EVAL      TMPFILE = %SUBST(TMPFILE:TMPLEN-30)
N12  c                   endif
N12  C                   EVAL      IFSFILE = '/syspeddev/temp/'+TMPFILE
N02  C     '.xml'        SCAN      IFSFILE                                33
N02  C  N33'.XML'        SCAN      IFSFILE                                33
N02  C  N33'.txt'        SCAN      IFSFILE                                33
N02  C  N33'.TXT'        SCAN      IFSFILE                                33
N02  C  N33'.csv'        SCAN      IFSFILE                                33
N02  C  N33'.CSV'        SCAN      IFSFILE                                33
N18  C  N33'.PDF'        SCAN      IFSFILE                                33
N18  C  N33'.pdf'        SCAN      IFSFILE                                33
N19  C  N33'.json'       SCAN      IFSFILE                                33
N02  C  N33              EVAL      IFSFILE = %trim(IFSFILE)+'.txt'
N02   * Vise fil
N02  C                   EVAL      QCMDA = *BLANKS
N02  C                   EVAL      QCMDA = 'CPY OBJ(' + X'7D'
N02  C                             + %trimr(SXIFSOBJ) + X'7D'
N02  C                             + ') TOOBJ('+ X'7D'
N02  C                             + %trimr(IFSFILE) + X'7D'
N02  C                             + ') REPLACE(*YES)'
N02  C                   EVAL      QCMDL = %len(%trim(QCMDA))
N02  C                   CALL      'QCMDEXC'                            98
N02  C                   PARM                    QCMDA
N02  C                   PARM                    QCMDL
N02   * VIS som weblink.. (ta hensyn til om user evt skal ha ekstern IP)
N02  c     arinek        ifeq      'E'
N02  c                   eval      arcpad = arcpae
N02  c                   end
N02  c                   EVAL      URL = 'http://'+%trim(arcpad)
N02  C                             + %trimr(IFSFILE)
N21  c                   if        ARCPSI='J'
N21  c                   eval      url = 'https://'+%trim(arcpad)
N21  C                             + %trimr(IFSFILE)
N21  c                   endif
N02  C                   CALL      'SYGE39CL'
N02  C                   PARM                    URL             512
N02   * Slett den temporære fila..
N02  C                   EVAL      QCMDA = *BLANKS
N02  C                   EVAL      QCMDA = 'DEL OBJLNK(' + X'7D'
N02  C                             + %trimr(IFSFILE) + X'7D'
N02  C                             + ')'
N02  C                   EVAL      QCMDL = %len(%trim(QCMDA))
N02  C                   CALL      'QCMDEXC'                            98
N02  C                   PARM                    QCMDA
N02  C                   PARM                    QCMDL
N02  C                   ENDIF
N02   *_________________
N02   * Oppdatere SUBFIL
N02   *"""""""""""""""""
N02  C                   MOVE      *BLANKS       ZOP
N02  C                   UPDATE    EDI40DB
N02   *
N02  C                   ENDSR
