     A*____________________________
     A* SYSPED / KOSTNADER
     A*""""""""""""""""""""""""""""
     A*
     A* OBS!! DOPLIKATOBJEKT "KOSTAOPD" LAGES PÅ NY VED ENDRING
     A*
     A                                      REF(SR)
     A          R KOSTAR
     A            KAST           1          COLHDG('STATUS')
     A            KABNR          7S 0       COLHDG('BILAGSNUMMER')
EV5  A            KABDT          8S 0       COLHDG('BILAGSDATO')
NV5  A            KAPCC          2S 0       COLHDG('PERIODE-ÅRHUNDRE')
     A            KAPÅR          2S 0       COLHDG('PERIODE-ÅR')
     A            KAPMN          2S 0       COLHDG('PERIODE-MÅNED')
     A            KALNR          8P 0       COLHDG('LEVERANDØRSNR.')
     A            KAVAL          3          COLHDG('VALUTAKODE')
     A            KABL          13P 2       COLHDG('BELØP')
     A            KAVK           3          COLHDG('VAREKODE')
     A            KAVKU          7P 4       COLHDG('VALUTAKURS')
     A            KAOMRF         3P 0       COLHDG('OMREGNINGSFAKTOR')
E02  A            KAFNR         13          COLHDG('LEV. FAKT.NUMMER')
EV5  A            KAFDT          8P 0       COLHDG('LEV. FAKTU.DATO')
EV5  A            KAFFDT         8P 0       COLHDG('FORFALLSDATO')
     A            KABB           2          COLHDG('BETAL.BETINGELSE')
     A            KASG           3          COLHDG('ATT.KODE/SIGNATUR')
     A            KALKID        25          COLHDG('LEV. KID')
     A            KARNR          7P 0       COLHDG('REFERANSENR.')
     A            KATAVD    R               REFFLD(AVD)
     A                                      COLHDG('K.STED "KRED.SIDE"')
     A            KATUNR    R               REFFLD(PRO)
     A                                      COLHDG('IKKE I BRUK (LEDIG)')
     A            KAOAVD    R               REFFLD(AVD)
     A                                      COLHDG('IKKE I BRUK (LEDIG)')
     A            KAOPD     R               REFFLD(OPD)
     A                                      COLHDG('IKKE I BRUK (LEDIG)')
     A            KABLST         1          COLHDG('1./2. GANGSREG')
     A            KATXT         35          COLHDG('Freetext')
     A            KABNR2         7S 0       COLHDG('"BIL.NR" I BUDSJETT')
     A            KAMVA          1          COLHDG('MVA-KODE')
     A            KABLM         13P 2       COLHDG('MVA-BELØP')
     A            KAUSER        10          COLHDG('BRUKER')
EV5  A            KADTE          8S 0       COLHDG('ENDRINGSDATO')
     A            KATME          6S 0       COLHDG('ENDRINGSTID')
     A            KAJOUR         7S 0       COLHDG('JOURNALNR')
     A            KADTR          8P 0       COLHDG('REGISTRERINGSDATO')
     A            KATDR          6P 0       COLHDG('REGISTRERINGSTID')
     A          K KABNR
