      *
      *OBS! OBS! OBS! OBS! OBS! OBS! OBS! OBS! OBS! OBS! OBS! OBS! OBS!
      * VED ENDING AV DENNE PRINTERFILEN MÅ MAN OGSÅ ENDRE SYSJ27PF.
      * SYSJ27PF ER SAMMENSATT AV SYSJ27P OG SYSJ27P2.
      *OBS! OBS! OBS! OBS! OBS! OBS! OBS! OBS! OBS! OBS! OBS! OBS! OBS!
      *
     A          R HCARGO
     A                                  2  5 'To:'
     A            UNAVN         30      2 12
     A                                  2 51MSGCON(30 DIV3635 SYMSGF)
     A                                      HIGHLIGHT
     A                                  2126MSGCON(6 DIV3636 SYMSGF)
     A            XIDAG          6  0   2133EDTCDE(Y)
     A                                  2155MSGCON(6 DIV3638 SYMSGF)
     A            XCSIDE         2  0   2162EDTCDE(3)
     A                                  3  5 'From:'
     A            KNAVN         30      3 12
     A                                  4  3MSGCON(8 DIV3639 SYMSGF)
     A                                      HIGHLIGHT
     A            CBLK           2      4 12
     A            CBNAB         30      4 15
     A                                  4 45MSGCON(5 DIV3640 SYMSGF)
     A                                      HIGHLIGHT
     A            UREIS         14      4 51
     A                                  4 68MSGCON(18 DIV3641 SYMSGF)
     A                                      HIGHLIGHT
     A            XLSHN         30      4 87
     A                                  4120MSGCON(15 DIV3642 SYMSGF)
     A                                      HIGHLIGHT
     A            XDT            6  0   4136EDTCDE(Y)
     A                                  4147MSGCON(18 DIV3643 SYMSGF)
     A                                      HIGHLIGHT
     A            XLOHN         30      4166
     A                                  6  1MSGCON(99 DIV3361 SYMSGF)
     A                                  6100MSGCON(99 DIV3361 SYMSGF)
     A                                  7  3MSGCON(132 DIV3644 SYMSGF)
     A                                  7151MSGCON(30 DIV3645 SYMSGF)
     A                                  8  1MSGCON(99 DIV3361 SYMSGF)
     A                                  8100MSGCON(99 DIV3361 SYMSGF)
      *
     A          R DCARGO                    SPACEB(1)
     A            BLBLNR        16         3
     A                                    19 '|'
     A            XKOL1         10        21
     A            XKOL2         30        32
     A                                    63 '|'
     A            XMN1          17        65
     A                                    84 '|'
     A            XAN1           5  0     89EDTCDE(Z)
     A                                    97 '|'
     A            XTX1          51        99
     A                                   151 '|'
     A            XVK1           9  0    153EDTCDE(Z)
     A                                   163 '|'
     A            XM31           9  3    165EDTCDE(2)
     A                                      SPACEA(1)
     A            BLOPD          7  0      7EDTCDE(Z)
     A                                    19 '|'
     A            XKOL22        30        32
     A                                    63 '|'
     A            XMN2          17        65
     A                                    84 '|'
     A            XAN2           5  0     89EDTCDE(Z)
     A                                    97 '|'
     A            XTX2          51        99
     A                                   151 '|'
     A            XVK2           9  0    153EDTCDE(Z)
     A                                   163 '|'
     A            XM32           9  3    165EDTCDE(2)
      *
     A          R DCARG2                    SPACEB(1)
     A                                    19 '|'
     A            XKOL1         10        21
     A            XKOL2         30        32
     A                                    63 '|'
     A            XMN           17        65
     A                                    84 '|'
     A            XAN            5  0     89EDTCDE(Z)
     A                                    97 '|'
     A            XTX           51        99
     A                                   151 '|'
     A            XVK            9  0    153EDTCDE(Z)
     A                                   163 '|'
     A            XM3            9  3    165EDTCDE(2)
      *
     A          R BCARGO                    SPACEB(1)
     A                                     1MSGCON(99 DIV3361 SYMSGF)
     A                                   100MSGCON(99 DIV3361 SYMSGF)
      *
     A          R TCARGO                    SPACEB(1)
     A                                     5MSGCON(7 DIV3650 SYMSGF)
     A                                    19 '|'
     A                                    63 '|'
     A                                    84 '|'
     A            XTOTAN         5  0     89EDTCDE(Z)
     A                                    97 '|'
     A                                   151 '|'
     A            XTOTVK         9  0    153EDTCDE(Z)
     A                                   163 '|'
     A            XTOTM3         9  3    164EDTCDE(2)
     A                                      SPACEA(1)
     A                                     1MSGCON(99 DIV3361 SYMSGF)
     A                                   100MSGCON(99 DIV3361 SYMSGF)
