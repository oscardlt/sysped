/* *********************************************************************   */
/*       * RETTINGER I DETTE PROGRAM:                                      */
/* PTFnr:*Sek Sig Vers  Beskrivelse..........................              */
/* """"""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""    */
/* 10013 * 01 CB  PTF10 REORG AV FTP-LOGG                                  */
/* ********************************************************************    */
             PGM
          /*_____________________*/
          /* Deklarere Variabler */
          /*"""""""""""""""""""""*/
             DCLF       FILE(EDI09F2)
             DCL        VAR(&DLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&OLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&MBNAME) TYPE(*CHAR) LEN(10)
             DCL        VAR(&MBFILE) TYPE(*CHAR) LEN(10)
             DCL        VAR(&MBFCDT) TYPE(*CHAR) LEN(6)
             DCL        VAR(&QYEAR) TYPE(*CHAR) LEN(2)
             DCL        VAR(&QMONTH) TYPE(*CHAR) LEN(2)
             DCL        VAR(&QDAY) TYPE(*CHAR) LEN(2)
             DCL        VAR(&YEAR) TYPE(*DEC) LEN(2 0)
             DCL        VAR(&MONTH) TYPE(*DEC) LEN(2 0)
             DCL        VAR(&DAY) TYPE(*DEC) LEN(2 0)
             DCL        VAR(&WDT) TYPE(*CHAR) LEN(8)
             DCL        VAR(&MBCDAT8) TYPE(*CHAR) LEN(8)
             RTVDTAARA  DTAARA(EDIDTA (171 10)) RTNVAR(&DLIB)
             RTVDTAARA  DTAARA(EDIDTA (181 10)) RTNVAR(&OLIB)
          /*______________________________________*/
          /* Beregne dato 7 dager tilbake i tiden */
          /*""""""""""""""""""""""""""""""""""""""*/
             RTVSYSVAL  SYSVAL(QYEAR) RTNVAR(&QYEAR)
             RTVSYSVAL  SYSVAL(QMONTH) RTNVAR(&QMONTH)
             RTVSYSVAL  SYSVAL(QDAY) RTNVAR(&QDAY)

             CHGVAR     VAR(&YEAR) VALUE(&QYEAR)
             CHGVAR     VAR(&MONTH) VALUE(&QMONTH)
             CHGVAR     VAR(&DAY) VALUE(&QDAY)

             CHGVAR     VAR(&DAY) VALUE(&DAY-7)

             IF         COND(&DAY *LT 1) THEN(DO)
             CHGVAR     VAR(&DAY) VALUE(&DAY+30)
             CHGVAR     VAR(&MONTH) VALUE(&MONTH-1)
             ENDDO

             IF         COND(&MONTH *LT 1) THEN(DO)
             CHGVAR     VAR(&MONTH) VALUE(&MONTH+12)
             CHGVAR     VAR(&YEAR) VALUE(&YEAR-1)
             ENDDO

             CHGVAR     VAR(&QYEAR) VALUE(&YEAR)
             CHGVAR     VAR(&QMONTH) VALUE(&MONTH)
             CHGVAR     VAR(&QDAY) VALUE(&DAY)

             IF         COND(&YEAR *GT 50) THEN(CHGVAR VAR(&WDT) +
                          VALUE('19' *CAT &QYEAR *CAT &QMONTH *CAT +
                          &QDAY))
             ELSE       CMD(CHGVAR VAR(&WDT) VALUE('20' *CAT &QYEAR +
                          *CAT &QMONTH *CAT &QDAY))
          /*_______________________*/
          /* REORGANISERE FTP-LOGG */
          /*"""""""""""""""""""""""*/
/*N01*/      CALL       PGM(E503R) PARM(&WDT)
          /*______________________*/
          /* Hente filbeskrivelse */
          /*""""""""""""""""""""""*/
             DSPFD      FILE(EDI.BACK) TYPE(*MBR) OUTPUT(*OUTFILE) +
                          OUTFILE(EDI09F2)
             DSPFD      FILE(EDI.BACK1K) TYPE(*MBR) OUTPUT(*OUTFILE) +
                          OUTFILE(EDI09F2) OUTMBR(*FIRST *ADD)
          /*____________________*/
          /* Les filbeskrivelse */
          /*""""""""""""""""""""*/
 READ2:      RCVF       RCDFMT(QWHFDMBR)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(EXIT))
          /*__________________________*/
          /* Slette gamle komponenter */
          /*""""""""""""""""""""""""""*/
             IF         COND(&MBCDAT *GT '501231') THEN(CHGVAR +
                          VAR(&MBCDAT8) VALUE('19' *CAT &MBCDAT))
             ELSE       CMD(CHGVAR VAR(&MBCDAT8) VALUE('20' *CAT +
                          &MBCDAT))
             IF         COND((&MBNAME *NE '          ') *AND +
                          (&MBCDAT8 *LT &WDT)) THEN(DO)
             IF         COND(&MBFILE *EQ EDI.BACK) THEN(DO)
             RMVM       FILE(EDI.BACK) MBR(&MBNAME)
             MONMSG     MSGID(CPF0000)
             ENDDO
             IF         COND(&MBFILE *EQ EDI.BACK1K) THEN(DO)
             RMVM       FILE(EDI.BACK1K) MBR(&MBNAME)
             MONMSG     MSGID(CPF0000)
             ENDDO
             ENDDO

             GOTO READ2
          /*____________________________*/
          /* STARTER SLETTING I E-FILER */
          /*""""""""""""""""""""""""""""*/
EXIT:       CALL       PGM(E500C)

             ENDPGM
