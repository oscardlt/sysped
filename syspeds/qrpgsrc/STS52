      *********************************************************************
      *  After compiling this RPG MODULE,
      *  create the related program with the following command:
      *
CRTPG+*  CRTPGM SYSPED/STS52 MODULE(*LIBL/STS52 *LIBL/INCGI)
CRTPGM*         ACTGRP(*NEW) AUT(*USE)
      *
      *
      *********************************************************************
      *
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
      *--------------------------------------------------------------------
      * Data base files
      *--------------------------------------------------------------------
     FBRIDF     IF   E           K DISK    usropn
     FGSSOLF    IF   E           K DISK    usropn
     FGSSOLL04  UF   E           K DISK    usropn
     F                                     RENAME(GSSOLFR:GSSOLFR4)
     FGSSORF    IF   E           K DISK    usropn
      *--------------------------------------------------------------------
      * Prototype defintions and standard system API error structure
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      *--------------------------------------------------------------------
      * Variables common to CGI programs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
      * Variables received from the input string
     D lng             s              2
     D BckGnd          s             10
     D XXUSER          S             10
     D XXTURNR         S             11
     D FEILKODE        s              1
     D STS101          s              1
     D lstbrd          s              1
     D SEMI            S              1
     D TKfeil          S            100
     D TKfeilK         S              1
      * andre variabler
     D Fn              s             10
     D Lib             s             10
     D Mbr             s             10
     D i               s             10i 0
      *
     D                 DS
     D  XXCLID20               1     20
     D  XXCLID35               1     35
     D                 DS
     D  XXCLID                 1     35
     D  XXPRE                  1      3
     D  XXPRE2                 1      2
     D  XXONO                  4     10
     D  XXLNO                 11     12
     D  XXOLTNO                4     14
     D  XXOLBOX               15     17
      *
      *--------------------------------------------------------------------
      * Prolog common to CGI programs
      * -Receive input from the remote browser
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prolog3
      *=====================================================================******
      * MAIN PROCESS LINE
      *=====================================================================******
      * Parse input string
     C                   EXSR      PARSE
      *
     C                   EXSR      INIT
      *
     C                   SELECT
     C                   WHEN      *IN60
     C                   MOVE      '1'           FEILKODE
     C                   exsr      SetSkl51B
     C                   WHEN      BIINN = ' '
     C                   MOVE      '1'           FEILKODE
     C                   exsr      SetSkl51B
      * Blank, ikke gjør noe..(vis bilde på ny)
     C                   WHEN      XXCLID20 = *blanks
     C                   EXSR      SetSkl52
     C                   WHEN      (((XXPRE <> '997') AND
     C                               (XXPRE <> '998')) AND
     C                               (XXPRE2 <> '00'))
     C                   MOVE      '2'           FEILKODE
     C                   EVAL      TKFEIL='Colli ID Must start 997.. or 00....'
     C                   EVAL      TKFEILK='X'
     C                   EXSR      SetSkl52
     C                   OTHER
     C     XXPRE2        IFEQ      '00'
     C     XXCLID35      CHAIN(N)  GSSOLL04                           33
     c* jobber videre med intern benevning
     c  n33              MOVE      '997'         XXPRE
     c  n33              MOVE      OLONO         XXONO
     c  n33              MOVE      OLLNO         XXLNO
     c                   else
     C                   IF        XXPRE = '997'
     C                   MOVE      XXONO         OLONO
     C                   MOVE      XXLNO         OLLNO
     C     GSSOLFK       CHAIN     GSSOLF                             33
     C                   ELSE
     C     XXCLID35      CHAIN(N)  GSSOLL04                           33
     C                   END
     c                   end
     C                   IF        *IN33
     C                   MOVE      '3'           FEILKODE
     C                   EVAL      TKFEIL='Scanned colli ID does not exist. '
     C                                  + '(in the database)'
     C                   EVAL      TKFEILK='X'
     C                   EXSR      SetSkl52
     C                   ELSE
     C     OLONO         CHAIN     GSSORF                             33
     C                   IF        ((ORTNO <> XXTNO) AND
     C                              (XXTNO <> 0))
     C                   MOVE      '4'           FEILKODE
     C                   EVAL      TKFEIL='Colli ' + XXCLID
     C                                  +' is NOT part of this shipment list.'
     C                   EVAL      TKFEILK='X'
     C                   EXSR      SetSkl52
     C                   ELSE
     C                   IF        XXPRE = '998'
     C     XXCLID35      SETLL     GSSOLL04                           33
     C     XXCLID35      READE     GSSOLL04                               33
     C                   DOW       *IN33 = *OFF
     C                   MOVE      'L'           OLST
     C                   UPDATE    GSSOLFR4
     C     XXCLID35      READE     GSSOLL04                               33
     C                   ENDDO
     C                   MOVE      ORTNO         XXTURNR
     C                   EXSR      SetSkl53
     C                   ELSE
     C                   exsr      SetSkl53
     C                   END
     C                   END
     C                   END
     C                   ENDSL
      * Read skeleton output html, etc.
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
      * 1-Section /$top
     C                   exsr      SetTop
     c     SEMI          ifeq      'Y'
     C                   callp     wrtsection('top')
     c                   else
     C                   SELECT
     C                   WHEN      *IN60
     C                   callp     wrtsection('all')
     C                   WHEN      BIINN = ' '
     C                   callp     wrtsection('all')
     C                   OTHER
     C                   callp     wrtsection('top')
     C                   ENDSL
     c                   endif
      *
     C                   callp     wrtsection('*fini')
      *
      *
     C                   close     BRIDF
     C                   close     GSSOLF
     C                   close     GSSOLL04
     C                   close     GSSORF
     C                   eval      *inlr = *on
     C                   RETURN
      *
      *=====================================================================******
      * Parse input
      *=====================================================================******
     C     Parse         begsr
      * TEST-JOV
     C                   eval      XXCLID20 = zhbgetvar('CLID')
     C                   eval      XXCLID = XXCLID20
     C                   eval      XXUSER = zhbgetvar('USER')
     C                   eval      XXTURNR= zhbgetvar('TURNR')
     C                   eval      STS101= zhbgetvar('STS101')
     C                   EVAL      SEMI     = zhbgetvar('SEMI')
      *
     C                   endsr
      *
      *=====================================================================******
      * Set html output skeleton member before its read into memory
      *=====================================================================******
     C     SetSkl51B     begsr
     C                   eval      lng = uppify(lng)
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC' + lng
     C     Sts101        IFEQ      'J'
     C                   eval      Mbr = 'STS101'
     c                   else
     C                   eval      Mbr = 'STS51B'
     c                   end
      *
     C                   endsr
      *=====================================================================******
      * Set html output skeleton member before its read into memory
      *=====================================================================******
     C     SetSkl52      begsr
     C                   eval      lng = uppify(lng)
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC' + lng
     C                   eval      Mbr = 'STS52'
     C     SEMI          IFEQ      'Y'
     C                   eval      Mbr = 'STS52T'
     C                   end
      *
     C                   endsr
      *=====================================================================******
      * Set html output skeleton member before its read into memory
      *=====================================================================******
     C     SetSkl53      begsr
     C                   eval      lng = uppify(lng)
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC' + lng
     C                   eval      Mbr = 'STS53'
     C     SEMI          IFEQ      'Y'
     C                   eval      Mbr = 'STS53T'
     C                   end
     C                   MOVE      ORTNO         XXTURNR
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$top
      *  (search criteria)
      *=====================================================================******
     C     SetTop        begsr
      * Repeat national language for the next invocation
     C                   callp     updhtmlvar('LNG':lng:
     C                             InitHTMLVars)
      * Repeat background color for the next invocation
     C                   callp     updhtmlvar('BCKGND':bckgnd)
      * Color for text, background, links, visited links, active links
     C                   exsr      setcolor
     C                   callp     updhtmlvar('STS101':STS101)
     C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updhtmlvar('USER':XXUSER)
     C                   callp     updhtmlvar('CLID':XXCLID)
     C                   callp     updhtmlvar('TURNR':XXTURNR)
     C                   callp     updHTMLvar('FEILKODE':FEILKODE)
     c     TKFEIL        ifeq      *blanks
     c                   eval      Tkfeil='Scan location for colli ID '
     c                                  + XXCLID
     c     XXTURNR       ifne      *blanks
     c     XXTURNR       andne     *zeros
     c                   eval      Tkfeil='Scan BOX ID into which '
     c                                  + XXCLID + ' is placed.'
     c                   end
     c                   end
     C                   callp     updHTMLvar('TKFEILK':TKFEILK)
     C                   callp     updHTMLvar('TKFEIL':TKFEIL)
      *
     C                   endsr
      *=====================================================================******
      * Color for font, background, links, visited links, active links
      *=====================================================================******
     C     SetColor      begsr
     C                   eval      BckGnd = uppify(BckGnd)
     C                   callp     updhtmlvar('TXTCOLOR':'black')
     C                   callp     updhtmlvar('BOLD':' ')
      * BGCOLOR white, LINK blue, VLINK red, ALINK green
     C     bckgnd        ifeq      'WHITE'
     C     bckgnd        oreq      *blank
     C                   callp     updhtmlvar('BGCOLOR':'FFFFFF')
     C                   callp     updhtmlvar('LINK':'0000FF')
     C                   callp     updhtmlvar('VLINK':'FF0000')
     C                   callp     updhtmlvar('ALINK':'00FF00')
     C                   endif
      * BGCOLOR gray, LINK blue, VLINK red, ALINK green
     C     bckgnd        ifeq      'GRAY'
     C                   callp     updhtmlvar('BGCOLOR':'CCCCCC')
     C                   callp     updhtmlvar('LINK':'0000FF')
     C                   callp     updhtmlvar('VLINK':'FF0000')
     C                   callp     updhtmlvar('ALINK':'00FF00')
     C                   endif
      * BGCOLOR light blue, LINK blue, VLINK red, ALINK green
     C     bckgnd        ifeq      'LBLUE'
     C                   callp     updhtmlvar('BGCOLOR':'5BE6F3')
     C                   callp     updhtmlvar('LINK':'0000FF')
     C                   callp     updhtmlvar('VLINK':'FF0000')
     C                   callp     updhtmlvar('ALINK':'00FF00')
     C                   endif
      * BGCOLOR black, LINK green, VLINK red, ALINK white
     C     bckgnd        ifeq      'BLACK'
     C                   callp     updhtmlvar('TXTCOLOR':'white')
     C                   callp     updhtmlvar('BGCOLOR':'000000')
     C                   callp     updhtmlvar('LINK':'00FF00')
     C                   callp     updhtmlvar('VLINK':'FF0000')
     C                   callp     updhtmlvar('ALINK':'FFFFFF')
     C                   callp     updhtmlvar('BOLD':'<b>')
     C                   endif
     C                   endsr
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     GSSOLFK       KLIST
     C                   KFLD                    OLONO
     C                   KFLD                    OLLNO
      *
     C                   open      BRIDF
     C     XXUSER        CHAIN     BRIDF                              60
     C                   IF        *IN60 = *OFF
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   CALLB     'INCGI'
     C                   PARM                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
      *
     C                   open      GSSOLF
     C                   open      GSSOLL04
     C                   open      GSSORF
     C                   MOVE      XXTURNR       XXTNO            11 0
     C                   END
     C                   MOVE      *BLANKS       FEILKODE
      *
     C                   ENDSR
