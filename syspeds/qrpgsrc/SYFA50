********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 SH  PTF10 VALTAHÅNDTERING    .....
10XXX * 02 SH  PTF11 PRJOIN FJERNET    .....
********************************************************************
      **************************************************************
      * PROGRAM SKAL OPPDATERE MVA/TOLL PÅ FAKTURAREGISTER     ***
      * INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER       ***
      * LEDIGE INDIKATORER 01-26                               ***
      * LEDIGE INDIKATORER 27-32 38 39 42-47 51-83 87-98
      **************************************************************
     FFAK4      UF A E           K DISK    USROPN
     FFAK2      IF   E           K DISK    USROPN
     F                                     RENAME(FAKTR:FAKTR2)
     FFIRM      IF   E           K DISK
     FKODTV     IF   E           K DISK
     FCUNDL01   UF   E           K DISK
     FHEADF     IF   E           K DISK
     FVALUL01   IF   E           K DISK
     FKODTGE    IF   E           K DISK
     FPRIK      IF   E           K DISK
     FPRIK1     IF   E           K DISK
     F                                     RENAME(PRIKR:PRIKR1)
s02  F*RJOIN    IF   E           K DISK
     FPRIS      IF   E           K DISK
     D*  Include/omit arrays for gebyravtaler
     D FS              S              5    DIM(10)                              I/O. frasted
     D TS              S              5    DIM(10)                              I/O. tilsted
     D LS              S             20    DIM(3)                               I/O. last/loss
     D                 DS
     D  PKKOLL                 1      1
     D  PRKOLL                 1      1
     D                 DS
     D  YTNR                   1      1
     D  YVK                    2      4
     D  YKEY                   1      4
     D                 DS
     D  PRTNR                  1      1
     D  PRGEBY                 2      4
     D  PRKEY                  1      4
     D                 DS
     D  YKNR                   1      8
     D  YAVTN                  9      9
     D  YGEBY                 10     12
     D  YKEY1                  1      9
     D  YKEY2                  1     12
     D                 DS
     D  PKKNR                  1      8
     D  PKAVTN                 9      9
     D  PKGEBY                10     12
     D  PKKEY1                 1      9
     D  PKKEY2                 1     12
     D                 DS
     D  UR                     1     10
     D                                     DIM(10)                              I/O. ursp.kode
     D  PKUR01                 1      1
     D  PKUR02                 2      2
     D  PKUR03                 3      3
     D  PKUR04                 4      4
     D  PKUR05                 5      5
     D  PKUR06                 6      6
     D  PKUR07                 7      7
     D  PKUR08                 8      8
     D  PKUR09                 9      9
     D  PKUR10                10     10
     D                 DS
     D  FR                     1     30
     D                                     DIM(10)                              I/O. frankatur
     D  PKFR01                 1      3
     D  PKFR02                 4      6
     D  PKFR03                 7      9
     D  PKFR04                10     12
     D  PKFR05                13     15
     D  PKFR06                16     18
     D  PKFR07                19     21
     D  PKFR08                22     24
     D  PKFR09                25     27
     D  PKFR10                28     30
     D                 DS
     D  OT                     1     20
     D                                     DIM(10)                              I/O. oppd.type
     D  PKOT01                 1      2
     D  PKOT02                 3      4
     D  PKOT03                 5      6
     D  PKOT04                 7      8
     D  PKOT05                 9     10
     D  PKOT06                11     12
     D  PKOT07                13     14
     D  PKOT08                15     16
     D  PKOT09                17     18
     D  PKOT10                19     20
     D                 DS
     D  LK                     1     20
     D                                     DIM(10)                              I/O. landkode
     D  PKLK01                 1      2
     D  PKLK02                 3      4
     D  PKLK03                 5      6
     D  PKLK04                 7      8
     D  PKLK05                 9     10
     D  PKLK06                11     12
     D  PKLK07                13     14
     D  PKLK08                15     16
     D  PKLK09                17     18
     D  PKLK10                19     20
      *
     C                   OPEN      FAK4
     C                   OPEN      FAK2
     C                   EXSR      INIT
      *
     C     HEADFK        CHAIN     HEADF                              37
     C     *IN37         IFEQ      '0'
     C                   EXSR      HOVED
     C                   ENDIF
      *
     C                   CLOSE     FAK4
     C                   CLOSE     FAK2
     C                   RETURN
      *
      ******************************************
     C     HOVED         BEGSR
      ******************************************
     C*
     C                   EXSR      TESTFA
     C   33              GOTO      UTUT
     C     KODTVK        CHAIN     KODTV                              41
      * BYGGER OPP FAKTURABILDET
      *
     C     HEKDTM        IFEQ      'S'
     C                   MOVE      HEKNSF        KUNDNR
     C                   ELSE
     C                   MOVE      HEKNKF        KUNDNR
     C                   END
      *
     C                   MOVE      *ZEROS        XUTLGR            9 0
     C                   MOVE      *ZEROS        XLN               2 0
     C                   MOVE      'N'           UTLMAN            1
      *
     C                   SETOFF                                       41
     C     FAK4K         SETLL     FAK4
      *
     C     STLES         TAG
     C     FAK4K         READE     FAK4                                   41
     C   41              GOTO      UTLES
     C                   MOVE      FABELN        XBELØN           13 2
      *
      * HOPPER UT HVIS FAKTURERT TIDLIGERE TOL/MVA
      *
     C     FAOPKO        IFNE      ' '
     C     FAOPKO        ANDNE     'C'
     C     FAVK          IFEQ      'TOL'
     C     FAVK          OREQ      'MVA'
     C                   GOTO      UTUT
     C                   END
     C                   END
      *
      * PRIS PR FORTOLLET LINJE
      *
     C     FAOPKO        IFEQ      ' '
     C     FAOPKO        OREQ      'C'
     C     FA01B         IFEQ      'L'
     C                   EXSR      BFAKT0
     C                   END
     C                   END
      *
     C     FAVK          IFEQ      'TOL'
     C                   DELETE    FAKTR
     C                   EXSR      MSALDO
     C                   END
     C     FAVK          IFEQ      'MVA'
     C                   DELETE    FAKTR
     C                   EXSR      MSALDO
     C                   END
      *** ---------------------------------------------
     C     FAOPKO        IFEQ      ' '
     C     FAOPKO        OREQ      'C'
     C     HEKDTM        IFEQ      FASK
      *** FJERNER UTLEGGSPOST
     C     FAVK          IFEQ      'UTL'
     C     FAOPKO        IFEQ      'M'
     C     FAOPKO        OREQ      'E'
     C                   MOVE      'J'           UTLMAN            1
     C                   ELSE
     C                   MOVE      FAKDM         XAKDM             1
     C                   DELETE    FAKTR
     C                   EXSR      MSALDO
     C                   END
     C                   ELSE
      *** ØKER UTLEGGSGRUNNLAG
     C     FAVK          IFNE      'TOL'
     C     FAVK          ANDNE     'MVA'
     C     FAKDUL        ANDEQ     'J'
     C                   ADD       FABELN        XUTLGR
     C                   END
     C                   END
     C                   END
     C                   END
      *** ---------------------------------------------
      *
     C                   GOTO      STLES
      *
     C     UTLES         TAG
      *
      *** OPPDATERER FIL
      ***----------------------------------------------
     C     HEKDTM        IFNE      ' '
     C     HEKDTM        ANDNE     'I'
      *** OPPDATERER UTLAGT TOLL
     C     HEBELT        IFNE      0
     C                   MOVE      'TOL'         FAVK
     C                   Z-ADD     HEBELT        FABELV
     C                   Z-ADD     HEBELT        FABELN
     C     KEYGE         CHAIN     KODTGE                             55
     C  N55              MOVE      KGEUTL        FAKDUL
     C   55              MOVE      ' '           FAKDUL
     C     FAKDUL        IFEQ      'J'
     C                   ADD       HEBELT        XUTLGR
     C                   END
     C                   MOVE      'M'           FAKDA
     C                   MOVE      'N'           FAKDM
     C                   EXSR      OPDFAK
     C                   END
      * OPPDATERER MOMSBELØP
     C     HEBELM        IFNE      0
     C                   MOVE      'MVA'         FAVK
     C                   Z-ADD     HEBELM        FABELV
     C                   Z-ADD     HEBELM        FABELN
     C     KEYGE         CHAIN     KODTGE                             55
     C  N55              MOVE      KGEUTL        FAKDUL
     C   55              MOVE      ' '           FAKDUL
     C     FAKDUL        IFEQ      'J'
     C                   ADD       HEBELM        XUTLGR
     C                   END
     C                   MOVE      'M'           FAKDA
     C                   MOVE      'N'           FAKDM
     C                   EXSR      OPDFAK
     C                   END
     C                   END
      ***----------------------------------------------
      *
      * OPPDATERER UTLEGGS PROVISJON
      *
     C     KUND1K        CHAIN(N)  CUNDL01                            48
     C  N48              Z-ADD     SYMINU        XUTLMI
     C  N48SYUTLP        IFEQ      *ZEROS
     C                   MOVE      'B'           FAKDA
     C                   MOVE      FIUPS         SYUTLP            4 2
     C                   Z-ADD     FIUPM         XUTLMI            4 0
     C     AVUTPR        IFNE      *ZEROS
     C                   Z-ADD     AVUTPR        SYUTLP
     C                   END
     C     AVUTMI        IFNE      *ZEROS
     C                   Z-ADD     AVUTMI        XUTLMI
     C                   END
     C                   END
     C     SYUTLP        IFEQ      99.99
     C                   MOVE      'A'           FAKDA
     C                   MOVE      *ZEROS        SYUTLP
     C                   END
      *
     C  N48XUTLGR        IFNE      *ZEROS
     C     HEKDTM        ANDNE     ' '
     C     SYUTLP        ANDNE     0
     C     UTLMAN        ANDNE     'J'
     C                   MOVE      'UTL'         FAVK
     C     XUTLGR        MULT      SYUTLP        FABELV
     C     FABELV        DIV(H)    100           FABELN
     C                   Z-ADD     FABELN        FABELV
     C     FABELN        IFLT      XUTLMI
     C                   Z-ADD     XUTLMI        FABELV
     C                   Z-ADD     XUTLMI        FABELN                          H
     C                   END
      *
     C     XAKDM         IFNE      *BLANK
     C                   MOVE      XAKDM         FAKDM
     C                   ELSE
     C     HEKDTM        IFEQ      'S'
     C                   MOVE      'N'           FAKDM
     C                   ELSE
     C                   MOVE      'J'           FAKDM
     C                   END
     C                   END
      *
     C                   EXSR      OPDFAK
     C                   END
      *
     C     UTUT          ENDSR
      *
      ******************************************
     C     OPDFAK        BEGSR
      ******************************************
     C                   MOVE      *BLANKS       FA01
     C                   MOVE      HEAVD         FAAVD
     C                   MOVE      HEOPD         FAOPD
     C                   MOVE      HEKDTM        FASK
     C                   MOVE      *BLANKS       FAVT
     C                   MOVE      *BLANKS       FA01B
     C                   MOVE      FICURR        FAVAL
     C                   MOVE      ' '           FAKDUK
     C                   MOVE      *ZEROS        FABELU
     C                   EXSR      DECI
     C                   CALL      'SYGE06'
     C                   PARM                    FAAVD
     C                   PARM                    FAOPD
     C                   PARM                    FALI
     C                   EXSR      SRKUNR
N01  C                   EXSR      VALUT2
     C                   EXCEPT    NYFAKT
     C                   EXSR      PSALDO
      *
     C                   ENDSR
      ***********************************************
     C     DECI          BEGSR
      ***********************************************
     C     FIDECI        IFEQ      0
     C                   Z-ADD(H)  FABELN        FABEL0           11 0
     C                   Z-ADD     FABEL0        FABELN
     C                   Z-ADD(H)  FABELU        FABEL0           11 0
     C                   Z-ADD     FABEL0        FABELU
     C                   ELSE
     C     FIDECI        IFEQ      1
     C                   Z-ADD(H)  FABELN        FABEL1           12 1
     C                   Z-ADD     FABEL1        FABELN
     C                   Z-ADD(H)  FABELU        FABEL1           12 1
     C                   Z-ADD     FABEL1        FABELU
     C                   END
     C                   END
      *
     C                   ENDSR
      *
      ******************************************
     C     BFAKT0        BEGSR
      ******************************************
     C                   MOVE      '1'           YTNR
     C                   MOVE      FICURR        VALKOD
     C                   MOVE      FASK          XSK               1
     C                   MOVE      YTNR          PKTNR
     C                   MOVE      FAVK          YGEBY
     C                   MOVE      *BLANKS       PRKOLL
      *
      * S E L G E R             F A K T U R E R I N G
      *
     C     HEKDFS        COMP      'X'                                    33
     C  N33XSK           COMP      'K'                                    33
     C   33              GOTO      VMOTT
     C                   MOVE      *ZEROS        XPROS            10 3
     C                   MOVE      HEFBV         YINTR
     C                   MOVE      *BLANKS       YAVTT
     C                   MOVE      *ZEROS        YINT
      *
      *    H A R  S E L G E R  A V T A L E
      *
     C     HEKDFS        IFEQ      ' '
     C                   MOVE      HEKNSF        YKNR
     C                   MOVE      HEANS         YAVTN
     C                   ELSE
     C     HEKDFS        IFEQ      'B'
     C                   MOVE      HEANK         YAVTN
     C     HEKDFK        IFEQ      'E'
     C                   MOVE      HEKNK         YKNR
     C                   ELSE
     C                   MOVE      HEKNKF        YKNR
     C                   END
     C                   ELSE
     C     HEKDFS        IFEQ      'E'
     C                   MOVE      HEKNS         YKNR
     C                   MOVE      HEANS         YAVTN
     C                   END
     C                   END
     C                   END
      *
     C                   EXSR      AVTSR
      *
      * SKJEKKER BRUTTOTARIFF   FOR SELGER
      *
     C   33              MOVE      'S'           YAVTN
     C   33              EXSR      BRUTAR
     C                   GOTO      UTLI
      *
      * K J Ø P E R    F A K T U R E R I N G
      *
     C     VMOTT         TAG
     C     HEKDFK        COMP      'X'                                    33
     C   33              GOTO      UTLI
     C                   MOVE      *ZEROS        XPROS
     C                   MOVE      HEFBV         YINTR
     C                   MOVE      *BLANKS       YAVTT
     C                   MOVE      *ZEROS        YINT
      *
      *       S K J E K K E R  K J Ø P E R S  A  V T A L E
      *
     C     HEKDFK        IFEQ      ' '
     C                   MOVE      HEKNKF        YKNR
     C                   MOVE      HEANK         YAVTN
     C                   ELSE
     C     HEKDFK        IFEQ      'B'
     C                   MOVE      HEANS         YAVTN
     C     HEKDFS        IFEQ      'E'
     C                   MOVE      HEKNS         YKNR
     C                   ELSE
     C                   MOVE      HEKNSF        YKNR
     C                   END
     C                   ELSE
     C     HEKDFK        IFEQ      'E'
     C                   MOVE      HEKNK         YKNR
     C                   MOVE      HEANK         YAVTN
     C                   END
     C                   END
     C                   END
      *
     C                   EXSR      AVTSR
      *
      * SKJEKKER BRUTTOTARIFF KJØPER
      *
     C   33              MOVE      'K'           YAVTN
     C   33              EXSR      BRUTAR
      *
     C     UTLI          ENDSR
      *
      ******************************************
     C     BRUTAR        BEGSR
      ******************************************
      *
      * H E N T E R   F R A   B R U T T O T A R I F F
      *
     C                   SETOFF                                       34
     C                   MOVE      '    ALLE'    YKNR
     C                   MOVE      HEFR          YFRAN
     C                   MOVE      HEOT          YOPDT
     C                   MOVE      *BLANKS       YAVTT
     C                   MOVE      *ZEROS        YINT
     C     PRIK1K        SETLL     PRIK1
     C                   READ      PRIK1                                  34
     C  N34YFRAN         COMP      PKFR01                             3434
     C  N34YOPDT         COMP      PKOT01                             3434
     C  N34YKEY2         COMP      PKKEY2                             3434
     C  N34              MOVEL     PKTNR         YTNR
     C  N34              EXSR      FBELØ
      *
     C                   ENDSR
      *
      ******************************************
     C     AVTSR         BEGSR
      ******************************************
     C                   MOVE      HEFR          YFRAN
     C                   MOVE      HEOT          YOPDT
      *
     C                   EXSR      BFAKT
      *
     C   33              GOTO      AVSRUT
      *
     C                   EXSR      FBELØ2
      *
     C     AVSRUT        ENDSR
      *
      ******************************************
     C     BFAKT         BEGSR
      ******************************************
      *
      *  Tests that there is at least one valid price line in the price
      *  agreement system (for a client (AVTSR)
      *  33 OFF after this subroutine - at least one valid line found.
      * ANNEN ENHET
      *
     C                   Z-ADD     HENTF         YINT
     C                   SETOFF                                       33
      * Find first priceline- assumes that ther is only one line
      * on this key (No testing on outdated lines,includ/omit etc.)
     C     PRIKK         SETLL     PRIK
     C                   READ      PRIK                                   33
      *
     C  N33YKEY2         COMP      PKKEY2                             3333
      *
     C  N33              EXSR      TESTIO
      *
     C  N33              MOVEL     PKTNR         YTNR
      *
     C                   ENDSR
      *
      ******************************************
     C     FBELØ         BEGSR
      ******************************************
      *
      * HENTER DATA FRA BRUTTO TARIFF
      * PRIS2K ER LEST
      * ANNEN ENHET
      *
     C                   Z-ADD     HENTF         YINTR
     C                   Z-ADD     HENTF         HENTFX
     C                   SETOFF                                       34
     C                   MOVE      FAVK          YVK
      *
      * HENTER BRUTTO GEBYR
      *
     C     PRIKEY        SETLL     PRIS
     C                   READ      PRIS                                   34
     C  N34YKEY          COMP      PRKEY                              3434
     C  N34PRTEGN        IFEQ      ' '
     C     PRJUST        ANDNE     *ZEROS
     C                   SUB       PRJUST        HENTFX            4 0
     C                   END
     C  N34HENTFX        COMP      0                                    3434
      *
      * HENTER BRUTTO FRAKT
      *
     C  N34              EXSR      SRTAB
      *
     C                   ENDSR
      *
      ******************************************
     C     FBELØ2        BEGSR
      ******************************************
      *
      * KALLES HVIS KUNDEN HAR AVTALE
      *
     C     PKGEBY        COMP      'FRA'                                  50
     C                   MOVE      *ZEROS        XPROS
     C     PKAVTT        COMP      'P'                                    34
     C  N34PKAVTT        COMP      'X'                                    34
     C   34              MOVE      PKBELØ        XPROS
     C  N34PKAVTT        COMP      'B'                                    34
     C   34              EXSR      FBELØ
     C     PKAVTT        COMP      'B'                                    34
     C  N34PKAVTT        COMP      'P'                                    34
     C  N34PKAVTT        COMP      'X'                                    34
     C   34              GOTO      UTSTP2
      *
      * N Ø K K E L   E R   N Å   T = E G E N   A V T A L E - T A B E L L
      *
     C                   MOVE      PKGEBY        YGEBY
     C                   MOVE      PKAVTT        YAVTT
      *
      * ANNEN ENHET
      *
     C                   Z-ADD     HENTF         YINT
     C                   Z-ADD     HENTF         HENTFX
      *
      * SKAL IKKE HA GEBYR VED X NÅR BRUTTOAVTALE BENYTTES
      * ELLER VED Y NÅR KALLES FRA REG LINJE
      *
     C     PKAVTT        COMP      'T'                                3434
     C   34PKAVTT        COMP      'Y'                                3434
     C   34              GOTO      UTSTP2
      *
     C                   SETOFF                                       34
     C     PRIKK         SETLL     PRIK
     C                   READ      PRIK                                   34
      *
     C  N34YKEY1         COMP      PKKEY1                             3434
     C  N34PKTEGN        IFEQ      ' '
     C     PKJUST        ANDNE     *ZEROS
     C                   SUB       PKJUST        HENTFX
     C                   END
     C  N34HENTFX        COMP      0                                    3434
     C   34              GOTO      UTSTP2
      *
     C                   MOVE      PKBELØ        PRBELØ
     C                   MOVE      PKTEGN        PRTEGN
     C                   MOVE      PKJUST        PRJUST
     C                   EXSR      SRTAB
      *
     C     UTSTP2        ENDSR
      *
      ******************************************
     C     SRTAB         BEGSR
      ******************************************
      *
      * PRIS PR LINJE
      *
     C                   MULT      HENTFX        PRBELØ
      *
     C     PRTEGN        IFNE      ' '
     C                   EXSR      OMREGN
     C                   END
      *
     C     XPROS         COMP      0                                  35
     C   35PRBELØ        DIV       100           XWORK            15 4
     C   35XWORK         MULT      XPROS         PRBELØ
     C   35              MOVE      '000'         PRBELØ
      *
     C                   Z-ADD(H)  PRBELØ        FABELN
     C                   Z-ADD(H)  PRBELØ        FABELV
     C                   EXSR      VALUTA
      *
     C                   ENDSR
      *
      ******************************************
     C     VALUTA        BEGSR
      ******************************************
      *
      * OMREGNER VALUTABELØP
      *
     C                   SETOFF                                       8586
     C     XSK           IFNE      'I'
     C     XSK           COMP      'S'                                868685
      *
     C   85HEVALS        COMP      '   '                                  84
     C   85
     CANN84HEVALS        COMP      FICURR                                 84
     C   86HEVALK        COMP      '   '                                  84
     C   86
     CANN84HEVALK        COMP      FICURR                                 84
     C   84              GOTO      UTVAL
      *
     C   85VALKOD        COMP      HEVALS                                 84
     C   86VALKOD        COMP      HEVALK                                 84
     C   84              GOTO      UTVAL
     C   85              MOVE      HEVALS        VALKOD
     C   86              MOVE      HEVALK        VALKOD
     C                   END
      *
     C     VALU1K        CHAIN     VALUL01                            84
     C  N84VALKU2        IFNE      0
     C                   Z-ADD     VALKU2        VALKU1
     C                   END
     C  N84FABELN        MULT      OMRFAK        XWORK            15 4
     C  N84XWORK         DIV(H)    VALKU1        FABELV
     C   85              MOVE      HEVALS        VALKOD
     C   86              MOVE      HEVALK        VALKOD
     C     UTVAL         TAG
     C                   SETOFF                                       848586
     C     FABELN        IFGT      0
     C                   EXSR      MSALDO
     C                   EXSR      SRKUNR
     C                   EXSR      PSALDO
     C                   EXSR      DECI
     C                   UPDATE    FAKTR
     C                   ELSE
     C                   DELETE    FAKTR
     C                   EXSR      MSALDO
     C                   END
      *
     C                   ENDSR
      *
      ******************************************
N01  C     VALUT2        BEGSR
N01   ******************************************
N01   *
N01   * OMREGNER VALUTABELØP
N01   *
N01  C                   SETOFF                                       8586
N01  C     XSK           IFNE      'I'
N01  C     XSK           COMP      'S'                                868685
N01   *
N01  C   85HEVALS        COMP      '   '                                  84
N01  C   85
N01  CANN84HEVALS        COMP      FICURR                                 84
N01  C   86HEVALK        COMP      '   '                                  84
N01  C   86
N01  CANN84HEVALK        COMP      FICURR                                 84
N01  C   84              GOTO      UTVAL2
N01   *
N01  C   85VALKOD        COMP      HEVALS                                 84
N01  C   86VALKOD        COMP      HEVALK                                 84
N01  C   84              GOTO      UTVAL2
N01  C   85              MOVE      HEVALS        VALKOD
N01  C   86              MOVE      HEVALK        VALKOD
N01  C                   END
N01   *
N01  C     VALU1K        CHAIN     VALUL01                            84
N01  C  N84VALKU2        IFNE      0
N01  C                   Z-ADD     VALKU2        VALKU1
N01  C                   END
N01  C  N84FABELN        MULT      OMRFAK        XWORK            15 4
N01  C  N84XWORK         DIV(H)    VALKU1        FABELV
N01  C   85              MOVE      HEVALS        VALKOD
N01  C   86              MOVE      HEVALK        VALKOD
N01  C     UTVAL2        TAG
     C                   SETOFF                                       848586
     C                   ENDSR
      *
      ******************************************
     C     OMREGN        BEGSR
      ******************************************
     C     PRTEGN        COMP      '-'                                    36
     C   36              SUB       PRJUST        PRBELØ
     C  N36              ADD       PRJUST        PRBELØ
      *
     C                   ENDSR
      ******************************************
     C     INIT          BEGSR
      ******************************************
     C     *ENTRY        PLIST
     C                   PARM                    UAVD              4 0
     C                   PARM                    UOPD              7 0
     C     FAK4K         KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    UOPD
      *                    KFLD           YSK
     C     KUND1K        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    KUNDNR
     C     CUND1K        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    FAKUNR
     C     KEYGE         KLIST
     C                   KFLD                    KGEUNI
     C                   KFLD                    FAVK
     C                   MOVE      'GE'          KGEUNI
      *
     C     KODTVK        KLIST
     C                   KFLD                    XVUNI
     C                   KFLD                    UAVD
     C                   MOVE      'V'           XVUNI             1
     C     HEADFK        KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    UOPD
     C     VALU1K        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    VALKOD
     C     KOAKEY        KLIST
     C                   KFLD                    YKOAUN
     C                   KFLD                    YKOAAV
     C     PRIKK         KLIST
     C                   KFLD                    YKNR
     C                   KFLD                    YAVTN
     C                   KFLD                    YGEBY
     C                   KFLD                    YAVTT
     C                   KFLD                    YINT
     C                   KFLD                    YLNR
     C     PRIK1K        KLIST
     C                   KFLD                    YKNR
     C                   KFLD                    YFRAN
     C                   KFLD                    YOPDT
     C                   KFLD                    YAVTN
     C                   KFLD                    YGEBY
     C                   KFLD                    YLNR
     C     PRIKEY        KLIST
     C                   KFLD                    YTNR
     C                   KFLD                    YVK
     C                   KFLD                    YINTR
      *
     C                   MOVE      'A'           YKOAUN            1
     C                   MOVE      *ZEROS        YKOAAV            4 0
     C                   MOVE      'M'           YST               1
      *                    MOVE ' '       YSK     1
     C                   MOVE      *ZEROS        YKNR              8
     C                   MOVE      *BLANKS       YFRAN             3
     C                   MOVE      *BLANKS       YOPDT             2
     C                   MOVE      *BLANKS       YAVTN             1
     C                   MOVE      *BLANKS       YGEBY             3
     C                   MOVE      *BLANKS       YAVTT             1
     C                   MOVE      *ZEROS        YINT              5 0
     C                   MOVE      *BLANKS       YTNR              1
     C                   MOVE      *BLANKS       YVK               3
     C                   MOVE      *ZEROS        YINTR             5 0
     C                   MOVE      *ZEROS        YLNR              4 0
      *
     C     FIFIRM        SETLL     FIRM
     C                   READ      FIRM                                   33
      *
     C                   ENDSR
      ***********************************************
     C     SRKUNR        BEGSR
      ****************************************************
      *
     C     FASK          IFEQ      'S'
     C                   MOVE      HEKNSF        FAKUNR
     C                   END
     C     FASK          IFEQ      'K'
     C                   MOVE      HEKNKF        FAKUNR
     C                   END
     C     FASK          IFEQ      'A'
     C                   MOVE      HEKNA         FAKUNR
     C                   END
     C                   ENDSR
      ***********************************************
     C     MSALDO        BEGSR
      ***********************************************
     C     CUND1K        CHAIN     CUNDL01                            49
     C  N49              SUB       XBELØN        SYSALU
     C  N49              EXCEPT    NYSALD
      *
     C                   ENDSR
      *
      ***********************************************
     C     PSALDO        BEGSR
      ***********************************************
     C     CUND1K        CHAIN     CUNDL01                            49
     C  N49              ADD       FABELN        SYSALU
     C  N49              EXCEPT    NYSALD
      *
     C                   ENDSR
      ***********************************************
     C     TESTFA        BEGSR
      ***********************************************
      * AVSLUTTER PROGRAM HVIS DET ER FAKTURERT TOL ELLER MVA
      * *IN33 SETTES DA PÅ
      *
     C                   SETOFF                                       41
     C     FAK4K         SETLL     FAK2
      *
     C     STLEST        TAG
     C     FAK4K         READE     FAK2                                   41
     C   41              GOTO      UTTEST
     C                   MOVE      FABELN        XBELØN           13 2
      *
      * HOPPER UT HVIS FAKTURERT TIDLIGERE TOL/MVA
      *
     C     FAOPKO        IFNE      ' '
     C     FAOPKO        ANDNE     'C'
      *
     C     FAVK          IFEQ      'TOL'
     C     FAVK          OREQ      'MVA'
     C                   SETON                                        33
     C                   GOTO      UTTEST
     C                   END
     C                   END
     C                   GOTO      STLEST
     C     UTTEST        ENDSR
      *
      ***********************************************
     C     TESTIO        BEGSR
      ***********************************************
s02  C*    PRJOIK        KLIST
s02  C*                  KFLD                    PKKNR
s02  C*                  KFLD                    PKAVTN
s02  C*                  KFLD                    PKLNR
      *                 (= PRGE (Urspr/Franka/Opd.ty.) + PRLK (landk.)
      *                  + PRFS (FRASTED) + PRTS (Tilsted))
      *          contains all include/omit conditions possible for one
      *          priceline (max. include or omit per condition)
      *
      * TEST LK INC/OMIT PÅ "UTENLANDS-SIDEN" (=HELKA VED SPEDISJ.OPPD)
     C                   MOVE      HELKA         INOMLK            2
      * TRANSP.OPPD: HELKA=FRASIDE/HETRI=TILSIDE (HVIS NORGE, BRUK TIL)
     C     HEUR          IFEQ      'H'
     C     HELKA         IFEQ      FILAND
     C                   MOVE      HETRI         INOMLK            2
     C                   ELSE
     C     HETRI         IFNE      FILAND
     C                   MOVE      HETRI         INOMLK            2
     C                   END
     C                   END
     C                   END
      *
     C                   SETOFF                                       7880
      *
      * HOPPER OVER GENERELLE GEBYRER FRA BRUTTO VED AVTALE
      *
      *
s02  C*    PRJOIK        CHAIN     PRJOIN                             80
s02  C* N80              DO                                                     I
      * ______________________________
      * Ursprungskode - Include / Omit
      * """"""""""""""""""""""""""""""
     C     PKURIO        IFNE      ' '                                          II
     C     PKURIO        IFEQ      'I'                                          III
      * Ved include må begrepet finnes i -arrayet (->80 on)
     C                   SETOFF                                       80
     C     HEUR          LOOKUP    UR                                     80
     C  N80              SETON                                        78
     C                   ELSE
      * Ved omit må begrepet IKKE finnes i -arrayet (->80 off)
     C                   SETOFF                                       80
     C     HEUR          LOOKUP    UR                                     80
     C   80              SETON                                        78
     C                   END                                                    III
     C                   END                                                    II
      * ___________________________________
      * Frankatur/Oppdt. er låst ved brutto
      * """""""""""""""""""""""""""""""""""
     C     PKKNR         IFNE      '    ALLE'                                   II
      * ______________________________
      * Frankatur     - Include / Omit
      * """"""""""""""""""""""""""""""
     C  N78PKFRIO        IFNE      ' '                                          III
     C     PKFRIO        IFEQ      'I'                                          IIII
      * Ved include må begrepet finnes i -arrayet (->80 on)
     C                   SETOFF                                       80
     C     HEFR          LOOKUP    FR                                     80
     C  N80              SETON                                        78
     C                   ELSE
      * Ved omit måbegrepet IKKE finnes i -arrayet (->80 off)
     C                   SETOFF                                       80
     C     HEFR          LOOKUP    FR                                     80
     C   80              SETON                                        78
     C                   END                                                    IIII
     C                   END                                                    III
      *
      * ______________________________
      * Oppdr.type    - Include / Omit
      * """"""""""""""""""""""""""""""
     C  N78PKOTIO        IFNE      ' '                                          III
     C     PKOTIO        IFEQ      'I'                                          IIII
      * Ved include må begrepet finnes i -arrayet (->80 on)
     C                   SETOFF                                       80
     C     HEOT          LOOKUP    OT                                     80
     C  N80              SETON                                        78
     C                   ELSE
      * Ved omit måbegrepet IKKE finnes i-arrayet (->80 off)
     C                   SETOFF                                       80
     C     HEOT          LOOKUP    OT                                     80
     C   80              SETON                                        78
     C                   END                                                    IIII
     C                   END                                                    III
      *
     C                   END                                                    II
      *
      * ______________________________
      * Landkode     - Include / Omit
      * """"""""""""""""""""""""""""""
     C  N78PKLKIO        IFNE      ' '                                          II
     C     PKLKIO        IFEQ      'I'                                          III
      * Ved include må begrepet finnes i -arrayet (->80 on)
     C                   SETOFF                                       80
     C     INOMLK        LOOKUP    LK                                     80
     C  N80              SETON                                        78
     C                   ELSE
      * Ved omit må begrepet IKKE finnes i -arrayet (->80 off)
     C                   SETOFF                                       80
     C     INOMLK        LOOKUP    LK                                     80
     C   80              SETON                                        78
     C                   END                                                    III
     C                   END                                                    II
      *
      * ______________________________
      * Frasted      - Include / Omit
      * """"""""""""""""""""""""""""""
     C  N78PKFSIO        IFNE      ' '                                          II
     C     PKFSIO        IFEQ      'I'                                          III
      * Ved include må begrepet finnes i -arrayet (->80 on)
     C                   SETOFF                                       80
     C     HESDF         LOOKUP    FS                                     80
     C  N80              SETON                                        78
     C                   ELSE
      * Ved omit måbegrepet IKKE finnes i -arrayet (->80 off)
     C                   SETOFF                                       80
     C     HESDF         LOOKUP    FS                                     80
     C   80              SETON                                        78
     C                   END                                                    III
     C                   END                                                    II
      *
      * ______________________________
      * Tilsted      - Include / Omit
      * """"""""""""""""""""""""""""""
     C  N78PKTSIO        IFNE      ' '                                          II
     C     PKTSIO        IFEQ      'I'                                          III
      * Ved include må begrepet finnes i -arrayet (->80 on)
     C                   SETOFF                                       80
     C     HESDT         LOOKUP    TS                                     80
     C  N80              SETON                                        78
     C                   ELSE
      * Ved omit måbegrepet IKKE finnes i -arrayet (->80 off)
     C                   SETOFF                                       80
     C     HESDT         LOOKUP    TS                                     80
     C   80              SETON                                        78
     C                   END                                                    III
     C                   END                                                    II
      * ______________________________
      * Laste/lossested - Include / Omit
      *(Gebyrer som er betinget av valgfrie felt tas
      * aldri med dersom feltet i oppdraget er blankt)
      * """"""""""""""""""""""""""""""
     C  N78PKLSIO        IFNE      ' '                                          II
     C     HESDL         IFEQ      *BLANKS                                      III
     C                   SETON                                        78
     C                   ELSE
     C     PKLSIO        IFEQ      'I'                                          IIII
      * Ved include må begrepet finnes i -arrayet (->80 on)
     C                   SETOFF                                       80
     C     HESDL         LOOKUP    LS                                     80
     C  N80              SETON                                        78
     C                   ELSE
      * Ved omit måbegrepet IKKE finnes i -arrayet (->80 off)
     C                   SETOFF                                       80
     C     HESDL         LOOKUP    LS                                     80
     C   80              SETON                                        78
     C                   END                                                    IIII
     C                   END                                                    III
     C                   END                                                    II
      *
s02  C*                  END                                                    I
      *
      * _______________________________________________________________
      * UTLØPSDATO (indikator 79 varsler at minsten linje i avt utløpt)
      * """""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     PKDT          IFGT      0
     C  N78PKDT          COMP      HEDTOP                               78
      *
     C   78              SETON                                        33
      *
     C                   END
      * Linjen ok? (indikator 78 varsler at denne linje ikke gjelder)
      *
     C     UTGIO         ENDSR
      *
     OFAKTR     EADD         NYFAKT
     O                       FA01
     O                       FALI
     O                       FAAVD
     O                       FAOPD
     O                       FASK
     O                       FAVK
     O                       FAVT
     O                       FAVAL
     O                       FABELV
     O                       FAKDM
     O                       FABELN
     O                       FAKDA
     O                       FA01B
     O                       FAKDUL
     O                       FAKDUK
     O                       FABELU
     O                       FAKUNR
     OKUNDR     E            NYSALD
     O                       SYSALU
