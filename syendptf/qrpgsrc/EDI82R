********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
12287 * 01 YBC PTF12 Oppdater på NCTS fase 5
********************************************************************
     FEDI85F    IF   E             DISK
     FEDIS2     UF   E           K DISK
     FEDIM6     UF   E           K DISK
     FSADH      UF   E           K DISK    USROPN
     FSAEH      UF   E           K DISK    USROPN
     FTRUH      UF   E           K DISK    USROPN
     FTRIH      UF   E           K DISK    USROPN
N01  FNCTSEH    UF   E           K DISK    USROPN
N01  F                                     PREFIX(P5_)
N01  FNCTSIH    UF   E           K DISK    USROPN
N01  F                                     PREFIX(P5_)
     FTRUGH     UF   E           K DISK    USROPN
     FSVXH      UF   E           K DISK    USROPN
     F                                     PREFIX(SV_)
     FSVNH      UF   E           K DISK    USROPN
     F                                     PREFIX(SV_)
     FSVXGH     UF   E           K DISK    USROPN
     F                                     PREFIX(SV_)
     D                 DS
     D  RDATA                  1     80
     D  RDELIV                36     36
     D  R0020                 37     44
     D                 DS
     D  W0020                  1     14
      *//////////////
      * MAIN LOGIC //
      *//////////////
     C                   EXSR      SRA
     C                   EXSR      SRB
     C                   MOVE      '1'           *INLR
      *////////////////////////////////////////////////////////////
      *  Oppdater Sendningslogg                               ACK /
      *////////////////////////////////////////////////////////////
     C     ACK           BEGSR
      * Hente sendningslogg
     C                   MOVEL     R0020         W0020
     C     W0020         CHAIN     EDIS2                              52
     C  N52SST           IFLT      'C'
      * Oppdatere sendningslogg med status=C (Hentet av TVINN)
     C     SST           IFEQ      'B'
     C                   MOVE      'C'           SST
     C                   END
     C                   MOVE      WDT           SDT
     C                   MOVE      WTM           STM
      *
     C                   UPDATE    EDISR
      * Oppdatere meldingslogg med samme status som sendningslogg
     C     SSN           CHAIN     EDIM6                              53
     C     *IN53         DOWEQ     '0'
      *
     C                   MOVE      'C'           MST
     C                   MOVE      WDT           MDT
     C                   MOVE      WTM           MTM
     C                   UPDATE    EDIMR
      *
     C     M0065         IFEQ      'CUSDEC'
     C     *IN54         ANDEQ     '0'
     C     *IN55         ANDEQ     '0'
     C     *IN57         ANDEQ     '0'
     C     *IN58         ANDEQ     '0'
     C     M0065         OREQ      'SANCRT'
     C     *IN59         ANDEQ     '0'
     C     *IN60         ANDEQ     *OFF
     C                   EXSR      SRC
     C                   ENDIF
      *
     C     SSN           READE     EDIM6                                  53
     C  N53              END
      *
     C                   END
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Init                                                   SRA
      *////////////////////////////////////////////////////////////
     C     SRA           BEGSR
      *__________________
      * Definiere Nøkkler
      *""""""""""""""""""
     C     KEYA          KLIST
     C                   KFLD                    MAVD
     C                   KFLD                    MTDN
     C     KEYB          KLIST
     C                   KFLD                    MAVD
     C                   KFLD                    MTDN
     C* Hente dagens dato og klokkeslett.
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
      *________________________________________
      * Open files from SYSPED400, if installed
      *""""""""""""""""""""""""""""""""""""""""
     C                   OPEN      SADH                                 54
     C                   OPEN      SAEH                                 55
     C                   OPEN      TRUH                                 57
     C                   OPEN      TRIH                                 58
     C                   OPEN      TRUGH                                59
     C                   OPEN      SVXH                                 60
     C                   OPEN      SVNH                                 60
     C                   OPEN      SVXGH                                60
N01  C                   OPEN      NCTSEH                               61
N01  C                   OPEN      NCTSIH                               62
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Les input                                            SRB /
      *////////////////////////////////////////////////////////////
     C     SRB           BEGSR
      *
     C                   READ      EDI85F                                 51
     C     *IN51         DOWEQ     '0'
      *
     C     RDELIV        IFEQ      'D'                                          ALLE TYPER
     C                   EXSR      ACK
     C                   END
      *
     C                   READ      EDI85F                                 51
     C  N51              END
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Update SYSPED400                                     SRB /
      *////////////////////////////////////////////////////////////
     C     SRC           BEGSR
     C                   SELECT
     C                   WHEN      M1001 = '929'
     C     KEYA          CHAIN     SADH                               56
     C     *IN56         IFEQ      '0'
     C                   MOVE      'C'           SIST
     C                   UPDATE    SADHR
     C                   ENDIF
     C                   WHEN      M1001 = '830'
     C     KEYA          CHAIN     SAEH                               56
     C     *IN56         IFEQ      '0'
     C                   MOVE      'C'           SEST
     C                   UPDATE    SAEHR
     C                   ENDIF
     C                   WHEN      M1001 = 'TET'
     C     KEYA          CHAIN     TRUH                               56
     C     *IN56         IFEQ      '0'
     C                   MOVE      'C'           THST
     C                   UPDATE    TRUHR
N01  C                   ELSE
N01  C     KEYA          CHAIN     NCTSEH                             56
N01  C     *IN56         IFEQ      '0'
N01  C                   MOVE      'C'           P5_THST
N01  C                   UPDATE    NCTSEHR
N01  C                   ENDIF
     C                   ENDIF
     C                   MOVEL     M0068         TGGNR
     C     TGGNR         CHAIN     TRUGH                              56
     C     *IN56         IFEQ      '0'
     C                   MOVE      'P'           TGST
     C                   UPDATE    TRUGHR
     C                   ENDIF
     C                   WHEN      M1001 = 'TEI'
     C     KEYA          CHAIN     TRIH                               56
     C     *IN56         IFEQ      '0'
     C     TIMF          IFEQ      '007'
     C                   MOVE      'C'           TIST
     C                   ELSE
     C                   MOVE      'F'           TIST
     C                   END
     C                   UPDATE    TRIHR
N01  C                   ELSE
N01  C     KEYA          CHAIN     NCTSIH                             56
N01  C     *IN56         IFEQ      '0'
N01  C     TIMF          IFEQ      '007'
N01  C                   MOVE      'C'           P5_TIST
N01  C                   ELSE
N01  C                   MOVE      'F'           P5_TIST
N01  C                   ENDIF
N01  C                   UPDATE    NCTSIHR
N01  C                   ENDIF
     C                   ENDIF
      *
     C                   WHEN      M1001 = 'STE'
     C     KEYB          CHAIN     SVXH                               56
     C     *IN56         IFEQ      '0'
     C     TIMF          IFEQ      '007'
     C                   MOVE      'C'           SV_THST
     C                   ELSE
     C                   MOVE      'L'           SV_THST
     C                   END
     C                   UPDATE    SVXHR
     C                   ENDIF
     C                   MOVEL     M0068         SV_TGGNR
     C     SV_TGGNR      CHAIN     SVXGH                              56
     C     *IN56         IFEQ      '0'
     C                   MOVE      'P'           SV_TGST
     C                   UPDATE    SVXGHR
     C                   ENDIF
      *
     C                   WHEN      M1001 = 'STI'
     C     KEYB          CHAIN     SVNH                               56
     C     *IN56         IFEQ      '0'
     C     TIMF          IFEQ      '007'
     C                   MOVE      'C'           SV_TIST
     C                   ELSE
     C                   MOVE      'L'           SV_TIST
     C                   END
     C                   UPDATE    SVNHR
     C                   ENDIF
      *
     C                   ENDSL
     C                   ENDSR
