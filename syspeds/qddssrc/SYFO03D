     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HELP
     A                                      ALTHELP(CA01)
      * "NOERASE" MÅ HA FOR Å IKKE TØMME BILDE SOM MAN KOMME FRA
     A          R NOERASE
     A                                      ASSUME
     A                                  1  2' '
      * MAN KUN DEFINNERE VINDU PÅ REKORDNIVÅ
     A          R WINDOWSZ
     A                                      WINDOW(*DFT 18 75 *NOMSGLIN)
     A                                      OVERLAY
     A          R SYFO03DA                  SFL
     A            WSNR           1A  I  6  2VALUES(' ' '5' '9')
     A            HEAVD          4  0O  6  5EDTCDE(4)
     A            HEOPD          7  0O  6 10EDTCDE(4)
     A            HESG           3A  O  6 18
     A            WSKF           2A  B  6 22
     A  61                                  DSPATR(RI)
     A  61                                  DSPATR(HI)
     A            WSKFT1         1A  B  6 25
     A  62                                  DSPATR(RI)
     A  62                                  DSPATR(HI)
     A            WSFT1A        30A  B  6 28
     A            WSFT1B        15A  B  6 59
     A            HEKF           2A  H
     A            HEKFT1         1A  H
     A            HEFT1A        30A  H
     A            HEFT1B        15A  H
     A            HEST           1A  O  7  2
     A            WSDT           6Y 0O  7  4EDTCDE(4)
     A            HEGN          15A  O  7 11
     A            HEPOS1         7A  H
     A            WSPOS1         2A  O  7 27
     A            HESDF          5A  O  7 30
     A            HESDT          5A  O  7 36
     A            WSNT           3  0O  7 42EDTCDE(4)
     A            WSVKT          5  0O  7 46EDTCDE(4)
     A            WSSELG         8A  O  7 52
     A            WSKJØP         8A  O  7 61
     A            HEOT           2A  O  7 70
     A            HEFR           3A  O  7 73
     A          R SYFO03DB                  SFLCTL(SYFO03DA)
     A                                      SFLSIZ(0006)
     A                                      SFLPAG(0005)
     A                                      WINDOW(WINDOWSZ)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                  1  2'SYSPED/SYFO03D'
     A                                      COLOR(BLU)
     A                                  1 21MSGCON(040 YBC0555 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                  2  2MSGCON(012 YBC0556 *LIBL/SYMSGF)
     A            UPRO           8Y 0O  2 15EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  2 26MSGCON(008 JOV0761 *LIBL/SYMSGF)
     A N50                              2 35MSGCON(025 JOV0762 *LIBL/SYMSGF)
     A  50                              2 35MSGCON(025 JOV0763 *LIBL/SYMSGF)
     A            ZRECNO         4S 0O  2 66SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  3  2MSGCON(060 YBC0557 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(072 YBC0558 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                  5  2MSGCON(072 YBC0559 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A          R SYFO03DC
     A                                      WINDOW(WINDOWSZ)
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CA03(03 'AVSLUTT')
     A                                      CF04(04 'Søking')
     A                                      CA05(05 'Frisk')
     A                                      CA07(07 'MODUS')
     A                                      CA10(10 'Oppheve utl.f.b')
     A                                      CA12(12 'Cancel')
     A                                      SLNO(16)
     A                                      CLRL(03)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A            WSUT9T         2A  O  1  2COLOR(WHT)
     A            WSUT9A         3A  O  1  5
     A            WSUT8T         2A  O  1  9COLOR(WHT)
     A            WSUT8A         3A  O  1 12
     A            WSUT7T         2A  O  1 16COLOR(WHT)
     A            WSUT7A         3A  O  1 19
     A            WSUT6T         2A  O  1 23COLOR(WHT)
     A            WSUT6A         3A  O  1 26
     A            WSUT5T         2A  O  1 30COLOR(WHT)
     A            WSUT5A         3A  O  1 33
     A            WSUT4T         2A  O  1 37COLOR(WHT)
     A            WSUT4A         3A  O  1 40
     A            WSUT3T         2A  O  1 44COLOR(WHT)
     A            WSUT3A         3A  O  1 47
     A            WSUT2T         2A  O  1 51COLOR(WHT)
     A            WSUT2A         3A  O  1 54
     A            WSUT1T         2A  O  1 58COLOR(WHT)
     A            WSUT1A         3A  O  1 61
     A            WSU10T         2A  O  1 65COLOR(WHT)
     A            WSU10A         3A  O  1 68
     A                                  2  2MSGCON(070 YBC0560 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        70A  O  3  2MSGID(&MSGNR GSSMSGF)
     A                                      COLOR(WHT)
     A          R SYFO03DG
     A                                      WINDOW(WINDOWSZ)
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CA03(03 'AVSLUTT')
     A                                      CF04(04 'Søking')
     A                                      CA05(05 'Frisk')
     A                                      CA07(07 'MODUS')
     A                                      CA10(10 'Oppheve utl.f.b')
     A                                      CA12(12 'Cancel')
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1  2'SYSPED/SYFO03R'
     A                                      COLOR(BLU)
     A                                  1 21MSGCON(040 YBC0555 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                  2  2MSGCON(012 YBC0556 *LIBL/SYMSGF)
     A            UPRO           8Y 0O  2 15EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  2 26MSGCON(008 JOV0761 *LIBL/SYMSGF)
     A N50                              2 35MSGCON(025 JOV0762 *LIBL/SYMSGF)
     A  50                              2 35MSGCON(025 JOV0763 *LIBL/SYMSGF)
     A                                  3  2MSGCON(060 YBC0557 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(072 YBC0558 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                  5  2MSGCON(072 YBC0559 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                 17  2MSGCON(070 YBC0560 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        70A  O 18  2MSGID(&MSGNR GSSMSGF)
     A                                      COLOR(WHT)
      *
     A          R SYFO03DX
     A                                      WINDOW(*DFT 7 45 *NOMSGLIN)
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF04(04 'Søking')
     A                                      CA12(12 'Cancel')
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/SYFO03R'
     A                                      COLOR(BLU)
     A                                  1 21MSGCON(20 YBC0561 SYMSGF)
     A                                      DSPATR(HI)
     A                                  3  2MSGCON(22 YBC0562 SYMSGF)
     A            WSKDGM         2A  B  3 25DSPATR(UL)
     A  71                                  DSPATR(PC)
     A  71                                  DSPATR(HI)
     A                                  4  2MSGCON(22 YBC0563 SYMSGF)
     A            WSKDNY         2A  B  4 25DSPATR(UL)
     A  72                                  DSPATR(PC)
     A  72                                  DSPATR(HI)
     A                                  4 28MSGCON(15 YBC0564 SYMSGF)
     A                                  6  2MSGCON(18 DIV0032 SYMSGF)
     A                                      COLOR(BLU)
     A                                  6 22MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        40A  O  7  2MSGID(&MSGNR GSSMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
