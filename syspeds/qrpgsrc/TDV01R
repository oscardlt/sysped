     H DATEDIT(*YMD)
      ***********************************************************
      *** FOREESPØRSEL PÅ INKOMMENDE FWB                      ***
      ***                                                     ***
      ***********************************************************
      *
     FLOGF      UF   E             DISK
     FLOGL01    IF   E           K DISK
     F                                     RENAME(LOGR:LOG1)
     F                                     INFDS(LOG1DS)
     FFWB01L    IF   E           K DISK
     FFZB01L    IF   E           K DISK
     FFSU00L1   IF   E           K DISK
     FCPFL01    IF   E           K DISK
     FTELL      UF   E           K DISK
     FHEADF     IF A E             DISK
     FFREETXT   IF A E             DISK
     FTDV01D    CF   E             WORKSTN
     F                                     SFILE(TDV01H50:ZRECNO)
      ***********************************************************
      *    Tabeller
      *
     D TXT             S             30    DIM(12) CTDATA PERRCD(1)
     D AR1             S              1    DIM(30)
     D AR2             S              1    DIM(30)
     D CD              S              3    DIM(16) CTDATA PERRCD(1)
     D TX              S             69    DIM(16) ALT(CD)
      ***********************************************************
     D                 DS
     D  XIDAGÅ                 1      4  0
     D  XIDAGM                 5      6  0
     D  XIDAGD                 7      8  0
     D  XIDAG                  1      8  0
     D                 DS
     D  DATE1                  1      2  0
     D  DATE2                  3      4  0
     D  DATE3                  5      6  0
     D  LGDATE                 1      6  0
     D LOG1DS          DS
     D  RECNO1               397    400B 0
     D                 DS
     D  WAWB                   1     12
     D  WAWBPR                 1      3
     D  WAWBST                 4      4
     D  WAWBNO                 5     12
     D                 DS
     D  WHEGN                  1     11
     D  WHEGNF                 1      1
     D  WHEGNA                 2      9
     D                 DS
     D  CPDATA                 1    138
     D  CPDAT3                 1      3
     D                 DS
     D  WTIME                  1     14  0
     D  WTIMEY                11     14  0
      *
      *______________________
      * Initiering
      *""""""""""""""""""""""
     C                   EXSR      INIT
      *___________________________
      * Hovedlogikk
      *"""""""""""""""""""""""""""
      *______________
      * Clear Subfil
      *""""""""""""""
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     TDV01K60
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     0             ZRECNO
      *_____________
      * Fyll Subfil
      *"""""""""""""
     C     *HIVAL        SETLL     LOGL01                             77
      *
     C     START1        TAG
     C                   DO        14
     C     NEXT          TAG
     C                   READ      LOGL01                                 94
     C     *IN94         IFEQ      '0'
     C     LGSTAT        CABNE     'C'           NEXT
     C     LGMSID        IFNE      'FWB'
     C     LGMSID        ANDNE     'FZB'
     C                   GOTO      NEXT
     C                   ENDIF
     C     LGTR          CABNE     'R'           NEXT
     C     LGMSID        IFEQ      'FWB'
     C     LGRECN        CHAIN     FWB00R                             90
     C     *IN90         CABEQ     '1'           NEXT
     C                   MOVEL     W0542         AVS
     C                   MOVEL     W0642         MOT
     C                   MOVEL     *BLANKS       HAWB
     C                   ENDIF
     C     LGMSID        IFEQ      'FZB'
     C     LGRECN        CHAIN     FZB00R                             90
     C     *IN90         CABEQ     '1'           NEXT
     C                   MOVEL     O0542         AVS
     C                   MOVEL     O0642         MOT
     C                   Z-ADD     O0222         W0211
     C                   Z-ADD     O0224         W0213
     C                   MOVEL     O0242         W0221
     C                   MOVEL     O0243         W0222
     C                   MOVEL     O0253         W0233
     C                   MOVEL     O0254         W0234
     C                   Z-ADD     O0255         W0235
     C                   MOVEL     'H'           HAWB
     C                   ENDIF
     C                   Z-ADD     RECNO1        RECNUM
     C                   MOVEL     LGMSID        SFMSID
     C                   ADD       1             ZRECNO
     C                   WRITE     TDV01H50
     C                   END                                                    <*IN94
     C  N94              ENDDO                                                  <DO 14
      *
     C                   Z-ADD     ZRECNO        YRECNO            5 0
      *________________
      * Display Subfil
      *""""""""""""""""
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C     VISB30        TAG
     C                   EXFMT     TDV01B30
     C     *IN03         IFNE      '1'
     C     *IN12         ANDNE     '1'
     C                   GOTO      VISB30
     C                   END                                                    <*IN12
      *
     C                   ELSE                                                   <ZRECNO
     C                   WRITE     TDV01B40
     C                   EXFMT     TDV01K60
     C                   END                                                    <ZRECNO
      *
     C                   MOVEA     '0000000'     *IN(30)
      *
     C                   SETOFF                                       9998
      *** F3=Gå ut
     C     *IN03         IFEQ      '1'
     C                   GOTO      ENDPGM
     C                   END                                                    <*IN03
      *** F5=Frisk opp
     C     *IN05         IFEQ      '1'
     C                   GOTO      START
     C                   END                                                    <*IN03
      *** Roll
     C     *IN25         IFEQ      '1'
     C     *IN94         ANDEQ     '0'                                           EOF
     C                   Z-ADD     YRECNO        ZRECNO
     C                   MOVE      '0'           *IN25
     C                   GOTO      START1
     C                   END                                                    <*IN25
      *** Finn valg
     C     ZRECNO        IFNE      0
     C                   EXSR      GETOPT
      *** F3=Gå ut
     C     *IN03         IFEQ      '1'
     C                   GOTO      ENDPGM
     C                   END                                                    <*IN03
      *** F12=Annuller
     C     *IN12         IFEQ      '1'
     C                   GOTO      START3
     C                   END                                                    <*IN12
     C                   END                                                    <ZRECNO
      *
     C                   GOTO      START3
      *
     C     ENDPGM        TAG
     C                   MOVE      '1'           *INLR
     C                   RETURN
      *
      ***********************************************
     C     GETOPT        BEGSR
      ***********************************************
     C                   Z-ADD     0             OPTNBR            2 0
     C                   SETOFF                                       919299
     C     *IN98         DOWEQ     '0'
     C                   READC     TDV01H50                               98
     C     *IN98         IFEQ      '1'
     C     *IN99         IFEQ      '1'
     C                   MOVE      '0'           *IN98
     C                   END                                                    <*IN99
     C                   GOTO      ENDGET
     C                   END                                                    <*IN98
     C                   MOVE      *OFF          *IN68
     C                   MOVE      *OFF          *IN70
      *** 1=Velg
     C     WSNR          IFEQ      '1'
     C                   MOVE      *BLANK        WSNR
     C                   UPDATE    TDV01H50
     C                   SELECT
     C     SFMSID        WHENEQ    'FWB'
     C                   EXSR      LAGOPD
     C     SFMSID        WHENEQ    'FZB'
     C                   EXSR      LAGFZB
     C                   ENDSL
     C                   MOVE      *ON           *IN03
     C                   GOTO      ENDGET
     C                   END                                                    <WSNR
      *** 5=Vis
     C     WSNR          IFEQ      '5'
     C                   MOVE      *BLANK        WSNR
     C                   UPDATE    TDV01H50
     C     RECNUM        CHAIN(N)  LOGR                               90
     C     LGTR          IFEQ      'R'
     C                   MOVEA     TXT(2)        SRTXT
     C                   ELSE                                                   <LGTR
     C                   MOVEA     TXT(1)        SRTXT
     C                   END                                                    <LGTR
     C                   SELECT
     C     LGSTAT        WHENEQ    'A'
     C                   MOVEA     TXT(3)        STATXT
     C     LGSTAT        WHENEQ    'B'
     C                   MOVEA     TXT(4)        STATXT
     C     LGSTAT        WHENEQ    'C'
     C                   MOVEA     TXT(5)        STATXT
     C     LGSTAT        WHENEQ    'D'
     C                   MOVEA     TXT(6)        STATXT
     C     LGSTAT        WHENEQ    'E'
     C                   MOVEA     TXT(7)        STATXT
     C     LGSTAT        WHENEQ    'F'
     C                   MOVEA     TXT(8)        STATXT
     C     LGSTAT        WHENEQ    'G'
     C                   MOVEA     TXT(9)        STATXT
     C     LGSTAT        WHENEQ    'H'
     C                   MOVEA     TXT(10)       STATXT
     C     LGSTAT        WHENEQ    'Z'
     C                   MOVEA     TXT(11)       STATXT
     C     LGSTAT        WHENEQ    'I'
     C                   MOVEA     TXT(12)       STATXT
     C                   ENDSL
     C                   EXFMT     TDV01B20
     C                   END                                                    <WSNR
      *** 7=Vis Copy Fil
     C     WSNR          IFEQ      '7'
     C                   MOVE      *BLANK        WSNR
     C                   UPDATE    TDV01H50
      * Kall program for visning av copy file
     C                   CALL      'SM050R'                             69
     C                   PARM                    LGRECN
     C     *IN69         IFEQ      '1'
     C                   MOVE      'BBA0022'     MSGNR
     C                   MOVE      *ON           *IN30
     C                   END                                                    <*IN69
     C                   END                                                    <WSNR
      *
     C     *IN03         IFEQ      '1'
     C                   GOTO      ENDGET
     C                   END                                                    <*IN03
      *
     C                   ENDDO
      *
     C     ENDGET        TAG
     C                   MOVE      *OFF          *IN54
      *
     C                   ENDSR
      *
      ***********************************************
     C     INIT          BEGSR
      ***********************************************
     C     *ENTRY        PLIST
     C                   PARM                    WSSG              3
     C                   PARM                    USAVD             4
     C                   PARM                    WSOPD             7 0
     C                   PARM                    PFLAGG            1
     C                   MOVE      'N'           PFLAGG
     C                   MOVE      USAVD         WSAVD             4 0
      * DUMMY READ
     C  N99
     CAN 99              READ      HEADF                                  90
     C  N99
     CAN 99              READ      FREETXT                                90
      *
     C     AWBKEY        KLIST
     C                   KFLD                    W0211
     C                   KFLD                    W0213
      *
     C     UYEAR         IFGT      50
     C     UYEAR         ADD       1900          XIDAGÅ
     C                   ELSE
     C     UYEAR         ADD       2000          XIDAGÅ
     C                   END
     C                   MOVE      UMONTH        XIDAGM
     C                   MOVE      UDAY          XIDAGD
     C                   TIME                    WTIME
      *
     C                   ENDSR
      ***********************************************
     C     LAGOPD        BEGSR
      ***********************************************
      * START Å LAGE OPPDRAG
     C     RECNUM        CHAIN(N)  LOGR                               90
     C     *IN90         IFEQ      '1'
     C     LGSTAT        ORNE      'C'
     C                   MOVE      *ON           *IN30
     C                   MOVE      *ON           *IN33
     C                   GOTO      LAG99
     C                   ENDIF
     C     LGRECN        CHAIN     FWB00R                             90
     C     *IN90         IFEQ      '1'
     C                   MOVE      *ON           *IN30
     C                   MOVE      *ON           *IN34
     C                   GOTO      LAG99
     C                   ENDIF
      *
     C                   MOVE      ' '           HEST
     C                   Z-ADD     WSAVD         HEAVD
     C     HEAVD         CHAIN     TELL                               90
     C     *IN90         IFEQ      '1'
     C                   MOVE      *ON           *IN30
     C                   MOVE      *ON           *IN35
     C                   GOTO      LAG99
     C                   ENDIF
     C                   Z-ADD     TEOPDN        HEOPD
     C                   Z-ADD     TEOPDN        WSOPD                          <PARAMETER
     C                   ADD       1             TEOPDN
     C                   UPDATE    TELLR
     C                   MOVE      'C'           HEUR
     C                   MOVEL     WSSG          HESG
     C                   MOVE      XIDAG         HEDTOP
     C                   MOVEL     W0221         HESDF
     C                   MOVEL     W0222         HESDT
     C                   Z-ADD     0             HEKNS
     C                   MOVE      'X'           HEKDFS
     C                   MOVEL     W0542         HENAS
     C                   MOVEL     W0552         HEADS1
     C                   MOVEL     W0562         HEADS2
     C                   MOVEL     *BLANKS       HEADS3
     C                   MOVEL     W0574         HEADS3
     C                   MOVE      W0564         HEADS3
     C                   Z-ADD     0             HEKNK
     C                   MOVEL     W0642         HENAK
     C                   MOVEL     W0652         HEADK1
     C                   MOVEL     W0662         HEADK2
     C                   MOVEL     *BLANKS       HEADK3
     C                   MOVEL     W0674         HEADK3
     C                   MOVE      W0664         HEADK3
     C                   MOVEL     DATE3         XPOS14            4
     C                   MOVE      DATE2         XPOS14
     C                   MOVE      *BLANKS       HEPOS1
     C                   MOVEL     XPOS14        HEPOS1
     C                   Z-ADD     W0233         HENT
     C                   Z-ADD(H)  W0235         HEVKT
     C                   Z-ADD     W11101        HEBELT
     C                   MOVEL     *BLANKS       WAWB
     C                   MOVEL     W0211         WAWBPR
     C                   MOVE      '-'           WAWBST
     C                   MOVEL     W0213         WAWBNO
     C                   MOVEL     *BLANKS       HERFA
     C                   MOVEL     WAWB          HERFA
     C                   MOVE      'F'           WHEGNF
     C                   MOVE      WAWBNO        WHEGNA
     C                   MOVEL(P)  WTIMEY        HEGN
     C                   MOVE      WHEGN         HEGN
     C                   Z-ADD     40            HETRM
     C                   MOVEL     *BLANKS       HEGM1
     C                   MOVEL     'ADR'         HEGM1
     C     LGRECN        CHAIN     FWB12R                             92
     C     *IN92         IFEQ      '0'
     C                   MOVEL     W12115        HEVS1
     C     LGRECN        READE     FWB12R                                 92
     C     *IN90         IFEQ      '0'
     C                   MOVEL     W12115        HEVS2
     C                   END
     C                   END
     C                   MOVE      W0572         HELKS
     C                   MOVE      W0672         HELKK
     C                   Z-ADD     0             WHEFBV           11 0
      * SJEKKER OM FRAKTBEREGNET VEKT SKAL FYLLES UT
     C     LGRECN        SETLL     FWB12R
     C     LGRECN        READE     FWB12R                                 92
     C     *IN92         DOWEQ     '0'
     C                   ADD       W1273         WHEFBV
     C     LGRECN        READE     FWB12R                                 92
     C                   ENDDO
     C     WHEFBV        IFGT      HEVKT
     C                   Z-ADD     WHEFBV        HEFBV
     C                   ELSE
     C                   Z-ADD     0             HEFBV
     C                   END
     C                   MOVE      XIDAG         HEDTR
      *
     C                   WRITE     HEADFR
      *
      * Når man er sikker på at oppdraget er laget kan statusen
      * oppdateres.
     C     RECNUM        CHAIN     LOGR                               90
     C     *IN90         IFEQ      '0'
     C                   MOVEL     'I'           LGSTAT
     C                   UPDATE    LOGR
     C                   END                                                    <*IN90
     C                   MOVE      'J'           PFLAGG
      *
      * Sjekker om det har kommet FSU (automatisk statusoppdatering)
      * som evt. legges inn på friteksten til kunden.
      *
     C     AWBKEY        SETLL     FSU00L1                                90
     C     *IN90         IFEQ      '1'                                          FOUND
     C     AWBKEY        READE     FSU00L1                                90
     C                   Z-ADD     0             FRTLI
      *
     C     *IN90         DOWEQ     '0'
     C     A00REC        SETLL     CPFL01
     C     A00REC        READE     CPFL01                                 91
      * "Hopper" over første posten som ikke legges ut
     C     *IN91         IFEQ      '0'
     C     A00REC        READE     CPFL01                                 91
      *
     C     *IN91         IFEQ      '0'
     C                   Z-ADD     HEAVD         FRTAVD
     C                   Z-ADD     HEOPD         FRTOPD
     C                   MOVE      XIDAG         FRTDT
     C                   MOVE      *BLANK        FRTKOD
     C                   Z-ADD     0             FRTFAK
     C     *IN91         DOWEQ     '0'
     C                   Z-ADD     1             X                 3 0
      * Hent tekst hvis det er en statuskode
     C     CPDAT3        LOOKUP    CD(X)                                  92
     C     *IN92         IFEQ      '1'                                          <FUNNET
     C                   ADD       1             FRTLI
     C                   MOVEA     TX(X)         FRTTXT
     C                   WRITE     FREETXTR
     C                   ENDIF                                                  <*IN92
     C                   ADD       1             FRTLI
     C                   MOVEL     CPDATA        FRTTXT
     C                   WRITE     FREETXTR
     C     A00REC        READE     CPFL01                                 91
     C     FRTLI         IFGE      99
     C                   Z-ADD     0             FRTLI
     C                   ENDIF                                                  <FRTLI
     C                   ENDDO                                                  <*IN91
     C                   ENDIF                                                  <*IN91
     C                   ENDIF                                                  <*IN91
      *
     C     AWBKEY        READE     FSU00L1                                90
     C                   ENDDO                                                  <*IN90
     C                   ENDIF                                                  <*IN90
      *
     C     LAG99         ENDSR
      ***********************************************
     C     LAGFZB        BEGSR
      ***********************************************
      * START Å LAGE OPPDRAG
     C     RECNUM        CHAIN(N)  LOGR                               90
     C     *IN90         IFEQ      '1'
     C     LGSTAT        ORNE      'C'
     C                   MOVE      *ON           *IN30
     C                   MOVE      *ON           *IN33
     C                   GOTO      LAGA99
     C                   ENDIF
     C     LGRECN        CHAIN     FZB00R                             90
     C     *IN90         IFEQ      '1'
     C                   MOVE      *ON           *IN30
     C                   MOVE      *ON           *IN34
     C                   GOTO      LAGA99
     C                   ENDIF
      *
     C                   MOVE      ' '           HEST
     C                   Z-ADD     WSAVD         HEAVD
     C     HEAVD         CHAIN     TELL                               90
     C     *IN90         IFEQ      '1'
     C                   MOVE      *ON           *IN30
     C                   MOVE      *ON           *IN35
     C                   GOTO      LAGA99
     C                   ENDIF
     C                   Z-ADD     TEOPDN        HEOPD
     C                   Z-ADD     TEOPDN        WSOPD                          <PARAMETER
     C                   ADD       1             TEOPDN
     C                   UPDATE    TELLR
     C                   MOVE      'C'           HEUR
     C                   MOVEL     WSSG          HESG
     C                   MOVE      XIDAG         HEDTOP
     C                   MOVEL     O0242         HESDF
     C                   MOVEL     O0243         HESDT
     C                   Z-ADD     0             HEKNS
     C                   MOVE      'X'           HEKDFS
     C                   MOVEL     O0542         HENAS
     C                   MOVEL     O0552         HEADS1
     C                   MOVEL     O0562         HEADS2
     C                   MOVEL     *BLANKS       HEADS3
     C                   MOVEL     O0574         HEADS3
     C                   MOVE      O0564         HEADS3
     C                   Z-ADD     0             HEKNK
     C                   MOVEL     O0642         HENAK
     C                   MOVEL     O0652         HEADK1
     C                   MOVEL     O0662         HEADK2
     C                   MOVEL     *BLANKS       HEADK3
     C                   MOVEL     O0674         HEADK3
     C                   MOVE      O0664         HEADK3
     C                   MOVEL     DATE3         XPOS14            4
     C                   MOVE      DATE2         XPOS14
     C                   MOVE      *BLANKS       HEPOS1
     C                   MOVEL     XPOS14        HEPOS1
     C                   Z-ADD     O0253         HENT
     C                   Z-ADD(H)  O0255         HEVKT
     C                   Z-ADD     O1172         HEBELT
     C                   MOVEL     *BLANKS       WAWB
     C                   MOVEL     O0222         WAWBPR
     C                   MOVE      '-'           WAWBST
     C                   MOVEL     O0224         WAWBNO
     C                   MOVEL     *BLANKS       HERFA
     C                   MOVEL     WAWB          HERFA
     C                   MOVEL(P)  WTIMEY        HEGN
     C     HEGN          CAT       WAWBPR        HEGN
     C     HEGN          CAT       WAWBNO        HEGN
     C                   Z-ADD     40            HETRM
     C                   MOVEL     *BLANKS       HEGM1
     C                   MOVEL     'ADR'         HEGM1
     C     LGRECN        CHAIN     FZB12R                             92
     C     *IN92         IFEQ      '0'
     C                   MOVEL     O12115        HEVS1
     C     LGRECN        READE     FZB12R                                 92
     C     *IN90         IFEQ      '0'
     C                   MOVEL     O12115        HEVS2
     C                   END
     C                   END
     C                   MOVE      O0572         HELKS
     C                   MOVE      O0672         HELKK
     C                   Z-ADD     0             WHEFBV           11 0
      * SJEKKER OM FRAKTBEREGNET VEKT SKAL FYLLES UT
     C     LGRECN        SETLL     FZB12R
     C     LGRECN        READE     FZB12R                                 92
     C     *IN92         DOWEQ     '0'
     C                   ADD       O1273         WHEFBV
     C     LGRECN        READE     FZB12R                                 92
     C                   ENDDO
     C     WHEFBV        IFGT      HEVKT
     C                   Z-ADD     WHEFBV        HEFBV
     C                   ELSE
     C                   Z-ADD     0             HEFBV
     C                   END
     C                   MOVE      XIDAG         HEDTR
      *
     C                   WRITE     HEADFR
      *
      * Når man er sikker på at oppdraget er laget kan statusen
      * oppdateres.
     C     RECNUM        CHAIN     LOGR                               90
     C     *IN90         IFEQ      '0'
     C                   MOVEL     'I'           LGSTAT
     C                   UPDATE    LOGR
     C                   END                                                    <*IN90
     C                   MOVE      'J'           PFLAGG
      *
      * Sjekker om det har kommet FSU (automatisk statusoppdatering)
      * som evt. legges inn på friteksten til kunden.
      *
     C     AWBKEY        SETLL     FSU00L1                                90
     C     *IN90         IFEQ      '1'                                          FOUND
     C     AWBKEY        READE     FSU00L1                                90
     C                   Z-ADD     0             FRTLI
      *
     C     *IN90         DOWEQ     '0'
     C     A00REC        SETLL     CPFL01
     C     A00REC        READE     CPFL01                                 91
      * "Hopper" over første posten som ikke legges ut
     C     *IN91         IFEQ      '0'
     C     A00REC        READE     CPFL01                                 91
      *
     C     *IN91         IFEQ      '0'
     C                   Z-ADD     HEAVD         FRTAVD
     C                   Z-ADD     HEOPD         FRTOPD
     C                   MOVE      XIDAG         FRTDT
     C                   MOVE      *BLANK        FRTKOD
     C                   Z-ADD     0             FRTFAK
     C     *IN91         DOWEQ     '0'
     C                   Z-ADD     1             X                 3 0
      * Hent tekst hvis det er en statuskode
     C     CPDAT3        LOOKUP    CD(X)                                  92
     C     *IN92         IFEQ      '1'                                          <FUNNET
     C                   ADD       1             FRTLI
     C                   MOVEA     TX(X)         FRTTXT
     C                   WRITE     FREETXTR
     C                   ENDIF                                                  <*IN92
     C                   ADD       1             FRTLI
     C                   MOVEL     CPDATA        FRTTXT
     C                   WRITE     FREETXTR
     C     A00REC        READE     CPFL01                                 91
     C     FRTLI         IFGE      99
     C                   Z-ADD     0             FRTLI
     C                   ENDIF                                                  <FRTLI
     C                   ENDDO                                                  <*IN91
     C                   ENDIF                                                  <*IN91
     C                   ENDIF                                                  <*IN91
      *
     C     AWBKEY        READE     FSU00L1                                90
     C                   ENDDO                                                  <*IN90
     C                   ENDIF                                                  <*IN90
      *
     C     LAGA99        ENDSR
     C***********************************************
     C     CHKNUM        BEGSR
     C***********************************************
     C                   MOVE      *OFF          *IN91
     C                   Z-ADD     30            ÆX                3 0
     C                   Z-ADD     30            ÆY                3 0
     C                   MOVEA     *BLANKS       AR1
     C                   MOVEA     *BLANKS       AR2
     C                   MOVEA     WRK           AR1
     C     WRK           CABEQ     *BLANKS       CHK99                    92
     C* POSISJONER TIL SISTE TEGN I FELTET
     C     AR1(ÆX)       DOWEQ     *BLANK
     C                   SUB       1             ÆX
     C                   ENDDO
     C* ERSTATT BLANK MED NULL OG SJEKK OM DET BARE ER TALL
     C     ÆX            DOWGT     0
     C     AR1(ÆX)       IFEQ      *BLANK
     C                   MOVE      '0'           AR1(ÆX)
     C                   ENDIF
     C     AR1(ÆX)       IFGE      '0'
     C     AR1(ÆX)       ANDLE     '9'
     C                   MOVE      AR1(ÆX)       AR2(ÆY)
     C                   SUB       1             ÆX
     C                   SUB       1             ÆY
     C                   ELSE                                                   <AR1,ÆX
     C* FELTET INNEHOLDER IKKE BARE TALL. 91 BLIR SATT PÅ
     C                   MOVE      *ON           *IN91
     C                   GOTO      CHK99
     C                   END                                                    <AR1,ÆX
     C                   ENDDO                                                  <ÆX
     C* FYLL OPP RESTEN AV FELTET MED NULL
     C     ÆY            DOWGT     0
     C                   MOVE      '0'           AR2(ÆY)
     C                   SUB       1             ÆY
     C                   ENDDO
     C*
     C                   MOVEA     AR2           WRK              30
     C*
     C     CHK99         ENDSR
      ***********************************************
      * COMPILE TIME ARRAY
      ***********************************************
**     TXT
Sending                               1  T (or S)
Mottak                                2  R
Ubehandlet (Ny)                       3  A        Status
Konvertering pågår                    4  B          "
Konvertering er vellykket             5  C          "
Feil i konvertering                   6  D          "
Sending pågår                         7  E          "
Sending er vellykket                  8  F          "
Feil i sending                        9  G          "
Response mottatt                      10 H          "
Inaktiv                               11 Z          "
Tatt inn i SYSPED400                  12 I          "
** Tabel for Status Codes   3 alfa + 69 alfa
AWDArrival Documents Delivered to Consignee or his Agent
NFDConsignee or his Agent Informed of Arrival of Shipment
RCFConsigment Arrived on a Flight (known or not)
CCDConsigment Cleared by Customs
DISConsigment Discrepancy
DEPConsigment has Departed
BKDConsigment has been Booked
DLVConsigment has been Delivered to the Consignee or his Agent
MANConsigment has been Manifested
TFDConsigment has been Transferred to another Carrier
PREConsigment Prepared for Loading
RCSConsigment Received from Shipper
TRMConsigment to be Transferred to Another Carrier
RCTConsigment Received from Another Carrier
DOC*Documents Received by Handling Party
OSIOther Service Information
