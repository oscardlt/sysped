     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      REF(*LIBL/AIRLD)
     A                                      PRINT
     A                                      HLPDOC('STD' 'HELPNU' 'HLP400')
     A                                      HELP
     A                                      ALTHELP(CA01)
      * "NOERASE" MÅ HA FOR Å IKKE TØMME BILDE SOM MAN KOMME FRA
     A          R NOERASE
     A                                      ASSUME
     A                                  1  2' '
      * MAN KUN DEFINNERE VINDU PÅ REKORDNIVÅ
     A          R WINDOWSZ
     A                                      WINDOW(*DFT 16 48 *NOMSGLIN)
     A                                      OVERLAY
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  6  2VALUES(' ' '1')
     A  27                                  DSPATR(PR)
     A            AIRCCP    R        O  6  4REFFLD(AIRLDR/AIRCCP)
     A            AIRCOD    R        O  6  7REFFLD(AIRLDR/AIRCOD)
     A            AIRWEI    R        O  6  8REFFLD(AIRLDR/AIRWEI)
     A                                      EDTCDE(4)
     A            AIRLIN    R        O  6 14REFFLD(AIRLDR/AIRLIN)
     A                                      EDTCDE(4)
     A            AIRGRN    R        O  6 18REFFLD(AIRLDR/AIRGRN)
     A                                      EDTCDE(4)
     A            AIRAWB    R        O  6 33REFFLD(AIRLDR/AIRAWB)
     A                                      EDTCDE(4)
     A*                                 7  3'C/P'
     A*           AIRCP     R        O  7  7REFFLD(AIRLDR/AIRCP)
     A*                                 7  9'T'
     A*           AIRTRC    R        O  7 11REFFLD(AIRLDR/AIRTRC)
     A                                  7 13'HAWB'
     A            AIRHAM    R        O  7 18REFFLD(AIRLDR/AIRHAM)
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
     A            AIRHAW    R        O  7 33REFFLD(AIRLDR/AIRHAW)
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A                                      SFLSIZ(0006)
     A                                      SFLPAG(0005)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A                                      CA03(03 'Forrige')
     A                                      CA05(05 'Frisk')
     A                                      CA06(06 'LAGE NY')
     A                                      CA12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            ZRECNO         4S 0O  3 25SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYEF62R'
     A                                      COLOR(BLU)
     A                                  1 10'Spør på flyfraktrater (brutto)'
     A                                      COLOR(WHT)
     A                                  2  2'Frasted:'
     A            WSSDF          3A  B  2 11DSPATR(UL)
     A                                  2 15'Tilsted:'
     A            WSSDT          3A  B  2 24DSPATR(UL)
     A N27                              3  2'Velg opsjon:   1=Plukk'
     A                                      COLOR(BLU)
     A                                  4  2'Op RK Vktgr Fly Grunnpris/min. ...-
     A                                      ..Kilopris'
     A                                      COLOR(WHT)
     A            W2VKT          5  0B  5  8DSPATR(UL)
     A                                      EDTCDE(4)
     A            W2LIN          3  0B  5 14DSPATR(UL)
     A                                      EDTCDE(4)
     A          R BUNN
     A                                      WINDOW(WINDOWSZ)
     A                                      SLNO(16)
     A                                      CLRL(01)
     A                                      OVERLAY
     A N30                              1  2'F3=Avslutt  F5=Forny  F6=Lage  F12-
     A                                      =Forrige'
     A                                      COLOR(BLU)
     A  30        MSGTXT        42A  O  1  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A          R NYWRK
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A                                      CA03(03 'Forrige')
     A                                      CA05(05 'Frisk')
     A                                      CA06(06 'LAGE NY')
     A                                      CA12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYEF62R'
     A                                      COLOR(BLU)
     A                                  1 10'Spør på flyfraktrater (brutto)'
     A                                      COLOR(WHT)
     A                                  2  2'Frasted:'
     A            WSSDF          3A  B  2 11DSPATR(UL)
     A                                  2 15'Tilsted:'
     A            WSSDT          3A  B  2 24DSPATR(UL)
     A N27                              3  2'Velg opsjon:   1=Plukk'
     A                                      COLOR(BLU)
     A                                  4  2'Op RK Vktgr Fly Grunnpris/min. ...-
     A                                      ..Kilopris'
     A                                      COLOR(WHT)
     A            W2VKT          5  0B  5  8DSPATR(UL)
     A                                      EDTCDE(4)
     A            W2LIN          3  0B  5 14DSPATR(UL)
     A                                      EDTCDE(4)
     A N30                             16  2MSGCON(42 YBC0212 SYMSGF)
     A                                      COLOR(BLU)
