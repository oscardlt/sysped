      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/TDKE033R MODULE(*LIBL/TDKE033R *LIBL/INCGI)
CRTPGM*        ACTGRP(CGI) AUT(*USE)
      *
      *********************************************************************
      /copy SYSPEDS/qrpgsrcpy,hspecs
      /copy SYSPEDS/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FDKEF      IF   E           K DISK    USROPN
     FDKTVK     IF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy SYSPEDS/qrpgsrcpy,prototypeb
      /copy SYSPEDS/qrpgsrcpy,usec
      /copy SYSPEDS/qrpgsrcpy,variables3
      *
      * Varible for
     D WSUSER          S             10
     D SYAVA           S              4
     D SYAV            S              4  0
     D SYOPA           S              7
     D SYOP            S              7  0
     D JSONstring      S          32000
     D Error           S            150
     D AntLest         S              9  0
      *get input-string
      /copy syspeds/qrpgsrcpy,prolog3

      * Parse input
     C                   exsr      Parse
      * Open files etc
     C                   EXSR      INIT
      *
     c                   callp     gethtmlifs(
     C                             '/syspeddev/html/JSON.html':
     C                             '/CGITAG/')
     C                   callp     wrtsection('top')
      *
      * ............................................................................................
      * skriv JSON-Object start..(alltid '{' + x ant param. m/komma mellom (IKKE komma etter siste))
      * ............................................................................................
      *
      /free
        JSONstring='{'
        +'¬user¬ : ¬'+%trim(WSUSER)+'¬, '
        +'¬avd¬ : ¬'+%trim(SYAVA)+'¬, '
        +'¬opd¬ : ¬'+%trim(SYOPA)+'¬, '
        +'¬errMsg¬ : ¬'+%trim(Error)+'¬, ';
      /end-free
      * JSONfix escaper " i tekst,  for deretter å bytte ¬->"
     c                   call      'JSONFIX'
     c                   parm                    JSONstring
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')
      *
      * ............................................................................................
      * Skriv JSON-LISTE Start (en liste er EN parameter(fra [ til   )
      * ............................................................................................
     c                   eval      JSONstring ='"invTot" : ['
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')
      *
      * Liste Ærende
      *
     c     Error         ifeq      *blanks
     C                   EXSR      TOT
     C                   Add       1             AntLest
      * ............................................................................................
      * Skriv en JSON-Array-linje pr menypunkt - komma før hvert nytt element
      * ............................................................................................
      /free
       if AntLest=1;
          JSONstring=*blanks;
          else;
          JSONstring=', ';
          endif;
       JSONstring=%trim(JSONstring)+'{'
          +'¬tot_vakd¬ : ¬'+%trim(tot_vakd)+'¬, '
          +'¬tot_vaku¬ : ¬'+%trim(%editc(tot_vaku:'4'))+'¬, '
          +'¬tot_omr¬ : ¬'+%trim(%editc(tot_omr:'4'))+'¬, '
          +'¬tot_fabl¬ : ¬'+%trim(%editc(tot_fabl:'4'))+'¬}';
      /end-free
     C                   call      'JSONFIX'
     C                   parm                    JSONstring
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')

     c                   ENDIF
      * ............................................................................................
      * Skriv JSON-Liste/Array  Avslutning
      * ............................................................................................
     c                   eval      JSONstring =']'
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')
      * ............................................................................................
      * Skriv JSON-string Avsluttning
      * ............................................................................................
     c                   eval      JSONstring ='}'
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')
      *
      * Quit
     C                   exsr      Exit


      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Lukk fil
     C                   CLOSE     BRIDF
     C  N50              CLOSE     DKEF
     C  N50              CLOSE     DKTVK

      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   MOVE      *ON           *INLR
     C                   return
     C                   endsr
      *
      *********************************************************
     C     Parse         Begsr
      *********************************************************
      * Get input
     C                   EVAL      WSUSER = zhbgetvar('USER')
     C                   EVAL      SYAVA = zhbgetvar('AVD')
     C                   EVAL      SYAV  = c2n2(SYAVA)
     C                   EVAL      SYOPA= zhbgetvar('OPD')
     C                   EVAL      SYOP = c2n2(SYOPA)
     c                   endsr
      *
      *********************************************************
     C     INIT          Begsr
      *********************************************************
     C                   OPEN      BRIDF
      * Test innlogging
     C     WSUSER        CHAIN     BRIDF                              50
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C                   call      BILIBL
     C                   end
      *
     C     DKEFKEY       KLIST
     C                   KFLD                    SYAV
     C                   KFLD                    SYOP
     C     DKTVKKEY      KLIST
     C                   KFLD                    DKEF_VAKD
     C                   KFLD                    ZKVK_DTS
     C     *LIKE         DEFINE    DKEF_vakd     tot_vakd
     C     *LIKE         DEFINE    DKEF_vaku     tot_vaku
     C     *LIKE         DEFINE    DKVK_omr      tot_omr
     C     *LIKE         DEFINE    DKEF_fabl     tot_fabl
     C     *LIKE         DEFINE    DKVK_DTS      ZKVK_DTS
     C     *LIKE         DEFINE    DKEF_fabl     W_FABL
      *
     C   50              eval      Error = 'Invalid userID'
     C  N50              OPEN      DKEF
     C  N50              OPEN      DKTVK
      *_________________________________
      * Hæmta dagens datum och klockslag
      *"""""""""""""""""""""""""""""""""
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVEL     WDT           ZKVK_DTS

     c                   endsr
      *_____________________________________________
      * Summera alla fakturor                    TOT
      *"""""""""""""""""""""""""""""""""""""""""""""
     C     TOT           BEGSR
      * Finnes beløp ?
     C                   Z-ADD     0             X                 5 0
     C                   EVAL      *IN55=*OFF
     C     DKEFKEY       SETLL     DKEF
     C     DKEFKEY       READE     DKEF                                   55
     C     *IN55         DOWEQ     '0'
     C     DKEF_FATX     IFNE      *BLANKS
     C     DKEF_VAKD     ANDNE     *BLANKS
     C     DKEF_VAKU     ANDNE     *ZEROS
     C     DKEF_FABL     ANDNE     *ZEROS

     C                   ADD       1             X
     C                   IF        X=1
     C                   EVAL      TOT_VAKD=DKEF_VAKD
     C                   ELSE
     C     DKEF_VAKD     IFNE      TOT_VAKD
     C                   EVAL      *IN71=*ON
     C                   EVAL      TOT_VAKD='DKK'
     C                   LEAVE
     C                   ENDIF
     C                   ENDIF

     C                   ENDIF
     C     DKEFKEY       READE     DKEF                                   55
     C                   ENDDO
     C     TOT_VAKD      CABEQ     *BLANKS       TOT9
      * Summera belopp
     C                   EVAL      *IN55=*OFF
     C     DKEFKEY       SETLL     DKEF
     C     DKEFKEY       READE     DKEF                                   55
     C     *IN55         DOWEQ     '0'
     C     DKEF_FATX     IFNE      *BLANKS
     C     DKEF_VAKD     ANDNE     *BLANKS
     C     DKEF_VAKU     ANDNE     *ZEROS
     C     DKEF_FABL     ANDNE     *ZEROS
     C                   IF        *IN71
     C                   EXSR      TOTVAL
     C                   ADD       W_FABL        TOT_FABL
     C                   ELSE
     C                   ADD       DKEF_FABL     TOT_FABL
     C                   ENDIF
     C                   ENDIF
     C     DKEFKEY       READE     DKEF                                   55
     C                   ENDDO
      * Hæmta valutakurs och omrækningsfaktor
     C                   IF        TOT_VAKD='DKK'
     C                   EVAL      TOT_VAKU=1
     C                   EVAL      TOT_OMR=1
     C                   ELSE
     C                   EVAL      *IN64=*OFF
     C                   EVAL      DKEF_VAKD=TOT_VAKD
     C     DKTVKKEY      SETGT     DKTVK
     C                   READP     DkTVK                                  64
     C     *IN64         IFEQ      *OFF
     C                   EVAL      TOT_VAKU=DKVK_KRS
     C                   EVAL      TOT_OMR=DKVK_OMR
     C                   ENDIF
     C                   ENDIF
     C     TOT9          ENDSR
      *_____________________________________________
      * Rækna om till DKK                     TOTVAL
      *"""""""""""""""""""""""""""""""""""""""""""""
     C     TOTVAL        BEGSR
     C                   EVAL      *IN64=*OFF
     C                   EVAL      W_FABL=*ZEROS
     C     DKTVKKEY      SETGT     DKTVK
     C                   READP     DKTVK                                  64
     C     *IN64         IFEQ      *OFF
     C     DKEF_FABL     MULT      DKVK_KRS      W_FABL
     C                   IF        DKVK_OMR > 0
     C     W_FABL        DIV       DKVK_OMR      W_FABL
     C                   ENDIF
     C                   ENDIF
     C                   ENDSR
