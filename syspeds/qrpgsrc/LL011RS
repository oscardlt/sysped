     FLLTELL    UF   E             DISK
     FLLHEAD    UF A E           K DISK
     FLLLINE    UF   E           K DISK
     FLLAGJD02  UF   E           K DISK
     D                SDS
     D  ZJOB                 244    253
     D  ZUSER                254    263
     D  ZJOBNR               264    269  0
     D XTXT01          C                   CONST('ALLOK.P.L:')
      *
      *
     C                   EXSR      INIT
     C                   EXSR      HOVED
     C                   MOVE      '1'           *INLR
      *
      *
      *////////////////////////////////////////////////////////////
     C     INIT          BEGSR
      *////////////////////////////////////////////////////////////
      *___________________
      * Definiere Workfelt
      *"""""""""""""""""""
     C     *LIKE         DEFINE    LHPAK         X_LHPAK
     C     *LIKE         DEFINE    LHALN         X_LHALN
     C     *LIKE         DEFINE    LHANT0        X_LHANT0
     C     *LIKE         DEFINE    LHANTL        X_LHANTL
     C     *LIKE         DEFINE    LHANT1        X_LHANT1
     C     *LIKE         DEFINE    LHANT2        X_LHANT2
     C     *LIKE         DEFINE    LHANT3        X_LHANT3
     C     *LIKE         DEFINE    LHVEK         X_LHVEK
     C     *LIKE         DEFINE    LHWVEK        X_LHWVEK
     C     *LIKE         DEFINE    LHKUB         X_LHKUB
     C     *LIKE         DEFINE    LHWKUB        X_LHWKUB
     C     *LIKE         DEFINE    LHTLIN        X_LHTLIN
      *
     C     *ENTRY        PLIST
     C                   PARM                    UMN               7 0
     C                   PARM                    UFLAGG            1 0
      *__________________
      * Definiere Nøkkler
      *""""""""""""""""""
      *
     C     LLAGJD02K     KLIST
     C                   KFLD                    LHGRP
     C                   KFLD                    LHPNR
     C                   KFLD                    UMN
     C                   KFLD                    LLVN
     C     LLAGJD02K2    KLIST
     C                   KFLD                    LHGRP
     C                   KFLD                    LHPNR
     C                   KFLD                    UMN
      *
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
      *
     C                   MOVE      *ZEROS        UFLAGG
      *
     C                   ENDSR
      *
      *
      *////////////////////////////////////////////////////////////
     C     HOVED         BEGSR
      *////////////////////////////////////////////////////////////
     C     UMN           CHAIN(N)  LLHEAD                             33
     C                   MOVE      LHPAK         X_LHPAK
     C                   MOVE      LHALN         X_LHALN
     C                   MOVE      *ZEROS        X_NYPL            1 0
      *
     C                   MOVE      *ZEROS        XANTLIN           3 0
     C     UMN           SETLL     LLLINE
     C     UMN           READE     LLLINE                                 33
     C                   DOW       *IN33 = *OFF
     C                   ADD       LLNTLK        XANTLIN
     C                   IF        XANTLIN > 99 AND X_LHTLIN > 0
     C                   Z-ADD     1             UFLAGG
      * OPPDATER ANTALL, VEKT OG KUBIKK PÅ PLUKKLISTE
     C     LHMN          CHAIN     LLHEAD                             33
     C                   EVAL      LHANT0 = X_LHANT0
     C                   EVAL      LHANTL = X_LHANTL
     C                   EVAL      LHANT1 = X_LHANT1
     C                   EVAL      LHANT2 = X_LHANT2
     C                   EVAL      LHANT3 = X_LHANT3
     C                   EVAL      LHVEK  = X_LHVEK
     C                   EVAL      LHWVEK = X_LHWVEK
     C                   EVAL      LHKUB  = X_LHKUB
     C                   EVAL      LHWKUB = X_LHWKUB
     C                   EVAL      LHTLIN = X_LHTLIN
     C                   IF        UMN = LHMN
     C                   ADD       1             X_NYPL
     C                   MOVE      X_NYPL        XAN01             1
     C                   EVAL      LHPAK = %TRIM(X_LHPAK) + '-' + XAN01
     C                   END
     C                   UPDATE    LLHEADR
      *
     C     1             CHAIN     LLTELL                             33
     C                   ADD       1             LLREC1
     C                   UPDATE    LLTELLR
     C                   MOVE      LLREC1        LHMN
     C     2             CHAIN     LLTELL                             33
     C                   ADD       1             LLREC1
     C                   UPDATE    LLTELLR
     C                   Z-ADD     LLREC1        LHALN
     C                   ADD       1             X_NYPL
     C                   MOVE      X_NYPL        XAN01             1
     C                   EVAL      LHPAK = %TRIM(X_LHPAK) + '-' + XAN01
     C                   MOVE      WDT           LHRDT
     C                   MOVE      WTM           LHRTD
     C                   EVAL      LHANT0 = 0
     C                   EVAL      LHANTL = 0
     C                   EVAL      LHANT1 = 0
     C                   EVAL      LHANT2 = 0
     C                   EVAL      LHANT3 = 0
     C                   EVAL      LHVEK  = 0
     C                   EVAL      LHWVEK = 0
     C                   EVAL      LHKUB  = 0
     C                   EVAL      LHWKUB = 0
     C                   EVAL      LHTLIN = 0
     C                   WRITE     LLHEADR
      *
     C                   EVAL      XANTLIN = LLNTLK
     C                   EVAL      X_LHANT0 = 0
     C                   EVAL      X_LHANTL = 0
     C                   EVAL      X_LHANT1 = 0
     C                   EVAL      X_LHANT2 = 0
     C                   EVAL      X_LHANT3 = 0
     C                   EVAL      X_LHVEK  = 0
     C                   EVAL      X_LHWVEK = 0
     C                   EVAL      X_LHKUB  = 0
     C                   EVAL      X_LHWKUB = 0
     C                   EVAL      X_LHTLIN = 0
     C                   END
      *
      * OPPDATER LAGERUTTAK
     C                   IF        UMN <> LHMN
     C     LLAGJD02K     SETLL     LLAGJD02
     C     LLAGJD02K     READE     LLAGJD02                               33
     C                   DOW       *IN33 = *OFF
     C                   EVAL      VDMNL  = LHMN
     C                   EVAL      VDBUSE = ZUSER
     C                   MOVE      WDT           VDBDAT
     C                   MOVE      WTM           VDBKL
     C                   EVAL      VDTEXT = XTXT01 + ' ' + %TRIM(LHPAK)
     C                   MOVE      LHALN         VDTEXT
     C                   UPDATE    LLAGJDR
     C     LLAGJD02K     READE     LLAGJD02                               33
     C                   ENDDO
     C                   END
      *
     C                   EVAL      LLMN = LHMN
     C                   UPDATE    LLLINER
      *
     C                   EVAL      X_LHANT0 = X_LHANT0 + LLWNT0
     C                   EVAL      X_LHANTL = X_LHANTL + LLWNTL
     C                   EVAL      X_LHANT1 = X_LHANT1 + LLWNT1
     C                   EVAL      X_LHANT2 = X_LHANT2 + LLWNT2
     C                   EVAL      X_LHANT3 = X_LHANT3 + LLWNT3
     C                   EVAL      X_LHVEK  = X_LHVEK  + LLVEK
     C                   EVAL      X_LHWVEK = X_LHWVEK + LLWVEK
     C                   EVAL      X_LHKUB  = X_LHKUB  + LLKUB
     C                   EVAL      X_LHWKUB = X_LHWKUB + LLWKUB
     C                   EVAL      X_LHTLIN = X_LHTLIN + 1
      *
     C     UMN           READE     LLLINE                                 33
     C                   ENDDO
      *
     C                   IF        UFLAGG = 1
      * OPPDATER ANTALL, VEKT OG KUBIKK PÅ PLUKKLISTE PÅ SISTE SPLITTET PLUKKLISTE
     C     LHMN          CHAIN     LLHEAD                             33
     C                   EVAL      LHANT0 = X_LHANT0
     C                   EVAL      LHANTL = X_LHANTL
     C                   EVAL      LHANT1 = X_LHANT1
     C                   EVAL      LHANT2 = X_LHANT2
     C                   EVAL      LHANT3 = X_LHANT3
     C                   EVAL      LHVEK  = X_LHVEK
     C                   EVAL      LHWVEK = X_LHWVEK
     C                   EVAL      LHKUB  = X_LHKUB
     C                   EVAL      LHWKUB = X_LHWKUB
     C                   EVAL      LHTLIN = X_LHTLIN
     C                   UPDATE    LLHEADR
      *
      * OPPDATER NYTT PAKKSEDDELNR FOR OPPRINNELIGE PLUKKLISTES PLUKKLINJER
     C                   EVAL      LHPAK = %TRIM(X_LHPAK) + '-1'
     C     LLAGJD02K2    SETLL     LLAGJD02
     C     LLAGJD02K2    READE     LLAGJD02                               33
     C                   DOW       *IN33 = *OFF
     C                   EVAL      VDBUSE = ZUSER
     C                   MOVE      WDT           VDBDAT
     C                   MOVE      WTM           VDBKL
     C                   EVAL      VDTEXT = XTXT01 + ' ' + %TRIM(LHPAK)
     C                   MOVE      X_LHALN       VDTEXT
     C                   UPDATE    LLAGJDR
     C     LLAGJD02K2    READE     LLAGJD02                               33
      *
     C                   ENDDO
     C                   END
      *
     C                   ENDSR
      *
