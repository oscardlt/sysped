     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A          R SUBFIL                    SFL
     A            WSNRO          1A  B  8  2VALUES(' ' '2' '4' '6' '7' '9')
     A            SFBILN        10A  O  8  4
     A  44                                  COLOR(RED)
     A            SFPRO          8Y 0O  8 15EDTCDE(4)
     A  44                                  COLOR(RED)
     A            SFDATO         6S 0O  8 24
     A  44                                  COLOR(RED)
     A            SFAVD          4Y 0O  8 31EDTCDE(4)
     A  44                                  COLOR(RED)
     A            SFOPD          7Y 0O  8 36EDTCDE(4)
     A  44                                  COLOR(RED)
     A            SFNT           7Y 0O  8 48EDTCDE(4)
     A  44                                  COLOR(RED)
     A            SFVKT          7Y 0O  8 56EDTCDE(4)
     A  44                                  COLOR(RED)
     A            SFFBV          7Y 0O  8 64EDTCDE(4)
     A  44                                  COLOR(RED)
     A            SFBELN         7Y 2O  8 72EDTCDE(M)
     A                                      DSPATR(HI)
     A  44                                  COLOR(RED)
     A            SFADK3        15A  H
     A            SFNAK         10A  H
     A            SFVS1          3A  H
     A            SFLI           5S 0H
     A            SFDEL          1A  H
     A            SFPOST         5A  O  8 42
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0100)
     A                                      SFLPAG(0013)
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A                                  1  2'SYSPED/SYAV01'
     A                                      COLOR(BLU)
     A                                  1 22'Spesifikasjon til avregning'
     A                                      DSPATR(HI)
     A            ZRECNO         4S 0O  1 65SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2  2'Transportør:'
     A            TUKNT          8Y 0O  2 15EDTCDE(4)
     A                                      COLOR(WHT)
     A            TUNAT         30A  O  2 24COLOR(WHT)
     A                                  2 72TIME
     A                                  4  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  2MSGCON(010 DIV2226 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5 13'4=Ikke avregne'
     A                                      COLOR(BLU)
     A                                  5 30'6=Oppdrag'
     A                                      COLOR(BLU)
     A                                  5 43'7=Faktura'
     A                                      COLOR(BLU)
     A                                  5 56'9=Slett permanent'
     A                                      COLOR(BLU)
     A                                  6 75'AVR'
     A                                      DSPATR(HI)
     A                                  7  2'O Bilnr.     Turnr.   Dato   -- Re-
     A                                      f. --  Post    Ant.     Kg. Fb.vekt-
     A                                         Beløp'
     A                                      DSPATR(HI)
     A          R BUNN
     A                                      CF03(03 'EXIT')
     A                                      CF10(10 'FERDIG')
     A                                      CF12(12 'FORIGE BILDE')
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF08(08 'Prøve')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            SMBELN        10Y 2B  1 69DSPATR(HI)
     A                                      EDTCDE(M)
     A                                  1 61'Sum:'
     A                                  2  3MSGCON(010 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  2 23'F8=Prøveutskrift'
     A                                      COLOR(BLU)
     A                                  2 45MSGCON(014 DIV2261 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  2 69MSGCON(011 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  98                              3  2MSGCON(078 DIV0012 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A          R NYWRK
     A                                      CF03(03 'EXIT')
     A                                      CF12(12 'FORIGE BILDE')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/SYAV01'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(045 DIV2417 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            ZRECNO         4S 0O  1 65DSPATR(ND)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2  2'Transportør:'
     A            W2KNT          8Y 0O  2 15EDTCDE(4)
     A                                      COLOR(WHT)
     A            W2NAT         30A  O  2 24COLOR(WHT)
     A                                  2 72TIME
     A                                  5  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  2MSGCON(018 DIV2337 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N95                                  DSPATR(ND)
     A                                  6  2MSGCON(010 DIV2226 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  6 13MSGCON(010 DIV2227 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  62                              6 24MSGCON(025 TRO0123 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  7  1MSGCON(078 DIV2428 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  7  4MSGCON(071 DIV2429 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 23MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 62MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  98                             24  2MSGCON(078 DIV0012 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                 23 42MSGCON(015 TRO0051 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A          R BILDE1
     A                                      CF03(03 'EXIT')
     A                                      CF12(03)
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/SYAV11'
     A                                      COLOR(BLU)
     A                                  1 22'Transportøravregning - liste'
     A                                      DSPATR(HI)
     A                                  1 67DATE
     A                                      EDTCDE(Y)
     A                                  2 67TIME
     A                                  7 12'      Turnr fra/til:'
     A            W1FTUR         8Y 0B  7 34CHECK(FE)
     A                                      CHECK(RB)
     A  70                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A            W1TTUR         8Y 0B  7 44CHECK(FE)
     A                                      CHECK(RB)
     A  70                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
