********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 YBC PTF10 FUNSJONSTAST ER IKKE DEFINERT VED TOMME BILDE
********************************************************************
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPPNLGRP(HLPSCR SYFA25PNL)
     A                                      HLPTITLE('VEDLIKEHOLD AV FRITEKSTER-
     A                                      ')
     A                                      HELP
     A                                      ALTHELP
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  5  3VALUES(' ' '5')
     A            HEAVD          4Y 0O  5  6EDTCDE(Z)
     A            HEOPD          7Y 0O  5 12EDTCDE(Z)
     A            FASUM         15Y 2O  5 20EDTCDE(J)
     A            HEPRO          8Y 0O  5 44EDTCDE(Z)
     A            HEDTOP         8Y 0O  5 55EDTWRD('    -  -  ')
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0016)
     A                                      SFLPAG(0015)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A            ZRECNO         4S 0O  2 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYSPED/SYGE07B'
     A                                      COLOR(BLU)
     A                                  1 53DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  1 64TIME
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(025 SHU1338 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  3  2MSGCON(040 SHU1340 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(070 SHU1339 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A          R BUNN
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF03(03 'AVSLUTT')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                      SLNO(20)
     A                                      CLRL(05)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  4  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  5  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                              5 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A          R NYWRK
N01  A                                      CF03(03 'AVSLUTT')
N01  A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                  1  2'SYSPED/SYGE07B'
     A                                      COLOR(BLU)
     A                                  1 53DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  1 64TIME
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(025 SHU1338 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  3  2MSGCON(040 SHU1340 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(070 SHU1339 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
