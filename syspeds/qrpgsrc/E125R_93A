     FEDIM      UF   E           K DISK
     FEDIMD     IF   E           K DISK
     F                                     RENAME(EDIMR:EDTMRD)
     FEDIMG     IF   E           K DISK
     F                                     RENAME(EDIMR:EDTMRG)
     FEDIM5     IF   E           K DISK
     F                                     RENAME(EDIMR:EDTMR5)
     FEDTM93A   IF   E           K DISK
     FEFTX93A   IF A E           K DISK
     FEIMD93A   IF   E           K DISK
     FELIN93A   IF   E           K DISK
     FENAD93A   IF   E           K DISK
     FEMOA93A   IF   E           K DISK
     FEPIA93A   IF   E           K DISK
     FERFF93A   IF   E           K DISK
     FEUNH      IF   E           K DISK
     FVAREL01   IF   E           K DISK
     FTRAVDF    IF   E           K DISK
     FE125F     UF A E           K DISK
     FE125F2    O  A E           K DISK
     FE125D     CF   E             WORKSTN
     F                                     SFILE(E125D01:ZRECNO)
     F                                     INFDS(FBAREA)
     FE125P     O    E             PRINTER OFLIND(*IN50)
     FE125P2    O    E             PRINTER OFLIND(*IN51)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D                SDS
     D  USERID               254    263
     D                 DS
     D  XAN15                  1     15
     D  V1                     1     15
     D                                     DIM(15)
     D                 DS
     D  VARENR                 1     15
     D  V2                     1     15
     D                                     DIM(15)
     D                 DS
     D  X5004                  1     18
     D  BL                     1     18
     D                                     DIM(18)
     D                 DS
     D  X4440                  1     70
     D  XFTX01                 1      3
     D  XFTX02                 5     14
     D  XFTX03                16     55
     D  XFTX04                57     60
     D  XFTX05                61     62
     D  XFTX06                63     64
     D  XFTX07                65     68
     D                 DS
     D  XTIME                  1     14  0
     D  XTID                   1      4  0
     D  XDTDD                  7      8  0
     D  XDTMM                  9     10  0
     D  XDTÅÅ                 11     14  0
     D                 DS
     D  XNAS                   1     13
     D  XNAS20                 1     20
      *
     C                   EXSR      INIT
      *
     C     START         TAG
     C                   MOVE      '1'           *IN(93)
     C                   WRITE     E125D02
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
     C                   MOVE      *BLANKS       WSNR
     C                   MOVE      *ZEROS        XMN
     C     *IN05         IFEQ      '0'
     C                   MOVE      W2ST          YST
     C                   MOVEL     W2FN          Y1004
     C                   MOVEL     W2AVD         Y0068
     C                   MOVE      W2AVD         YAVD
     C                   MOVE      *BLANKS       W2FN
     C                   MOVE      *ZEROS        W2AVD
     C                   END
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C   27EDIMDK        SETLL     EDIMD
     C   28EDIMGK        SETLL     EDIMG
     C   29Y0068         SETLL     EDIM5
     C     START1        TAG
     C   27EDIMDK        READE     EDIMD                                  94
     C   28EMGK2         READE     EDIMG                                  94
     C   29Y0068         READE     EDIM5                                  94
     C   28
     CANN94
     COR 29
     CANN94M0065         CABNE     'INVOIC'      START1
     C   29
     CANN94MSR           CABNE     'R'           START1
     C   29
     CANN94MST           CABNE     W2ST          START1
     C  N29
     CANN94              DO
     C                   MOVEL     M0068         XAVD
     C     XAVD          CHAIN     TRAVDF                             33
     C  N33AVEDI         COMP      'J'                                3333
     C   33              GOTO      START1
     C                   END
      *
     C     START2        TAG
      *
     C  N94              DO
     C                   DO        8
     C                   ADD       1             ZRECNO
     C                   EXSR      MOVDTA
     C                   WRITE     E125D01
     C                   MOVE      ZRECNO        XRECNO            4 0
     C     ST21          TAG
     C   27EDIMDK        READE     EDIMD                                  94
     C   28EMGK2         READE     EDIMG                                  94
     C   29Y0068         READE     EDIM5                                  94
     C   28
     CANN94
     COR 29
     CANN94M0065         CABNE     'INVOIC'      ST21
     C   29
     CANN94MSR           CABNE     'R'           ST21
     C   29
     CANN94MST           CABNE     W2ST          ST21
     C  N29
     CANN94              DO
     C                   MOVEL     M0068         XAVD
     C     XAVD          CHAIN     TRAVDF                             33
     C  N33AVEDI         COMP      'J'                                3333
     C   33              GOTO      ST21
     C                   END
     C  N94              MOVE      MMN           XMN
     C  N94              END
     C                   END
      *
     C     START3        TAG
     C  N30              Z-ADD     5             LINNBR
     C  N30              Z-ADD     5             POSNBR
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     E125D04
     C                   ELSE
     C                   WRITE     E125D03
     C                   EXFMT     E125D02
     C                   END
      *
     C                   MOVE      '0'           *IN30
     C                   MOVEA     '00'          *IN(98)
      *
     C   03              GOTO      SLUTT
     C   05              GOTO      START
     C   25              DO
     C                   Z-ADD     XRECNO        ZRECNO
     C  N94XMN           CHAIN(N)  EDIM                               25
     C  N94              MOVEL     M0068         XAVD
     C                   SETOFF                                       25
     C                   GOTO      START2
     C                   END
      *
     C     W2AVD         IFNE      0
     C     W2AVD         CHAIN     TRAVDF                             99
     C  N99AVEDI         COMP      'J'                                9999
     C     *IN99         IFEQ      '1'
     C                   MOVE      'YBC0008'     MSGNR
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     40            POSNBR
     C                   MOVE      '1'           *IN30
     C                   GOTO      START3
     C                   END
     C                   END
      *
     C     W2ST          IFNE      YST
     C     W2FN          ORNE      *BLANKS
     C     W2AVD         ORNE      0
     C     W2FN          COMP      *BLANKS                            282827
     C   28              MOVE      *ZEROS        W2AVD
     C  N28W2AVD         COMP      0                                  292927
     C                   GOTO      START
     C                   END
      *
     C     ZRECNO        IFNE      0
     C                   EXSR      HHENT
     C                   END
     C                   GOTO      START3
      *
     C     SLUTT         TAG
     C                   EXSR      PRINT
     C                   SETON                                        LR
     C                   RETURN
      *
      ***********************************************
     C     MOVDTA        BEGSR
      ***********************************************
     C     M1001         IFEQ      '380'
     C                   MOVE      'F'           XFK
     C                   ELSE
     C                   MOVE      'K'           XFK
     C                   END
     C                   MOVEL     M1004         XFN
      *
     C                   MOVE      *ZEROS        XDT
     C                   MOVE      *ZEROS        XTIDW
     C     DTM00K        SETLL     EDTM93A
     C     DTM00K        READE     EDTM93A                                33
     C     *IN33         DOWEQ     '0'
     C     D2005         IFEQ      '137'
     C                   MOVEL     D2380         XN12             12
     C                   MOVEL     XN12          XDT
     C                   MOVE      XN12          XTIDW
     C                   MOVE      '1'           *IN33
     C                   END
     C  N33DTM00K        READE     EDTM93A                                33
     C  N33              ENDDO
      *
     C                   MOVE      *BLANKS       XBN
     C                   MOVE      *BLANKS       XKON
     C                   MOVE      *BLANKS       XGMFN
     C     RFF01K        SETLL     ERFF93A
     C     STMD1         TAG
     C     RFF01K        READE     ERFF93A                                33
     C     *IN33         IFEQ      '0'
     C                   SELECT
     C     R1153         WHENEQ    'ON '
     C                   MOVEL     R1154         XBN
     C     R1153         WHENEQ    'VN '
     C                   MOVEL     R1154         XKON
     C     R1153         WHENEQ    'IV '
     C                   MOVEL     R1154         XGMFN
     C                   ENDSL
     C                   GOTO      STMD1
     C                   END
      *
     C                   MOVEL     XMSG          XNAS20
     C                   MOVEL     XMSG          XNAK
     C     NAD02K        SETLL     ENAD93A
     C     STMD2         TAG
     C     NAD02K        READE     ENAD93A                                33
     C     *IN33         IFEQ      '0'
     C                   SELECT
     C     N3035         WHENEQ    'BY '
     C                   MOVEL     N3036A        XNAK
     C     N3035         WHENEQ    'SU '
     C                   MOVEL     N3036A        XNAS20
     C                   ENDSL
     C                   GOTO      STMD2
     C                   END
      *
     C                   MOVE      *ZEROS        XDTF
     C     DTM08K        SETLL     EDTM93A
     C     STMD3         TAG
     C     DTM08K        READE     EDTM93A                                33
     C     *IN33         IFEQ      '0'
     C                   SELECT
     C     D2005         WHENEQ    '13 '
     C                   MOVEL     D2380         XDTF
     C                   OTHER
     C                   GOTO      STMD3
     C                   ENDSL
     C                   END
      *
     C                   MOVE      *ZEROS        XNETTO
     C                   MOVE      *ZEROS        XMVAT
     C                   MOVE      *ZEROS        XTOTAL
     C     MOA45K        SETLL     EMOA93A
     C     STMD4         TAG
     C     MOA45K        READE     EMOA93A                                33
     C     *IN33         IFEQ      '0'
     C                   SELECT
     C     M5025         WHENEQ    'NET'
     C                   MOVE      M5004         X5004
     C                   EXSR      KNVBL
     C                   Z-ADD(H)  XBL           XNETTO
     C     M5025         WHENEQ    '150'
     C                   MOVE      M5004         X5004
     C                   EXSR      KNVBL
     C                   Z-ADD(H)  XBL           XMVAT
     C     M5025         WHENEQ    '86 '
     C                   MOVE      M5004         X5004
     C                   EXSR      KNVBL
     C                   Z-ADD(H)  XBL           XTOTAL
     C                   ENDSL
     C                   GOTO      STMD4
     C                   END
      *
     C                   ENDSR
      *
      ***********************************************
     C     KNVBL         BEGSR
      ***********************************************
     C                   MOVE      *ZEROS        XBL              17 2
     C                   Z-ADD     100           XFAKT             9 6
     C                   Z-ADD     1             XFTEGN            1 0
     C                   MOVE      '0'           *IN33
     C     1             DO        18            XN                3 0
     C                   SELECT
     C     BL(XN)        WHENGE    '0'
     C     BL(XN)        ANDLE     '9'
     C                   MULT      10            XBL
     C                   MOVE      BL(XN)        XBL
     C   33              DIV       10            XFAKT
     C     BL(XN)        WHENEQ    ','
     C     BL(XN)        OREQ      '.'
     C                   MOVE      '1'           *IN33
     C     BL(XN)        WHENEQ    '-'
     C                   Z-SUB     1             XFTEGN
     C                   OTHER
     C                   Z-ADD     100           XN
     C                   ENDSL
     C                   END
     C                   MULT      XFAKT         XBL
     C                   MULT      XFTEGN        XBL
      *
     C                   ENDSR
      *
      ***********************************************
     C     HHENT         BEGSR
      ***********************************************
     C                   MOVE      *BLANKS       MSGNR
     C                   MOVEA     '000'         *IN(90)
     C                   MOVEA     '00'          *IN(98)
     C                   Z-ADD     2             LINNBR
     C     *IN98         DOWEQ     '0'
     C                   READC     E125D01                                98
     C   98              DO
     C   99              MOVE      '0'           *IN98
     C                   MOVE      '0'           *IN99
     C                   GOTO      SLHENT
     C                   END
      *
     C     ZRECNO        DIV       8             LINNBR
     C                   MVR                     XLIN              1 0
     C     XLIN          IFEQ      0
     C                   Z-ADD     16            LINNBR
     C                   ELSE
     C     XLIN          MULT      2             LINNBR
     C                   END
      *
     C                   SELECT
     C     WSNR          WHENEQ    '5'
     C     MMN           CHAIN     EUNH                               98
     C     *IN98         IFEQ      '1'
     C                   MOVE      'YBC0003'     MSGNR
     C                   UPDATE    E125D01
     C                   GOTO      SLHENT
     C                   END
     C                   EXSR      DSPMSG
     C     UFLAGG        CABEQ     3             SLHENT
     C     WSNR          WHENEQ    '8'
     C                   EXSR      GODKJ
     C   30              GOTO      SLHENT
     C     WSNR          WHENEQ    '9'
     C                   EXSR      UGODKJ
     C   30              GOTO      SLHENT
     C                   ENDSL
     C                   MOVE      '1'           *IN99
      *
     C                   END
      *
     C     SLHENT        TAG
     C   98MSGNR         IFEQ      *BLANKS
     C                   MOVE      'DIV0012'     MSGNR
     C                   END
     C   98              MOVE      '1'           *IN30
     C                   ADD       4             LINNBR
     C                   Z-ADD     2             POSNBR
      *
     C                   ENDSR
      *
      ***********************************************
     C     DSPMSG        BEGSR
      ***********************************************
     C                   CALL      XP126
     C                   PARM                    MMN
     C                   PARM                    XFN
     C                   PARM                    XKON
     C                   PARM                    UFLAGG            1 0
     C                   MOVE      ' '           WSNR
     C                   UPDATE    E125D01
      *
     C                   ENDSR
      *
      ***********************************************
     C     GODKJ         BEGSR
      ***********************************************
      * Test om faktura er overført før.
     C                   CALL      'E125R1'
     C                   PARM                    M1004
     C                   PARM                    UFLAGG            1 0
     C     UFLAGG        IFEQ      9
     C                   MOVE      'YBC0007'     MSGNR
     C                   MOVE      '1'           *IN98
     C                   MOVE      '1'           *IN30
     C                   GOTO      SLGK
     C                   END
      * Test om varenr finnes hvis kostnadsbærer må hentes.
     C     XAVD          CHAIN     TRAVDF                             33
     C     AVBÆRE        IFEQ      'J'
     C                   EXSR      TESTVN
     C     *IN30         CABEQ     '1'           SLGK
     C                   END
      *
     C     MMN           CHAIN     EDIM                               33
     C     *IN33         IFEQ      '0'
     C                   SELECT
     C     MST           WHENEQ    'C'
     C                   MOVE      'G'           MST
     C                   UPDATE    EDIMR
     C     XFK           IFEQ      'K'
     C     XGMFN         ANDNE     *BLANKS
      * Oppdater status 'G' på faktura
     C                   CALL      'E125R2'
     C                   PARM                    XGMFN
     C                   ENDIF
     C     MST           WHENEQ    'F'
     C                   MOVE      'G'           MST
     C                   UPDATE    EDIMR
     C                   GOTO      SLGK
     C                   OTHER
     C                   MOVE      'YBC0006'     MSGNR
     C                   GOTO      SLGK
     C                   ENDSL
      * OPPDATER PÅ EFTX93A
     C                   MOVE      '9999'        FREP0
     C     FTX99K        SETGT     EFTX93A
     C     FT99K2        READPE(N) EFTX93A                                33
     C                   MOVE      MMN           FMN
     C     *IN33         IFEQ      '1'
     C                   Z-ADD     1             FREP0
     C                   ELSE
     C                   ADD       1             FREP0
     C                   END
     C                   TIME                    XTIME
     C                   MOVEL     'GODKJENT'    XFTX03
     C                   MOVE      XDTÅÅ         XFTX04
     C                   MOVE      XDTMM         XFTX05
     C                   MOVE      XDTDD         XFTX06
     C                   MOVE      XTID          XFTX07
     C                   MOVE      X4440         F4440A
     C                   MOVE      *BLANKS       F4440B
     C                   MOVE      *BLANKS       F4440C
     C                   MOVE      *BLANKS       F4440D
     C                   MOVE      *BLANKS       F4440E
     C                   WRITE     FTXR
      *
     C                   MOVE      'JA '         XGODKJ
     C                   MOVE      *BLANKS       WSTXT1
     C                   MOVE      *BLANKS       WSTXT2
     C                   MOVE      *BLANKS       WSTXT3
     C                   MOVE      *BLANKS       WSTXT4
     C                   MOVE      *BLANKS       WSTXT5
     C                   WRITE     E125FR
      *
     C                   MOVE      MMN           EF2MN
     C                   MOVE      XAVD          EF2AVD
     C                   MOVE      XFN           EF2FN
     C                   MOVE      XTOTAL        EF2TOT
     C     XMVAT         IFEQ      0
     C                   MOVE      '0'           EF2MVA
     C                   ELSE
     C                   MOVE      '1'           EF2MVA
     C                   END
     C                   MOVE      'G'           EF2ST
     C                   MOVE      USERID        EF2UID
     C                   WRITE     E125F2R
     C                   END
      *
     C     SLGK          TAG
     C  N30              MOVE      ' '           WSNR
     C                   UPDATE    E125D01
      *
     C                   ENDSR
      *
      ***********************************************
     C     UGODKJ        BEGSR
      ***********************************************
     C                   MOVE      *BLANKS       WSTXT1
     C                   MOVE      *BLANKS       WSTXT2
     C                   MOVE      *BLANKS       WSTXT3
     C                   MOVE      *BLANKS       WSTXT4
     C                   MOVE      *BLANKS       WSTXT5
     C                   MOVE      'J'           WSOVRF
     C     STUGK         TAG
     C                   EXFMT     E125D10
     C                   MOVE      '0'           *IN99
     C     *IN12         CABEQ     '1'           SLUGK
     C     WSTXT1        IFEQ      *BLANKS
     C                   MOVE      '1'           *IN99
     C                   GOTO      STUGK
     C                   END
      *
     C     WSOVRF        IFEQ      'J'
      * Test om faktura er overført før.
     C                   CALL      'E125R1'
     C                   PARM                    M1004
     C                   PARM                    UFLAGG            1 0
     C     UFLAGG        IFEQ      9
     C                   MOVE      'YBC0007'     MSGNR
     C                   MOVE      '1'           *IN98
     C                   MOVE      '1'           *IN30
     C                   GOTO      SLUGK
     C                   END
      *
     C     XAVD          CHAIN     TRAVDF                             33
     C     AVBÆRE        IFEQ      'J'
     C                   EXSR      TESTVN
     C     *IN30         CABEQ     '1'           SLUGK
     C                   END
     C                   END
      *
     C     MMN           CHAIN     EDIM                               33
     C     *IN33         IFEQ      '0'
     C     MST           IFEQ      'C'
     C     WSOVRF        IFEQ      'J'
     C                   MOVE      'F'           MST
     C                   ELSE
     C                   MOVE      'G'           MST
     C                   END
     C                   UPDATE    EDIMR
     C     XFK           IFEQ      'K'
     C     XGMFN         ANDNE     *BLANKS
      * Oppdater status 'G' på faktura
     C                   CALL      'E125R2'
     C                   PARM                    XGMFN
     C                   ENDIF
     C                   ELSE
     C                   MOVE      'YBC0006'     MSGNR
     C                   GOTO      SLUGK
     C                   END
      *
      * OPPDATER PÅ EFTX93A
     C                   MOVE      '9999'        FREP0
     C     FTX99K        SETGT     EFTX93A
     C     FT99K2        READPE(N) EFTX93A                                33
     C                   MOVE      MMN           FMN
     C     *IN33         IFEQ      '1'
     C                   Z-ADD     1             FREP0
     C                   ELSE
     C                   ADD       1             FREP0
     C                   END
     C                   MOVE      'SPH'         F4451
     C                   TIME                    XTIME
     C                   MOVE      WSTXT1        XFTX03
     C                   MOVE      XDTÅÅ         XFTX04
     C                   MOVE      XDTMM         XFTX05
     C                   MOVE      XDTDD         XFTX06
     C                   MOVE      XTID          XFTX07
     C                   MOVE      X4440         F4440A
     C                   MOVE      WSTXT2        XFTX03
     C                   MOVE      X4440         F4440B
     C                   MOVE      WSTXT3        XFTX03
     C                   MOVE      X4440         F4440C
     C                   MOVE      WSTXT4        XFTX03
     C                   MOVE      X4440         F4440D
     C                   MOVE      WSTXT5        XFTX03
     C                   MOVE      X4440         F4440E
     C                   WRITE     FTXR
      *
     C                   MOVE      'NEI'         XGODKJ
     C                   WRITE     E125FR
     C                   END
      *
     C     WSOVRF        IFEQ      'J'
     C                   MOVE      MMN           EF2MN
     C                   MOVE      XAVD          EF2AVD
     C                   MOVE      XFN           EF2FN
     C                   MOVE      XTOTAL        EF2TOT
     C     XMVAT         IFEQ      0
     C                   MOVE      '0'           EF2MVA
     C                   ELSE
     C                   MOVE      '1'           EF2MVA
     C                   END
     C                   MOVE      'F'           EF2ST
     C                   MOVE      USERID        EF2UID
     C                   WRITE     E125F2R
     C                   END
      *
     C     SLUGK         TAG
     C  N30              MOVE      ' '           WSNR
     C                   UPDATE    E125D01
      *
     C                   ENDSR
      *
      ***********************************************
     C     TESTVN        BEGSR
      ***********************************************
     C                   MOVE      '0'           *IN30
     C     LIN22K        SETLL     ELIN93A
     C     LIN22K        READE     ELIN93A                                33
     C     *IN33         DOWEQ     '0'                                          1<-B
     C     L7143         COMP      'EN'                               3434
     C     *IN34         IFEQ      '0'                                           2<-B
     C                   MOVEL     L7140         XAN15
     C                   Z-ADD     15            XN                3 0
     C     XN            DOWGE     1                                              3<-B
     C     V1(XN)        IFNE      *BLANKS                                         4<-B
     C                   MOVE      *BLANKS       VARENR
     C     16            SUB       XN            XN3               3 0
     C     1             DO        XN            XN2               3 0              5<-B
     C                   MOVE      V1(XN2)       V2(XN3)
     C                   ADD       1             XN3
     C                   END                                                        5<-E
     C     VARE1K        CHAIN     VAREL01                            34
     C                   MOVE      *ZEROS        XN
     C                   END                                                       4<-E
     C                   SUB       1             XN
     C                   END                                                      3<-E
     C                   END                                                     2<-E
      * Hvis EAN-nr ikke finnes, forsøker på selgers varenr.
     C     *IN34         IFEQ      '1'                                           2<-B
     C                   MOVE      VARENR        XEANVN
     C                   MOVE      *BLANKS       VARENR
     C     PIA22K        SETLL     EPIA93A
     C     STGK01        TAG
     C     PIA22K        READE     EPIA93A                                34
     C  N34P7143A        CABNE     'SA'          STGK01
     C     *IN34         IFEQ      '0'                                            3<-B
     C                   MOVEL     P7140A        XAN15
     C                   Z-ADD     15            XN                3 0
     C     XN            DOWGE     1                                               4<-B
     C     V1(XN)        IFNE      *BLANKS                                          5<-B
     C     16            SUB       XN            XN3               3 0
     C     1             DO        XN            XN2               3 0               6<-B
     C                   MOVE      V1(XN2)       V2(XN3)
     C                   ADD       1             XN3
     C                   END                                                         6<-E
     C     VARE1K        CHAIN     VAREL01                            34
     C                   MOVE      *ZEROS        XN
     C                   END                                                        5<-E
     C                   SUB       1             XN
     C                   END                                                       4<-E
     C                   END                                                      3<-E
      * Varenr finnes ikke. Skriv ut feilmelding.
     C     *IN34         IFEQ      '1'                                            3<-B
      * Forsøk å hente varegruppe, i så fall bruke varegr. som k.bærer
     C     PIA22K        SETLL     EPIA93A
     C                   MOVE      *BLANKS       XVGR
     C     PIA22K        READE     EPIA93A                                34
     C     *IN34         DOWEQ     '0'
     C     P7143A        IFEQ      'GN'
     C     P7143A        OREQ      'GD'
     C                   MOVEL     P7140A        XVGR
     C                   MOVE      '1'           *IN34
     C                   END
     C  N34PIA22K        READE     EPIA93A                                34
     C  N34              END
     C     XVGR          IFEQ      *BLANKS
     C     XVGR          OREQ      *ZEROS
     C                   MOVE      '1'           *IN30
     C                   END
      *
     C                   MOVE      VARENR        XSAVN
     C                   MOVE      *BLANKS       XVNTXT
     C     IMD22K        SETLL     EIMD93A
     C     STGK02        TAG
     C     IMD22K        READE     EIMD93A                                34
     C  N34I7077         IFEQ      'FL'                                            4<-B
     C                   MOVEL     I7008A        XVNTXT
     C                   ELSE                                                      4
     C                   GOTO      STGK02
     C                   END                                                       4<-E
     C   51              WRITE     E125P201
     C   51              MOVE      '0'           *IN51
     C                   WRITE     E125P202
     C                   END                                                      3<-E
      *
     C                   END                                                     2<-E
     C  N34LIN22K        READE     ELIN93A                                34
     C  N34              ENDDO                                                  1<-E
      *
     C   30              MOVE      'YBC0005'     MSGNR
      *
     C                   ENDSR
      *
      ***********************************************
     C     PRINT         BEGSR
      ***********************************************
     C                   MOVE      '1'           *IN50
     C                   MOVE      *BLANKS       XGODKJ
     C                   MOVE      *ZEROS        XLNR
     C     XGODKJ        SETLL     E125F
     C                   READ      E125F                                  33
     C     *IN33         DOWEQ     '0'
     C   50              WRITE     E125P01
     C   50              MOVE      '0'           *IN50
     C                   ADD       1             XLNR
     C                   WRITE     E125P02
     C     WSTXT1        IFNE      *BLANKS
     C                   MOVE      WSTXT1        XTXT
     C                   EXSR      PRINT2
     C                   END
     C     WSTXT2        IFNE      *BLANKS
     C                   MOVE      WSTXT2        XTXT
     C                   EXSR      PRINT2
     C                   END
     C     WSTXT3        IFNE      *BLANKS
     C                   MOVE      WSTXT3        XTXT
     C                   EXSR      PRINT2
     C                   END
     C     WSTXT4        IFNE      *BLANKS
     C                   MOVE      WSTXT4        XTXT
     C                   EXSR      PRINT2
     C                   END
     C     WSTXT5        IFNE      *BLANKS
     C                   MOVE      WSTXT5        XTXT
     C                   EXSR      PRINT2
     C                   END
     C                   READ      E125F                                  33
     C  N33              ENDDO
      *
     C                   ENDSR
      *
      ***********************************************
     C     PRINT2        BEGSR
      ***********************************************
     C   50              WRITE     E125P01
     C   50              MOVE      '0'           *IN50
     C                   WRITE     E125P03
      *
     C                   ENDSR
      *
      ***********************************************
     C     INIT          BEGSR
      ***********************************************
     C     EDIMDK        KLIST
     C                   KFLD                    YST
     C                   KFLD                    YSR
     C                   KFLD                    Y0065
     C     *LIKE         DEFINE    MST           YST
     C                   MOVE      'C'           YST
     C     *LIKE         DEFINE    MSR           YSR
     C                   MOVE      'R'           YSR
     C     *LIKE         DEFINE    M0065         Y0065
     C                   MOVE      'INVOIC'      Y0065
     C     EDIMGK        KLIST
     C                   KFLD                    YST
     C                   KFLD                    YSR
     C                   KFLD                    Y1004
     C     *LIKE         DEFINE    M1004         Y1004
     C     EMGK2         KLIST
     C                   KFLD                    YST
     C                   KFLD                    YSR
     C     RFF01K        KLIST
     C                   KFLD                    MMN
     C                   KFLD                    RGN
     C                   Z-ADD     1             RGN
     C     NAD02K        KLIST
     C                   KFLD                    MMN
     C                   KFLD                    NGN
     C                   Z-ADD     2             NGN
     C     DTM00K        KLIST
     C                   KFLD                    MMN
     C                   KFLD                    YGN00
     C     *LIKE         DEFINE    DGN           YGN00
     C                   MOVE      *ZEROS        YGN00
     C     DTM08K        KLIST
     C                   KFLD                    MMN
     C                   KFLD                    YGN08
     C     *LIKE         DEFINE    DGN           YGN08
     C                   Z-ADD     8             YGN08
     C     LIN22K        KLIST
     C                   KFLD                    MMN
     C                   KFLD                    YGN22
     C     *LIKE         DEFINE    DGN           YGN22
     C                   Z-ADD     22            YGN22
     C     PIA22K        KLIST
     C                   KFLD                    MMN
     C                   KFLD                    YGN22
     C                   KFLD                    LREP0
     C     IMD22K        KLIST
     C                   KFLD                    MMN
     C                   KFLD                    YGN22
     C                   KFLD                    LREP0
     C     MOA45K        KLIST
     C                   KFLD                    MMN
     C                   KFLD                    MGN
     C                   Z-ADD     45            MGN
     C     FTX99K        KLIST
     C                   KFLD                    MMN
     C                   KFLD                    FGN
     C                   KFLD                    FREP0
     C                   Z-ADD     99            FGN
     C     FT99K2        KLIST
     C                   KFLD                    MMN
     C                   KFLD                    FGN
     C     VARE1K        KLIST
     C                   KFLD                    FIRMA
     C                   KFLD                    VARENR
      *
     C     *LIKE         DEFINE    MMN           XMN
     C     *LIKE         DEFINE    M0068         Y0068
     C     *LIKE         DEFINE    W2AVD         YAVD
     C                   MOVEA     '100'         *IN(27)
     C                   MOVE      '1'           *IN51
     C                   MOVE      '01'          FIRMA
     C                   MOVE      'C'           W2ST
     C                   MOVEL     'E126R_93'    XP126            10
     C                   MOVE      'A'           XP126
     C                   MOVE      '5'           UALT              1
     C                   MOVE      *BLANKS       X4440
     C                   MOVE      'INV'         XFTX01
     C                   MOVE      USERID        XFTX02
      *
     C                   MOVE      'YBC0002'     XMSGNR            7            MELDING-ID
     C                   Z-ADD     3             XLEVEL            1 0          NIVÅ
     C                   Z-ADD     0             XRETUR            1 0          RETURKODE
     C                   Z-ADD     17            XMSGL             4 0          LENGDE PÅ MELDING
     C                   CALL      'SUBR23R3'
     C                   PARM                    XMSGNR
     C                   PARM                    XMSG             62
     C                   PARM                    XLEVEL
     C                   PARM                    XRETUR
     C                   PARM                    XMSGL
      *
     C                   ENDSR
      *
