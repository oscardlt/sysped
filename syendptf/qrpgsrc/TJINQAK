********************************************************************
      * Hente liste over arkivkoder - &type=blank ->alle,&type=angitt ->kun den aktuelle
      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/TJINQAK MODULE(*LIBL/TJINQAK *LIBL/INCGI)
CRTPGM*        ACTGRP(*NEW) AUT(*USE)
      *
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
12XXX * 17 jov PTF12 'SGAKX' arkiv-koder ekspressfortolling
********************************************************************
      *********************************************************************
      /copy SYSPEDS/qrpgsrcpy,hspecs
      /copy SYSPEDS/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FBRPARF    IF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy SYSPEDS/qrpgsrcpy,prototypeb
      /copy SYSPEDS/qrpgsrcpy,usec
      /copy SYSPEDS/qrpgsrcpy,variables3
     D user            S             10
     D type            S              5
     D FirUser         S             10
     D Error           S            150
     D JSONstring      s          32000
     D AntLest         S              9  0

      *get input-string
      /copy syspeds/qrpgsrcpy,prolog3
      *
     C                   EXSR      PARSE
     C                   EXSR      INIT

     C                   CALLP     gethtmlifs(
     C                             '/syspeddev/html/JSON.html':
     C                             '/CGITAG/')
     C                   CALLP     wrtsection('top')
      /free
        JSONstring='{'
        +'¬user¬ : ¬'+%trim(USER)+'¬, '
        +'¬errMsg¬ : ¬'+%trim(Error)+'¬ ';
      /end-free
     c                   call      'JSONFIX'
     c                   parm                    JSONstring
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      *
     C                   exsr      ARKKOD
      * Skriv JSON-string Avsluttning
     C                   EVAL      JSONstring ='}'
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%ADDr(JSONstring)
     C                                         :%len(JSONstring))
     C                   CALLP     wrtsection('JSONlin')

     C                   EXSR      EXIT
      *
     C                   EVAL      *INLR=*ON
     C                   RETURN
      *
      **************************************************
     C     ARKKOD        BEGSR
      **************************************************
      * på oppdrag
     C                   MOVEL     'SGAKO'       PAID
      * listetopp
     c                   eval      JSONstring =', "arkivKodOpdList" : ['
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      * listeinnhold
     c                   EXSR      ARKKOD2
      * listebunn
     c                   eval      JSONstring =']'
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      *
      *
      * på tur
     c                   eval      JSONstring =', "arkivKodTurList" : ['
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
     C                   MOVEL     'SGAKT'       PAID
      * listeinnhold
     c                   EXSR      ARKKOD2
      * listebunn
     c                   eval      JSONstring =']'
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      * på ekspressfortolling
     c                   eval      JSONstring =', "arkivKodExpList" : ['
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
     C                   MOVEL     'SGAKX'       PAID
      * listeinnhold
     c                   EXSR      ARKKOD2
      * listebunn
     c                   eval      JSONstring =']'
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      *
     C                   ENDSR
      **************************************************
     C     ARKKOD2       BEGSR
      **************************************************
     c                   if        type=PAID or type=' '
      * primært fra user / sekundært firmaverdier
     C                   move      *zeros        AntLest
     C                   MOVEL     BIBRID        PABRID
      * Innhold i arakivkode-list
     C     Brparkey      setll     BRPARF
     C     Brparkey      reade     BRPARF
     C                   if        %eof
     C                   eval      PABRID = FirUser
     C     Brparkey      setll     BRPARF
     C     Brparkey      reade     BRPARF
     C                   endif
     C                   dow       not %eof
     C                   ADD       1             AntLest
      /free
       if AntLest=1;
          JSONstring=*blanks;
          else;
          JSONstring=', ';
          endif;
       JSONstring=%trim(JSONstring)+'{'
          +'¬arkKod¬ : ¬'+%trim(%subst(PAVRDA:1:2))+'¬, '
          +'¬arkTxt¬ : ¬'+%trim(%subst(PAVRDA:4))+'¬}';
      /end-free
     C                   call      'JSONFIX'
     C                   parm                    JSONstring
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
     C     Brparkey      reade     BRPARF
     C                   enddo
      * Ingen ? ZO  er hadkodet default ...
     C     Antlest       ifeq      0
      /free
        PAVRDA = 'ZO Opplastet dokument';
        JSONstring='{'
          +'¬arkKod¬ : ¬'+%trim(%subst(PAVRDA:1:2))+'¬, '
          +'¬arkTxt¬ : ¬'+%trim(%subst(PAVRDA:4))+'¬}';
      /end-free
     C                   call      'JSONFIX'
     C                   parm                    JSONstring
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
     C                   endif
      *
     C                   endif
     C                   ENDSR
      *
      ************************************************************
     C     Parse         BEGSR
      ************************************************************
     C                   EVAL      user = zhbgetvar('user')
     C                   EVAL      type = zhbgetvar('type')
     C                   ENDSR
      ************************************************************
     C     INIT          BEGSR
      ************************************************************
     C                   OPEN      BRIDF
     C     USER          CHAIN     BRIDF                              50
      * En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        IFEQ      *blanks
     C                   CALLB     'INCGI'
     C                   PARM                    BISTDL
     C                   ELSE
      * Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   CALL      BILIBL
     C                   ENDIF
     C   50              eval      Error = 'Invalid userID'
     C  N50              OPEN      BRPARF
      * Key for BRPARF
     C     Brparkey      KLIST
     C                   KFLD                    PABRID
     C                   KFLD                    PAKUNR
     C                   KFLD                    PAID

     c                   Eval      FirUser = '*KUNDFT*'+BIFIRM

     C                   ENDSR
      *
      ************************************************************
     C     Exit          BEGSR
      ************************************************************
     C                   callp     wrtsection('*fini')
     C                   CLOSE     BRIDF
     C  N50              CLOSE     BRPARF
     C                   ENDSR
