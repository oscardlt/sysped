     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPDOC('STD' 'HELPNU' 'HLP400')
     A                                      HELP
      *
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  7  2
     A  99                                  DSPATR(RI)
     A                                      VALUES('5' '4' '3' '2')
     A            WSSLOP         3Y 0O  7  4EDTCDE(4)
     A            WSSTUN         8Y 0H
     A            WSSAVD         4Y 0H
     A            WSSOPD         7Y 0H
     A            WSST           1A  H
     A            WSFN           7Y 0H
     A            WSSPFO         5A  O  7  8
     A            WSSAVA         5A  O  7 14
     A            WSSANS         5A  O  7 20
     A            WSSAKN         7Y 0O  7 26EDTCDE(4)
     A  41                                  COLOR(YLW)
     A            WSSÅRS        36A  O  7 34
     A            WSSREG         6Y 0O  7 71EDTCDE(Y)
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0016)
     A                                      SFLPAG(0015)
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A                                  1  2'SYSPED/SYIS10A'
     A                                      COLOR(BLU)
     A                                  1 23MSGCON(035 TBR0117 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            UAVD           4Y 0O  1 60EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  1 65'/'
     A            UOPD           7Y 0O  1 67EDTCDE(4)
     A                                      COLOR(WHT)
     A            ZRECNO         4S 0O  1 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  2 65TIME
     A                                  3  2MSGCON(007 TBR0017 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 13MSGCON(009 TBR0018 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 25MSGCON(009 TBR0071 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  1'Op Lnr Post  Avvik Ans.  Saksnr. Å-
     A                                      rsak                               -
     A                                       Reg.dato'
     A                                      COLOR(WHT)
     A                                  6  1'       type  art k part           -
     A                                                                         -
     A                                               '
     A                                      COLOR(WHT)
     A*
     A          R BUNN
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF03(03 'Avslutt')
     A                                      CF06(06)
     A                                      CF12(12)
     A                                      SLNO(22)
     A                                      CLRL(03)
     A                                      OVERLAY
     A                                  2  2MSGCON(010 TBR0013 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  2 19MSGCON(009 TBR0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  3  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
