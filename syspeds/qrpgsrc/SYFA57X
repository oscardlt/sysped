     FSYFA57FX  CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     FSYFLD     IF   E           K DISK
     FSYFLDOPD  UF A E           K DISK
     F                                     RENAME(SYFLDR:SYFLDOR)
     D                 DS
     D  SFDBN                  1      8
     D  SLETT                  1      8
      *
     C                   EXSR      INIT
     C                   EXSR      HOVED
     C                   MOVE      '1'           *INLR
      *
      *
      *////////////////////////////////////////////////////////////
     C     INIT          BEGSR
      *////////////////////////////////////////////////////////////
      *___________________
      * Definiere Workfelt
      *"""""""""""""""""""
     C     *LIKE         DEFINE    WSFNR         WWNR
     C     *ENTRY        PLIST
     C                   PARM                    WSFNR
     C                   PARM                    UPDATE            1
      *__________________
      * Definiere Nøkkler
      *""""""""""""""""""
     C* Update tillatt ?? (50 ON--> KUN PLUKKE)
     C     UPDATE        COMP      'J'                                5050       *NONDISP
      *
     C                   ENDSR
      *
      *
      *////////////////////////////////////////////////////////////
     C     HOVED         BEGSR
      *////////////////////////////////////////////////////////////
      *______________
      * Tømme Subfile
      *""""""""""""""
      *
     C     START         TAG
     C                   MOVE      WSFNR         WWNR
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '000'         *IN(93)
     C                   Z-ADD     *ZERO         ZRECNO
     C     WSFNR         SETLL     SYFLD
      *_____________
      * Fyll Subfile (med alle poster uavhengig av linjer pr side)
      *"""""""""""""
     C                   READ      SYFLD                                  94
     C     *IN94         DOWEQ     '0'
     C                   ADD       1             ZRECNO
     C                   WRITE     SUBFIL
     C                   READ      SYFLD                                  94
     C  N94              END
      *
      *
      *_______________
      * Ingen record fantes??
      *_______________
     C     ZRECNO        IFEQ      0
     C                   MOVE      'JOV0495'     ZMSGNO
     C                   MOVE      '1'           *IN30
     C                   WRITE     BLANKREC
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     5             POSNBR
     C                   EXFMT     NOREC
     C                   MOVE      '0'           *IN30
     C                   ELSE
      *
      *_______________
      * Bevar sist brukt recno, vis subfila fra post begynnelsen
      *_______________
      *
     C     *LIKE         DEFINE    ZRECNO        XRECNO
     C                   MOVE      ZRECNO        XRECNO
     C                   Z-ADD     1             ZRECNO
      *
     C     STBILD        TAG
      *
     C                   MOVEA     '11'          *IN(91)
     C  N30              MOVE      'SY00000'     ZMSGNO
     C                   WRITE     BLANKREC
     C                   WRITE     BOTREC
     C                   Z-ADD     7             LINNBR
     C                   Z-ADD     2             POSNBR
     C                   EXFMT     SUBCTL
     C                   END
      *
     C     *IN12         CABEQ     '1'           UTHOV
     C     *IN03         CABEQ     '1'           UTHOV
      *
     C                   MOVEA     '00'          *IN(91)
      *_____________
      * F5=Frisk opp
      *"""""""""""""
     C     *IN05         CABEQ     '1'           START
      *_____________
      * F6=Lag ny
      *"""""""""""""
     C     *IN06         IFEQ      '1'
     C                   MOVE      *ZEROS        W2NR
     C                   EXSR      CRTNEW
     C                   GOTO      START
     C                   END
      *_____________
      * Options på topplinja
      *"""""""""""""
     C     TOPOPT        IFNE      ' '
     C     TOPOPT        IFEQ      '1'
     C                   MOVE      WSFNR         W2NR
     C                   EXSR      CRTNEW
     C                   ELSE
     C                   MOVE      *BLANKS       SLETT
     C                   MOVE      TOPOPT        WSNR
     C                   MOVE      WSFNR         SFNR
     C                   EXSR      OPTION
     C                   END
      * Søk utvalg til gammel verdi.. Vis subfila fra front igjen..
     C                   MOVE      WWNR          WSFNR
     C                   MOVE      ' '           TOPOPT
     C                   GOTO      START
     C                   END
      *_________________
      * Nytt søk/utvalg
      *"""""""""""""""""
     C     WSFNR         IFNE      WWNR
     C                   GOTO      START
     C                   END
      *_______
      * Valg ?
      *"""""""
     C                   MOVE      '0'           *IN52
     C                   MOVE      '0'           *IN30
     C                   MOVE      '0'           *IN95
     C     *IN52         DOWEQ     '0'
      *
     C                   READC     SUBFIL                                 52
     C     *IN52         IFEQ      '0'
     C                   EXSR      OPTION
     C                   END
      *
     C  N30
     CANN52              END
      *
     C     *IN03         CABEQ     '1'           UTHOV
     C     *IN95         CABEQ     '1'           START
     C     *IN95         CABEQ     '0'           STBILD
      *
     C     UTHOV         ENDSR
      *
      *
      *////////////////////////////////////////////////////////////
     C     OPTION        BEGSR
      *////////////////////////////////////////////////////////////
     C*
     C     WSNR          IFEQ      '4'
     C     UPDATE        ANDEQ     'J'
     C     SLETT         IFNE      '*SLETTET'
     C     SFNR          CHAIN     SYFLDOPD                           55
     C  N55              DELETE    SYFLDOR
     C                   MOVE      ' '           WSNR
     C     TOPOPT        IFEQ      ' '
     C                   MOVEL     '*SLETTET'    SLETT
     C                   UPDATE    SUBFIL
     C                   END
     C                   END
     C                   END
     C* ENDRE
     C     WSNR          IFEQ      '2'
     C     UPDATE        ANDEQ     'J'
     C     SLETT         IFNE      '*SLETTET'
     C     SFNR          CHAIN(N)  SYFLDOPD                           55
     C     *IN55         IFEQ      '0'
     C                   MOVE      SFNR          W2NR
     C                   MOVE      SFDBN         W2DBN
     C                   MOVE      SFBESK        W2BESK
     C                   MOVE      SFOVER        W2OVER
     C                   MOVE      SFLEN         W2LEN
     C                   MOVE      SFDEC         W2DEC
     C                   MOVE      SFEDIT        W2EDIT
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     5             POSNBR
     C                   EXSR      CHGNY
     C                   END
     C                   END
     C                   END
     C                   ENDSR
      *
      *
      *////////////////////////////////////////////////////////////
     C     CRTNEW        BEGSR
      *////////////////////////////////////////////////////////////
     C                   MOVE      *BLANKS       W2DBN
     C                   MOVE      *BLANKS       W2BESK
     C                   MOVE      *BLANKS       W2OVER
     C                   MOVE      *ZEROS        W2LEN
     C                   MOVE      *BLANKS       W2DEC
     C                   MOVE      *BLANKS       W2EDIT
     C                   Z-ADD     22            LINNBR
     C     W2NR          IFEQ      *ZEROS
     C                   Z-ADD     5             POSNBR
     C                   ELSE
     C                   Z-ADD     9             POSNBR
     C                   END
      *
     C                   EXSR      CHGNY
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
     C     CHGNY         BEGSR
      *////////////////////////////////////////////////////////////
     C     FEILNR        TAG
     C                   EXFMT     CHGREC
     C     *IN12         IFNE      '1'
     C     W2NR          CHAIN     SYFLDOPD                           55
      *Endret NR?? Kan IKKE finnes fra FØR....
      *   Slett gammel/lag ny..(nytt nr kan selvsagt ikke være i bruk)
     C     WSNR          IFEQ      '2'
     C     W2NR          IFNE      SFNR
     C     *IN55         IFEQ      '0'
     C                   MOVE      'JOV0496'     ZMSGNO
     C                   MOVE      '1'           *IN30
     C                   GOTO      FEILNR
     C                   ELSE
      * Nr er endret, men OK - Slett gammel - klargjør write av ny
     C     SFNR          CHAIN     SYFLDOPD                           55
     C     *IN55         IFEQ      '0'
     C                   DELETE    SYFLDOR
     C                   END
     C                   MOVE      '1'           *IN55
     C                   END
     C                   END
     C                   ELSE
     C* NY.. kan ikke finnes fra før
     C     *IN55         IFEQ      '0'
     C                   MOVE      'JOV0496'     ZMSGNO
     C                   MOVE      '1'           *IN30
     C                   GOTO      FEILNR
     C                   END
     C                   END
      *
     C                   MOVE      '0'           *IN30
     C                   MOVE      W2NR          SFNR
     C                   MOVE      W2DBN         SFDBN
     C                   MOVE      W2BESK        SFBESK
     C                   MOVE      W2OVER        SFOVER
     C                   MOVE      W2LEN         SFLEN
     C                   MOVE      W2DEC         SFDEC
     C                   MOVE      W2EDIT        SFEDIT
     C     *IN55         IFEQ      '0'
     C                   UPDATE    SYFLDOR
     C                   ELSE
     C                   WRITE     SYFLDOR
     C                   END
     C* ENDRING BASERT PÅ SUBFILPOST UPDATE SUBFIL..
     C     WSNR          IFEQ      '2'
     C                   MOVE      ' '           WSNR
     C     TOPOPT        IFEQ      ' '
     C                   UPDATE    SUBFIL
     C                   END
     C                   ELSE
     C     XRECNO        ADD       1             ZRECNO
     C                   WRITE     SUBFIL
     C                   END
     C                   END
     C                   ENDSR
      *
     C*
