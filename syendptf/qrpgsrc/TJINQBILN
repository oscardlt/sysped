      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/TJINQBILN MODULE(*LIBL/TJINQBILN *LIBL/INCGI)
CRTPGM*        ACTGRP(*NEW) AUT(*USE)
      *
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
12XXX * 01 JOV PTF12 Inføre transportmåte samt KMpris(x3) ..
********************************************************************
      *********************************************************************
      /copy SYSPEDS/qrpgsrcpy,hspecs
      /copy SYSPEDS/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FBRPARF    IF   E           K DISK    USROPN
     FUNIT      IF   E           K DISK    USROPN
     FUNITL01   IF   E           K DISK    USROPN
     F                                     RENAME(UNITR:UNITR01)
     FUNITL02   IF   E           K DISK    USROPN
     F                                     RENAME(UNITR:UNITR02)
N01  FUNITAKM   IF   E           K DISK    USROPN
     FTRAN      IF   E           K DISK    USROPN
     FDRIV      IF   E           K DISK    USROPN
      /copy SYSPEDS/qrpgsrcpy,prototypeb
      /copy SYSPEDS/qrpgsrcpy,usec
      /copy SYSPEDS/qrpgsrcpy,variables3
     D SOKBNR          s             10    varying
     D SOKBNR2         s             10
     D SOKTNR          s              8
     D SOKTNRN         s              8  0
     D SOKUT           s              4
N01  D SOKAVD          s              4
     D USER            s             10
     D MAXVIS          s              5  0
     D JSONstring      S          32000
     D Error           S            150
     D AntLest         S              9  0
     D                 DS
     D  UNRETU                 1     30
     D  UNRETU1                1      4
     D  UNRETU2                5     12
     D UPC             C                   CONST('ABCDEFGHIJKLMNOPQRST-
     D                                     UVWXYZÆØÅ')
     D LO              C                   CONST('abcdefghijklmnopqrst-
     D                                     uvwxyzæøå')
      *get input-string
      /copy syspeds/qrpgsrcpy,prolog3

     C                   EXSR      Parse

     C                   EXSR      INIT

     c                   callp     gethtmlifs(
     C                             '/syspeddev/html/JSON.html':
     C                             '/CGITAG/')
     C                   callp     wrtsection('top')

      /free
        JSONstring='{'
        +'¬user¬ : ¬'+%trim(USER)+'¬, '
        +'¬sokbnr¬ : ¬'+%trim(SOKBNR)+'¬, '
        +'¬soktnr¬ : ¬'+%trim(SOKTNR)+'¬, '
        +'¬sokut¬ : ¬'+%trim(SOKUT)+'¬, '
        +'¬error¬ : ¬'+%trim(ERROR)+'¬, ';
      /end-free
      * JSONfix escaper " i tekst,  for deretter å bytte ¬->"
     c                   call      'JSONFIX'
     c                   parm                    JSONstring
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')
      * Find/send lines
     c     Error         IFEQ      *blanks
     C                   EXSR      LINES
     C                   ENDIF

     c                   eval      JSONstring ='}'
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')
      * Quit
     C                   EXSR      Exit
      *=====================================================================
      * Parse input / cvt to numeric
      *=====================================================================
     C     PARSE         BEGSR
     C                   EVAL      USER = zhbgetvar('USER')
     C                   EVAL      SOKBNR  = zhbgetvar('SOKBNR')
     C                   EVAL      SOKBNR  = uppify(SOKBNR)
     C     LO:UPC        XLATE     SOKBNR        SOKBNR
     C                   EVAL      SOKTNR  = zhbgetvar('SOKTNR')
     C                   EVAL      SOKTNRN = c2n2(SOKTNR)
     C                   EVAL      SOKUT   = zhbgetvar('SOKUT')
     C                   EVAL      SOKUT   = uppify(SOKUT)
     C     LO:UPC        XLATE     SOKUT         SOKUT
N01  C                   EVAL      SOKAVD   = zhbgetvar('SOKAVD')
N01  C                   EVAL      UNSAVD  = c2n2(SOKAVD)
     c                   Z-ADD     100           MAXVIS
     C                   ENDSR
      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          BEGSR
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
     C                   CLOSE     BRIDF
     C  N50              CLOSE     BRPARF
     C  N50              CLOSE     UNIT
     C  N50              CLOSE     UNITL01
     C  N50              CLOSE     UNITL02
     C  N50              CLOSE     TRAN
     C  N50              CLOSE     DRIV
N01  C  N50              CLOSE     UNITAKM
     C                   MOVE      *ON           *INLR
     C                   return
     C                   ENDSR
      *=====================================================================
      * Fine lins - send to browser
      *=====================================================================
     C     Lines         BEGSR
     C                   EVAL      SOKBNR2   = SOKBNR

     c                   eval      JSONstring ='"bilnrlist" : ['
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')

     C*    SOKBNR        IFEQ      *BLANKS
     C*    SOKTNRN       ANDEQ     *ZEROS
     C*    SOKUT         ANDEQ     *BLANKS
     C*                  GOTO      UTLES
     C*                  END

     C                   SELECT
     C                   WHEN      SOKBNR <> *BLANKS
     C     SOKBNR2       SETLL     UNITL01
     C                   READ      UNITL01                                33
     C  N33SOKBNR        SCAN      UNBILN                                 34
     C                   IF        *IN33 = *OFF
     C                   IF        *IN34 = *OFF
     C                   EVAL      *IN33 = *ON
     C                   ENDIF
     C                   ENDIF
     C                   WHEN      SOKTNRN <> 0
     C     SOKTNRN       SETLL     UNIT
     C     SOKTNRN       READE     UNIT                                   33
     C                   WHEN      SOKUT <> *BLANKS
     C     SOKUT         SETLL     UNITL02
     C                   READ      UNITL02                                33
     C                   OTHER
     C     *LOVAL        SETLL     UNITL01
     C                   READ      UNITL01                                33
     C                   ENDSL

     C                   DOW       *IN33 = *OFF
     c     TRANK         CHAIN     TRAN                               33
     c   33              MOVE      *BLANKS       VMNAVN

     C                   EVAL      WDRNAVN=*BLANKS
     C                   EVAL      WDRANSA=*ZEROS
     C                   IF        UNRETU1 = 'PDA:'
     C                   EVAL      WDRANSA = c2n2(UNRETU2)
     C     WDRANSA       CHAIN     DRIV                               34
     C                   IF        *IN34 = *OFF
     C                   EVAL      WDRNAVN=DRNAVN
     C                   ENDIF
     C                   ENDIF
N01  C                   IF        UNTRMA = *zeros
N01  C                   move      *blanks       UNTRMAa           2
N01  C                   ELSE
N01  C                   move      UNTRMA        UNTRMAa
N01  C                   ENDIF
N01   * avvikende pris pr avd ??
N01  C     UnKmKey       chain     UNITAKM
      *
     C                   Add       1             AntLest
      /free
       if AntLest=1;
          JSONstring=*blanks;
          else;
          JSONstring=', ';
          endif;
       JSONstring=%trim(JSONstring)+'{'
          +'¬untran¬ : ¬'+%trim(%editc(UNTRAN:'Z'))+'¬, '
          +'¬unbiln¬ : ¬'+%trim(UNBILN)+'¬, '
          +'¬untilh¬ : ¬'+%trim(UNTILH)+'¬, '
          +'¬unland¬ : ¬'+%trim(UNLAND)+'¬, '
N01       +'¬untrme¬ : ¬'+%trim(UNTRME)+'¬, '
          +'¬untrma¬ : ¬'+%trim(UNTRMAA)+'¬, '
          +'¬unvekt¬ : ¬'+%trim(%editc(UNVEKT:'4'))+'¬, '
          +'¬untara¬ : ¬'+%trim(%editc(UNTARA:'4'))+'¬, '
          +'¬unm3¬ : ¬'+%trim(%editc(UNM3:'4'))+'¬, '
          +'¬unlm¬ : ¬'+%trim(%editc(UNLM:'4'))+'¬, '
N01       +'¬unval¬ : ¬'+%trim(UNVAL)+'¬, '
N01       +'¬unkmp¬ : ¬'+%trim(%editc(UNKMP:'4'))+'¬, '
N01       +'¬unvali¬ : ¬'+%trim(UNVALI)+'¬, '
N01       +'¬unkmpi¬ : ¬'+%trim(%editc(UNKMPI:'4'))+'¬, '
N01       +'¬unvale¬ : ¬'+%trim(UNVALE)+'¬, '
N01       +'¬unkmpe¬ : ¬'+%trim(%editc(UNKMPE:'4'))+'¬, '
          +'¬ununty¬ : ¬'+%trim(UNUNTY)+'¬, '
          +'¬unretu¬ : ¬'+%trim(%editc(WDRANSA:'Z'))+'¬, '
          +'¬unretunavn¬ : ¬'+%trim(WDRNAVN)+'¬, '
          +'¬vmnavn¬ : ¬'+%trim(VMNAVN)+'¬}';
      /end-free
     C                   call      'JSONFIX'
     C                   parm                    JSONstring
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')



     c     ANTLEST       CABGE     MAXVIS        UTLES

     C                   SELECT
     C                   WHEN      SOKBNR <> *BLANKS
     C                   READ      UNITL01                                33
     C  N33SOKBNR        SCAN      UNBILN                                 34
     C                   IF        *IN33 = *OFF
     C                   IF        *IN34 = *OFF
     C                   EVAL      *IN33 = *ON
     C                   ENDIF
     C                   ENDIF
     C                   WHEN      SOKTNRN <> 0
     C     SOKTNRN       READE     UNIT                                   33
     C                   WHEN      SOKUT <> *BLANKS
     C                   READ      UNITL02                                33
     C                   OTHER
     C                   READ      UNITL01                                33
     C                   ENDSL

     C                   ENDDO

     C     UTLES         TAG
     c                   eval      JSONstring =']'
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')

     C                   ENDSR
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          BEGSR
     C                   OPEN      BRIDF
     C     USER          CHAIN     BRIDF                              50
      * En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        IFEQ      *BLANKS
     C                   CALLB     'INCGI'
     C                   PARM                    BISTDL
     C                   ELSE
      * Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   CALL      BILIBL
     C                   ENDIF

     C   50              eval      Error = 'Invalid userID'
     C  N50              OPEN      BRPARF
     C  N50              OPEN      UNIT
     C  N50              OPEN      UNITL01
     C  N50              OPEN      UNITL02
     C  N50              OPEN      TRAN
     C  N50              OPEN      DRIV
N01  C  N50              OPEN      UNITAKM
      * Key for CUNDL01
     C     TRANK         KLIST
     C                   KFLD                    BIFIRM
     C                   KFLD                    UNTRAN
N01  C     UnKmKey       KLIST
N01  C                   kfld                    UNTRAN
N01  C                   kfld                    UNBILN
N01  C                   kfld                    UNSAVD
     C     BRPARKEY      KLIST
     C                   kfld                    PABRID
     C                   kfld                    PAKUNR
     C                   kfld                    PAID

     C     *LIKE         DEFINE    DRANSA        WDRANSA
     C     *LIKE         DEFINE    DRNAVN        WDRNAVN
     C                   MOVE      BIBRID        PABRID
     C                   MOVE      *zeros        PAKUNR
     C                   MOVE      'OSAVD'       PAID
     C  N50BRPARKEY      CHAIN     BRPARF                             33

     C                   ENDSR
