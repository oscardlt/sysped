*****F**************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnrF*Sek Sig Vers Beskrivelse...........................
"""""F*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
P0514F* 01 JOV PTF5 ENDRINGER I DOKUF (SE SYFA10)
*****F**************************************************************
     FZBA01D    CF   E             WORKSTN
     FDOKUF     IF   E           K DISK
     FKUNDL01   IF   E           K DISK
     FHEADF     IF   E           K DISK
     D                UDS
     D  UFIRM                  1      2
      *//////////////
      *  Hodelogikk /
      *//////////////
     C                   EXSR      SRA
     C                   EXSR      SRB
     C                   MOVE      '1'           *INLR
      *////////////////////////////////////////////////////////////
      *  Initiering                                           SRA /
      *////////////////////////////////////////////////////////////
     C     SRA           BEGSR
      *___________________
      * Definiere Workfelt
      *"""""""""""""""""""
     C     *LIKE         DEFINE    DFRI          YRI
     C     *LIKE         DEFINE    KUNDNR        YKN
     C                   Z-ADD     0             XTOTAL            9 2
      *__________________
      * Definiere Nøkkler
      *""""""""""""""""""
     C     KEYA          KLIST
     C                   KFLD                    YRI
     C                   KFLD                    ZFAVD
     C                   KFLD                    ZFOPD
     C                   KFLD                    ZFFBNR
     C     KEYB          KLIST
     C                   KFLD                    ZFAVD
     C                   KFLD                    ZFOPD
     C     KUND1K        KLIST
     C                   KFLD                    UFIRM
     C                   KFLD                    YKN
      *_____________
      * Init av felt
      *"""""""""""""
     C                   MOVE      'F'           YRI
     C                   MOVE      *ZEROS        YKN
      *__________________
      * Output parametrer
      *""""""""""""""""""
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Bearbeide Formater                                   SRB /
      *////////////////////////////////////////////////////////////
     C     SRB           BEGSR
      *___________
      * Startbilde
      *"""""""""""
     C     SRB1          TAG
     C  N30              MOVE      'SY00000'     ZMSGNO
     C                   WRITE     ZBA01DØ
     C                   EXFMT     ZBA01DA
     C                   MOVE      '0'           *IN30
     C     *IN03         CABEQ     '1'           SRB9
     C     *IN12         CABEQ     '1'           SRB9
      * bytte printerstyring
     C     *IN10         IFEQ      '1'
     C                   EXSR      SROVR
     C                   GOTO      SRB1
     C                   END
      * hent fraktbrev
     C     KEYA          CHAIN     DOKUF                              30
     C     *IN30         IFEQ      '1'
     C                   MOVE      'SPA0021'     ZMSGNO
     C                   GOTO      SRB1
     C                   END
     C     KEYB          CHAIN     HEADF                              30
      *________
      * Bilde02
      *""""""""
     C                   MOVE      'SY00000'     ZMSGNO
     C                   WRITE     ZBA01DØ
     C                   EXFMT     ZBA01DB
     C     *IN03         CABEQ     '1'           SRB9
     C     *IN12         CABEQ     '1'           SRB1
      * Hente kundenavn
E01  C                   MOVE      DFKNSS        YKN
     C     KUND1K        CHAIN     KUNDL01                            41
      * skriv ut merkelapp
     C                   Z-ADD     ZFNTS         DFNTXX            7 0
     C                   ADD       ZFNT          DFNTXX
     C                   SUB       1             DFNTXX
     C                   Z-ADD     ZFNTS         XANTM             4 0
     C     1             DO        ZFNT
     C                   CALL      'ZBARCR'
E01  C                   PARM                    DFPOUL
E01  C                   PARM                    DFADUL
     C                   PARM                    DF1004
     C                   PARM                    XANTM
     C                   PARM                    DFNTXX
     C                   PARM                    XTOTAL
     C                   PARM                    KNAVN
     C                   PARM                    ADR1
     C                   PARM                    ADR2
E01  C                   PARM                    DFNAVM
E01  C                   PARM                    DFAD1M
E01  C                   PARM                    DFAD3M
     C                   PARM                    DFAVD
     C                   PARM                    DFOPD
     C                   PARM                    ZANT
     C                   PARM                    HENAS
     C                   ADD       1             XANTM
     C                   END
     C                   GOTO      SRB1
      *
     C     SRB9          ENDSR
      *////////////////////////////////////////////////////////////
      *  Overstyr printerparametere                         SROVR /
      *////////////////////////////////////////////////////////////
     C     SROVR         BEGSR
     C                   MOVEL     'OVRPRTF '    QCMDA             8
     C     QCMDA         CAT       'ZBARC ??'    QCMDB            16
     C     QCMDB         CAT       'OUTQ()':0    QCMDC            21
     C                   Z-ADD     22            QCMDD            15 5
     C                   CALL      'QCMDEXC'
     C                   PARM                    QCMDC
     C                   PARM                    QCMDD
     C                   ENDSR
