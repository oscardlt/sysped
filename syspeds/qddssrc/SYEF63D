     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      REF(*LIBL/AIRNRD)
     A                                      PRINT
     A                                      HLPDOC('STD' 'HELPNU' 'HLP400')
     A                                      HELP
     A                                      ALTHELP(CA01)
      * "NOERASE" MÅ HA FOR Å IKKE TØMME BILDE SOM MAN KOMME FRA
     A          R NOERASE
     A                                      ASSUME
     A                                  1  2' '
      * MAN KUN DEFINNERE VINDU PÅ REKORDNIVÅ
     A          R WINDOWSZ
     A                                      WINDOW(*DFT 16 63 *NOMSGLIN)
     A                                      OVERLAY
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  6  2VALUES(' ' '1')
     A  27                                  DSPATR(PR)
     A            AIRCCP    R        O  6  4REFFLD(AIRNRDR/AIRCCP)
     A            AIRCOD    R        O  6  7REFFLD(AIRNRDR/AIRCOD)
     A            AIRKN     R        O  6  8REFFLD(AIRNRDR/AIRKN)
     A                                      EDTCDE(4)
     A            AIRCOM    R        O  6 17REFFLD(AIRNRDR/AIRCOM)
     A            AIRWEI    R        O  6 22REFFLD(AIRNRDR/AIRWEI)
     A                                      EDTCDE(4)
     A            AIRSRT    R        O  6 28REFFLD(AIRNRDR/AIRSRT)
     A            AIRGCD    R        O  6 30REFFLD(AIRNRDR/AIRGCD)
     A            AIRGRN    R        O  6 32REFFLD(AIRNRDR/AIRGRN)
     A                                      EDTCDE(4)
     A            AIRACD    R        O  6 47REFFLD(AIRNRDR/AIRGCD)
     A            AIRAWB    R        O  6 49REFFLD(AIRNRDR/AIRAWB)
     A                                      EDTCDE(4)
     A                                  7  5'C/P'
     A            AIRCP     R        O  7  9REFFLD(AIRNRDR/AIRCP)
     A                                      DSPATR(UL)
     A                                  7 11'T'
     A            AIRTRC    R        O  7 13REFFLD(AIRNRDR/AIRTRC)
     A                                      DSPATR(UL)
     A                                  7 16'Flys:'
     A            AIRLIN    R        O  7 22REFFLD(AIRNRDR/AIRLIN)
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
     A            AIRGC2    R        O  7 30REFFLD(AIRNRDR/AIRGC2)
     A                                      DSPATR(UL)
     A            AIRGR2    R        O  7 32REFFLD(AIRNRDR/AIRGR2)
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
     A            AIRAC2    R        O  7 47REFFLD(AIRNRDR/AIRGCD)
     A                                      DSPATR(UL)
     A            AIRAW2    R        O  7 49REFFLD(AIRNRDR/AIRAWB)
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A                                      SFLSIZ(0006)
     A                                      SFLPAG(0005)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A                                      CA03(03 'Forrige')
     A                                      CA05(05 'Frisk')
     A                                      CA06(06 'LAGE NY')
     A                                      CA12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            ZRECNO         4S 0O  3 25SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYEF63R'
     A                                      COLOR(BLU)
     A                                  1 10'Spørring på flyfrakt rater (netto)'
     A                                      COLOR(WHT)
     A                                  2  2'Frasted:'
     A            WSSDF          3A  B  2 11DSPATR(UL)
     A                                  2 22'Tilsted:'
     A            WSSDT          3A  B  2 31DSPATR(UL)
     A                                  2 42'Flysels:'
     A            WSLIN          3  0B  2 51DSPATR(UL)
     A                                      EDTCDE(4)
     A N27                              3  2'Velg opsjon:   1=Plukk'
     A                                      COLOR(BLU)
     A                                  4  2'Op RK Kundenr. Com. Vktgr S Kode G-
     A                                      r.pris/min Kode    Kilopris'
     A                                      COLOR(WHT)
     A            W2COD          1A  B  5  5DSPATR(UL)
     A            W2KN           8  0B  5  8DSPATR(UL)
     A                                      EDTCDE(4)
     A            W2COM          4A  B  5 17DSPATR(UL)
     A            W2VKT          5  0B  5 22DSPATR(UL)
     A                                      EDTCDE(4)
     A          R BUNN
     A                                      WINDOW(WINDOWSZ)
     A                                      SLNO(16)
     A                                      CLRL(01)
     A                                      OVERLAY
     A N30                              1  2'F3=Avslutt  F5=Forny  F6=Lage  F12-
     A                                      =Forrige'
     A                                      COLOR(BLU)
     A  30        MSGTXT        60A  O  1  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A          R NYWRK
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A                                      CA03(03 'Forrige')
     A                                      CA05(05 'Frisk')
     A                                      CA06(06 'LAGE NY')
     A                                      CA12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYEF63R'
     A                                      COLOR(BLU)
     A                                  1 10'Spørring på flyfrakt rater (netto)'
     A                                      COLOR(WHT)
     A                                  2  2'Frasted:'
     A            WSSDF          3A  B  2 11DSPATR(UL)
     A                                  2 22'Tilsted:'
     A            WSSDT          3A  B  2 31DSPATR(UL)
     A                                  2 42'Flysels:'
     A            WSLIN          3  0B  2 51DSPATR(UL)
     A                                      EDTCDE(4)
     A N27                              3  2'Velg opsjon:   1=Plukk'
     A                                      COLOR(BLU)
     A                                  4  2'Op RK Kundenr. Com. Vktgr S Kode G-
     A                                      r.pris/min Kode    Kilopris'
     A                                      COLOR(WHT)
     A            W2COD          1A  B  5  5DSPATR(UL)
     A            W2KN           8  0B  5  8DSPATR(UL)
     A                                      EDTCDE(4)
     A            W2COM          4A  B  5 17DSPATR(UL)
     A            W2VKT          5  0B  5 22DSPATR(UL)
     A                                      EDTCDE(4)
     A N30                             16  2MSGCON(60 YBC0212 SYMSGF)
     A                                      COLOR(BLU)
