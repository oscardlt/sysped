     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      REF(*LIBL/TRAN)
     A                                      PRINT
     A                                      HELP
     A                                      ALTHELP(CA01)
      * "NOERASE" MÅ HA FOR Å IKKE TØMME BILDE SOM MAN KOMME FRA
     A          R NOERASE
     A                                      ASSUME
     A                                  1  2' '
     A* MAN KUN DEFINNERE VINDU PÅ REKORDNIVÅ
     A          R WINDOWSZ
     A                                      OVERLAY
     A                                      WINDOW(*DFT 15 72 *NOMSGLIN)
     A          R SUBFIL                    SFL
     A            WSNR           1A  B  5  2VALUES(' ' '1' '2' '3')
     A            GFLBKO    R        O  5  4REFFLD(GODSFIR/GFLBKO *LIBL/GODSFI)
     A            GFLBS1    R        O  5 12REFFLD(GODSFIR/GFLBS1 *LIBL/GODSFI)
     A            GFLBS4    R        O  5 43REFFLD(GODSFIR/GFLBS4 *LIBL/GODSFI)
     A            GFENH          1   O  5 10
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A                                      CA03(03 'Forrige')
     A                                      CA05(05 'Frisk')
     A  50                                  CA06(06 'LAGE NY')
     A                                      CA12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYTL03B'
     A                                      COLOR(BLU)
     A N50                              1 10'Søking/plukking  lagerbevill.koder'
     A                                      COLOR(WHT)
     A  50                              1 10'Vedlikehold toll-lagerbevill.koder'
     A                                      COLOR(WHT)
     A N50                              2  2MSGCON(025 DIV1034 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A            ZRECNO         4S 0O  2 42SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  3  1'Op Bev.ko  Navn                   -
     A                                              Sted                       -
     A                                       '
     A                                      COLOR(WHT)
     A  50                              2  2'Velg option:  2 = Endre '
     A                                      COLOR(BLU)
     A  50                              2 27'3 = Kopier'
     A                                      COLOR(BLU)
     A N50        WSDATO         6Y 0B  4 17EDTCDE(4)
     A N50                              4 12MSGCON(004 SHU0951 *LIBL/SYMSGF)
     A*
     A          R BUNN
     A                                      WINDOW(WINDOWSZ)
     A                                      SLNO(15)
     A                                      CLRL(01)
     A                                      OVERLAY
     A N30                              1  2MSGCON(029 YBC0212 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        28A  O  1  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A*
     A          R NYWRK
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A                                      CA03(03 'Forrige')
     A                                      CA05(05 'Frisk')
     A  50                                  CA06(06 'LAGE NY')
     A                                      CA12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYTL03B'
     A                                      COLOR(BLU)
     A N50                              1 10'Søking/plukking  lagerbevill.koder'
     A                                      COLOR(WHT)
     A  50                              1 10'Vedlikehold toll-lagerbevill.koder'
     A                                      COLOR(WHT)
     A                                  2  2MSGCON(025 DIV1034 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                      DSPATR(ND)
     A                                  3  1'Op Bev.ko Navn                    -
     A                                            Sted                         '
     A                                      COLOR(WHT)
     A N30                             15  2MSGCON(029 YBC0212 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        28A  O 15  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
