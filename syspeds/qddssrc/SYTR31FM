*****A**************************************************************
     A* RETTINGER I DETTE PROGRAM:
PTFnrA*Sek Sig Vers Beskrivelse...........................
"""""A*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
P7XXXA* 01 YBC ULESTE MED FARGE
11175 * 02 YBC VIS ALLE LINJER OG 27X132 BILDE. INGEN MERKING
*****A**************************************************************
     A                                      DSPSIZ(27 132 *DS4)
     A                                      PRINT
     A                                      HELP
     A          R SUBFIL                    SFL
S01  A*           WSNR           1A  I  5  4VALUES(' ' '2' '4' '5')
N01  A            WSNR           1A  I  5  4VALUES(' ' '2' '4' '5' 'L' 'U')
     A  55                                  DSPATR(ND)
     A  55                                  DSPATR(PR)
     A            WSLI           2Y 0H
     A            WSDT           6Y 0O  5  9EDTCDE(Y)
     A  55                                  DSPATR(ND)
     A            WSKODE         1   O  5 18DSPATR(HI)
     A  55                                  DSPATR(ND)
     A            WSTXT         70A  O  5 20
N01  A  81                                  COLOR(YLW)
N01  A  82                                  COLOR(RED)
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0021)
     A                                      SFLPAG(0020)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                  1  2'SYSPED/SYTR31'
     A                                      COLOR(BLU)
     A N27                              1 19MSGCON(036 DIV3801 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A  27                              1 19MSGCON(036 DIV3802 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                  1 58DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  1 68TIME
     A                                      COLOR(BLU)
     A            ZRECNO         4S 0O  1 75SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  2  2MSGCON(009 DIV2122 *LIBL/SYMSGF)
     A            UAVD      R        O  2 12REFFLD(FREETXTR/FRTAVD SYSPEDF/FREE-
     A                                      TXT)
     A                                      DSPATR(HI)
     A                                      EDTCDE(4)
     A            KOANVN    R        O  2 17REFFLD(KOAR/KOANVN SYSPEDF/KODTA)
     A                                      DSPATR(HI)
     A                                  2 52MSGCON(005 DIV0074 *LIBL/SYMSGF)
     A            UTUR           8  0O  2 62COLOR(WHT)
     A N27                              3  2MSGCON(040 DIV2123 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  27                              3  2MSGCON(040 DIV2124 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
N01  A                                  3 45MSGCON(020 JOV1513 *LIBL/SYMSGF)
N01  A                                      COLOR(BLU)
     A                                  4  2MSGCON(078 DIV2125 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A          R BUNN
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A  27                                  CF03(03 'AVSLUTT')
     A                                      CF05(05 'Frisk')
     A N27N28                               CF06(06 'LAGE NY')
     A                                      CF12(12 'FORRIGE')
     A                                      SLNO(26)
     A                                      CLRL(02)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A  27                              1  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  1 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N27N28                           1 42MSGCON(018 DIV0022 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  1 62MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  2  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                              2 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A          R NYVARE
     A                                      CSRLOC(LINNBR     POSNBR)
     A  27                                  CF03(03 'AVSLUTT')
     A                                      CF05(05 'Frisk')
     A N27                                  CF06(06 'LAGE NY')
     A                                      CF12(12 'FORRIGE')
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/SYTR31'
     A                                      COLOR(BLU)
     A N27                              1 19MSGCON(036 DIV3801 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A  27                              1 19MSGCON(036 DIV3802 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                  1 58DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  1 68TIME
     A                                      COLOR(BLU)
     A            UAVD      R        O  2 12REFFLD(FREETXTR/FRTAVD SYSPEDF/FREE-
     A                                      TXT)
     A                                      DSPATR(HI)
     A                                      EDTCDE(4)
     A            KOANVN    R        O  2 17REFFLD(KOAR/KOANVN SYSPEDF/KODTA)
     A                                      DSPATR(HI)
     A                                  2 52MSGCON(005 DIV0074 *LIBL/SYMSGF)
     A            UTUR           8  0O  2 61COLOR(WHT)
     A N27                              3  2MSGCON(030 DIV2123 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  27                              3  2MSGCON(030 DIV2124 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(078 DIV2125 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A  27                             26  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 26 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N27                             26 42MSGCON(018 DIV0022 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 26 62MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 27  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                             27 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A          R BILDE01
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF03(03 'AVSLUTT')
     A                                      CF04(04 'SØK')
     A                                      CF10(10 'NESTE SIDE')
     A                                      CF12(12 'FORRIGE')
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/SYTR31'
     A                                      COLOR(BLU)
     A N27                              1 19MSGCON(036 DIV3801 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A  27                              1 19MSGCON(036 DIV3802 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                  1 58DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  1 71TIME
     A                                      COLOR(BLU)
     A                                  2  2MSGCON(009 DIV2122 *LIBL/SYMSGF)
     A            UAVD      R        O  2 12REFFLD(FREETXTR/FRTAVD SYSPEDF/FREE-
     A                                      TXT)
     A                                      DSPATR(HI)
     A                                      EDTCDE(4)
     A            KOANVN    R        O  2 17REFFLD(KOAR/KOANVN SYSPEDF/KODTA)
     A                                      DSPATR(HI)
     A                                  2 51MSGCON(005 DIV0074 *LIBL/SYMSGF)
     A            UTUR           8  0O  2 58COLOR(WHT)
     A                                  2 67MSGCON(005 DIV1296 *LIBL/SYMSGF)
S01  A*           WSDT      R        O  2 73REFFLD(FREETXTR/FRTDT SYSPEDF/FREE-
S01  A*                                     TXT)
N01  A            WSDT           6  0O  2 73
     A                                      DSPATR(HI)
     A                                      EDTCDE(Y)
     A            WKOD01         1A  B  3  2DSPATR(UL)
     A  51
     AO 27                                  DSPATR(PR)
     A            WKO001         2A  B  3  5
     A  51                                  DSPATR(PR)
     A            WSTX01        70A  B  3 11DSPATR(UL)
     A  51
     AO 27                                  DSPATR(PR)
     A            WKOD02         1A  B  4  2DSPATR(UL)
     A  52
     AO 27                                  DSPATR(PR)
     A            WKO002         2A  B  4  5
     A  52                                  DSPATR(PR)
     A            WSTX02        70A  B  4 11DSPATR(UL)
     A  52
     AO 27                                  DSPATR(PR)
     A            WKOD03         1A  B  5  2DSPATR(UL)
     A  53
     AO 27                                  DSPATR(PR)
     A            WKO003         2A  B  5  5
     A  53                                  DSPATR(PR)
     A            WSTX03        70A  B  5 11DSPATR(UL)
     A  53
     AO 27                                  DSPATR(PR)
     A            WKOD04         1A  B  6  2DSPATR(UL)
     A  54
     AO 27                                  DSPATR(PR)
     A            WKO004         2A  B  6  5
     A  54                                  DSPATR(PR)
     A            WSTX04        70A  B  6 11DSPATR(UL)
     A  54
     AO 27                                  DSPATR(PR)
     A            WKOD05         1A  B  7  2DSPATR(UL)
     A  55
     AO 27                                  DSPATR(PR)
     A            WKO005         2A  B  7  5
     A  55                                  DSPATR(PR)
     A            WSTX05        70A  B  7 11DSPATR(UL)
     A  55
     AO 27                                  DSPATR(PR)
     A            WKOD06         1A  B  8  2DSPATR(UL)
     A  56
     AO 27                                  DSPATR(PR)
     A            WKO006         2A  B  8  5
     A  56                                  DSPATR(PR)
     A            WSTX06        70A  B  8 11DSPATR(UL)
     A  56
     AO 27                                  DSPATR(PR)
     A            WKOD07         1A  B  9  2DSPATR(UL)
     A  57
     AO 27                                  DSPATR(PR)
     A            WKO007         2A  B  9  5
     A  57                                  DSPATR(PR)
     A            WSTX07        70A  B  9 11DSPATR(UL)
     A  57
     AO 27                                  DSPATR(PR)
     A            WKOD08         1A  B 10  2DSPATR(UL)
     A  58
     AO 27                                  DSPATR(PR)
     A            WKO008         2A  B 10  5
     A  58                                  DSPATR(PR)
     A            WSTX08        70A  B 10 11DSPATR(UL)
     A  58
     AO 27                                  DSPATR(PR)
     A            WKOD09         1A  B 11  2DSPATR(UL)
     A  59
     AO 27                                  DSPATR(PR)
     A            WKO009         2A  B 11  5
     A  59                                  DSPATR(PR)
     A            WSTX09        70A  B 11 11DSPATR(UL)
     A  59
     AO 27                                  DSPATR(PR)
     A            WKOD10         1A  B 12  2DSPATR(UL)
     A  60
     AO 27                                  DSPATR(PR)
     A            WKO010         2A  B 12  5
     A  60                                  DSPATR(PR)
     A            WSTX10        70A  B 12 11DSPATR(UL)
     A  60
     AO 27                                  DSPATR(PR)
     A            WKOD11         1A  B 13  2DSPATR(UL)
     A  61
     AO 27                                  DSPATR(PR)
     A            WKO011         2A  B 13  5
     A  61                                  DSPATR(PR)
     A            WSTX11        70A  B 13 11DSPATR(UL)
     A  61
     AO 27                                  DSPATR(PR)
     A            WKOD12         1A  B 14  2DSPATR(UL)
     A  62
     AO 27                                  DSPATR(PR)
     A            WKO012         2A  B 14  5
     A  62                                  DSPATR(PR)
     A            WSTX12        70A  B 14 11DSPATR(UL)
     A  62
     AO 27                                  DSPATR(PR)
     A            WKOD13         1A  B 15  2DSPATR(UL)
     A  63
     AO 27                                  DSPATR(PR)
     A            WKO013         2A  B 15  5
     A  63                                  DSPATR(PR)
     A            WSTX13        70A  B 15 11DSPATR(UL)
     A  63
     AO 27                                  DSPATR(PR)
     A            WKOD14         1A  B 16  2DSPATR(UL)
     A  64
     AO 27                                  DSPATR(PR)
     A            WKO014         2A  B 16  5
     A  64                                  DSPATR(PR)
     A            WSTX14        70A  B 16 11DSPATR(UL)
     A  64
     AO 27                                  DSPATR(PR)
     A            WKOD15         1A  B 17  2DSPATR(UL)
     A  65
     AO 27                                  DSPATR(PR)
     A            WKO015         2A  B 17  5
     A  65                                  DSPATR(PR)
     A            WSTX15        70A  B 17 11DSPATR(UL)
     A  65
     AO 27                                  DSPATR(PR)
     A            WKOD16         1A  B 18  2DSPATR(UL)
     A  66
     AO 27                                  DSPATR(PR)
     A            WKO016         2A  B 18  5
     A  66                                  DSPATR(PR)
     A            WSTX16        70A  B 18 11DSPATR(UL)
     A  66
     AO 27                                  DSPATR(PR)
     A            WKOD17         1A  B 19  2DSPATR(UL)
     A  67
     AO 27                                  DSPATR(PR)
     A            WKO017         2A  B 19  5
     A  67                                  DSPATR(PR)
     A            WSTX17        70A  B 19 11DSPATR(UL)
     A  67
     AO 27                                  DSPATR(PR)
     A            WKOD18         1A  B 20  2DSPATR(UL)
     A  68
     AO 27                                  DSPATR(PR)
     A            WKO018         2A  B 20  5
     A  68                                  DSPATR(PR)
     A            WSTX18        70A  B 20 11DSPATR(UL)
     A  68
     AO 27                                  DSPATR(PR)
     A            WKOD19         1A  B 21  2DSPATR(UL)
     A  69
     AO 27                                  DSPATR(PR)
     A            WKO019         2A  B 21  5
     A  69                                  DSPATR(PR)
     A            WSTX19        70A  B 21 11DSPATR(UL)
     A  69
     AO 27                                  DSPATR(PR)
     A            WKOD20         1A  B 22  2DSPATR(UL)
     A  70
     AO 27                                  DSPATR(PR)
     A            WKO020         2A  B 22  5
     A  70                                  DSPATR(PR)
     A            WSTX20        70A  B 22 11DSPATR(UL)
     A  70
     AO 27                                  DSPATR(PR)
     A            WKOD21         1A  B 23  2DSPATR(UL)
     A  71
     AO 27                                  DSPATR(PR)
     A            WKO021         2A  B 23  5
     A  71                                  DSPATR(PR)
     A            WSTX21        70A  B 23 11DSPATR(UL)
     A  71
     AO 27                                  DSPATR(PR)
     A            WKOD22         1A  B 24  2DSPATR(UL)
     A  72
     AO 27                                  DSPATR(PR)
     A            WKO022         2A  B 24  5
     A  72                                  DSPATR(PR)
     A            WSTX22        70A  B 24 11DSPATR(UL)
     A  72
     AO 27                                  DSPATR(PR)
     A            WKOD23         1A  B 25  2DSPATR(UL)
     A  73
     AO 27                                  DSPATR(PR)
     A            WKO023         2A  B 25  5
     A  73                                  DSPATR(PR)
     A            WSTX23        70A  B 25 11DSPATR(UL)
     A  73
     AO 27                                  DSPATR(PR)
     A                                 26  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 26 42MSGCON(018 DIV2127 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 26 66MSGCON(005 DIV1297 *LIBL/SYMSGF)
     A            WSSIDE         1Y 0O 26 73DSPATR(HI)
     A                                      EDTCDE(4)
     A                                      DSPATR(UL)
     A  30        MSGTXT        78A  O 27  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                             27  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N30                             27 42MSGCON(018 DIV0032 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A            MSGNR          7A  P
     A          R BILDE00
     A                                      CF03(03 'Avslutt program')
     A                                      CF12(03)
     A                                  1  2'SYSPED/SYTR31'
     A                                      COLOR(BLU)
     A N27                              1 19MSGCON(036 DIV3801 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A  27                              1 19MSGCON(036 DIV3802 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                  1 58DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  1 71TIME
     A                                      COLOR(BLU)
     A                                 11 15MSGCON(018 DIV0600 *LIBL/SYMSGF)
     A            UAVD      R        B 11 35REFFLD(FREETXTR/FRTAVD SYSPEDF/FREE-
     A                                      TXT)
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
     A  30                                  DSPATR(PC)
     A  30                                  DSPATR(RI)
     A                                 13 15MSGCON(018 DIV3803 *LIBL/SYMSGF)
     A            UTUR           8Y 0B 13 35
     A  30                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A N30                             26  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30                             27  1MSGCON(078 SPO0002 *LIBL/SYMSGF)
      *
     A          R PDAWIN
     A                                      WINDOW(*DFT 7 60)
     A                                      OVERLAY
     A                                  1  2'SYSPED/SYTR31 '
     A                                      COLOR(BLU)
     A                                  1 20'Melding fra PDA (kode P) finnes'
     A                                      COLOR(WHT)
     A                                  3  2'Videre status skal være '
     A            WSPDST         1A  B  3 30
     A                                  3 33'L=Lest                 '
     A                                  4 33'Blank = Åpen/varsles   '
     A                                  6 34'Enter=Videre'
     A                                      COLOR(BLU)
