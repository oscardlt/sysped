     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPDOC('STD' 'HELPNU' 'HLP400')
     A                                      HELP
     A          R NC068D01
     A                                      CA03(03 'Avslutt program.')
     A                                  1  3'SYSPED/NCTS068R'
     A                                      COLOR(BLU)
     A                                  1 62DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(GRN)
     A                                  1 71TIME
     A                                      COLOR(GRN)
     A                                  4 23'Endre status på NCTS ankomst'
     A                                      COLOR(WHT)
     A                                  8 23'Avdeling:'
     A            WSAVD          4Y 0B  8 40DSPATR(UL)
     A  99                                  DSPATR(RI)
     A  99                                  DSPATR(PC)
     A                                      EDTCDE(4)
     A                                 10 23'Signatur:'
     A            WSSG           3A  B 10 40TEXT('Signatur')
     A                                      DSPATR(UL)
     A  98                                  DSPATR(RI)
     A  98                                  DSPATR(PC)
     A  98                                  ERRMSG('Tastet tariffør er ikke reg-
     A                                      istrert.' 98)
     A                                 12 23'Oppdragsnr.'
     A            WSOPD          7Y 0B 12 40TEXT('Oppdragsnummer')
     A                                      DSPATR(UL)
     A  99                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                 15 23'Ny status:'
     A            WSST           1A  B 15 40TEXT('Status ny')
     A                                      DSPATR(UL)
     A                                 23 23'F3 = Avslutt'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R NC068D02
     A                                      CA05(05 'Endre status')
     A                                  1  3'SYSPED/NCTS068R'
     A                                      COLOR(BLU)
     A                                  1 20'Endre status på NCTS ankomst'
     A                                      COLOR(WHT)
     A                                  1 48'Ref.'
     A            WSAVD          4Y 0O  1 53COLOR(WHT)
     A                                      EDTCDE(4)
     A            WSOPD          7Y 0O  1 58COLOR(WHT)
     A                                      EDTCDE(4)
     A            WSSG           3A  O  1 66DSPATR(UL)
     A                                      COLOR(WHT)
     A            TITARF        16A  O  2 64TEXT('Tariffer')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  3  2'MRN-nr.:'
     A            TITRNR        18A  O  3 13TEXT('MRN-nr')
     A                                      COLOR(YLW)
     A                                  3 42'Ankomst dato/tid:'
     A            WSDTA          6Y 0O  3 63TEXT('Ankomst dato')
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSTMA          4Y 0O  3 70TEXT('Ankomst tid')
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  4  2'Forenklet:'
     A            TISPR          1Y 0O  4 13TEXT('FORENKLET PROSEDYRE')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  4 32'Hendelse:'
     A            THIFLG         1Y 0O  4 43TEXT('Hendelsesflagg')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  6  2'Autorisasjon:'
     A                                  6 22'Type:'
     A            THATYP         4A  O  6 28TEXT('Autorisasjonstype')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  6 33'Ref:'
     A            THAREF        35A  O  6 38TEXT('Autorisasjons ref.nr')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  7  2'Destinasj:'
     A            THTSBA         8A  O  7 13TEXT('Destinasjons tollsted')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  8  2'Trader:'
     A            THKNTA         8Y 0O  8 13TEXT('Trade kundenr')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            TINATA        30A  O  8 22TEXT('Trader navn')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            TITINTA       17A  O  8 53TEXT('Trader org.nr')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  8 71'Språk'
     A            TISKTA         2A  O  8 77TEXT('Språkkode')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  9  2'Lokasjon:'
     A                                 10  4'Type:'
     A            TILCTY         1A  O 10 13TEXT('Lokasjon type')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 10 15'Kvalif:'
     A            TIQID          1A  O 10 23TEXT('Qualifier')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 10 25'Autorisasjon:'
     A            TIANR         35A  O 10 39TEXT('Autorisasjonsnr')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 11  4'Till:'
     A            TIAID          4A  O 11 13TEXT('Tilleggs identifier')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 11 18'Lok:'
     A            TISD          17A  O 11 23TEXT('Lokasjons UN kode')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 12  2'Tollsted:'
     A            TITS           8A  O 12 13TEXT('Tolldted')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 13  2'Økonomi:'
     A            TIKNE          8Y 0O 13 13TEXT('Økonomi operatørsnr')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            TITINE        17A  O 13 22TEXT('Økonomi operatørs org.nr')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 14  2'Addresse:'
     A            TIADE1        30A  O 14 13TEXT('Gateadresse')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 15  2'PN/PS/LK:'
     A            TIPNE         17A  O 15 13TEXT('Postnr')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            TIPSE         24A  O 15 31TEXT('Poststed')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            TILKE          2A  O 15 56TEXT('Landkode')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 16  2'Post adr.:'
     A            TIADE2        17A  O 16 13TEXT('Husnr')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            TIPNE2        17A  O 16 31TEXT('Postnr')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            TILKE2         2A  O 16 49TEXT('Landkode')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 17  2'KontaktP:'
     A            TICPE         30A  O 17 13TEXT('Kontaktperson')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 18  2'Tlf.:'
     A            TITLE         20A  O 18 13TEXT('Tlf')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 19  2'Epost:'
     A            TIEME         50A  O 19 13TEXT('Epostadresse')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 20  2'Reg.dato'
     A            TIDTR          8Y 0H
     A            WSDTR          6Y 0O 20 13TEXT('Registreringsdato')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(Y)
     A                                 23  1'Gammel status'
     A                                      COLOR(TRQ)
     A            TIST           1A  O 23 15TEXT('Status')
     A                                      DSPATR(UL)
     A                                      COLOR(PNK)
     A                                 23 18'Ny status'
     A                                      COLOR(TRQ)
     A            WSST           1A  O 23 28TEXT('Status')
     A                                      DSPATR(UL)
     A                                      COLOR(RED)
     A                                 24  2'F5=Endre status'
     A                                      COLOR(BLU)
