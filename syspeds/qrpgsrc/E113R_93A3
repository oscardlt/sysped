     H DATEDIT(*YMD)
     FEDIM      IF   E           K DISK
     FECNI93A   IF   E           K DISK
     FECNT93A   IF   E           K DISK
     FEDTM93A   IF   E           K DISK
     FEFTX93A   IF   E           K DISK
     FEGID93A   IF   E           K DISK
     FELOC93A   IF   E           K DISK
     FEMEA93A   IF   E           K DISK
     FEMOA93A   IF   E           K DISK
     FENAD93A   IF   E           K DISK
     FEPCI93A   IF   E           K DISK
     FETDT93A   IF   E           K DISK
     FVAADVH    O  A E             DISK
     FVAADVV    O  A E             DISK
     D A               S              1    DIM(79)
     D B               S              1    DIM(18)
      *________________________
      * DS for decimal handling
      *""""""""""""""""""""""""
     D                 DS
     D  ZDEC                   1      7  6
     D  ZDEC6                  2      7
     D  ZHEL                  11     22  0
     D                 DS
     D  WMN2                   1      9  0
     D                 DS
     D  WTIME                  1     14  0
     D  WTID                   1      6  0
     D  WDD                    7      8  0
     D  WMM                    9     10  0
     D  W≈≈                   11     14  0
     D                 DS
     D  WDATO                  1      8  0
     D  W≈                     1      4  0
     D  WM                     5      6  0
     D  WD                     7      8  0
      */////////////
      * MAIN LOGIC /
      */////////////
      *___________
      * Initiering
      *"""""""""""
     C                   EXSR      INIT
      *________
      * CONVERT
      *""""""""
     C                   EXSR      BGM00
     C                   EXSR      FTX00
     C                   EXSR      CNT00
     C                   EXSR      TDT05
     C                   EXSR      CNI15
     C                   WRITE     VAADVHR
      *
     C                   MOVE      '1'           *INLR
      *////////////////////////////////////////////////////////////
      * BGM00                                               BGM00 /
      *////////////////////////////////////////////////////////////
     C     BGM00         BEGSR
      *__________________
      * CLEAR HEADINGFILE
      *""""""""""""""""""
     C                   CLEAR                   VAADVHR
      *____________
      * INIT FIELDS
      *""""""""""""
     C                   MOVEL     M1004         VHSREF
     C                   Z-ADD     MSN           VHSN
     C                   Z-ADD     MMN           VHMN
     C                   MOVEL     WDATO         VHDT
     C                   MOVEL     WTID          VHTM
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * CNI15                                               CNI15 /
      *////////////////////////////////////////////////////////////
     C     CNI15         BEGSR
      *___________________
      * READ CONSIGNEMENTS
      *"""""""""""""""""""
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     15            ZGN0
     C     KEYMS0        SETLL     ECNI93A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS0        READE     ECNI93A                                51
     C     *IN51         DOWEQ     '0'
      *
     C                   EXSR      RTVDB
      *
     C     KEYMS0        READE     ECNI93A                                51
     C  N51              ENDDO
     C                   ENDIF
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * CNT00                                               CNT00 /
      *////////////////////////////////////////////////////////////
     C     CNT00         BEGSR
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     0             ZGN0
     C     KEYMS0        SETLL     ECNT93A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS0        READE     ECNT93A                                51
     C     *IN51         DOWEQ     '0'
      *
     C     C6069         IFEQ      '11 '
     C                   MOVEA     C6066         A
     C                   EXSR      GETNUM
     C                   Z-ADD     ZNUM          VHGIDN
     C                   ENDIF
      *
     C     KEYMS0        READE     ECNT93A                                51
     C  N51              ENDDO
     C                   ENDIF
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * DTM05                                               DTM05 /
      *////////////////////////////////////////////////////////////
     C     DTM05         BEGSR
     C                   MOVE      '0'           *IN52
     C                   Z-ADD     5             ZGN1
     C     KEYMS1        SETLL     EDTM93A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS1        READE     EDTM93A                                52
     C     *IN52         DOWEQ     '0'
      *
     C     D2005         IFEQ      '136'
     C                   MOVEL     D2380         VHETD
     C     VHETD         DIV       100           VHETD
     C                   ADD       19000000      VHETD
     C                   ELSE
     C     D2005         IFEQ      '232'
     C                   MOVEL     D2380         VHETA
     C     VHETA         DIV       100           VHETA
     C                   ADD       19000000      VHETA
     C                   ENDIF
     C                   ENDIF
      *
     C     KEYMS1        READE     EDTM93A                                52
     C                   ENDDO
     C                   ENDIF
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * FTX00                                               FTX00 /
      *////////////////////////////////////////////////////////////
     C     FTX00         BEGSR
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     0             ZGN0
     C     KEYMS0        SETLL     EFTX93A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS0        READE     EFTX93A                                51
     C     *IN51         DOWEQ     '0'
      *
     C                   SELECT
     C     VHFTXA        WHENEQ    *BLANKS
     C                   MOVEL     F4440A        VHFTXA
     C                   MOVEL     F4440B        VHFTXB
     C                   MOVEL     F4440C        VHFTXC
     C                   MOVEL     F4440D        VHFTXD
     C                   MOVEL     F4440E        VHFTXE
     C     VHFTXF        WHENEQ    *BLANKS
     C                   MOVEL     F4440A        VHFTXF
     C                   MOVEL     F4440B        VHFTXG
     C                   MOVEL     F4440C        VHFTXH
     C                   MOVEL     F4440D        VHFTXI
     C                   MOVEL     F4440E        VHFTXJ
     C     VHFTXK        WHENEQ    *BLANKS
     C                   MOVEL     F4440A        VHFTXK
     C                   MOVEL     F4440B        VHFTXL
     C                   MOVEL     F4440C        VHFTXM
     C                   MOVEL     F4440D        VHFTXN
     C                   MOVEL     F4440E        VHFTXO
     C     VHFTXP        WHENEQ    *BLANKS
     C                   MOVEL     F4440A        VHFTXP
     C                   MOVEL     F4440B        VHFTXQ
     C                   MOVEL     F4440C        VHFTXR
     C                   MOVEL     F4440D        VHFTXS
     C                   MOVEL     F4440E        VHFTXT
     C     VHFTXU        WHENEQ    *BLANKS
     C                   MOVEL     F4440A        VHFTXU
     C                   MOVEL     F4440B        VHFTXV
     C                   MOVEL     F4440C        VHFTXW
     C                   MOVEL     F4440D        VHFTXX
     C                   MOVEL     F4440E        VHFTXY
     C                   ENDSL
      *
     C     KEYMS0        READE     EFTX93A                                51
     C  N51              ENDDO
     C                   ENDIF
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * FTX15                                               FTX15 /
      *////////////////////////////////////////////////////////////
     C     FTX15         BEGSR
     C                   MOVE      '0'           *IN52
     C                   Z-ADD     15            ZGN1
     C     KEYMS1        SETLL     EFTX93A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS1        READE     EFTX93A                                52
     C     *IN52         DOWEQ     '0'
      *
     C                   SELECT
     C     F4451         WHENEQ    'AAA'
     C                   MOVEL     F4440A        VVGODA
     C                   MOVEL     F4440B        VVGODB
     C                   MOVEL     F4440C        VVGODC
     C                   MOVEL     F4440D        VVGODD
     C                   MOVEL     F4440E        VVGODE
     C     F4451         WHENEQ    'AAI'
     C                   MOVEL     F4440A        VVPCIA
     C                   MOVEL     F4440B        VVPCIB
     C                   MOVEL     F4440C        VVPCIC
     C                   MOVEL     F4440D        VVPCID
     C                   MOVEL     F4440E        VVPCIE
     C     F4451         WHENEQ    'AAR'
     C                   MOVEL     F4440A        VVPCIF
     C                   MOVEL     F4440B        VVPCIG
     C                   MOVEL     F4440C        VVPCIH
     C                   MOVEL     F4440D        VVPCII
     C                   MOVEL     F4440E        VVPCIJ
     C     F4451         WHENEQ    'CCI'
     C                   MOVEL     F4440A        VVAASA
     C                   MOVEL     F4440B        VVAASB
     C                   MOVEL     F4440C        VVAASC
     C                   MOVEL     F4440D        VVAASD
     C                   MOVEL     F4440E        VVAASE
     C     F4451         WHENEQ    'SIC'
     C                   MOVEL     F4440A        VVAASA
     C                   MOVEL     F4440B        VVAASB
     C                   MOVEL     F4440C        VVAASC
     C                   MOVEL     F4440D        VVAASD
     C                   MOVEL     F4440E        VVAASE
     C                   ENDSL
      *
     C     KEYMS1        READE     EFTX93A                                52
     C  N52              ENDDO
     C                   ENDIF
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * FTX31                                               FTX31 /
      *////////////////////////////////////////////////////////////
     C     FTX31         BEGSR
     C                   MOVE      '0'           *IN53
     C                   Z-ADD     31            ZGN2
     C     KEYMS2        SETLL     EFTX93A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS2        READE     EFTX93A                                53
     C     *IN53         DOWEQ     '0'
      *
     C                   SELECT
     C     F4451         WHENEQ    'AAA'
     C                   MOVEL     F4440A        VVPRDA
     C                   ENDSL
      *
     C     KEYMS2        READE     EFTX93A                                53
     C  N53              ENDDO
     C                   ENDIF
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Get numerical value                               GETNUM /
      *////////////////////////////////////////////////////////////
     C     GETNUM        BEGSR
     C                   MOVE      *ZEROS        ZHEL
     C                   MOVE      *ZEROS        ZDEC
     C                   MOVE      *ZEROS        B
     C                   Z-ADD     1             AX                2 0
     C                   Z-ADD     1             BX                2 0
      * Get Integer
     C     A(AX)         DOWGE     '0'
     C     ZHEL          MULT      10            ZHEL
     C                   MOVE      A(AX)         ZHEL
     C                   ADD       1             AX
     C                   ENDDO
      * Decimal point ?
     C     A(AX)         IFEQ      '.'
     C     A(AX)         OREQ      ','
     C                   ADD       1             AX
     C     A(AX)         DOWGE     '0'
     C                   MOVE      A(AX)         B(BX)
     C                   ADD       1             AX
     C                   ADD       1             BX
     C                   ENDDO
     C                   MOVEA     B             ZDEC6
     C                   ENDIF
      * Concatenate
     C                   Z-ADD     ZHEL          ZNUM             18 6
     C                   ADD       ZDEC          ZNUM
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * GID31                                               GID31 /
      *////////////////////////////////////////////////////////////
     C     GID31         BEGSR
     C                   MOVE      '0'           *IN52
     C                   Z-ADD     31            ZGN1
     C     KEYMS1        SETLL     EGID93A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS1        READE     EGID93A                                52
     C     *IN52         DOWEQ     '0'
      *
     C                   MOVEA     G7224A        A
     C                   EXSR      GETNUM
     C                   ADD       ZNUM          VVPACN
     C                   ADD       ZNUM          VVCT
     C                   MOVEL     G7065A        VVPACT
     C                   MOVE      *ON           *IN52
     C                   Z-ADD     GREP0         ZREPC0
     C                   Z-ADD     GREP1         ZREPC1
     C                   EXSR      FTX31
     C                   EXSR      MEA34
     C                   EXSR      PCI37
      *
     C     KEYMS1        READE     EGID93A                                52
     C  N52              ENDDO
     C                   ENDIF
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *                                                      INIT /
      *////////////////////////////////////////////////////////////
     C     INIT          BEGSR
     C     KEYMS0        KLIST
     C                   KFLD                    ZMN
     C                   KFLD                    ZGN0
     C     KEYMS1        KLIST
     C                   KFLD                    ZMN
     C                   KFLD                    ZGN1
     C                   KFLD                    ZREPB0
     C     KEYMS2        KLIST
     C                   KFLD                    ZMN
     C                   KFLD                    ZGN2
     C                   KFLD                    ZREPC0
     C                   KFLD                    ZREPC1
      *
     C     *LIKE         DEFINE    NMN           ZMN
     C     *LIKE         DEFINE    NGN           ZGN0
     C     *LIKE         DEFINE    NGN           ZGN1
     C     *LIKE         DEFINE    NGN           ZGN2
     C     *LIKE         DEFINE    NREP0         ZREPB0
     C     *LIKE         DEFINE    NREP0         ZREPC0
     C     *LIKE         DEFINE    NREP0         ZREPC1
      * INIT WORKFIELDS
     C     *ENTRY        PLIST
     C                   PARM                    WMN1              9
     C                   MOVEL     WMN1          WMN2
     C                   Z-ADD     WMN2          ZMN
      * Get msg and interchange values
     C     ZMN           CHAIN     EDIM                               51
      * HENTE DAGENS DATO OG TID
     C                   TIME                    WTIME
     C                   Z-ADD     W≈≈           W≈
     C                   Z-ADD     WDD           WD
     C                   Z-ADD     WMM           WM
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * LOC05                                               LOC05 /
      *////////////////////////////////////////////////////////////
     C     LOC05         BEGSR
     C                   MOVE      '0'           *IN52
     C                   Z-ADD     5             ZGN1
     C     KEYMS1        SETLL     ELOC93A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS1        READE     ELOC93A                                52
     C     *IN52         DOWEQ     '0'
      *
     C     L3227         IFEQ      '8  '
     C     L1131A        IFEQ      '16 '
     C                   MOVEL     L3225         VHLODA
     C                   ELSE
     C     L1131A        IFEQ      '162'
     C                   MOVEL     L3225         VHLODB
     C                   ENDIF
     C                   ENDIF
     C                   ENDIF
      *
     C     KEYMS1        READE     ELOC93A                                52
     C  N52              ENDDO
     C                   ENDIF
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * LOC23                                               LOC23 /
      *////////////////////////////////////////////////////////////
     C     LOC23         BEGSR
     C                   MOVE      '0'           *IN53
     C                   Z-ADD     23            ZGN2
     C     KEYMS2        SETLL     ELOC93A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS2        READE     ELOC93A                                53
     C     *IN53         DOWEQ     '0'
      *
     C     L3227         IFEQ      '8'
     C                   MOVEL     L3225         VHLODA
     C                   END
      *
     C     KEYMS2        READE     ELOC93A                                53
     C                   ENDDO
     C                   ENDIF
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * MOA15                                               MOA15 /
      *////////////////////////////////////////////////////////////
     C     MOA15         BEGSR
     C                   MOVE      '0'           *IN52
     C                   Z-ADD     15            ZGN1
     C     KEYMS1        SETLL     EMOA93A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS1        READE     EMOA93A                                52
     C     *IN52         DOWEQ     '0'
      *
     C     M5025         IFEQ      '22 '
     C                   MOVEA     M5004         A
     C                   EXSR      GETNUM
     C                   Z-ADD     ZNUM          VVFRCA
     C                   MOVEL     M6345         VVFRCC
     C                   ENDIF
      *
     C     KEYMS1        READE     EMOA93A                                52
     C  N52              ENDDO
     C                   ENDIF
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * MEA34                                               MEA34 /
      *////////////////////////////////////////////////////////////
     C     MEA34         BEGSR
     C                   MOVE      '0'           *IN53
     C                   Z-ADD     34            ZGN2
     C     KEYMS2        SETLL     EMEA93A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS2        READE     EMEA93A                                53
     C     *IN53         DOWEQ     '0'
      *
     C                   MOVEA     M6314         A
     C                   EXSR      GETNUM
     C                   SELECT
     C     M6311         WHENEQ    'WT '
     C     M6313         ANDEQ     'G  '
     C                   Z-ADD     ZNUM          VVWT
     C     M6311         WHENEQ    'VOL'
     C                   Z-ADD     ZNUM          VVVO
     C                   ENDSL
      *
     C     KEYMS2        READE     EMEA93A                                53
     C  N53              ENDDO
     C                   ENDIF
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * NAD09                                               NAD09 /
      *////////////////////////////////////////////////////////////
     C     NAD09         BEGSR
     C                   MOVE      '0'           *IN52
     C                   Z-ADD     9             ZGN1
     C     KEYMS1        SETLL     ENAD93A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS1        READE     ENAD93A                                52
     C     *IN52         DOWEQ     '0'
      *
     C                   SELECT
     C     N3035         WHENEQ    'DC'
     C                   MOVEL     N3039         VHNIA
     C     N3035         WHENEQ    'FW'
     C                   MOVEL     N3039         VHCARI
     C                   ENDSL
      *
     C     KEYMS1        READE     ENAD93A                                52
     C  N52              ENDDO
     C                   ENDIF
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * NAD25                                               NAD25 /
      *////////////////////////////////////////////////////////////
     C     NAD25         BEGSR
     C                   MOVE      '0'           *IN52
     C                   Z-ADD     25            ZGN1
     C     KEYMS1        SETLL     ENAD93A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS1        READE     ENAD93A                                52
     C     *IN52         DOWEQ     '0'
      *
     C                   SELECT
     C     N3035         WHENEQ    'CZ'
     C                   MOVEL     N3039         VVCZA
     C                   MOVEL     N3124A        VVCZB
     C                   MOVEL     N3124B        VVCZC
     C                   MOVEL     N3124C        VVCZD
     C                   MOVEL     N3124D        VVCZE
     C     N3035         WHENEQ    'CN'                                         LEV.ADR
     C                   MOVEL     N3039         VVCNA
     C                   MOVEL     N3124A        VVCNB
     C                   MOVEL     N3124B        VVCNC
     C                   MOVEL     N3124C        VVCND
     C                   MOVEL     N3124D        VVCNE
     C     N3035         WHENEQ    'DP'                                         LEV.ADR
     C                   MOVEL     N3039         VVCNA
     C                   MOVEL     N3124A        VVCNB
     C                   MOVEL     N3124B        VVCNC
     C                   MOVEL     N3124C        VVCND
     C                   MOVEL     N3124D        VVCNE
      *
     C                   ENDSL
      *
     C     KEYMS1        READE     ENAD93A                                52
     C  N52              ENDDO
     C                   ENDIF
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * PCI37                                               PCI37 /
      *////////////////////////////////////////////////////////////
     C     PCI37         BEGSR
     C                   MOVE      '0'           *IN53
     C                   Z-ADD     37            ZGN2
     C     KEYMS2        SETLL     EPCI93A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS2        READE     EPCI93A                                53
     C     *IN53         IFEQ      '0'
      *
     C                   MOVEL     P7102A        VVPACA
      *
     C                   ENDIF
     C                   ENDIF
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * RETREIVE INFO FROM EDIFACT DATABASE TO SYSPED400    RTVDB /
      *////////////////////////////////////////////////////////////
     C     RTVDB         BEGSR
      *__________________
      * INIT. DATAFELTER
      *""""""""""""""""""
     C                   CLEAR                   VAADVVR
     C                   Z-ADD     VHETA         VVETA
     C                   Z-ADD     VHMN          VVMN
     C                   MOVEA     C1490         A
     C                   EXSR      GETNUM
     C                   Z-ADD     ZNUM          VVVN
     C                   Z-ADD     ZNUM          VVSEQN
     C                   MOVEL     C1004         VVSREF
      *__________________
      * READ ALL SEGMENTS
      *""""""""""""""""""
     C                   Z-ADD     CREP0         ZREPB0
     C                   EXSR      MOA15
     C                   EXSR      FTX15
     C                   EXSR      NAD25
     C                   EXSR      GID31
      *________________
      * WRITE TO VARNER
      *""""""""""""""""
     C                   WRITE     VAADVVR
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * TDT05                                               TDT05 /
      *////////////////////////////////////////////////////////////
     C     TDT05         BEGSR
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     5             ZGN0
     C     KEYMS0        SETLL     ETDT93A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS0        READE     ETDT93A                                51
     C     *IN51         DOWEQ     '0'
      *
     C     T8051         IFEQ      '20 '
     C                   Z-ADD     TREP0         ZREPB0
     C                   EXSR      LOC05
     C                   EXSR      DTM05
     C                   EXSR      NAD09
     C                   ENDIF
      *
     C     KEYMS0        READE     ETDT93A                                51
     C                   ENDDO
     C                   ENDIF
      *
     C                   ENDSR
