      *********************************************************************
      * Manuell registrering av notisblokk
      * PROGRAM BEHANDLER INPUT FRA SYSO02
CRTPG+* CRTPGM SYSPED/SYSO05 MODULE(*LIBL/SYSO05
CRTPGM*        *LIBL/CHECKUSR *LIBL/INCGI) ACTGRP(CGI) AUT(*USE)
      *
      *********************************************************************
      * MAIN PROGRAM FLOW
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
     FBRIDF     UF   E           K DISK    USROPN
     FFREETXT2  UF A E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Prototype defintions and standard system API error structure
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      *--------------------------------------------------------------------
      * Variables common to CGI programs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
     D OddEven         s             10
      * Variables received from the input string
     D lng             s              2
     D UsrSpcName      s             10
     D WSUSER          S             10
     D WSAVD           S              4
     D BSAVD           S              4  0
     D WSOPD           S              7
     D BSOPD           S              7  0
     D WSIN            S              1
     D AVSLUTT         S              1
     D PGM             C                   CONST('*LIBL/CHECKUSR')
     D FMELDNR         S              2
     D FEILMELDING     S             80
      *
     D Lib             s             10
     D Fn              s             10
     D Mbr             s             10
      *
     D                 DS
     D WSTXT01                 1     70
     D WSTXT02                71    140
     D WSTXT03               141    210
     D WSTXT04               211    280
     D WSTXT05               281    350
     D WSTXT06               351    420
     D WSTXT07               421    490
     D WSTXT08               491    560
     D WSTXT09               561    630
     D WSTXT10               631    700
     D TXT                     1    700    DIM(10)
      *
      *--------------------------------------------------------------------
      * Prolog common to CGI programs
      * -Receive input from the remote browser
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prolog3
      *=====================================================================******
      * MAIN PROCESS LINE
      *=====================================================================******
      * Get program start time for calculating execution time
     C                   time                    timedata1
      * Parse input
     C                   exsr      Parse
      *
     C                   EXSR      INIT
      *
     C                   CALLB     PGM
     C                   PARM                    BIBRID
     C                   PARM                    UsrSpcName
     C                   PARM                    UINN              1
     C                   IF        UINN = 'N'
     C                   GOTO      SLUTT
     C                   END
     C     WSUSER        CHAIN     BRIDF                              50
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVE      WDT           BIDTF
     C                   MOVE      WTM           BITIDF
     C                   UPDATE    BRIDFR
      * Set output skeleton html member name
     C                   exsr      SetSkl
      * Read skeleton output html, etc.
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
      * Retrieve environment variables
     C                   exsr      RtvEnvVar
      *
     C                   IF        WSIN = 'J'
     C                   MOVE      *BLANKS       TXT
     C                   MOVE      *ZEROS        XN                3 0
     C     FREETXT2K     SETLL     FREETXT2
     C     FREETXT2K     READE(N)  FREETXT2                               33
     C                   DOW       *IN33 = *OFF
     C                   ADD       1             XN
     C                   MOVE      FRTTXT        TXT(XN)
     C     FREETXT2K     READE(N)  FREETXT2                               33
     C                   ENDDO
     C                   ELSE
     C                   exsr      TESTIN
     C                   END
      * 1-Section /$top
     C                   exsr      SetTop
     C                   callp     wrtsection('top')
      *
     C     SLUTT         TAG
     C                   EXSR      EXIT
     C                   return
      *
      *=====================================================================******
      * Parse input
      *=====================================================================******
     C     Parse         begsr
     C                   eval      lng     = zhbgetvar('lng')
     C                   eval      WSUSER  = zhbgetvar('USER')
     C                   EVAL      UsrSpcName  = zhbgetvar('UsrSpcName')
     C                   eval      WSAVD   = zhbgetvar('WSAVD')
     C                   EVAL      BSAVD   = c2n2(WSAVD)
     C                   eval      WSOPD   = zhbgetvar('WSOPD')
     C                   EVAL      BSOPD   = c2n2(WSOPD)
     C                   eval      WSTXT01 = zhbgetvar('WSTXT01')
     C                   eval      WSTXT02 = zhbgetvar('WSTXT02')
     C                   eval      WSTXT03 = zhbgetvar('WSTXT03')
     C                   eval      WSTXT04 = zhbgetvar('WSTXT04')
     C                   eval      WSTXT05 = zhbgetvar('WSTXT05')
     C                   eval      WSTXT06 = zhbgetvar('WSTXT06')
     C                   eval      WSTXT07 = zhbgetvar('WSTXT07')
     C                   eval      WSTXT08 = zhbgetvar('WSTXT08')
     C                   eval      WSTXT09 = zhbgetvar('WSTXT09')
     C                   eval      WSTXT10 = zhbgetvar('WSTXT10')
     C                   eval      WSIN    = zhbgetvar('WSIN')
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$top
      *  (search criteria)
      *=====================================================================******
     C     SetTop        begsr
      * Repeat national language for the next invocation
     C                   callp     updhtmlvar('LNG':lng:
     C                             InitHTMLVars)
     C                   callp     updHTMLvar('ROWHEAD':RowHead)
     C                   callp     updHTMLvar('LogoImg':LogoImg)
      * Color for text, background, links, visited links, active links
     C                   callp     updhtmlvar('TXTCOLOR':'black')
     C                   callp     updhtmlvar('BOLD':' ')
     C                   callp     updhtmlvar('BGCOLOR':BIFARG)
     C                   callp     updhtmlvar('LINK':'0000FF')
     C                   callp     updhtmlvar('VLINK':'FF0000')
     C                   callp     updhtmlvar('ALINK':'00FF00')
      * KUNDE
BODY C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updHtmlVar('USER':WSUSER)
     C                   callp     updHtmlVar('UsrSpcName':UsrSpcName)
     C                   callp     updHTMLvar('WSNA':BIBESK)
     C                   callp     updHtmlVar('KUNDE':
     C                             %trim(%editc(BIKUNR:'Z')))
     C                   callp     updHTMLvar('sdato':
     C                             %trim(%editw(BIDTV:'    .  .  ')))
     C                   callp     updHTMLvar('stid':
     C                             %trim(%editw(BITIDV:'  .  .  ')))
      *
     C                   callp     updHtmlVar('WSAVD':
     C                             %trim(%editc(BSAVD:'Z')))
     C                   callp     updHtmlVar('WSOPD':
     C                             %trim(%editc(BSOPD:'Z')))
     C                   callp     updHTMLvar('WSTXT01':WSTXT01)
     C                   callp     updHTMLvar('WSTXT02':WSTXT02)
     C                   callp     updHTMLvar('WSTXT03':WSTXT03)
     C                   callp     updHTMLvar('WSTXT04':WSTXT04)
     C                   callp     updHTMLvar('WSTXT05':WSTXT05)
     C                   callp     updHTMLvar('WSTXT06':WSTXT06)
     C                   callp     updHTMLvar('WSTXT07':WSTXT07)
     C                   callp     updHTMLvar('WSTXT08':WSTXT08)
     C                   callp     updHTMLvar('WSTXT09':WSTXT09)
     C                   callp     updHTMLvar('WSTXT10':WSTXT10)
      *
     C                   callp     updHTMLvar('WSIN':'N')
     C                   callp     updHTMLvar('AVSLUTT':AVSLUTT)
      *
     C                   endsr
      *=====================================================================******
      * Set html output skeleton member before its read into memory
      *=====================================================================******
     C     SetSkl        begsr
      *------------------
      * Set html output skeleton member
     C                   eval      lng = uppify(lng)
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC' + lng
     C                   eval      Mbr = 'SYSO05'
     C                   CAT       XSPRÅK:0      MBR
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$endhtml
      *=====================================================================******
     C     SetEnd        begsr
      * Server protocol
     C                   callp     updhtmlvar('PROTOCOL':S_Protocol)
      * Compute run time
     C                   time                    timedata2
     C     timedata2     subdur    timedata1     ms:*ms
     C                   eval      sec = ms / 1000000
     C                   callp     updhtmlvar('runtime':
     C                             %trim(%editw(sec:'     0 .   ')))
      *
     C                   endsr
      *=====================================================================******
      *  Retrieve environment variables
      *=====================================================================******
     C     RtvEnvVar     begsr
      * Use getenvp to get this server's protocol and name
     C                   eval      S_Protocol =getenv('SERVER_PROTOCOL':
     C                             qusec)
     C                   eval      S_Name     =getenv('SERVER_NAME':
     C                             qusec)
      *
     C                   endsr
      *
      *////////////////////////////////////////////////////////////
     C     TESTIN        BEGSR
      *////////////////////////////////////////////////////////////
      *
      * OPPDATER POD
     C                   MOVE      'Y'           AVSLUTT
     C     FREETXT2K     SETLL     FREETXT2
     C     FREETXT2K     READE     FREETXT2                               33
     C                   DOW       *IN33 = *OFF
     C                   DELETE    FREETXTR
     C     FREETXT2K     READE     FREETXT2                               33
     C                   ENDDO
      *
     C                   EVAL      FRTAVD = BSAVD
     C                   EVAL      FRTOPD = BSOPD
     C                   MOVE      WDT           FRTDT
     C                   EVAL      FRTKOD = '1'
     C                   EVAL      FRTFAK = 0
     C     1             DO        10            XN
     C                   IF        TXT(XN) <> *BLANKS
     C                   EVAL      FRTLI  = XN + 89
     C                   EVAL      FRTTXT = TXT(XN)
     C                   WRITE     FREETXTR
     C                   END
     C                   ENDDO
      *
     C     SLTBDT        ENDSR
      *
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
     C     FREETXT2K     KLIST
     C                   KFLD                    BSAVD
     C                   KFLD                    BSOPD
     C                   KFLD                    XKOD
      *
     C                   OPEN      BRIDF
     C     WSUSER        CHAIN(N)  BRIDF                              50
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
      * hent Parametre som går igjen i mange prog:
      *      Odd/Even/Rowhead-farger,Logobilde,Språk,Intern/Ekstern bruker,  mm
     c                   call      'ESGETPAR'
     c                   parm                    BIBRID
     c                   parm                    BIFIRM
     c                   parm                    BIKUNR
     c                   parm                    BIKNG
     c                   parm                    RowHead          10
     c                   parm                    Odd              10
     c                   parm                    Even             10
     c                   parm                    LogoImg          50
     c                   parm                    XSPRÅK            2
     c                   parm                    XINTERN           1
     c                   parm                    ParmDS1        1024
     c                   parm                    ParmDS2        1024
     c                   parm                    ParmDS3        1024
      *
     C                   OPEN      FREETXT2
      *
     C                   MOVE      '1'           XKOD              1
      *
     C                   endsr
      *
      *=====================================================================******
      *  AVSLUTT
      *=====================================================================******
     C     EXIT          begsr
     C                   callp     wrtsection('end')
     C                   callp     wrtsection('*fini')
     C                   close     BRIDF
     C                   close     FREETXT2
     C                   eval      *inlr = *on
      *
     C                   endsr
      *
