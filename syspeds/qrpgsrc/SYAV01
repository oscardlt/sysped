      *
      **************************************************************
      *** PROGRAM SKAL ARBEIDE MED TRANSPORTØRAVREGNING          ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER       ***
      *** LEDIGE INDIKATORER                                     ***
      **************************************************************
      *
     FTURER16   IF   E           K DISK
     FTURER14   IF   E           K DISK
     F                                     RENAME(TURERR:TUR14R)
     FTURER17   IF   E           K DISK
     F                                     RENAME(TURERR:TUR17R)
     FFIRM      IF   E           K DISK
     FTRAN      IF   E           K DISK
     FLEVEL01   IF   E           K DISK
     FWAVREGN   O    E             DISK
     FSYAV01FM  CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
      *___________________________
      * Finner username
      *"""""""""""""""""""""""""""
     D                SDS
     D  ZJOB                 244    253
     D  ZUSER                254    263
     D  ZJOBNR               264    269  0
     D                 DS
     D  PPARM                  1     20
     D  PXUNIK                 1      6  0
     D  PXKL                   7     12  0
     D  PXFAKT                13     19  0
     D  PXUTSK                20     20
     D                 DS
     D  TUDT                   1      8  0
     D  TUDTÅ                  1      4  0
     D  TUDTM                  5      6  0
     D  TUDTD                  7      8  0
     D                 DS
     D  FMDT                   1      6  0
     D  FMDTD                  1      2  0
     D  FMDTM                  3      4  0
     D  FMDTÅ                  5      6  0
     D                 DS
     D  FOMDT                  1      8  0
     D  FOMDTÅ                 1      4  0
     D  FOMDTM                 5      6  0
     D  FOMDTD                 7      8  0
     D                 DS
     D  TMDT                   1      6  0
     D  TMDTD                  1      2  0
     D  TMDTM                  3      4  0
     D  TMDTÅ                  5      6  0
     D                 DS
     D  TOMDT                  1      8  0
     D  TOMDTÅ                 1      4  0
     D  TOMDTM                 5      6  0
     D  TOMDTD                 7      8  0
     D                UDS
     D  UDAVD                 15     18
     D  UDSIGN                19     21
      *
     C     *ENTRY        PLIST
     C                   PARM                    PRINTE           10
     C                   PARM                    HOLD              4
     C                   PARM                    BEVAR             4
     C                   PARM                    ANT               1 0
     C                   PARM                    FORFAL            6
     C                   EXSR      INIT
      *
     C     CHOISE        TAG
     C                   MOVE      ' '           ZAVRE
     C                   EXFMT     BILDE1
      **  AVSLUTT
     C     *IN03         IFEQ      *ON
     C                   GOTO      SLUTT
     C                   ENDIF                                                  <*IN03
      **  SØK TRANSPORTØR
     C     *IN04         IFEQ      *ON
     C                   CALL      'SYTR04B'
     C                   PARM                    PKNT              8 0
     C                   PARM                    W1DATO
     C                   PARM                    W1AVD
     C                   PARM                    W1F4KD
     C                   Z-ADD     PKNT          W1KNT
     C                   Z-ADD     06            LINNBR
     C                   Z-ADD     29            POSNBR
     C                   GOTO      CHOISE
     C                   ENDIF                                                  <*IN04
     C                   MOVE      *OFF          *IN30
      * KUN ETT TURNR??
     C     W1TUNR        IFNE      *ZEROS
     C     W1TUNR        CHAIN     TURER14                            33
     C     *IN33         IFEQ      *ON
     C                   MOVE      'SPT0002'     MSGNR
     C                   MOVE      *ON           *IN30
     C                   GOTO      CHOISE
     C                   ELSE
     C     TUAVRE        IFEQ      'X'
     C                   MOVE      'J'           W1VISA
     C                   ELSE
     C                   MOVE      'N'           W1VISA
     C                   END
      * FIXER KEY SLIK AT SAMME FIL (TURER16) KAN BRUKES OGSå VED KUN 1
     C                   MOVE      TUKNT         W1KNT
     C                   MOVE      TUDTÅ         FMDTÅ
     C                   MOVE      TUDTM         FMDTM
     C                   MOVE      TUDTD         FMDTD
     C                   MOVE      FMDT          W1DATF
     C                   MOVE      W1DATF        W1DATO
     C                   MOVE      *ZEROS        W1AVD
     C                   MOVE      *ZEROS        W1AVNR
     C                   ENDIF
     C                   ENDIF
      * KUN ETT AVR.NR?
     C     W1AVNR        IFNE      *ZEROS
     C     W1AVNR        CHAIN     TURER17                            33
     C     *IN33         IFEQ      *ON
     C                   MOVE      'JOV0749'     MSGNR
     C                   MOVE      *ON           *IN30
     C                   GOTO      CHOISE
     C                   ELSE
     C                   MOVE      'J'           W1VISA
     C                   MOVE      TUKNT         W1KNT
     C                   MOVE      *ZEROS        W1DATF
     C                   MOVE      *ZEROS        W1AVD
     C                   MOVE      *ZEROS        W1TUNR
     C                   ENDIF
     C                   ENDIF
      * FRA DATO MÅ GIS(HVIS IKKE 1 AVR.)NÅR EN SKAL SE PÅ AVREGNEDE...
     C     W1VISA        IFEQ      'J'
     C     W1AVNR        IFEQ      *ZEROS
     C     W1DATF        ANDEQ     *ZEROS
     C                   MOVE      'JOV0750'     MSGNR
     C                   MOVE      *ON           *IN30
     C                   GOTO      CHOISE
     C                   ENDIF
      * VIS AVREGNEDE VALGT/OK - SETT KEY.
     C                   MOVE      'X'           ZAVRE
     C                   ENDIF
      * (IKKE VIS AVR.NR = DEFAULT, F7 - VIS ISTEDET FOR DATO)
     C                   MOVE      *OFF          *IN47                            VIS AVRNR?
      * TILDATO MÅ  GIS HVIS EN SKAL ARBEIDE MED IKKE AVREGNEDE...
     C     W1VISA        IFNE      'J'
     C     W1DATO        ANDEQ     *ZEROS
     C                   Z-ADD     501231        W1DATO
     C                   MOVE      'JOV0751'     MSGNR
     C                   MOVE      *ON           *IN30
     C                   GOTO      CHOISE
     C                   ENDIF
      * TEST OM TRANSPORTØR SKAL AVREGNES
     C     TRANK         CHAIN     TRAN                               33
     C     *IN33         IFEQ      *ON
     C                   MOVE      'TRO0489'     MSGNR
     C                   MOVE      *ON           *IN30
     C                   GOTO      CHOISE
     C                   ELSE
     C     VMINCR        IFNE      9
     C                   MOVE      'TRO0489'     MSGNR
     C                   MOVE      *ON           *IN30
     C                   GOTO      CHOISE
     C                   ENDIF                                                  <VMINCR
     C                   ENDIF                                                  <*IN33
      * FOM DATO
     C                   MOVE      W1DATF        FMDT
     C                   Z-ADD     FMDTD         FOMDTD
     C                   Z-ADD     FMDTM         FOMDTM
     C                   Z-ADD     FMDTÅ         FOMDTÅ
     C     FMDTÅ         IFGT      50
     C                   MOVEL     '19'          FOMDTÅ
     C                   ELSE
     C                   MOVEL     '20'          FOMDTÅ
     C                   END
      * TOM DATO
     C                   MOVE      W1DATO        TMDT
     C                   Z-ADD     TMDTD         TOMDTD
     C                   Z-ADD     TMDTM         TOMDTM
     C                   Z-ADD     TMDTÅ         TOMDTÅ
     C     TMDTÅ         IFGT      50
     C                   MOVEL     '19'          TOMDTÅ
     C                   ELSE
     C                   MOVEL     '20'          TOMDTÅ
     C                   END
      * HENT NAVN OG ADRESSE
     C                   Z-ADD     VMTRLE        LEVNR
     C     LEVKY         CHAIN     LEVEL01                            33
      *  SUBFILE CLEAR
     C     START         TAG
     C                   CLEAR                   SMBELN
     C                   MOVEA     '00'          *IN(91)
     C                   MOVE      *ON           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C                   CLEAR                   ZRECNO
     C                   CLEAR                   MRECNO
      * MODUS  N45=FERDIGE
      *         45/N46=FERDIGE(KONTROLLERTE) MEN IKKE AVREGNET
      *         45/ 46=AVREGNEDE
      *
     C                   SELECT
     C     ZAVRE         WHENEQ    ' '
     C                   MOVEA     '00'          *IN(45)
     C     ZAVRE         WHENEQ    'K'
     C                   MOVEA     '10'          *IN(45)
     C                   OTHER
     C                   MOVEA     '11'          *IN(45)
     C                   ENDSL
     C                   MOVE      *OFF          *IN44                            WHT ?
     C*
      * FINN ALLE TURER FOR TRANSPORTøREN
     C     W1AVNR        IFEQ      *ZEROS
     C     TURL16        SETLL     TURER16
     C                   ELSE
     C     W1AVNR        SETLL     TURER17
     C                   END
      *
     C     TURLES        TAG
     C     W1AVNR        IFEQ      *ZEROS
     C     TURK16        READE     TURER16                                34
     C                   ELSE
     C     W1AVNR        READE     TURER17                                34
     C                   END
     C     *IN34         IFEQ      *ON
     C                   GOTO      START2
     C                   ENDIF                                                  <*IN34
      *
     C     W1AVNR        IFEQ      *ZEROS
      * TURDATO
     C     TUDT          IFGT      TOMDT
     C                   GOTO      START2
     C                   ENDIF                                                  <TURDT
     C     W1AVD         IFNE      *ZEROS
     C     W1AVD         ANDNE     TUAVD
     C                   GOTO      TURLES
     C                   ENDIF                                                  <W1AVD
      * KUN ETT TURNR??
     C     W1TUNR        IFNE      *ZEROS
     C     W1TUNR        ANDNE     TUPRO
     C                   GOTO      TURLES
     C                   ENDIF
      *
     C                   END
      * LEST TUR SKAL MED....
     C     *IN47         IFEQ      '1'
     C                   Z-ADD     TUAVNR        SFDATO
     C                   ELSE
     C                   Z-ADD     TUDT          SFDATO
     C                   END
     C                   Z-ADD     TUPRO         SFPRO
     C                   MOVE      TUBILN        SFBILN
     C                   Z-ADD     TUAO          SFNTO
     C                   Z-ADD     TUTS          SFNT
     C                   Z-ADD     TUTVKT        SFVKT
     C                   Z-ADD     TUTM3         SFCBM
     C                   Z-ADD     TUTBEL        SFBELN
     C                   ADD       SFBELN        SMBELN
      *  SKRIV SUBFILE
     C                   ADD       1             ZRECNO
     C                   ADD       1             MRECNO
     C     TUAVRE        COMP      ' '                                4444
     C                   WRITE     SUBFIL
     C                   GOTO      TURLES
      *
     C     START2        TAG
     C                   Z-ADD     8             LINNBR
     C                   Z-ADD     2             POSNBR
     C                   Z-ADD     1             ZRECNO
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     MRECNO        IFEQ      0
     C                   Z-ADD     8             POSNBR
     C                   EXFMT     NYWRK
     C                   ELSE
     C                   MOVE      *ON           *IN(94)
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   EXFMT     BUNN
     C                   ENDIF
      *
     C                   SETOFF                                       9899
      * AVSLUTT
     C     *IN03         IFEQ      *ON
     C                   GOTO      SLUTT
     C                   ENDIF                                                  <*IN03
      * FOREGÅENDE BILDE / NY TRANSPORTøR / NYTT UTVALG
     C     *IN12         IFEQ      *ON
     C     ZAVRE         IFEQ      ' '
     C     ZAVRE         OREQ      'X'
     C                   Z-ADD     4             LINNBR
     C                   Z-ADD     29            POSNBR
     C                   GOTO      CHOISE
     C                   ENDIF                                                  <*ZAVRE
     C                   ENDIF                                                  <*IN12
      * FOREGÅENDE BILDE / HVIS FRA KONTROLLERT --> MUDUS UKONTROLLERTE
     C     *IN12         IFEQ      *ON
     C     ZAVRE         ANDEQ     'K'
     C                   MOVE      ' '           ZAVRE
     C                   GOTO      START
     C                   ENDIF
      * FORANDRE FRISK OPP
     C     *IN05         IFEQ      *ON
     C                   GOTO      START
     C                   ENDIF                                                  <*IN05
      * FORANDRE MODUS
     C     *IN07         IFEQ      *ON
      * HVIS EN   SER På AVREGNEDE    ER F7 På/AV  DATO/AVREGN-NR
     C     *IN46         IFEQ      '1'
     C     *IN47         IFEQ      '1'
     C                   MOVE      '0'           *IN47
     C                   ELSE
     C                   MOVE      '1'           *IN47
     C                   END
      * ELLERS,   VED ARBEIDE MED IKKE AVREGNEDE   KAN EN SWITCHE MODUS
     C                   ELSE
     C     ZAVRE         IFEQ      ' '
     C                   MOVE      'K'           ZAVRE
     C                   ELSE
     C                   MOVE      ' '           ZAVRE
     C                   END
     C                   END
     C                   GOTO      START
     C                   ENDIF                                                  <*IN07
      * PRØVEKJØRING / ENDELIG UTSKRIFT
     C     *IN08         IFEQ      *ON
     C     *IN16         OREQ      *ON
     C                   EXSR      UTSKR
     C                   Z-ADD     4             LINNBR
     C                   Z-ADD     29            POSNBR
     C                   GOTO      CHOISE
     C                   ENDIF                                                  <*IN08/*IN16
      *
     C                   SETOFF                                       94
      * ROLLUP
     C     *IN25         IFEQ      *ON
     C                   MOVE      *OFF          *IN25
     C                   MOVE      *ON           *IN94
     C                   GOTO      START3
     C                   ENDIF                                                  <*IN25
      * ROLLDOWN
     C     *IN26         IFEQ      *ON
     C                   MOVE      *OFF          *IN25
     C                   MOVE      *ON           *IN94
     C                   GOTO      START3
     C                   ENDIF                                                  <*IN26
      * ENTER ER TASTET
     C     ZRECNO        IFNE      0
     C                   EXSR      HENTSR
     C                   ENDIF
      *
     C                   GOTO      START3
      *
     C     SLUTT         TAG
     C                   MOVE      *ON           *INLR
     C*
     C***********************************************
     C     HENTSR        BEGSR
     C***********************************************
     C                   Z-ADD     0             LINNBR
     C                   SETOFF                                       919299
     C     *IN98         DOWEQ     *OFF
     C                   READC     SUBFIL                                 98
     C   98              DO
     C   99              SETOFF                                       98
     C                   Z-ADD     1             LINNBR
     C                   GOTO      SLHENT
     C                   ENDDO                                                  <98 DO
     C     ZRECNO        DIV       13            LINNBR
     C                   MVR                     XLIN              2 0
     C     XLIN          IFEQ      0
     C                   Z-ADD     13            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   ENDIF                                                  <XLIN
     C*    OPTION 2=ARBEIDE M/KOMPONENTER
     C     WSNRO         IFEQ      '2'                                          ENDRE
     C     *IN46         ANDNE     '1'                                          IKKE AVREGN.
     C                   MOVE      *ON           *IN99
     C*
     C                   SUB       SFBELN        SMBELN
     C                   CALL      'SYAV01A'
     C                   PARM                    SFPRO
     C                   PARM                    WSNRO
     C*
     C                   MOVE      ' '           WSNRO
     C     SFPRO         CHAIN     TURER14                            35
     C     *IN35         IFEQ      *OFF
     C                   Z-ADD     TUTBEL        SFBELN
     C                   END
     C                   ADD       SFBELN        SMBELN
     C     TUAVRE        COMP      ' '                                4444
     C                   UPDATE    SUBFIL
     C                   ENDIF                                                  <WSNRO
     C*
     C*    OPTION 5=VISE KOMPONENTER
     C     WSNRO         IFEQ      '5'                                          VISE
     C                   MOVE      *ON           *IN99
     C*
     C                   CALL      'SYAV01A'
     C                   PARM                    SFPRO
     C                   PARM                    WSNRO
     C*
     C                   MOVE      ' '           WSNRO
     C     SFPRO         CHAIN     TURER14                            35
     C     *IN35         IFEQ      *OFF
     C                   Z-ADD     TUTBEL        SFBELN
     C                   END
     C     TUAVRE        COMP      ' '                                4444
     C                   UPDATE    SUBFIL
     C                   ENDIF                                                  <WSNRO
     C*
     C                   ENDDO                                                  <*IN98
     C*
     C     SLHENT        TAG
     C                   ADD       7             LINNBR
     C                   Z-ADD     2             POSNBR
     C*
     C     UTPGM         ENDSR
     C*
     C***********************************************
     C     UTSKR         BEGSR
     C***********************************************
      *  Legg ut post i workfile evt. m.kundens spesielle sort.ønske
     C                   TIME                    WXKL
     C                   MOVEL     ZJOBNR        WXUNIK
     C                   Z-ADD     0             WXFAKT
      * LES SUBFIL
     C                   Z-ADD     0             ZRRN
     C     NYSUB         TAG
     C                   ADD       1             ZRRN
     C     ZRRN          CHAIN     SUBFIL                             77
     C     *IN77         IFEQ      *OFF
     C                   Z-ADD     SFPRO         WXPRO
     C                   MOVEL     SFBILN        WXBILN
     C                   WRITE     WAVREGNR
     C                   GOTO      NYSUB
     C                   ENDIF                                                  <*IN77
      *  Legg utskriftsprogram i jobbkø
     C     *IN08         IFEQ      *ON
     C                   MOVE      'P'           PXUTSK
     C                   ELSE
     C                   MOVE      'A'           PXUTSK
     C                   ENDIF                                                  <*IN08
     C                   Z-ADD     WXKL          PXKL
     C                   Z-ADD     WXUNIK        PXUNIK
     C                   Z-ADD     WXFAKT        PXFAKT
     C                   MOVEL     PPARM         APARM            20
     C                   MOVE      PRINTE        APRINT           10
     C                   MOVE      HOLD          AHOLD             4
     C                   MOVE      BEVAR         ABEVAR            4
     C                   MOVE      ANT           AANT              1
     C                   MOVE      FORFAL        FFDATO            6
      *
     C                   MOVE      FFDATO        UFFDT             6
     C                   CALL      'SYAV02CL'
     C                   PARM                    APARM
     C                   PARM                    APRINT
     C                   PARM                    AHOLD
     C                   PARM                    ABEVAR
     C                   PARM                    AANT
     C                   PARM                    UFFDT
     C                   ENDSR
     C*
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C                   MOVE      'N'           W1VISA
     C                   Z-ADD     0             W1DATO
     C                   Z-ADD     010100        W1DATF
     C                   TESTN                   UDAVD                32
     C   32              MOVE      UDAVD         W1AVD
     C     *LIKE         DEFINE    ZRECNO        ZRRN
     C*--------------------------------------------------------------
     C* DEFINERER NØKKEL TIL TURER16
     C*--------------------------------------------------------------
     C     TURL16        KLIST
     C                   KFLD                    ZAVRE             1
     C                   KFLD                    W1KNT
     C                   KFLD                    FOMDT
     C     TURK16        KLIST
     C                   KFLD                    ZAVRE             1
     C                   KFLD                    W1KNT
     C                   MOVE      ' '           ZAVRE
     C*--------------------------------------------------------------
     C     TRANK         KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    W1KNT
     C     LEVKY         KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    LEVNR
      *
     C                   MOVE      *ZEROS        TUDT
     C                   MOVE      *ZEROS        FMDT
     C                   MOVE      *ZEROS        FOMDT
     C                   MOVE      *ZEROS        TMDT
     C                   MOVE      *ZEROS        TOMDT
      *
     C                   READ      FIRM                                   32
     C                   ENDSR
     C*
