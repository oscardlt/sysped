     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPDOC('VEDLGRP1' 'HELPNU' 'HLP400')
     A                                      ALTHELP(CA01)
     A                                      HELP
      * "NOERASE" MÅ HA FOR Å IKKE TØMME BILDE SOM MAN KOMME FRA
     A          R NOERASE
     A                                      ASSUME
     A                                  1  2' '
      *
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  5  2VALUES(' ' '1' '5' '8')
     A            KUNDNR    R        O  5  5REFFLD(KUNDR/KUNDNR *LIBL/CUNDF)
     A  42                                  COLOR(WHT)
     A                                      EDTCDE(4)
     A            VADRNA    R        H      REFFLD(VADRR/VADRNA *LIBL/VADR)
     A            KNAVN     R        O  5 14REFFLD(KUNDR/KNAVN *LIBL/CUNDF)
     A  42                                  COLOR(WHT)
     A            ADR1      R        H      REFFLD(KUNDR/ADR1 *LIBL/CUNDF)
     A            VADRN1    R        H      REFFLD(VADRR/VADRN1 *LIBL/VADR)
     A            ADR2      R        H      REFFLD(KUNDR/ADR2 *LIBL/CUNDF)
     A            VADRN2    R        H      REFFLD(VADRR/VADRN2 *LIBL/VADR)
     A            POSTNR    R        H      REFFLD(KUNDR/POSTNR *LIBL/CUNDF)
     A            WSPNR     R        O  5 45REFFLD(KUNDR/POSTNR *LIBL/CUNDF)
     A  42                                  COLOR(WHT)
     A                                      EDTCDE(4)
     A            ADR3      R        H      REFFLD(KUNDR/ADR3 *LIBL/CUNDF)
     A            VADRN3    R        H      REFFLD(VADRR/VADRN3 *LIBL/VADR)
     A            WSADR3        23A  O  5 50DSPATR(UL)
     A  42                                  COLOR(WHT)
     A            AKTKOD         1A  H
     A            FIRMA          2A  H
     A            DKUND          1A  H
     A            KPERS         30A  H
     A            TLF           15A  H
     A            SONAVN        10A  H
     A            VALKOD         3A  H
     A            SPRAAK         1A  H
     A            BANKG         15A  H
     A            POSTG         20A  H
     A            FMOT           8S 0H
     A            BETBET         2A  H
     A            BETMAT         1A  H
     A            SFAKT          1A  H
     A            KGRENS         7Y 0H
     A            TFAXNR        15A  H
     A            SYREGN         8S 0H
     A            SYKONT         7Y 0H
     A            SYLIKV         1A  H
     A            SYOPDT         2A  H
     A            SYMINU         4S 2H
     A            SYUTLP         4S 2H
     A            SYRG          14A  H
     A            SYPOGE         9A  H
     A            SYSTAT         3A  H
     A            SYLAND         2A  H
     A            SYSELG         3A  H
     A            SYIAT1         7S 0H
     A            SYIAT2         4S 0H
     A            SYCOTY         1A  H
     A            SYFR01         1A  H
     A            SYFR02         1A  H
     A            SYFR03         2A  H
     A            SYFR04         2A  H
     A            SYFR05         3A  H
     A            SYFR06         3A  H
     A            SYSALU        13Y 0H
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A  91N93N94                            ROLLUP(25)
     A                                      CA03(03 'Forrige')
     A                                      CA05(05 'Frisk')
     A                                      CA06(06 'LAGE NY')
     A                                      CA09(09 'SYSPED VEDL.')
     A                                      CA12(12 'Forrige')
     A                                      CA10(10 'Frisøk')
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  *DS3                                SFLSIZ(0019)
     A  *DS3                                SFLPAG(0018)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'MAIL06R'
     A                                      COLOR(BLU)
     A                                  1 10MSGCON(050 DIV1694 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  2  2MSGCON(060 YBC0242 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A            ZRECNO         4S 0O  2 68SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  3  2MSGCON(070 SKU0060 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            W2NR           1A  I  4  2DSPATR(UL)
     A                                      VALUES(' ' '1' '5')
     A            W2KUNR         8D 0I  4  5DSPATR(UL)
     A            W2ALFA        10A  I  4 14DSPATR(UL)
     A                                  4 25MSGCON(012 DIV1698 *LIBL/SYMSGF)
     A                                  4 40MSGCON(010 SHU0117 *LIBL/SYMSGF)
     A            W2POST         4D  I  4 51DSPATR(UL)
      *
     A          R BUNN
     A                                      SLNO(23)
     A                                      CLRL(02)
     A                                      OVERLAY
     A                                  1 58MSGCON(012 SHU1027 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  1  2MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  1 21MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        70A  O  2  2MSGID(&MSGNR SYMSGF)
     A            MSGNR          7A  P
      *
     A          R NYWRK
     A                                      OVERLAY
     A                                      CA03(03 'Forrige')
     A                                      CA05(05 'Frisk')
     A                                      CA06(06 'LAGE NY')
     A                                      CA10(10 'Frisøk')
     A                                      CA09(09 'SYSPED VEDL.')
     A                                      CA12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'MAIL06R'
     A                                      COLOR(BLU)
     A                                  1 10MSGCON(050 DIV1694 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  2  2MSGCON(060 YBC0242 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3  2MSGCON(070 SKU0060 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            W2NR           1A  I  4  2VALUES(' ' '1')
     A                                      DSPATR(UL)
     A            W2KUNR         8D 0I  4  5DSPATR(UL)
     A            W2ALFA        10A  I  4 14DSPATR(UL)
     A                                  4 25MSGCON(012 DIV1698 *LIBL/SYMSGF)
     A                                  4 40MSGCON(010 SHU0117 *LIBL/SYMSGF)
     A            W2POST         4D  I  4 51DSPATR(UL)
     A N30                             15  2MSGCON(029 YBC0212 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        70A  O 15  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A                                 15 38MSGCON(018 JOV0998 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 15 57MSGCON(012 SHU1027 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
      *
     A          R BILDE01
     A                                  1  2'SYSPED/MAIL06R'
     A                                      COLOR(BLU)
     A                                  2 71DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  1 71TIME
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(040 DIV1590 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  3  3MSGCON(025 DIV1592 *LIBL/SYMSGF)
     A            KUNDNR         8Y 0O  3 32TEXT('Kundenummer')
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  5  3MSGCON(025 DIV1593 *LIBL/SYMSGF)
     A            KNAVN         30A  O  5 32TEXT('Kundenavn')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  7  3MSGCON(025 DIV1594 *LIBL/SYMSGF)
     A            KPERS         30A  O  7 32TEXT('Kontaktperson')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  8  3MSGCON(025 DIV1595 *LIBL/SYMSGF)
     A            ADR1          30A  O  8 32TEXT('Adresse 1')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  9  3MSGCON(025 DIV1596 *LIBL/SYMSGF)
     A            ADR2          30A  O  9 32TEXT('Adresse 2')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 10  3MSGCON(025 DIV1597 *LIBL/SYMSGF)
     A            POSTNR         4Y 0O 10 32TEXT('Postnummer')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 10 37'/'
     A            SYPOGE         9A  O 10 39DSPATR(UL)
     A                                      COLOR(WHT)
     A            ADR3          24A  O 10 51TEXT('Sted')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 11  3MSGCON(025 DIV2005 *LIBL/SYMSGF)
     A            SYLAND         2A  O 11 32
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 11 51MSGCON(016 SHU0116 *LIBL/SYMSGF)
     A                                 12  3MSGCON(025 DIV1599 *LIBL/SYMSGF)
     A            SONAVN        10A  O 12 32TEXT('Søkenavn')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            VADRNA    R        O 12 51REFFLD(VADRR/VADRNA *LIBL/VADR)
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 13  3MSGCON(025 DIV1600 *LIBL/SYMSGF)
     A            VALKOD         3A  O 13 32TEXT('Valutakode')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            VADRN1    R        O 13 51REFFLD(VADRR/VADRN1 *LIBL/VADR)
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 14  3MSGCON(025 DIV1601 *LIBL/SYMSGF)
     A            FMOT           8Y 0O 14 32TEXT('Fakturamottager v/kjede')
     A                                      EDTCDE(4)
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            VADRN2    R        O 14 51REFFLD(VADRR/VADRN2 *LIBL/VADR)
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 15  3MSGCON(025 DIV1598 *LIBL/SYMSGF)
     A            TLF           15A  O 15 32TEXT('Telefon')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            VADRN3    R        O 15 51REFFLD(VADRR/VADRN3 *LIBL/VADR)
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 16  3MSGCON(025 DIV3560 *LIBL/SYMSGF)
     A            TFAXNR        15A  O 16 32
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 17  3MSGCON(025 DIV1602 *LIBL/SYMSGF)
     A            SYMINU         4Y 2O 17 32TEXT('MIN.UTLPRO')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 18  3MSGCON(025 DIV1603 *LIBL/SYMSGF)
     A            SYUTLP         4Y 2O 18 32TEXT('UTL.PROV')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 18 42MSGCON(040 DIV1612 *LIBL/SYMSGF)
     A                                 19  3MSGCON(025 DIV1604 *LIBL/SYMSGF)
     A            SYREGN         8Y 0O 19 32TEXT('REGNR')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 19 42MSGCON(025 DIV1605 *LIBL/SYMSGF)
     A            SYFR02         1A  O 19 69DSPATR(UL)
     A                                      COLOR(WHT)
     A            SYKONT    R        O 19 71REFFLD(KUNDR/SYKONT *LIBL/CUNDF)
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTWRD('     -  ')
     A                                 20  3MSGCON(025 DIV1606 *LIBL/SYMSGF)
     A            SYOPDT         2A  O 20 32TEXT('OPD.TYPE')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 20 42MSGCON(025 DIV1607 *LIBL/SYMSGF)
     A            SYLIKV         1A  O 20 70TEXT('LIKV.KODE')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 21  3MSGCON(025 SHU0024 *LIBL/SYMSGF)
     A            SFAKT          1A  O 21 32DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 21 42MSGCON(025 SHU0172 *LIBL/SYMSGF)
     A            SYFORE         9A  O 21 70DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 22  3MSGCON(025 SHU0139 *LIBL/SYMSGF)
     A            DKUND          1A  O 22 32DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 22 42MSGCON(025 JOV0265 *LIBL/SYMSGF)
     A            SYFR03         2A  O 22 70
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
