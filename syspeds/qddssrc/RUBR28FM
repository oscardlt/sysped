     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HELP
     A                                      ALTHELP(CA01)
      * "NOERASE" MÅ HA FOR Å IKKE TØMME BILDE SOM MAN KOMME FRA
     A          R NOERASE
     A                                      ASSUME
     A                                  1  2' '
      * MAN KUN DEFINNERE VINDU PÅ REKORDNIVÅ
     A          R WINDOWSZ
     A                                      WINDOW(5 5 15 62 *NOMSGLIN)
     A                                      OVERLAY
      *
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  6  2VALUES(' ' '2' '4')
     A            SFTXT         17A  O  6  5
     A            SFDT           8Y 0H
     A            WSDT           6Y 0O  6 23EDTCDE(Y)
     A            SFBL28        12Y 2O  6 32EDTCDE(L)
     A            SFVK28         3A  O  6 47
     A            SFKR28         6Y 3O  6 51EDTCDE(4)
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A                                      SFLSIZ(0007)
     A                                      SFLPAG(0006)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  28                                  CA02(02 'Plukk fra EDI')
     A                                      CA05(05 'Frisk')
     A                                      CA06(06 'LAGE NY')
     A  27                                  CA07(07 'HENT PÅ NYTT')
     A                                      CA12(12 'Forrige')
     A            ZRECNO         4S 0O  2 56SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'RUBR28'
     A                                      COLOR(BLU)
     A                                  1 10MSGCON(40 DIV1831 SYMSGF)
     A                                      COLOR(WHT)
     A                                  3  2'Velg opsjon;   1=Lage ny  2=Endre -
     A                                       4=Slett'
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(56 DIV1830 SYMSGF)
     A                                      COLOR(WHT)
     A            W2NR           1A  I  5  2VALUES(' ' '1')
     A            W2TXT         17A  I  5  5
     A            W2DT           6D 0I  5 23DSPATR(UL)
     A          R BUNN
     A                                      WINDOW(WINDOWSZ)
     A                                 14  2'F5=Frisk F6=Lage F12=Forrige'
     A                                      COLOR(BLU)
     A  27                             14 31'F7=Ny henting'
     A                                      COLOR(BLU)
     A  27                             14 45'F2=Plukk fra EDI'
     A                                      COLOR(BLU)
     A  30        MSGTXT        58A  O 15  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R VARELI
     A                                      WINDOW(WINDOWSZ)
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF04(04 'SØK')
     A                                      CA12(12 'Forrige')
     A                                      SLNO(12)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  5MSGCON(53 DIV1832 SYMSGF)
     A            W28TXT        17A  B  2  5TEXT('TXT AV FINANS. OPPL.')
     A                                      DSPATR(UL)
     A  40
     AO 90                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            W28DT          6D 0B  2 23TEXT('DATO AV RUB. 28')
     A                                      DSPATR(UL)
     A  40
     AO 89                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSBL28        12Y 2B  2 32TEXT('BELØP FOR RUBR.28')
     A                                      DSPATR(UL)
     A  87                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A*                                     EDTCDE(L)
     A                                      EDTCDE(M)
     A            WSVK28         3A  B  2 47TEXT('VALUTAKODE RUBR.28')
     A                                      DSPATR(UL)
     A  88                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSKR28         6Y 3B  2 51TEXT('KURS PÅ RUBR.28')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  3  2MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 22MSGCON(18 DIV0032 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        58A  O  4  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R VAREL3
     A                                      WINDOW(WINDOWSZ)
     A                                      CA12(12 'Forrige')
     A                                      SLNO(12)
     A                                      CLRL(04)
     A                                      OVERLAY
     A                                  1  5MSGCON(53 DIV1832 SYMSGF)
     A            SFTXT         17A  O  2  5TEXT('TXT AV FINANS. OPPL.')
     A                                      DSPATR(UL)
     A                                      DSPATR(PC)
     A                                      COLOR(WHT)
     A            WSDT           6Y 0O  2 23TEXT('DATO AV RUB. 28')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(Y)
     A            SFBL28        12Y 2O  2 32TEXT('BELØP FOR RUBR.28')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            SFVK28         3A  O  2 47TEXT('VALUTAKODE RUBR.28')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            SFKR28         6Y 3   2 51TEXT('KURS PÅ RUBR.28')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  3  2MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
      *
     A          R NYWRK
     A                                      WINDOW(WINDOWSZ)
     A  28                                  CA02(02 'Plukk fra EDI')
     A                                      CA05(05 'Frisk')
     A                                      CA06(06 'LAGE NY')
     A  27                                  CA07(07 'HENT PÅ NYTT')
     A                                      CA12(12 'Forrige')
     A                                  1  2'RUBR28'
     A                                      COLOR(BLU)
     A                                  1 10MSGCON(40 DIV1831 SYMSGF)
     A                                      COLOR(WHT)
     A                                  3  2'Velg opsjon;   1=Lage ny  2=Endre -
     A                                       4=Slett'
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(56 DIV1830 SYMSGF)
     A                                      COLOR(WHT)
     A            W2NR           1A  I  5  2VALUES(' ' '1')
     A            W2TXT         17A  I  5  5
     A            W2DT           6D 0I  5 23DSPATR(UL)
     A                                 14  2'F5=Frisk F6=Lage F12=Forrige'
     A                                      COLOR(BLU)
     A  27                             14 31'F7=Ny henting'
     A                                      COLOR(BLU)
     A  28                             14 45'F2=Plukk fra EDI'
     A                                      COLOR(BLU)
     A  30        MSGTXT        58A  O 15  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R BEKREFT
     A                                      WINDOW(*DFT 6 40 *NOMSGLIN)
     A                                      CA12(12 'Avbryt')
     A                                      OVERLAY
     A                                  1  2'Hent varelinjer fra Z-filer på nyt-
     A                                      t'
     A                                      COLOR(WHT)
     A                                  3  2'ENTER = Slette alle eksistrerende '
     A                                      COLOR(BLU)
     A                                  4  2'        linjer og hente linjer fra'
     A                                      COLOR(BLU)
     A                                  5  2'        Z-filer'
     A                                      COLOR(BLU)
     A                                  6  2'F12   = Avbryt'
     A                                      COLOR(BLU)
