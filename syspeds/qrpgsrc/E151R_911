     H DATEDIT(*YMD)
      *****************************************************************
      *** ARBEIDE MED INNGÅENDE ORDRE
      ***--------------------------------------------------------------
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER
      *** LEDIGE INDIKATORER 01 02 04 06-11 13-24
      *** LEDIGE INDIKATORER 27-29 31 32 34-69 71-90 95-97
      ***--------------------------------------------------------------
     FEDIMD     IF   E           K DISK
     FEDIMG     IF   E           K DISK
     F                                     RENAME(EDIMR:EDIMRG)
     FEUNH      IF   E           K DISK
     FEDTM911   IF   E           K DISK
     FENAD911   IF   E           K DISK
     FERFF911   IF   E           K DISK
     FE151D     CF   E             WORKSTN
     F                                     SFILE(E151DB:ZRECNO)
     D                UDS
     D  USG                   19     21
     D  ULK                  301    302
     D  UPRT                 303    312
     D  ULN                  313    318
     D  UDT                  319    324
     D  UWS                  325    334
     D  UPGM                 335    344
     D  UMNPGM               345    353
     D  UDTA                 301    400
      */////////////
      * MAIN LOGIC /
      */////////////
      *___________
      * Initiering
      *"""""""""""
     C                   EXSR      INIT
      *______________
      * Hent landkode
      *""""""""""""""
     C     START0        TAG
     C                   EXFMT     E151DA
     C                   MOVEA     '00'          *IN(98)
     C   03              GOTO      ENDPGM
      *
     C     WSLK          COMP      'SE'                               9999
     C   99WSLK          COMP      'DK'                               9999
     C   99WSLK          COMP      'FI'                               9999
     C     WSPRT         COMP      *BLANKS                                98
     C   99
     COR 98              GOTO      START0
      *
     C                   MOVE      WSPRT         UPRT
     C                   MOVE      WSLK          ULK
     C     WSLN          IFNE      0
     C                   EXSR      MKCOPY
     C                   GOTO      ENDPGM
     C                   END
      *______________
      * Clear Subfil
      *""""""""""""""
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     E151DC
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     0             ZRECNO
      *_____________
      * Fyll Subfil
      *"""""""""""""
     C     KEYTVD        SETLL     EDIMD
     C     START1        TAG
     C     KEYTVD        READE     EDIMD                                  94
      *
     C     *IN94         DOWEQ     '0'
     C                   EXSR      GETINF
     C     KEYTVD        READE     EDIMD                                  94
     C  N94              ENDDO
      *
     C                   Z-ADD     ZRECNO        YRECNO            5 0
      *________________
      * Display Subfil
      *""""""""""""""""
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C     VISDE         TAG
     C                   EXFMT     E151DG
     C     *IN03         IFNE      '1'
     C     *IN05         ANDNE     '1'
     C                   GOTO      VISDE
     C                   END                                                    <*IN03
      *
     C                   ELSE                                                   <ZRECNO
     C                   WRITE     E151DD
     C                   EXFMT     E151DC
     C                   END                                                    <ZRECNO
      *
     C                   MOVE      '0'           *IN(30)
      *
     C                   SETOFF                                       9998
      *** F3=Gå ut
     C     *IN03         IFEQ      '1'
     C                   GOTO      ENDPGM
     C                   END                                                    <*IN03
      *** F5=Frisk opp
     C     *IN05         IFEQ      '1'
     C                   GOTO      START
     C                   END                                                    <*IN03
      *** Finn valg
     C     ZRECNO        IFNE      0
     C                   EXSR      GETALT
      *** F3=Gå ut
     C     *IN03         IFEQ      '1'
     C                   GOTO      ENDPGM
     C                   ENDIF                                                  <*IN03
     C                   ENDIF                                                  <ZRECNO
      *
     C                   GOTO      START3
      *
     C     ENDPGM        TAG
     C                   MOVE      '1'           *INLR
     C                   RETURN
      *////////////////////////////////////////////////////////////
      *                                                    GETINF /
      *////////////////////////////////////////////////////////////
     C     GETINF        BEGSR
     C     MMN           CHAIN     EUNH                               51
     C                   MOVEL     U1004         WSDNO
     C                   MOVE      *BLANKS       WSONO
     C                   MOVE      *BLANKS       WSINO
     C                   MOVE      *BLANKS       WSAVS
     C                   MOVE      *BLANKS       WSMOT
      *
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     MMN           ZMN
     C                   Z-ADD     2             ZGN
     C     KEYMSG        SETLL     ENAD911                                33
     C     *IN33         IFEQ      '1'                                          1<-B
     C     KEYMSG        READE     ENAD911                                51
     C     *IN51         DOWEQ     '0'                                           2<-B
     C     N3035         IFEQ      'DP '                                          3<-B
     C     N3035         OREQ      'ZZ '
     C     N3207         IFNE      WSLK                                            4<-B
     C                   GOTO      SLGINF
     C                   END                                                       4<-E
     C     N3035         IFEQ      'DP '                                           4<-B
     C                   MOVEL     N3036A        WSAVS
     C                   ELSE                                                      4
     C     WSAVS         IFEQ      *BLANKS                                          5<-B
     C                   MOVEL     N3036A        WSAVS
     C                   END                                                        5<-E
     C                   END                                                       4<-E
     C                   ELSE                                                     3
     C     N3035         IFEQ      'CN '                                           4<-B
     C                   MOVEL     N3036A        WSMOT
     C                   ENDIF                                                     4<-E
     C                   ENDIF                                                    3<-E
     C     KEYMSG        READE     ENAD911                                51
     C  N51              ENDDO                                                   2<-E
     C                   ENDIF                                                  1<-E
      *
     C                   Z-ADD     0             ZGN
     C     KEYMSG        CHAIN     EDTM911                            51
     C     *IN51         IFEQ      '0'                                          1<-B
     C                   MOVEL     D2005         WSDDT
     C                   ENDIF                                                  1<-E
      *
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     1             ZGN
     C     KEYMSG        SETLL     ERFF911                                33
     C     *IN33         IFEQ      '1'                                          1<-B
     C     KEYMSG        READE     ERFF911                                51
     C     *IN51         DOWEQ     '0'                                           2<-B
     C     R1153         IFEQ      'Z22'                                          3<-B
     C                   MOVEL     R1154         WSONO
     C                   ELSE                                                     3
     C     R1153         IFEQ      'IC '                                           4<-B
     C                   MOVEL     R1154         WSINO
     C                   ENDIF                                                     4<-E
     C                   ENDIF                                                    3<-E
     C     KEYMSG        READE     ERFF911                                51
     C  N51              ENDDO                                                   2<-E
     C                   ENDIF                                                  1<-E
      *
     C                   MOVE      '0'           *IN70
     C                   ADD       1             ZRECNO
     C                   WRITE     E151DB
      *
     C     SLGINF        ENDSR
      *////////////////////////////////////////////////////////////
      *                                                    GETALT /
      *////////////////////////////////////////////////////////////
     C     GETALT        BEGSR
      *
     C                   MOVE      *ZEROS        OPTNBR            2 0
     C                   MOVEA     '00'          *IN(91)
     C                   MOVEA     '000'         *IN(97)
     C                   MOVE      *ZEROS        ZTOTWT           11 3
     C     *IN98         DOWEQ     '0'
     C                   READC     E151DB                                 98
     C     *IN98         IFEQ      '1'
     C     *IN99         IFEQ      '1'
     C                   MOVE      '0'           *IN98
     C                   ENDIF                                                  <*IN99
     C                   GOTO      ENDGET
     C                   ENDIF                                                  <*IN98
      *** 5=Vis
     C     WSOPT         IFEQ      '5'
     C     WSDNO         ANDNE     '*PRINT'
     C                   MOVE      *BLANK        WSOPT
     C                   UPDATE    E151DB
     C                   MOVEL     'E152R_'      ZPGM             10
     C                   MOVE      '911 '        ZPGM
     C                   Z-ADD     MMN           ZMN
     C                   CALL      ZPGM
     C                   PARM                    ZMN
     C                   PARM                    ZIND              2
     C     ZIND          COMP      '03'                                   03
     C     ZIND          COMP      '12'                                   12
     C                   ENDIF
      *** 6=PRINT
     C     WSOPT         IFEQ      '6'
     C     WSDNO         ANDNE     '*PRINT'
     C                   MOVE      *BLANK        WSOPT
     C                   MOVE      '1'           *IN70
     C                   MOVE      '1'           *IN97
     C                   MOVE      '*PRINT'      WSDNO
     C                   MOVE      *BLANKS       WSDDT
     C                   MOVE      *BLANKS       WSONO
     C                   MOVE      *BLANKS       WSINO
     C                   MOVE      *BLANKS       WSAVS
     C                   MOVE      *BLANKS       WSMOT
     C                   UPDATE    E151DB
     C                   MOVEL     'E153R_'      UPGM
     C                   MOVE      '911 '        UPGM
     C                   MOVE      MMN           UMNPGM
     C                   CALL      'E153C'
     C                   ENDIF
      *
     C     *IN97         IFEQ      '1'
     C                   MOVEL     'E154R_'      UPGM
     C                   MOVE      '911 '        UPGM
     C                   CALL      'E154C'
     C                   MOVE      '0'           *IN97
     C                   ENDIF
      *
     C     *IN03         IFEQ      '1'
     C     *IN12         OREQ      '1'
     C                   GOTO      ENDGET
     C                   ENDIF
      *
     C                   ENDDO
      *
     C     ENDGET        TAG
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *                                                      INIT /
      *////////////////////////////////////////////////////////////
     C     INIT          BEGSR
      *
     C     KEYTVD        KLIST
     C                   KFLD                    ZST
     C                   KFLD                    ZSR
     C                   KFLD                    Z0065
     C     KEYTVG        KLIST
     C                   KFLD                    ZST
     C                   KFLD                    ZSR
     C                   KFLD                    Z1004
     C     KEYMSG        KLIST
     C                   KFLD                    ZMN
     C                   KFLD                    ZGN
      *
     C     *ENTRY        PLIST
     C                   PARM                    ZWS              10
     C                   PARM                    WSPRT
      *
     C     *LIKE         DEFINE    MST           ZST
     C     *LIKE         DEFINE    MSR           ZSR
     C     *LIKE         DEFINE    M0065         Z0065
     C     *LIKE         DEFINE    M1004         Z1004
     C     *LIKE         DEFINE    NMN           ZMN
     C     *LIKE         DEFINE    NGN           ZGN
     C                   MOVE      ' '           ZST
     C                   MOVE      'R'           ZSR
     C                   MOVE      'ORDERS'      Z0065
      *
     C                   MOVE      *BLANKS       UDTA
     C                   MOVE      ZWS           UWS
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      *                                                    MKCOPY /
      *////////////////////////////////////////////////////////////
     C     MKCOPY        BEGSR
     C                   MOVE      WSLN          ULN
      *
     C                   SELECT
     C     WSLK          WHENEQ    'SE'
     C                   MOVEL     'RXS'         XSTDN             9
     C     WSLK          WHENEQ    'DK'
     C                   MOVEL     'RXD'         XSTDN
     C     WSLK          WHENEQ    'FI'
     C                   MOVEL     'RXF'         XSTDN
     C                   ENDSL
     C                   MOVE      WSLN          XSTDN
     C                   MOVEL     XSTDN         Z1004
      *
     C     KEYTVG        SETLL     EDIMG
     C     STMKC         TAG
     C     KEYTVG        READE     EDIMG                                  94
     C  N94              DO
     C                   MOVEL     'E153R_'      UPGM
     C                   MOVE      '911 '        UPGM
     C                   MOVE      MMN           UMNPGM
     C                   CALL      'E153C'
     C  N97              MOVE      MDT           UDT
     C  N97              MOVE      '1'           *IN97
     C                   GOTO      STMKC
     C                   END
      *
     C     *IN97         IFEQ      '1'
     C                   MOVEL     'E154R_'      UPGM
     C                   MOVE      '911 '        UPGM
     C                   CALL      'E154C'
     C                   ENDIF
      *
     C                   ENDSR
      *
