     FSVIH      UF   E           K DISK
     FSVIHE     UF   E           K DISK
     FSVTHA     IF   E           K DISK
     FEDIC      UF   E             DISK
     FEDIM      O  A E             DISK
     FSVTFI     IF   E             DISK
      *//////////////
      *  Hodelogikk /
      *//////////////
     C                   EXSR      SRA
     C                   EXSR      SRB
     C                   MOVE      '1'           *INLR
      *////////////////////////////////////////////////////////////
      *  Initiering                                           SRA /
      *////////////////////////////////////////////////////////////
     C     SRA           BEGSR
      *____________________
      * Definiere variabler
      *""""""""""""""""""""
     C     *LIKE         DEFINE    SVIH_SYAV     ZVIH_SYAV
     C     *LIKE         DEFINE    SVIH_SYOP     ZVIH_SYOP
     C     *LIKE         DEFINE    SVIH_SGDK     ZVIH_SGDK
     C     *LIKE         DEFINE    SVIH_SGUT     ZVIH_SGUT
      *_________________
      * Input parametere
      *"""""""""""""""""
     C     *ENTRY        PLIST
     C                   PARM                    ZVIH_SYAV
     C                   PARM                    ZVIH_SYOP
      *__________________
      * Definiere Nøkkler
      *""""""""""""""""""
     C     SVIH_KEY      KLIST
     C                   KFLD                    ZVIH_SYAV
     C                   KFLD                    ZVIH_SYOP
      *_________________________________
      * Hæmta dagens datum och klockslag
      *"""""""""""""""""""""""""""""""""
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
      *________________________
      * Hæmta firmaupplysningar
      *""""""""""""""""""""""""
     C     1             CHAIN     SVTFI                              51
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Bearbeide Formater                                   SRB /
      *////////////////////////////////////////////////////////////
     C     SRB           BEGSR
     C     SVIH_KEY      CHAIN(N)  SVIH                               90
     C                   EVAL      ZVIH_SGDK=SVIH_SGDK
     C                   EVAL      ZVIH_SGUT=SVIH_SGUT
      * Sigiller via keypad eller automatsik ?
     C     SVIH_SYSG     CHAIN     SVTHA
     C*    SVTH_DKEY     IFNE      *BLANKS
     C*                  EXSR      SRCC
     C*                  ENDIF
     C     SVIH_KEY      CHAIN     SVIH                               90
     C                   EVAL      SVIH_SGDK=ZVIH_SGDK
     C                   EVAL      SVIH_SGUT=ZVIH_SGUT
     C                   UPDATE    SVIHR
      * Slett edifact
     C     SVIH_KEY      SETLL     SVIHE
     C     SVIH_KEY      READE     SVIHE                                  51
     C     *IN51         DOWEQ     *OFF
     C                   DELETE    SVIHER
     C     SVIH_KEY      READE     SVIHE                                  51
     C                   ENDDO
      * Lag edifact pånytt med riktig sigill
     C     SVIH_SGDK     IFNE      *BLANKS
     C     SVIH_SGDK     ANDNE     '000000'
     C                   MOVEL     SVIH_MTYP     WVIH_MTYP1        1
     C                   SELECT
     C     SVIH_MTYP     WHENEQ    'HBK'
     C                   CALL      'SVI022RK'
     C                   PARM                    ZVIH_SYAV
     C                   PARM                    ZVIH_SYOP
     C                   PARM                    ZVIH_SGDK
     C                   PARM                    ZVIH_SGUT
     C     SVIH_MTYP     WHENEQ    'DBK'
     C                   CALL      'SVI022RK'
     C                   PARM                    ZVIH_SYAV
     C                   PARM                    ZVIH_SYOP
     C                   PARM                    ZVIH_SGDK
     C                   PARM                    ZVIH_SGUT
     C     WVIH_MTYP1    WHENEQ    'H'
     C                   CALL      'SVI022RH'
     C                   PARM                    ZVIH_SYAV
     C                   PARM                    ZVIH_SYOP
     C                   PARM                    ZVIH_SGDK
     C                   PARM                    ZVIH_SGUT
     C                   OTHER
     C                   CALL      'SVI022RN'
     C                   PARM                    ZVIH_SYAV
     C                   PARM                    ZVIH_SYOP
     C                   PARM                    ZVIH_SGDK
     C                   PARM                    ZVIH_SGUT
     C                   ENDSL

     C                   ENDIF
      *_____________
      * Ændra status
      *"""""""""""""
     C     SVIH_KEY      CHAIN     SVIH                               52
     C                   EVAL      SVIH_SYST='A'
     C                   UPDATE    SVIHR
      *_____________________________
      * Hæmta næsta meddelandenummer
      *"""""""""""""""""""""""""""""
     C     1             CHAIN     EDIC                               52
     C                   ADD       1             CMN
     C                   UPDATE    EDICR
      *_______________
      * Skriv til EDIM
      *"""""""""""""""
     C                   CLEAR                   EDIMR
     C                   EVAL      M0004=SVTF_0004
     C                   EVAL      M0010=SVTF_0010
     C                   EVAL      M0035=SVTF_0035
     C                   MOVEL     SVIH_SYAV     W0062            11
     C                   MOVE      SVIH_SYOP     W0062
     C                   EVAL      M0062=W0062
     C                   EVAL      M0065='CUSDEC'
     C                   EVAL      M1001=SVIH_MTYP
     C                   EVAL      M1004=SVIH_TUID
     C                   EVAL      M1225=SVIH_MTYP
     C                   EVAL      M1N07='SVI'
     C                   EVAL      MMN=CMN
     C                   EVAL      MSR='S'
     C                   MOVEL     WDT           MDT
     C                   MOVEL     WTM           MTM
     C                   EVAL      MAVD=SVIH_SYAV
     C                   EVAL      MTDN=SVIH_SYOP

     C                   WRITE     EDIMR

     C     SRB9          TAG
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Berækna utstællarsigill                             SRCC /
      *////////////////////////////////////////////////////////////
     C     SRCC          BEGSR
      * Send oppdrag til sigillering
     C                   MOVEL     ZVIH_SYAV     QTFIEL
     C                   MOVE      ZVIH_SYOP     QTFIEL
     C                   CALL      'QSNDDTAQ'
     C                   PARM      'SVGSRQ'      QTNAME           10
     C                   PARM      '*LIBL'       QTLIB            10
     C                   PARM      11            QTLEN             5 0
     C                   PARM                    QTFIEL           11
      * Sigillering
     C                   CALL      'SEALCTL'
     C                   PARM      'SVG003R   '  QPARM            10
      * Retur fra sigillering
     C                   MOVEL     ZVIH_SYAV     QKEY
     C                   MOVE      ZVIH_SYOP     QKEY
     C                   CALL      'QRCVDTAQ'
     C                   PARM      'SVGSSQ'      QRNAME           10
     C                   PARM      '*LIBL'       QRLIB            10
     C                   PARM      6             QRLEN             5 0
     C                   PARM                    ZVIH_SGUT
     C                   PARM      99            QWAIT             5 0
     C                   PARM      'EQ'          QORDER            2
     C                   PARM      11            QKILEN            3 0
     C                   PARM                    QKEY             11
     C                   PARM      10            QSNDRL            3 0
     C                   PARM                    QSNDR            10
     C                   ENDSR
