     FSVIHF     IF   E           K DISK
     FSVIHG     O  A E             DISK
      *____________________
      * DS for input string
      *""""""""""""""""""""
     D                 DS
     D  A                      1     80
     D                                     DIM(80)                              Input string
     D  ADS                    1     80
      *_____________________
      * DS for output string
      *"""""""""""""""""""""
     D                 DS
     D  B                      1    256
     D                                     DIM(256)                             Output string
     D  SVIHG_DTA              1    256
      *//////////////
      *  Hodelogikk /
      *//////////////
     C                   EXSR      SRA
     C                   EXSR      SRB
     C                   MOVE      '1'           *INLR
      *////////////////////////////////////////////////////////////
      *  Initiering                                           SRA /
      *////////////////////////////////////////////////////////////
     C     SRA           BEGSR
      *___________________
      * Definiere Workfelt
      *"""""""""""""""""""
     C     *LIKE         DEFINE    SVIHF_SYA     UVIHF_SYA
     C     *LIKE         DEFINE    SVIHF_SYO     UVIHF_SYO
      *__________________
      * Definiere Nøkkler
      *""""""""""""""""""
     C     KEYA          KLIST
     C                   KFLD                    UVIHF_SYA
     C                   KFLD                    UVIHF_SYO
      *_____________
      * Init av felt
      *"""""""""""""
      * Workfields for Array's
     C                   Z-ADD     0             AX                2 0          Index for A
     C                   Z-ADD     1             BX                4 0          Index for B
     C                   Z-ADD     0             AXV               4 0          Leftindx A
     C                   Z-ADD     0             AXH               4 0          Rightindx A
     C                   MOVE      *BLANKS       WCHAR             1            Test character
     C                   EVAL      SVIHG_SYA=UVIHF_SYA
     C                   EVAL      SVIHG_SYO=UVIHF_SYO
     C                   Z-ADD     256           LEN               5 0
     C                   MOVEL     'SVGSENO'     TABLE            10
     C                   MOVEL     '*LIBL'       LIB              10
      *_________________
      * Input parametrer
      *"""""""""""""""""
     C     *ENTRY        PLIST
     C                   PARM                    UVIHF_SYA
     C                   PARM                    UVIHF_SYO
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Start av file                                        SRB /
      *////////////////////////////////////////////////////////////
     C     SRB           BEGSR
     C     KEYA          SETLL     SVIHF
     C     KEYA          READE     SVIHF                                  51
     C     *IN51         DOWEQ     *OFF
     C                   MOVEL     SVIHF_DTA     ADS
     C                   EXSR      SRÅ
     C     KEYA          READE     SVIHF                                  51
     C                   ENDDO
     C     BX            IFGT      1
     C                   WRITE     SVIHGR
     C                   ENDIF
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR for output string, read input from left              SRÅ
      *////////////////////////////////////////////////////////////
     C     SRÅ           BEGSR
      *_________________
      * Trim left blanks
      *"""""""""""""""""
     C                   MOVEL(P)  ADS           ADS2             70
     C                   EVAL      ADS=*BLANKS
     C                   EVAL      ADS=%TRIML(ADS2)
      *_______________
      * Find positions
      *""""""""""""""""
     C                   EVAL      AXV=1
     C     ' '           CHECKR    ADS           AXH
      *________________________________________________
      * Read input string and place it in output string
      *""""""""""""""""""""""""""""""""""""""""""""""""
     C     AXV           DO        AXH           AXV
     C                   MOVE      A(AXV)        WCHAR
      * Move to output string                                            I
     C                   MOVE      WCHAR         B(BX)
     C                   ADD       1             BX
      * If arrayindex for output array is over 256 Then add a record
     C     BX            IFGT      256
     C                   WRITE     SVIHGR
     C                   Z-ADD     1             BX
     C                   MOVE      *BLANKS       SVIHG_DTA
     C                   ENDIF

     C                   ENDDO

     C                   EVAL      ADS=*BLANKS

     C                   ENDSR
