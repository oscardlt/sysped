      *
      ***********************************************************
      *** PROGRAM SKAL SPØRRE PÅ FLYFRAKT RATER
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER
      *** LEDIGE INDIKATORER 02 04 07-11 13-24
      *** LEDIGE INDIKATORER 31-90 95-97 99
      ***********************************************************
      *
     FAIRNRDL1  IF   E           K DISK
     FSYEF63D   CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
      *
     C                   EXSR      INIT
      *
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
     C                   MOVE      WSSDF         XSDF              3
     C                   MOVE      WSSDT         XSDT              3
     C                   MOVE      WSLIN         XLIN              3 0
     C                   MOVE      W2COD         XCOD              1
     C                   MOVE      W2KN          XKN               8 0
     C                   MOVE      W2COM         XCOM              4
     C                   MOVE      W2VKT         XVKT              5 0
     C                   MOVE      *ZEROS        XIRLIN            3 0
     C                   MOVE      *ZEROS        XIRKN             8 0
     C                   MOVE      *BLANKS       XIRCOD            1
     C                   MOVE      *BLANKS       XIRCOM            4
     C                   MOVE      *BLANKS       XIRCP             1
     C                   MOVE      *BLANKS       XIRTRC            1
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     AIRRDX        SETLL     AIRNRDL1
     C     START1        TAG
     C     AIRRDK        READE     AIRNRDL1                               94
     C  N94              EXSR      TSTRCD
     C  N94
     CAN 51              GOTO      START1
      *
     C     START2        TAG
      *
     C  N94              DO
     C                   DO        5
     C                   ADD       1             ZRECNO
     C                   MOVE      AIRLIN        XIRLIN
     C                   MOVE      AIRKN         XIRKN
     C                   MOVE      AIRCOD        XIRCOD
     C                   MOVE      AIRCOM        XIRCOM
     C                   MOVE      AIRCP         XIRCP
     C                   MOVE      AIRTRC        XIRTRC
     C                   WRITE     SUBFIL
     C     ST21          TAG
     C     AIRRDK        READE     AIRNRDL1                               94
     C  N94              EXSR      TSTRCD
     C  N94
     CAN 51              GOTO      ST21
     C  N94              END
     C                   END
      *
     C                   Z-ADD     ZRECNO        YRECNO            5 0
     C     ZRECNO        IFEQ      0
     C     *IN27         OREQ      '1'
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     5             POSNBR
     C                   ELSE
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     2             POSNBR
     C                   END
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYWRK
     C                   ELSE
     C                   WRITE     BUNN
     C                   EXFMT     SUBCTL
     C                   END
      *
     C                   SETOFF                                       9830
     C   03
     COR 12              GOTO      SLUTT
      *** FRISK OPP
     C   05              GOTO      START
      *** LAGE NY
     C   06              DO
     C                   CALL      'SYEF61C'
     C                   GOTO      START
     C                   END
      *
      *** BLA VIDERE
     C   25              DO
     C                   Z-ADD     YRECNO        ZRECNO
     C                   SETOFF                                       25
     C                   GOTO      START2
     C                   END
      *
     C     WSSDF         IFNE      XSDF
     C     WSSDT         ORNE      XSDT
     C     WSLIN         ORNE      XLIN
     C     W2KN          ORNE      XKN
     C     W2COD         ORNE      XCOD
     C     W2COM         ORNE      XCOM
     C     W2VKT         ORNE      XVKT
     C                   GOTO      START
     C                   END
      *
     C     ZRECNO        IFEQ      0
     C                   GOTO      START3
     C                   ELSE
     C                   EXSR      HENT
     C   98              GOTO      START3
     C                   GOTO      SLUTT
     C                   END
      *
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
      *
     C***********************************************
     C     TSTRCD        BEGSR
     C***********************************************
     C     WSLIN         IFNE      0
     C     WSLIN         ANDNE     AIRLIN
     C                   MOVE      '1'           *IN94
     C                   GOTO      SLTSTR
     C                   END
      *
     C     W2COD         IFNE      *BLANKS
     C     W2COD         ANDNE     AIRCOD
     C                   MOVE      '1'           *IN51
     C                   GOTO      SLTSTR
     C                   END
      *
     C     W2KN          IFNE      0
     C     W2KN          ANDNE     AIRKN
     C                   MOVE      '1'           *IN51
     C                   GOTO      SLTSTR
     C                   END
      *
     C     W2COM         IFNE      *BLANKS
     C     W2COM         ANDNE     AIRCOM
     C                   MOVE      '1'           *IN51
     C                   GOTO      SLTSTR
     C                   END
      *
     C     W2VKT         COMP      AIRWEI                             51
     C  N51XIRLIN        IFEQ      AIRLIN
     C     XIRKN         ANDEQ     AIRKN
     C     XIRCOD        ANDEQ     AIRCOD
     C     XIRCOM        ANDEQ     AIRCOM
     C     XIRCP         ANDEQ     AIRCP
     C     XIRTRC        ANDEQ     AIRTRC
     C                   MOVE      '1'           *IN51
     C                   END
      *
     C     SLTSTR        ENDSR
      *
     C***********************************************
     C     HENT          BEGSR
     C***********************************************
     C                   MOVEA     '00'          *IN(91)
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C  N98WSNR          IFEQ      '1'
     C                   MOVE      AIRCOD        URK
     C                   MOVE      AIRCOM        UCOM
     C                   MOVE      AIRGCD        URPR
     C                   MOVE      AIRAWB        URATE
     C                   GOTO      SLHENT
     C                   END
     C                   END
      *
     C                   MOVE      'DIV0012'     MSGNR
     C                   SETON                                            30
      *
     C     SLHENT        ENDSR
      *
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     AIRRDX        KLIST
     C                   KFLD                    XSDF
     C                   KFLD                    XSDT
     C                   KFLD                    WSLIN
     C     AIRRDK        KLIST
     C                   KFLD                    XSDF
     C                   KFLD                    XSDT
      *
     C     *ENTRY        PLIST
     C     WSSDF         PARM                    USDF              3
     C     WSSDT         PARM                    USDT              3
     C     WSLIN         PARM                    ULIN              3 0
     C     W2KN          PARM                    UKN               8 0
     C     W2COD         PARM                    URK               1
     C     W2COM         PARM                    UCOM              4
     C     W2VKT         PARM                    UVKT              5 0
     C                   PARM                    URPR              1
     C                   PARM                    URATE            13 2
     C                   PARM                    UMOD              1
     C     UMOD          COMP      'O'                                    27
      *
     C                   WRITE     WINDOWSZ
      *
     C                   ENDSR
      *
