     A*%%TS  SD  20190524  175109  SVEIN       REL-V7R1M0  5770-WDS
     A*%%EC
     A                                      DSPSIZ(27 132 *DS4)
     A                                      PRINT
     A                                      HLPPNLGRP(HLPSCR SYEX88PNL)
     A                                      HLPTITLE('ARBEIDE MED TURER')
     A                                      HELP
     A                                      ALTHELP
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  6  3
     A  98                                  DSPATR(RI)
     A                                      VALUES(' ' '1' '2' '4' '5' '7' '8' -
N09  A                                      '9' 'I' 'O' 'S' 'P' 'T' 'C' 'E' 'G')
S09  A*                                     '9' 'I' 'O' 'S' 'P' 'T' 'C')
     A            TUAVD          4Y 0O  6  6EDTCDE(4)
     A  27                                  COLOR(YLW)
     A            TUPRO          8Y 0O  6 11EDTCDE(4)
     A  27 78 79                            COLOR(YLW)
     A N79                                  COLOR(RED)
     A N78 79                               DSPATR(RI)
     A            TUBILN        10A  O  6 20
     A  28                                  COLOR(YLW)
     A            WSSDFX        13A  O  6 43
     A            TUSDTX        13A  O  6 57
     A  29                                  COLOR(YLW)
     A            WSDT           6Y 0O  6 71
     A            TUST           1A  O  6 78
     A            TUNAT         30A  O  6 81
     A            KNAVN19       19A  O  6112
     A            TUDT           8Y 0H
     A            TUHENG        10A  O  6 31
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A*%%TS  SD  20180612  132918  JOVO        REL-V7R1M0  5770-WDS
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A  91N93N94                            ROLLUP(25)
S10  A* 91N93N94                            ROLLDOWN(26)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A                                  1  2'SYSPED/SYEX88'
     A                                      COLOR(BLU)
     A  31                              1 19MSGCON(030 DIV0655 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A N31                              1 19MSGCON(030 DIV0722 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1123DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A  27                              2  2MSGCON(009 DIV0050 *LIBL/SYMSGF)
     A  27        WSAVD          4Y 0O  2 12COLOR(BLU)
     A                                      EDTCDE(4)
     A                                  2123TIME
     A                                      COLOR(BLU)
     A            ZRECNO         4S 0O  2 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  3  2MSGCON(026 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 74'I=ISO'
     A                                      COLOR(BLU)
     A N32                              4  2MSGCON(007 DIV0656 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  32                              4  2MSGCON(007 DIV0986 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  32                              4 12MSGCON(007 DIV0656 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N32                              4 12MSGCON(007 SHU0662 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 22MSGCON(015 JOV0213 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 40MSGCON(013 DIV2222 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 56'8=Fritekst'
     A                                      COLOR(BLU)
     A                                  4 69MSGCON(005 YBC0855 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  2MSGCON(129 SHU1296 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  3 29MSGCON(008 SHU1312 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 40MSGCON(030 SHU1373 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 82'C=SamleCMR'
     A                                      COLOR(BLU)
     A                                  4 77'E=EventGrabber SMS'
     A                                      COLOR(BLU)
     A                                  4 98'G=Godsl.på mail'
     A                                      COLOR(BLU)
     A          R BUNN
     A                                      ROLLUP(25)
S10  A*                                     ROLLDOWN(26)
     A N32                                  CF03(03 'Avslutt')
     A                                      CF05(05 'Frisk')
     A  27                                  CF06(06 'LAGE NY')
     A                                      CF08(08 'SØKE TUR')
     A  32                                  CF12(12 'TIL SYEX10')
     A                                      CF14(14 'legg inn tillatelser')
     A                                      SLNO(20)
     A                                      CLRL(05)
     A                                      OVERLAY
     A N32                              4  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  32                              4  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  27                              4 42MSGCON(018 DIV0022 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 62MSGCON(018 SHU0148 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  5  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A          R SØKTUR
     A*%%TS  SD  20190524  175109  SVEIN       REL-V7R1M0  5770-WDS
     A                                      ROLLUP(25)
     A                                      CF12(12 'FORRIGE')
     A                                      CF14(14 'legge inn tillatelser')
     A                                      CF04(04 'SØK TRANSPORTØR')
     A                                      SLNO(20)
     A                                      CLRL(05)
     A                                      OVERLAY
     A            MSGNR          7A  P
     A            WSAVD          4Y 0B  1  2DSPATR(UL)
     A                                      DSPATR(PC)
     A  99                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSPRO          8Y 0B  1  7DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  1 17MSGCON(030 DIV0658 *LIBL/SYMSGF)
     A            YST            1A  B  1 53DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  1 55MSGCON(014 SHU0149 *LIBL/SYMSGF)
     A            WSBILN        10A  B  2  2DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSDT1          6Y 0B  2 13DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSHENG         1A  B  2 20VALUES(' ' 'H')
     A                                      COLOR(YLW)
     A                                  2 23MSGCON(050 DIV0981 *LIBL/SYMSGF)
     A            WSSDT         20A  B  3  2DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSDT2          6Y 0B  3 23DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  3 30MSGCON(030 DIV0982 *LIBL/SYMSGF)
     A            WSKNT          8Y 0B  4  2EDTCDE(Z)
     A                                      COLOR(YLW)
     A            YST4           1A  B  4 11COLOR(YLW)
     A            WSDT4          6Y 0B  4 13DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSAVD4         4Y 0B  4 21COLOR(YLW)
     A                                      EDTCDE(Z)
     A                                  4 45MSGCON(035 SHU2019 *LIBL/SYMSGF)
     A                                  6  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  7  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A          R NYWRK
     A N32                                  CF03(03 'Avslutt')
     A                                      CF05(05 'Frisk')
     A  27                                  CF06(06 'LAGE NY')
     A                                      CF08(08 'SØKE TUR')
     A  32                                  CF12(12 'TIL SYEX10')
     A                                  1  2'SYSPED/SYEX88'
     A                                      COLOR(BLU)
     A  31                              1 19MSGCON(030 DIV0655 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A N31                              1 19MSGCON(030 DIV0722 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 58DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A  27                              2  2MSGCON(009 DIV0050 *LIBL/SYMSGF)
     A  27        WSAVD          4Y 0O  2 12COLOR(BLU)
     A                                      EDTCDE(4)
     A                                  2 58TIME
     A                                      COLOR(BLU)
     A                                  3  2MSGCON(026 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N32                              4  2MSGCON(015 DIV0656 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  32                              4  2MSGCON(015 DIV0986 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  32                              4 18MSGCON(015 DIV0656 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  2MSGCON(078 DIV0657 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A N32                             23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  32                             23  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  27                             23 42MSGCON(018 DIV0022 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 62MSGCON(018 SHU0148 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      * MAN KUN DEFINNERE VINDU PÅ REKORDNIVÅ
     A          R WINDOWSZ
     A                                      WINDOW(7 25 4 30 *NOMSGLIN)
     A                                      OVERLAY
     A          R SCANN
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A                                      CF12(12 'AVBRYT')
     A                                  2  2MSGCON(020 SHU1372 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  4  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A            SCATYP         2   B  2 24
     A  33                                  DSPATR(RI)
