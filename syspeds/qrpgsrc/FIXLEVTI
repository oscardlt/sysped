     FLEVTICSV  IP   F 1024        DISK
      * eks:
      *  KUNDE;PRODUCT;FraLK;FraPostnr-start;FraPostnr-slutt;TilLK;TilPostnr-start;TilPostnr-slutt;
      *  .........Avg.dgr 1=ma,2=ti,..;Framføringstid i dgr;Framføringstid timer;Kode for Start-tid;
      *  .........Seneste BOOKTid;Seneste INNtid TERM;Seneste AVGtid;Seneste LEVtid;
      *  0;;NO;4000;4399;NO;5000;5330;12345;1;0;OPPD ;1130;;1530;1000;
      *  0;;NO;4000;4399;NO;5954;5954;12345;1;0;OPPD ;1130;;1530;1000;
      *  0;;NO;4000;4399;NO;6900;6999;12345;1;0;OPPD ;900;;1300;1000;;
      *  0;E;NO;4000;4399;NO;5000;5330;12345;0;6;OPPD ;1130;;1530;1000;
      *  0;E;NO;4000;4399;NO;5954;5954;12345;0;8;OPPD ;1130;;1530;1000;
      *  0;E;NO;4000;4399;NO;6900;6999;12345;0;12;OPPD ;900;;1300;1000;
N01  FLEVTID    UF A E           K DISK
     D A               S              1    DIM(79)
     D B               S              1    DIM(18)
      *________________________
      * DS for decimal handling
      *""""""""""""""""""""""""
     D                 DS
     D  ZDEC                   1      7  6
     D  ZDEC6                  2      7
     D  ZHEL                  11     22  0
     D                 DS
     D  data                   1   1024
     D  da                     1   1024
     D                                     DIM(1024)
     D                 DS
     D  felta                  1     60
     D  fa                     1     60
     D                                     DIM(60)
      * ARRAY MED INTIL 600 KOLONNER A 100 TEGN etter dekoding.
     D                 DS
     D  dta                    1  60000
     D                                     DIM(600)
     D LO              C                   CONST('abcdefghijklmnopqrst-
     D                                     uvwxyzæøå')
     D UP              C                   CONST('ABCDEFGHIJKLMNOPQRST-
     D                                     UVWXYZÆØÅ')
     D                 DS
     D  Test                   1      5
     D  Test1_3                1      3
     D  Test4_5                4      5
     D  Test5_5                5      5
     D  Test1_4                1      4
     D                 DS
     D  ADR1                   1     30
     D  ADR1_1_4               1      4
     ILEVTICSV  NC  01
     I                                  1    8  Data1_8
     I                                  1 1024  Data
      * Første syklus = TØM
     C     Ini           ifeq      ' '
     c                   exsr      delPnr
     c                   move      'X'           Ini               1
     c                   endif
      *
      * INITIERER VARIABLE
      *
     c                   z-add     1             fra               3 0
     c                   z-add     1             til               3 0
     c                   z-add     0             DN                3 0
     c                   clear                   dta
     C     DATA1_8       IFeq      'KUNDE;PR'
     C     DATA1_8       oreq      ';;;;;;;;'
     C                   GOTO      NextRcd
     C                   END
      *
      * leser til neste skilletegn ';'
      *
     c     til           dowlt     900
     C     da(til)       ifne      ';'
     c                   add       1             til
     c                   else
      *
      * fra og til er funnet
      *
     c                   exsr      dekod
     c                   add       1             til
     c                   z-add     til           fra
     c                   endif
     c                   enddo
      *
      * siste post
      *
     c     fra           add       58            til
     c                   exsr      dekod
      * DETALJBEHANDLING
      *
     c                   exsr      DETSR
      *
     c     NextRcd       tag
      *
     c***************************************************************
     c     delPnr        begsr
     c***************************************************************
     c     *loval        setll     LEVTID
     c                   read      LEVTID                                 33
     c     *in33         doweq     '0'
     c                   delete    LEVTIDR
     c                   read      LEVTID                                 33
     c                   enddo
     c                   endsr
     c***************************************************************
     c     dekod         begsr
     c***************************************************************
      *
      * dekoder innlest post
      *
     c     til           sub       fra           antp              3 0
     c     antp          ifgt      0
     c                   exsr      dokoda
     c                   add       1             dn
     c                   movel     felta         dta(dn)
     C                   ELSE
     c                   add       1             dn
     c                   end
     c                   endsr
     c***************************************************************
     c     DOKODA        begsr
     c***************************************************************
     c*    dekoder  TIL FELTA
     c*
     c                   move      *blanks       felta
     c                   z-add     fra           nr                3 0
     c                   z-add     1             n2                3 0
     c                   do        antp
     c                   move      da(nr)        fa(n2)
     c                   add       1             n2
     c                   add       1             nr
     c                   enddo
     c                   endsr
     c***************************************************************
     c     DETSR         begsr
     c***************************************************************
     c                   clear                   LEVTIDR
      *
      *1 = kunde
     c                   movea     dta(1)        a
     c                   exsr      getnum
     c                   z-add     znum          LTKNR
      *
      *2 = Prod
     c                   movel     dta(2)        LTPROD
      *3 = Fra LK
     c                   movel     dta(3)        LTFLK
      *4 = Fra PnrFra
     c                   movel     dta(4)        LTFPN1
      *5 = Fra PnrTil
     c                   movel     dta(5)        LTFPN2
      *6 = Til LK
     c                   movel     dta(6)        LTTLK
      *7 = Til PnrFra
     c                   movel     dta(7)        LTTPN1
      *8 = Til PnrTil
     c                   movel     dta(8)        LTTPN2
      *9 = Avgangsdager
     c                   movel     dta(9)        LTAVGD
      *10= Framføringstid  dgr
     c                   movea     dta(10)       a
     c                   exsr      getnum
     c                   z-add     znum          LTFRFT
      *11= Framføringstid  timer
     c                   movea     dta(11)       a
     c                   exsr      getnum
     c                   z-add     znum          LTFRTT
      *12= Kode for starttid
     c                   movel     dta(12)       LTSTMK
      *13= Seneste BOOKtid
     c                   movea     dta(13)       a
     c                   exsr      getnum
     c                   z-add     znum          LTBOTI
      *14= Seneste INNtid
     c                   movea     dta(14)       a
     c                   exsr      getnum
     c                   z-add     znum          LTINTI
      *15= Seneste AVGtid
     c                   movea     dta(15)       a
     c                   exsr      getnum
     c                   z-add     znum          LTAVTI
      *16= Seneste LEVtid
     c                   movea     dta(16)       a
     c                   exsr      getnum
     c                   z-add     znum          LTLETI
      *
     c                   write     LEVTIDR
     C     Next          tag
      *
      *
     c                   endsr
     c*****************************************
     c     getnum        begsr
     c*****************************************
     C                   MOVE      *ZEROS        ZHEL
     C                   MOVE      *ZEROS        ZDEC
     C                   MOVE      *ZEROS        B
     C                   Z-ADD     1             AX                2 0
     C                   Z-ADD     1             BX                2 0
      * Get Integer
     C     A(AX)         DOWGE     '0'
     C     ZHEL          MULT      10            ZHEL
     C                   MOVE      A(AX)         ZHEL
     C                   ADD       1             AX
     C                   ENDDO
      * Decimal point ?
     C     A(AX)         IFEQ      '.'
     C     A(AX)         OREQ      ','
     C                   ADD       1             AX
     C     A(AX)         DOWGE     '0'
     C                   MOVE      A(AX)         B(BX)
     C                   ADD       1             AX
     C                   ADD       1             BX
     C                   ENDDO
     C                   MOVEA     B             ZDEC6
     C                   ENDIF
      * Concatenate
     C                   Z-ADD     ZHEL          ZNUM             18 6
     C                   ADD       ZDEC          ZNUM
     C                   ENDSR
     c
