      *********************************************************************
      * SPØRRING PÅ LAGERPOSTER
      * PROGRAM BEHANDLER INPUT FRA SLAGSC1 OG VISER UTVALGT(E) POST(ER)
      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/SLAGSC2 MODULE(*LIBL/SLAGSC2
CRTPGM*        *LIBL/CHECKUSR *LIBL/INCGI) ACTGRP(CGI) AUT(*USE)
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 YBC PTF10 NYTT VALG FOR "KUN MED SALDO > 0"
10XXX * 02 YBC PTF10 VIS DOKUMENTER
********************************************************************
      *
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
      * Boats for sale data base file (LF on BOATSALE by boat serial number)
     FLLGRPK05  if   e           k disk    USROPN
     FLLVARE01  if   e           k disk    USROPN
     FLLVADR3   if   e           k disk    USROPN
     FKUFAST    IF   E           K DISK    USROPN
     FBRIDF     UF   E           K DISK    USROPN
     FBRPARF    IF   E           K DISK    USROPN
N02  FFIRMARC   IF   E           K DISK    USROPN
N02  FARKTXT    IF   E           K DISK    USROPN
N02  FARKEXT    IF   E           K DISK    USROPN
N02  FARKIVL14  IF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Prototype definitions and standard system API error structure
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      * Variables common to CGI programs
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
      * VARIABLES SPECIFIC TO THIS PROGRAM
      *--------------------------------------------------------------------
      * Variables received from the input string
     D lng             s              2
     D UsrSpcName      s             10
      * Variables used to load the external html source member
     D Lib             s             10
     D Fn              s             10
     D Mbr             s             10
      * Boats serial numbers received in the input string
     D ItemCount       s             10i 0
     D SernoC          s             15a
     D USER            s             10a
     D artnr           s             15
     D artnM           s             35
     D lastartnr       s             15
     D vagrp           s              5
     D inkansv         s             30
     D minpros         s              2
     D eier            s              5
     D BevDato         S             10
N01  D Saldopl         S              1
N02  D XBANE           S             50
     D XXENDRE         s            150
     D i               s             10i 0
     D PGM             C                   CONST('SYSPED/CHECKUSR')
      *
     D Align           s              5a
     D r               s             10i 0
     D Spaces          s             12a   inz('&nbsp;&nbsp;')
      * Varegrupper 1 - 5
     D                 DS
     D  VAREGRPKD              1     30
     D  LVAG01                 1      5
     D  LVAG02                 7     11
     D  LVAG03                13     17
     D  LVAG04                19     23
     D  LVAG05                25     29
     D XXENDRE1        C                   CONST('<a href="/sycgip/slagsch2-
     D                                     B.pgm?serno=')
     D XXENDRE2        C                   CONST('" target="_blank">Endre a-
     D                                     rt. ')
     D XXENDRE3        C                   CONST('</a>')
N02  D                 DS
N02  D  updf1                  1    400
N02  D  updf2                401    800
N02  D  updf3                801   1200
N02  D  updf4               1201   1600
N02  D  updf5               1601   2000
N02  D  updf6               2001   2400
N02  D  updf7               2401   2800
N02  D  updf8               2801   3200
N02  D  updf9               3201   3600
N02  D  updf10              3601   4000
N02  D  updf11              4001   4400
N02  D  updf12              4401   4800
N02  D  updf13              4801   5200
N02  D  updf14              5201   5600
N02  D  updf15              5601   6000
N02  D  updf16              6001   6400
N02  D  updf17              6401   6800
N02  D  updf18              6801   7200
N02  D  updf19              7201   7600
N02  D  updf20              7601   8000
N02  D  updf21              8001   8400
N02  D  updf22              8401   8800
N02  D  updf23              8801   9200
N02  D  updf24              9201   9600
N02  D  updf25              9601  10000
N02  D  updf26             10001  10400
N02  D  updf27             10401  10800
N02  D  updf28             10801  11200
N02  D  updf29             11201  11600
N02  D  updf30             11601  12000
N02  D  updf31             12001  12400
N02  D  updf32             12401  12800
N02  D  updf33             12801  13200
N02  D  updf34             13201  13600
N02  D  updf35             13601  14000
N02  D  updf36             14001  14400
N02  D  updf37             14401  14800
N02  D  updf38             14801  15200
N02  D  updf39             15201  15600
N02  D  updf40             15601  16000
N02  D  updf                   1  16000    DIM(40)
      *--------------------------------------------------------------------
      * Prolog common to CGI programs
      * -Receive input from the remote browser
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prolog3
      *=====================================================================
      * MAIN PROCESSING LINE
      *=====================================================================
      * Get program start time for calculating execution time
     C                   time                    timedata1
      * Parse input string
     C                   exsr      Parse
      *
     C                   EXSR      INIT
     C                   CALLB     PGM
     C                   PARM                    BIBRID
     C                   PARM                    UsrSpcName
     C                   PARM                    UINN              1
     C                   IF        UINN = 'N'
     C                   GOTO      SLUTT
     C                   END
     C     USER          CHAIN     BRIDF                              50
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVE      WDT           BIDTF
     C                   MOVE      WTM           BITIDF
     C                   UPDATE    BRIDFR
      * Set output skeleton html member name
     C                   exsr      SetSkl
      * Read skeleton output html into core
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
      * Retrieve environment variables
     C                   exsr      RtvEnvVar
      *
      * Issue html section /$top
     C                   exsr      SetTop
     C                   callp     wrtsection('top')
      *------------------
      * Retrieve number of boats to be shown
     C                   eval      ItemCount = ZhbGetVarCnt('serno')
     C     ItemCount     ifeq      0
      * If no boats selected
     C                   callp     wrtsection('noselect')
     C                   ELSE
      * Show the requested boats
     C                   eval      i =1
     C     i             DOUGT     ItemCount
     C                   eval      SernoC = ZhbGetVar('serno':i)
     C                   move      *BLANKS       LvAnr
     C                   movel     SernoC        LvAnr
     C     VareKey       chain     LLVARER                            60
     C     *in60         ifeq      '0'
     C                   exsr      SetFound
     C                   callp     wrtsection('found')
      * Får bruker lov til å gå videre til endring av artikkel ?
     C                   EVAL      PABRID = USER
     C                   EVAL      PAKUNR = 0
     C                   EVAL      PAID   = 'LLVEN'
     C     BRPARFK       CHAIN     BRPARF                             40
     C                   IF        *IN40 = *OFF
     C                   callp     wrtsection('endre1')
     C                   ENDIF
     C                   callp     wrtsection('endre2')
     C                   else
     C                   callp     wrtsection('notfound')
     C                   endif
     C                   eval      i = i +1
     C                   ENDDO
     C                   endif
      * Send output buffer and quit
     C     SLUTT         TAG
     C                   exsr      Exit
      *=====================================================================
      * Send output buffer and quit
      *=====================================================================
     C     Exit          begsr
      * Compute and display run time
     C                   time                    timedata2
     C     timedata2     subdur    timedata1     ms:*ms
     C                   eval      sec = ms / 1000000
     C                   callp     updhtmlvar('runtime':
     C                             %trim(%editw(sec:'     0 .   ')))
     C                   callp     wrtsection('endhtml')
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      *
     C                   close     LLVARE01
     C                   close     LLGRPK05
     C                   close     LLVADR3
     C                   close     KUFAST
     C                   close     BRIDF
     C                   close     BRPARF
N02  C                   close     FIRMARC
N02  C                   close     ARKTXT
N02  C                   close     ARKEXT
N02  C                   close     ARKIVL14
     C                   eval      *inlr = *on
      *
     C                   return
     C                   endsr
      *=====================================================================
      * Parse input string
      *=====================================================================
     C     Parse         begsr
     C                   eval      lng     = zhbgetvar('lng')
     C                   eval      lastartnr = zhbgetvar('LastArtnr')
     C                   eval      USER    = zhbgetvar('USER')
     C                   EVAL      UsrSpcName  = zhbgetvar('UsrSpcName')
     C                   eval      artnr   = zhbgetvar('artnr')
     C                   eval      artnM   = zhbgetvar('artnM')
     C                   eval      vagrp   = zhbgetvar('varegrp')
     C                   eval      inkansv = zhbgetvar('inkansv')
     C                   eval      minpros = zhbgetvar('minpros')
     C                   eval      eier    = zhbgetvar('eier')
     C                   eval      Bevdato  = zhbgetvar('Bevdato')
N01  C                   eval      Saldopl  = zhbgetvar('Saldopl')
     C                   endsr
      *=====================================================================
      * Set html output variables for section /$top
      *=====================================================================
     C     SetTop        begsr
      * Repeat national language for the next invocation
     C                   callp     updhtmlvar('LNG':lng:
     C                             InitHTMLVars)
      * Repeat background color for the next invocation
     C                   callp     updhtmlvar('TXTCOLOR':'black')
     C                   callp     updhtmlvar('BOLD':' ')
     C                   callp     updhtmlvar('BGCOLOR':BIFARG)
     C                   callp     updhtmlvar('LINK':'0000FF')
     C                   callp     updhtmlvar('VLINK':'FF0000')
     C                   callp     updhtmlvar('ALINK':'00FF00')
      * Protocol
     C                   callp     updHtmlVar('PROTOCOL':S_Protocol)
N02  C                   callp     updhtmlvar('user':user)
N02  C                   callp     updHtmlVar('UsrSpcName':UsrSpcName)
     C                   endsr
      *=====================================================================
      * Set html output variables for section /$found (display a boat)
      *=====================================================================
     C     SetFound      begsr
      * Hent Leverandør
     C     LLVADR3K      CHAIN     LLVADR3                            33
     C   33              MOVE      *BLANKS       VADRNA
      * HENT VAREGRUPETEKST
     C                   MOVEL     LVAG01        KFKOD
     C                   MOVE      *BLANKS       LVVGT1           50
     C     KufastKVG     chain     KUFAST                             33
     C  N33              MOVEL     KFTXT         LVVGT1
     C                   callp     updHTMLvar('LVAG01':LVAG01)
     C                   callp     updHTMLvar('LVVGT1':LVVGT1)
     C                   MOVEL     LVAG02        KFKOD
     C                   MOVE      *BLANKS       LVVGT2           50
     C     KufastKVG     chain     KUFAST                             33
     C  N33              MOVEL     KFTXT         LVVGT2
     C                   callp     updHTMLvar('LVAG02':LVAG02)
     C                   callp     updHTMLvar('LVVGT2':LVVGT2)
     C                   MOVEL     LVAG03        KFKOD
     C                   MOVE      *BLANKS       LVVGT3           50
     C     KufastKVG     chain     KUFAST                             33
     C  N33              MOVEL     KFTXT         LVVGT3
     C                   callp     updHTMLvar('LVAG03':LVAG03)
     C                   callp     updHTMLvar('LVVGT3':LVVGT3)
     C                   MOVEL     LVAG04        KFKOD
     C                   MOVE      *BLANKS       LVVGT4           50
     C     KufastKVG     chain     KUFAST                             33
     C  N33              MOVEL     KFTXT         LVVGT4
     C                   callp     updHTMLvar('LVAG04':LVAG04)
     C                   callp     updHTMLvar('LVVGT4':LVVGT4)
     C                   MOVEL     LVAG05        KFKOD
     C                   MOVE      *BLANKS       LVVGT5           50
     C     KufastKVG     chain     KUFAST                             33
     C  N33              MOVEL     KFTXT         LVVGT5
     C                   callp     updHTMLvar('LVAG05':LVAG05)
     C                   callp     updHTMLvar('LVVGT5':LVVGT5)
      * HENT VAREEIERTEKST
     C                   MOVEL     LVEIER        KFKOD
     C                   MOVE      *blanks       VareEier         30
     C     KufastKVE     chain     KUFAST                             33
     C  N33              MOVEL     KFTXT         VareEier
      * Sett inn i HTMLvariabler
     C                   callp     updHtmlVar('LVANR':lvanr)
     C                   callp     updHtmlVar('LVVB':lvvb)
     C                   callp     updHTMLvar('LVEIER':lveier)
     C                   callp     updHTMLvar('VAREEIER':VareEier)
     C     lvans         ifeq      *blanks
     C                   callp     updhtmlvar('LVANS':spaces)
     C                   else
     C                   callp     updHtmlVar('LVANS':lvans)
     C                   endif
     C                   callp     updHtmlVar('LVTXT':lvtxt)
     C                   select
     C     LVMIN         wheneq    0
     C                   movel     'Stykk      ' Lsalg            12
     C     LVMIN         wheneq    1
     C                   movel     'Eske       ' Lsalg            12
     C     LVMIN         wheneq    2
     C                   movel     'Kartong    ' Lsalg            12
     C     LVMIN         wheneq    3
     C                   movel     'Pall       ' Lsalg            12
     C                   endsl
     C                   move      *blanks       KrtPllTxt
     C     lvan02        ifne      *zeros
     C     lvan03        andne     *zeros
     C                   movel     'Krt/Pll'     KrtPllTxt         7
     c                   end
     C                   callp     updHTMLvar('LSALG':LSalg)
     C                   callp     updhtmlvar('ANTKRT':
     C                             %trim(%editc(lvan02:'J')))
     C                   callp     updhtmlvar('ANTPALL':
     C                             %trim(%editc(lvan03:'J')))
     C                   callp     updHTMLvar('KRTPALLTXT':KrtPllTxt)
     C                   callp     updhtmlvar('KRTPALLANT':
     C                             %trim(%editc(lvan23:'Z')))
     C                   callp     updHTMLvar('LEVER':VADRNA)
     C                   callp     updhtmlvar('LVSALD':
     C                             %trim(%editc(lvsald:'J')))
     C                   callp     updhtmlvar('LVMSTK':
     C                             %trim(%editc(lvmstk:'J')))
      * prosent  over minimumsnivå..
     C     LvMstk        IFEQ      0
     C                   z-add     100,00        DiffPros          8 2
     C                   else
     C     LVsald        sub       LVMstk        XXDiff            7 0
     C                   eval      DiffPros = XXDiff / LvMstk * 100
     C                   end
     C                   callp     updhtmlvar('DIFFPROS':
     C                             %trim(%editc(DiffPros:'J')))
      * gif name
     C                   move      *blanks       LvGif            15
     C     LvGif         ifeq      *blanks
     C                   move      LvAnr         LvGif
     C                   endif
     C                   callp     updhtmlvar('LVGIF':LvGif)
     c* Gif alignment
     C     LvGifA        ifeq      *blank
     C                   eval      LvGifA ='L'
     C                   endif
     C     LvGifA        ifeq      'R'
     C                   eval      align = 'right'
     C                   else
     C                   eval      align = 'left'
     C                   endif
     C                   callp     updhtmlvar('ALIGN':align)
     c* Gif horizontal spacing
     C     LvGifH        ifeq      0
     C                   eval      LvGifH =8
     C                   endif
     C                   callp     updhtmlvar('LVGIFH':
     C                             %trim(%editc(LvGifH:'1')))
     c* Gif vertical spacing
     C     LvGifV        ifeq      0
     C                   eval      LvGifV=5
     C                   endif
     C                   callp     updhtmlvar('LVGIFV':
     C                             %trim(%editc(LvGifV:'1')))
      *
BODY C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updhtmlvar('user':user)
     C                   callp     updHtmlVar('UsrSpcName':UsrSpcName)
     C                   callp     updhtmlvar('LASTLVANR':lastartnr)
     C                   callp     updhtmlvar('ARTNR':artnr)
     C                   callp     updhtmlvar('ARTNM':artnM)
     C                   callp     updhtmlvar('VAREGRP':vagrp)
     C                   callp     updhtmlvar('INKANSV':inkansv)
     C                   callp     updhtmlvar('MINPROS':minpros)
     C                   callp     updhtmlvar('eier':eier)
     C                   callp     updhtmlvar('BEVDATO':BEVDATO)
N01  C                   callp     updhtmlvar('SALDOPL':SALDOPL)
      *
     C                   IF        BIEIER = *BLANKS
     C                   EVAL      XXENDRE = XXENDRE1
     C                   CAT       LVANR:0       XXENDRE
     C                   CAT       XXENDRE2:0    XXENDRE
     C                   CAT       LVANR:1       XXENDRE
     C                   CAT       XXENDRE3:0    XXENDRE
     C                   ELSE
     C                   EVAL      XXENDRE = *BLANKS
     C                   END
     C                   callp     updhtmlvar('XXENDRE':XXENDRE)
N02   *
N02  C                   EXSR      HENTDOK
      *
     C                   endsr
      *=====================================================================
      * Set html output skeleton member before its read into memory
      *=====================================================================
     C     SetSkl        begsr
     C                   eval      lng = uppify(lng)
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC' + lng
     C                   eval      Mbr = 'SLAGSC2'
     C                   CAT       XSPRÅK:0      MBR
     C                   endsr
      *=====================================================================
      *  Retrieve environment variables
      *=====================================================================
     C     RtvEnvVar     begsr
      * Use getenvp to get this server's protocol
     C                   eval      S_Protocol =getenv('SERVER_PROTOCOL':
     C                             qusec)
      *
     C                   endsr
N02   *=====================================================================
N02   *  HENT DOKUMENTER
N02   *=====================================================================
N02  C     HENTDOK       BEGSR
N02  C                   EVAL      ARUNDE = 'L:'
N02  C                   MOVE      BIKUNR        ARUNDE
N02  C                   EVAL      ARFRI  = LVANR
N02   *
N02  C     ARKIVL14K     SETLL     ARKIVL14
N02  C     ARKIVL14K     READE     ARKIVL14                               33
N02  C                   MOVE      *BLANKS       UPDF
N02  C                   MOVE      *ZEROS        XN                3 0
N02  C                   DOW       *IN33 = *OFF
N02  C                   ADD       1             XN
N02  C                   EVAL      UPDF(XN) = '<a href="'
N02  C                   CAT       XBANE:0       UPDF(XN)
N02  C                   CAT       ARLINK:0      UPDF(XN)
N02  C                   CAT       '" target="':0UPDF(XN)
N02  C                   CAT       'SyPDFWind': 0UPDF(XN)
N02  C                   CAT       '">':0        UPDF(XN)
N02  C                   IF        ARRFK = *BLANKS
N02  C                   CAT       'DOC':0       UPDF(XN)
N02  C                   MOVE      XN            XAN               3
N02  C                   CAT       XAN:0         UPDF(XN)
N02  C                   ELSE
N02  C                   CAT       ARRFK:0       UPDF(XN)
N02  C                   END
N02  C                   CAT       '<img src="':0UPDF(XN)
N02  C                   CAT       '/syspeddev':0UPDF(XN)
N02  C                   CAT       '/PDF.gif"':0 UPDF(XN)
N02  C                   CAT       'ALT="':1     UPDF(XN)
N02  C                   CAT       'DOC':0       UPDF(XN)
N02  C                   CAT       XAN:0         UPDF(XN)
N02  C                   CAT       '"></a>':0    UPDF(XN)
N02   *
N02  C                   IF        XAUT = 'L'
N02  C                   EVAL      UPDF(XN) = %trim(UPDF(XN))
N02  C                             + ' <a href="#" OnClick="DltDoc('
N02  C                             + 'this.form,'
N02  C                             + %trim(XAPSTR)
N02  C                             + %trim(XBANE) + %trim(ARLINK)
N02  C                             + %trim(XAPSTR) + ',' + %trim(XAPSTR)
N02  C                             + %trim(ARRFK)
N02  C                             + %trim(XAPSTR)
N02  C                             + ')"> <img src="/syspeddev/DeleteX.gif"'
N02  C                             + ' width=13 height=13 border=0>'
N02  C                   END
N02   *
N02  C     XN            COMP      40                                 33  33
N02  C  N33ARKIVL14K     READE     ARKIVL14                               33
N02  C                   ENDDO
N02   *
N02  C                   callp     updHTMLvar('UPDF1':UPDF1)
N02  C                   callp     updHTMLvar('UPDF2':UPDF2)
N02  C                   callp     updHTMLvar('UPDF3':UPDF3)
N02  C                   callp     updHTMLvar('UPDF4':UPDF4)
N02  C                   callp     updHTMLvar('UPDF5':UPDF5)
N02  C                   callp     updHTMLvar('UPDF6':UPDF6)
N02  C                   callp     updHTMLvar('UPDF7':UPDF7)
N02  C                   callp     updHTMLvar('UPDF8':UPDF8)
N02  C                   callp     updHTMLvar('UPDF9':UPDF9)
N02  C                   callp     updHTMLvar('UPDF10':UPDF10)
N02  C                   callp     updHTMLvar('UPDF11':UPDF11)
N02  C                   callp     updHTMLvar('UPDF12':UPDF12)
N02  C                   callp     updHTMLvar('UPDF13':UPDF13)
N02  C                   callp     updHTMLvar('UPDF14':UPDF14)
N02  C                   callp     updHTMLvar('UPDF15':UPDF15)
N02  C                   callp     updHTMLvar('UPDF16':UPDF16)
N02  C                   callp     updHTMLvar('UPDF17':UPDF17)
N02  C                   callp     updHTMLvar('UPDF18':UPDF18)
N02  C                   callp     updHTMLvar('UPDF19':UPDF19)
N02  C                   callp     updHTMLvar('UPDF20':UPDF20)
N02  C                   callp     updHTMLvar('UPDF21':UPDF21)
N02  C                   callp     updHTMLvar('UPDF22':UPDF22)
N02  C                   callp     updHTMLvar('UPDF23':UPDF23)
N02  C                   callp     updHTMLvar('UPDF24':UPDF24)
N02  C                   callp     updHTMLvar('UPDF25':UPDF25)
N02  C                   callp     updHTMLvar('UPDF26':UPDF26)
N02  C                   callp     updHTMLvar('UPDF27':UPDF27)
N02  C                   callp     updHTMLvar('UPDF28':UPDF28)
N02  C                   callp     updHTMLvar('UPDF29':UPDF29)
N02  C                   callp     updHTMLvar('UPDF30':UPDF30)
N02  C                   callp     updHTMLvar('UPDF31':UPDF31)
N02  C                   callp     updHTMLvar('UPDF32':UPDF32)
N02  C                   callp     updHTMLvar('UPDF33':UPDF33)
N02  C                   callp     updHTMLvar('UPDF34':UPDF34)
N02  C                   callp     updHTMLvar('UPDF35':UPDF35)
N02  C                   callp     updHTMLvar('UPDF36':UPDF36)
N02  C                   callp     updHTMLvar('UPDF37':UPDF37)
N02  C                   callp     updHTMLvar('UPDF38':UPDF38)
N02  C                   callp     updHTMLvar('UPDF39':UPDF39)
N02  C                   callp     updHTMLvar('UPDF40':UPDF40)
N02   *
N02  C                   ENDSR
N02   *
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
     C                   OPEN      BRIDF
     C     USER          CHAIN(N)  BRIDF                              50
     C     BILIBL        ifeq      *blanks
     C                   CALLB     'INCGI'
     C                   PARM                    BISTDL
     c                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
     C                   open      LLVARE01                             50
     C                   open      LLGRPK05                             50
     C                   open      LLVADR3                              50
     C                   open      KUFAST
     C                   open      BRPARF                               50
N02  C                   open      FIRMARC                              50
N02  C                   open      ARKTXT                               50
N02  C                   open      ARKEXT                               50
N02  C                   open      ARKIVL14                             50
      *
     C     BIKUNR        chain     LLGrpK05                           33
     c     VareKey       klist
     c                   kfld                    GKGRP
     c                   kfld                    GKPNR
     c                   kfld                    LVANR
     C     LLVADR3K      klist
     C                   kfld                    BIFIRM
     C                   kfld                    GKPNR
     C                   kfld                    LVLNR
     C     KufastKVG     klist
     C                   kfld                    KFTYPVG          10
     C                   kfld                    KFKOD
     C     KufastKVE     klist
     C                   kfld                    KFTYPVE          10
     C                   kfld                    KFKOD
     C     BRPARFK       klist
     C                   kfld                    PABRID
     C                   kfld                    PAKUNR
     C                   kfld                    PAID
N02  C     ARKIVL14K     klist
N02  C                   kfld                    BIFIRM
N02  C                   kfld                    ARTYPE
N02  C                   kfld                    ARUNDE
N02  C                   kfld                    ARFRI
N02  C     ARKLAGK       KLIST
N02  C                   KFLD                    BIFIRM
N02  C                   KFLD                    ARKLAG
      *
     C                   move      *blanks       LvGif            15
     C                   move      *blanks       LvGifA            1
     C                   move      *zeros        LvGifH            1 0
     C                   move      *zeros        LvGifV            1 0
     C                   move      *blanks       varegrpkd
N02  C                   BITON     '123457'      XAPSTR            1
N02  C                   BITOFF    '06'          XAPSTR
N02  C                   EVAL      ARTYPE  = 'ZO'
      *
     C                   MOVE      ' '           XVGNIVÅ           1
     C                   EVAL      KFTYPVG = 'VG'
      * VAREGRUPPER (BRUKER-NIVÅ)
     C                   EVAL      PABRID = USER
     C                   EVAL      PAKUNR = 0
     C                   EVAL      PAID   = 'LLVG'
     C     BRPARFK       CHAIN     BRPARF                             40
     C                   IF        *IN40 = *OFF
     C                   MOVE      'B'           XVGNIVÅ           1
     C                   MOVE      BIKUNR        KFTYPVG
     C                   ELSE
      * VAREGRUPPER (KUNDE-NIVÅ)
     C                   EVAL      PABRID = '*KUNDFT*'
     C                   MOVE      BIFIRM        PABRID
     C                   EVAL      PAKUNR = BIKUNR
     C     BRPARFK       CHAIN     BRPARF                             40
     C                   IF        *IN40 = *OFF
     C                   MOVE      'K'           XVGNIVÅ           1
     C                   MOVE      BIKUNR        KFTYPVG
     C                   ELSE
      * VAREGRUPPER (FIRMA-NIVÅ)
     C                   EVAL      PAKUNR = 0
     C     BRPARFK       CHAIN     BRPARF                             40
     C                   IF        *IN40 = *OFF
     C                   MOVE      'F'           XVGNIVÅ           1
     C                   CAT       '*':0         KFTYPVG
     C                   CAT       BIFIRM:0      KFTYPVG
     C                   CAT       '*':0         KFTYPVG
     C                   ELSE
     C                   MOVE      'K'           XVGNIVÅ           1
     C                   MOVE      BIKUNR        KFTYPVG
     C                   END
     C                   END
     C                   END
      *
     C                   MOVE      ' '           XVENIVÅ           1
     C                   EVAL      KFTYPVE = 'VE'
      * VAREEIER (BRUKER-NIVÅ)
     C                   EVAL      PABRID = USER
     C                   EVAL      PAKUNR = 0
     C                   EVAL      PAID   = 'LLVE'
     C     BRPARFK       CHAIN     BRPARF                             40
     C                   IF        *IN40 = *OFF
     C                   MOVE      'B'           XVENIVÅ           1
     C                   MOVE      BIKUNR        KFTYPVE
     C                   ELSE
      * VAREEIER (KUNDE-NIVÅ)
     C                   EVAL      PABRID = '*KUNDFT*'
     C                   MOVE      BIFIRM        PABRID
     C                   EVAL      PAKUNR = BIKUNR
     C     BRPARFK       CHAIN     BRPARF                             40
     C                   IF        *IN40 = *OFF
     C                   MOVE      'K'           XVENIVÅ           1
     C                   MOVE      BIKUNR        KFTYPVE
     C                   ELSE
      * VAREEIER (FIRMA-NIVÅ)
     C                   EVAL      PAKUNR = 0
     C     BRPARFK       CHAIN     BRPARF                             40
     C                   IF        *IN40 = *OFF
     C                   MOVE      'F'           XVENIVÅ           1
     C                   CAT       '*':0         KFTYPVE
     C                   CAT       BIFIRM:0      KFTYPVE
     C                   CAT       '*':0         KFTYPVE
     C                   ELSE
     C                   MOVE      'K'           XVENIVÅ           1
     C                   MOVE      BIKUNR        KFTYPVE
     C                   END
     C                   END
     C                   END
      *
     C                   MOVE      BIBRID        PABRID
     C                   MOVE      *ZEROS        PAKUNR
     C                   MOVEL     'SPRÅK'       PAID
     C     BRPARFK       chain     BRPARF                             33
     C                   IF        (*IN33 OR (PAVRDA <> 'EN'))
     C                   MOVE      *BLANKS       XSPRÅK            2
     C                   ELSE
     C                   MOVE      'EN'          XSPRÅK            2
     C                   END
N02   *
N02  C                   MOVE      'AUTOR'       PAID
N02  C     BRPARFK       chain     BRPARF                             33
N02  C                   IF        *IN33
N02  C                   MOVE      'N'           XAUT              1
N02  C                   ELSE
N02  C                   MOVEL     PAVRDA        XAUT
N02  C                   END
N02   *
N02  C     'ZO'          CHAIN     ARKTXT                             33
N02  C                   IF        ARKLAG = *BLANKS
N02  C     BIFIRM        READE     FIRMARC                                50
N02  C                   ELSE
N02  C     ARKLAGK       CHAIN     ARKEXT                             33
N02  C                   END
N02  C                   EVAL      XBANE = '/' + %TRIM(ARCANE) + '/'
      *
     C                   endsr
