      ***********************************************************
      *** PROGRAM HENTER OPPDRAG TIL HOUSECONS. VIA TURNR     ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01 02 04 07 09-11 13-24          ***
      *** LEDIGE INDIKATORER 27-29 31 32 34-39 41-85 90 95 96 ***
      ***********************************************************
      *
     FFIRM      IF   E           K DISK
     FCUNDL01   IF   E           K DISK
     FKOFAST    IF   E           K DISK
     FEDIM2     IF   E           K DISK
     FHEAD14    IF   E           K DISK
     FSADH      IF   E           K DISK
     FSADV4     IF   E           K DISK
     FSADEXIL1  IF A E           K DISK
     FSADEXHF   O  A E             DISK
     FSADEXHL1  UF   E           K DISK    RENAME(SADEXHFR:SADEXHFR1)
     F                                     PREFIX(H1_)
     FSADEXML2  IF   E           K DISK
     FSVEA      IF   E           K DISK
     FSVEH      IF   E           K DISK
     FSVZH      IF   E           K DISK
     FTRIH      IF   E           K DISK
      *
     C                   EXSR      INIT
      *
     C     HEPRO         SETLL     HEAD14
     C     HEPRO         READE     HEAD14                                 33
     C                   DOW       *IN33 = *OFF                                 1<-B
      *
     C     SADEXHL1K     CHAIN     SADEXHL1                           33
     C                   IF        *IN33                                         2<-B
     C                   EVAL      EHAVD   = HEAVD
     C                   EVAL      EHTDN   = HEOPD
     C                   EVAL      EHMID   = UMID
     C                   EVAL      EHPRO   = HEPRO
     C                   EVAL      EHVKB   = HEVKT
     C                   EVAL      EHNTK   = HENT
     C                   EVAL      EHVT    = %TRIM(HEVS1) + ' ' + %TRIM(HEVS2)
     C                   EVAL      EHLKF   = HELKA
     C                   EVAL      EHSDF   = HESDF
     C                   EVAL      EHLKT   = FILAND
     C                   EVAL      EHSDT   = HESDT
      *
     C                   EVAL      EHKNM   = HEKNK
     C                   EVAL      EHNAM   = HENAK
     C                   EVAL      EHAD1M  = HEADK1
     C                   EVAL      EHNRM   = HEADK2
     C     ' '           SCAN      HEADK3        XPOS              3 0    33
     C                   IF        XPOS = 0                                       3<-B
     C                   EVAL      EHPNM   = HEADK3
     C                   EVAL      EHPSM   = HEADK3
     C                   ELSE                                                     3
     C                   EVAL      EHPNM   = %SUBST(HEADK3:1:XPOS-1)
     C                   EVAL      EHPSM   = %SUBST(HEADK3:XPOS+1:30-XPOS)
     C                   END                                                      3<-E
     C                   IF        EHKNM = 0                                      3<-B
     C                   EVAL      EHLKM   = FILAND
     C                   ELSE                                                     3
     C                   EVAL      KUNDNR = EHKNM
     C     CUNDL01K      CHAIN     CUNDL01                            33
     C                   SELECT                                                    4<-B
     C                   WHEN      *IN33                                           4
     C                   EVAL      EHLKM   = FILAND
     C                   WHEN      SYLAND = *BLANKS
     C                   EVAL      EHLKM   = FILAND
     C                   OTHER                                                     4
     C                   EVAL      EHLKM   = SYLAND
     C                   ENDSL                                                     4<-E
     C  N33              SELECT                                                    4<-B
     C                   WHEN      SYEPOS <> *BLANKS                               4
     C                   EVAL      EHEMM   = SYEPOS
     C                   EVAL      EHEMMT  = 'EM'
     C                   WHEN      TLF <> *BLANKS                                  4
     C                   EVAL      EHEMM   = TLF
     C                   EVAL      EHEMMT  = 'TE'
     C                   ENDSL                                                     4<-E
     C                   END                                                      3<-E
      *
     C                   EVAL      EHKNS   = HEKNS
     C                   EVAL      EHNAS   = HENAS
     C                   EVAL      EHAD1S  = HEADS1
     C                   EVAL      EHNRS   = HEADS2
     C     ' '           SCAN      HEADS3        XPOS              3 0    33
     C                   IF        XPOS = 0                                       3<-B
     C                   EVAL      EHPNS   = HEADS3
     C                   EVAL      EHPSS   = HEADS3
     C                   ELSE                                                     3
     C                   EVAL      EHPNS   = %SUBST(HEADS3:1:XPOS-1)
     C                   EVAL      EHPSS   = %SUBST(HEADS3:XPOS+1:30-XPOS)
     C                   END                                                      3<-E
     C                   IF        EHKNS = 0                                      3<-B
     C                   EVAL      EHLKS   = FILAND
     C                   ELSE                                                     3
     C                   EVAL      KUNDNR = EHKNS
     C     CUNDL01K      CHAIN     CUNDL01                            33
     C                   SELECT                                                    4<-B
     C                   WHEN      *IN33                                           4
     C                   EVAL      EHLKS   = FILAND
     C                   WHEN      SYLAND = *BLANKS                                4
     C                   EVAL      EHLKS   = FILAND
     C                   OTHER                                                     4
     C                   EVAL      EHLKS   = SYLAND
     C                   ENDSL                                                     4<-E
     C  N33              SELECT                                                    4<-B
     C                   WHEN      SYEPOS <> *BLANKS                               4
     C                   EVAL      EHEMS   = SYEPOS
     C                   EVAL      EHEMST  = 'EM'
     C                   WHEN      TLF <> *BLANKS                                  4
     C                   EVAL      EHEMS   = TLF
     C                   EVAL      EHEMST  = 'TE'
     C                   ENDSL                                                     4<-E
     C                   END                                                      3<-E
      *
     C                   EVAL      EHLKR1  = HELKA
     C                   EVAL      EHLKR2  = FILAND
     C                   IF        HETRCN <> *BLANKS                              3<-B
     C                   EVAL      EHCNR   = HETRCN
     C                   END                                                      3<-E
      *
     C     SADHK         CHAIN     SADH                               33
     C                   IF        *IN33 = *OFF                                   3<-B
     C                   EVAL      EHTCVA = SIVAL1
     C                   EVAL      EHTCBL = SIBEL1
     C     SADHK         SETGT     EDIM2
     C     SADHK         READPE    EDIM2                                  33
     C                   DOW       *IN33 = *OFF                                    4<-B
     C                   IF        M1001 = '930'                                    5<-B
     C                   EVAL      EHRG = %SUBST(M0068:1:9)
     C                   EVAL      EH0068A = M0068A
     C                   EVAL      EH0068B = M0068B
     C                   EVAL      EH3039E = M3039E
     C                   LEAVE
     C                   END                                                        5<-E
     C     SADHK         READPE    EDIM2                                  33
     C                   ENDDO                                                     4<-E
     C                   ELSE                                                     3
     C     SADHK         CHAIN     TRIH                               33
     C                   IF        *IN33 = *OFF                                    4<-B
     C                   EVAL      EHTRNR = TITRNR
     C                   END                                                       4<-E
     C                   END                                                      3<-E
      *
     C     SADHK         CHAIN     SVEH                               33
     C                   IF        *IN33 = *OFF
     C                   IF        SVEH_MTYP = 'UGE'
     C                   EVAL      EHETYP = '05'
     C                   EVAL      KFTYP = 'SADEFETYPE'
     C                   EVAL      KFKOD = EHETYP
     C     KOFASTK       CHAIN     KOFAST                             33
     C                   IF        *IN33 = *OFF
     C                   EVAL      EHETYPT = KFTXT
     C                   END
     C                   END
     C     SVEH_TUID     CHAIN     SVZH                               33
     C  N33              IF        SVZH_MRNN <> *BLANKS
     C                   EVAL      EHEID = SVZH_MRNN
     C                   END
     C                   END
      *
      * Houseconsignment dokument
     C     HEPRO         CHAIN     SADEXML2                           33
     C                   MOVE      HEAVD         XAN04             4
     C                   MOVE      HEOPD         XAN07             7
     C                   EVAL      EHDKH  = %TRIM(EMRGD)
     C                             + '-' + XAN04 + '-' + XAN07
     C                   EVAL      EHDKHT = 'N714'
     C                   EVAL      EHST   = 'F'
      *
     C                   WRITE     SADEXHFR
      *
     C                   EXSR      SROPDITEM
      *
     C                   ELSE                                                    2
     C                   EVAL      EHMID   = UMID
     C                   UPDATE    SADEXHFR1
     C                   END                                                     2<-E
      *
     C     HEPRO         READE     HEAD14                                 33
     C                   ENDDO                                                  1<-E
      *
     C     SLUTT         TAG
     C                   EVAL      *INLR = *ON
     C                   RETURN
      *
      ***********************************************
     C     SROPDITEM     BEGSR
      ***********************************************
     C     SADEXHL1K     CHAIN     SADEXIL1                           33
     C                   IF        *IN33
      * INGEN LINJE FRA FØR. OPPDATER LINJE FRA SAD-LINJE
     C                   CLEAR                   SADEXIFR
     C                   EVAL      EIPRO  = UPRO
     C                   EVAL      EIAVD  = HEAVD
     C                   EVAL      EITDN  = HEOPD
     C                   EVAL      EIVAL  = SIVAL3
     C     SADHK         SETLL     SADV4
     C     SADHK         READE     SADV4                                  33
     C                   DOW       *IN33 = *OFF
     C                   IF        EILI = SVLI
     C                   EVAL      EIBL   = EIBL + SVBELT
     C                   EVAL      EICVKB = EICVKB + SVVKTB
     C                   EVAL      EICVKN = EICVKN + SVVKTN
     C                   EVAL      EINT   = EINT + SVNT01
     C                   ELSE
     C                   IF        EILI > 0
     C                   WRITE     SADEXIFR
     C                   END
     C                   EVAL      EILI   = SVLI
     C                   EVAL      EILID  = SVLI
     C                   EVAL      EIBL   = SVBELT
     C                   EVAL      EIVT   = SVVT01
     C                   EVAL      EIVNT  = SVVNT
     C                   EVAL      EICVKB = SVVKTB
     C                   EVAL      EICVKN = SVVKTN
     C                   EVAL      EILK   = SVLK
     C                   EVAL      EIMK   = SVFT01
     C                   EVAL      EINT   = SVNT01
     C                   EVAL      EINTEH = SVEH01
     C                   END
     C     SADHK         READE     SADV4                                  33
     C                   ENDDO
      *
     C                   END
      *
     C                   IF        EILI > 0
     C                   WRITE     SADEXIFR
     C                   END
      *
     C                   ENDSR
      *
      ***********************************************
     C     INIT          BEGSR
      ***********************************************
     C     *ENTRY        PLIST
     C                   PARM                    UPRO              8 0
     C                   PARM                    UMID             18
      *
     C     SADEXHL1K     KLIST
     C                   KFLD                    HEPRO
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C     CUNDL01K      KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    KUNDNR
     C     SADHK         KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C     KOFASTK       KLIST
     C                   KFLD                    KFTYP
     C                   KFLD                    KFKOD
      *
     C                   EVAL      HEPRO  = UPRO
      *
     C     FIFIRM        SETLL     FIRM
     C                   READ      FIRM                                   33
      *
     C                   ENDSR
      *
