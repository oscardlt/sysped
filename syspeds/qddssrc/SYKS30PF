     A* OBS! PAGESIZE(66 196)
     A* OBS! linjelengde 196 (sidelengde kan gjerne være 66)
     A* ( Det er "relativ plassering" av kolonner, om det kompileres
     A*   med 132 så brytes linja (over hverandre) og det hjelper
     A*   ikke at CL overrider til 196....)
     A*****************************************************************
     A*   UTLISTING AV AVSETNINGER VED PERIODESLUTT DETALJERT/SUMMERT *
     A* ANTALL LINJE=48,  ANTALL TEGN PR. LINJE=196, OVERFLOW=42      *
     A*****************************************************************
     A          R HEAD1P                    SKIPB(1)
     A            KFMT1         75         1
     A            KFMT2         75        76
     A* PCL-5 KODE FOR Å ROTERE 90 GRADER..
     A*                                                                        '
     A  89                                 1'¤&l'
     A                                       SPACEB(1)
     A  89        COPY           2  0      4
     A  89                                 6'X'
     A  89                                 8'¤&a-3R'
     A  89                                15'¤&l1O'
     A  89                                20'¤(12U¤(s0p19h0s3b4099T'
     A                                   155DATE EDTCDE(Y)
     A                                      SPACEB(1)
     A                                      HIGHLIGHT
     A                                   164TIME EDTCDE(Y)
     A                                      HIGHLIGHT
     A                                   155'Side'
     A                                      SPACEB(1)
     A                                   159PAGNBR EDTCDE(Z)
     A                                      HIGHLIGHT
     A                                     1MSGCON(132 JOV0900 SYMSGF)
     A                                      SPACEB(3)
     A                                      HIGHLIGHT
     A                                     1MSGCON(132 JOV0901 SYMSGF)
     A                                      SPACEB(2)
     A                                      HIGHLIGHT
     A                                    23MSGCON(020 SHU0552 *LIBL/SYMSGF)
     A                                      SPACEB(2)
     A                                      HIGHLIGHT
     A            WSOPTU         1A       46
     A                                      HIGHLIGHT
     A                                    23MSGCON(020 JOV1355 *LIBL/SYMSGF)
     A                                      SPACEB(1)
     A                                      HIGHLIGHT
     A            SRNAV         10A       46
     A                                      HIGHLIGHT
     A            SBESK         30A       60
     A                                      HIGHLIGHT
     A  55                                23MSGCON(020 JOV1354 *LIBL/SYMSGF)
     A                                      SPACEB(1)
     A                                      HIGHLIGHT
     A  55        WSGLKJ         1A       46
     A                                      HIGHLIGHT
     A                                    23MSGCON(020 JOV1067 *LIBL/SYMSGF)
     A                                      SPACEB(2)
     A                                      HIGHLIGHT
     A            WSFALI         1A       46
     A                                      HIGHLIGHT
     A                                    23MSGCON(020 SHU0547 *LIBL/SYMSGF)
     A                                      SPACEB(1)
     A                                      HIGHLIGHT
     A            WSUFLI         1A       46
     A                                      HIGHLIGHT
     A                                    23MSGCON(020 SHU0554 *LIBL/SYMSGF)
     A                                      SPACEB(1)
     A                                      HIGHLIGHT
     A            WSFKOS         1A       46
     A                                      HIGHLIGHT
     A                                    23MSGCON(020 SHU0555 *LIBL/SYMSGF)
     A                                      SPACEB(1)
     A                                      HIGHLIGHT
     A            WSBKOS         1A       46
     A                                      HIGHLIGHT
     A                                    23MSGCON(020 SHU0558 *LIBL/SYMSGF)
     A                                      SPACEB(1)
     A                                      HIGHLIGHT
     A            WSILIN         1A       46
     A                                      HIGHLIGHT
     A                                    23MSGCON(020 SHU0557 *LIBL/SYMSGF)
     A                                      SPACEB(1)
     A                                      HIGHLIGHT
     A            WSAVRS         1A       46
     A                                      HIGHLIGHT
     A                                    23MSGCON(020 JOV1065 *LIBL/SYMSGF)
     A                                      SPACEB(1)
     A                                      HIGHLIGHT
     A            WSAVSL         1A       46
     A                                      HIGHLIGHT
     A                                    56MSGCON(040 JOV1066 *LIBL/SYMSGF)
     A                                      HIGHLIGHT
     A                                    23MSGCON(020 JOV1353 *LIBL/SYMSGF)
     A                                      SPACEB(1)
     A                                      HIGHLIGHT
     A            WSOTYP         2A       46
     A                                      HIGHLIGHT
     A                                    23MSGCON(020 SHU0548 *LIBL/SYMSGF)
     A                                      SPACEB(2)
     A                                      HIGHLIGHT
     A            WSREGT         7S 0     46EDTCDE(M)
     A                                      HIGHLIGHT
     A                                    56MSGCON(020 JOV0896 *LIBL/SYMSGF)
     A                                      HIGHLIGHT
     A            WSDBGT         5  2     77EDTCDE(M)
     A                                      HIGHLIGHT
     A                                    85'Or/And.:'
     A                                      HIGHLIGHT
     A            WSORAN         1A       94
     A                                      HIGHLIGHT
     A                                    23MSGCON(020 SHU0549 *LIBL/SYMSGF)
     A                                      SPACEB(1)
     A                                      HIGHLIGHT
     A            WSRELT         7S 0     46EDTCDE(M)
     A                                      HIGHLIGHT
     A                                    56MSGCON(020 JOV0897 *LIBL/SYMSGF)
     A                                      HIGHLIGHT
     A            WSDBLT         5  2     77EDTCDE(M)
     A                                      HIGHLIGHT
     A                                    23MSGCON(020 SHU0550 *LIBL/SYMSGF)
     A                                      SPACEB(2)
     A                                      HIGHLIGHT
     A N54        WSPERF         6  0     46EDTWRD('    .  ')
     A                                      HIGHLIGHT
     A  54        FRADAT         8  0     46EDTWRD('    .  .  ')
     A                                      HIGHLIGHT
     A N54        WSPERT         6  0     57EDTWRD('    .  ')
     A                                      HIGHLIGHT
     A  54        TILDAT         8  0     57EDTWRD('    .  .  ')
     A                                      HIGHLIGHT
     A                                    23MSGCON(020 SHU0551 *LIBL/SYMSGF)
     A                                      SPACEB(2)
     A                                      HIGHLIGHT
     A            WSAVDF         4  0     46EDTCDE(Z)
     A                                      HIGHLIGHT
     A            WSAVDT         4  0     53EDTCDE(Z)
     A                                      HIGHLIGHT
     A                                    23MSGCON(020 SHU1155 *LIBL/SYMSGF)
     A                                      SPACEB(1)
     A                                      HIGHLIGHT
     A            WSSG           3        46
     A                                      HIGHLIGHT
     A                                    23MSGCON(020 JOV0899 *LIBL/SYMSGF)
     A                                      SPACEB(1)
     A                                      HIGHLIGHT
     A            WSAVDG         7        46HIGHLIGHT
     A                                    23MSGCON(020 SHU0556 *LIBL/SYMSGF)
     A                                      SPACEB(2)
     A                                      HIGHLIGHT
     A            WSGEIO         1A       46HIGHLIGHT
     A            WSGE01         3A       48HIGHLIGHT
     A            WSGE02         3A       52HIGHLIGHT
     A            WSGE03         3A       56HIGHLIGHT
     A            WSGE04         3A       60HIGHLIGHT
     A            WSGE05         3A       64HIGHLIGHT
     A            WSGE06         3A       68HIGHLIGHT
     A            WSGE07         3A       72HIGHLIGHT
     A            WSGE08         3A       76HIGHLIGHT
     A            WSGE09         3A       80HIGHLIGHT
     A            WSGE10         3A       84HIGHLIGHT
     A            WSGE11         3A       88HIGHLIGHT
     A            WSGE12         3A       92HIGHLIGHT
     A            WSGE13         3A       96HIGHLIGHT
     A                                    23MSGCON(020 JOV0906 *LIBL/SYMSGF)
     A                                      SPACEB(1)
     A                                      HIGHLIGHT
     A            WSSTIO         1A       46HIGHLIGHT
     A            WSSTGR        10A       48HIGHLIGHT
     A                                    23MSGCON(020 JOV1062 *LIBL/SYMSGF)
     A                                      SPACEB(2)
     A                                      HIGHLIGHT
     A            WSBAVD         1A       46HIGHLIGHT
     A                                    56MSGCON(020 JOV1063 *LIBL/SYMSGF)
     A                                      HIGHLIGHT
     A  51                                23MSGCON(100 JOV1054 *LIBL/SYMSGF)
     A                                      SPACEB(3)
     A                                      HIGHLIGHT
     A  51                                23MSGCON(100 JOV1055 *LIBL/SYMSGF)
     A                                      SPACEB(1)
     A                                      HIGHLIGHT
     A  51                                23MSGCON(100 JOV1056 *LIBL/SYMSGF)
     A                                      SPACEB(1)
     A                                      HIGHLIGHT
     A  51                                23MSGCON(100 JOV1230 *LIBL/SYMSGF)
     A                                      SPACEB(1)
     A                                      HIGHLIGHT
     A  51                                23MSGCON(100 JOV1060 *LIBL/SYMSGF)
     A                                      SPACEB(1)
     A                                      HIGHLIGHT
     A          R HEAD0D                    SKIPB(1)
     A            KFMT1         75         1
     A            KFMT2         75        76
     A* PCL-5 KODE FOR Å ROTERE 90 GRADER..
     A  89                               151'¤&l1O                             '
     A  89                               160'¤(12U¤(s0p19h0s3b4099T'
     A                                     1MSGCON(132 JOV0900 SYMSGF)
     A                                      SPACEB(1)
     A                                      HIGHLIGHT
     A                                   155DATE EDTCDE(Y)
     A                                      HIGHLIGHT
     A                                   164TIME EDTCDE(Y)
     A                                      HIGHLIGHT
     A                                   155'Side'
     A                                      SPACEB(1)
     A                                   159PAGNBR EDTCDE(Z)
     A                                      HIGHLIGHT
     A                                     1MSGCON(132 JOV1059 SYMSGF)
     A                                      SPACEB(1)
     A                                      HIGHLIGHT
     A  89                                 1'¤*c4000A¤*c100B¤*c11G¤*c2P'
     A                                      SPACEA(1)
     A N56                                 1MSGCON(132 JOV1057 SYMSGF)
     A                                      SPACEB(1)
     A                                      HIGHLIGHT
     A  56                                 1MSGCON(132 JOV1352 SYMSGF)
     A                                      SPACEB(1)
     A                                      HIGHLIGHT
     A                                   133MSGCON(64 JOV1058 SYMSGF)
     A                                      HIGHLIGHT
     A                                     1MSGCON(132 PRI9999 SYMSGF)
     A                                      HIGHLIGHT
     A                                      SPACEB(1)
     A                                   133MSGCON(64 JOV1010 SYMSGF)
     A                                      HIGHLIGHT
     A          R DETO                      SPACEB(1)
     A N55        WAVD           4  0      1EDTCDE(Z)
     A* 50                                  HIGHLIGHT
     A N55        WOPD           7  0      6EDTCDE(Z)
     A* 50                                  HIGHLIGHT
     A  56        WTURHK         8  0      1EDTCDE(Z)
     A                                      HIGHLIGHT
     A*           WSG            3        12
     A            WTUR           8  0     16EDTCDE(Z)
     A N50                                  HIGHLIGHT
     A            WTANT          4S 0     25EDTCDE(Z)
     A            WPER           4S 0     30
     A            YFALI         11S 2     35EDTCDE(M)
     A            YUFLI         11S 2     +1EDTCDE(M)
     A            YIILIN        11S 2     +1EDTCDE(M)
     A            YOMS          11S 2     +1EDTCDE(M)
     A                                      HIGHLIGHT
     A            YFKOS         11S 2     +1EDTCDE(M)
     A            YBKOS         11S 2     +1EDTCDE(M)
     A            WBÅ            1        +0
     A            YBTKOS        11S 2     +1EDTCDE(M)
     A            WBP            1        +0
     A            WBV            1        +0
     A            YKILIN        11S 2     +1EDTCDE(M)
     A            YKOS          11S 2     +1EDTCDE(M)
     A                                      HIGHLIGHT
     A            YRES          11S 2     +1EDTCDE(M)
     A                                      HIGHLIGHT
     A            WDB1           5S 2     +1EDTCDE(M)
     A                                      HIGHLIGHT
      * STREK-LINJE
     A          R DETB                      SPACEB(1)
     A                                     1MSGCON(132 PRI9999 SYMSGF)
     A                                      HIGHLIGHT
     A                                   133MSGCON(64 JOV1010 SYMSGF)
     A                                      HIGHLIGHT
      *
     A          R TOT0                      SPACEB(2)
     A N55                                16MSGCON(50 JOV1061 SYMSGF)
     A  55                                16MSGCON(50 JOV1229 SYMSGF)
     A                                      HIGHLIGHT
     A                                      SPACEA(2)
     A          R TOT                       SPACEB(1)
      * TAR MANUELL KONTR OVER TELLING AV START-POS TIL OG MED XFALI
     A  52                                 1'*TOT*'
     A N52        ZAVD           4  0      1EDTCDE(Z)
     A  50                                  HIGHLIGHT
     A            ZOPD           7  0      6EDTCDE(Z)
     A  50                                  HIGHLIGHT
     A  55                                 6'*TOT*'
     A                                      HIGHLIGHT
     A* 55        GMSG           3        12HIGHLIGHT
     A            ZANT           5  0     16EDTCDE(Z)
     A N50                                  HIGHLIGHT
     A            ZTANT          7S 0     22EDTCDE(Z)
     A            UFALI         11S 2     35EDTCDE(M)
     A            UUFLI         11S 2     +1EDTCDE(M)
     A            UIILIN        11S 2     +1EDTCDE(M)
     A            UOMS          11S 2     +1EDTCDE(M)
     A                                      HIGHLIGHT
     A            UFKOS         11S 2     +1EDTCDE(M)
     A            UBKOS         11S 2     +1EDTCDE(M)
     A            ZBÅ            1        +0
     A            UBTKOS        11S 2     +1EDTCDE(M)
     A            ZBP            1        +0
     A            ZBV            1        +0
     A            UKILIN        11S 2     +1EDTCDE(M)
     A            UKOS          11S 2     +1EDTCDE(M)
     A                                      HIGHLIGHT
     A            URES          11S 2     +1EDTCDE(M)
     A                                      HIGHLIGHT
     A            ZDB1           5S 2     +1EDTCDE(M)
     A                                      HIGHLIGHT
     A                                      SPACEA(1)
