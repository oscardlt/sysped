     A                                      DSPSIZ(27 132 *DS4 24 80 *DS3)
     A                                      PRINT
     A                                      ALTHELP
     A                                      HELP
      *
     A          R SVX040DA                  SFL
     A            THAVD          4S 0H
     A            THSG           3A  H
     A            ZOP            1A  I  9  2VALUES(' ' '4' '5' 'S')
     A            THTDN          7Y 0O  9  5EDTCDE(4)
     A            THTUID        14A  H
     A            THTUID_2      10A  O  9 13
     A            THDT           8Y 0O  9 24EDTCDE(4)
     A            THMF           3A  O  9 33
     A            THNAS         30A  O  9 37
      *
     A          R SVX040DB                  SFLCTL(SVX040DA)
     A                                      INVITE
     A                                      FRCDTA
     A                                      SFLSIZ(0014)
     A                                      SFLPAG(0013)
     A  91N93N94                            ROLLUP(25 'Rollup')
     A  91N93N94                            ROLLDOWN(26 'Rolldown')
     A                                      CF03(03 'Gå ut')
     A                                      CF04(04 'Sök')
     A                                      CF05(05 'Refresh')
     A                                      CF06(06 'NY')
     A                                      CF12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/SVX040R'
     A                                      COLOR(BLU)
     A                                  1 16'DA'
     A                                      COLOR(BLU)
     A                                  1 53'Underhåll av NCTS eksport'
     A                                      COLOR(WHT)
     A                                  1113DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  1124TIME
     A                                      COLOR(BLU)
     A                                  2 56'Godkänn och signera'
     A                                      COLOR(WHT)
     A            ZRECNO         4S 0O  2 80SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  3  2'Avdelning/signatur:'
     A            WSAVD          4Y 0O  3 22EDTCDE(4)
     A            WSSG           3A  O  3 27
     A                                  5  2'Välj option eller tryck Enter.'
     A                                      COLOR(BLU)
     A                                  6  4'4=Återkall   5=Visa edifact   S=Si-
     A                                      gnera'
     A                                      COLOR(BLU)
     A                                  8  5'Uppdrag'
     A                                      COLOR(WHT)
     A                                  8 13'Tullid'
     A                                      COLOR(WHT)
     A                                  8 24'Datum'
     A                                      COLOR(WHT)
     A                                  8 33'Typ'
     A                                      COLOR(WHT)
     A                                  8 37'Avsändare'
     A                                      COLOR(WHT)
      *
     A          R SVX040DC
     A                                      OVERLAY
     A                                 26  2'F3=Avsluta'
     A                                      COLOR(BLU)
     A                                 26 15'F5=Uppdatera'
     A                                      COLOR(BLU)
     A                                 26 30'F12=Avbryt'
     A                                      COLOR(BLU)
      *
     A          R SVX040DE
     A                                  1  2'SYSPED/SVI010'
     A                                      COLOR(BLU)
     A                                  1 16'DE'
     A                                      COLOR(BLU)
     A                                  1 51'Signering av Ärenden - Tidsavbrott'
     A                                      COLOR(WHT)
     A                                  1115DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  1124TIME
     A                                      COLOR(BLU)
     A                                  2 65'Import'
     A                                      COLOR(WHT)
     A                                  4  3'Pogramet avslutas på grund av tids-
     A                                      avbrott. Det har gått 30 minuter se-
     A                                      dan ni sist var aktiv i detta progr-
     A                                      am.'
     A                                  5  4'Tryck ENTER för att avsluta, välj -
     A                                      menypunkt för signering igen.'
      *
     A          R SVX040DØ
     A                                      OVERLAY
     A            ZMSGNO         7A  P
     A            ZMSGTX        79   O 27  2MSGID(&ZMSGNO SYMSGF)
     A                                      COLOR(WHT)
