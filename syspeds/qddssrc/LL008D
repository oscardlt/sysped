********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
P7XXX * 01 YBC PTF7 NYTT FELT FOR OMREGNINGSFAKTOR (I.F.T. EURO-PALL)
********************************************************************
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HELP
     A                                      ALTHELP(CA01)
      * "NOERASE" MÅ HA FOR Å IKKE TØMME BILDE SOM MAN KOMME FRA
     A          R NOERASE
     A                                      ASSUME
     A                                  1  2' '
      * MAN KUN DEFINNERE VINDU PÅ REKORDNIVÅ
     A          R WINDOWSZ
     A                                      WINDOW(*DFT 10 66 *NOMSGLIN)
     A                                      OVERLAY
      *
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  4  2VALUES(' ' '1')
     A            TKLLT          3A  O  4  5
     A            TKTXT         50A  O  4 11
N01  A            TKOMRF         3  2O  4 62EDTCDE(4)
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A                                      SFLSIZ(0007)
     A                                      SFLPAG(0006)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A                                      CA03(03 'Forrige')
     A                                      CA05(05 'Frisk')
     A                                      CA06(06 'LAGE NY')
     A                                      CA12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            ZRECNO         4S 0O  2 28SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'LL008R'
     A                                      COLOR(BLU)
     A                                  1 10MSGCON(40 YBC0966 SYMSGF)
     A                                      COLOR(WHT)
     A                                  2  2MSGCON(40 YBC0967 SYMSGF)
     A                                      COLOR(BLU)
     A                                  3  2MSGCON(64 YBC0968 SYMSGF)
     A                                      COLOR(WHT)
      *
     A          R BUNN
     A                                      WINDOW(WINDOWSZ)
     A                                      SLNO(10)
     A                                      CLRL(01)
     A                                      OVERLAY
     A N30                              1  2MSGCON(45 YBC0969 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        43A  O  1  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R NYWRK
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A                                      CA03(03 'Forrige')
     A                                      CA05(05 'Frisk')
     A                                      CA06(06 'LAGE NY')
     A                                      CA12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'LL008R'
     A                                      COLOR(BLU)
     A                                  1 10MSGCON(40 YBC0966 SYMSGF)
     A                                      COLOR(WHT)
     A                                  2  2MSGCON(40 YBC0967 SYMSGF)
     A                                      COLOR(BLU)
     A                                  3  2MSGCON(64 YBC0968 SYMSGF)
     A                                      COLOR(WHT)
     A                                 10  2MSGCON(45 YBC0969 SYMSGF)
     A                                      COLOR(BLU)
