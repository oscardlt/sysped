     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPDOC('STD' 'HELPNU' 'HLP400')
     A                                      HELP
     A          R BILDE01
     A                                      CF03(03 'Avslutt program')
     A                                  1  2'SYSPED/SYTR40A'
     A                                      COLOR(BLU)
     A                                  1 21MSGCON(040 DIV4095 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 62DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  1 73TIME
     A                                      COLOR(BLU)
     A                                  7  2MSGCON(024 DIV1497 *LIBL/SYMSGF)
     A            WSVAVD         4Y 0B  7 28DSPATR(UL)
     A                                      EDTCDE(4)
     A  99                                  DSPATR(RI)
     A N50                                  DSPATR(PR)
     A  50                                  DSPATR(PC)
     A                                  9  2MSGCON(024 DIV4094 *LIBL/SYMSGF)
     A            WSSFSI         3A  B  9 28DSPATR(UL)
     A  98                                  DSPATR(RI)
     A  98                                  DSPATR(PC)
     A N50                                  DSPATR(PR)
     A                                  9 32MSGCON(010 DIV1513 *LIBL/SYMSGF)
     A            WSDATO         6Y 0B  9 44EDTCDE(4)
     A                                  9 52MSGCON(010 DIV1518 *LIBL/SYMSGF)
     A                                 11  2MSGCON(024 DIV4057 *LIBL/SYMSGF)
     A            WSTUR          8Y 0B 11 28EDTCDE(4)
     A  97                                  DSPATR(RI)
     A N50                                  DSPATR(PR)
     A  97                                  DSPATR(PC)
     A                                 11 44MSGCON(010 DIV1518 *LIBL/SYMSGF)
     A                                 13  2MSGCON(024 DIV4093 *LIBL/SYMSGF)
     A            WSTY           1A  B 13 28
     A  96                                  DSPATR(RI)
     A  96                                  DSPATR(PC)
     A                                 13 32MSGCON(045 JOV0664 *LIBL/SYMSGF)
     A                                 17  2MSGCON(024 DIV4058 *LIBL/SYMSGF)
     A  50                                  DSPATR(ND)
     A            WSPRT         10A  B 17 28
     A  50                                  DSPATR(PR)
     A N50                                  DSPATR(PC)
     A  50                                  DSPATR(ND)
     A                                 19  2MSGCON(024 DIV4059 *LIBL/SYMSGF)
     A  50                                  DSPATR(ND)
     A            WSFORM        10A  B 19 28
     A  50                                  DSPATR(PR)
     A  50                                  DSPATR(ND)
     A                                 21  2MSGCON(024 DIV4060 *LIBL/SYMSGF)
     A  50                                  DSPATR(ND)
     A            WSJOBQ         1A  B 21 28
     A  50                                  DSPATR(PR)
     A  50                                  DSPATR(ND)
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
