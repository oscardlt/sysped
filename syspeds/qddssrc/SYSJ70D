     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      REF(*LIBL/ZILLOF)
     A                                      PRINT
     A                                      ALTHELP(CA01)
     A                                      CF03(03 'Exit')
     A                                      CF04(04 'Prompt')
     A                                      CF12(12 'Cancel')
     A                                      HELP
     A                                      CF05(05 'Refresh')
     A          R SYSJ70DA                  SFL
     A            BLUNIK    R        H
     A            ZOP            1A  I  7  2VALUES(' ' '1' '2' '4' '5' '6' 'X')
     A            ZHAVN         10A  O  7  4
     A            ZBAAT         10A  O  7 15
     A            ZREDERI        8A  O  7 26
     A            ZCONT         15A  O  7 35
     A            ZLBLNR        12A  O  7 51
     A            ZLAD2G         5A  O  7 64
     A            ZLDTA          6A  O  7 74
     A          R SYSJ70DB                  SFLCTL(SYSJ70DA)
     A                                      SFLSIZ(0014)
     A                                      SFLPAG(0013)
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND(*MORE)
     A                                  1  2'SYSJ70D'
     A                                      COLOR(BLU)
     A                                  1 24'Innkommende Bill of Lading'
     A                                      COLOR(WHT)
     A                                  1 63DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  1 73TIME
     A                                      COLOR(BLU)
     A            ZRECNO         4S 0O  2 75SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  3  2'Velg og trykk Enter.'
     A                                      COLOR(BLU)
     A                                  4  4'1=Plukk   2=Endre felles   4=Slett'
     A                                      COLOR(BLU)
     A                                  4 41'5=Vise'
     A                                      COLOR(BLU)
     A                                  4 61'X=Slå sammen 2 B/L'
     A                                      COLOR(BLU)
     A                                  5 10'Ref.'
     A            XREFF         17A  I  5 15DSPATR(UL)
     A            XCONT         15A  I  5 35DSPATR(UL)
     A            XLBLNR        12A  I  5 51DSPATR(UL)
     A                                  5 74'Ank.'
     A                                      COLOR(WHT)
     A                                  6  2'A'
     A                                      COLOR(WHT)
     A                                  6  4'Havn'
     A                                      COLOR(WHT)
     A                                  6 15'Båt'
     A                                      COLOR(WHT)
     A                                  6 26'Rederi'
     A                                      COLOR(WHT)
     A                                  6 35'Container'
     A                                      COLOR(WHT)
     A                                  6 51'B/L'
     A                                      COLOR(WHT)
     A                                  6 64'Antall'
     A                                      COLOR(WHT)
     A                                  6 74'Dato'
     A                                      COLOR(WHT)
     A                                  4 50'6=Skriv ut'
     A                                      COLOR(BLU)
     A          R SYSJ70DC
     A                                      OVERLAY
     A                                 22  2MSGCON(010 EDI0803 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 22 15MSGCON(010 EDI0805 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 22 26MSGCON(010 EDI0812 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A          R SYSJ70DD
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSJ70D'
     A                                      COLOR(BLU)
     A                                  1 24'Innkommende Bill of Lading'
     A                                      COLOR(WHT)
     A                                  1 63DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  1 73TIME
     A                                      COLOR(BLU)
     A                                  2 27'Fellesopplysninger'
     A                                      COLOR(WHT)
     A            ZRECNO         4S 0O  2 75DSPATR(ND)
     A                                  3  2'Velg og trykk Enter.'
     A                                      COLOR(BLU)
     A                                  5  2'År . . . . . . . . . . . :'
     A            ZLAAR          2S 0B  5 29
     A                                  6  4'Uke  . . . . . . . . . :'
     A            ZLUKE          2S 0B  6 29
     A                                  8  2'Avdeling . . . . . . . . :'
     A            ZLAVD          4S 0B  8 29
     A                                  9  4'Signatur . . . . . . . :'
     A            ZLSG           3A  B  9 29
     A                                 11  2'Båt  . . . . . . . . . . :'
     A            ZLOB           5A  B 11 29
     A                                 12  2'Rederi . . . . . . . . . :'
     A            ZLKNR          8S 0B 12 29
     A                                 13  2'TR . . . . . . . . . . . :'
     A            ZLTR           5A  B 13 29
     A                                 14  2'Lsst . . . . . . . . . . :'
     A            ZLLSS          5A  B 14 29
     A                                 15  2'Lasth  . . . . . . . . . :'
     A            ZLLSH          5A  B 15 29
     A                                 16  2'Viah . . . . . . . . . . :'
     A            ZLVH           5A  B 16 29
     A                                 17  2'Lossh  . . . . . . . . . :'
     A            ZLLOH          5A  B 17 29
     A                                 22  2MSGCON(010 EDI0803 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 22 15'F4=Søk'
     A                                      COLOR(BLU)
     A                                 22 24MSGCON(010 EDI0805 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 22 35MSGCON(010 EDI0812 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A          R SYSJ70DE
     A                                  1  2'SYSJ70D'
     A                                      COLOR(BLU)
     A                                  1 24'Innkommende Bill of Lading'
     A                                      COLOR(WHT)
     A                                  1 63DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  1 73TIME
     A                                      COLOR(BLU)
     A                                  5 10'Ref.'
     A            XREFF         17A  I  5 15DSPATR(UL)
     A            XCONT         15A  I  5 35DSPATR(UL)
     A            XLBLNR        12A  I  5 51DSPATR(UL)
     A                                  5 74'Ank.'
     A                                      COLOR(WHT)
     A                                  6  2'A'
     A                                      COLOR(WHT)
     A                                  6  4'Havn'
     A                                      COLOR(WHT)
     A                                  6 15'Båt'
     A                                      COLOR(WHT)
     A                                  6 26'Rederi'
     A                                      COLOR(WHT)
     A                                  6 35'Container'
     A                                      COLOR(WHT)
     A                                  6 51'B/L'
     A                                      COLOR(WHT)
     A                                  6 64'Antall'
     A                                      COLOR(WHT)
     A                                  6 74'Dato'
     A                                      COLOR(WHT)
     A                                  8 10'Ingen inngående BL finnes.'
     A                                      COLOR(RED)
     A          R SYSJ70DØ
     A                                      OVERLAY
     A            ZMSGNO         7A  P
     A            ZMSGTX        79A  O 23  2MSGID(&ZMSGNO SYMSGF)
     A N30                                  COLOR(WHT)
     A  30                                  COLOR(RED)
