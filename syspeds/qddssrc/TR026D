     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HELP
     A                                      ALTHELP
      * "NOERASE" MÅ HA FOR Å IKKE TØMME BILDE SOM MAN KOMME FRA
     A          R NOERASE
     A                                      ASSUME
     A                                  1  2' '
      * MAN KUN DEFINNERE VINDU PÅ REKORDNIVÅ
     A          R WINDOWSZ
     A                                      WINDOW(*DFT 10 50 *NOMSGLIN)
     A                                      OVERLAY
      *
     A          R SUBFIL                    SFL
     A            WSNR           1A  B  4  2VALUES(' ' '2' '4')
     A  28                                  DSPATR(PR)
     A            WSBL          11  0O  4  5EDTCDE(4)
     A            WSVAL          3A  O  4 18
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      WINDOW(WINDOWSZ)
     A                                      SFLSIZ(0006)
     A                                      SFLPAG(0005)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A                                      CA03(03 'Avslutt')
     A                                      CA06(06 'Lage')
     A                                      CA12(12 'Forrige')
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            ZRECNO         4S 0O  1 45SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYSPED/TR026R'
     A                                      COLOR(BLU)
     A                                  1 17'Beregn total vareverdi'
     A                                      COLOR(WHT)
     A                                  2  2'Velg opsjon:  2=Endre  4=Slett'
     A                                      COLOR(BLU)
     A                                  3  2'Op    Vareverdi Valuta'
     A                                      COLOR(WHT)
      *
     A          R BUNN
     A                                      WINDOW(WINDOWSZ)
     A*                                     SLNO(09)
     A*                                     CLRL(02)
     A                                      OVERLAY
     A N30                             10  2'F3=Avslutt  F6=Lag ny  F12=Avbryt'
     A                                      COLOR(BLU)
     A  30        MSGTXT        40A  O 10  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R VARELI
     A                                      WINDOW(WINDOWSZ)
     A                                      SLNO(09)
     A                                      CLRL(02)
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF04(04 'Søk')
     A                                      CA12(12 'Forrige')
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'Vareverdi:'
     A            W2BL          11  0B  1 13DSPATR(UL)
     A  90                                  DSPATR(RI)
     A  90                                  DSPATR(PC)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  1 27'Valuta:'
     A            W2VAL          3A  B  1 35DSPATR(UL)
     A  89                                  DSPATR(RI)
     A  89                                  DSPATR(PC)
     A                                      COLOR(YLW)
     A N30                              2  2'F4=Søk F12=Avbryt'
     A                                      COLOR(BLU)
     A  30        MSGTXT        48A  O  2  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R NYWRK
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A                                      CA03(03 'Avslutt')
     A                                      CA06(06 'Lage')
     A                                      CA12(12 'Forrige')
     A                                  1  2'SYSPED/TR026R'
     A                                      COLOR(BLU)
     A                                  1 17'Beregn total vareverdi'
     A                                      COLOR(WHT)
     A                                  2  2'Velg opsjon:  2=Endre  4=Slett'
     A                                      COLOR(BLU)
     A                                  3  2'Op    Vareverdi Valuta'
     A                                      COLOR(WHT)
     A N30                             10  2'F3=Avslutt  F6=Lag ny  F12=Avbryt'
     A                                      COLOR(BLU)
     A  30        MSGTXT        48A  O 10  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
