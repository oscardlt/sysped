     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPDOC('VEDLSTED' 'HELPNU' 'HLP400')
     A                                      HELP
     A                                      ALTHELP
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  5  3VALUES(' ' '2' '4')
     A            SFOTF          2A  O  5 10TEXT('FRA OPPDRAGSTYPE')
     A            SFOT           2A  O  5 21TEXT('TIL OPPDRAGSTYPE')
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0016)
     A                                      SFLPAG(0015)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A            ZRECNO         4S 0O  2 64SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYSPED/SYFA97'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(030 DIV2485 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                  2 70TIME
     A            W1KNR          8Y 0O  2  5EDTCDE(4)
     A                                      COLOR(WHT)
     A            W1NAVN        30A  O  2 15COLOR(WHT)
     A                                  3  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 34MSGCON(030 DIV0017 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(078 DIV2484 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A          R BUNN
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF03(03 'AVSLUTT')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                      CF12(12 'FORIGE BILDE')
     A                                      SLNO(23)
     A                                      CLRL(02)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2MSGCON(010 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  1 15MSGCON(010 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  1 30MSGCON(010 DIV0022 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  1 45MSGCON(010 DIV0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  1 60MSGCON(012 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  2  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                              2 40'(C) COPYRIGHT  SYSTEMA A/S  1992  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A          R VARELI
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2MSGCON(075 DIV2484 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            WSOTF          2A  B  2 10TEXT('FRA OPPDRAGSTYPE')
     A                                      DSPATR(UL)
     A  90                                  DSPATR(RI)
     A            WSOT           2A  B  2 21TEXT('TIL OPPDRAGSTYPE')
     A                                      DSPATR(UL)
     A  89                                  DSPATR(RI)
     A                                  3  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  4  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                              4 40'(C) COPYRIGHT  SYSTEMA A/S  1992  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A          R VAREL2
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1 12MSGCON(060 DIV2484 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  2  2MSGCON(015 DIV1147 *LIBL/SYMSGF)
     A            WSOTF          2A  B  2 20TEXT('FRA OPPDRAGSTYPE')
     A                                      DSPATR(UL)
     A            WSOT           2A  B  2 31TEXT('TIL OPPDRAGSTYPE')
     A                                      DSPATR(UL)
     A                                  3  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  4  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                              4 40'(C) COPYRIGHT  SYSTEMA A/S  1992  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A          R VAREL3
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A                                  1  6MSGCON(075 DIV2484 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A            SFOTF          2A  O  2 13TEXT('FRA OPPDRAGSTYPE')
     A                                      DSPATR(HI)
     A                                      DSPATR(PC)
     A            SFOT           2A  O  2 24TEXT('TIL OPPDRAGSTYPE')
     A                                      DSPATR(HI)
     A                                  3  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 40'(C) COPYRIGHT  SYSTEMA A/S  1992  '
     A                                      COLOR(WHT)
     A          R NYKUNDE
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF03(03 'AVSLUTT')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                      CF12(12 'FORIGE BILDE')
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1 19MSGCON(030 DIV2485 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                  1  2'SYSPED/SYFA97'
     A                                      COLOR(BLU)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                  2 70TIME
     A            W1KNR          8Y 0O  2  5EDTCDE(4)
     A                                      COLOR(WHT)
     A            W1NAVN        30A  O  2 15COLOR(WHT)
     A                                  3  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 34MSGCON(030 DIV0017 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(078 DIV2484 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 42MSGCON(018 DIV0022 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 62MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                             24 40'(C) COPYRIGHT  SYSTEMA A/S  1992  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A          R BILDE1
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF03(03 'EXIT')
     A                                      CF04(04 'Søk kunde')
     A                                      CF12(03)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/SYFA97'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(030 DIV2485 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                  2 70TIME
     A                                 11  2MSGCON(012 DIV0199 *LIBL/SYMSGF)
     A            W1KNR          8Y 0B 11 17DSPATR(UL)
     A                                      EDTCDE(4)
     A  70                                  DSPATR(RI)
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(018 DIV0032 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
