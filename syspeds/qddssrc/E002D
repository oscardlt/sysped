     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      REF(*LIBL/EQTA902)
     A                                      PRINT
     A                                      CF03(03 'Gå ut')
     A                                      CF12(12 'Forrige')
      *
     A          R E002DA
     A                                      CF04(04)
     A                                      OVERLAY
     A                                  1  2'EDI'
     A                                      COLOR(BLU)
     A                                  1 31MSGCON(021 EDI0336 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 76'E002D'
     A                                  3  2MSGCON(026 EDI0602 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  2MSGCON(034 EDI0338 *LIBL/SYMSGF)
     A            XTAGNO         4A  I  5 39DFT('*ALL')
     A                                  5 52MSGCON(021 EDI0608 *LIBL/SYMSGF)
     A                                  6  4MSGCON(032 EDI0339 *LIBL/SYMSGF)
     A            XTAGTX        10A  I  6 39DFT('*ALL')
     A                                      CHECK(LC)
     A                                  6 52MSGCON(021 EDI0608 *LIBL/SYMSGF)
     A                                 22  3MSGCON(010 EDI0803 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 22 16MSGCON(014 EDI0804 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
      *
     A          R E002DB                    SFL
     A            ZOP            1A  I  9  3VALUES(' ' '1' '2' '3' '4' '5')
     A  41                                  DSPATR(ND)
     A                                      DSPATR(PC)
     A            QTAGNO    R        O  9  6REFFLD(QTAGNO)
     A            QTAGTX    R        O  9 11REFFLD(QTAGTX)
      *
     A          R E002DC                    SFLCTL(E002DB)
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0012)
     A  91N93N94                            ROLLUP(25 'Rollup')
     A  91N93N94                            ROLLDOWN(26 'Rolldown')
     A                                      CF05(05 'Refresh')
     A                                      CF06(06 'Create')
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A                                  1  2'EDI'
     A                                      COLOR(BLU)
     A                                  1 30MSGCON(021 EDI0336 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            ZRECNO         4S 0O  1 60SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1 76'E002D'
     A                                  3 49MSGCON(019 EDI0344 *LIBL/SYMSGF)
     A            XTAGNO         4A  I  3 71DFT('*ALL')
     A                                  4 49MSGCON(019 EDI0345 *LIBL/SYMSGF)
     A            XTAGTX        10A  I  4 71DFT('*ALL')
     A                                      CHECK(LC)
     A                                  5  2MSGCON(026 EDI0602 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  6  4MSGCON(008 EDI0701 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  6 15MSGCON(008 EDI0702 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  6 26MSGCON(008 EDI0703 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  6 37MSGCON(008 EDI0704 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  6 48MSGCON(009 EDI0705 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  8  2MSGCON(003 EDI0609 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  8  6MSGCON(004 EDI0349 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  8 11MSGCON(005 EDI0327 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
      *
     A          R E002DD
     A                                      OVERLAY
     A                                 22  2MSGCON(010 EDI0803 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 22 15MSGCON(010 EDI0805 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 22 28MSGCON(010 EDI0806 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 22 41MSGCON(010 EDI0812 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
      *
     A          R E002DE
     A                                      OVERLAY
     A                                  1  2'EDI'
     A                                      COLOR(BLU)
     A                                  1 31MSGCON(021 EDI0336 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 76'E002D'
     A                                  3  2MSGCON(026 EDI0602 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  2MSGCON(034 EDI0353 *LIBL/SYMSGF)
     A            ÅTAGNO         4A  B  5 39
     A  61                                  DSPATR(PR)
     A  31                                  COLOR(RED)
     A                                  6  4MSGCON(032 EDI0333 *LIBL/SYMSGF)
     A            ÅTAGTX        62A  B  7  4CHECK(LC)
     A  62                                  DSPATR(PR)
     A                                  9  4MSGCON(032 EDI0334 *LIBL/SYMSGF)
     A            ÅTAGD1        62A  B 10  4CHECK(LC)
     A  62                                  DSPATR(PR)
     A            ÅTAGD2        62A  B 11  4CHECK(LC)
     A  62                                  DSPATR(PR)
     A            ÅTAGD3        62A  B 12  4CHECK(LC)
     A  62                                  DSPATR(PR)
     A            ÅTAGD4        62A  B 13  4CHECK(LC)
     A  62                                  DSPATR(PR)
     A            ÅTAGD5        62A  B 14  4CHECK(LC)
     A  62                                  DSPATR(PR)
     A                                 16  4MSGCON(032 EDI0356 *LIBL/SYMSGF)
     A            ÅTAGN1        62A  B 17  4CHECK(LC)
     A  62                                  DSPATR(PR)
     A            ÅTAGN2        62A  B 18  4CHECK(LC)
     A  62                                  DSPATR(PR)
     A                                 22  2MSGCON(010 EDI0803 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 22 15MSGCON(010 EDI0812 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
      *
     A          R E002DØ
     A                                      OVERLAY
     A            ZMSGTX        79A  O 23  2MSGID(&ZMSGNO SYMSGF)
     A N30                                  COLOR(WHT)
     A  30                                  COLOR(RED)
     A            ZMSGNO         7A  P
