********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
11XXX * 01 SH  PTF11 BRINGTMS             ...
********************************************************************
      *
      ****************************************************************
      *** PROGRAM SKAL VEDLIKEHOLDE TUR (PLUKKE OPPDRAG)           ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER         ***
      *** LEDIGE INDIKATORER 02 04-07 09-11 13-24                  ***
      *** LEDIGE INDIKATORER          XXXX                         ***
      ****************************************************************
      *
     FOMDI      O  A E           K DISK
     FTELL      IF   E           K DISK
     FFIRM      IF   E           K DISK
     FHESTED    IF   E           K DISK
     FTURER     UF   E           K DISK
     FHEADF     UF   E           K DISK
     FDISP      UF   E           K DISK
     FDISPL05   IF   E           K DISK
     F                                     RENAME(DISPR:DISPR1)
     FDISPL06   IF   E           K DISK
     F                                     RENAME(DISPR:DISPR2)
     FDISPL07   IF   E           K DISK
     F                                     RENAME(DISPR:DISPR3)
     FDISPL08   IF   E           K DISK
     F                                     RENAME(DISPR:DISPR4)
     FKOSBU9    UF   E           K DISK
     FSYTR88FD  CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     D                 DS
     D  HEVKT                  1      9  0
     D  HEVKTW                 5      9  0
     D                 DS
     D  YSONF2                 1      5
     D  AL                     1      5
     D                                     DIM(5)
     D                 DS
     D  YSONT2                 1      5
     D  AL2                    1      5
     D                                     DIM(5)
     D                 DS
     D  HEVS1                  1     25
     D  HEVS10                 1     10
     D                 DS
     D  HENT                   1      7  0
     D  HENT4                  4      7  0
     C*
     C                   EXSR      INIT
     C*
     C     START         TAG
     C     TURERK        CHAIN(N)  TURER                              34
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '000'         *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C   26DISP4K        SETLL     DISPL08
     C   27DISP3K        SETLL     DISPL07
     C   28DISP1K        SETLL     DISPL05
     C   29DISP2K        SETLL     DISPL06
     C     START1        TAG
     C                   EXSR      LESFIL
      *
     C     START2        TAG
      *
     C  N94              DO                                                     1<-B
     C                   DO        4                                             2<-B
     C                   MOVE      *BLANKS       IMCO
     C                   MOVE      *BLANKS       ETTE
     C     TRFA21        IFNE      *BLANKS
     C                   MOVE      'A'           IMCO                            *ADR.gods
     C                   END
     C     TRFA11        IFNE      *BLANKS
     C                   MOVE      'I'           IMCO                            *IMCO gods
     C                   END
     C     TRETTB        IFNE      *ZEROS
     C                   MOVE      'E'           ETTE
     C                   END
     C*
     C* FIKSER DATOFORMAT PÅ BILDET
     C*
     C  N33FIDTFM        IFNE      3
     C                   MOVE      TRSDFD        WRSDFD
     C                   MOVE      TRSDTD        WRSDTD
     C                   CALL      'DATFM4'
     C                   PARM                    FIDTFM
     C                   PARM                    WRSDFD
     C                   PARM                    WRSDTD
     C                   PARM                    DATO0
     C                   PARM                    DATO0
     C                   END
     C                   Z-ADD     HEM3          WSM3SU
     C*
     C* HENTER TUROPPLYSNINGER
     C*
     C     TURK2         CHAIN(N)  TURER                              34
     C   34              GOTO      ST21
     C*
     C* FIKSER DATOFORMAT PÅ BILDET
     C*
     C  N34FIDTFM        IFNE      3
     C                   MOVE      TUDT          TUDAFR
     C                   MOVE      TUDTT         TUDATI
     C                   CALL      'DATFM4'
     C                   PARM                    FIDTFM
     C                   PARM                    TUDAFR
     C                   PARM                    TUDATI
     C                   PARM                    DATO0
     C                   PARM                    DATO0
     C                   END
     C*
     C     HEADFK        CHAIN     HESTED                             33
     C  N33              DO
     C                   MOVEL     HSFROM        HESDF
     C                   MOVEL     HSTO          HESDT
     C                   END
     C                   ADD       1             ZRECNO
     C     TROPD0        COMP      *ZEROS                             5959
     C                   WRITE     SUBFIL
     C     ST21          TAG
     C                   EXSR      LESFIL
     C  N94              END                                                     2<-E
     C                   END                                                    1<-E
     C*
     C* Sist leste post (hvis ikke EOF) er 1. post for neste skjermside
     C* (det leses "en for langt" for at en skal kunne vise '+' i bunn)
     C* Bevar alle subfildata i arbeidsvariable slik at de kan hentes
     C* tilbake uten disk-read ved roll videre (forenkler + sparer tid)
     C*
     C     *IN94         IFEQ      '0'
     C                   EXSR      MOVSUX
     C                   END
     C*
     C                   MOVE      ZRECNO        XRECNO            5 0
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYWRK
     C                   ELSE
     C                   WRITE     SUBCTL
     C     UTVKT         ADD       UTARA         WSBER1
     C                   EXFMT     BUNN
     C                   END
     C*
     C                   SETOFF                                       9830
     C   03              Z-ADD     3             UFLAGG
     C   03              GOTO      SLUTT
     C   12              Z-ADD     9             UFLAGG
     C   12              GOTO      SLUTT
     C*
     C   08              DO
     C                   EXSR      MOVB
     C                   GOTO      START6
     C                   END
     C*
     C*
     C*** BLA VIDERE
     C   25              DO
     C                   EXSR      MOVXSU
     C                   Z-ADD     XRECNO        ZRECNO
     C                   SETOFF                                       25
     C                   GOTO      START2
     C                   END
     C*
     C     ZRECNO        IFNE      0
     C                   EXSR      TESTIN
     C                   END
     C                   GOTO      START3
     C*
     C     START6        TAG
     C                   EXFMT     SØKOPD
     C                   SETOFF                                       9930
     C   03
     COR 12              GOTO      START3
     C                   EXSR      TSTSØK
     C   30              GOTO      START6
     C                   GOTO      START
     C*
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
     C*
     C***********************************************
     C     MOVSUX        BEGSR
     C***********************************************
     C* Post er lest, men skal først senere skrives til subfil.
     C* Flytter de nødvendige subfilbegrep
     C* (KUN HEADF-FELT TRENGS BEVARES-HEADTR CHAINES FØR WRITESUBFIL)
     C* Felt i subfila som er hentet opp FØR SETSUB
     C                   MOVE      HEAVD         XXAVD
     C                   MOVE      HEOPD         XXOPD
     C                   MOVE      HENAS         XXNAS
     C                   MOVE      HENAK         XXNAK
     C                   MOVE      HESDF         XXSDF
     C                   MOVE      HESDT         XXSDT
     C                   MOVE      HENT4         XXNT4
     C                   MOVE      HEVS10        XXVS10
     C                   MOVE      HEVKTW        XXVKTW
     C                   MOVE      HELM          XXLM
N01  C                   MOVE      HELMLA        XXLMLA
N01  C                   MOVE      HEPOEN        XXPOEN
     C                   MOVE      HEOT          XXOT
     C                   MOVE      HEFR          XXFR
     C                   MOVE      HEST          XXST
     C                   MOVE      DISTA1        XXSTA1
     C                   MOVE      TROPD0        XXOPD0
     C                   MOVE      WSM3SU        XXM3SU
     C                   MOVE      WRSDFD        XXSDFD
     C                   MOVE      WRSDTD        XXSDTD
     C                   MOVE      IMCO          XXIMCO
     C                   MOVE      ETTE          XXETTE
     C* HIDDEN
     C                   MOVE      DITUAV        XXTUAV
     C                   ENDSR
     C*
     C***********************************************
     C     MOVXSU        BEGSR
     C***********************************************
     C*
     C                   MOVE      XXAVD         HEAVD
     C                   MOVE      XXOPD         HEOPD
     C                   MOVE      XXNAS         HENAS
     C                   MOVE      XXNAK         HENAK
     C                   MOVE      XXSDF         HESDF
     C                   MOVE      XXSDT         HESDT
     C                   MOVE      XXNT4         HENT4
     C                   MOVE      XXVS10        HEVS10
     C                   MOVE      XXVKTW        HEVKTW
     C                   MOVE      XXLM          HELM
N01  C                   MOVE      XXLMLA        HELMLA
N01  C                   MOVE      XXPOEN        HEPOEN
     C                   MOVE      XXOT          HEOT
     C                   MOVE      XXFR          HEFR
     C                   MOVE      XXST          HEST
     C                   MOVE      XXSTA1        DISTA1
     C                   MOVE      XXOPD0        TROPD0
     C                   MOVE      XXM3SU        WSM3SU
     C                   MOVE      XXSDFD        WRSDFD
     C                   MOVE      XXSDTD        WRSDTD
     C                   MOVE      XXIMCO        IMCO
     C                   MOVE      XXETTE        ETTE
     C* HIDDEN
     C                   MOVE      XXTUAV        DITUAV
     C                   ENDSR
     C*
     C***********************************************
     C     MAX           BEGSR
     C***********************************************
     C                   MOVE      WSSTEF        YSTEF             5
     C                   MOVE      WSSTEF        YSTEF2            5
     C                   MOVE      WSSTET        YSTET             5
     C                   MOVE      WSSTET        YSTET2            5
     C                   MOVE      WSSONF        YSONF             5
     C                   MOVE      WSSONF        YSONF2            5
     C                   MOVE      WSSONT        YSONT             5
     C                   MOVE      WSSONT        YSONT2            5
     C*
     C     YSTEF2        IFEQ      *BLANKS
     C                   MOVE      '99999'       YSTEF2
     C                   END
     C*
     C     YSTET2        IFEQ      *BLANKS
     C                   MOVE      '99999'       YSTET2
     C                   END
     C*
     C                   Z-ADD     5             XN                2 0
     C     YSONF2        IFEQ      *BLANKS
     C                   MOVE      '99999'       YSONF2
     C                   ELSE
     C     STMBIL        TAG
     C     AL(XN)        IFEQ      ' '
     C                   MOVE      '9'           AL(XN)
     C                   SUB       1             XN
     C                   GOTO      STMBIL
     C                   END
     C                   END
     C*
     C                   Z-ADD     5             XN                2 0
     C     YSONT2        IFEQ      *BLANKS
     C                   MOVE      '99999'       YSONT2
     C                   ELSE
     C     STMBI2        TAG
     C     AL2(XN)       IFEQ      ' '
     C                   MOVE      '9'           AL2(XN)
     C                   SUB       1             XN
     C                   GOTO      STMBI2
     C                   END
     C                   END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     TSTSØK        BEGSR
     C***********************************************
     C                   SETOFF                                       99
     C     WSAVD         IFNE      0
     C     WSAVD         CHAIN     TELL                               99
     C   99AVDZER        CHAIN     TELL                               99
     C   99              MOVE      'SPA0009'     MSGNR
     C   99              SETON                                        30
     C   99              GOTO      SLTST
     C                   END
     C*
     C* LEGGER OVER MAKSVERDIER FOR STEDSNAVN
     C*
     C                   EXSR      MAX
     C*
     C* FIXER DATOBEGREP
     C*
     C                   MOVE      WSDATO        LLDAT6            6 0
     C     WSDATO        IFNE      *ZEROS
     C     FIDTFM        ANDNE     3
     C                   CALL      'DATFM5'
     C                   PARM      FIDTFM        FIDTFM
     C                   PARM                    LLDAT6
     C                   END
     C     LLDAT6        IFEQ      0
     C                   MOVE      *ZEROS        LLDATO            8 0
     C                   ELSE
     C     LLDAT6        IFGT      501231
     C     LLDAT6        ADD       19000000      LLDATO
     C                   ELSE
     C     LLDAT6        ADD       20000000      LLDATO
     C                   END
     C                   END
     C*
     C     WSSTEF        IFEQ      *BLANKS
     C     WSSONF        IFEQ      *BLANKS
     C     WSAVD         IFNE      0
     C     WSOPDT        IFEQ      *BLANKS
     C                   MOVEA     '1000'        *IN(26)
     C                   GOTO      SLTST
     C                   END
     C                   END
     C                   END
     C                   END
     C*
     C*
     C     WSSTEF        IFNE      *BLANKS
     C                   MOVEA     '0010'        *IN(26)
     C                   GOTO      SLTST
     C                   END
     C*
     C     WSSONF        IFNE      *BLANKS
     C                   MOVEA     '0001'        *IN(26)
     C                   GOTO      SLTST
     C                   END
     C*
     C     WSOPDT        IFNE      *BLANKS
     C                   MOVEA     '0100'        *IN(26)
     C                   GOTO      SLTST
     C                   END
     C*
     C                   MOVE      'DIV0983'     MSGNR
     C                   SETON                                        9930
     C*
     C     SLTST         ENDSR
     C*
     C***********************************************
     C     MOVB          BEGSR
     C***********************************************
     C                   MOVE      *ZEROS        LLDATO
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     LESFIL        BEGSR
     C***********************************************
     C     STLES         TAG
     C   26K4EQ          READE     DISPL08                                94
     C   27K3EQ          READE     DISPL07                                94
     C   28              READ      DISPL05                                94
     C   29              READ      DISPL06                                94
     C*
     C   94              GOTO      SLXY
     C*
     C* OPPDRAG PÅ EGEN TUR VISES IKKE
     C*
     C     UPRO          IFEQ      DITUR
     C                   GOTO      STLES
     C                   END
     C*
     C* HVIS AVDELING ER UTFYLLT SKAL TESTES PÅ DETTE
     C*
     C     WSAVD         IFNE      *ZEROS
     C     WSAVD         IFNE      DIAVD
     C  N26              GOTO      STLES
     C                   SETON                                        94
     C                   GOTO      SLXY
     C                   END
     C                   END
     C*
     C* HVIS OPPDRAGSTYPE IKKE ER BLANK , TEST PÅ DETTE
     C*
     C     WSOPDT        IFNE      *BLANKS
     C     DIOT          IFNE      WSOPDT
     C                   GOTO      STLES
     C                   END
     C                   END
     C*
     C*
     C* HVIS TILSTED ER PUNCHET SELEKTER KUN DETTE
     C*
     C     WSSTET        IFNE      *BLANKS
     C     DISTET        IFNE      WSSTET
     C                   GOTO      STLES
     C                   END
     C                   END
     C*
     C     WSDATO        IFNE      *ZEROS
     C     DIDATF        ANDLT     LLDATO
     C                   GOTO      STLES
     C                   END
     C*
     C* SØKING VED STED FRA UTFYLLT
     C*
     C   28
     CANN94DISTEF        COMP      YSTEF2                             94
     C   94              GOTO      SLXY
     C   28              DO
     C     DISONT        IFGT      YSONT2
     C                   GOTO      STLES
     C                   END
     C     DISTET        IFGT      YSTET2
     C                   GOTO      STLES
     C                   END
     C                   END
     C*
     C* SLUTT PÅ TEST VED 28   STED FRA ER UTFYLLT
     C*
     C*
     C* SØKING VED SONE FRA UTFYLLT
     C*
     C   29
     CANN94DISONF        COMP      YSONF2                             94
     C   94              GOTO      SLXY
     C   29
     CANN94              DO
     C     DISONT        IFGT      YSONT2
     C                   GOTO      STLES
     C                   END
     C*
     C     DISTET        IFGT      YSTET2
     C                   GOTO      STLES
     C                   END
     C     DISONT        IFLT      YSONT
     C                   GOTO      STLES
     C                   END
     C     DISTET        IFLT      YSTET
     C                   GOTO      STLES
     C                   END
     C                   END
     C*
     C* SLUTT SONETEST
     C*
     C     *IN94         IFEQ      '0'
     C     DIKEY         CHAIN(N)  HEADF                              33
     C   33              GOTO      STLES
     C                   END
     C*
     C     SLXY          ENDSR
     C***********************************************
     C     TESTIN        BEGSR
     C***********************************************
     C                   SETON                                        32
     C                   MOVEA     '00'          *IN(91)
     C     *IN98         DOWEQ     '0'                                          1<-B
     C                   READC     SUBFIL                                 98
     C  N98WSNR          IFEQ      '1'                                           2<-B
     C     HENAS         IFEQ      XPLUKK                                         3<-B
     C                   MOVE      'SPO0010'     MSGNR
     C                   SETON                                        983032
     C                   END                                                      3<-E
     C  N98              EXSR      PLUKK
     C                   SETON                                        55
     C                   UPDATE    SUBFIL
     C                   SETOFF                                       55
     C  N98              SETOFF                                       32
     C   98              GOTO      SLTST2
     C                   END                                                     2<-E
     C*
     C  N98WSNR          IFEQ      '5'                                           2<-B
     C                   UPDATE    SUBFIL
     C  N98              EXSR      VISOPD
     C  N98              SETOFF                                       32
     C   98              GOTO      SLTST2
     C                   END                                                     2<-E
     C                   END                                                    1<-E
     C*
     C  N32              SETOFF                                       98
     C   98              DO
     C                   MOVE      'DIV0012'     MSGNR
     C                   SETON                                            30
     C                   END
     C*
     C     SLTST2        ENDSR
     C*
     C***********************************************
     C     PLUKK         BEGSR
     C***********************************************
     C*
     C* LOGGER I OMDISPONERINGSFILE
     C*
     C                   MOVE      HEAVD         OMAVD
     C                   MOVE      HEOPD         OMOPD
     C                   MOVE      DITUAV        OMTUAF
     C                   MOVE      TUPRO         OMTURF
     C                   MOVE      TURUND        OMRUNF
     C                   MOVE      UAVD          OMTUAT
     C                   MOVE      UPRO          OMTURT
     C                   MOVE      URUND         OMRUNT
     C                   WRITE     OMDIR
     C* OPPDATERER PLUKKET ORDRE
     C*
     C     HEADFK        CHAIN     HEADF                              33
     C     HEPRO         IFNE      0
     C                   Z-ADD     UPRO          HEPRO
     C                   MOVE      URUND         TRRUND
     C                   MOVEL     UBILN         HEHAWB
     C                   MOVE      UTRM          HETRM
     C     UCON1         IFNE      *BLANKS
     C                   MOVE      1             HETRC
     C                   MOVEL     UCON1         HETRCN
     C                   END
     C                   MOVE      'J'           HEPK3
     C     HEPK5         IFEQ      'P'
     C                   MOVE      'J'           HEPK5
     C                   END
     C                   END
     C                   UPDATE    HEADFR
     C     HEPRO         IFEQ      UPRO
     C*
     C* TREKKER FRA DATA PÅ GAMMEL TUR
     C*
     C     TURK2         CHAIN     TURER                              33
     C                   SUB       HENT          TUTS
     C                   SUB       HEVKT         TUTVKT
     C                   SUB       WSM3SU        TUTM3
     C                   SUB       HELM          TUTLM
N01  C                   SUB       HELMLA        TUTLM2
N01  C                   SUB       HEPOEN        TUPOEN
     C                   SUB       1             TUAO
     C                   UPDATE    TURERR
     C* FJERN GAMMELT TURNR FRA REKV. KNYTTET DIREKTE TIL OPPDRAG
     C                   EXSR      BUTRMV
     C*
     C*
     C* ØKER VEKTER I TURREGISTER
     C*
     C     TURERK        CHAIN     TURER                              33
     C                   MOVE      HEOT          TUOPDT
     C                   ADD       HENT          TUTS
     C                   ADD       HEVKT         TUTVKT
     C                   ADD       WSM3SU        TUTM3
     C                   ADD       HELM          TUTLM
N01  C                   ADD       HELMLA        TUTLM2
N01  C                   ADD       HEPOEN        TUPOEN
     C                   ADD       1             TUAO
     C                   UPDATE    TURERR
     C* LEGG PÅ NYTT  TURNR FRA REKV. KNYTTET DIREKTE TIL OPPDRAG
     C                   EXSR      BUTADD
     C*
     C* ØKER VEKTER PÅ SKJERM
     C*
     C                   ADD       HENT          UTS
     C                   ADD       HEVKT         UTVKT
     C                   ADD       WSM3SU        UTM3
     C                   ADD       HELM          UTLM
N01  C                   ADD       HELMLA        UTLMLA
N01  C                   ADD       HEPOEN        UTPOEN
     C                   ADD       1             UAO
     C*
     C                   MOVE      XPLUKK        HENAS
     C                   MOVE      *BLANKS       HENAK
     C                   MOVE      *ZEROS        HENT
     C                   MOVE      *BLANKS       HEVS1
     C                   MOVE      *BLANKS       HESDF
     C                   MOVE      *BLANKS       HESDT
     C                   MOVE      *ZEROS        HEVKT
     C                   MOVE      *ZEROS        WSM3SU
     C                   MOVE      *ZEROS        HELM
N01  C                   ADD       *ZEROS        HELMLA
N01  C                   ADD       *ZEROS        HEPOEN
     C                   MOVE      *BLANKS       HEOT
     C                   MOVE      *BLANKS       HEFR
     C                   MOVE      *BLANKS       HEST
     C                   MOVE      *BLANKS       IMCO
     C                   MOVE      *BLANKS       ETTE
     C*
     C* ENDRE DISPONERINGSREGISTER
     C*
     C     HEADFK        CHAIN     DISP                               33
     C  N33              MOVE      UAVD          DITUAV
     C  N33              MOVE      *BLANKS       DISTA1
     C  N33              MOVE      UPRO          DITUR
     C  N33              MOVE      URUND         DIRUND
     C  N33              UPDATE    DISPR
     C*
     C* OPPDATERER RUNDTUR I HEADFR
     C*
     C                   MOVE      *ZEROS        TRSDFD
     C                   MOVE      *ZEROS        WRSDFD
     C                   MOVE      *BLANKS       TRSTAF
     C                   MOVE      *ZEROS        TRSDTD
     C                   MOVE      *ZEROS        WRSDTD
     C                   MOVE      *BLANKS       TRSTAE
     C                   END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     BUTRMV        BEGSR
     C***********************************************
     C* GÅR IGJENNOM EVT. REKVISISJONER PÅ DETTE OPPDRAG OG FJERNER
     C* TURNUMMER
     C     HEOPD         CABEQ     *ZEROS        UTBUTR
     C                   MOVE      ' '           YYST
     C     KOSB9K        CHAIN     KOSBU9                             33
     C     *IN33         DOWNE     '1'
     C                   MOVE      *ZEROS        BUTAVD
     C                   MOVE      *ZEROS        BUTUNR
     C     BUBILN        IFEQ      TUBILN
     C                   MOVE      *BLANKS       BUBILN
     C                   MOVE      *ZEROS        BUFEDT
     C                   MOVE      *BLANKS       BUBILK
     C                   END
     C                   UPDATE    KOSBUR
     C     KOSB9K        READE     KOSBU9                                 33
     C  N33              END
     C* OPPDATER TUR OGSÅ PÅ EVT. DUMMY/DUPREKV. PÅ OPPDRAGET
     C                   EXSR      BUTRM2
     C*
     C     UTBUTR        ENDSR
     C***********************************************
     C     BUTRM2        BEGSR
     C***********************************************
     C* FJERN OGSÅ DUMMYREKV
     C                   MOVE      'D'           YYST
     C     KOSB9K        CHAIN     KOSBU9                             33
     C     *IN33         DOWNE     '1'
     C                   MOVE      *ZEROS        BUTAVD
     C                   MOVE      *ZEROS        BUTUNR
     C                   UPDATE    KOSBUR
     C     KOSB9K        READE     KOSBU9                                 33
     C  N33              END
     C                   ENDSR
     C*
     C*
     C***********************************************
     C     BUTADD        BEGSR
     C***********************************************
     C     HEOPD         CABEQ     *ZEROS        UTBUTA
     C* GÅR IGJENNOM EVT. REKVISISJONER PÅ DETTE OPPDRAG OG LEGGER NYTT
     C* TURNUMMER (FOR AT DE SKAL KOMME MED PÅ TRANSP.AVREGN.)
     C                   MOVE      ' '           YYST
     C     KOSB9K        CHAIN     KOSBU9                             33
     C     *IN33         DOWNE     '1'
     C                   MOVE      TUAVD         BUTAVD
     C                   MOVE      TUPRO         BUTUNR
     C                   MOVE      TURMND        BUPMN                          *PERIODE FØLGER
     C                   MOVE      TURAAR        BUPÅR                           ALLTID TUREN...
     C                   MOVE      TURACC        BUPCC                           ALLTID TUREN...
     C     BUBILN        IFEQ      *BLANKS
     C                   MOVE      TUBILN        BUBILN
     C                   MOVE      TUBILK        BUBILK
     C                   MOVE      TUDT          BUFEDT
     C                   END
     C                   UPDATE    KOSBUR
     C     KOSB9K        READE     KOSBU9                                 33
     C  N33              END
     C* OPPDATER TUR OGSÅ PÅ EVT. DUMMY/DUPREKV. PÅ OPPDRAGET
     C                   EXSR      BUTAD2
     C*
     C     UTBUTA        ENDSR
     C*
     C***********************************************
     C     BUTAD2        BEGSR
     C***********************************************
     C* EVT. DUMMYREKVISISJONER (DUP)PÅ DETTE OPPDRAG-LEGGER INN TURNR
     C* (FOR Å FÅ "TOTALBILDE" VED SPØRRING PÅ REKV - STATUS "*")
     C                   MOVE      'D'           YYST
     C     KOSB9K        CHAIN     KOSBU9                             33
     C     *IN33         DOWNE     '1'
     C                   MOVE      TUAVD         BUTAVD
     C                   MOVE      TUPRO         BUTUNR
     C                   MOVE      TURMND        BUPMN                          *PERIODE FØLGER
     C                   MOVE      TURAAR        BUPÅR                           ALLTID TUREN...
     C                   MOVE      TURACC        BUPCC                           ALLTID TUREN...
     C                   UPDATE    KOSBUR
     C     KOSB9K        READE     KOSBU9                                 33
     C  N33              END
     C                   ENDSR
     C*
     C***********************************************
     C     VISOPD        BEGSR
     C***********************************************
     C                   MOVE      *ZEROS        UFLAGG            1 0
     C                   CALL      'SYFM21A'
     C                   PARM                    UFIRM
     C                   PARM                    HEAVD
     C                   PARM                    HEOPD
     C                   PARM                    UFLAGG
     C                   MOVE      *ZEROS        UFLAGG
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     DIKEY         KLIST
     C                   KFLD                    DIAVD
     C                   KFLD                    DIOPD
     C     DISP1K        KLIST
     C                   KFLD                    YSTEF
     C                   KFLD                    LLDATO
     C                   KFLD                    YSTET
     C     DISP2K        KLIST
     C                   KFLD                    YSONF
     C                   KFLD                    LLDATO
     C                   KFLD                    YSONT
     C     DISP3K        KLIST
     C                   KFLD                    WSOPDT
     C                   KFLD                    LLDATO
     C     DISP4K        KLIST
     C                   KFLD                    WSAVD
     C                   KFLD                    LLDATO
     C     K3EQ          KLIST
     C                   KFLD                    WSOPDT
     C     K4EQ          KLIST
     C                   KFLD                    WSAVD
     C*
     C     TURERK        KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    UPRO
     C     TURK2         KLIST
     C                   KFLD                    DITUAV
     C                   KFLD                    DITUR
     C     HEADFK        KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C     KOSB9K        KLIST
     C                   KFLD                    YYST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C     KODTAK        KLIST
     C                   KFLD                    YUNI
     C                   KFLD                    WSAVD
     C                   MOVE      ' '           YYST              1
     C*
     C                   READ      FIRM                                   33
     C                   MOVE      *ZEROS        AVDZER            4 0
     C     *ENTRY        PLIST
     C                   PARM                    UFIRM             2
     C                   PARM                    UAVD              4 0
     C                   PARM                    UPRO              8 0
     C                   PARM                    URUND             8 0
     C                   PARM                    UFLAGG            1 0
     C                   MOVE      *ZEROS        UFLAGG
     C                   MOVE      *ZEROS        DATO0             6 0
     C                   MOVE      *ZEROS        TUDAFR            6 0
     C                   MOVE      *ZEROS        TUDATI            6 0
     C                   MOVE      UAVD          WSAVD
     C                   MOVE      *ZEROS        YPRO              8 0
     C                   MOVE      *ZEROS        YKNA              8 0
     C                   MOVE      'A'           YUNI              1
     C                   MOVE      *BLANKS       YSONF
     C                   MOVE      *BLANKS       YSONF2
     C                   MOVE      *BLANKS       YSONT
     C                   MOVE      *BLANKS       YSONT2
     C                   MOVE      *BLANKS       YSTEF
     C                   MOVE      *BLANKS       YSTEF2
     C                   MOVE      *BLANKS       YSTET
     C                   MOVE      *BLANKS       YSTET2
     C*
     C                   MOVE      'DIV0702'     XMSGNR            7            MELDING-ID
     C                   Z-ADD     3             XLEVEL            1 0          NIVÅ
     C                   Z-ADD     0             XRETUR            1 0          RETURKODE
     C                   Z-ADD     30            XMSGL             4 0          LENGDE PÅ MELDING
     C                   CALL      'SUBR23R3'
     C                   PARM                    XMSGNR
     C                   PARM                    XPLUKK           30
     C                   PARM                    XLEVEL
     C                   PARM                    XRETUR
     C                   PARM                    XMSGL
     C*
     C     TURERK        CHAIN(N)  TURER                              33
     C                   MOVE      TUSDF         USDF
     C                   MOVE      TUBILN        UBILN
     C                   MOVE      TULK          ULK
     C                   MOVE      TUHENG        UHENG
     C                   MOVE      TULKH         ULKH
     C                   MOVE      TUSDT         USDT
     C                   MOVE      TUKNT2        UKNT2
     C                   MOVE      TUNAT         UNAT
     C                   MOVE      TUKVKT        UKVKT
     C                   MOVE      TUKAM3        UKAM3
     C                   MOVE      TUKALM        UKALM
     C                   MOVE      TUTARA        UTARA
     C     UKVKT         ADD       UTARA         TUBER2
     C                   MOVE      TUTS          UTS
     C                   MOVE      TUTVKT        UTVKT
     C                   MOVE      TUTM3         UTM3
     C                   MOVE      TUTLM         UTLM
N01  C                   MOVE      TUTLM2        UTLMLA
N01  C                   MOVE      TUPOEN        UTPOEN
     C                   MOVE      TUAO          UAO
     C                   MOVE      TUTRMA        UTRM
     C                   MOVE      TUCON1        UCON1
     C     *LIKE         DEFINE    TUTRMA        UTRM
     C     *LIKE         DEFINE    TUCON1        UCON1
     C                   MOVE      TUDT          UDAFR
     C                   MOVE      TUDTT         UDATI
     C                   CALL      'DATFM4'
     C                   PARM                    FIDTFM
     C                   PARM                    UDAFR
     C                   PARM                    UDATI
     C                   PARM                    DATO0
     C                   PARM                    DATO0
     C                   MOVE      *ZEROS        WSDATO
     C*
     C* Definer hjelpevariable for å bevare subfildata
     C     *LIKE         DEFINE    HEAVD         XXAVD
     C     *LIKE         DEFINE    HEOPD         XXOPD
     C     *LIKE         DEFINE    HENAS         XXNAS
     C     *LIKE         DEFINE    HENAK         XXNAK
     C     *LIKE         DEFINE    HESDF         XXSDF
     C     *LIKE         DEFINE    HESDT         XXSDT
     C     *LIKE         DEFINE    HENT4         XXNT4
     C     *LIKE         DEFINE    HEVS10        XXVS10
     C     *LIKE         DEFINE    HEVKTW        XXVKTW
     C     *LIKE         DEFINE    HELM          XXLM
N01  C     *LIKE         DEFINE    HELMLA        XXLMLA
N01  C     *LIKE         DEFINE    HEPOEN        XXPOEN
     C     *LIKE         DEFINE    HEOT          XXOT
     C     *LIKE         DEFINE    HEFR          XXFR
     C     *LIKE         DEFINE    HEST          XXST
     C     *LIKE         DEFINE    DISTA1        XXSTA1
     C     *LIKE         DEFINE    DITUAV        XXTUAV
     C     *LIKE         DEFINE    TROPD0        XXOPD0
     C     *LIKE         DEFINE    TRSDFD        XXSDFD
     C     *LIKE         DEFINE    TRSDTD        XXSDTD
     C     *LIKE         DEFINE    TRSTAF        XXSTAF
     C     *LIKE         DEFINE    TRSTAE        XXSTAE
     C     *LIKE         DEFINE    WSM3SU        XXM3SU
     C     *LIKE         DEFINE    IMCO          XXIMCO
     C     *LIKE         DEFINE    ETTE          XXETTE
     C*
     C                   EXSR      TSTSØK
     C                   ENDSR
     C*
