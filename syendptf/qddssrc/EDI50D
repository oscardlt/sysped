     A*%%TS  SD  20230109  082028  CB          REL-V7R4M0  5770-WDS
     A*%%EC
     A                                      DSPSIZ(27 132 *DS4)
     A                                      REF(*LIBL/EDII)
     A                                      PRINT
     A                                      HLPDOC('STD' 'HELPNU' 'HLP400')
     A                                      ALTHELP
     A                                      HELP
     A                                      CF03(03 'Gå ut')
     A                                      CF12(12 'Forrige')
     A                                      CF11(11 'BEKR SLETT')
      *
     A          R EDI50DA                   SFL
     A            INTCRD    R        H
     A            INTCLL    R        H
     A            INIFSR    R        H
     A            INIFSS    R        H
E05  A            ZOP            1A  I  8  3VALUES(' ' 'I' 'U' '2' '3' '4' '5' -
     A                                      '8' '0' '9' '6')
     A            INID      R        O  8  6
     A  60                                  COLOR(RED)
     A            INNA      R        O  8 43
     A  60                                  COLOR(RED)
     A            INNETW    R        O  8 74
     A  60                                  COLOR(RED)
     A            INEX      R        O  8 85
     A  60                                  COLOR(RED)
     A*
     A          R EDI50DB                   SFLCTL(EDI50DA)
     A*%%TS  SD  20180522  081137  JOVO        REL-V7R1M0  5770-WDS
     A                                      SFLSIZ(0017)
     A                                      SFLPAG(0016)
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                      CF05(05 'Frisk opp')
     A                                      CF06(06 'Lage Ny')
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND(*MORE)
     A                                  1  2'EDI'
     A                                      COLOR(BLU)
     A                                  1 58'UtvekslingsID 1/3'
     A                                      COLOR(WHT)
     A                                  1127'EDI50D'
     A                                      COLOR(BLU)
     A                                  3  2MSGCON(020 EDI0602 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  4'2=Endre   3=Kopier   4=Slette   5=-
     A                                      Vise   6=Flere fjerntl. mapper     -
     A                                       8=Fritekst   I=IFS_inn   U=IFS_ut -
     A                                        9=POD signatur        '
     A                                      COLOR(BLU)
     A            ZRECNO         4S 0O  1113SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  6  2MSGCON(003 EDI0609 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  6  6MSGCON(014 EDI0370 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  6 43MSGCON(004 EDI0186 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  6 74'Nettverk'
     A                                      COLOR(WHT)
     A                                  6 85'Intern/Ekstern'
     A                                      COLOR(WHT)
     A            ZNID      R        B  7  6REFFLD(INID)
     A            ZNNA      R        B  7 43REFFLD(INNA)
     A            ZNNETW    R        B  7 74REFFLD(INNETW)
     A            ZNEX      R        B  7 85REFFLD(INEX)
      *
     A          R EDI50DC
     A                                      OVERLAY
     A                                 25  4'F3=Avslutt   F5=Forny   F6=Lage ny-
     A                                         F12=Avbryt'
     A                                      COLOR(BLU)
      *
     A          R EDI50DD
     A                                      OVERLAY
     A                                  1  2'EDI'
     A                                      COLOR(BLU)
     A                                  1 58'UtvekslingsID 2/3'
     A                                      COLOR(WHT)
     A                                  1127'EDI50D'
     A                                      COLOR(BLU)
     A                                  3  2MSGCON(028 EDI0602 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  2MSGCON(029 EDI0066 *LIBL/SYMSGF)
     A            ÅNID      R        B  5 34REFFLD(INID)
     A N41                                  DSPATR(PC)
     A  41                                  DSPATR(PR)
     A            ÅN0007    R        B  5 70REFFLD(IN0007)
     A                                  6  4MSGCON(027 EDI0067 *LIBL/SYMSGF)
     A            ÅN0022    R        B  6 34REFFLD(IN0022)
     A                                  7  4MSGCON(027 EDI0068 *LIBL/SYMSGF)
     A            ÅN0026    R        B  7 34REFFLD(IN0026)
     A                                  8  4MSGCON(027 EDI0069 *LIBL/SYMSGF)
     A            ÅN0032    R        B  8 34REFFLD(IN0032)
     A                                  9  4MSGCON(027 EDI0070 *LIBL/SYMSGF)
     A            ÅNNA      R        B  9 34REFFLD(INNA)
     A  41                                  DSPATR(PC)
     A                                      CHECK(LC)
     A                                 10  4MSGCON(027 EDI0071 *LIBL/SYMSGF)
     A            ÅNNETW    R        B 10 34REFFLD(INNETW)
     A                                      TEXT('Network')
     A                                      VALUES(' ' 'A' 'B' 'E' '3' '6' '7' -
     A                                      '8' '9' '0')
     A                                 10 41'3=FTP      6=E-Post     7=TDV/IS'
     A                                 11 41'8=OFTP     9=Privat     A=EVRY FTP'
     A                                 12 41'B=IBM FTP  E=Ergo       0=SKAT'
     A                                 13  4MSGCON(027 EDI0072 *LIBL/SYMSGF)
     A            ÅNEX      R        B 13 34REFFLD(INEX)
     A                                      VALUES('E' 'I')
     A                                 13 41'I=Internal E=External'
     A                                 14  4'Dupkontr. edifact mottak ?:'
     A            ÅNDUPCTL  R        B 14 34REFFLD(INDUPCTL)
     A                                 14 41'J=ja  Y=ja  N=nei  Blank=nei'
     A                                 25  2'F12=Avbryt'
     A                                      COLOR(BLU)
      *
     A          R EDI50DG
     A                                      OVERLAY
     A                                 24  1'                                  -
     A                                                                         -
     A                                                                         -
     A                                                                '
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
     A                                 25  2'F3=Avslutt   F12=Avbryt'
     A                                      COLOR(BLU)
      *
     A          R EDI50DØ
     A                                      OVERLAY
     A            ZMSGNO         7A  P
     A            ZMSGTX        79A  O 26  2MSGID(&ZMSGNO SYMSGF)
     A N30                                  COLOR(WHT)
     A  30                                  COLOR(RED)
     A*
     A          R EDI50D3
     A*%%TS  SD  20230109  082028  CB          REL-V7R4M0  5770-WDS
     A                                  1  2'EDI'
     A                                      COLOR(BLU)
     A                                  1 58'UtvekslingsID FTP 1/2'
     A                                      COLOR(WHT)
     A                                  1127'EDI50D'
     A                                      COLOR(BLU)
     A                                  3  2'Velg og trykk Enter.'
     A                                      COLOR(BLU)
     A                                  3 26'UtvekslingID:'
     A            ÅNID      R        O  3 42REFFLD(INID)
     A                                  5  2'Vertsnavn/Adresse. :'
     A            ÅNTCHN    R        B  5 25REFFLD(INTCHN)
     A  42                                  DSPATR(PR)
     A                                      CHECK(LC)
     A                                  6  2'BrukerID / Passord :'
     A            ÅNTCUS    R        B  6 25REFFLD(INTCUS)
     A  42                                  DSPATR(PR)
     A                                      CHECK(LC)
     A            ÅNTCPW    R        B  6 76REFFLD(INTCPW)
     A  42                                  DSPATR(PR)
     A                                      CHECK(LC)
     A                                  8  2'Fjern katalogbane. :'
     A            ÅNTCRD    R        B  8 25REFFLD(INTCRD)
     A  42                                  DSPATR(PR)
     A                                      CHECK(LC)
     A                                  8 92'*IFS=Skriv til IFS isteden for via-
     A                                       FTP.'''
     A                                  9  4'Temporær . . . . :'
     A            ÅNTCRDT   R        B  9 25REFFLD(INTCRDT)
     A  42                                  DSPATR(PR)
     A                                      CHECK(LC)
     A                                 11  2'Fast filnavn . . . :'
     A            ÅNTCRF    R        B 11 25REFFLD(INTCRF)
     A  42                                  DSPATR(PR)
     A                                      CHECK(LC)
     A                                 12  2'Lokalt bibliotek . :'
     A            ÅNTCLL    R        B 12 25REFFLD(INTCLL)
     A  42                                  DSPATR(PR)
     A                                 12 38'*IFS=Hent fil fra IFS isteden bibl-
     A                                      iotek.'
     A                                 13  4'Fil  . . . . . . :'
     A            ÅNTCLF    R        B 13 25REFFLD(INTCLF)
     A  42                                  DSPATR(PR)
     A                                 13 79' '
     A                                 15  2'SENDPA ? . . . . :  '
     A            ÅNSPASV        1A  B 15 25VALUES(' ' 'N' 'J' 'Y' 'X')
     A                                 15 29'Y/N/X (X=Slå av extended passive m-
     A                                      ode  SENDEPSV 0)'
     A                                 16  2'BINARY ? . . . . :'
     A            ÅNBIN          1A  B 16 25VALUES(' ' 'N' 'J' 'Y')
     A                                 16 29'Y/N'
     A                                 17  2'EBCDIC ? . . . . :'
     A            ÅNEBCDIC       1A  B 17 25VALUES(' ' 'N' 'J' 'Y')
     A                                 17 29'Y/N'
     A                                 18  2'ASCII ?  . . . . :'
     A            ÅNASCII        1A  B 18 25VALUES(' ' 'N' 'J' 'Y')
     A                                 18 29'Y/N'
     A                                 19  4'Kodesett . . . :'
     A            ÅNCODPAG       9A  B 19 25
     A                                 19 37'*STMF *PCASCII *STDASCII 1-32767'
     A                                 19 70'(819'
     A                                 19 75'AS400)'
     A                                 20  4'Linjeslutt . . :'
     A            ÅNENDLIN       6A  B 20 25VALUES(' ' '*CRLF' '*LF' '*CR' '*LF-
     A                                      CR' '*FIXED' '*NONE')
S02  A*                                     CR' 'FIXED' '*NONE')
     A                                 20 34' '
     A                                 20 37'*CR *LF *CR *LFCR *FIXED *NONE'
     A                                 20 80' '
     A            ÅNCMDE3       64A  B 23 25CHECK(LC)
     A                                 25  2MSGCON(011 EDI0812 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N55                             10 25MSGCON(050 JOV1767 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                 22  2'sFTP Location Name'
     A                                 23  2'via Arpeggio/CGI :'
      *
     A          R EDI50D32
     A                                  1  2'EDI'
     A                                      COLOR(BLU)
     A                                  1 58'UtvekslingsID FTP 2/2'
     A                                      COLOR(WHT)
     A                                  1127'EDI50D'
     A                                      COLOR(BLU)
     A                                  3  2MSGCON(023 EDI0602 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  2'UtvekslingID . . . . . . . . . . .-
     A                                       . . . . . . :'
     A            ÅNID      R        O  5 53REFFLD(INID)
     A                                  7  2'IFS lokal katalogbane, overstyr st-
     A                                      andard mottak:'
     A            ÅNIFSR        64A  B  7 53CHECK(LC)
     A                                  8  4'Overstyr standard sendning . . . .-
     A                                       . . . . . :'
     A            ÅNIFSS        64A  B  8 53CHECK(LC)
     A                                 10  2'FTP kommandoer foran PUT . . . . .-
     A                                       . . . . . . :'
     A            ÅNCMDF1       64A  B 10 53CHECK(LC)
     A            ÅNCMDF2       64A  B 11 53CHECK(LC)
     A            ÅNCMDF3       64A  B 12 53CHECK(LC)
     A                                 14  2'FTP kommandoer etter PUT . . . . .-
     A                                       . . . . . . :'
     A            ÅNCMDE1       64A  B 14 53CHECK(LC)
     A            ÅNCMDE2       64A  B 15 53CHECK(LC)
     A                                 25  2MSGCON(011 EDI0812 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
      *
     A          R EDI50D4
     A                                  1  2'EDI'
     A                                      COLOR(BLU)
     A                                  1 58'UtvekslingsID SNADS'
     A                                      COLOR(WHT)
     A                                  1127'EDI50D'
     A                                      COLOR(BLU)
     A                                  3  2MSGCON(023 EDI0602 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  2MSGCON(029 EDI0066 *LIBL/SYMSGF)
     A            ÅNID      R        O  5 34REFFLD(INID)
     A            ÅN0007    R        O  5 70REFFLD(IN0007)
     A                                  6  4MSGCON(027 EDI0067 *LIBL/SYMSGF)
     A            ÅN0022    R        O  6 34REFFLD(IN0022)
     A                                  7  4MSGCON(027 EDI0068 *LIBL/SYMSGF)
     A            ÅN0026    R        O  7 34REFFLD(IN0026)
     A                                  8  4MSGCON(027 EDI0069 *LIBL/SYMSGF)
     A            ÅN0032    R        O  8 34REFFLD(IN0032)
     A                                  9  4MSGCON(027 EDI0070 *LIBL/SYMSGF)
     A            ÅNNA      R        O  9 34REFFLD(INNA)
     A                                 10  4MSGCON(027 EDI0071 *LIBL/SYMSGF)
     A            ÅNNETW    R        O 10 34REFFLD(INNETW)
     A                                      TEXT('Network')
     A                                 10 41'3=FTP      6=E-Post     7=TDV/IS'
     A                                 11 41'8=OFTP     9=Privat     A=EVRY FTP'
     A                                 12 41'B=IBM FTP  E=Ergo'
     A                                 13  4MSGCON(027 EDI0072 *LIBL/SYMSGF)
     A            ÅNEX      R        O 13 34REFFLD(INEX)
     A                                 13 41MSGCON(022 EDI0073 *LIBL/SYMSGF)
     A                                 14  4'Dupkontr. edifact mottak ?:'
     A            ÅNDUPCTL  R        O 14 34REFFLD(INDUPCTL)
     A                                 14 41'J=ja  Y=ja  N=nei  Blank=nei'
     A                                 16  4MSGCON(027 EDI0133 *LIBL/SYMSGF)
     A            ÅNOUSR    R        B 16 34REFFLD(INOUSR)
     A  42                                  DSPATR(PR)
     A            ÅNOADR    R        B 16 45REFFLD(INOADR)
     A  42                                  DSPATR(PR)
     A                                 17  4MSGCON(027 EDI0092 *LIBL/SYMSGF)
     A            ÅNLIND    R        B 17 34REFFLD(INLIND)
     A  42                                  DSPATR(PR)
     A            ÅNCTLD    R        B 17 45REFFLD(INCTLD)
     A  42                                  DSPATR(PR)
     A            ÅNDEVD    R        B 17 56REFFLD(INDEVD)
     A  42                                  DSPATR(PR)
     A                                 25  2MSGCON(011 EDI0812 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
      *
     A          R EDI50D6
     A                                  1  2'EDI'
     A                                      COLOR(BLU)
     A                                  1 58'UtvekslingsID X.400'
     A                                      COLOR(WHT)
     A                                  1127'EDI50D'
     A                                      COLOR(BLU)
     A                                  3  2MSGCON(023 EDI0602 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  2MSGCON(029 EDI0066 *LIBL/SYMSGF)
     A            ÅNID      R        O  5 34REFFLD(INID)
     A            ÅN0007    R        O  5 70REFFLD(IN0007)
     A                                  6  4MSGCON(027 EDI0067 *LIBL/SYMSGF)
     A            ÅN0022    R        O  6 34REFFLD(IN0022)
     A                                  7  4MSGCON(027 EDI0068 *LIBL/SYMSGF)
     A            ÅN0026    R        O  7 34REFFLD(IN0026)
     A                                  8  4MSGCON(027 EDI0069 *LIBL/SYMSGF)
     A            ÅN0032    R        O  8 34REFFLD(IN0032)
     A                                  9  4MSGCON(027 EDI0070 *LIBL/SYMSGF)
     A            ÅNNA      R        O  9 34REFFLD(INNA)
     A                                 10  4MSGCON(027 EDI0071 *LIBL/SYMSGF)
     A            ÅNNETW    R        O 10 34REFFLD(INNETW)
     A                                      TEXT('Network')
     A                                 10 41'3=FTP      6=E-Post     7=TDV/IS'
     A                                 11 41'8=OFTP     9=Privat     A=EVRY FTP'
     A                                 12 41'B=IBM FTP  E=Ergo'
     A                                 13  4MSGCON(027 EDI0072 *LIBL/SYMSGF)
     A            ÅNEX      R        O 13 34REFFLD(INEX)
     A                                 13 41MSGCON(024 EDI0073 *LIBL/SYMSGF)
     A                                 14  4'Dupkontr. edifact mottak ?:'
     A            ÅNDUPCTL  R        O 14 34REFFLD(INDUPCTL)
     A                                 14 41'J=ja  Y=ja  N=nei  Blank=nei'
     A                                 16  4MSGCON(027 EDI0133 *LIBL/SYMSGF)
     A            ÅNOUSR    R        B 16 34REFFLD(INOUSR)
     A  42                                  DSPATR(PR)
     A            ÅNOADR    R        B 16 45REFFLD(INOADR)
     A  42                                  DSPATR(PR)
     A                                 25  2MSGCON(011 EDI0812 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
      *
     A          R EDI50D7
     A                                  1  2'EDI'
     A                                      COLOR(BLU)
     A                                  1 58'UtvekslingsID TDV/IS'
     A                                      DSPATR(HI)
     A                                  1127'EDI50D'
     A                                      COLOR(BLU)
     A                                  3  2MSGCON(023 EDI0602 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  2MSGCON(029 EDI0066 *LIBL/SYMSGF)
     A            ÅNID      R        O  5 34REFFLD(INID)
     A            ÅN0007    R        O  5 70REFFLD(IN0007)
     A                                  6  4MSGCON(027 EDI0067 *LIBL/SYMSGF)
     A            ÅN0022    R        O  6 34REFFLD(IN0022)
     A                                  7  4MSGCON(027 EDI0068 *LIBL/SYMSGF)
     A            ÅN0026    R        O  7 34REFFLD(IN0026)
     A                                  8  4MSGCON(027 EDI0069 *LIBL/SYMSGF)
     A            ÅN0032    R        O  8 34REFFLD(IN0032)
     A                                  9  4MSGCON(027 EDI0070 *LIBL/SYMSGF)
     A            ÅNNA      R        O  9 34REFFLD(INNA)
     A                                 10  4MSGCON(027 EDI0071 *LIBL/SYMSGF)
     A            ÅNNETW    R        O 10 34REFFLD(INNETW)
     A                                      TEXT('Network')
     A                                 10 41'3=FTP      6=E-Post     7=TDV/IS'
     A                                 11 41'8=OFTP     9=Privat     A=EVRY FTP'
     A                                 12 41'B=IBM FTP  E=Ergo       0=SKAT'
     A                                 13  4MSGCON(027 EDI0072 *LIBL/SYMSGF)
     A            ÅNEX      R        O 13 34REFFLD(INEX)
     A                                 13 41MSGCON(027 EDI0073 *LIBL/SYMSGF)
     A                                 14  4'Dupkontr. edifact mottak ?:'
     A            ÅNDUPCTL  R        O 14 34REFFLD(INDUPCTL)
     A                                 14 41'J=ja  Y=ja  N=nei  Blank=nei'
     A                                 16 11MSGCON(058 EDI0171 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 25  2MSGCON(011 EDI0812 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
      *
     A          R EDI50D8
     A                                  1  2'EDI'
     A                                      COLOR(BLU)
     A                                  1 58'UtvekslingsID OFTP'
     A                                      COLOR(WHT)
     A                                  1127'EDI50D'
     A                                      COLOR(BLU)
     A                                  3  2MSGCON(023 EDI0602 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  2MSGCON(029 EDI0066 *LIBL/SYMSGF)
     A            ÅNID      R        O  5 34REFFLD(INID)
     A            ÅN0007    R        O  5 70REFFLD(IN0007)
     A                                  6  4MSGCON(027 EDI0067 *LIBL/SYMSGF)
     A            ÅN0022    R        O  6 34REFFLD(IN0022)
     A                                  7  4MSGCON(027 EDI0068 *LIBL/SYMSGF)
     A            ÅN0026    R        O  7 34REFFLD(IN0026)
     A                                  8  4MSGCON(027 EDI0069 *LIBL/SYMSGF)
     A            ÅN0032    R        O  8 34REFFLD(IN0032)
     A                                  9  4MSGCON(027 EDI0070 *LIBL/SYMSGF)
     A            ÅNNA      R        O  9 34REFFLD(INNA)
     A                                 10  4MSGCON(027 EDI0071 *LIBL/SYMSGF)
     A            ÅNNETW    R        O 10 34REFFLD(INNETW)
     A                                      TEXT('Network')
     A                                 10 41'3=FTP      6=E-Post     7=TDV/IS'
     A                                 11 41'8=OFTP     9=Privat     A=EVRY FTP'
     A                                 12 41'B=IBM FTP  E=Ergo       0=SKAT'
     A                                 13  4MSGCON(027 EDI0072 *LIBL/SYMSGF)
     A            ÅNEX      R        O 13 34REFFLD(INEX)
     A                                 13 41MSGCON(024 EDI0073 *LIBL/SYMSGF)
     A                                 14  4'Dupkontr. edifact mottak ?:'
     A            ÅNDUPCTL  R        O 14 34REFFLD(INDUPCTL)
     A                                 14 41'J=ja  Y=ja  N=nei  Blank=nei'
     A                                 16  4'OFTP ID . . . . . . . . . :'
     A                                 16  4'OFTP ID . . . . . . . . . :'
     A            ÅNOFID    R        B 16 34REFFLD(INOFID)
     A  42                                  DSPATR(PR)
     A                                 17  6'Password  . . . . . . . :'
     A            ÅNOFPW    R        B 17 34REFFLD(INOFPW)
     A  42                                  DSPATR(PR)
     A                                 18  6'X.25 Line . . . . . . . :'
     A            ÅNOFXL    R        B 18 34REFFLD(INOFXL)
     A  42                                  DSPATR(PR)
     A                                 19  8'Number  . . . . . . . :'
     A            ÅNOFXN    R        B 19 34REFFLD(INOFXN)
     A  42                                  DSPATR(PR)
     A                                 25  2MSGCON(011 EDI0812 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
      *
     A          R EDI50D9
     A                                  1  2'EDI'
     A                                      COLOR(BLU)
     A                                  1 58'UtvekslingsID Privat'
     A                                      DSPATR(HI)
     A                                  1127'EDI50D'
     A                                      COLOR(BLU)
     A                                  3  2MSGCON(023 EDI0602 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  2MSGCON(029 EDI0066 *LIBL/SYMSGF)
     A            ÅNID      R        O  5 34REFFLD(INID)
     A            ÅN0007    R        O  5 70REFFLD(IN0007)
     A                                  6  4MSGCON(027 EDI0067 *LIBL/SYMSGF)
     A            ÅN0022    R        O  6 34REFFLD(IN0022)
     A                                  7  4MSGCON(027 EDI0068 *LIBL/SYMSGF)
     A            ÅN0026    R        O  7 34REFFLD(IN0026)
     A                                  8  4MSGCON(027 EDI0069 *LIBL/SYMSGF)
     A            ÅN0032    R        O  8 34REFFLD(IN0032)
     A                                  9  4MSGCON(027 EDI0070 *LIBL/SYMSGF)
     A            ÅNNA      R        O  9 34REFFLD(INNA)
     A                                 10  4MSGCON(027 EDI0071 *LIBL/SYMSGF)
     A            ÅNNETW    R        O 10 34REFFLD(INNETW)
     A                                      TEXT('Network')
     A                                 10 41'3=FTP      6=E-Post     7=TDV/IS'
     A                                 11 41'8=OFTP     9=Privat     A=EVRY FTP'
     A                                 12 41'B=IBM FTP  E=Ergo       0=SKAT'
     A                                 13  4MSGCON(027 EDI0072 *LIBL/SYMSGF)
     A            ÅNEX      R        O 13 34REFFLD(INEX)
     A                                 13 41MSGCON(027 EDI0073 *LIBL/SYMSGF)
     A                                 14  4'Dupkontr. edifact mottak ?:'
     A            ÅNDUPCTL  R        O 14 34REFFLD(INDUPCTL)
     A                                 14 41'J=ja  Y=ja  N=nei  Blank=nei'
     A                                 16 11MSGCON(058 EDI0172 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 25  2MSGCON(011 EDI0812 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A*
     A          R EDI50D0
     A*%%TS  SD  20151124  085032  SVEIN       REL-V7R1M0  5770-WDS
     A                                  1  2'EDI'
     A                                      COLOR(BLU)
     A                                  1 58'UtvekslingsID EVRY'
     A                                      COLOR(WHT)
     A                                  1127'EDI50D'
     A                                      COLOR(BLU)
     A                                  3  2MSGCON(023 EDI0602 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  2'Utvekslingsid . . . . . . . :'
     A            ÅNID      R        O  5 34REFFLD(INID)
     A            ÅN0007    R        O  5 70REFFLD(IN0007)
     A                                  7  4'Vertsnavn/adresse . . . . :'
     A            ÅNTCHN    R        B  7 34REFFLD(INTCHN)
     A  42                                  DSPATR(PR)
     A                                      CHECK(LC)
     A                                  8  4'BrukerID / Passord  . . . :'
     A            ÅNTCUS    R        B  8 34REFFLD(INTCUS)
     A  42                                  DSPATR(PR)
     A                                      CHECK(LC)
     A            ÅNTCPW    R        B  8 85REFFLD(INTCPW)
     A  42                                  DSPATR(PR)
     A                                      CHECK(LC)
     A                                 10  4'Fjerntliggende katalogbane:'
     A            ÅNTCRD    R        B 10 34REFFLD(INTCRD)
     A  42                                  DSPATR(PR)
     A                                      CHECK(LC)
     A                                 11  6'Temporær  . . . . . . . :'
     A            ÅNTCRDT   R        B 11 34REFFLD(INTCRDT)
     A  42                                  DSPATR(PR)
     A                                      CHECK(LC)
     A                                 13  4'Hent data selv ?  . . . . :'
     A            ÅNEVRYG        1A  B 13 34VALUES(' ' 'N' 'J' 'Y')
     A                                 13 46'Y/J=Ja   Blank/N=Nei'
     A                                 14  4'Passive mode ?  . . . . . :'
     A            ÅNSPASV        1A  B 14 34VALUES(' ' 'N' 'J' 'Y' 'X')
     A                                 14 46'Y/N/X (X=Slå av extended passive m-
     A                                      ode  SENDEPSV 0)'
     A                                 15  4'Binary ?  . . . . . . . . :'
     A            ÅNBIN          1A  B 15 34VALUES(' ' 'N' 'J' 'Y')
     A                                 15 46'Y/N'
     A                                 16  4'EBCDIC ?  . . . . . . . . :'
     A            ÅNEBCDIC       1A  B 16 34VALUES(' ' 'N' 'J' 'Y')
     A                                 16 46'Y/N'
     A                                 17  4'ASCII ?   . . . . . . . . :'
     A            ÅNASCII        1A  B 17 34VALUES(' ' 'N' 'J' 'Y')
     A                                 17 46'Y/N'
     A                                 18  6'Kodesett  . . . . . . . :'
     A            ÅNCODPAG       9A  B 18 34
     A                                 18 46'*STMF *PCASCII *STDASCII 1-32767'
     A                                 19  6'Linjeslutt  . . . . . . :'
     A            ÅNENDLIN       6A  B 19 34VALUES(' ' '*CRLF' '*LF' '*CR' '*LF-
     A                                      CR' 'FIXED' '*NONE')
     A                                 19 46'*CR *LF *CR *LFCR *FIXED *NONE'
     A                                 25  2MSGCON(011 EDI0812 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
      *
     A          R EDI50DN
     A                                  1  2'EDI'
     A                                      COLOR(BLU)
     A                                  1 58'UtvekslingsID IBM FTP'
     A                                      COLOR(WHT)
     A                                  1127'EDI50D'
     A                                      COLOR(BLU)
     A                                  3  2MSGCON(023 EDI0602 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  2MSGCON(029 EDI0066 *LIBL/SYMSGF)
     A            ÅNID      R        O  5 34REFFLD(INID)
     A            ÅN0007    R        O  5 70REFFLD(IN0007)
     A                                  6  4MSGCON(027 EDI0067 *LIBL/SYMSGF)
     A            ÅN0022    R        O  6 34REFFLD(IN0022)
     A                                  7  4MSGCON(027 EDI0068 *LIBL/SYMSGF)
     A            ÅN0026    R        O  7 34REFFLD(IN0026)
     A                                  8  4MSGCON(027 EDI0069 *LIBL/SYMSGF)
     A            ÅN0032    R        O  8 34REFFLD(IN0032)
     A                                  9  4MSGCON(027 EDI0070 *LIBL/SYMSGF)
     A            ÅNNA      R        O  9 34REFFLD(INNA)
     A                                 10  4MSGCON(027 EDI0071 *LIBL/SYMSGF)
     A            ÅNNETW    R        O 10 34REFFLD(INNETW)
     A                                      TEXT('Network')
     A                                 10 41'3=FTP      6=E-Post     7=TDV/IS'
     A                                 11 41'8=OFTP     9=Privat     A=EVRY FTP'
     A                                 12 41'B=IBM FTP  E=Ergo       0=SKAT'
     A                                 13  4MSGCON(027 EDI0072 *LIBL/SYMSGF)
     A            ÅNEX      R        O 13 34REFFLD(INEX)
     A                                 13 41'I=Internal  E=External'
     A                                 14  4'Dupkontr. edifact mottak ?:'
     A            ÅNDUPCTL  R        O 14 34REFFLD(INDUPCTL)
     A                                 14 41'J=ja  Y=ja  N=nei  Blank=nei'
     A                                 16  4MSGCON(027 EDI0117 *LIBL/SYMSGF)
     A            ÅNTCHN    R        B 17  4REFFLD(INTCHN)
     A  42                                  DSPATR(PR)
     A                                      CHECK(LC)
     A                                 18  6MSGCON(025 EDI0118 *LIBL/SYMSGF)
     A            ÅNTCUS    R        B 18 34REFFLD(INTCUS)
     A  42                                  DSPATR(PR)
     A                                      CHECK(LC)
     A            ÅNTCPW    R        B 18 85REFFLD(INTCPW)
     A  42                                  DSPATR(PR)
     A                                      CHECK(LC)
     A                                 25  2MSGCON(011 EDI0812 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
      *
     A          R EDI50DP
     A                                  1  2'EDI'
     A                                      COLOR(BLU)
     A                                  1 58'UtvekslingsID ERGO FTP'
     A                                      COLOR(WHT)
     A                                  1127'EDI50D'
     A                                      COLOR(BLU)
     A                                  3  2MSGCON(023 EDI0602 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  2MSGCON(029 EDI0066 *LIBL/SYMSGF)
     A            ÅNID      R        O  5 34REFFLD(INID)
     A            ÅN0007    R        O  5 70REFFLD(IN0007)
     A                                  6  4MSGCON(027 EDI0067 *LIBL/SYMSGF)
     A            ÅN0022    R        O  6 34REFFLD(IN0022)
     A                                  7  4MSGCON(027 EDI0068 *LIBL/SYMSGF)
     A            ÅN0026    R        O  7 34REFFLD(IN0026)
     A                                  8  4MSGCON(027 EDI0069 *LIBL/SYMSGF)
     A            ÅN0032    R        O  8 34REFFLD(IN0032)
     A                                  9  4MSGCON(027 EDI0070 *LIBL/SYMSGF)
     A            ÅNNA      R        O  9 34REFFLD(INNA)
     A                                 10  4MSGCON(027 EDI0071 *LIBL/SYMSGF)
     A            ÅNNETW    R        O 10 34REFFLD(INNETW)
     A                                      TEXT('Network')
     A                                 10 41'3=FTP      6=E-Post     7=TDV/IS'
     A                                 11 41'8=OFTP     9=Privat     A=EVRY FTP'
     A                                 12 41'B=IBM FTP  E=Ergo'
     A                                 13  4MSGCON(027 EDI0072 *LIBL/SYMSGF)
     A            ÅNEX      R        O 13 34REFFLD(INEX)
     A                                 13 41'I=Internal  E=External'
     A                                 14  4'Dupkontr. edifact mottak ?:'
     A            ÅNDUPCTL  R        O 14 34REFFLD(INDUPCTL)
     A                                 14 41'J=ja  Y=ja  N=nei  Blank=nei'
     A                                 16  4MSGCON(027 EDI0117 *LIBL/SYMSGF)
     A            ÅNTCHN    R        B 17  4REFFLD(INTCHN)
     A  42                                  DSPATR(PR)
     A                                      CHECK(LC)
     A                                 18  6MSGCON(025 EDI0118 *LIBL/SYMSGF)
     A            ÅNTCUS    R        B 18 34REFFLD(INTCUS)
     A  42                                  DSPATR(PR)
     A                                      CHECK(LC)
     A            ÅNTCPW    R        B 18 85REFFLD(INTCPW)
     A  42                                  DSPATR(PR)
     A                                      CHECK(LC)
     A                                 25  2MSGCON(011 EDI0812 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
      *
     A          R FRITXT
     A                                      WINDOW(*DFT 14 70 *NOMSGLIN)
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/EDI50D'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(030 EDI0816 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            WSFTX01       65A  B  3  2DSPATR(UL)
     A                                      CHECK(LC)
     A            WSFTX02       65A  B  4  2DSPATR(UL)
     A                                      CHECK(LC)
     A            WSFTX03       65A  B  5  2DSPATR(UL)
     A                                      CHECK(LC)
     A            WSFTX04       65A  B  6  2DSPATR(UL)
     A                                      CHECK(LC)
     A            WSFTX05       65A  B  7  2DSPATR(UL)
     A                                      CHECK(LC)
     A            WSFTX06       65A  B  8  2DSPATR(UL)
     A                                      CHECK(LC)
     A            WSFTX07       65A  B  9  2DSPATR(UL)
     A                                      CHECK(LC)
     A            WSFTX08       65A  B 10  2DSPATR(UL)
     A                                      CHECK(LC)
     A            WSFTX09       65A  B 11  2DSPATR(UL)
     A                                      CHECK(LC)
     A            WSFTX10       65A  B 12  2DSPATR(UL)
     A                                      CHECK(LC)
     A N30                             14 22MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        58A  O 14  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A            INID          35A  O  2  2COLOR(WHT)
     A            INNA          30A  O  2 38COLOR(WHT)
     A          R EDI50D1
     A*%%TS  SD  20151124  085032  SVEIN       REL-V7R1M0  5770-WDS
     A                                  1  2'EDI'
     A                                      COLOR(BLU)
     A                                  1 58'UtvekslingsID SKAT'
     A                                      COLOR(WHT)
     A                                  1127'EDI50D'
     A                                      COLOR(BLU)
     A                                  3  2MSGCON(023 EDI0602 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  2'Utvekslingsid . . . . . . . :'
     A            ÅNID      R        O  5 34REFFLD(INID)
     A            ÅN0007    R        O  5 70REFFLD(IN0007)
     A                                  7  4'Vertsnavn/adresse . . . . :'
     A            ÅNTCHN    R        B  7 34REFFLD(INTCHN)
     A  42                                  DSPATR(PR)
     A                                      CHECK(LC)
     A                                  8  4'BrukerID / Passord  . . . :'
     A            ÅNTCUS    R        B  8 34REFFLD(INTCUS)
     A  42                                  DSPATR(PR)
     A                                      CHECK(LC)
     A            ÅNTCPW    R        B  8 85REFFLD(INTCPW)
     A  42                                  DSPATR(PR)
     A                                      CHECK(LC)
     A                                 10  4'Passive mode ?  . . . . . :'
     A            ÅNSPASV        1A  B 10 34VALUES(' ' 'N' 'J' 'Y' 'X')
     A                                 10 46'Y/N/X (X=Slå av extended passive m-
     A                                      ode  SENDEPSV 0)'
     A                                 25  2MSGCON(011 EDI0812 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A*         R FRITXT
     A          R POD
     A*%%TS  SD  20151015  140727  SVEIN       REL-V7R1M0  5770-WDS
     A                                      WINDOW(*DFT 14 70 *NOMSGLIN)
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/EDI50D'
     A                                      COLOR(BLU)
     A            WSPOD01       65A  B  3  2DSPATR(UL)
     A                                      CHECK(LC)
     A            WSPOD02       65A  B  4  2DSPATR(UL)
     A                                      CHECK(LC)
     A            WSPOD03       65A  B  5  2DSPATR(UL)
     A                                      CHECK(LC)
     A            WSPOD04       65A  B  6  2DSPATR(UL)
     A                                      CHECK(LC)
     A            WSPOD05       65A  B  7  2DSPATR(UL)
     A                                      CHECK(LC)
     A            WSPOD06       65A  B  8  2DSPATR(UL)
     A                                      CHECK(LC)
     A            WSPOD07       65A  B  9  2DSPATR(UL)
     A                                      CHECK(LC)
     A            WSPOD08       65A  B 10  2DSPATR(UL)
     A                                      CHECK(LC)
     A            WSPOD09       65A  B 11  2DSPATR(UL)
     A                                      CHECK(LC)
     A            WSPOD10       65A  B 12  2DSPATR(UL)
     A                                      CHECK(LC)
     A N30                             14 22MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        58A  O 14  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A            INID          35A  O  2  2COLOR(WHT)
     A            INNA          30A  O  2 38COLOR(WHT)
     A                                  1 19MSGCON(030 SHU1606 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A*         R POD
     A          R DELETE
     A*%%TS  SD  20220429  095949  JOVO        REL-V7R4M0  5770-WDS
     A                                      WINDOW(*DFT 09 70 *NOMSGLIN)
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/EDI50D'
     A                                      COLOR(BLU)
     A                                  1 20'Sletting av EDI-part'
     A                                      COLOR(WHT)
     A            INID          35A  O  2  2COLOR(WHT)
     A            INNA          30A  O  2 38COLOR(WHT)
     A                                  4  2'Vil du slette denne Edi-parten ? '
     A                                  5  2'Trykk F11 for å bekrefte eller F12-
     A                                       for å avbryte.'
     A                                  7  2'Merk: Før sletting bør evt bruk i -
     A                                      edimnu3 pkt 5 slettes !!!'
     A                                  9  2'F11=Slett '
     A                                      COLOR(BLU)
     A N30                              9 22MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
