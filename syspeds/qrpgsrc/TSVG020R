      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/TSVG020R MODULE(*LIBL/TSVG020R *LIBL/INCGI)
CRTPGM*        ACTGRP(*NEW) AUT(*USE)
      *
      *********************************************************************
      /copy SYSPEDS/qrpgsrcpy,hspecs
      /copy SYSPEDS/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FSAEHD     IF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy SYSPEDS/qrpgsrcpy,prototypeb
      /copy SYSPEDS/qrpgsrcpy,usec
      /copy SYSPEDS/qrpgsrcpy,variables3
      *
      * Varible for
     D WSUSER          S             10
     D AVD             S              4
     D AVDN            S              4S 0
     D SIGN2           S              3
     D OPD             S              7
     D OPDN            S              7S 0
     D DATUM           S              8
     D DATUMN          S              8S 0
     D STATUS          S              1
     D AVSNAVN         S             35
     D MOTNAVN         S             35
     D JSONstring      s          32000
     D Error           S            150
     D AntLest         S              9  0
     D STOPP1          S             35
     D STOPP2          S             35
     D ZB              S              1    DIM(35)

      *get input-string
      /copy syspeds/qrpgsrcpy,prolog3

      * Parse input
     C                   exsr      Parse
      * Open files etc
     C                   EXSR      INIT
      *
     c                   callp     gethtmlifs(
     C                             '/syspeddev/html/JSON.html':
     C                             '/CGITAG/')
     C                   callp     wrtsection('top')
      *
      * ............................................................................................
      * skriv JSON-Object start..(alltid '{' + x ant param. m/komma mellom (IKKE komma etter siste))
      * ............................................................................................
      *
      /free
        JSONstring='{'
        +'¬user¬ : ¬'+%trim(WSUSER)+'¬, '
        +'¬avd¬ : ¬'+%trim(%editc(AVDN:'Z'))+'¬, '
        +'¬sign¬ : ¬'+%trim(SIGN2)+'¬, '
        +'¬opd¬ : ¬'+%trim(%editc(OPDN:'Z'))+'¬, '
        +'¬datum¬ : ¬'+%trim(%editc(DATUMN:'Z'))+'¬, '
        +'¬status¬ : ¬'+%trim(STATUS)+'¬, '
        +'¬avsNavn¬ : ¬'+%trim(AVSNAVN)+'¬, '
        +'¬motNavn¬ : ¬'+%trim(MOTNAVN)+'¬, '
        +'¬errMsg¬ : ¬'+%trim(Error)+'¬, ';
      /end-free
      * JSONfix escaper " i tekst,  for deretter å bytte ¬->"
     c                   call      'JSONFIX'
     c                   parm                    JSONstring
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      *
      * ............................................................................................
      * Skriv JSON-LISTE Start (en liste er EN parameter(fra [ til   )
      * ............................................................................................
     c                   eval      JSONstring ='"sadeksport lista" : ['
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      *
      * Lista uppdrag
      *
     c     Error         ifeq      *blanks
     C     WSETGT        SETGT     SAEHD
     C                   READP     SAEHD                                  55
     C     *IN55         DOWEQ     '0'

     C                   IF        AVDN > 0
     C     SEAVD         CABNE     AVDN          HOPP
     C                   ENDIF
     C                   IF        SIGN2 <> *BLANKS
     C     SESG          CABNE     SIGN2         HOPP
     C                   ENDIF
     C                   IF        OPDN > 0
     C     SETDN         CABNE     OPDN          HOPP
     C                   ENDIF
     C                   IF        DATUMN > 0
     C     SEDT          CABLT     DATUMN        HOPP
     C                   ENDIF
     C                   IF        STATUS <> *BLANKS
     C     STATUS        IFNE      'b'
     C     SEST          CABNE     STATUS        HOPP
     C                   ENDIF
     C     STATUS        IFEQ      'b'
     C     SEST          CABNE     *BLANKS       HOPP
     C                   ENDIF
     C                   ENDIF
     C     AVSNAVN       IFNE      *BLANKS
N01  C     SENAS         CABLT     AVSNAVN       HOPP
N01  C     SENAS         CABGT     STOPP1        HOPP
     C                   ENDIF
     C     MOTNAVN       IFNE      *BLANKS
N01  C     SENAK         CABLT     MOTNAVN       HOPP
N01  C     SENAK         CABGT     STOPP2        HOPP
     C                   ENDIF

     C                   Add       1             AntLest
      * ............................................................................................
      * Skriv en JSON-Array-linje pr menypunkt - komma før hvert nytt element
      * ............................................................................................
      /free
       if AntLest=1;
          JSONstring=*blanks;
          else;
          JSONstring=', ';
          endif;
       JSONstring=%trim(JSONstring)+'{'
          +'¬avd¬ : ¬'+%trim(%editc(SEAVD:'Z'))+'¬, '
          +'¬opd¬ : ¬'+%trim(%editc(SETDN:'Z'))+'¬, '
          +'¬sign¬ : ¬'+%trim(SESG)+'¬, '
          +'¬datum¬ : ¬'+%trim(%editc(SEDT:'Z'))+'¬, '
          +'¬status¬ : ¬'+%trim(SEST)+'¬, '
          +'¬avsNavn¬ : ¬'+%trim(SENAS)+'¬, '
          +'¬motNavn¬ : ¬'+%trim(SENAK)+'¬, '
          +'¬godsnr¬ : ¬'+%trim(SEGN)+'¬}';
      /end-free
     C                   call      'JSONFIX'
     C                   parm                    JSONstring
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')

     C     HOPP          TAG

     C                   READP     SAEHD                                  55
     C                   ENDDO
     c                   endif
      * ............................................................................................
      * Skriv JSON-Liste/Array  Avslutning
      * ............................................................................................
     c                   eval      JSONstring =']'
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      * ............................................................................................
      * Skriv JSON-string Avsluttning
      * ............................................................................................
     c                   eval      JSONstring ='}'
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      *
      * Quit
     C                   exsr      Exit


      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Lukk fil
     C                   CLOSE     BRIDF
     C  N50              CLOSE     SAEHD

      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   MOVE      *ON           *INLR
     C                   return
     C                   endsr
      *
      *********************************************************
     C     Parse         Begsr
      *********************************************************
      * Get input
     C                   EVAL      WSUSER  = zhbgetvar('USER')
     C                   EVAL      AVD     = zhbgetvar('AVD')
     C                   eval      AVDN    = c2n2(AVD)
     C                   EVAL      SIGN2   = zhbgetvar('SIGN')
     C                   EVAL      OPD     = zhbgetvar('OPD')
     C                   EVAL      OPDN    = c2n2(OPD)
     C                   EVAL      DATUM   = zhbgetvar('DATUM')
     C                   EVAL      DATUMN  = c2n2(DATUM)
     C                   EVAL      STATUS  = zhbgetvar('STATUS')
     C                   EVAL      AVSNAVN = zhbgetvar('AVSNAVN')
     C                   EVAL      MOTNAVN = zhbgetvar('MOTNAVN')
      * Er dato= blank eller 0, sett  da dato 7 dager tilbake
     C                   IF        OPDN = *ZEROS
     C                   IF        DATUMN = *ZEROS
     C                   CALL      'SYGE20'
     C                   PARM      'SUB'         UFUNK             3
     C                   PARM      DATUMN        UDAT1             8 0
     C                   PARM                    UDAT2             8 0
     C                   PARM      7             UDGR              5 0
     C                   EVAL      DATUMN=UDAT2
     C                   ENDIF
     C                   ENDIF
      * Sett stop søking alfa
     C                   MOVEA     AVSNAVN       ZB
     C     ZB(1)         IFNE      ' '
     C                   Z-ADD     0             ZAX               2 0
     C     1             DO        35            ZAX               2 0
     C     36            SUB       ZAX           WAX               2 0
     C     ZB(WAX)       CABNE     *BLANKS       SRB3
     C                   MOVE      '9'           ZB(WAX)
     C                   ENDDO
     C                   ENDIF
     C     SRB3          TAG
     C                   MOVEA     ZB            STOPP1
      * Sett stop søking alfa
     C                   MOVEA     *BLANKS       ZB
     C                   MOVEA     MOTNAVN       ZB
     C     ZB(1)         IFNE      ' '
     C                   Z-ADD     0             ZAX               2 0
     C     1             DO        35            ZAX               2 0
     C     36            SUB       ZAX           WAX               2 0
     C     ZB(WAX)       CABNE     *BLANKS       SRB4
     C                   MOVE      '9'           ZB(WAX)
     C                   ENDDO
     C                   ENDIF
     C     SRB4          TAG
     C                   MOVEA     ZB            STOPP2
     c                   endsr
      *
      *********************************************************
     C     INIT          Begsr
      *********************************************************
     C                   OPEN      BRIDF
      * Test innlogging
     C     WSUSER        CHAIN     BRIDF                              50
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C                   call      BILIBL
     C                   end

     C     *LIKE         DEFINE    SEDT          WSETGT
     C                   EVAL      WSETGT = 20991231

     C   50              eval      Error = 'Invalid userID'
     C  N50              OPEN      SAEHD

     c                   endsr
