     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPPNLGRP(HLPSCR SYIN10PNL)
     A                                      HLPTITLE('OPPDRAGSREGISTRERING FLYF-
     A                                      RAKT INNLAND')
     A                                      HELP
     A                                      ALTHELP
     A          R BILDE01
     A                                      CA12(12 'Avslutt program')
     A                                      CF04(04 'Kopierer data fra et oppdr-
     A                                      ag')
     A                                      CF05(05 'Gå direkte til faktura')
     A                                      CF07(07 'SPØØRING OPPDRAG')
     A                                      CF09(09 'Bytt signatur')
     A                                  2 58MSGCON(022 DIV0047 *LIBL/SYMSGF)
     A                                      COLOR(TRQ)
     A                                  3 15MSGCON(045 DIV0596 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  4 15MSGCON(045 DIV0612 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  9 15MSGCON(018 DIV0599 *LIBL/SYMSGF)
     A            WSSG           3A  B  9 35TEXT('Signatur')
     A                                      DSPATR(UL)
     A  99                                  DSPATR(PC)
     A  99                                  DSPATR(RI)
     A                                 11 15MSGCON(018 DIV0600 *LIBL/SYMSGF)
     A            WSAVD          4Y 0B 11 35TEXT('Avdeling')
     A                                      DSPATR(UL)
     A  98                                  DSPATR(PC)
     A  98                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                 13 15MSGCON(018 DIV0601 *LIBL/SYMSGF)
     A            WSOPD          7Y 0B 13 35TEXT('Oppdragsnummer')
     A                                      DSPATR(UL)
     A N96                                  DSPATR(PC)
     A  97                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                 13 50MSGCON(020 DIV0604 *LIBL/SYMSGF)
     A                                 15 15MSGCON(018 DIV0602 *LIBL/SYMSGF)
     A            HEAVD          4Y 0B 15 35TEXT('Avdeling')
     A                                      DSPATR(UL)
     A  96                                  DSPATR(PC)
     A  96                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A            HEOPD          7Y 0B 15 41TEXT('Oppdragsnummer')
     A                                      DSPATR(UL)
     A  96                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                 15 50MSGCON(020 DIV0036 *LIBL/SYMSGF)
     A                                 18 15MSGCON(018 DIV0603 *LIBL/SYMSGF)
     A            WSOPDG         7Y 0O 18 35COLOR(WHT)
     A                                      EDTCDE(4)
     A            WSMSG         62A  O 20 15COLOR(RED)
     A                                      DSPATR(BL)
     A                                 23  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(018 DIV0605 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 42MSGCON(018 DIV0606 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 62MSGCON(018 DIV0033 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A          R BILDE02
     A N31                                  CF02(02 'Samlast')
     A N31                                  CF03(03 'Avslutt registrering av op-
     A                                      pdrag')
     A                                      CF04(04 'SØKING')
     A N31                                  CF05(05 'Gå til flyfraktbrev (ikke -
     A                                      oppdater gammel flyfr.b)')
     A N31                                  CF06(06 'Beregner kubikk')
     A                                      CF07(07 'SPØRRING PÅ OPPDRAG')
     A N31                                  CF09(09 'Tildeler AWBnr')
     A N31                                  CF10(10 'Fjerne fra tur')
     A N31                                  CF08(08 'Gå til flyfraktbrev (oppda-
     A                                      ter gammel flyfr.brev)')
     A                                      CF11(11 'DUP-OPPDRAG')
     A                                      CA12(12 'Avbryt registrering av opp-
     A                                      drag')
     A                                      CF14(14 'Vise opplysning')
     A                                      CF24(24 'FLERE TASTER')
     A                                      CF18(18 'Freetext')
     A                                      CF15(15 'FrI SØK')
     A                                      CF20(20 'sende fax  direkte')
     A                                      CF21(21 'sende fax')
     A                                      CF22(22 'sende FACT booking')
     A  27                                  ALARM
     A                                      CSRLOC(LINNBR     POSNBR)
     A          H                           HLPPNLGRP(SYIN10010 SYIN10PNL)
     A                                      HLPARA(01 001 02 080)
     A          H                           HLPPNLGRP(SYIN10015 SYIN10PNL)
     A                                      HLPARA(03 001 03 032)
     A          H                           HLPPNLGRP(SYIN10020 SYIN10PNL)
     A                                      HLPARA(03 033 03 080)
     A          H                           HLPPNLGRP(SYIN10025 SYIN10PNL)
     A                                      HLPARA(04 001 05 080)
     A          H                           HLPPNLGRP(SYIN10030 SYIN10PNL)
     A                                      HLPARA(06 001 06 080)
     A          H                           HLPPNLGRP(SYIN10035 SYIN10PNL)
     A                                      HLPARA(08 001 08 023)
     A          H                           HLPPNLGRP(SYIN10040 SYIN10PNL)
     A                                      HLPARA(08 024 08 080)
     A          H                           HLPPNLGRP(SYIN10045 SYIN10PNL)
     A                                      HLPARA(09 001 11 080)
     A          H                           HLPPNLGRP(SYIN10050 SYIN10PNL)
     A                                      HLPARA(12 001 15 080)
     A          H                           HLPPNLGRP(SYIN10055 SYIN10PNL)
     A                                      HLPARA(16 001 16 032)
     A          H                           HLPPNLGRP(SYIN10055 SYIN10PNL)
     A                                      HLPARA(17 001 17 032)
     A          H                           HLPPNLGRP(SYIN10055 SYIN10PNL)
     A                                      HLPARA(18 001 18 032)
     A          H                           HLPPNLGRP(SYIN10060 SYIN10PNL)
     A                                      HLPARA(16 033 16 080)
     A          H                           HLPPNLGRP(SYIN10060 SYIN10PNL)
     A                                      HLPARA(17 033 17 080)
     A          H                           HLPPNLGRP(SYIN10060 SYIN10PNL)
     A                                      HLPARA(18 033 18 080)
     A          H                           HLPPNLGRP(SYIN10065 SYIN10PNL)
     A                                      HLPARA(20 026 20 080)
     A          H                           HLPPNLGRP(SYIN10070 SYIN10PNL)
     A                                      HLPARA(21 001 21 080)
     A          H                           HLPPNLGRP(SYIN10999 SYIN10PNL)
     A                                      HLPARA(22 001 24 080)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGLN1         7A  P
     A            MSGLN2         7A  P
     A            MSGLN3         7A  P
     A            MSGLN4         7A  P
     A            MSGNR          7A  P
     A                                  1  2MSGCON(020 DIV0115 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 23MSGCON(004 DIV0071 *LIBL/SYMSGF)
     A            WSAVD          4Y 0B  1 28TEXT('Avdeling')
     A N58                                  DSPATR(PR)
     A N58                                  COLOR(WHT)
     A  58                                  COLOR(YLW)
     A                                      EDTCDE(4)
     A  58                              1 33MSGCON(005 YBC0035 *LIBL/SYMSGF)
     A                                      DSPATR(RI)
     A                                      COLOR(YLW)
     A N58        WSOPD          7Y 0O  1 33TEXT('Oppdragsnummer')
     A N29                                  DSPATR(BL)
     A N29                                  COLOR(RED)
     A  29                                  COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  1 41MSGCON(003 DIV0072 *LIBL/SYMSGF)
     A            WSSG           3A  B  1 45TEXT('Signatur')
     A                                      COLOR(YLW)
     A                                  1 49MSGCON(004 DIV0073 *LIBL/SYMSGF)
     A            WSSTD          4Y 0B  1 54TEXT('Kostnadssted')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  1 59MSGCON(022 DIV0047 *LIBL/SYMSGF)
     A                                      COLOR(TRQ)
     A                                  2  2MSGCON(005 DIV0062 *LIBL/SYMSGF)
     A            WSDT           6Y 0B  2  8DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A  96                                  DSPATR(RI)
     A                                  2 21MSGCON(009 DIV0116 *LIBL/SYMSGF)
     A            WSPK9          1A  B  2 31TEXT('Printerkode-9')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'J' 'N')
     A                                  2 61MSGCON(004 DIV0117 *LIBL/SYMSGF)
     A            WSPRO          8Y 0O  2 66TEXT('Prosjektnummer')
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  3  2MSGCON(004 DIV0079 *LIBL/SYMSGF)
     A            WSSDF          5A  B  3  8TEXT('Frakt fra')
     A                                      DSPATR(UL)
     A  95                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                  3 14MSGCON(004 DIV0080 *LIBL/SYMSGF)
     A            WSSDT          5A  B  3 19TEXT('Frakt til')
     A                                      DSPATR(UL)
     A  94                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                  3 25MSGCON(004 DIV0082 *LIBL/SYMSGF)
     A            WSLKA          2A  B  3 30TEXT('Landkode')
     A                                      DSPATR(UL)
     A  93                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                  3 33MSGCON(006 DIV0613 *LIBL/SYMSGF)
     A            WSSDL         20A  B  3 40TEXT('Lossested')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A                                  3 61MSGCON(004 DIV0078 *LIBL/SYMSGF)
     A            WSMAWB        11S 0O  3 66COLOR(WHT)
     A                                  4  2MSGCON(005 DIV0118 *LIBL/SYMSGF)
     A            WSKNF          3Y 0B  4  8DSPATR(UL)
     A  69                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            KFSNAV        30A  O  4 17COLOR(WHT)
     A                                  4 61MSGCON(004 DIV0081 *LIBL/SYMSGF)
     A            WSHAWB         9A  O  4 66TEXT('House Airway Billno')
     A                                      COLOR(WHT)
     A                                  5  2MSGCON(005 DIV0119 *LIBL/SYMSGF)
     A            WSKNA          8Y 0B  5  8TEXT('Kundenr Agent')
     A                                      DSPATR(UL)
     A  70                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A  75                                  DSPATR(BL)
     A  75                                  COLOR(RED)
     A N75                                  COLOR(YLW)
     A            WSNAA         30A  O  5 17TEXT('Navn Agent')
     A                                      COLOR(WHT)
     A                                  5 61MSGCON(004 DIV0120 *LIBL/SYMSGF)
     A            WSSAML         6A  O  5 66TEXT('Referanse Agent')
     A                                      COLOR(WHT)
     A                                  6  2MSGCON(005 DIV3654 *LIBL/SYMSGF)
     A            WSAWBK         1A  B  6  8COLOR(YLW)
     A            WSAWBN        11A  B  6 11COLOR(YLW)
     A                                  6 25MSGCON(004 DIV0588 *LIBL/SYMSGF)
     A            WSRFA         15A  B  6 31COLOR(YLW)
     A                                  8  2MSGCON(004 DIV0090 *LIBL/SYMSGF)
     A            WSKNS          8Y 0B  8  8TEXT('Kundenr Selger')
     A                                      DSPATR(UL)
     A  67
     AO 68                                  DSPATR(RI)
     A  34                                  DSPATR(PR)
     A                                      EDTCDE(4)
     A  77                                  DSPATR(BL)
     A  77                                  COLOR(RED)
     A N77                                  COLOR(YLW)
     A                                  8 18MSGCON(003 DIV0091 *LIBL/SYMSGF)
     A            WSANS          1Y 0B  8 22TEXT('Avtalenr Selger')
     A                                      DSPATR(UL)
     A  34                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  8 24MSGCON(002 DIV0092 *LIBL/SYMSGF)
     A            WSKDFS         1A  B  8 27TEXT('Fakturakode Selger')
     A                                      DSPATR(UL)
     A  34                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'X' 'B' 'E')
     A                                  8 29MSGCON(003 DIV0093 *LIBL/SYMSGF)
     A            WSVALS         3A  B  8 33TEXT('Valutakode Selger')
     A                                      DSPATR(UL)
     A  66                                  DSPATR(RI)
     A  34                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                  8 37MSGCON(004 DIV0094 *LIBL/SYMSGF)
     A            WSKNSF         8Y 0B  8 42TEXT('Kundenr Selg.fakm')
     A                                      DSPATR(UL)
     A  65                                  DSPATR(RI)
     A  34                                  DSPATR(PR)
     A                                      EDTCDE(4)
     A  99                                  DSPATR(BL)
     A  99                                  COLOR(RED)
     A N99                                  COLOR(YLW)
     A            WSNASF        30A  O  8 51TEXT('Navn Selg.fakm')
     A                                      DSPATR(UL)
     A  99                                  DSPATR(BL)
     A  99                                  COLOR(RED)
     A N99                                  COLOR(WHT)
     A                                  9  2MSGCON(004 DIV0095 *LIBL/SYMSGF)
     A            WSNAS         30A  B  9  8TEXT('Navn Selger')
     A                                      DSPATR(UL)
     A  67                                  DSPATR(RI)
     A  34                                  DSPATR(PR)
     A  77                                  DSPATR(BL)
     A  77                                  COLOR(RED)
     A N77                                  COLOR(YLW)
     A            WSADS1        30A  B  9 42TEXT('Adresse-1 Selger')
     A                                      DSPATR(UL)
     A  34                                  DSPATR(PR)
     A  99                                  DSPATR(BL)
     A  99                                  COLOR(RED)
     A N99                                  COLOR(YLW)
     A            WSADS2        30A  B 10  8TEXT('Adresse-3 Selger')
     A                                      DSPATR(UL)
     A  34                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A            WSADS3        30A  B 10 42TEXT('Adresse-2 Selger')
     A                                      DSPATR(UL)
     A  34                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                 12  2MSGCON(004 DIV0096 *LIBL/SYMSGF)
     A            WSKNK          8Y 0B 12  8TEXT('Kundenr Kjøper')
     A                                      DSPATR(UL)
     A  62
     AO 61                                  DSPATR(RI)
     A  32                                  DSPATR(PR)
     A                                      EDTCDE(4)
     A  76                                  DSPATR(BL)
     A  76                                  COLOR(RED)
     A N76                                  COLOR(YLW)
     A                                 12 18MSGCON(003 DIV0091 *LIBL/SYMSGF)
     A            WSANK          1Y 0B 12 22TEXT('Avtalenr Kjøper')
     A                                      DSPATR(UL)
     A  32                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 12 24MSGCON(002 DIV0092 *LIBL/SYMSGF)
     A            WSKDFK         1A  B 12 27TEXT('Fakturakode Kjøper')
     A                                      DSPATR(UL)
     A  32                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'X' 'B' 'E')
     A                                 12 29MSGCON(003 DIV0093 *LIBL/SYMSGF)
     A            WSVALK         3A  B 12 33TEXT('Valutakode Kjøper')
     A                                      DSPATR(UL)
     A  60                                  DSPATR(RI)
     A  32                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                 12 37MSGCON(004 DIV0094 *LIBL/SYMSGF)
     A            WSKNKF         8Y 0B 12 42TEXT('Kundenr Kjøp.fakm')
     A                                      DSPATR(UL)
     A  59                                  DSPATR(RI)
     A  32                                  DSPATR(PR)
     A                                      EDTCDE(4)
     A  98                                  DSPATR(BL)
     A  98                                  COLOR(RED)
     A N98                                  COLOR(YLW)
     A            WSNAKF        30A  O 12 51TEXT('Navn Kjøp.fakm')
     A                                      DSPATR(UL)
     A  98                                  DSPATR(BL)
     A  98                                  COLOR(RED)
     A N98                                  COLOR(WHT)
     A                                 13  2MSGCON(004 DIV0097 *LIBL/SYMSGF)
     A            WSNAK         30A  B 13  8TEXT('Navn Kjøper')
     A                                      DSPATR(UL)
     A  62                                  DSPATR(RI)
     A  32                                  DSPATR(PR)
     A  76                                  DSPATR(BL)
     A  76                                  COLOR(RED)
     A N76                                  COLOR(YLW)
     A            WSADK1        30A  B 13 42TEXT('Adresse-1 Kjøper')
     A                                      DSPATR(UL)
     A  32                                  DSPATR(PR)
     A  98                                  DSPATR(BL)
     A  98                                  COLOR(RED)
     A N98                                  COLOR(YLW)
     A            WSADK2        30A  B 14  8TEXT('Adresse-2 Kjøper')
     A                                      DSPATR(UL)
     A  32                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A            WSADK3        30A  B 14 42TEXT('Adresse-3 Kjøper')
     A                                      DSPATR(UL)
     A  32                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                 16  2MSGCON(075 DIV0121 *LIBL/SYMSGF)
     A            WSOT           2A  B 17  2TEXT('Oppdragstype')
     A                                      DSPATR(UL)
     A  89                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSFR           3A  B 17  5TEXT('Frankatur')
     A                                      DSPATR(UL)
     A  88                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSGM1         15A  B 17  9TEXT('Godsmerking-1')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSNT           7Y 0B 17 25TEXT('Antall')
     A                                      DSPATR(UL)
     A  87                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSVS1         25A  B 17 33TEXT('Vareslag-1')
     A                                      DSPATR(UL)
     A  86                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSVKT          9Y 0B 17 59TEXT('Vekt')
     A                                      DSPATR(UL)
     A  85                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSM3           7Y 3B 17 69TEXT('Kubikk')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSGM2         15A  B 18  9TEXT('Godsmerking-2')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSVS2         25A  B 18 33TEXT('Vareslag-2')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 20  2MSGCON(020 DIV5101 *LIBL/SYMSGF)
     A                                 20 54MSGCON(005 DIV0100 *LIBL/SYMSGF)
     A            WSKDL         15A  B 20 60TEXT('Lagerkode')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSPK3          1A  B 21  5TEXT('Printerkode-3')
     A                                      DSPATR(UL)
     A  82                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'J' 'N' 'P')
     A            WSPK5          1A  B 21 10TEXT('Printerkode-5')
     A                                      DSPATR(UL)
     A  80                                  DSPATR(RI)
     A                                      VALUES(' ' 'J' 'N' 'P')
     A                                      COLOR(YLW)
     A            WSPK1          1A  B 21 16COLOR(YLW)
     A  81                                  DSPATR(RI)
     A            WSMSG         62A  O 21 19COLOR(RED)
     A                                      DSPATR(BL)
     A                                 22  2MSGCON(009 DIV0607 *LIBL/SYMSGF)
     A            WSOPDG         7Y 0O 22 12DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            MSGL1         18A  O 23  2MSGID(&MSGLN1 SYMSGF)
     A                                      COLOR(BLU)
     A            MSGL2         18A  O 23 22MSGID(&MSGLN2 SYMSGF)
     A                                      COLOR(BLU)
     A            MSGL3         18A  O 23 42MSGID(&MSGLN3 SYMSGF)
     A                                      COLOR(BLU)
     A            MSGL4         18A  O 23 62MSGID(&MSGLN4 SYMSGF)
     A                                      COLOR(BLU)
     A            MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
