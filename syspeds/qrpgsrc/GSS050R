********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10027 * 01 YBC PTF10 ORDRETYPE
10217 * 02 YBC PTF10 FLERE DEFAULT PRINTER
xxxxxx*xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
11XXX * 03 YBC PTF11 SØK PÅ DATO
12XXX * 04 YBC PTF12 To ekstra parameter ved kall GSS034C
********************************************************************
      *
      **************************************************************
      *** PROGRAM SKAL SKRIVE UT KOPI AV PACK.LISTE, PROFORMA, FAKT.GR.
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER
      *** LEDIGE INDIKATORER 01 02 04 06-22 24
      *** LEDIGE INDIKATORER 27-29 32 34-40 48-89 95-97
      **************************************************************
      *
     FHEADF     IF   E           K DISK
     FGSSBXF    IF   E           K DISK
     FGSSTRF    UF   E           K DISK
     FGSSTRL31  IF   E           K DISK    RENAME(GSSTRFR:GSSTRF31)
     FGSSTRL32  IF   E           K DISK    RENAME(GSSTRFR:GSSTRF32)
     FGSSTRL33  IF   E           K DISK    RENAME(GSSTRFR:GSSTRF33)
     FGSSTRL34  IF   E           K DISK    RENAME(GSSTRFR:GSSTRF34)
     FGSSTRL35  IF   E           K DISK    RENAME(GSSTRFR:GSSTRF35)
     FGSSTRL36  IF   E           K DISK    RENAME(GSSTRFR:GSSTRF36)
N03  FGSSTRL37  IF   E           K DISK    RENAME(GSSTRFR:GSSTRF37)
     FGSSBIF    IF   E           K DISK
     FGSSCGF    IF   E           K DISK
     FFIRM      IF   E           K DISK
     FVALUL01   IF   E           K DISK
     FCUNDL01   IF   E           K DISK
     FGSS050D   CF   E             WORKSTN
     F                                     SFILE(GSS050DA:ZRECNO)
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D                 DS
     D XFT01           C                   CONST('DEL. BY:')
     D XFT02           C                   CONST('TRUCK NO:')
     D XFT03           C                   CONST('AWBNO:')
     D                 DS
     D  XFT11                  1     10
     D  XFT12                 13     27
     D  WSFT1                  1     70
     D                 DS
     D  XFT21                  1     10
     D  XFT22                 13     19
     D  XFT23                 20     20
     D  XFT24                 21     28
     D  XFT25                 13     27
     D  WSFT2                  1     70
     D                 DS
     D  TRAD1                  1     30
     D  XTRAD1                 1     28
     D                 DS
EV5  D  TRDTLÅ                 1      4  0
EV5  D  TRDTLM                 5      6  0
EV5  D  TRDTLD                 7      8  0
EV5  D  TRDTL                  1      8  0
     D                 DS
     D  WXDTLD                 1      2  0
     D  WXDTLM                 3      4  0
     D  WXDTLÅ                 5      6  0
     D  WXDTL                  1      6  0
     D                 DS
     D  TRDTSÅ                 1      4  0
     D  TRDTSM                 5      6  0
     D  TRDTSD                 7      8  0
     D  TRDTS                  1      8  0
     D                 DS
     D  WXDTSD                 1      2  0
     D  WXDTSM                 3      4  0
     D  WXDTSÅ                 5      6  0
     D  WXDTS                  1      6  0
N03  D                 DS
N03  D  W2DTLD                 1      2  0
N03  D  W2DTLM                 3      4  0
N03  D  W2DTLA                 5      6  0
N03  D  W2DTL                  1      6  0
     D                SDS
     D  UUSER                254    263
     D SAVUDS         UDS
     D  UFIRM                  1      2
     D  UCURR                  3      5
     D  UBIUSI               101    110
     D  UBICGR               111    112
     D  UBICNO               113    120  0
     D  UBIFC                121    123
     D  UBIPRT               124    133
     D  UBIPRM               134    143
N02  D  UBIPRP               149    158
N02  D  UBIPRI               159    168
      *
     C                   EXSR      INIT
     C   33              GOTO      SLUTT
      *
     C     START         TAG
     C                   MOVE      '1'           *IN(93)
     C                   WRITE     GSS050DB
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
     C                   MOVE      *BLANKS       WSNR
     C                   MOVE      *ZEROS        XTNO             11 0
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C                   EXSR      SETKEY
     C     START1        TAG
     C                   EXSR      LESREC
      *
     C     START2        TAG
      *
     C  N94              DO
     C                   DO        15
     C                   ADD       1             ZRECNO
     C                   MOVE      TRDTLD        WXDTLD
     C                   MOVE      TRDTLM        WXDTLM
     C                   MOVE      TRDTLÅ        WXDTLÅ
     C                   MOVE      TRDTSD        WXDTSD
     C                   MOVE      TRDTSM        WXDTSM
     C                   MOVE      TRDTSÅ        WXDTSÅ
     C                   WRITE     GSS050DA
     C                   MOVE      ZRECNO        XRECNO            5 0
     C                   EXSR      LESREC
     C  N94              END
     C                   END
      *
     C     START3        TAG
     C  N30              Z-ADD     5             LINNBR
     C  N30              Z-ADD     2             POSNBR
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     GSS050DG
     C                   ELSE
     C                   WRITE     GSS050DC
     C                   EXFMT     GSS050DB
     C                   END
      *
      *** AVSLUTT
     C                   MOVE      '0'           *IN30
     C                   MOVEA     '00'          *IN(98)
      *
     C   03              GOTO      SLUTT
     C   04              DO
     C                   EXSR      SØKDTA
     C     W2BCD         IFEQ      *BLANKS
     C     W2CNO         ANDEQ     0
     C                   GOTO      START3
     C                   END
     C                   END
     C   05              GOTO      START
      *
      *** BLA VIDERE
     C   25              DO
     C                   Z-ADD     XRECNO        ZRECNO
     C  N94XTNO          CHAIN(N)  GSSTRF                             25
     C                   SETOFF                                       25
     C                   GOTO      START2
     C                   END
      *
     C     W2NR          IFNE      *BLANKS                                      1<-B
     C     W2TNO         ORNE      0
     C     W2BCD         ORNE      *BLANKS
     C     W2CNO         ORNE      0
N03  C     W2DTL         ORNE      0
     C     W2NR          IFEQ      *BLANKS                                       2<-B
     C                   EXSR      NYSØK
     C                   GOTO      START
     C                   ELSE                                                    2
     C     W2TNO         IFEQ      0                                              3<-B
     C                   SETON                                        9830
     C                   MOVE      'GSS0047'     MSGNR
     C                   ELSE                                                     3
     C                   EXSR      NYOPT
     C                   END                                                      3<-E
     C                   MOVE      *ZEROS        W2TNO
     C                   MOVE      *BLANKS       W2BCD
     C                   MOVE      *ZEROS        W2CNO
N03  C                   MOVE      *ZEROS        W2DTL
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     2             POSNBR
     C                   GOTO      START3
     C                   END                                                     2<-E
     C                   END                                                    1<-E
      *
     C     ZRECNO        IFNE      0
     C                   EXSR      HHENT
     C                   END
     C                   GOTO      START3
      *
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
      *
     C***********************************************
     C     TESTB         BEGSR
     C***********************************************
     C     YTNO          COMP      0                                  4141
     C   41              GOTO      SLTB2
     C     YBCD          COMP      *BLANKS                            4242
     C   42              GOTO      SLTB2
     C     YCNO          COMP      0                                  4343
     C   43              GOTO      SLTB2
N03  C     YDTL          COMP      0                                  4848
N03  C   48              GOTO      SLTB2
      *
     C     SLTB2         TAG
     C   41UBICNO        IFNE      0
     C                   MOVE      '0'           *IN41
     C                   MOVE      '1'           *IN44
     C                   ELSE
     C     UBICGR        IFNE      *BLANKS
     C                   MOVE      '0'           *IN41
     C                   MOVE      '1'           *IN46
     C                   END
     C                   END
     C   42UBICNO        IFNE      0
     C                   MOVE      '0'           *IN42
     C                   MOVE      '1'           *IN45
     C                   ELSE
     C     UBICGR        IFNE      *BLANKS
     C                   MOVE      '0'           *IN42
     C                   MOVE      '1'           *IN47
     C                   END
     C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     NYSØK         BEGSR
     C***********************************************
     C                   MOVE      *ZEROS        YTNO
     C                   MOVE      *BLANKS       YBCD
     C                   MOVE      *ZEROS        YCNO
N03  C                   MOVE      *ZEROS        YDTL
     C     W2TNO         IFNE      0
     C                   MOVE      W2TNO         YTNO
     C                   END
     C     W2BCD         IFNE      *BLANKS
     C                   MOVE      W2BCD         YBCD
     C                   END
     C     W2CNO         IFNE      YCNO
     C     UBICNO        IFNE      0
     C                   MOVE      UBICNO        W2CNO
     C                   END
     C                   MOVE      W2CNO         YCNO
     C                   END
N03  C     W2DTL         IFNE      0
N03  C                   EVAL      YDTL = 20000000 + W2DTLA * 10000
N03  C                             + W2DTLM * 100 + W2DTLD
N03  C                   END
      *
     C                   MOVE      *ZEROS        W2TNO
     C                   MOVE      *BLANKS       W2BCD
     C                   MOVE      *ZEROS        W2CNO
N03  C                   MOVE      *ZEROS        W2DTL
      *
     C                   MOVEA     '0000000'     *IN(41)
     C                   EXSR      TESTB
      *
     C                   ENDSR
      *
     C***********************************************
     C     NYOPT         BEGSR
     C***********************************************
     C     W2TNO         CHAIN(N)  GSSTRF                             98
     C  N98TRST          IFNE      '6'
     C     TRST          COMP      'D'                                9898
     C                   END
     C   98              MOVE      'GSS0048'     MSGNR
     C   98              GOTO      SLOPT
     C     UBICGR        IFNE      *BLANKS                                      1<-B
     C     TRCNO         CHAIN     GSSCGF                             98
     C  N98CGCG          COMP      UBICGR                             9898
     C   98              MOVE      'GSS0029'     MSGNR
     C   98              GOTO      SLOPT
     C                   END                                                    1<-E
     C     UBICNO        IFNE      0                                            1<-B
     C     UBICNO        ANDNE     TRCNO
     C                   MOVE      '1'           *IN98
     C                   MOVE      'GSS0030'     MSGNR
     C                   GOTO      SLOPT
     C                   END                                                    1<-E
     C                   MOVE      '1'           *IN90
      *
     C                   SELECT
     C     W2NR          WHENEQ    '2'
     C                   EXSR      EDTTR
     C     W2NR          WHENEQ    '5'
     C                   EXSR      DSPTR
     C     W2NR          WHENEQ    '6'
     C                   EXSR      CPYPL
     C     W2NR          WHENEQ    '7'
     C                   EXSR      CPYPI
     C     W2NR          WHENEQ    '8'
     C                   EXSR      CPYIB
     C     W2NR          WHENEQ    'P'
     C                   EXSR      PLKTR
     C     W2NR          WHENEQ    'A'
     C                   EXSR      DOCUMO
     C     W2NR          WHENEQ    'I'
     C                   EXSR      BOXINF
     C                   OTHER
     C                   MOVE      'GSS0009'     MSGNR
     C                   MOVE      '1'           *IN98
     C                   GOTO      SLOPT
     C                   ENDSL
      *
     C     SLOPT         TAG
     C   98              MOVE      '1'           *IN30
     C                   MOVE      '0'           *IN90
     C                   MOVE      *BLANKS       W2NR
      *
     C                   ENDSR
      *
     C***********************************************
     C     HHENT         BEGSR
     C***********************************************
     C                   MOVE      *BLANKS       MSGNR
     C                   MOVEA     '000'         *IN(90)
     C                   MOVE      '0'           *IN99
     C     *IN98         DOWEQ     '0'
     C                   READC     GSS050DA                               98
     C   98              DO
     C   99              MOVE      '0'           *IN98
     C                   GOTO      SLHENT
     C                   END
      *
     C     ZRECNO        DIV       15            LINNBR
     C                   MVR                     XLIN              1 0
     C     XLIN          IFEQ      0
     C                   Z-ADD     15            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
      *
     C                   SELECT
     C     WSNR          WHENEQ    '2'
     C                   EXSR      EDTTR
     C     WSNR          WHENEQ    '5'
     C                   EXSR      DSPTR
     C     WSNR          WHENEQ    '6'
     C                   EXSR      CPYPL
     C     WSNR          WHENEQ    '7'
     C                   EXSR      CPYPI
     C     WSNR          WHENEQ    '8'
     C                   EXSR      CPYIB
     C     WSNR          WHENEQ    'P'
     C                   EXSR      PLKTR
     C     WSNR          WHENEQ    'A'
     C                   EXSR      DOCUMO
     C     WSNR          WHENEQ    'B'
     C                   EXSR      PRTBML
     C     WSNR          WHENEQ    'C'
     C                   EXSR      CHGST
     C     WSNR          WHENEQ    'I'
     C                   EXSR      BOXINF
     C                   OTHER
     C                   MOVE      '1'           *IN98
     C                   MOVE      *BLANKS       WSNR
     C                   UPDATE    GSS050DA
     C                   GOTO      SLHENT
     C                   ENDSL
     C                   MOVE      '1'           *IN99
      *
     C                   END
      *
     C     SLHENT        TAG
     C                   MOVE      '0'           *IN99
     C   98MSGNR         IFEQ      *BLANKS
     C                   MOVE      'GSS0009'     MSGNR
     C                   END
     C   98              MOVE      '1'           *IN30
     C  N30              Z-ADD     5             LINNBR
     C   30              ADD       5             LINNBR
     C                   Z-ADD     2             POSNBR
      *
     C                   ENDSR
      *
     C***********************************************
     C     SETKEY        BEGSR
     C***********************************************
     C   41YTNO          IFEQ      99999999999
     C     GTRK01        SETGT     GSSTRL31
     C                   ELSE
     C     GTRK01        SETLL     GSSTRL31
     C                   END
     C   42GTRK02        SETGT     GSSTRL32
     C   43GTRK03        SETGT     GSSTRL33
     C   44YTNO          IFEQ      99999999999
     C     GTRK04        SETGT     GSSTRL33
     C                   ELSE
     C     GTRK04        SETLL     GSSTRL33
     C                   END
     C   45GTRK05        SETGT     GSSTRL34
     C   46YTNO          IFEQ      99999999999
     C     GTRK06        SETGT     GSSTRL35
     C                   ELSE
     C     GTRK06        SETLL     GSSTRL35
     C                   END
     C   47GTRK07        SETGT     GSSTRL36
N03  C   48YDTL          SETLL     GSSTRL37
      *
     C                   ENDSR
      *
     C***********************************************
     C     LESREC        BEGSR
     C***********************************************
     C     STLF          TAG
     C   41YTNO          IFEQ      99999999999
     C                   READP     GSSTRL31                               94
     C                   ELSE
     C                   READ      GSSTRL31                               94
     C                   END
     C   42YBCD          READPE    GSSTRL32                               94
     C   43YCNO          READPE    GSSTRL33                               94
     C   44YTNO          IFEQ      99999999999
     C     UBICNO        READPE    GSSTRL33                               94
     C                   ELSE
     C     UBICNO        READE     GSSTRL33                               94
     C                   END
     C   45GTRX05        READPE    GSSTRL34                               94
     C   46YTNO          IFEQ      99999999999
     C     UBICGR        READPE    GSSTRL35                               94
     C                   ELSE
     C     UBICGR        READE     GSSTRL35                               94
     C                   END
     C   47GTRX07        READPE    GSSTRL36                               94
N03  C   48              READ      GSSTRL37                               94
     C  N94              MOVE      TRTNO         XTNO
      *
     C                   ENDSR
      *
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     GTRK01        KLIST
     C                   KFLD                    YTNO
     C     *LIKE         DEFINE    TRTNO         YTNO
     C     GTRK02        KLIST
     C                   KFLD                    YBCD
     C                   KFLD                    YTNO2
     C     *LIKE         DEFINE    TRBCD         YBCD
     C     *LIKE         DEFINE    TRTNO         YTNO2
     C                   Z-ADD     99999999999   YTNO2
     C     GTRK03        KLIST
     C                   KFLD                    YCNO
     C                   KFLD                    YTNO2
     C     *LIKE         DEFINE    TRCNO         YCNO
     C     GTRK04        KLIST
     C                   KFLD                    UBICNO
     C                   KFLD                    YTNO
     C     GTRK05        KLIST
     C                   KFLD                    UBICNO
     C                   KFLD                    YBCD
     C                   KFLD                    YTNO2
     C     GTRX05        KLIST
     C                   KFLD                    UBICNO
     C                   KFLD                    YBCD
     C     GTRK06        KLIST
     C                   KFLD                    UBICGR
     C                   KFLD                    YTNO
     C     GTRK07        KLIST
     C                   KFLD                    UBICGR
     C                   KFLD                    YBCD
     C                   KFLD                    YTNO2
     C     GTRX07        KLIST
     C                   KFLD                    UBICGR
     C                   KFLD                    YBCD
     C     KODVAK        KLIST
     C                   KFLD                    UFIRM
     C                   KFLD                    WSCUR
     C     CUND1K        KLIST
     C                   KFLD                    UFIRM
     C                   KFLD                    WSKNC
      *
N03  C     *LIKE         DEFINE    TRDTL         YDTL
N03  C                   MOVE      *ZEROS        W2DTL
     C                   MOVE      '2'           UMODE             1
     C                   MOVE      'P'           WSNYL
     C                   MOVE      'N'           WSFAX
     C                   MOVE      'N'           WSCOMM
     C                   Z-ADD     1             WSCPY
     C     UBIUSI        CHAIN     GSSBIF                             33
     C     *IN33         IFEQ      '0'
     C     FIFIRM        SETLL     FIRM
     C                   READ      FIRM                                   33
     C                   MOVE      *ZEROS        WXDTL
     C                   MOVE      *ZEROS        WXDTS
     C                   MOVE      *ZEROS        TRDTL
     C                   MOVE      *ZEROS        TRDTS
     C     *DTAARA       DEFINE    *LDA          SAVUDS
     C                   IN        SAVUDS
     C                   MOVE      FIFIRM        UFIRM
     C                   MOVE      FICURR        UCURR
     C                   MOVE      BICGR         UBICGR
     C                   MOVE      BICNO         UBICNO
     C                   MOVE      BIFC          UBIFC
     C                   MOVE      BIPRT         UBIPRT
     C                   MOVE      BIPRTM        UBIPRM
N02  C                   MOVE      BIPRTP        UBIPRP
N02  C                   MOVE      BIPRTI        UBIPRI
     C                   OUT       SAVUDS
S02  C*                  MOVE      BIPRT         WSPRT
     C                   MOVE      BIPRTM        WSPRTM
     C                   Z-ADD     99999999999   YTNO
     C                   EXSR      TESTB
     C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     EDTTR         BEGSR
     C***********************************************
     C  N90              MOVE      '1'           *IN99
     C  N90              MOVE      *BLANKS       WSNR
     C  N90              UPDATE    GSS050DA
      *
      * MAN KAN IKKE ARBEIDE MED TURENSINNHOLD VED TRANSITTERING
     C     TRBCD         IFNE      *BLANKS
     C     TRLOCF        ANDEQ     *BLANKS
     C                   Z-ADD     2             UFLAGG
     C                   MOVE      'EDT'         UTYPE
     C                   MOVE      '2'           UMODE
     C                   MOVE      TRTNO         UTNO
     C                   CALL      'GSS023R'
     C                   PARM                    UTNO
     C                   PARM                    UTYPE
     C                   PARM                    UMODE
     C                   PARM                    UFLAGG            1 0
     C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     DSPTR         BEGSR
     C***********************************************
     C  N90              MOVE      '1'           *IN99
     C  N90              MOVE      *BLANKS       WSNR
     C  N90              UPDATE    GSS050DA
      *
     C                   MOVE      'DSP'         UTYPE             3
     C                   MOVE      TRTNO         UTNO
     C     *LIKE         DEFINE    TRTNO         UTNO
     C                   CALL      'GSS021R'
     C                   PARM                    UTNO
     C                   PARM                    UTYPE
     C                   PARM                    UMODE
      *
     C                   ENDSR
      *
     C***********************************************
     C     CPYPL         BEGSR
     C***********************************************
     C                   MOVE      '0'           *IN99
     C                   MOVE      '1'           *IN31
     C                   MOVE      'GSS0058'     MSGHN
N01  C                   MOVE      ' '           WSTYPE
N02  C                   IF        WSPRT = *BLANKS
N02  C                   EVAL      WSPRT = BIPRT
N02  C                   END
     C     STCPYL        TAG
     C                   EXFMT     GSS050DX
     C                   MOVE      '0'           *IN99
     C   12              GOTO      SLCPYL
     C     WSPRT         COMP      *BLANKS                                99
     C   99              GOTO      STCPYL
      *
     C                   MOVE      'CPY'         UTYPE
     C                   MOVE      TRCNO         UCNO2             8
     C                   MOVE      TRTNO         UTNO2            11
      *
     C     WSFAX         IFEQ      'Y'
     C                   MOVE      '1'           UMOD
     C                   CALL      'GSS034C'
     C                   PARM                    UCNO2
     C                   PARM                    UUTFAX            1
     C                   PARM                    UTNO2
     C                   PARM                    UTYPE
N01  C                   PARM                    WSTYPE
     C                   PARM                    WSCUR
     C                   PARM                    UMOD
     C                   PARM                    WSCOMM
     C                   PARM                    UKNC
     C     UUTFAX        CABEQ     'J'           STCPL2
     C                   GOTO      STCPYL
     C                   END
      *
     C     WSCPY         IFEQ      0
     C                   Z-ADD     1             WSCPY
     C                   END
     C                   MOVE      WSCPY         UCPY              1
      *
     C                   CALL      'GSS022R'
     C                   PARM                    UTNO2
     C                   PARM                    UTYPE
     C                   PARM                    UCPY
     C                   PARM                    WSPRT
     C                   PARM                    WSFAX
      *
     C     STCPL2        TAG
     C  N90              MOVE      '1'           *IN99
     C  N90              MOVE      *BLANKS       WSNR
     C  N90              UPDATE    GSS050DA
      *
     C     SLCPYL        ENDSR
      *
     C***********************************************
     C     CPYPI         BEGSR
     C***********************************************
     C                   MOVEA     '00'          *IN(98)
     C                   MOVE      '0'           *IN31
     C                   MOVE      'GSS0056'     MSGHN
N01  C                   MOVE      ' '           WSTYPE
N02  C                   IF        WSPRT = *BLANKS
N02  C                   EVAL      WSPRT = BIPRTI
N02  C                   END
     C                   MOVE      UCURR         WSCUR
     C     TRTNO         CHAIN(N)  GSSTRF                             33
     C     TRMFT1        IFEQ      *BLANKS
     C     HEADFK        KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C                   MOVEL     TRTNO         HEAVD
     C                   MOVE      TRTNO         HEOPD
     C     HEADFK        CHAIN     HEADF                              99
     C                   MOVE      *BLANKS       WSFT1
     C                   MOVE      *BLANKS       WSFT2
     C                   MOVEL     XFT01         XFT11
     C                   MOVE      TRDW          XFT12
     C     HEUR          IFEQ      'A'
     C     HEUR          OREQ      'B'
     C                   MOVEL     XFT02         XFT21
     C                   MOVEL     HEGN          XFT25
     C                   ELSE
     C                   MOVEL     XFT03         XFT21
     C                   MOVEL     HEGN          XFT22
     C                   MOVE      '-'           XFT23
     C                   MOVE      HEGN          XFT24
     C                   END
     C                   ELSE
     C                   MOVE      TRMFT1        WSFT1
     C                   MOVE      TRMFT2        WSFT2
     C                   END
      *
     C     STCPYI        TAG
     C                   EXFMT     GSS050DY
     C                   MOVEA     '00'          *IN(98)
     C   04              DO
     C                   MOVE      *BLANKS       USØKKN           30
     C                   CALL      'SOEKKUN'
     C     WSKNC         PARM      WSKNC         UKNC80            8 0
     C                   PARM                    UFIRM
     C                   PARM                    USØKKN
     C                   GOTO      STCPYI
     C                   END
     C   12              GOTO      SLCPYI
     C     WSPRT         COMP      *BLANKS                                99
     C   99              GOTO      STCPYI
     C     KODVAK        CHAIN     VALUL01                            98
     C   98              GOTO      STCPYI
     C     WSCOMM        IFEQ      'Y'
     C     WSKNC         ANDNE     0
     C     CUND1K        CHAIN     CUNDL01                            97
     C   97              GOTO      STCPYI
     C                   END
      *
     C     TRTNO         CHAIN     GSSTRF                             33
     C                   MOVE      WSFT1         TRMFT1
     C                   MOVE      WSFT2         TRMFT2
     C                   UPDATE    GSSTRFR
     C                   SELECT
     C     WSNYL         WHENEQ    'P'
     C     WSNYL         OREQ      'O'
     C                   MOVE      'CPY'         UTYPE
     C     WSNYL         WHENEQ    'L'
     C                   MOVE      'CPL'         UTYPE
     C                   ENDSL
     C                   MOVE      WSKNC         UKNC              8
     C                   MOVE      TRCNO         UCNO2             8
     C                   MOVE      TRTNO         UTNO2            11
      *
     C     WSFAX         IFEQ      'Y'
     C     WSNYL         IFEQ      'P'
     C     WSNYL         OREQ      'L'
     C                   MOVE      '2'           UMOD              1
     C                   ELSE
     C                   MOVE      '3'           UMOD
     C                   END
     C                   CALL      'GSS034C'
     C                   PARM                    UCNO2
     C                   PARM                    UUTFAX            1
     C                   PARM                    UTNO2
     C                   PARM                    UTYPE
N01  C                   PARM                    WSTYPE
     C                   PARM                    WSCUR
     C                   PARM                    UMOD
     C                   PARM                    WSCOMM
     C                   PARM                    UKNC
     C     UUTFAX        CABEQ     'J'           STCPI2
     C                   GOTO      STCPYI
     C                   END
      *
     C     WSCPY         IFEQ      0
     C                   Z-ADD     1             WSCPY
     C                   END
     C                   MOVE      WSCPY         UCPY              1
      *
     C     WSNYL         IFEQ      'P'
     C     WSNYL         OREQ      'L'
     C     TRTNO         CHAIN     GSSBXF                             33
     C  N33BXCN          COMP      *BLANKS                                33
     C                   IF        *IN33
     C                   EVAL      WSPOS1 = *BLANKS
     C                   ELSE
     C                   EVAL      UTYPE  = 'CPC'
     C                   EVAL      WSPOS1 = 'CT'
     C                   END
     C                   CALL      'GSS031R'
     C                   PARM                    UTNO2
     C                   PARM                    UTYPE
     C                   PARM                    WSPRT
N01  C                   PARM                    WSTYPE
     C                   PARM                    WSCUR
     C                   PARM                    UCPY
     C                   PARM                    WSFAX
     C                   PARM                    WSCOMM
     C                   PARM                    UKNC
     C                   PARM                    UQJOB            10
     C                   PARM                    UQUSER           10
     C                   PARM                    UQNBR             6
     C                   PARM                    WSPOS1            5
     C                   PARM                    WSPOS2            5
     C                   PARM                    WSPOS3            5
     C                   ELSE
     C                   CALL      'GSS033R'
     C                   PARM                    UTNO2
     C                   PARM                    UTYPE
     C                   PARM                    WSPRT
N01  C                   PARM                    WSTYPE
     C                   PARM                    UCPY
     C                   PARM                    WSFAX
     C                   END
      *
     C     STCPI2        TAG
     C  N90              MOVE      '1'           *IN99
     C  N90              MOVE      *BLANKS       WSNR
     C  N90              UPDATE    GSS050DA
      *
     C     SLCPYI        ENDSR
      *
     C***********************************************
     C     CPYIB         BEGSR
     C***********************************************
     C                   MOVE      '0'           *IN99
     C                   MOVE      '1'           *IN31
     C                   MOVE      'GSS0059'     MSGHN
N01  C                   MOVE      ' '           WSTYPE
N02  C                   IF        WSPRT = *BLANKS
N02  C                   EVAL      WSPRT = BIPRT
N02  C                   END
     C     STCPYB        TAG
     C                   EXFMT     GSS050DX
     C                   MOVE      '0'           *IN99
     C   12              GOTO      SLCPYB
     C     WSPRT         COMP      *BLANKS                                99
     C   99              GOTO      STCPYB
      *
     C                   MOVE      'CPY'         UTYPE
     C                   MOVE      TRCNO         UCNO2             8
     C                   MOVE      TRTNO         UTNO2            11
      *
     C     WSFAX         IFEQ      'Y'
     C                   MOVE      '4'           UMOD
     C                   CALL      'GSS034C'
     C                   PARM                    UCNO2
     C                   PARM                    UUTFAX            1
     C                   PARM                    UTNO2
     C                   PARM                    UTYPE
N01  C                   PARM                    WSTYPE
     C                   PARM                    WSCUR
     C                   PARM                    UMOD
N04  C                   PARM                    UCOMM             1
N04  C                   PARM                    UKNC              8
     C     UUTFAX        CABEQ     'J'           STCPB2
     C                   GOTO      STCPYB
     C                   END
      *
     C     WSCPY         IFEQ      0
     C                   Z-ADD     1             WSCPY
     C                   END
     C                   MOVE      WSCPY         UCPY              1
      *
     C                   CALL      'GSS041R'
     C                   PARM                    UTNO2
     C                   PARM                    UTYPE
     C                   PARM                    WSPRT
     C                   PARM                    UCPY
     C                   PARM                    WSFAX
      *
     C     STCPB2        TAG
     C  N90              MOVE      '1'           *IN99
     C  N90              MOVE      *BLANKS       WSNR
     C  N90              UPDATE    GSS050DA
      *
     C     SLCPYB        ENDSR
      *
      ***********************************************
     C     CHGST         BEGSR
      ***********************************************
      *
     C                   IF        TRST <> '3'
     C     STCHGST       TAG
     C                   EXFMT     GSS050DS
     C   12              GOTO      SLCHGST
     C  N23              GOTO      STCHGST
      *
     C                   MOVE      TRTNO         UTNO
     C                   CALL      'GSS048R'
     C                   PARM                    UTNO
     C                   PARM                    UOK               1
     C                   IF        UOK   = 'J'
     C                   EVAL      TRST  = '3'
     C                   EVAL      TRAD1 = 'STATUS IS CHANGED TO 3'
     C                   END
     C                   END
      *
     C     SLCHGST       TAG
     C  N90              MOVE      '1'           *IN99
     C  N90              MOVE      *BLANKS       WSNR
     C  N90              UPDATE    GSS050DA
      *
     C                   ENDSR
      *
     C***********************************************
     C     PLKTR         BEGSR
     C***********************************************
     C                   MOVE      '0'           *IN99
     C                   MOVE      'F'           WSSORT
     C                   MOVE      'N'           WSFAX
N01  C                   MOVE      ' '           WSTYPE
N02  C                   IF        WSPRT = *BLANKS
N02  C                   EVAL      WSPRT = BIPRTP
N02  C                   END
     C     STPLK         TAG
     C                   EXFMT     GSS050DZ
     C                   MOVE      '0'           *IN99
     C   12              GOTO      SLPLK
     C     WSPRT         COMP      *BLANKS                                99
     C   99              GOTO      STPLK
      *
     C                   MOVE      'PRT'         UTYPE
     C                   MOVE      TRTNO         UTNO2            11
      *
     C                   MOVE      'J'           UKOPI             1
      *
     C                   SELECT
     C     WSSORT        WHENEQ    'L'
     C     WSSORT        OREQ      'O'
     C     WSSORT        OREQ      'B'
     C                   CALL      'GSS025R'
     C                   PARM                    UTNO2
     C                   PARM                    WSPRT
     C                   PARM                    WSSORT
     C                   PARM                    UKOPI
     C                   PARM                    UUSER
     C     WSSORT        WHENEQ    'P'
     C     WSSORT        OREQ      'F'
     C     WSSORT        IFEQ      'P'
     C                   MOVE      'N'           WSFAX
     C                   END
     C     WSFAX         IFEQ      'Y'
     C                   MOVE      TRCNO         UCNO2             8
     C                   MOVE      '5'           UMOD              1
     C                   CALL      'GSS034C'
     C                   PARM                    UCNO2
     C                   PARM                    UUTFAX            1
     C                   PARM                    UTNO2
     C                   PARM                    UTYPE
N01  C                   PARM                    WSTYPE
     C                   PARM                    UVALK             3
     C                   PARM                    UMOD              1
     C                   PARM                    UCOMM             1
     C                   PARM                    UKNC              8
     C                   ELSE
     C                   MOVE      '1'           UMOD              1
     C                   MOVE      '*JOB'        WSPRT2           10
     C                   CALL      'GSS026R'
     C                   PARM                    UTNO2
     C                   PARM                    WSPRT
     C                   PARM                    WSPRT2
     C                   PARM                    WSSORT
     C                   PARM                    WSFAX
     C                   PARM                    UMOD
     C                   PARM                    UQJOB            10
     C                   PARM                    UQUSER           10
     C                   PARM                    UQNBR             6
     C                   PARM                    UUSER
     C                   END
     C                   ENDSL
      *
     C  N90              MOVE      '1'           *IN99
     C  N90              MOVE      *BLANKS       WSNR
     C  N90              UPDATE    GSS050DA
      *
     C     SLPLK         ENDSR
      *
     C***********************************************
     C     SØKDTA        BEGSR
     C***********************************************
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C                   Z-ADD     WSSUBN        GRECNO            4 0
     C                   Z-ADD     1             UFLAGG            1 0
      *
     C     LINNBR        IFEQ      5
     C     POSNBR        ANDGE     48
     C     POSNBR        ANDLE     52
     C                   CALL      'SYSJ11A'
     C                   PARM                    W2BCD
     C                   GOTO      SLSØK
     C                   END
      *
     C     LINNBR        IFEQ      5
     C     POSNBR        ANDGE     54
     C     POSNBR        ANDLE     61
     C                   MOVE      *BLANKS       USØKKN           30
     C                   CALL      'SOEKKUN'
     C                   PARM                    W2CNO
     C                   PARM                    UFIRM
     C                   PARM                    USØKKN           30
     C                   GOTO      SLSØK
     C                   END
      *
     C                   MOVE      'GSS0013'     MSGNR
     C                   SETON                                        30
      *
     C     SLSØK         ENDSR
      *
     C***********************************************
     C     DOCUMO        BEGSR
     C***********************************************
     C                   MOVEl     trtno         avd
     C                   MOVE      trtno         opd
     C                   CALL      'ARC003'
     C                   PARM                    avd               4 0
     C                   PARM                    opd               7 0
     C                   PARM                    urunde           10
     C  N90              MOVE      '1'           *IN99
     C  N90              MOVE      *BLANKS       WSNR
     C  N90              UPDATE    GSS050DA
      *
     C                   ENDSR
     C***********************************************
     C     BOXINF        BEGSR
     C***********************************************
     C                   MOVE      TRTNO         UTNO
     C                   CALL      'GSS047R'
     C                   PARM                    UTNO
     C  N90              MOVE      '1'           *IN99
     C  N90              MOVE      *BLANKS       WSNR
     C  N90              UPDATE    GSS050DA
      *
     C                   ENDSR
      *
     C***********************************************
     C     PRTBML        BEGSR
     C***********************************************
     C                   MOVE      '0'           *IN99
     C                   MOVE      '0'           *IN31
     C                   MOVE      'GSS0056'     MSGHN
      *
     C     STPRTBM       TAG
     C                   EXFMT     GSS050DI
     C                   MOVE      '0'           *IN89
     C   12              GOTO      SLPRTBM
     C     WSPRTM        COMP      *BLANKS                                89
     C   89              GOTO      STPRTBM
     C                   MOVE      'M'           UMOD
     C                   IF        WSBOXN = 0
     C                   MOVEL     '*ALL'        WSPRTM1          10
     C                   ELSE
     C                   MOVEL     WSBOXN        WSPRTM1          10
     C                   END
     C                   MOVE      TRTNO         UTNO2
     C                   CALL      'GSS026R'
     C                   PARM                    UTNO2
     C                   PARM                    WSPRTM1
     C                   PARM                    WSPRTM
     C                   PARM                    WSSORT
     C                   PARM                    WSFAX
     C                   PARM                    UMOD
     C                   PARM                    UQJOB            10
     C                   PARM                    UQUSER           10
     C                   PARM                    UQNBR             6
     C                   PARM                    UUSER
      *
     C  N90              MOVE      '1'           *IN99
     C  N90              MOVE      *BLANKS       WSNR
     C  N90              UPDATE    GSS050DA
      *
     C     SLPRTBM       ENDSR
      *
