      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/TTCI003R2 MODULE(*LIBL/TTCI003R2 *LIBL/INCGI)
CRTPGM*        ACTGRP(*NEW) AUT(*USE)
      *
      *********************************************************************
      /copy SYSPEDS/qrpgsrcpy,hspecs
      /copy SYSPEDS/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy SYSPEDS/qrpgsrcpy,prototypeb
      /copy SYSPEDS/qrpgsrcpy,usec
      /copy SYSPEDS/qrpgsrcpy,variables3
     ‚*  Prototype for 'JSONFIX'
     d jsonfix         pr                  extpgm('JSONFIX')
     d jsonstring_                         like(jsonstring)
     ‚*  Prototype for 'INCGI'
     d incgi           pr                  extproc('INCGI')
     d bistdl_                             like(bistdl)
     ‚*  Prototype for variabel program
     D Varpgm          Pr                  ExtPgm(BILIBL)
     d WhereC          s            512a
     d FirstW          s              1A
     d cQuote          C                   const('''')
     d SqlStm          s            512a
     d myrows          s             10i 0
     D WSUSER          S             10
     D UGN             S             15
     D JSONstring      s          32000
     D Error           S            150
     D AntLest         S              9  0
     d X               S             10  0
     d MyPoDS          ds                  Qualified  dim(100)
     D GOGN                          15
     D GOMOTT                        15
     D GOBILN                        13
     D GOAVG                         15
     D GOTRNR                        20
     D GOTRDT                         8S 0
     D GOREI1                        20
     D GOREI2                        20
     D GOREI3                        20
     D GOREI4                        20
     D GOGREN                        20
     D GOGRDT                         8S 0
     D GOGRKL                         4S 0
     D GOLSEN                        20
     D GOLSDT                         8S 0
     D GOLSKL                         4S 0
     D GOTYPE                        20
     D GOTURN                        17
      *get input-string
      /copy syspeds/qrpgsrcpy,prolog3

      /free
         exsr ParseSr;
         exsr init;

       gethtmlifs(
       '/syspeddev/html/JSON.html':
       '/CGITAG/');
       wrtsection('top');

       //...........................................................................................
       //skriv JSON-Object start (alltid '{' + x ant param. m/komma mellom (IKKE komma etter siste))
       //...........................................................................................

        JSONstring='{'
        +'¬user¬ : ¬'+%trim(WSUSER)+'¬, '
        +'¬ugn¬ : ¬'+%trim(ugn)+'¬, '
        +'¬errMsg¬ : ¬'+%trim(Error)+'¬, ';

       //JSONfix escaper " i tekst,  for deretter å bytte ¬->"

       jsonfix ( jsonstring );

       updhtmlvar2('JSONstring'
                  :%addr(jsonstring)
                  :%len(jsonstring));
       wrtsection('JSONlin');

       //...........................................................................................
       // Skriv JSON-LISTE Start (en liste er EN parameter(fra [ til   )
       //..........................................................................................
       jsonstring ='"godsliste" : [';

       updhtmlvar2('JSONstring'
                  :%addr(jsonstring)
                  :%len(jsonstring));
       wrtsection('JSONlin');

       if error = *blanks;
         exsr SelSr;
         if myrows > 0;
           exsr LesSr;
         endif;
       endif;
       // ..........................................................................................
       // Skriv JSON-Liste/Array  Avslutning
       // ..........................................................................................
       jsonstring =']';
       updhtmlvar2('JSONstring'
                  :%addr(jsonstring)
                  :%len(jsonstring));
       wrtsection('JSONlin');
       // ..........................................................................................
       // Skriv JSON-string Avsluttning
       // ..........................................................................................
       jsonstring ='}';
       updhtmlvar2('JSONstring'
                  :%addr(jsonstring)
                  :%len(jsonstring));
       wrtsection('JSONlin');
         exsr Exit;

       //-----------------------------------------------------------------
       // Select fra fil via SQL
       //-----------------------------------------------------------------
       begsr     SelSr;

       If    UGN  <> *blanks;
         Exsr  AddAnd;
         Eval  WhereC =  %trim(WhereC)  +
         ' GOGN LIKE ' + cQuote + %trim(UGN) + '%' + cQuote;
        EndIf;

         Eval      WhereC =  ' Where ' + %trim(WhereC);

         Eval       SqlStm = 'Select GOGN, GOMOTT, GOBILN, ' +
                             'GOAVG, GOTRNR, GOTRDT, GOREI1, ' +
                             'GOREI2, GOREI3, GOREI4, GOGREN, ' +
                             'GOGRDT, GOGRKL, GOLSEN, GOLSDT, ' +
                             'GOLSKL, GOTYPE, GOTURN ' +
                             'from GODSJL01 ' +
                              %trim(WhereC)  +
                             ' Order by '       +
                             'GOGN asc ' +
                             'FOR READ ONLY ';

         Exec SQL PREPARE DynSel from :SqlStm;
         EXEC SQL DECLARE  A33  cursor for DynSel;
         Exec SQL OPEN A33;
         Myrows = %elem(MyPoDS);
         exec sql FETCH A33   for :MyRows rows
                  INTO :MyPoDs;
       endsr;
       //-----------------------------------------------------------------
       // Set inn "AND" mellom where settninger
       //-----------------------------------------------------------------
       begsr     AddAnd;
       If        FirstW =   'N';
         Eval      WhereC =  %trim(WhereC) + ' and ';
       EndIf;
         Eval      FirstW =   'N';
       endsr;
       //-----------------------------------------------------------------
       // hent input
       //-----------------------------------------------------------------
       begsr     ParseSr;
         wsuser  = zhbgetvar('USER');
         ugn     = zhbgetvar('UGN');

        endsr;
        // __________________________________________________
        // Læs array                                    LESSR
        // """"""""""""""""""""""""""""""""""""""""""""""""""
           begsr LesSr;
       for x = 1 to myrows;

       if mypods(x).gogn = *blanks;
         leave;
       endif;


        // Lægg ut Json stræng
           AntLest = AntLest + 1;

        // Skriv en JSON-Array-linje pr menypunkt - komma før hvert nytt element

       if AntLest=1;
          JSONstring=*blanks;
          else;
          JSONstring=', ';
          endif;
       JSONstring=%trim(JSONstring)+'{'
          +'¬gogn¬ : ¬'+%trim(mypods(x).GOGN)+'¬, '
          +'¬gomott¬ : ¬'+%trim(mypods(x).GOMOTT)+'¬, '
          +'¬gobiln¬ : ¬'+%trim(mypods(x).GOBILN)+'¬, '
          +'¬goavg¬ : ¬'+%trim(mypods(x).GOAVG)+'¬, '
          +'¬gotrnr¬ : ¬'+%trim(mypods(x).GOTRNR)+'¬, '
          +'¬gotrdt¬ : ¬'+%trim(%editc(mypods(x).GOTRDT:'Z'))+'¬, '
          +'¬gorei1¬ : ¬'+%trim(mypods(x).GOREI1)+'¬, '
          +'¬gorei2¬ : ¬'+%trim(mypods(x).GOREI2)+'¬, '
          +'¬gorei3¬ : ¬'+%trim(mypods(x).GOREI3)+'¬, '
          +'¬gorei4¬ : ¬'+%trim(mypods(x).GOREI4)+'¬, '
          +'¬gogren¬ : ¬'+%trim(mypods(x).GOGREN)+'¬, '
          +'¬gogrdt¬ : ¬'+%trim(%editc(mypods(x).GOGRDT:'Z'))+'¬, '
          +'¬gogrkl¬ : ¬'+%trim(%editc(mypods(x).GOGRKL:'Z'))+'¬, '
          +'¬golsen¬ : ¬'+%trim(mypods(x).GOLSEN)+'¬, '
          +'¬golsdt¬ : ¬'+%trim(%editc(mypods(x).GOLSDT:'Z'))+'¬, '
          +'¬golskl¬ : ¬'+%trim(%editc(mypods(x).GOLSKL:'Z'))+'¬, '
          +'¬gotype¬ : ¬'+%trim(mypods(x).GOTYPE)+'¬, '
          +'¬goturn¬ : ¬'+%trim(mypods(x).GOTURN)+'¬}';
         jsonfix (JSONstring);
       updhtmlvar2('JSONstring'
                  :%addr(jsonstring)
                  :%len(jsonstring));
         wrtsection ('JSONlin');

        endfor;

       endsr;
       //********************************************************
       begsr init;
       //********************************************************
         open bridf;
         // Test innlogging
         chain wsuser bridf;

         if %found;
           if bilibl = *blanks;
             incgi ( bistdl );
           else;

             Monitor;
                   Varpgm();
                 On-Error;
                   //
                 EndMon;

           endif;
         else;

           error = 'Invalid userID';
         endif;

       endsr;
       //=====================================================================
       // Close output html and quit
       //=====================================================================
       begsr exit;
         // Lukk fil
         close bridf;

         wrtsection('*fini');
         *inlr = *on;
         return;
       endsr;
      /end-free
