      *
      ***********************************************************
      *** PROGRAM SKAL VEDLIKEHOLDE SAKSBEHANDLER(SIGNATUR)   ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01 02 04 07 09-11 13-24          ***
      *** LEDIGE INDIKATORER 27-29 31 32 34-39 41-88 95-97    ***
      ***********************************************************
      *
     FHEURFIL   UF A E           K DISK
     FEDI23D    CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D                 DS
     D  BRUKE1                 1     10
     D  KOSFN7                 1      7
     IHEURFILR
     I              BRUKER1                     BRUKE1
     I              BRUKER2                     BRUKE2
     I              BRUKER3                     BRUKE3
     I              BRUKER4                     BRUKE4
     I              BRUKER5                     BRUKE5
     C*
     C                   EXSR      INIT
     C*
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
     C                   MOVE      W2PREF        YSPREF           10
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     KODSF1        SETLL     HEURFIL
     C     START1        TAG
     C                   READ(N)   HEURFIL                                94
      *
     C     START2        TAG
      *
     C  N94              DO
     C                   DO        14
     C                   ADD       1             ZRECNO
     C                   WRITE     SUBFIL
     C                   READ(N)   HEURFIL                                94
     C  N94              MOVE      PREFCA        YSPREF
     C  N94              MOVE      CUSTCL        YSCUST           10
     C  N94              END
     C                   END
     C*
     C                   Z-ADD     ZRECNO        YRECNO            5 0
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     5             POSNBR
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYWRK
     C                   ELSE
     C                   WRITE     SUBCTL
     C                   EXFMT     BUNN
     C                   END
     C*
     C***
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C                   Z-ADD     WSSUBN        XRECNO            4 0
     C     XRECNO        IFEQ      0
     C                   Z-ADD     ZRECNO        XRECNO            4 0
     C                   END
     C***
     C*
     C                   SETOFF                                       9899
     C*
     C*** AVSLUTT
     C   03              GOTO      SLUTT
     C*
     C*** FRISK OPP
     C   05              GOTO      START
     C*
     C*** LAGE NY
     C   06              DO
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     7             POSNBR
     C                   MOVE      *BLANKS       WSPREF
     C                   GOTO      START5
     C                   END
     C*** SØK FRA
     C   08              GOTO      START6
     C*
     C*** BLA VIDERE
     C   25              DO
     C                   SETOFF                                       25
     C                   Z-ADD     YRECNO        ZRECNO
     C  N94KODSF3        SETLL     HEURFIL
     C  N94              READ(N)   HEURFIL                                94
     C                   GOTO      START2
     C                   END
     C*
     C     ZRECNO        IFNE      0
     C                   EXSR      HENT
     C                   END
     C   98XRECNO        IFNE      0
     C                   Z-ADD     XRECNO        ZRECNO
     C                   END
     C                   GOTO      START3
     C*
     C*** LAGE NY
     C     START5        TAG
     C                   EXFMT     VARELI
     C                   SETOFF                                       309089
     C   12              EXSR      MOVB2
     C  N12              EXSR      TESTB1
     C  N12
     CAN 30              GOTO      START5
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     5             POSNBR
     C                   GOTO      START3
     C*
     C*** SØK FRA
     C     START6        TAG
     C                   EXFMT     VAREL2
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     15            POSNBR
     C   12              GOTO      START3
     C                   GOTO      START
     C*
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
     C*
     C***********************************************
     C     TESTB1        BEGSR
     C***********************************************
     C  N40              DO
     C     WSPREF        COMP      *BLANKS                                90
     C  N90WSCUST        COMP      *BLANKS                                90
     C   90              MOVE      'SHU0012'     MSGNR
     C  N90KODSFK        CHAIN(N)  HEURFIL                            33
     C  N90
     CANN33              DO
     C                   SETON                                        90
     C                   MOVE      'SHU0013'     MSGNR
     C                   END
     C   90              Z-ADD     7             POSNBR
     C   90              SETON                                        30
     C                   END
     C*
     C     WSHEUR        COMP      *BLANKS                                89
     C   89
     CANN30              MOVE      'SHU0014'     MSGNR
     C   89
     CANN30              Z-ADD     14            POSNBR
     C   89              SETON                                        30
     C*
     C   30              GOTO      SLTB
     C*
     C*** OPPDATERE PÅ REGISTER OG SUBFIL
     C     KODSFK        CHAIN     HEURFIL                            33
     C                   EXSR      MOVWSF
     C  N33              UPDATE    HEURFILR
     C   33              WRITE     HEURFILR
     C   40              UPDATE    SUBFIL
     C  N40              ADD       1             YRECNO
     C  N40              Z-ADD     YRECNO        ZRECNO
     C  N40              WRITE     SUBFIL
     C                   WRITE     SUBCTL
     C                   EXSR      MOVB2
     C*
     C     SLTB          ENDSR
     C*
     C***********************************************
     C     HENT          BEGSR
     C***********************************************
     C                   Z-ADD     0             LINNBR
     C                   SETOFF                                       919299
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C   98              DO
     C   99              SETOFF                                       98
     C                   Z-ADD     1             LINNBR
     C                   GOTO      SLHENT
     C                   END
     C     ZRECNO        DIV       14            LINNBR
     C                   MVR                     XLIN              2 0
     C     XLIN          IFEQ      0
     C                   Z-ADD     14            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C     WSNR          IFEQ      '2'                                          ENDRE
     C                   MOVE      ' '           WSNR
     C     KOSFN7        COMP      '*DELETE'                              98
     C   98              GOTO      SLHENT
     C                   EXSR      CHGSF
     C   12              GOTO      SLHENT
     C                   END
     C     WSNR          IFEQ      '4'                                          SLETTE
     C                   MOVE      ' '           WSNR
     C     KOSFN7        COMP      '*DELETE'                              98
     C   98              GOTO      SLHENT
     C                   EXSR      DLTSF
     C   12              GOTO      SLHENT
     C                   END
     C                   END
     C*
     C     SLHENT        TAG
     C                   ADD       5             LINNBR
     C                   Z-ADD     5             POSNBR
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     DLTSF         BEGSR
     C***********************************************
     C                   SETON                                        99
     C                   MOVE      PREFCA        WSPREF
     C                   MOVE      CUSTCL        WSCUST
     C                   EXFMT     VAREL3
     C  N12              DO
     C     KODSFK        CHAIN     HEURFIL                            33
     C  N33              DELETE    HEURFILR
     C                   EXSR      MOVB1
     C                   END
     C     XLIN          IFEQ      0
     C                   Z-ADD     14            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C                   UPDATE    SUBFIL
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     CHGSF         BEGSR
     C***********************************************
     C                   EXSR      MOVSFW
     C                   SETON                                        99
     C     STCHG         TAG
     C                   EXFMT     VARELI
     C                   SETOFF                                       309089
     C   12              DO
     C                   UPDATE    SUBFIL
     C                   EXSR      MOVB2
     C                   END
     C  N12              EXSR      TESTB1
     C  N12
     CAN 30              GOTO      STCHG
     C     XLIN          IFEQ      0
     C                   Z-ADD     14            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVB1         BEGSR
     C***********************************************
     C                   MOVE      *BLANKS       PREFCA
     C                   MOVE      *BLANKS       CUSTCL
     C                   MOVE      *BLANKS       HEUR
     C                   MOVE      *BLANKS       HEAVD
     C                   MOVEL     '*DELETE'     KOSFN7
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVB2         BEGSR
     C***********************************************
     C                   SETOFF                                       40
     C                   MOVE      *BLANKS       WSPREF
     C                   MOVE      *BLANKS       WSCUST
     C                   MOVE      *ZEROS        WSHEUR
     C                   MOVE      *ZEROS        WSAVD
     C                   MOVE      *BLANKS       WSBRU1
     C                   MOVE      *BLANKS       WSBRU2
     C                   MOVE      *BLANKS       WSBRU3
     C                   MOVE      *BLANKS       WSBRU4
     C                   MOVE      *BLANKS       WSBRU5
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVSFW        BEGSR
     C***********************************************
     C                   MOVE      PREFCA        WSPREF
     C                   MOVE      CUSTCL        WSCUST
     C                   MOVE      HEUR          WSHEUR
     C                   MOVE      HEAVD         WSAVD
     C                   MOVE      BRUKE1        WSBRU1
     C                   MOVE      BRUKE2        WSBRU2
     C                   MOVE      BRUKE3        WSBRU3
     C                   MOVE      BRUKE4        WSBRU4
     C                   MOVE      BRUKE5        WSBRU5
     C                   SETON                                        40
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     29            POSNBR
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVWSF        BEGSR
     C***********************************************
     C   33              DO
     C                   MOVE      WSPREF        PREFCA
     C                   MOVE      WSCUST        CUSTCL
     C                   END
     C*
     C                   MOVE      WSHEUR        HEUR
     C                   MOVE      WSAVD         HEAVD
     C                   MOVE      WSBRU1        BRUKE1
     C                   MOVE      WSBRU2        BRUKE2
     C                   MOVE      WSBRU3        BRUKE3
     C                   MOVE      WSBRU4        BRUKE4
     C                   MOVE      WSBRU5        BRUKE5
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     KODSFK        KLIST
     C                   KFLD                    WSPREF
     C                   KFLD                    WSCUST
     C     KODSF1        KLIST
     C                   KFLD                    W2PREF
     C                   KFLD                    W2CUST           10
     C     KODSF2        KLIST
     C                   KFLD                    PREFCA
     C                   KFLD                    CUSTCL
     C     KODSF3        KLIST
     C                   KFLD                    YSPREF
     C                   KFLD                    YSCUST
     C*
     C*
     C                   ENDSR
     C*
