*****A**************************************************************
     A* RETTINGER I DETTE PROGRAM:
PTFnrA*Sek Sig Vers  Beskrivelse...........................
"""""A*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXXA* 01 YBC PTF10 SKJERMEN STØRRELSE
*****A**************************************************************
S01  A*                                     DSPSIZ(24 80 *DS3)
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPDOC('STD' 'HELPNU' 'HLP400')
     A                                      ALTHELP(CA01)
     A                                      HELP
      * "NOERASE" MÅ HA FOR Å IKKE TØMME BILDE SOM MAN KOMME FRA
     A          R NOERASE
     A                                      ASSUME
     A                                  1  2' '
      * MAN KUN DEFINNERE VINDU PÅ REKORDNIVÅ
     A          R WINDOWSZ
     A                                      WINDOW(5 10 6 52 *NOMSGLIN)
     A                                      OVERLAY
     A          R BILDE01
     A  *DS3                                WINDOW(WINDOWSZ)
     A  *DS4                                WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A                                      CF03(03 'Avslutt program')
     A                                      CF04(04 'søking')
     A                                      CF05(05 'SE ORDRE')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            KUNDNR         8Y 0O  1  2EDTCDE(Z)
     A            KNAVN         30A  O  1 11
     A                                  2  2MSGCON(013 SHU1335 *LIBL/SYMSGF)
     A            WSGREN        13Y 0O  2 16EDTCDE(K)
     A                                  3  2MSGCON(013 SHU1333 *LIBL/SYMSGF)
     A            WSSALD        13Y 0O  3 16EDTCDE(K)
     A                                  4  2MSGCON(013 SHU1334 *LIBL/SYMSGF)
     A            WSAKKU        13Y 0O  4 16EDTCDE(K)
     A                                  5  2MSGCON(013 SHU1336 *LIBL/SYMSGF)
     A            WSDINO        13Y 0O  5 16EDTCDE(K)
     A                                  6  2MSGCON(013 SHU1332 *LIBL/SYMSGF)
     A            WSREST        13Y 0O  6 16EDTCDE(K)
     A                                  4 37MSGCON(015 SHU1337 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
