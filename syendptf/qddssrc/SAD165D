      *
     A                                      DSPSIZ(27 132 *DS4)
     A                                      PRINT
     A                                      HELP
     A                                      ALTHELP
      *
     A          R NOERASE
     A                                      ASSUME
     A                                  1  2' '
     A          R WINDOWSZ
     A                                      WINDOW(*DFT 19 102 *NOMSGLIN)
     A                                      OVERLAY
      *
     A          R SUBFIL                    SFL
     A            WSNR           2Y 0B  5  2EDTCDE(4)
     A            EIAVD          4Y 0H
     A            EITDN          7Y 0H
     A            EILI           5Y 0O  5  5TEXT('Linjenr')
     A                                      EDTCDE(4)
     A            EILID          5Y 0H
     A            EILIT          5Y 0H
     A            EIST           1A  O  5 11TEXT('Status')
     A            EIGTY          2A  O  5 14TEXT('Gods type')
     A            EIVAL          3A  O  5 17TEXT('Valutakode')
     A            EIBL          13Y 2O  5 21TEXT('Beløp')
     A                                      EDTCDE(4)
     A            EIRGE         17A  H
     A            EIROE8A        2A  H
     A            EIUCR         50A  H
     A            EIVT          70A  H
     A            EIVT_20       20A  O  5 36TEXT('Varebeskrivelse')
     A            EICKD          9A  H
     A            EIVNT          8Y 0O  5 57TEXT('Tariffnr')
     A                                      EDTCDE(4)
     A            EIUNNR         4A  H
     A            EICVKB        11Y 3O  5 66TEXT('Bruttovekt')
     A                                      EDTCDE(4)
     A            EICVKN        11Y 3O  5 79TEXT('Nettovekt')
     A                                      EDTCDE(4)
     A            EIUNIT        35A  H
     A            EILK           2A  O  5 92TEXT('Landkode')
     A            EIMK          17A  H
     A            EINT           7Y 0O  5 95TEXT('Kolli')
     A                                      EDTCDE(4)
     A            EIPMRK        30A  H
     A            EIPTYP         2A  H
     A            EIPTM          4A  H
     A            EIPTMT        50A  H
     A            EIPLK          2A  H
     A            EICNR         11A  H
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      WINDOW(WINDOWSZ)
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0012)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A                                      CF03(03 'Avslutt program')
     A                                      CF12(12 'AVBRYT')
     A                                      CF05(05 'Frisk')
     A N49                                  CF06(06 'Lag ny')
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A            ZRECNO         4S 0O  1 70SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYSPED/SAD165R'
     A                                      COLOR(BLU)
     A                                  1 20'Vedlikehold GodsItem - Liste'
     A                                      COLOR(WHT)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                  2 70TIME
     A                                  2  2'Avd.:'
     A            UAVD           4Y 0O  2  8TEXT('AVDELINGSNR')
     A                                      COLOR(BLU)
     A                                      EDTCDE(4)
     A            UTDN           7Y 0O  2 13TEXT('OPPDRAGSNR')
     A                                      COLOR(BLU)
     A                                      EDTCDE(4)
     A                                  2 21'Turnr:'
     A            UPRO           8Y 0O  2 28TEXT('Turnr')
     A                                      COLOR(BLU)
     A                                      EDTCDE(4)
     A                                  2 37'M-ID:'
     A            UMID          18A  O  2 43TEXT('UUID')
     A                                      COLOR(BLU)
     A  49                              3  2'Velg:  5=Vis'
     A                                      COLOR(BLU)
     A N49                              3  2'Velg:  2=Endre  4=Slett  5=Vis'
     A                                      COLOR(BLU)
     A                                  4  2'Op L.nr. ST GT Val .........Beløp -
     A                                      Varebeskrivelse      Tariffnr ..Bru-
     A                                      ttovekt ...Nettovekt LK ..Kolli'
     A                                      COLOR(WHT)
      *
     A          R BUNN
     A                                      WINDOW(WINDOWSZ)
     A                                 18  2'F3=Avslutt   F5=Frisk   F12=Avbryt'
     A                                      COLOR(BLU)
     A N49                             18 42'F6=Lag ny'
     A                                      COLOR(BLU)
     A  30        MSGTXT        68A  O 19  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R NYVARE
     A                                      WINDOW(WINDOWSZ)
     A                                      CF03(03 'Avslutt program')
     A                                      CF05(05 'Frisk')
     A N49                                  CF06(06 'Lag ny')
     A                                      CF12(12 'AVBRYT')
     A                                  1  2'SYSPED/SAD165R'
     A                                      COLOR(BLU)
     A                                  1 20'Vedlikehold GodsItem - Liste'
     A                                      COLOR(WHT)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                  2 70TIME
     A                                  2  2'Avd.:'
     A            UAVD           4Y 0O  2  8TEXT('AVDELINGSNR')
     A                                      COLOR(BLU)
     A                                      EDTCDE(4)
     A            UTDN           7Y 0O  2 13TEXT('OPPDRAGSNR')
     A                                      COLOR(BLU)
     A                                      EDTCDE(4)
     A                                  2 21'Turnr:'
     A            UPRO           8Y 0O  2 28TEXT('Turnr')
     A                                      COLOR(BLU)
     A                                      EDTCDE(4)
     A                                  2 37'M-ID:'
     A            UMID          18A  O  2 43TEXT('UUID')
     A                                      COLOR(BLU)
     A  49                              3  2'Velg:  5=Vis'
     A                                      COLOR(BLU)
     A N49                              3  2'Velg:  2=Endre  4=Slett  5=Vis'
     A                                      COLOR(BLU)
     A                                  4  2'Op L.nr. ST GT Val .........Beløp -
     A                                      Varebeskrivelse      Tariffnr ..Bru-
     A                                      ttovekt ...Nettovekt LK ..Kolli'
     A                                      COLOR(WHT)
     A                                 18  2'F3=Avslutt   F5=Frisk   F12=Avbryt'
     A                                      COLOR(BLU)
     A N49                             18 42'F6=Lag ny'
     A                                      COLOR(BLU)
     A  30        MSGTXT        68A  O 19  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
