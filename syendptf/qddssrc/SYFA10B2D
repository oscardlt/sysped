     A*%%TS  SD  20211013  212136  JOVO        REL-V7R4M0  5770-WDS
     A* NY LINJE FOR HENTET I HEADER.. RESTEN/SUBFIL 1 LINJE NED
     A*%%EC
     A                                      DSPSIZ(27 132 *DS4)
     A                                      PRINT
     A          R NOERAS
     A                                      ASSUME
     A                                      OVERLAY
     A                                  1  2' '
     A          R WIND01
     A                                      OVERLAY
     A*                                     WINDOW(*DFT 18 72)
     A*                                     WINDOW(*DFT 18 98)
     A                                      WINDOW(*DFT 18 114)
     A          R SUBFIL                    SFL
     A*%%TS  SD  20211013  212136  JOVO        REL-V7R4M0  5770-WDS
     A            FNDATE6        6Y 0O  4  1EDTWRD('  .  .  ')
     A            FNTIME4        4Y 0O  4 10EDTWRD('  :  ')
     A            FNTERM         5A  O  4 16
     A            TERMTXT       35A  O  4 28COLOR(WHT)
     A*           TERMAVV       25A  O  4 64COLOR(RED)
     A            TERMAVV       25A  O  4 64
     A N51                                  COLOR(RED)
     A  51                                  COLOR(WHT)
     A            FNUSER        10A  O  4 90
     A            FNDATL6        6Y 0O  4101EDTWRD('  .  .  ')
     A            FNTIML4        4Y 0O  4110EDTWRD('  :  ')
     A            RETNING        5A  O  4 22
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A*%%TS  SD  20211013  212136  JOVO        REL-V7R4M0  5770-WDS
     A                                      SFLSIZ(0014)
     A                                      SFLPAG(0013)
     A                                      WINDOW(WIND01)
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                      CF12(12 'tilbake')
     A                                      CF03(12)
     A                                      CF05(05)
     A                                      KEEP
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND(*MORE)
     A            ZRECNO         4S 0H      SFLRCDNBR
     A                                  1  1'SYFA10B2'
     A                                      COLOR(BLU)
     A                                  1 22'Vise logg på ett kolli    '
     A                                      COLOR(WHT)
     A            UUCLID        18A  O  1 50COLOR(WHT)
     A                                  3  1' Dato    Tid   Kode  Retn. Beskriv-
     A                                      else                         Skade/-
     A                                      avvik               Loggført av....-
     A                                      ...       '
     A                                      COLOR(WHT)
     A          R BUNN
     A*%%TS  SD  20140408  080115  JOVO        REL-V7R1M0  5770-WDS
     A                                      WINDOW(WIND01)
     A                                      SLNO(17)
     A                                      CLRL(01)
     A                                      OVERLAY
     A                                  1 26'F5=Forny '
     A                                      COLOR(BLU)
     A                                  1 52'F3/F13=Avbryt'
     A                                      COLOR(BLU)
