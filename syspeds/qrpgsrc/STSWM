      *********************************************************************
      *  Inngang til STS - Strekkode tur utgående. MANGE turer på en gang *
      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/STSWM MODULE(*LIBL/STSWM *LIBL/INCGI)
CRTPGM*         ACTGRP(*NEW) AUT(*USE)
      *
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
11XXX * 01 JOV PTF11 parameter for tillat slett oppdrag
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
11XXX * 01 JOV PTF11 parameter for tillat slett oppdrag
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
      *--------------------------------------------------------------------
      * Data base files
     FBRIDF     if   e           k disk    usropn
     FBRPARF    if   e           k disk    usropn
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      *--------------------------------------------------------------------
      * Variables common to CGI programs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
      * Varibles
     DWSUSER           S             10
     DTN1              s              8
     DTN2              s              8
     DTN3              s              8
N01  D FLERPAR         s            200
N01  D VERSJ           s             10
N01  D SlettOppd       s              1
N01  D MerkManKll      s              1
     D LAGBUT          s            200
     D MTU             s              8  0
     D MTUR            s              8
     D TKId            s             10
     D SEMI            s              1
     D Fn              s             10
     D Lib             s             10
     D Mbr             s             10
     D Spaces          s             12a   inz('&nbsp;&nbsp;')
      *--------------------------------------------------------------------
      * Prolog common to CGI programs   (C-specs)
      * -Receive input from the remote browser
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prolog3
      *=====================================================================******
      *
      *=====================================================================
      * MAIN PROCESS LINE
      *=====================================================================
      * Hent / repeter UserID
     C                   EVAL      WSUSER   = zhbgetvar('user')
     C                   eval      TN1      = zhbgetvar('TN1   ')
     C                   eval      TN2      = zhbgetvar('TN2   ')
     C                   eval      TN3      = zhbgetvar('TN3   ')
     C                   eval      SEMI    = zhbgetvar('SEMI')
N01  C                   eval      VERSJ = zhbgetvar('V')
      * set libl / hent parameter for om user tilates å opprette tom tur
     C                   OPEN      BRIDF
     C     WSUSER        CHAIN     BRIDF                              50
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
     C                   OPEN      BRPARF
     C     BrParKey      klist
S01  C*                  kfld                    BIBRID
N01  C                   kfld                    PABRID
     C                   kfld                    PAKUNR
     C                   kfld                    PAID
N01  C                   MOVE      BIBRID        PABRID
     C                   MOVE      'TUTUR'       PAID
     C     BrParKey      Chain     BRPARF                             33
     C                   move      *BLANKS       LAGBUT
     C     *in33         ifeq      *off
     C                   z-add     PAVRDN        MTu
     C                   movel     Mtu           MTur
     C                   movel     PAVRDA        TKID
     c                   eval      LAGBUT = '<INPUT TYPE="button" '
     C                             + 'VALUE="Lag/Finn tur" NAME="LagButt" '
     C                             + 'onClick="LagFinn()">'
     C                   end
N01   * kode for om slett av oppdrag er tillatt
N01   * Ved User NN ( el andre anonyme) må tillatelse være gitt på user
N01  c                   if        Versj>='01.28'
N01  c                   eval      SlettOppd = 'N'
N01  C                   EVAL      PABRID   = BIBRID
N01  C                   EVAL      PAID     = 'TUSLO'
N01  C     BrParKey      Chain     BRPARF                             33
N01  C                   IF        BIPO <> *BLANKS
N01  c   33              eval      PABRID   = '*KUNDFT*'+BIFIRM
N01  C   33BrParKey      Chain     BRPARF                             33
N01  C                   ENDIF
N01  C                   IF        *IN33 = '0' AND %SUBST(PAVRDA:1:1)='J'
N01  C                   EVAL      SlettOppd = 'J'
N01  c                   ENDIF
N01   * kode for om manuell merking av oppdrag som er kollimerket er tillatt
N01  c                   eval      MerkManKll= 'N'
N01  C                   EVAL      PABRID   = BIBRID
N01  C                   EVAL      PAID     = 'TUMOK'
N01  C     BrParKey      Chain     BRPARF                             33
N01  C                   IF        BIPO <> *BLANKS
N01  c   33              eval      PABRID   = '*KUNDFT*'+BIFIRM
N01  C   33BrParKey      Chain     BRPARF                             33
N01  C                   ENDIF
N01  C                   IF        *IN33 = '0' AND %SUBST(PAVRDA:1:1)='J'
N01  C                   EVAL      MerkManKll= 'J'
N01  c                   ENDIF
N01  c                   endif
      * Set/Read skeleton output html, etc.
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC'
     C     SEMI          IFNE      'Y'
     C                   eval      Mbr = 'STSWM'
     C                   else
     C                   eval      Mbr = 'STSWT'
     C                   end
N01  c                   if        Versj>='01.28'
N01  c                   eval      FlerPar=%trim(Flerpar)
N01  c                             +';'+SlettOppd                               SlettOppdragJ/N
N01  c                             +';'+MerkManKll                              Merk Manu v/kolli
N01  c                   endif
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
      * sett html-variable
     C                   callp     updhtmlvar('USER':WSUSER:
     C                             InitHTMLVars)
     C                   callp     updhtmlvar('TN1':TN1)
     C                   callp     updhtmlvar('TN2':TN2)
     C                   callp     updhtmlvar('TN3':TN3)
     C                   callp     updhtmlvar('LAGBUT':LAGBUT)
     C                   callp     updhtmlvar('MTUR':MTUR)
     C                   callp     updhtmlvar('TKID':TKID)
N01  C                   callp     updhtmlvar('FLERPAR':FLERPAR)
      * Write HTML-section
     C                   callp     wrtsection('all')
      * dummy fini-section
     C                   callp     wrtsection('*fini')
      * Quit
     C                   close     BRIDF
     C                   close     BRPARF
     C                   MOVE      *ON           *INLR
     C                   return
