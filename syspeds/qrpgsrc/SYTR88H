      *
      ***********************************************************
      *** PROGRAM FOR Å AUTOSKAPE/PLUKKE TURNR BASERT PÅ BILNR. *
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER      *
      ***********************************************************
     FTURER1    IF A E           K DISK
     FTELL      UF   E           K DISK
     FTELLRU    UF   E           K DISK
     FUNITL01   IF   E           K DISK
     FTRAN      IF   E           K DISK
     FFIRM      IF   E           K DISK
     D                 DS
     D  TUDTÅ                  1      4  0
     D  TUDTM                  5      6  0
     D  TUDTD                  7      8  0
     D  TUDT                   1      8  0
     D                UDS
     D  UFIRM                  1      2
     D  UUAVD                 15     18
     D  USG                   19     21
      *
     C                   EXSR      INIT
      *
     C     FINNTU        IFEQ      'J'
     C     TURKEY        SETLL     TURER1
     C     TURKEY        READE     TURER1                                 94
     C     *IN94         DOWEQ     '0'
     C     TUBILN        IFEQ      UBILN
     C     TUDT          IFEQ      IDAG
     C     TUDT          OREQ      *ZEROS
     C                   MOVE      TUPRO         XXTUR             8 0
     C                   ADD       1             UANT
     C                   END
     C                   END
     C     TURKEY        READE     TURER1                                 94
     C                   END
     C                   END
      *
      * FANT 1 OG BARE 1,
     C     UANT          IFEQ      1
     C                   MOVE      XXTUR         UPRO
     C                   ELSE
      * FANT INGEN - OPPRETT / PLUKK (RETUR MED AUNT 0= NY ER LAGD.)
     C     UANT          IFEQ      0
     C                   EXSR      CRTTUR
     C                   MOVE      TUPRO         UPRO
     C                   END
     C                   END
      *
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
      *
     C***********************************************
     C     CRTTUR        BEGSR
     C***********************************************
     C                   CLEAR                   TURERR
      *
     C                   MOVE      USG           TUSG
     C                   MOVE      UAVD          TUAVD
     C     UAVD          CHAIN     TELL                               33
     C   33AVDZER        CHAIN     TELL                               33
     C  N33              MOVE      TETURN        TUPRO
     C  N33              ADD       1             TETURN
     C  N33              UPDATE    TELLR
     C     UAVD          CHAIN     TELLRU                             33
     C   33AVDZER        CHAIN     TELLRU                             33
     C  N33              MOVE      TERUND        TURUND
     C  N33              ADD       1             TERUND
     C  N33              UPDATE    TELLRUR
      *
     C     2000          ADD       UYEAR         TUDTÅ
     C                   MOVE      UMONTH        TUDTM
     C                   MOVE      UDAY          TUDTD
     C                   MOVE      TUDT          TUDTT
     C                   MOVE      20            TURACC
     C                   MOVE      UYEAR         TURAAR
     C                   MOVE      UMONTH        TURMND
     C                   MOVE      UBILN         TUBILN
     C     TUBILN        CHAIN     UNITL01                            33
     C                   MOVE      UNTRME        TUBILK
     C                   MOVE      UNTRMA        TUTRMA
     C                   MOVE      UNLAND        TULK
     C                   MOVE      UNTILH        TUHENG
     C                   MOVE      UNVEKT        TUKVKT
     C                   MOVE      UNHØYD        TUHØYB
     C                   MOVE      UNTARA        TUTARA
     C                   Z-ADD     UNM3          TUKAM3
     C                   Z-ADD     UNLM          TUKALM
     C                   MOVE      UNTRAN        TUKNT2
     C     TRANKE        CHAIN     TRAN                               33
     C                   MOVE      VMNAVN        TUNAT
     C                   WRITE     TURERR
      *
     C                   ENDSR
      *
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
      *
     C     *ENTRY        PLIST
     C                   PARM                    UAVD              4 0
     C                   PARM                    UBILN            10
     C                   PARM                    UPRO              8 0
     C                   PARM                    UANT              3 0
      *
     C     TURKEY        KLIST
     C                   KFLD                    TUST
     C                   KFLD                    TUAVD
     C                   MOVE      ' '           TUST
     C                   MOVE      UAVD          TUAVD
      *
     C     TRANKE        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    TUKNT2
      * 999 = LAG NY TUR UBETINGET
     C     UANT          IFEQ      999
     C                   MOVE      'N'           FINNTU            1
     C                   ELSE
     C                   MOVE      'J'           FINNTU
     C                   END
     C                   MOVE      *ZEROS        UANT
     C     *LIKE         DEFINE    TEAVD         AVDZER
     C                   MOVE      *ZEROS        AVDZER
     C     *LOVAL        SETLL     FIRM
     C                   READ      FIRM                                   33
      *
     C     2000          ADD       UYEAR         TUDTÅ
     C                   MOVE      UMONTH        TUDTM
     C                   MOVE      UDAY          TUDTD
     C                   MOVE      TUDT          IDAG              8 0
      *
     C                   ENDSR
      *
