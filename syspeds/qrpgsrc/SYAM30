     H DATEDIT(*YMD)
      ***********************************************************
      *** PERIODEAVREGNING                                    ***
      ***                                                     ***
      *** INDIKATORER  01 - 26 FUNCTION  / ROLL KEYS          ***
      *** BRUKTE INDIKATORER                                  ***
      ***                                                     ***
      ***  03 - G≈ UT                                         ***
      ***  05 - FRISK OPP                                     ***
      ***  06 - LAG NY                                        ***
      ***  11 - SLETT                                         ***
      ***  12 - ANNULLER                                      ***
      ***  25 - ROLL                                          ***
      ***                                                     ***
      ***  30 - GENERELL FEIL                                 ***
      ***  66 - READ INDICATOR FOR SUBCTL                     ***
      ***  91 - SFLDSP                                        ***
      ***  92 - SFLDSPCTL                                     ***
      ***  93 - SFLCLR                                        ***
      ***  94 - SFLEND                                        ***
      ***  98 - END OF READC SUBFILE                          ***
      ***********************************************************
      *
     FAMHE3     IF   E           K DISK
     FAMDE      IF   E           K DISK
     FAMANDEL   IF   E           K DISK
     FCUNDL01   IF   E           K DISK
     FFIRM      IF   E           K DISK
     FSYAM30FM  CF   E             WORKSTN
     F                                     INFDS(FBAREA)
     F                                     SFILE(SAM30H50:ZRECNO)
     FSYAM30PF  O    E             PRINTER
      ***********************************************************
      *    Tabeller
      *
      ***********************************************************
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D                 DS
     D  DSNVN                  1     30
     D  DSNVN1                 1     10
     D  DSNVN2                11     11
     D  DSNVN3                12     30
     D                 DS
     D  AHA≈≈                  1      4  0
     D  AHAMM                  5      6  0
     D  AHADD                  7      8  0
     D  AHADAT                 1      8  0
     D                 DS
     D  WHADD                  1      2  0
     D  WHAMM                  3      4  0
     D  WHA≈≈                  5      6  0
     D  WHADAT                 1      6  0
     D                 DS
     D  AHB≈≈                  1      4  0
     D  AHBMM                  5      6  0
     D  AHBDD                  7      8  0
     D  AHBDAT                 1      8  0
     D                 DS
     D  WHBDD                  1      2  0
     D  WHBMM                  3      4  0
     D  WHB≈≈                  5      6  0
     D  WHBDAT                 1      6  0
      *______________________
      * Initiering
      *""""""""""""""""""""""
     C                   EXSR      INIT
      *___________________________
      * Hovedlogikk
      *"""""""""""""""""""""""""""
     C     STRPGM        TAG
     C                   EXFMT     SAM30B10
     C                   MOVE      *OFF          *IN30
      *** F3=GÂ ut
     C     *IN03         IFEQ      '1'
     C                   GOTO      ENDPGM
     C                   END                                                    <*IN03
      *** F12=GÂ ut
     C     *IN12         IFEQ      '1'
     C                   GOTO      ENDPGM
     C                   END                                                    <*IN12
      *** AGENTNAVN/STED
     C     CUNKE1        CHAIN     CUNDL01                            35
     C     *IN35         IFEQ      *OFF
     C                   CLEAR                   DSNVN
     C                   MOVEL     KNAVN         DSNVN1
     C                   MOVEL     '/'           DSNVN2
     C                   MOVEL     ADR3          DSNVN3
     C                   MOVE      DSNVN         W2NAVN
     C                   ELSE
     C                   MOVE      'TRO0258'     MSGNR
     C                   MOVE      *ON           *IN30
     C                   GOTO      STRPGM
     C                   ENDIF                                                  <*IN35
     C* SJEKK OM AGENT SKAL HA PERIODEAVREGNING
     C     WKNA          CHAIN     AMANDEL                            35
     C     *IN35         IFEQ      *OFF
     C     AANETT        IFNE      'P'
     C                   MOVE      'TRO0259'     MSGNR
     C                   MOVE      *ON           *IN30
     C                   GOTO      STRPGM
     C                   ENDIF                                                  <AANETT
     C                   ENDIF                                                  <*IN35
      *
     C     WPER≈         IFGT      50
     C                   MOVE      '19'          WPERC             2 0
     C                   ELSE
     C                   MOVE      '20'          WPERC
     C                   END
      *______________
      * Clear Subfil
      *""""""""""""""
     C     START         TAG
     C                   MOVEA     '00'          *IN(91)
     C                   MOVE      '1'           *IN93
     C                   WRITE     SAM30K60
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     0             ZRECNO
      *_____________
      * Fyll Subfil
      *"""""""""""""
     C     AMHEK3        SETLL     AMHE3                                  86
      *
     C     START1        TAG
     C                   DO        1000
     C     AMHEK3        READE     AMHE3                                  94
     C     *IN94         IFEQ      '0'
     C                   MOVEL     *BLANKS       WSKODE
     C                   MOVE      AHADD         WHADD
     C                   MOVE      AHAMM         WHAMM
     C                   MOVE      AHA≈≈         WHA≈≈
     C                   MOVE      AHBDD         WHBDD
     C                   MOVE      AHBMM         WHBMM
     C                   MOVE      AHB≈≈         WHB≈≈
     C                   ADD       1             ZRECNO
     C                   WRITE     SAM30H50
     C                   END                                                    <*IN94
     C  N94              ENDDO                                                  <DO 14
      *
     C                   Z-ADD     ZRECNO        YRECNO            5 0
     C     ZRECNO        IFGT      0
     C     ZRECNO        SUB       1             CURS1
     C     CURS1         DIV       14            CURS1
     C     CURS1         MULT      14            CURS1
     C                   ADD       1             CURS1
     C                   ENDIF
     C                   Z-ADD     1             ZRECNO
      *________________
      * Display Subfil
      *""""""""""""""""
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C     VISB30        TAG
     C                   EXFMT     SAM30B30
     C                   ELSE
     C                   WRITE     SAM30B40
     C                   EXFMT     SAM30K60
     C                   END                                                    <ZRECNO
      *
     C                   MOVEA     '0000000'     *IN(30)
      *
     C                   SETOFF                                       9998
      *** F3=GÂ ut
     C     *IN03         IFEQ      *ON
     C                   GOTO      ENDPGM
     C                   END                                                    <*IN03
      *** F10=Avregning/utskrift
     C     *IN10         IFEQ      *ON
     C                   EXSR      AVREGN
     C                   GOTO      STRPGM
     C                   END                                                    <*IN10
      *** F12=GÂ ut
     C     *IN12         IFEQ      *ON
     C                   GOTO      STRPGM
     C                   END                                                    <*IN12
      *** Roll
     C     *IN25         IFEQ      *ON
     C     *IN94         ANDEQ     *OFF                                          EOF
     C                   Z-ADD     YRECNO        ZRECNO
     C                   MOVE      *OFF          *IN25
     C                   GOTO      START1
     C                   END                                                    <*IN25
      *** Finn valg
     C     ZRECNO        IFNE      0
     C                   EXSR      GETOPT
     C                   END                                                    <ZRECNO
      *
     C                   GOTO      START3
      *
     C     ENDPGM        TAG
     C                   MOVE      *ON           *INLR
     C                   RETURN
      *
      ***********************************************
     C     GETOPT        BEGSR
      ***********************************************
     C                   SETOFF                                       919299
     C     *IN98         DOWEQ     *OFF
     C                   READC     SAM30H50                               98
     C     *IN98         IFEQ      *ON
     C                   GOTO      ENDGET
     C                   END                                                    <*IN98
     C                   MOVEA     '000000'      *IN(30)
      ***  BLANK
     C     WSNR          IFEQ      ' '
     C                   UPDATE    SAM30H50
     C                   ELSE                                                   <WSNR
     C                   Z-ADD     ZRECNO        CURS1
      *** 2=Reaktivere
     C     WSNR          IFEQ      '2'
     C                   MOVE      *BLANKS       WSKODE
     C                   MOVE      *BLANK        WSNR
     C                   UPDATE    SAM30H50
     C                   END                                                    <WSNR
      *** 4=Slette
     C     WSNR          IFEQ      '4'
     C                   MOVE      '*DELETE*'    WSKODE
     C                   MOVE      *BLANK        WSNR
     C                   UPDATE    SAM30H50
     C                   END                                                    <WSNR
     C                   END                                                    <WSNR
      *
     C                   ENDDO
      *
     C     ENDGET        TAG
      *
     C                   ENDSR
      *
      ***********************************************
     C     AVREGN        BEGSR
      ***********************************************
      *
     C                   Z-ADD     0             ZRECNO
      * SKRIV HEADING P≈ PERIODEAVREGNING
     C                   WRITE     HEAD1
     C                   WRITE     STREK
      * SKRIV DETALJEHEADING P≈ PERIODEAVREGNING
     C                   WRITE     HEAD2
     C                   WRITE     STREK
      *
     C     LESSUB        TAG
     C                   ADD       1             ZRECNO
     C     ZRECNO        CHAIN     SAM30H50                           98
     C     *IN98         IFEQ      *OFF
     C     WSKODE        IFNE      '*DELETE*'
      * SKRIV AMETASKJEMA P≈ TUREN
     C                   CALL      'SYAM02'
     C                   PARM                    AHTUNR
     C                   PARM                    WKNA
     C                   PARM                    WFRAP
     C                   EXSR      SKRIVD
     C                   ENDIF                                                  <WSKODE
     C                   GOTO      LESSUB
     C                   ENDIF                                                  <*IN98
      * SKRIV SUM OG AVREGNING
     C                   WRITE     STREK
     C                   WRITE     SUMTUR
     C                   WRITE     STREK
     C                   Z-ADD     WSUME         WTOTE
     C                   ADD       WSUMF         WTOTE
     C                   WRITE     SUMAV1
     C                   Z-ADD     WSUMB         WTOTB
     C                   ADD       WSUMC         WTOTB
     C                   WRITE     SUMAV2
     C     WSUMB         MULT      100           WB100            15 2
     C     WB100         DIV       WTOTB         WPROB
     C     100           SUB       WPROB         WPROC
     C                   WRITE     SUMAV3
     C     WTOTE         SUB       WTOTB         WKORRT           13 2
     C     WKORRT        MULT      WPROB         WK100            15 2
     C     WK100         DIV       100           WKORB
     C     WKORRT        SUB       WKORB         WKORC
     C                   WRITE     SUMAV4
     C                   Z-ADD     WSUMA         WSUMTA
     C                   Z-ADD     WSUMB         WSUMTB
     C                   ADD       WKORB         WSUMTB
     C                   Z-ADD     WSUMC         WSUMTC
     C                   ADD       WKORC         WSUMTC
     C                   Z-ADD     WSUMD         WSUMTD
     C                   Z-ADD     WSUME         WSUMTE
     C                   Z-ADD     WSUMF         WSUMTF
     C                   WRITE     SUMAV5
     C                   WRITE     STREK
     C                   WRITE     AVREG1
     C                   WRITE     AVREG2
     C                   WRITE     AVREG3
     C                   WRITE     AVREG4
     C                   Z-SUB     WSUMTA        WAVRGE
     C                   ADD       WSUMTD        WAVRGE
     C                   ADD       WSUMTB        WAVRGE
     C                   SUB       WSUMTE        WAVRGE
     C     WAVRGE        IFLT      *ZEROS
     C                   MOVE      *ON           *IN30
     C                   ELSE
     C                   MOVE      *OFF          *IN30
     C                   ENDIF
     C                   WRITE     AVREG5
     C                   WRITE     STREK
     C                   ENDSR
      *
      ***********************************************
     C     SKRIVD        BEGSR
      ***********************************************
     C                   Z-ADD     0             WBELA
     C                   Z-ADD     0             WBELB
     C                   Z-ADD     0             WBELC
     C                   Z-ADD     0             WBELD
     C                   Z-ADD     0             WBELE
     C                   Z-ADD     0             WBELF
      *
     C                   Z-ADD     0             TELL              4 0
     C                   MOVE      *BLANKS       ADPOS
     C                   Z-ADD     0             ADUNR
     C     AMDEK         SETLL     AMDE
      *
     C     LESDET        TAG
     C     AMDEKE        READE     AMDE                                   98
     C     *IN98         IFEQ      *OFF
     C     ADPOS         IFNE      '9999'
     C                   ADD       ADAUTG        WBELA
     C                   ADD       ADAINT        WBELB
     C                   ADD       ADEINT        WBELC
     C                   ADD       ADEUTG        WBELD
     C                   ELSE
     C                   ADD       ADAUTG        WBELE
     C                   SUB       ADAINT        WBELE
     C                   SUB       ADEINT        WBELF
     C                   ADD       ADEUTG        WBELF
     C                   ENDIF                                                  <ADPOS
     C                   GOTO      LESDET
     C                   ENDIF                                                  <*IN98
      * SKRIV DETALJEHEADING P≈ PERIODEAVREGNING ??
     C                   ADD       1             TELL
     C     TELL          IFGT      50
     C                   WRITE     HEAD2
     C                   WRITE     STREK
     C                   Z-ADD     0             TELL
     C                   ENDIF                                                  <*IN98
      *
      * SKRIV DETALJELINJE P≈ PERIODEAVREGNING
     C                   WRITE     DETALJ
     C                   ADD       WBELA         WSUMA
     C                   ADD       WBELB         WSUMB
     C                   ADD       WBELC         WSUMC
     C                   ADD       WBELD         WSUMD
     C                   ADD       WBELE         WSUME
     C                   ADD       WBELF         WSUMF
     C                   ENDSR
      *
      ***********************************************
     C     INIT          BEGSR
      ***********************************************
      *
     C                   Z-ADD     0             ZRECNO            4 0
     C                   MOVE      'P'           WFRAP             1
      *
     C     CUNKE1        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    WKNA
     C*
     C     AMHEK3        KLIST
     C                   KFLD                    WKNA
     C                   KFLD                    WPERC
     C                   KFLD                    WPER≈
     C                   KFLD                    WPERM
     C*
     C     AMDEKE        KLIST
     C                   KFLD                    AHTUNR
     C                   KFLD                    WKNA
     C*
     C     AMDEK         KLIST
     C                   KFLD                    AHTUNR
     C                   KFLD                    WKNA
     C                   KFLD                    ADPOS
     C                   KFLD                    ADUNR
     C*
     C                   READ      FIRM                                   66
     C                   ENDSR
