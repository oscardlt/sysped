     A                                      REF(HEADF)
      *_________________________________
      * FILE FOR SEPARAT GIROPRINT     .
      *"""""""""""""""""""""""""""""""""
      *________________________________
      * Format for Ordinary giro Norway
      *""""""""""""""""""""""""""""""""
     A          R SGIR
     A N70N79                           2  7'Faktura nummer:'
     A  70                              2  7'Invoice number:'
     A  79                              2  7'Rechnung nr...:'
     A            XFN            7  0   2 29 EDTCDE(3)
     A  59        KFFDT          6  0   2 67 EDTCDE(Y)
     A N70N79                           3  7'Kundenummer...:'
     A  70                              3  7'Customer no...:'
     A  79                              3  7'Kunden nr.....:'
     A            KUNDNR    R           3 28 REFFLD(KUNDNR CUNDF)
     A                                       EDTCDE(3)
     A N70N79                           4  7'Faktura dato..:'
     A  70                              4  7'Invoice date..:'
     A  79                              4  7'Rechnung datum'
     A            IDAGX          6  0   4 28 EDTCDE(Y)
     A N70N79                           5  7'Forfallsdato..:'
     A  70                              5  7'Date due......:'
     A  79                              5  7'Verfallstag...:'
     A N39        XFFDT          6  0   5 28 EDTCDE(Y)
     A  39                              5 28'Kontant'
     A  39 70                           5 28'Cash'
     A N70N79                           6  7'Vår Referanse.:'
     A  70                              6  7'Our reference.:'
     A  79                              6  7'Unsere ref....:'
     A            HEAVD     R           6 22
     A                                  6 26'/'
     A            HEOPD     R           6 27
     A                                  6 34'/'
     A            HESG      R           6 35
     A N48        KNAVN     R          10  7REFFLD(KNAVN CUNDF)
     A  48        XDINV         30     10  7
     A            XFNAVN        30     10 46
     A N48        ADR1      R          11  7REFFLD(ADR1 CUNDF)
     A  48        XDIAD1        30     11  7
     A            XFADR1        30     11 46
     A N48        ADR2      R          12  7REFFLD(ADR2 CUNDF)
     A  48        XDIAD2        30     12  7
     A            XFADR2        30     12 46
     A N37N48     POSTNR    R          13  7REFFLD(POSTNR CUNDF)
     A                                      DLTEDT
     A N37N48     ADR3      R          13 13REFFLD(ADR3 CUNDF)
     A N38N48     SYPOGE         9     13  7
     A N38N48     ADR3B     R          13 18REFFLD(ADR3 CUNDF)
     A  37 38N48  ADR3C     R          13  7REFFLD(ADR3 CUNDF)
     A  48        XDIAD3        30     13  7
     A            XFPONR         4  0  13 46
     A            XFADR3        24     13 52
      *_________________________________________
      * Format for H-line giro, OCR    5224/5225
      *"""""""""""""""""""""""""""""""""""""""""
     A          R SGIR2A
     A                                      CPI(10)
     A                                      DFNCHR( +
     A                                      X'F0' X'7E818181817E000000' +
     A                                      X'F1' X'00004000FF00000000' +
     A                                      X'F2' X'438589819161000000' +
     A                                      X'F3' X'818191A1D18E000000' +
     A                                      X'F4' X'040C1424448F040000' +
     A                                      X'F5' X'E1A1A1A1928C000000' +
     A                                      X'F6' X'1E2949890906000000' +
     A                                      X'F7' X'80808F90A0C0800000' +
     A                                      X'F8' X'066991919169060000' +
     A                                      X'F9' X'609091929478000000')
     A  59        BANKGK        24     15 17HIGHLIGHT
     A N59        BANKG         15     16 13HIGHLIGHT
     A                                 19  1'H'
     A                                      HIGHLIGHT
     A N59N62     XWKID         19     19 11HIGHLIGHT
     A N59N62     XBAK           1  0  19 30HIGHLIGHT
     A N59 62     KID8           8     19 16HIGHLIGHT
     A N59        XTOTKR        11  0  19 31EDTCDE(3)
     A                                      HIGHLIGHT
     A N59                             19 31'<'
     A                                      HIGHLIGHT
     A N59        XTOTØR         2  0  19 46HIGHLIGHT
     A  59N62     KWKID         19     19  9HIGHLIGHT
     A  59N62     KBAK           1     19 28HIGHLIGHT
     A  59 62     KID8B          8     19 14HIGHLIGHT
     A  59        KTOTKR        11  0  19 29EDTCDE(3)
     A                                      HIGHLIGHT
     A  59        KTOTØR         2  0  19 42HIGHLIGHT
     A  59        KNYMOD         1     19 47HIGHLIGHT
     A            XFBANK        15     19 52HIGHLIGHT
     A          R SGIR2D
     A                                      CPI(10)
     A                                      DFNCHR( +
     A                                      X'F0' X'7E818181817E000000' +
     A                                      X'F1' X'00004000FF00000000' +
     A                                      X'F2' X'438589819161000000' +
     A                                      X'F3' X'818191A1D18E000000' +
     A                                      X'F4' X'040C1424448F040000' +
     A                                      X'F5' X'E1A1A1A1928C000000' +
     A                                      X'F6' X'1E2949890906000000' +
     A                                      X'F7' X'80808F90A0C0800000' +
     A                                      X'F8' X'066991919169060000' +
     A                                      X'F9' X'609091929478000000')
     A            BANKG         15     16 13HIGHLIGHT
     A                                 19  1'H'
     A                                      HIGHLIGHT
     A            XTOTKR        11  0  19 31EDTCDE(3)
     A                                      HIGHLIGHT
     A            XTOTØR         2  0  19 46HIGHLIGHT
     A            XFBANK        15     19 52HIGHLIGHT
      *___________________________________________
      * Format for H-line giro, OCR    4224(*IPDS)
      *"""""""""""""""""""""""""""""""""""""""""""
     A          R SGIR2B
     A                                      FONT(011)
     A  59        BANKGK        24     15 17HIGHLIGHT
     A N59        BANKG         15     16 13HIGHLIGHT
     A                                      CHRID
     A                                 19  1'H'
     A                                      HIGHLIGHT
     A N59N62     XWKID         19     19 11HIGHLIGHT
     A                                      CHRID
     A N59N62     ABAK           1     19 30HIGHLIGHT
     A                                      CHRID
     A N59 62     KID8           8     19 16HIGHLIGHT
     A                                      CHRID
     A N59        ATOTKR        11     19 31HIGHLIGHT
     A                                      CHRID
     A N59        HAKE           1     19 31HIGHLIGHT
     A                                      CHRID
     A N59        ATOTØR         2     19 46HIGHLIGHT
     A                                      CHRID
     A  59N62     KWKID         19     19  9HIGHLIGHT
     A                                      CHRID
     A  59N62     KBAK           1     19 28HIGHLIGHT
     A                                      CHRID
     A  59 62     KID8B          8     19  9HIGHLIGHT
     A                                      CHRID
     A  59        ATOTKK        11     19 29HIGHLIGHT
     A                                      CHRID
     A  59        ATOTØK         2     19 42HIGHLIGHT
     A                                      CHRID
     A  59        KNYMOD         1     19 47HIGHLIGHT
     A                                      CHRID
     A            XFBANK        15     19 52HIGHLIGHT
     A                                      CHRID
     A          R SGIR2E
     A                                      FONT(011)
     A N59        BANKG         15     16 13HIGHLIGHT
     A                                      CHRID
     A                                 19  1'H'
     A                                      HIGHLIGHT
     A N59        ATOTKR        11     19 31HIGHLIGHT
     A                                      CHRID
     A N59        ATOTØR         2     19 46HIGHLIGHT
     A                                      CHRID
     A  59        ATOTKK        11     67 29HIGHLIGHT
     A                                      CHRID
     A  59        ATOTØK         2     67 42HIGHLIGHT
     A                                      CHRID
     A  59        KNYMOD         1     67 47HIGHLIGHT
     A                                      CHRID
     A            XFBANK        15     19 52HIGHLIGHT
     A                                      CHRID
      *_____________________________________________
      * Format for H-line giro, OCR    BELTESSKRIVER
      *"""""""""""""""""""""""""""""""""""""""""""""
     A          R SGIR2C
     A  59        BANKGK        24     15 17HIGHLIGHT
     A N59        BANKG         15     16 13HIGHLIGHT
     A                                      CHRID
     A                                 19  1'H'
     A                                      HIGHLIGHT
     A N59N62     XWKID         19     19 11HIGHLIGHT
     A N59N62     XBAK           1  0  19 30HIGHLIGHT
     A N59 62     KID8           8     19 16HIGHLIGHT
     A N59        XTOTKR        11  0  19 31EDTCDE(3)
     A                                      HIGHLIGHT
     A N59                             19 31'<'
     A                                      HIGHLIGHT
     A N59        XTOTØR         2  0  19 46HIGHLIGHT
     A  59N62     KWKID         19     19  9HIGHLIGHT
     A  59N62     KBAK           1     19 28HIGHLIGHT
     A  59        KID8B          8     19 14HIGHLIGHT
     A  59        KTOTKR        11  0  19 29EDTCDE(3)
     A                                      HIGHLIGHT
     A  59        KTOTØR         2  0  19 42HIGHLIGHT
     A  59        KNYMOD         1     19 47HIGHLIGHT
     A            XFBANK        15     19 52HIGHLIGHT
      *_____________________________________________
      * Format for H-line giro, OCR    BELTESSKRIVER
      * LE*2391(4214-2)  ¤1=DRAFT, ¤2=LOUVIER 10, ¤3=OCR-B(KODESETT877)
      *"""""""""""""""""""""""""""""""""""""""""""""
     A          R SGIR2F
     A  59        BANKGK        24     15 17HIGHLIGHT
     A N59        BANKG         15     16 13HIGHLIGHT
     A                                 18  1'¤Z3'
     A                                 19  1'H'
     A                                      HIGHLIGHT
     A N59N62     XWKID         19     19 11HIGHLIGHT
     A N59N62     XBAK           1  0  19 30HIGHLIGHT
     A N59 62     KID8           8     19 16HIGHLIGHT
     A N59        XTOTKR        11  0  19 31EDTCDE(3)
     A                                      HIGHLIGHT
     A N59                             19 31'<'
     A                                      HIGHLIGHT
     A  59        XTOTØR         2  0  19 46HIGHLIGHT
     A  59N62     KWKID         19     19  9HIGHLIGHT
     A  59N62     KBAK           1     19 28HIGHLIGHT
     A  59 62     KID8B          8     19 14HIGHLIGHT
     A  59        KTOTKR        11  0  19 29EDTCDE(3)
     A  59                                  HIGHLIGHT
     A  59        KTOTØR         2  0  19 42HIGHLIGHT
     A  59        KNYMOD         1     19 47HIGHLIGHT
     A            XFBANK        15     19 52HIGHLIGHT
     A                                 20  1'¤Z1'
      *________________________________
      * Format for H-line giro, Not OCR
      *""""""""""""""""""""""""""""""""
     A          R SGIR3
     A                                 19  2'H'
     A                                      HIGHLIGHT
     A            XTOTKR        11  0  19 29EDTCDE(3)
     A                                      HIGHLIGHT
     A            XTOTØR         2  0  19 42HIGHLIGHT
     A            XFBANK        15     19 52HIGHLIGHT
