      *
      ***********************************************************
      *** PROGRAM SKAL HURTIGREGISATERE B/L OG OPPDRAG        ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 02 06 07 09-11 13-24             ***
      *** LEDIGE INDIKATORER 28 29 31 32 34-83 95-97          ***
      ***********************************************************
      *
     FHEADF     UF   E           K DISK
     FBILLOF    UF   E           K DISK
     FBLV1      IF   E           K DISK
     FCDBOAT    IF   E           K DISK
     FKODTS2    IF   E           K DISK
     FKODTS4    IF   E           K DISK
     FMULTKODT  IF   E           K DISK
     FTRAN      IF   E           K DISK
     FSYSJ20FM  CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
NRFA D                 DS
NRFA D  HERFA                  1     35
NRFA D  HERFA15                1     15
     D                 DS
     D  HEPOS2                 1      4  0
     D  W2POS2                 4      4  0
     D                 DS
     D  WSSDL                  1     20
     D  WSSDL1                 1      1
     D  WSSDL2                 2     20
     D                 DS
     D  HEDTRÅ                 1      4  0
     D  HEDTRM                 5      6  0
     D  HEDTRD                 7      8  0
     D  HEDTR                  1      8  0
     D                 DS
     D  W2DTD                  1      2  0
     D  W2DTM                  3      4  0
     D  W2DTÅ                  5      6  0
     D  W2DT                   1      6  0
     D                 DS
     D  HEDTÅ                  1      4  0
     D  HEDTM                  5      6  0
     D  HEDTD                  7      8  0
     D  HEDTOP                 1      8  0
     D                 DS
     D  WSDTD                  1      2  0
     D  WSDTM                  3      4  0
     D  WSDTÅ                  5      6  0
     D  WSDT                   1      6  0
     D                 DS
     D  BLDTAÅ                 1      4  0
     D  BLDTAM                 5      6  0
     D  BLDTAD                 7      8  0
     D  BLDTA                  1      8  0
     D                 DS
     D  HEGN                   1     15
     D  WHEGN                  5     15
     D                 DS
     D  HEGNN                  1     15
     D  WHEGNN                 5     15
     D                UDS
     D  UFIRM                  1      2
     D  UAVD                  15     18  0
     C*
     C                   EXSR      INIT
     C*
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
     C                   MOVE      W2OPD         YOPD              7 0
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     HEDFK1        SETLL     HEADF
     C     START1        TAG
     C     UAVD          READE     HEADF                                  94
     C  N94              EXCEPT    DMHEAD
     C  N94
     CANN27HEUR          IFNE      'F'
     C                   GOTO      START1
     C                   END
     C  N94
     CAN 27HEUR          IFNE      'G'
     C                   GOTO      START1
     C                   END
      *
     C     START2        TAG
      *
     C  N94              DO
     C                   DO        7
     C                   ADD       1             ZRECNO
     C                   EXSR      KDTFM2
     C                   MOVE      'J'           XLCL
     C     BLV1K         SETLL     BLV1
     C     BLV1K         READE     BLV1                                   33
     C     *IN33         DOWEQ     '0'
     C     BVKDC         IFNE      *BLANKS
     C                   MOVE      'N'           XLCL
     C                   LEAVE
     C                   END
     C     BLV1K         READE     BLV1                                   33
     C  N33              ENDDO
     C                   WRITE     SUBFIL
     C     ST21          TAG
     C     UAVD          READE     HEADF                                  94
     C  N94              EXCEPT    DMHEAD
     C  N94
     CANN27HEUR          IFNE      'F'
     C                   GOTO      ST21
     C                   END
     C  N94
     CAN 27HEUR          IFNE      'G'
     C                   GOTO      ST21
     C                   END
     C  N94              MOVE      HEOPD         YOPD
     C  N94              END
     C                   END
     C                   Z-ADD     ZRECNO        YRECNO            4 0
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     2             POSNBR
     C*
     C     START3        TAG
     C                   SETOFF                                       9998
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYVARE
     C                   ELSE
     C                   WRITE     SUBCTL
     C                   EXFMT     BUNN
     C                   END
     C*
     C***
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C                   Z-ADD     WSSUBN        XRECNO            4 0
     C     XRECNO        IFEQ      0
     C                   Z-ADD     ZRECNO        XRECNO            4 0
     C                   END
     C***
     C*
     C                   SETOFF                                       9830
     C*** BYTTE AVDELINGER
     C   12              DO
     C                   MOVE      *BLANKS       UFIRM
     C                   Z-ADD     0             UAVD
     C                   Z-ADD     1             UFLAGG
     C                   GOTO      SLUTT
     C                   END
     C*** AVSLUTT
     C   03              Z-ADD     3             UFLAGG
     C   03              GOTO      SLUTT
     C*** FRISK OPP
     C   05              GOTO      START
     C*** SØK FRA
     C   08              GOTO      START6
     C*
     C*** BLA VIDERE
     C   25              DO
     C                   Z-ADD     YRECNO        ZRECNO
     C                   SETOFF                                       25
     C  N94HEDFK1        SETLL     HEADF
     C  N94UAVD          READE     HEADF                                  94
     C  N94              EXCEPT    DMHEAD
     C                   GOTO      START2
     C                   END
     C*
     C     ZRECNO        IFNE      0
     C                   EXSR      HHENT
     C                   END
     C   98XRECNO        IFNE      0
     C                   Z-ADD     XRECNO        ZRECNO
     C                   END
     C                   MOVE      *BLANKS       WSTRCN
     C                   GOTO      START3
     C*
     C*** SØK FRA
     C     START6        TAG
     C                   EXFMT     VAREL2
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     30            POSNBR
     C   12              GOTO      START3
     C                   GOTO      START
     C*
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
     C*
     C***********************************************
     C     TESTB1        BEGSR
     C***********************************************
     C*
     C*** HENTE LOSSESTED
     C     WSSDL2        IFEQ      *BLANKS
     C     KODTEK        CHAIN     KOTMR                              33
     C  N33              MOVE      KOTMNV        WSSDL
     C                   END
     C*
     C*** TEST FRANKATUR
     C     WSFR          IFNE      *BLANKS
     C     KODFRK        CHAIN     KOFRR                              90
     C   90              DO
     C                   Z-ADD     20            LINNBR
     C                   Z-ADD     39            POSNBR
     C                   MOVE      'SPF0006'     MSGNR
     C                   SETON                                        30
     C                   END
     C                   END
     C*
     C*** TEST OPPDRAGSTYPE
     C     WSOT          IFNE      *BLANKS
     C     KODOTK        CHAIN     KOOTYR                             89
     C   89
     CANN30              DO
     C                   Z-ADD     20            LINNBR
     C                   Z-ADD     46            POSNBR
     C                   MOVE      'SPO0007'     MSGNR
     C                   SETON                                        30
     C                   END
     C                   END
     C*
     C*** TEST DATO
     C                   MOVE      WSDT          XMMÅÅ             4 0
     C                   MOVE      XMMÅÅ         UÅÅ               2 0
     C                   MOVEL     XMMÅÅ         UMM               2 0
     C                   MOVEL     WSDT          UDD               2 0
     C                   CALL      'TESTDATO'
     C                   PARM                    UDD
     C                   PARM                    UMM
     C                   PARM                    UÅÅ
     C                   PARM                    UFLAGG            1 0
     C     UFLAGG        COMP      1                                      88
     C   88
     CANN30              DO
     C                   Z-ADD     20            LINNBR
     C                   Z-ADD     56            POSNBR
     C                   MOVE      'SPD0001'     MSGNR
     C                   SETON                                        30
     C                   END
     C*
     C*** TEST LOKAL BÅT
     C     WSLB          IFNE      *BLANKS
     C     WSLB          CHAIN     CDBOAT                             87
     C   87
     CANN30              DO
     C                   Z-ADD     21            LINNBR
     C                   Z-ADD     40            POSNBR
     C                   MOVE      'SPB0102'     MSGNR
     C                   SETON                                        30
     C                   END
     C                   END
     C*
     C*** TEST TRANSPORTMÅTE
     C     WSTRM         IFNE      0
     C     KODS4K        CHAIN     KODTS4                             86
     C   86
     CANN30              DO
     C                   Z-ADD     21            LINNBR
     C                   Z-ADD     77            POSNBR
     C                   MOVE      'SPT0003'     MSGNR
     C                   SETON                                        30
     C                   END
     C                   END
     C*
     C*** TEST LANDKODE TIL TRANSPORTIDENTITET
     C     WSTRI         IFNE      *BLANKS
     C     KODS2K        CHAIN     KODTS2                             85
     C   85
     CANN30              DO
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     17            POSNBR
     C                   MOVE      'SPL0003'     MSGNR
     C                   SETON                                        30
     C                   END
     C                   END
     C*
     C*** TRANSPORTØR
     C     WSKNT         IFNE      0
     C     TRANK         CHAIN     TRAN                               84
     C   84
     CANN30              DO
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     58            POSNBR
     C                   MOVE      'SPK0009'     MSGNR
     C                   SETON                                        30
     C                   END
     C                   END
     C*
     C   30              GOTO      SLTB1
     C*
     C*
     C*** OPPDATERE PÅ REGISTER OG SUBFIL
     C     HEADFK        CHAIN     HEADF                              33
     C  N33              EXSR      MOVWOP
     C  N33              EXCEPT    NYHEAD
     C     BILLK         CHAIN     BILLOF                             33
     C  N33              EXSR      MOVWBL
     C  N33              EXCEPT    NYBILL
     C                   EXSR      KDTFM2
     C                   UPDATE    SUBFIL
     C                   WRITE     SUBCTL
     C*
     C     SLTB1         ENDSR
     C*
     C***********************************************
     C     HHENT         BEGSR
     C***********************************************
     C                   Z-ADD     0             LINNBR
     C                   SETOFF                                       919299
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C   98              DO
     C   99              SETOFF                                       98
     C                   Z-ADD     2             LINNBR
     C                   GOTO      SLHENT
     C                   END
     C     ZRECNO        DIV       7             LINNBR
     C                   MVR                     XLIN              2 0
     C                   MULT      2             XLIN
     C     XLIN          IFEQ      0
     C                   Z-ADD     14            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C     WSNR          IFEQ      '2'
     C                   MOVE      ' '           WSNR
     C                   EXSR      CHGOPD
     C   12              GOTO      SLHENT
     C                   END
     C                   END
     C*
     C     SLHENT        TAG
     C                   ADD       4             LINNBR
     C                   Z-ADD     2             POSNBR
     C                   SETOFF                                       99
     C   98              DO
     C                   MOVE      'DIV0012'     MSGNR
     C                   SETON                                            30
     C                   END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     CHGOPD        BEGSR
     C***********************************************
     C                   EXSR      MOVOPW
     C                   SETON                                        99
     C     STCHG         TAG
     C                   EXSR      KDTFMT
     C                   EXFMT     VARELI
     C                   EXSR      KDTFMT
     C                   MOVEA     '0000000'     *IN(84)
     C                   SETOFF                                         30
     C*
     C***
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C                   Z-ADD     WSSUBN        XRECNO            4 0
     C     XRECNO        IFEQ      0
     C                   Z-ADD     ZRECNO        XRECNO            4 0
     C                   END
     C***
     C*
     C   04              DO
     C                   EXSR      SØKDTA
     C                   GOTO      STCHG
     C                   END
     C   12              DO
     C                   UPDATE    SUBFIL
     C                   END
     C  N12              EXSR      TESTB1
     C  N12
     CAN 30              GOTO      STCHG
     C     XLIN          IFEQ      0
     C                   Z-ADD     14            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVWOP        BEGSR
     C***********************************************
     C     WSFR          IFNE      *BLANKS
     C                   MOVE      WSFR          HEFR
     C                   END
     C     WSOT          IFNE      *BLANKS
     C                   MOVE      WSOT          HEOT
     C                   END
     C                   MOVE      WSDTD         HEDTD
     C                   MOVE      WSDTM         HEDTM
     C     WSDTÅ         IFGT      50
     C     WSDTÅ         ADD       1900          HEDTÅ
     C                   ELSE
     C     WSDTÅ         ADD       2000          HEDTÅ
     C                   END
     C*          WSPOS1    IFNE 0
     C     WSPOS1        IFNE      *BLANKS
     C                   MOVE      WSPOS1        HEPOS1
     C                   MOVE      WSPOS1        XPOS1
     C                   ELSE
     C     XTRCN         IFEQ      WSTRCN
     C     XTRCN         ANDNE     *BLANKS
     C     XPOS1         ANDNE     *BLANKS
     C     XLCL          ANDEQ     'J'
     C                   CALL      'SYFA10C'
     C                   PARM                    XPOS1             7
     C                   MOVE      XPOS1         HEPOS1
     C                   END
     C                   END
     C     WSPOS2        IFNE      0
     C                   MOVE      WSPOS2        HEPOS2
     C                   END
     C     WSGN          IFNE      *BLANKS
     C                   MOVE      WSGN          HEGN
     C                   END
     C     WSLB          IFNE      *BLANKS
     C                   MOVE      WSLB          HELB
     C                   END
     C     WSSDL         IFNE      *BLANKS
     C                   MOVE      WSSDL         HESDL
     C                   END
     C     WSTRM         IFNE      0
     C                   MOVE      WSTRM         HETRM
     C                   END
     C     WSTRI         IFNE      *BLANKS
     C                   MOVE      WSTRI         HETRI
     C                   END
     C     WSTRC         IFNE      0
     C                   MOVE      WSTRC         HETRC
     C                   END
     C     WSTRCN        IFNE      *BLANKS
     C                   MOVE      WSTRCN        HETRCN
     C                   MOVE      WSTRCN        XTRCN            17
     C                   END
     C     WSKNT         IFNE      0
     C                   MOVE      WSKNT         HEKNT
     C                   END
     C     WSNTF         IFNE      0
     C                   MOVE      WSNTF         HENTF
     C                   END
     C     WSRFA         IFNE      *BLANKS
     C                   MOVE      WSRFA         HERFA
     C                   END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVWBL        BEGSR
     C***********************************************
     C     WSLB          IFNE      *BLANKS
     C                   MOVE      WSLB          BLLB
     C                   END
     C                   MOVE      WSDTD         BLDTAD
     C                   MOVE      WSDTM         BLDTAM
     C     WSDTÅ         IFGT      50
     C     WSDTÅ         ADD       1900          BLDTAÅ
     C                   ELSE
     C     WSDTÅ         ADD       2000          BLDTAÅ
     C                   END
     C     WSTRCN        IFNE      *BLANKS
     C                   MOVE      WSTRCN        BLMN
     C                   END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVOPW        BEGSR
     C***********************************************
     C                   MOVE      HERFA         WSRFA
     C                   MOVE      HEFR          WSFR
     C                   MOVE      HEOT          WSOT
     C     WSDT          IFEQ      0
     C                   MOVE      HEDTD         WSDTD
     C                   MOVE      HEDTM         WSDTM
     C                   MOVE      HEDTÅ         WSDTÅ
     C                   END
     C     HEPOS1        IFNE      *BLANKS
     C                   MOVE      HEPOS1        WSPOS1
     C                   ELSE
     C     HETRCN        IFEQ      XTRCN
     C     HETRCN        ANDNE     *BLANKS
     C     XPOS1         ANDNE     *BLANKS
     C     XLCL          ANDEQ     'J'
      * ØK POSISJON MED 1
     C                   CALL      'SYFA10C'
     C                   PARM                    WSPOS1
     C                   ELSE
     C                   MOVE      *BLANKS       WSPOS1
     C                   END
     C                   END
     C                   MOVE      HEPOS2        WSPOS2
     C     WSGN          IFEQ      *BLANKS
     C                   MOVE      HEGN          WSGN
     C                   END
     C     WSLB          IFEQ      *BLANKS
     C                   MOVE      HELB          WSLB
     C                   END
     C     WSSDL         IFEQ      *BLANKS
     C                   MOVE      HESDL         WSSDL
     C                   END
     C     WSTRM         IFEQ      0
     C                   MOVE      HETRM         WSTRM
     C                   END
     C     WSTRI         IFEQ      *BLANKS
     C                   MOVE      HETRI         WSTRI
     C                   END
     C     WSTRCN        IFEQ      *BLANKS
     C                   MOVE      HETRCN        WSTRCN
     C                   END
     C                   MOVE      HEKNT         WSKNT
     C                   MOVE      HENTF         WSNTF
     C                   Z-ADD     20            LINNBR
     C                   Z-ADD     19            POSNBR
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     SØKDTA        BEGSR
     C***********************************************
     C                   Z-ADD     1             UFLAGG
     C     LINNBR        IFEQ      22
     C     POSNBR        ANDGE     58
     C     POSNBR        ANDLE     65
     C                   CALL      'SYTR04A'
     C                   PARM                    WSKNT
     C                   Z-ADD     73            POSNBR
     C                   GOTO      SLSØK
     C                   END
     C*
     C     LINNBR        IFEQ      21
     C     POSNBR        ANDGE     52
     C     POSNBR        ANDLE     71
     C                   CALL      'SYFI10J'
     C                   PARM                    WSSDL
     C                   Z-ADD     77            POSNBR
     C                   GOTO      SLSØK
     C                   END
     C*
     C     LINNBR        IFEQ      21
     C     POSNBR        ANDGE     40
     C     POSNBR        ANDLE     44
     C                   CALL      'SYSJ11A'
     C                   PARM                    WSLB
     C                   Z-ADD     52            POSNBR
     C                   GOTO      SLSØK
     C                   END
     C*
     C     LINNBR        IFEQ      22
     C     POSNBR        ANDGE     17
     C     POSNBR        ANDLE     18
     C                   CALL      'SYFI10L'
     C                   PARM                    WSTRI
     C                   PARM                    USPED
     C                   Z-ADD     25            POSNBR
     C                   GOTO      SLSØK
     C                   END
     C*
     C     LINNBR        IFEQ      20
     C     POSNBR        ANDGE     46
     C     POSNBR        ANDLE     47
     C                   CALL      'SYFI10C'
     C                   PARM                    WSOT
     C                   Z-ADD     56            POSNBR
     C                   GOTO      SLSØK
     C                   END
     C*
     C     LINNBR        IFEQ      20
     C     POSNBR        ANDGE     39
     C     POSNBR        ANDLE     41
     C                   MOVE      'J'           USPED             1
     C                   CALL      'SYFI10G'
     C                   PARM                    WSFR
     C                   PARM                    USPED
     C                   Z-ADD     46            POSNBR
     C                   GOTO      SLSØK
     C                   END
     C*
     C     LINNBR        IFEQ      21
     C     POSNBR        ANDGE     77
     C     POSNBR        ANDLE     78
     C                   CALL      'SAD061I'
     C                   PARM                    WSTRM
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     17            POSNBR
     C                   GOTO      SLSØK
     C                   END
     C*
     C                   MOVE      'DIV1037'     MSGNR
     C                   SETON                                        30
     C*
     C     SLSØK         ENDSR
     C*
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     TRANK         KLIST
     C                   KFLD                    UFIRM
     C                   KFLD                    WSKNT
     C     HEADFK        KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    HEOPD
     C     BILLK         KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    HEOPD
     C     BLV1K         KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C     HEDFK1        KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    YOPD
     C     KODTEK        KLIST
     C                   KFLD                    XTEUNI
     C                   KFLD                    WSSDL1
     C     KODOTK        KLIST
     C                   KFLD                    XOTUNI
     C                   KFLD                    WSOT
     C     KODFRK        KLIST
     C                   KFLD                    XFRUNI
     C                   KFLD                    WSFR
     C     KODS2K        KLIST
     C                   KFLD                    XS2UNI
     C                   KFLD                    WSTRI
     C     KODS4K        KLIST
     C                   KFLD                    XS4UNI
     C                   KFLD                    WSTRM
     C*
     C     *ENTRY        PLIST
     C                   PARM                    UFLAGG            1 0
      *
      * IND. 27 AV ---> IMPORT,   IND. 27 PÅ ---> EXPORT
     C     UFLAGG        COMP      1                                      27
     C*
     C                   MOVE      'N'           USPED             1
     C                   MOVE      'J'           USØK              1
     C                   MOVE      'TERM'        XTEUNI            4
     C                   MOVE      'OTY'         XOTUNI            3
     C                   MOVE      'FR'          XFRUNI            2
     C                   MOVE      'S2 '         XS2UNI            3
     C                   MOVE      'S4 '         XS4UNI            3
     C                   MOVE      *ZEROS        W2DT
     C                   MOVE      *ZEROS        WSDT
     C                   MOVE      *ZEROS        BLDTA
     C                   MOVE      *ZEROS        HEDTOP
     C                   MOVE      *ZEROS        HEDTR
     C                   MOVE      *ZEROS        HEPOS2
     C*
     C                   WRITE     NYVARE
     C                   EXFMT     VAREL2
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     KDTFMT        BEGSR
     C***********************************************
     C                   MOVE      WSDTD         UDT1              2 0
     C                   MOVE      WSDTM         UDT2              2 0
     C                   MOVE      WSDTÅ         UDT3              2 0
     C                   CALL      'DATOFMT'
     C                   PARM                    UDT1
     C                   PARM                    UDT2
     C                   PARM                    UDT3
     C                   MOVE      UDT1          WSDTD
     C                   MOVE      UDT2          WSDTM
     C                   MOVE      UDT3          WSDTÅ
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     KDTFM2        BEGSR
     C***********************************************
     C                   MOVE      HEDTD         UDT1              2 0
     C                   MOVE      HEDTM         UDT2              2 0
     C                   MOVE      HEDTÅ         UDT3              2 0
     C                   CALL      'DATOFMT'
     C                   PARM                    UDT1
     C                   PARM                    UDT2
     C                   PARM                    UDT3
     C                   MOVE      UDT1          W2DTD
     C                   MOVE      UDT2          W2DTM
     C                   MOVE      UDT3          W2DTÅ
     C*
     C                   ENDSR
     C*
     OHEADFR    E            DMHEAD
     OHEADFR    E            NYHEAD
     O                       HEDTOP
     O                       HEGN
     O                       HEOT
     O                       HEPOS1
     O                       HEFR
     O                       HEKNT
     O                       HETRM
     O                       HETRI
     O                       HETRC
     O                       HETRCN
     O                       HEDTR
     O                       HERFA
     O                       HESDL
     O                       HEPOS2
     O                       HENTF
     O                       HELB
     OBILLOFR   E            NYBILL
     O                       BLLB
     O                       BLDTA
     O                       BLMN
