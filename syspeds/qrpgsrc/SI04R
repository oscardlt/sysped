      *
     FSI04D     CF   E             WORKSTN
     F                                     SFILE(SI04DG:ZRECNO)
     F                                     INFDS(FBAREA)
     FSIF       IF   E           K DISK
     FFIRM      IF   E           K DISK
     FFAIN      IF   E           K DISK
     FHEADF     IF   E           K DISK
     FHEAD4RMT  IF   E           K DISK
     F                                     RENAME(HEADFR:HEAD4R)
     FHEAD5RMT  IF   E           K DISK
     F                                     RENAME(HEADFR:HEAD5R)
     FSTED2     IF   E           K DISK
     FKODTFR    IF   E           K DISK
     FFRISOL01  IF   E           K DISK
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D                 DS
NRFA D  HERFA                  1     35
NRFA D  HERFA15                1     15
     D                 DS
     D  ZDTF8                  1      8  0
     D  ZDTFC                  1      2  0
     D  ZDTF                   3      8  0
     D                 DS
     D  ZDTT8                  1      8  0
     D  ZDTTC                  1      2  0
     D  ZDTT                   3      8  0
     D                 DS
     D  WA                     1     15
     D                                     DIM(15)                              Godsnummer
     D  WEGN                   1     15
     D                 DS
     D  WB                     1     15
     D                                     DIM(15)                              Agentref
     D  WERFA                  1     15
     D                 DS
     D  WDT                    1      6  0
     D  WÅ                     1      2  0
     D  WM                     3      4  0
     D  WD                     5      6  0
      *//////////////
      *  Hodelogikk /
      *//////////////
     C                   EXSR      SRA
     C                   EXSR      SRB
     C                   MOVE      '1'           *INLR
      *////////////////////////////////////////////////////////////
      *  F4=Søk                                              PMTB /
      *////////////////////////////////////////////////////////////
     C     PMTB          BEGSR
      *_____________
      * Søk Fra sted
      *"""""""""""""
     C     ZLIN          IFEQ      5
     C                   MOVE      '*SECU'       ZESDF
     C                   MOVE      *BLANKS       UNVN
     C                   MOVE      *BLANKS       ULK
     C                   CALL      'SYFI10F'
     C     ZESDF         PARM      ZESDF         ZESDF
     C                   PARM                    UNVN             30
     C                   PARM                    ULK               2
     C                   GOTO      PMTB9
     C                   END
      *_____________
      * Søk Til sted
      *"""""""""""""
     C     ZLIN          IFEQ      6
     C                   MOVE      '*SECU'       ZESDT
     C                   MOVE      *BLANKS       UNVN
     C                   MOVE      *BLANKS       ULK
     C                   CALL      'SYFI10F'
     C     ZESDT         PARM      ZESDT         ZESDT
     C                   PARM                    UNVN             30
     C                   PARM                    ULK               2
     C                   GOTO      PMTB9
     C                   END
     C     PMTB9         ENDSR
      *////////////////////////////////////////////////////////////
      *  Initiering                                           SRA /
      *////////////////////////////////////////////////////////////
     C     SRA           BEGSR
      *___________________
      * Definiere Workfelt
      *"""""""""""""""""""
     C                   MOVE      *ZEROS        WSKNR             8 0
     C                   MOVE      *ZEROS        WX                2 0
      *________________
      * Input paramtrer
      *""""""""""""""""
     C     *ENTRY        PLIST
     C                   PARM                    WUSR             10
      *__________________
      * Definiere Nøkkler
      *""""""""""""""""""
     C     KEYA          KLIST                                                  SIF
     C                   KFLD                    WUSR                           SIF
      *
     C     KEYB          KLIST                                                  KUNDL01
     C                   KFLD                    FIFIRM                         KUNDL01
     C                   KFLD                    WSKNR                          KUNDL01
      *
     C     KEYC          KLIST                                                  HEADF
     C                   KFLD                    FIAVD                          HEADF
     C                   KFLD                    FIOPD                          HEADF
     C     KEYD          KLIST                                                  HEADF
     C                   KFLD                    HEAVD                          HEADF
     C                   KFLD                    HEOPD                          HEADF
     C     KEYE          KLIST                                                  KODTFR
     C                   KFLD                    KFRUNI                         KODTFR
     C                   KFLD                    HEFR                           KODTFR
     C     KEYF          KLIST                                                  FRISOL01
     C                   KFLD                    ZFRI1                          FRISOL01
     C                   KFLD                    ZFRI2                          FRISOL01
     C     KEYF1         KLIST                                                  HEADF
     C                   KFLD                    FSAVD                          HEADF
     C                   KFLD                    FSOPD                          HEADF
      *_____________
      * Init av felt
      *"""""""""""""
     C                   MOVE      'FR'          KFRUNI
     C                   MOVE      *ZEROS        ZDTF8
     C                   MOVE      *ZEROS        ZDTT8
      *________________
      * Hente Firmakode
      *""""""""""""""""
     C                   MOVE      *BLANKS       FIFIRM
     C     FIFIRM        SETLL     FIRM
     C                   SETOFF                                       51
     C                   READ      FIRM                                   51
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Spørring.                                            SRB /
      *////////////////////////////////////////////////////////////
     C     SRB           BEGSR
      *__________________
      * Hente kundenummer
      *""""""""""""""""""
     C     KEYA          CHAIN     SIF                                51
      *___________________
      * Bilde-A, valgbilde
      *"""""""""""""""""""
     C     SRB1          TAG
     C  N30              MOVE      'SY00000'     ZMSGNO
     C                   WRITE     SI04DØ
     C                   EXFMT     SI04DA
     C                   MOVEA     '000'         *IN(30)
      * Funksjonstaster
     C     *IN03         CABEQ     '1'           SRB9
     C     *IN12         CABEQ     '1'           SRB9
      * Teste Input
     C                   EXSR      SRBA
     C   30              GOTO      SRB1
     C     *IN41         IFEQ      '0'
     C     *IN42         ANDEQ     '0'
     C     *IN43         ANDEQ     '0'
     C     *IN44         ANDEQ     '0'
     C     *IN45         ANDEQ     '0'
     C     *IN46         ANDEQ     '0'
     C                   GOTO      SRB1
     C                   END
      *________________
      * Bilde-B C D E F
      *""""""""""""""""
     C     SRB2          TAG
      *
     C  N30              MOVE      'SY00000'     ZMSGNO
     C                   WRITE     SI04DØ
     C   41              EXFMT     SI04DB
     C   42              EXFMT     SI04DC
     C   43              EXFMT     SI04DD
     C   44              EXFMT     SI04DE
     C   45              EXFMT     SI04DF
     C   46              EXFMT     SI04DK
     C                   MOVEA     '00000000'    *IN(30)
      *________________
      * Funksjonstaster
      *""""""""""""""""
     C     *IN03         CABEQ     '1'           SRB9
     C     *IN12         CABEQ     '1'           SRB1
      * Konvertere Cursorlocation
     C*  45                CALL 'SYFI10E' PLISTD
     C   45              DO
     C     WSCLOC        DIV       256           ZLIN
     C                   MVR                     ZPOS
     C                   Z-ADD     WSSUBN        XRECNO            4 0
     C                   END
      * Søk
     C   45*IN04         IFEQ      '1'
     C                   EXSR      PMTB
     C                   GOTO      SRB2
     C                   END
      * Teste Input
     C                   EXSR      SRBB
     C   30              GOTO      SRB2
      * Les og fyll subfile
     C                   EXSR      SRBC
     C   30              GOTO      SRB2
      *______________________
      * Bilde-H, vise subfile
      *""""""""""""""""""""""
     C     SRB3          TAG
      *
     C                   Z-ADD     1             ZRECNO
     C                   MOVEA     '11'          *IN(91)
     C  N30              MOVE      'SY00000'     ZMSGNO
     C                   WRITE     SI04DØ
     C                   WRITE     SI04DI
     C                   EXFMT     SI04DH
      * Funksjonstaster
     C     *IN03         CABEQ     '1'           SRB9
     C     *IN12         CABEQ     '1'           SRB2
      *_______
      * Valg ?
      *"""""""
     C                   MOVE      '0'           *IN52
     C                   MOVE      '0'           *IN95
     C     *IN52         DOWEQ     '0'
      *
     C                   READC     SI04DG                                 52
     C     *IN52         IFEQ      '0'
     C     ZOP           CASEQ     '5'           SRBH                           VISE
     C                   END
     C                   END
      *
     C     *IN03         CABEQ     '1'           SRB9
     C  N52              END
     C                   GOTO      SRB3
      *
     C     SRB9          ENDSR
      *////////////////////////////////////////////////////////////
      *  Valg av type spørring                               SRBA /
      *////////////////////////////////////////////////////////////
     C     SRBA          BEGSR
      * Hvilken type av spørring ?
     C                   MOVEA     '00000'       *IN(41)
     C     ZALT1         COMP      '1'                                    41     Faktura
     C     ZALT2         COMP      '1'                                    42     Godsnr
     C     ZALT3         COMP      '1'                                    43     Aref
     C     ZALT4         COMP      '1'                                    44     Dato
     C     ZALT5         COMP      '1'                                    45     Sted
     C     ZALT6         COMP      '1'                                    46     Fri søkevei
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Teste bilde B, C, D, E eller F                      SRBB /
      *////////////////////////////////////////////////////////////
     C     SRBB          BEGSR
     C     *IN44         IFEQ      '1'
     C     *IN45         OREQ      '1'
     C     ZDTT          IFEQ      0
     C                   Z-ADD     501231        ZDTT
     C                   END
     C     ZDTF          IFGT      0
     C                   Z-ADD     ZDTF          WDT
     C     WM            COMP      1                                    30
     C  N30WM            COMP      12                                 30
     C  N30WD            COMP      1                                    30
     C  N30WD            COMP      31                                 30
     C   30              MOVE      'SPD0001'     ZMSGNO
     C   30
     CAN 44              MOVE      '1'           *IN31
     C   30
     CAN 45              MOVE      '1'           *IN33
     C   30              GOTO      SRBB9
     C     ZDTF          IFGT      500000
     C                   MOVE      '19'          ZDTFC
     C                   ELSE
     C                   MOVE      '20'          ZDTFC
     C                   END
     C                   END
     C                   Z-ADD     ZDTT          WDT
     C     WM            COMP      1                                    30
     C  N30WM            COMP      12                                 30
     C  N30WD            COMP      1                                    30
     C  N30WD            COMP      31                                 30
     C   30              MOVE      'SPD0001'     ZMSGNO
     C   30
     CAN 44              MOVE      '1'           *IN32
     C   30
     CAN 45              MOVE      '1'           *IN34
     C   30              GOTO      SRBB9
     C     ZDTT          IFGT      500000
     C                   MOVE      '19'          ZDTTC
     C                   ELSE
     C                   MOVE      '20'          ZDTTC
     C                   END
     C                   END
      *
     C     *IN45         IFEQ      '1'
     C     ZESDF         CHAIN     STED2                              30
     C   30              MOVE      'SPS0012'     ZMSGNO
     C   30              MOVE      '1'           *IN31
     C   30              GOTO      SRBB9
     C     ZESDT         CHAIN     STED2                              30
     C   30              MOVE      'SPS0013'     ZMSGNO
     C   30              MOVE      '1'           *IN32
     C   30              GOTO      SRBB9
     C                   END
      *
     C     SRBB9         ENDSR
      *////////////////////////////////////////////////////////////
      *  Tømm og fyll subfile                                SRBC /
      *////////////////////////////////////////////////////////////
     C     SRBC          BEGSR
      *______________
      * Tømme Subfile
      *""""""""""""""
     C                   MOVE      '1'           *IN93
     C                   WRITE     SI04DH
     C                   MOVEA     '000'         *IN(93)
     C                   Z-ADD     *ZERO         ZRECNO
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C   41              EXSR      SRBCA
     C   42              EXSR      SRBCB
     C   43              EXSR      SRBCC
     C   44              EXSR      SRBCD
     C   45              EXSR      SRBCE
     C   46              EXSR      SRBCF
      *_________________
      * Ingenting å vise
      *"""""""""""""""""
     C     ZRECNO        IFEQ      0
     C                   MOVE      '1'           *IN30
     C                   MOVE      'SPR0002'     ZMSGNO
     C                   END
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Fyll subfile via fakturanummer                     SRBCA /
      *////////////////////////////////////////////////////////////
     C     SRBCA         BEGSR
     C     ZEFNK         CHAIN     FAIN                               94
     C  N94KEYC          CHAIN     HEADF                              94
     C  N94              ADD       1             ZRECNO
     C  N94              WRITE     SI04DG
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Fyll subfile via godsnummer                        SRBCB /
      *////////////////////////////////////////////////////////////
     C     SRBCB         BEGSR
      * Fikse stopper for søking
     C                   Z-ADD     15            WX
     C                   MOVE      *BLANKS       WA
     C                   MOVE      ZEGN          WEGN
     C     WX            DOWGT     0
     C     WA(WX)        ANDEQ     *BLANKS
     C                   MOVE      '9'           WA(WX)
     C                   SUB       1             WX
     C                   END
      * LES MOT KJØPER
     C     IKN           SETLL     HEAD4RMT
     C     IKN           READE     HEAD4RMT                               94
      *
     C  N94*IN94         DOWEQ     '0'
     C     HEGN          CABLT     ZEGN          SRBCB1
     C     HEGN          CABGT     WEGN          SRBCB1
     C                   ADD       1             ZRECNO
     C                   WRITE     SI04DG
     C     SRBCB1        TAG
     C     IKN           READE     HEAD4RMT                               94
     C  N94              END
      * LES MOT SELGER
     C                   MOVE      '0'           *IN94
     C     IKN           SETLL     HEAD5RMT
     C     IKN           READE     HEAD5RMT                               94
      *
     C  N94*IN94         DOWEQ     '0'
     C     HEGN          CABLT     ZEGN          SRBCB2
     C     HEGN          CABGT     WEGN          SRBCB2
     C                   ADD       1             ZRECNO
     C                   WRITE     SI04DG
     C     SRBCB2        TAG
     C     IKN           READE     HEAD5RMT                               94
     C  N94              END
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Fyll subfile via agentref                          SRBCC /
      *////////////////////////////////////////////////////////////
     C     SRBCC         BEGSR
      * Fikse stopper for søking
     C                   Z-ADD     15            WX
     C                   MOVE      *BLANKS       WB
     C                   MOVE      ZERFA         WERFA
     C     WX            DOWGT     0
     C     WB(WX)        ANDEQ     *BLANKS
     C                   MOVE      '9'           WB(WX)
     C                   SUB       1             WX
     C                   END
      * KJØPER
     C     IKN           SETLL     HEAD4RMT
     C     IKN           READE     HEAD4RMT                               94
      *
     C  N94*IN94         DOWEQ     '0'
     C     HERFA         CABLT     ZERFA         SRBCC1
     C     HERFA         CABGT     WERFA         SRBCC1
     C                   ADD       1             ZRECNO
     C                   WRITE     SI04DG
     C     SRBCC1        TAG
     C     IKN           READE     HEAD4RMT                               94
     C  N94              END
      * SELGER
     C                   MOVE      '0'           *IN94
     C     IKN           SETLL     HEAD5RMT
     C     IKN           READE     HEAD5RMT                               94
      *
     C  N94*IN94         DOWEQ     '0'
     C     HERFA         CABLT     ZERFA         SRBCC2
     C     HERFA         CABGT     WERFA         SRBCC2
     C                   ADD       1             ZRECNO
     C                   WRITE     SI04DG
     C     SRBCC2        TAG
     C     IKN           READE     HEAD5RMT                               94
     C  N94              END
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Fyll subfile via dato                              SRBCD /
      *////////////////////////////////////////////////////////////
     C     SRBCD         BEGSR
      * KJØPER
     C     IKN           SETLL     HEAD4RMT
     C     IKN           READE     HEAD4RMT                               94
     C     *IN94         DOWEQ     '0'
     C     HEDTOP        CABLT     ZDTF8         SRBCD1
     C     HEDTOP        CABGT     ZDTT8         SRBCD1
     C                   ADD       1             ZRECNO
     C                   WRITE     SI04DG
     C     SRBCD1        TAG
     C     IKN           READE     HEAD4RMT                               94
     C  N94              END
      * SELGER
     C                   MOVE      '0'           *IN94
     C     IKN           SETLL     HEAD5RMT
     C     IKN           READE     HEAD5RMT                               94
     C     *IN94         DOWEQ     '0'
     C     HEDTOP        CABLT     ZDTF8         SRBCD2
     C     HEDTOP        CABGT     ZDTT8         SRBCD2
     C                   ADD       1             ZRECNO
     C                   WRITE     SI04DG
     C     SRBCD2        TAG
     C     IKN           READE     HEAD5RMT                               94
     C  N94              END
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Fyll subfile via sted                              SRBCE /
      *////////////////////////////////////////////////////////////
     C     SRBCE         BEGSR
      *KJØPER
     C     IKN           SETLL     HEAD4RMT
     C     IKN           READE     HEAD4RMT                               94
      *
     C  N94*IN94         DOWEQ     '0'
     C     ZESDF         IFNE      *BLANKS
     C     HESDF         CABNE     ZESDF         SRBCE1
     C                   END
     C     ZESDT         IFNE      *BLANKS
     C     HESDT         CABNE     ZESDT         SRBCE1
     C                   END
     C     HEDTOP        CABLT     ZDTF8         SRBCE1
     C     HEDTOP        CABGT     ZDTT8         SRBCE1
     C                   ADD       1             ZRECNO
     C                   WRITE     SI04DG
     C     SRBCE1        TAG
     C     IKN           READE     HEAD4RMT                               94
     C  N94              END
      *SELGER
     C                   MOVE      '0'           *IN94
     C     IKN           SETLL     HEAD5RMT
     C     IKN           READE     HEAD5RMT                               94
      *
     C  N94*IN94         DOWEQ     '0'
     C     ZESDF         IFNE      *BLANKS
     C     HESDF         CABNE     ZESDF         SRBCE2
     C                   END
     C     ZESDT         IFNE      *BLANKS
     C     HESDT         CABNE     ZESDT         SRBCE2
     C                   END
     C     HEDTOP        CABLT     ZDTF8         SRBCE2
     C     HEDTOP        CABGT     ZDTT8         SRBCE2
     C                   ADD       1             ZRECNO
     C                   WRITE     SI04DG
     C     SRBCE2        TAG
     C     IKN           READE     HEAD5RMT                               94
     C  N94              END
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Fyll subfile via fri søkevei                       SRBCF /
      *////////////////////////////////////////////////////////////
     C     SRBCF         BEGSR
     C     KEYF          SETLL     FRISOL01
     C     SRBCF1        TAG
     C     ZFRI1         READE     FRISOL01                               94
     C  N94              DO
     C     KEYF1         CHAIN     HEADF                              59
     C  N59HEKNS         IFEQ      IKN
     C     HEKNSF        OREQ      IKN
     C     HEKNK         OREQ      IKN
     C     HEKNKF        OREQ      IKN
     C                   ADD       1             ZRECNO
     C                   WRITE     SI04DG
     C                   END
     C                   GOTO      SRBCF1
     C                   END
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Valg av oppdrag                                     SRBH /
      *////////////////////////////////////////////////////////////
     C     SRBH          BEGSR
      * Oppdatere SUBFIL
     C                   MOVE      *BLANKS       ZOP
     C                   UPDATE    SI04DG
      *
      * Søk oppdrag
     C     KEYD          CHAIN     HEADF                              20
      * Tjenestetype
     C     HEUR          IFEQ      'A'
     C                   MOVEL     'Land Imp'    ZEURTX
     C                   MOVE      'ort  '       ZEURTX
     C                   ELSE
     C     HEUR          IFEQ      'B'
     C                   MOVEL     'Land Eks'    ZEURTX
     C                   MOVE      'port '       ZEURTX
     C                   ELSE
     C     HEUR          IFEQ      'C'
     C                   MOVEL     'Fly Impo'    ZEURTX
     C                   MOVE      'rt   '       ZEURTX
     C                   ELSE
     C     HEUR          IFEQ      'D'
     C                   MOVEL     'Fly Eksp'    ZEURTX
     C                   MOVE      'ort  '       ZEURTX
     C                   ELSE
     C     HEUR          IFEQ      'E'
     C                   MOVEL     'Fly Inne'    ZEURTX
     C                   MOVE      'nland'       ZEURTX
     C                   ELSE
     C     HEUR          IFEQ      'F'
     C                   MOVEL     'Båt Impo'    ZEURTX
     C                   MOVE      'rt   '       ZEURTX
     C                   ELSE
     C     HEUR          IFEQ      'G'
     C                   MOVEL     'Båt Eksp'    ZEURTX
     C                   MOVE      'ort  '       ZEURTX
     C                   ELSE
     C     HEUR          IFEQ      'H'
     C                   MOVEL     'Båt Inne'    ZEURTX
     C                   MOVE      'nland'       ZEURTX
     C                   END
     C                   END
     C                   END
     C                   END
     C                   END
     C                   END
     C                   END
     C                   END
      * Fra sted
     C     HESDF         CHAIN     STED2                              20
     C                   MOVEL     ST2NVN        ZESDFT
      * Til sted
     C     HESDT         CHAIN     STED2                              20
     C                   MOVEL     ST2NVN        ZESDTT
      * Frankatur
     C     KEYE          CHAIN     KODTFR                             20
      *
     C                   MOVE      'SY00000'     ZMSGNO
     C                   WRITE     SI04DØ
     C                   EXFMT     SI04DJ
      *
     C     *IN03         CABEQ     '1'           SRBH9
     C     *IN12         CABEQ     '1'           SRBH9
      *
     C                   CALL      'SYFM21B'
     C                   PARM                    FIFIRM
     C                   PARM                    HEAVD
     C                   PARM                    HEOPD
     C                   PARM                    UFLAGG            1 0
     C     UFLAGG        COMP      1                                      01
     C     UFLAGG        COMP      3                                      03
      *
     C     SRBH9         ENDSR
