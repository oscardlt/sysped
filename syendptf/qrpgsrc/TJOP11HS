      * Program som sender SMS med lenke til TKeventGrabber
      *********************************************************************
CRTPG+*  CRTPGM SYSPED/TJOP11HS MODULE(*LIBL/TJOP11HS *LIBL/INCGI)
CRTPGM*        ACTGRP(*NEW) AUT(*USE)
      *********************************************************************
      *  01 PTF12 JOV  Innføre språk
      *********************************************************************
      /copy SYSPEDS/qrpgsrcpy,hspecs
      /copy SYSPEDS/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy SYSPEDS/qrpgsrcpy,prototypeb
      /copy SYSPEDS/qrpgsrcpy,usec
      /copy SYSPEDS/qrpgsrcpy,variables3
      *
      * Varible for
     D P               S              5U 0
     D WSUSER          S             10
     D WSAVDA          S              4
     D WSAVD           S              4  0
     D WSOPDA          S              7
     D WSOPD           S              7  0
     D SMSNR           S             15
     D TYPE            S              3
N01  D WSSPRAK         S              2
     D JSONstring      S          32000
     D Error           S           5000                                         flere feilmeldinger
     D InfoMsg         S           5000                                         flere infomeldinger
     D Values          S           5000                                         Flere nye verdier
      *get input-string
      /copy syspeds/qrpgsrcpy,prolog3

      * Parse input
     C                   EXSR      Parse
      * Open files etc
     C                   EXSR      INIT
      *
     c                   callp     gethtmlifs(
     C                             '/syspeddev/html/JSON.html':
     C                             '/CGITAG/')
     C                   callp     wrtsection('top')
      * Teste input og utfør
     c                   if        Type = *blanks
     c                   move      'COL'         TYPE                           Forselå Hent Kunde
     c                   endif
     c                   call      'ESOP11HS'
     c                   parm                    WSAVDA
     c                   parm                    WSOPDA
     c                   parm                    TYPE
     c                   parm                    SMSNR
     c                   parm                    WSUSER
N01  c                   parm                    WSSPRAK
      * ............................................................................................
      * skriv JSON-Object start..(alltid '{' + x ant param. m/komma mellom (IKKE komma etter siste))
      * ............................................................................................
      *
      /free
        JSONstring='{'
        +'¬user¬ : ¬'+%trim(WSUSER)+'¬, '
        +'¬heavd¬ : ¬'+%trim(%editc(WSAVD:'Z'))+'¬, '
        +'¬heopd¬ : ¬'+%trim(%editc(WSOPD:'Z'))+'¬, '
        +'¬smsnr¬ : ¬'+%trim(smsnr)+'¬, '
        +'¬type¬ : ¬'+%trim(type)+'¬, '
        +'¬errMsg¬ : ¬'+%trim(Error)+'¬}';
      /end-free
      * JSONfix escaper " i tekst,  for deretter å bytte ¬->"
     c                   call      'JSONFIX'
     c                   parm                    JSONstring
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      *
      *
      * Quit
     C                   exsr      Exit


      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Lukk fil
     C                   CLOSE     BRIDF

      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   MOVE      *ON           *INLR
     C                   return
     C                   endsr
      *
      *********************************************************
     C     Parse         Begsr
      *********************************************************
      * Get input
     C                   EVAL      WSUSER = zhbgetvar('USER')
     C                   EVAL      WSAVDA = zhbgetvar('AVD')
     C                   EVAL      WSAVD  = c2n2(WSAVDA)
     C                   move      WSAVD         WSAVDA
     C                   EVAL      WSOPDA = zhbgetvar('OPD')
     C                   EVAL      WSOPD  = c2n2(WSOPDA)
     C                   move      WSOPD         WSOPDA
     C                   EVAL      SMSNR  = zhbgetvar('SMSNR')
     C                   EVAL      TYPE   = zhbgetvar('TYPE')
N01  C                   EVAL      WSSPRAK= zhbgetvar('SPRAK')
     C                   ENDSR
      *********************************************************
     C     INIT          Begsr
      *********************************************************
     C                   OPEN      BRIDF
      * Test innlogging
     C     WSUSER        CHAIN     BRIDF                              50
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C                   call      BILIBL
     C                   end
      *
     c     UtInit        ENDSR
      *
