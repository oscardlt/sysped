*****A**************************************************************
     A* RETTINGER I DETTE PROGRAM:
PTFnrA*Sek Sig Vers Beskrivelse...........................
"""""A*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
P8XXXA* 01 YBC PTF8 FAX OG E-POST
*****A**************************************************************
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HELP
     A                                      ALTHELP(CA01)
      * "NOERASE" MÅ HA FOR Å IKKE TØMME BILDE SOM MAN KOMME FRA
     A          R NOERASE
     A                                      ASSUME
     A                                  1  2' '
      * MAN KUN DEFINNERE VINDU PÅ REKORDNIVÅ
     A          R WINDOWSZ
     A                                      WINDOW(*DFT 10 44 *NOMSGLIN)
     A                                      OVERLAY
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  5  2VALUES(' ' '1' '5')
     A            CBKDB          5A  O  5  5
     A            CBNAB         30A  O  5 11
     A            XINFO          1A  O  5 42
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0006)
     A                                      SFLPAG(0005)
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A                                      CA03(03 'Forrige')
     A                                      CA05(05 'Frisk')
     A                                      CA06(06 'LAGE NY')
     A                                      CA12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            ZRECNO         4S 0O  3 25SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYSJ11A'
     A                                      COLOR(BLU)
     A                                  1 10MSGCON(030 YBC0243 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                  2  2MSGCON(035 YBC0244 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3  2MSGCON(042 YBC0245 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A            W2NR           1A  I  4  2VALUES(' ' '1')
     A                                      DSPATR(UL)
     A            W2KOD          5A  I  4  5DSPATR(UL)
     A            W2SØK         30A  I  4 11DSPATR(UL)
     A          R BUNN
     A                                      WINDOW(WINDOWSZ)
     A                                      SLNO(10)
     A                                      CLRL(01)
     A                                      OVERLAY
     A N30                              1  2MSGCON(029 YBC0212 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        40A  O  1  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A          R BILDE
     A                                  1  2'SYSJ11A'
     A                                      COLOR(BLU)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                  1 70TIME
     A                                  1 10MSGCON(030 YBC0243 *LIBL/SYMSGF)
     A                                  3  5MSGCON(066 YBC0171 *LIBL/SYMSGF)
     A            CBKDB          5A  O  4  6DSPATR(UL)
     A                                      DSPATR(PC)
     A            CBNAB         30A  O  4 15DSPATR(UL)
     A            CBKN           8Y 0O  4 48DSPATR(UL)
     A                                      EDTCDE(4)
     A            CBLK           2A  O  4 59DSPATR(UL)
     A                                  5  5'Fax:'
     A            CBFAX         20A  B  5 10DSPATR(UL)
     A            CBEPOS        40A  B  5 40DSPATR(UL)
     A            WSTX1         62A  O  6 10DSPATR(UL)
     A            WSTX2         62A  O  7 10DSPATR(UL)
     A            WSTX3         62A  O  8 10DSPATR(UL)
     A            WSTX4         62A  O  9 10DSPATR(UL)
     A            WSTX5         62A  O 10 10DSPATR(UL)
     A            WSTX6         62A  O 11 10DSPATR(UL)
     A            WSTX7         62A  O 12 10DSPATR(UL)
     A            WSTX8         62A  O 13 10DSPATR(UL)
     A            WSTX9         62A  O 14 10DSPATR(UL)
     A            WSTX10        62A  O 15 10DSPATR(UL)
     A            WSTX11        62A  O 16 10DSPATR(UL)
     A            WSTX12        62A  O 17 10DSPATR(UL)
     A            WSTX13        62A  O 18 10DSPATR(UL)
     A            WSTX14        62A  O 19 10DSPATR(UL)
     A            WSTX15        62A  O 20 10DSPATR(UL)
     A            WSTX16        62A  O 21 10DSPATR(UL)
     A            WSTX17        62A  O 22 10DSPATR(UL)
     A                                 24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A                                  5 32MSGCON(007 DIV2565 *LIBL/SYMSGF)
     A          R NYVARE
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A                                      CA03(03 'Forrige')
     A                                      CA05(05 'Frisk')
     A                                      CA06(06 'LAGE NY')
     A                                      CA12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSJ11A'
     A                                      COLOR(BLU)
     A                                  1 10MSGCON(30 YBC0243 SYMSGF)
     A                                      DSPATR(HI)
     A                                  2  2MSGCON(35 YBC0244 SYMSGF)
     A                                      COLOR(BLU)
     A                                  3  2MSGCON(42 YBC0245 SYMSGF)
     A                                      DSPATR(HI)
     A            W2NR           1A  I  4  2VALUES(' ' '1')
     A                                      DSPATR(UL)
     A            W2KOD          5A  I  4  5DSPATR(UL)
     A            W2SØK         30A  I  4 11DSPATR(UL)
     A N30                             10  2MSGCON(29 YBC0212 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        40A  O 10  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
