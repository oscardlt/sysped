      * TAR IKKE HØYDE FOR RJE I 1K-FIL
     FINFIL     IF   F 1024        DISK
     FEDIS      UF   E           K DISK
     FEDISE1K   O  A E             DISK
     IINFIL     AA  09
     I                                  1 1024  INDATA
      *//////////////
      *  Hodelogikk /
      *//////////////
     C                   EXSR      SRA
     C                   EXSR      SRB
     C                   MOVE      '1'           *INLR
      *
      *////////////////////////////////////////////////////////////
      *  Initiering                                           SRA /
      *////////////////////////////////////////////////////////////
     C     SRA           BEGSR
      *
     C     *ENTRY        PLIST
     C     WSN           PARM                    WSN
     C     FMS1          PARM                    FMS1             80
     C     FMS2          PARM                    FMS2             80
      *___________________
      * Definiere Workfelt
      *"""""""""""""""""""
     C     *LIKE         DEFINE    SSN           WSN
     C     *LIKE         DEFINE    S0036         W0036
      *__________________
      * Definiere Nøkkler
      *""""""""""""""""""
     C     KEYA          KLIST
     C                   KFLD                    WSN
      *_____________
      * Init av felt
      *"""""""""""""
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  LAGE INNEHOLD                                        SRB /
      *////////////////////////////////////////////////////////////
     C     SRB           BEGSR
     C     KEYA          CHAIN     EDIS                               81
     C                   Z-ADD     0             W0036

      * Hent in komponentnavn
     C                   EVAL      SEMBR=SMBR

      * Legg in eventuell FMS-record NR 1
     C     FMS1          IFNE      *BLANKS
     C                   MOVEL     FMS1          SDATA
     C                   WRITE     EDISER
     C                   ADD       1             W0036
     C                   END
      * Legg in eventuell FMS-record NR 2
     C     FMS2          IFNE      *BLANKS
     C                   MOVEL     FMS2          SDATA
     C                   WRITE     EDISER
     C                   ADD       1             W0036
     C                   END

      * Hent alle poster i aktuell transaksjonsfil
     C                   READ      INFIL                                  20
     C     *IN20         DOWEQ     '0'
     C                   MOVEL     INDATA        SDATA
     C                   WRITE     EDISER
     C                   ADD       1             W0036
     C                   READ      INFIL                                  20
     C                   ENDDO
      *________________________
      * Oppdatere sendningslogg
      *""""""""""""""""""""""""
     C                   MOVE      'A'           SST
     C                   Z-ADD     W0036         S0036                          * ANT LINJER
     C                   UPDATE    EDISR

     C                   ENDSR
