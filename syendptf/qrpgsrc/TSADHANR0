      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/TSADHANR0 MODULE(*LIBL/TSADHANR0 *LIBL/INCGI)
CRTPGM*        ACTGRP(*NEW) AUT(*USE)
      *
      *********************************************************************
      /copy SYSPEDS/qrpgsrcpy,hspecs
      /copy SYSPEDS/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     fsadhan    uf   e             disk    USROPN
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy SYSPEDS/qrpgsrcpy,prototypeb
      /copy SYSPEDS/qrpgsrcpy,usec
      /copy SYSPEDS/qrpgsrcpy,variables3
      *
      * Varible for
     D WSUSER          S             10
     D DTFRA           S              8
     D DTFRAN          S              8S 0
     D DTTIL           S              8
     D DTTILN          S              8S 0
     D kns             S              8
     D knsn            S              8S 0
     D knk             S              8
     D knkn            S              8S 0
     D JSONstring      S          32000
     D Error           S            150

     d sadhanr1        pr                  extpgm('SADHANR1')
     d udtfra_                             like(udtfra)
     d udttil_                             like(udttil)
     d ukns_                               like(ukns)
     d uknk_                               like(uknk)
     d sadhanr2        pr                  extpgm('SADHANR2')
     d udtfra_                             like(udtfra)
     d udttil_                             like(udttil)
     d ukns_                               like(ukns)
     d uknk_                               like(uknk)
     d udtfra          s              8S 0
     d udttil          s              8S 0
     d ukns            s              8S 0
     d uknk            s              8S 0

      *get input-string
      /copy syspeds/qrpgsrcpy,prolog3

      * Parse input
     C                   exsr      Parse
      * Open files etc
     C                   EXSR      INIT
      *
     c                   callp     gethtmlifs(
     C                             '/syspeddev/html/JSON.html':
     C                             '/CGITAG/')
     C                   callp     wrtsection('top')
      *
      * ............................................................................................
      * skriv JSON-Object start..(alltid '{' + x ant param. m/komma mellom (IKKE komma etter siste))
      * ............................................................................................
      *
      /free
        JSONstring='{'
        +'¬user¬ : ¬'+%trim(WSUSER)+'¬, '
        +'¬dtfra¬ : ¬'+%trim(dtfra)+'¬, '
        +'¬dttil¬ : ¬'+%trim(dttil)+'¬, '
        +'¬sikns¬ : ¬'+%trim(kns)+'¬, '
        +'¬siknk¬ : ¬'+%trim(knk)+'¬, '
        +'¬errMsg¬ : ¬'+%trim(Error)+'¬ ';
      /end-free
      * JSONfix escaper " i tekst,  for deretter å bytte ¬->"
     c                   call      'JSONFIX'
     c                   parm                    JSONstring
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')

     c     Error         ifeq      *blanks

      * Slett arbetsfil
      /free
       read  sadhan;
       dow not %eof;
         delete sadhanr;
       read  sadhan;
       enddo;

       // Hent eksport
         sadhanr1 ( udtfra : udttil : ukns : uknk );
       // Hent import
         sadhanr2 ( udtfra : udttil : ukns : uknk );

      /end-free
     c                   endif
      * ............................................................................................
      * Skriv JSON-string Avsluttning
      * ............................................................................................
     c                   eval      JSONstring ='}'
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')
      *
      * Quit
     C                   exsr      Exit


      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Lukk fil
     C                   CLOSE     BRIDF
     C  N50              CLOSE     SADHAN

      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   MOVE      *ON           *INLR
     C                   return
     C                   endsr
      *
      *********************************************************
     C     Parse         Begsr
      *********************************************************
      * Get input
     C                   EVAL      WSUSER  = zhbgetvar('USER')
     C                   EVAL      DTFRA   = zhbgetvar('DTFRA')
     C                   EVAL      DTFRAN  = c2n2(DTFRA)
     C                   EVAL      DTTIL   = zhbgetvar('DTTIL')
     C                   EVAL      DTTILN  = c2n2(DTTIL)
     C                   EVAL      KNS     = zhbgetvar('SIKNS')
     C                   EVAL      KNSN    = c2n2(KNS)
     C                   EVAL      KNK     = zhbgetvar('SIKNK')
     C                   EVAL      KNKN    = c2n2(KNK)
     C                   EVAL      UDTFRA  =DTFRAN
     C                   EVAL      UDTTIL  =DTTILN
     C                   EVAL      UKNS    =KNSN
     C                   EVAL      UKNK    =KNKN
     c                   endsr
      *
      *********************************************************
     C     INIT          Begsr
      *********************************************************
     C                   OPEN      BRIDF
      * Test innlogging
     C     WSUSER        CHAIN     BRIDF                              50
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C                   call      BILIBL
     C                   end

     C   50              eval      Error = 'Invalid userID'
     C  N50              OPEN      SADHAN

     C                   endsr
