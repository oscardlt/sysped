      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/TSVI000R MODULE(*LIBL/TSVI000R *LIBL/INCGI)
CRTPGM*        ACTGRP(CGI) AUT(*USE)
      *
      *********************************************************************
      /copy SYSPEDS/qrpgsrcpy,hspecs
      /copy SYSPEDS/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FBRPARF    IF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy SYSPEDS/qrpgsrcpy,prototypeb
      /copy SYSPEDS/qrpgsrcpy,usec
      /copy SYSPEDS/qrpgsrcpy,variables3
     ‚*  Prototype for 'JSONFIX'
     d jsonfix         pr                  extpgm('JSONFIX')
     d jsonstring_                         like(jsonstring)
     ‚*  Prototype for 'INCGI'
     d incgi           pr                  extproc('INCGI')
     d bistdl_                             like(bistdl)
     ‚*  Prototype for variabel program
     D Varpgm          Pr                  ExtPgm(BILIBL)
     ‚*  Prototype for 'SYGE20'
     d syge20          pr                  extpgm('SYGE20')
     d ufunk_                              like(ufunk)
     d udat1_                              like(udat1)
     d udat2_                              like(udat2)
     d udgr_                               like(udgr)
     d WhereC          s            512a
     d FirstW          s              1A
     d cQuote          c                   const('''')
     d SqlStm          s            512a
     d myrows          s             10i 0
     D WSUSER          S             10
     D AVD             S              4
     D AVDN            S              4  0
     D SIGN            S              3
     D OPD             S              7
     D OPDN            S              7S 0
     D TULLID          S             10
     D DATUM           S              8
     D DATUMN          S              8S 0
     D DATUMT          S              8
     D DATUMTN         S              8S 0
     D STATUS          S              1
     D AVSNAVN         S             35
     D MOTNAVN         S             35
     D MTYP            S              3
     D JSONstring      s          32000
     D Error           S            150
     D AntLest         S              9  0
     D Jn043           S              1
     d X               S             10  0
     d hopp            S              1
     d wspll           S              1
     d STOPP5          S             35
     d pos             S              5U 0
     d udat1           s              8  0
     d udat2           s              8  0
     d udgr            s              5  0
     d ufunk           s              3
     d wpabrid         s                   like(pabrid)
     d wpakunr         s                   like(pakunr)
     d wpasort         s                   like(pasort)
     d MyPoDS          ds                  Qualified  dim(100)
     d SVIH_SYAV                      4S 0
     d SVIH_SYOP                      7S 0
     d SVIH_TUID                     10
     d SVIH_MTYP                      3
     d SVIH_SYSG                      3
     d SVIH_SYDT                      8S 0
     d SVIH_SYST                      1
     d SVIH_AVNA                     30
     d SVIH_MONA                     30
     d SVIH_GODN                     15
      *get input-string
      /copy syspeds/qrpgsrcpy,prolog3

      /free
         exsr ParseSr;
         exsr init;

       gethtmlifs(
       '/syspeddev/html/JSON.html':
       '/CGITAG/');
       wrtsection('top');

       //...........................................................................................
       //skriv JSON-Object start (alltid '{' + x ant param. m/komma mellom (IKKE komma etter siste))
       //...........................................................................................

        JSONstring='{'
        +'¬user¬ : ¬'+%trim(WSUSER)+'¬, '
        +'¬avd¬ : ¬'+%trim(%editc(AVDN:'Z'))+'¬, '
        +'¬sign¬ : ¬'+%trim(SIGN)+'¬, '
        +'¬opd¬ : ¬'+%trim(%editc(OPDN:'Z'))+'¬, '
        +'¬tullId¬ : ¬'+%trim(TULLID)+'¬, '
        +'¬datum¬ : ¬'+%trim(%editc(DATUMN:'Z'))+'¬, '
        +'¬datumt¬ : ¬'+%trim(%editc(DATUMTN:'Z'))+'¬, '
        +'¬status¬ : ¬'+%trim(STATUS)+'¬, '
        +'¬avsNavn¬ : ¬'+%trim(AVSNAVN)+'¬, '
        +'¬motNavn¬ : ¬'+%trim(MOTNAVN)+'¬, '
        +'¬mtyp¬ : ¬'+%trim(MTYP)+'¬, '
        +'¬errMsg¬ : ¬'+%trim(Error)+'¬, ';

       //JSONfix escaper " i tekst,  for deretter å bytte ¬->"

       jsonfix ( jsonstring );

       updhtmlvar2('JSONstring'
                  :%addr(jsonstring)
                  :%len(jsonstring));
       wrtsection('JSONlin');

       //...........................................................................................
       // Skriv JSON-LISTE Start (en liste er EN parameter(fra [ til   )
       //..........................................................................................
       jsonstring ='"orderList" : [';

       updhtmlvar2('JSONstring'
                  :%addr(jsonstring)
                  :%len(jsonstring));
       wrtsection('JSONlin');

       if error = *blanks;
         exsr SelSr;
         if myrows > 0;
           exsr LesSr;
         endif;
       endif;
       // ..........................................................................................
       // Skriv JSON-Liste/Array  Avslutning
       // ..........................................................................................
       jsonstring =']';
       updhtmlvar2('JSONstring'
                  :%addr(jsonstring)
                  :%len(jsonstring));
       wrtsection('JSONlin');
       // ..........................................................................................
       // Skriv JSON-string Avsluttning
       // ..........................................................................................
       jsonstring ='}';
       updhtmlvar2('JSONstring'
                  :%addr(jsonstring)
                  :%len(jsonstring));
       wrtsection('JSONlin');
         exsr Exit;

       //-----------------------------------------------------------------
       // Select fra fil via SQL
       //-----------------------------------------------------------------
       begsr     SelSr;

       If    AVDN > 0;
         AVD = %editc(AVDN:'X');
         Exsr  AddAnd;
         Eval  WhereC = ' SVIH_SYAV  = ' + AVD;
       EndIf;

       If    SIGN  <> *blanks;
         Exsr  AddAnd;
         Eval  WhereC =  %trim(WhereC)  +
         ' SVIH_SYSG = ' + cQuote + SIGN + cQuote +' ';
       EndIf;

       If    OPDN > 0;
         OPD = %editc(OPDN:'X');
         Exsr  AddAnd;
         Eval  WhereC =  %trim(WhereC)  +
         ' SVIH_SYOP  = ' + OPD;
       EndIf;

       If    TULLID  <> *blanks;
         Exsr  AddAnd;
         Eval  WhereC =  %trim(WhereC)  +
         ' SVIH_TUID LIKE ' + cQuote + '%' + %trim(TULLID) + '%' + cQuote;
        EndIf;

       If    DATUMN > 0;
         DATUM = %editc(DATUMN:'X');
         Exsr  AddAnd;
         Eval  WhereC =  %trim(WhereC)  +
         ' SVIH_SYDT  >= ' + DATUM;
       EndIf;

       If    DATUMTN > 0;
         DATUMT = %editc(DATUMTN:'X');
         Exsr  AddAnd;
         Eval  WhereC =  %trim(WhereC)  +
         ' SVIH_SYDT  <= ' + DATUMT;
       EndIf;

       If    STATUS  <> *blanks and STATUS <> 'b';
         Exsr  AddAnd;
         Eval  WhereC =  %trim(WhereC)  +
         ' SVIH_SYST = ' + cQuote + STATUS + cQuote;
        EndIf;

       If    AVSNAVN  <> *blanks;
         Exsr  AddAnd;
         Eval  WhereC =  %trim(WhereC)  +
         ' UPPER(SVIH_AVNA) LIKE UPPER(' + cQuote + '%' +
         %trim(AVSNAVN) + '%' + cQuote + ')';
        EndIf;

       If    MOTNAVN  <> *blanks;
         Exsr  AddAnd;
         Eval  WhereC =  %trim(WhereC)  +
         ' UPPER(SVIH_MONA) LIKE UPPER(' + cQuote + '%' +
         %trim(MOTNAVN) + '%' + cQuote + ')';
        EndIf;

       If    MTYP  <> *blanks;
         Exsr  AddAnd;
         Eval  WhereC =  %trim(WhereC)  +
         ' SVIH_MTYP = ' + cQuote + MTYP + cQuote +' ';
       EndIf;

         Eval      WhereC =  ' Where ' + %trim(WhereC);

         Eval       SqlStm = 'Select SVIH_SYAV, SVIH_SYOP, SVIH_TUID, ' +
                             'SVIH_MTYP, SVIH_SYSG, SVIH_SYDT, ' +
                             'SVIH_SYST, SVIH_AVNA, SVIH_MONA, ' +
                             'SVIH_GODN ' +
                             'from SVIH ' +
                              %trim(WhereC)  +
                             ' Order by '       +
                             'SVIH_SYDT desc ' +
                             'FOR READ ONLY ';
         Exec SQL PREPARE DynSel from :SqlStm;
         EXEC SQL DECLARE  A33  cursor for DynSel;
         Exec SQL OPEN A33;
         Myrows = %elem(MyPoDS);
         exec sql FETCH A33   for :MyRows rows
                  INTO :MyPoDs;
       endsr;
       //-----------------------------------------------------------------
       // Set inn "AND" mellom where settninger
       //-----------------------------------------------------------------
       begsr     AddAnd;
       If        FirstW =   'N';
         Eval      WhereC =  %trim(WhereC) + ' and ';
       EndIf;
         Eval      FirstW =   'N';
       endsr;
       //-----------------------------------------------------------------
       // hent input
       //-----------------------------------------------------------------
       begsr     ParseSr;
         wsuser  = zhbgetvar('USER');
         avd     = zhbgetvar('AVD');
         avdn    = c2n2(avd);
         sign    = zhbgetvar('SIGN');
         opd     = zhbgetvar('OPD');
         opdn    = c2n2(opd);
         tullid  = zhbgetvar('TULLID');
         datum   = zhbgetvar('DATUM');
         datumn  = c2n2(datum);
         datumt  = zhbgetvar('DATUMT');
         datumtn = c2n2(datumt);
         status  = zhbgetvar('STATUS');
         avsnavn = zhbgetvar('AVSNAVN');
         motnavn = zhbgetvar('MOTNAVN');
         mtyp    = zhbgetvar('MTYP');
        endsr;
        // __________________________________________________
        // Læs array                                    LESSR
        // """"""""""""""""""""""""""""""""""""""""""""""""""
           begsr LesSr;
       for x = 1 to myrows;

       if mypods(x).svih_sysg = *blanks;
         leave;
       endif;

        // Valider extern ref
           error=' ';
           if error = *blanks;

        // Lægg ut Json stræng
           AntLest = AntLest + 1;

        // Skriv en JSON-Array-linje pr menypunkt - komma før hvert nytt element

       if AntLest=1;
          JSONstring=*blanks;
          else;
          JSONstring=', ';
          endif;
       JSONstring=%trim(JSONstring)+'{'
          +'¬avd¬ : ¬'+%trim(%editc(mypods(x).SVIH_SYAV:'Z'))+'¬, '
          +'¬opd¬ : ¬'+%trim(%editc(mypods(x).SVIH_SYOP:'Z'))+'¬, '
          +'¬tullid¬ : ¬'+%trim(mypods(x).SVIH_TUID)+'¬, '
          +'¬mtyp¬ : ¬'+%trim(mypods(x).SVIH_MTYP)+'¬, '
          +'¬sign¬ : ¬'+%trim(mypods(x).SVIH_SYSG)+'¬, '
          +'¬datum¬ : ¬'+%trim(%editc(mypods(x).SVIH_SYDT:'Z'))+'¬, '
          +'¬status¬ : ¬'+%trim(mypods(x).SVIH_SYST)+'¬, '
          +'¬avsNavn¬ : ¬'+%trim(mypods(x).SVIH_AVNA)+'¬, '
          +'¬motNavn¬ : ¬'+%trim(mypods(x).SVIH_MONA)+'¬, '
          +'¬godsnr¬ : ¬'+%trim(mypods(x).SVIH_GODN)+'¬}';
         jsonfix (JSONstring);
       updhtmlvar2('JSONstring'
                  :%addr(jsonstring)
                  :%len(jsonstring));
         wrtsection ('JSONlin');

        endif;

        endfor;

       endsr;
       //********************************************************
       begsr init;
       //********************************************************
         open bridf;
         open brparf;
         // Test innlogging
         chain wsuser bridf;

         if %found;
           if bilibl = *blanks;
             incgi ( bistdl );
           else;

             Monitor;
                   Varpgm();
                 On-Error;
                   //
                 EndMon;

           endif;
         else;

           error = 'Invalid userID';
         endif;

         // Er dato= blank eller 0, sett  da dato 7 dager tilbake
         if opdn=*zeros and tullid=*blanks and datumn=*zeros;

           wpabrid=wsuser;
           wpakunr=0;
           wpasort=0;
           chain (WPABRID: WPAKUNR: 'DFTDG': WPASORT) BRPARF;
           if not %found or pavrdn = 0;
              pavrdn=7;
           endif;

           ufunk = 'SUB';
           udat1 = datumn;
           udgr = pavrdn;
           syge20 ( ufunk : udat1 : udat2 : udgr );
           datumn=udat2;
         endif;


       endsr;
       //=====================================================================
       // Close output html and quit
       //=====================================================================
       begsr exit;
         // Lukk fil
         close bridf;
         close brparf;

         wrtsection('*fini');
         *inlr = *on;
         return;
       endsr;
      /end-free
