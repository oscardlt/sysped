      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/TTCE026R MODULE(*LIBL/TTCE026R *LIBL/INCGI)
CRTPGM*        ACTGRP(*NEW) AUT(*USE)
      *
      *********************************************************************
      /copy SYSPEDS/qrpgsrcpy,hspecs
      /copy SYSPEDS/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FTRUVFV    IF   E             DISK    USROPN
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy SYSPEDS/qrpgsrcpy,prototypeb
      /copy SYSPEDS/qrpgsrcpy,usec
      /copy SYSPEDS/qrpgsrcpy,variables3
     ‚*  Prototype for 'JSONFIX'
     d jsonfix         pr                  extpgm('JSONFIX')
     d jsonstring_                         like(jsonstring)
     ‚*  Prototype for 'INCGI'
     d incgi           pr                  extproc('INCGI')
     d bistdl_                             like(bistdl)
     ‚*  Prototype for variabel program
     D Varpgm          Pr                  ExtPgm(BILIBL)
      * Varible for
     D WSUSER          S             10
     D WMODE           S              1
     D JSONstring      s          32000
     D Error           S            150
     D AntLest         S              9  0
     d myrows          s             10i 0
     d  X              S              5  0
     d  WERR           S              1
     d  W              S             20

      *get input-string
      /copy syspeds/qrpgsrcpy,prolog3
        /free

        // Parse input
         exsr Parse;
        // Open files etc
         exsr Init;

       gethtmlifs(
       '/syspeddev/html/JSON.html':
       '/CGITAG/');
       wrtsection('top');

       //...........................................................................................
       //skriv JSON-Object start (alltid '{' + x ant param. m/komma mellom (IKKE komma etter siste))
       //...........................................................................................

        JSONstring='{'
        +'¬user¬ : ¬'+%trim(WSUSER)+'¬, '
        +'¬errMsg¬ : ¬'+%trim(Error)+'¬, ';

       //JSONfix escaper " i tekst,  for deretter å bytte ¬->"

       jsonfix ( jsonstring );

       updhtmlvar2('JSONstring'
                  :%addr(jsonstring)
                  :%len(jsonstring));
       wrtsection('JSONlin');

       //...........................................................................................
       // Skriv JSON-LISTE Start (en liste er EN parameter(fra [ til   )
       //..........................................................................................
       jsonstring ='"securitylineadd" : [';

       updhtmlvar2('JSONstring'
                  :%addr(jsonstring)
                  :%len(jsonstring));
       wrtsection('JSONlin');

       if error = *blanks;
         // Hæmta til array
         select;
         when wmode = 'A';
         exsr sradd;
         when wmode = 'D';
         exsr srdel;
         when wmode = 'U';
         exsr srupd;
         endsl;
         // Avsluta
         exsr avslut;
       endif;
       // ..........................................................................................
       // Skriv JSON-Liste/Array  Avslutning
       // ..........................................................................................
       jsonstring =']';
       updhtmlvar2('JSONstring'
                  :%addr(jsonstring)
                  :%len(jsonstring));
       wrtsection('JSONlin');
       // ..........................................................................................
       // Skriv JSON-string Avsluttning
       // ..........................................................................................
       jsonstring ='}';
       updhtmlvar2('JSONstring'
                  :%addr(jsonstring)
                  :%len(jsonstring));
       wrtsection('JSONlin');

       // Quit
       exsr exit;

       //=====================================================================
       // Close output html and quit
       //=====================================================================
       begsr exit;
         // Lukk fil
         close bridf;
         if *in50 = *off;
         close truvfv;
         endif;

         // Do not delete the call to wrtsection with section name *fini.  It is needed
         // to ensure that all output html that has been buffered gets output.
         wrtsection('*fini');
         // Quit
         *inlr = *on;
         return;
       endsr;
       //********************************************************
       begsr parse;
       //********************************************************
       // Get input
        wsuser  = zhbgetvar('USER');
        wmode   = zhbgetvar('MODE');
        w         = zhbgetvar('TVAVD');
        TVAVD     = c2n2(w);
        w         = zhbgetvar('TVTDN');
        TVTDN     = c2n2(w);
        w         = zhbgetvar('TVLI');
        TVLI      = c2n2(w);
        TVTKBM    = zhbgetvar('TVTKBM');
        TVKREF    = zhbgetvar('TVKREF');
        TVFGKD    = zhbgetvar('TVFGKD');
        w         = zhbgetvar('TVKNSS');
        TVKNSS    = c2n2(w);
        TVNASS    = zhbgetvar('TVNASS');
        TVADSS1   = zhbgetvar('TVADSS1');
        TVPNSS    = zhbgetvar('TVPNSS');
        TVPSSS    = zhbgetvar('TVPSSS');
        TVLKSS    = zhbgetvar('TVLKSS');
        TVSKSS    = zhbgetvar('TVSKSS');
        TVTINSS   = zhbgetvar('TVTINSS');
        w         = zhbgetvar('TVKNKS');
        TVKNKS    = c2n2(w);
        TVNAKS    = zhbgetvar('TVNAKS');
        TVADKS1   = zhbgetvar('TVADKS1');
        TVPNKS    = zhbgetvar('TVPNKS');
        TVPSKS    = zhbgetvar('TVPSKS');
        TVLKKS    = zhbgetvar('TVLKKS');
        TVSKKS    = zhbgetvar('TVSKKS');
        TVTINKS   = zhbgetvar('TVTINKS');

       endsr;
       //********************************************************
       begsr init;
       //********************************************************
         open bridf;
         // Test innlogging
         chain wsuser bridf;
         *in50 = not %found;

         if *in50 = *off;
           if bilibl = *blanks;
             incgi ( bistdl );
           else;

             Monitor;
                   Varpgm();
                 On-Error;
                   //
                 EndMon;

           endif;
        //Dummy read for feltbeskrivelser, kuttes ut i java
         open truvfv;
         if *in50 = *on;
         read truvfv;
         endif;
         else;

           error = 'Invalid userID';
         endif;

       endsr;
        // __________________________________________________
        // Addera post till fil                         SRADD
        // """"""""""""""""""""""""""""""""""""""""""""""""""
           begsr sradd;

          exec sql

          INSERT INTO TRUVFV
          (TVAVD, TVTDN, TVLI, TVTKBM, TVKREF, TVFGKD, TVKNSS,
          TVNASS, TVADSS1, TVPNSS, TVPSSS, TVLKSS, TVSKSS, TVTINSS,
          TVKNKS, TVNAKS, TVADKS1, TVPNKS, TVPSKS, TVLKKS, TVSKKS,
          TVTINKS)
          VALUES(:TVAVD, :TVTDN, :TVLI, :TVTKBM, :TVKREF, :TVFGKD,
          :TVKNSS, :TVNASS, :TVADSS1, :TVPNSS, :TVPSSS, :TVLKSS,
          :TVSKSS, :TVTINSS, :TVKNKS, :TVNAKS, :TVADKS1, :TVPNKS,
          :TVPSKS, :TVLKKS, :TVSKKS, :TVTINKS)
          WITH NC;

       endsr;
        // __________________________________________________
        // Update post i fil                            SRUPD
        // """"""""""""""""""""""""""""""""""""""""""""""""""
           begsr srupd;

          exec sql

          UPDATE TRUVFV
          SET TVTKBM=:TVTKBM, TVKREF=:TVKREF, TVFGKD=:TVFGKD,
          TVKNSS=:TVKNSS,
          TVNASS=:TVNASS, TVADSS1=:TVADSS1, TVPNSS=:TVPNSS,
          TVPSSS=:TVPSSS, TVLKSS=:TVLKSS, TVSKSS=:TVSKSS,
          TVTINSS=:TVTINSS, TVKNKS=:TVKNKS, TVNAKS=:TVNAKS,
          TVADKS1=:TVADKS1, TVPNKS=:TVPNKS, TVPSKS=:TVPSKS,
          TVLKKS=:TVLKKS, TVSKKS=:TVSKKS, TVTINKS=:TVTINKS
          WHERE TVAVD=:TVAVD and TVTDN=:TVTDN and TVLI=:TVLI
          WITH NC;

       endsr;
        // __________________________________________________
        // Slette post i fil                            SRDEL
        // """"""""""""""""""""""""""""""""""""""""""""""""""
           begsr srdel;

          exec sql

          DELETE FROM TRUVFV
          WHERE TVAVD=:TVAVD and TVTDN=:TVTDN and TVLI=:TVLI
          WITH NC;

       endsr;
        // __________________________________________________
        // Avsluta                                     AVSLUT
        // """"""""""""""""""""""""""""""""""""""""""""""""""
           begsr avslut;
          JSONstring=*blanks;
       JSONstring=%trim(JSONstring)+'{'
          +'¬result¬ : ¬'+ 'ok' + '¬}';
         jsonfix (JSONstring);
       updhtmlvar2('JSONstring'
                  :%addr(jsonstring)
                  :%len(jsonstring));
         wrtsection ('JSONlin');

       endsr;
      /end-free
