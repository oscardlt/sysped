      *
      ***********************************************************
      *** PROGRAM SKAL VEDLIKEHOLDE enhetskoder               ***
      ***********************************************************
      *
     FENHETF    UF A E           K DISK
     FENHETL01  IF   E           K DISK
     F                                     RENAME(ENHETFR:ENHET01)
     Fsytr27fm  CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D                 DS
     D  KOFMNV                 1     20
     D  KOSFN7                 1      7
     C*
     C                   EXSR      INIT
     C*
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
     C                   MOVE      W2KODE        YNKODE
     C                   MOVE      W2TEXT        YNTEXT
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     W2TEXT        IFEQ      *BLANKS
     C     KEYSTR        SETLL     ENHETF
     c                   else
     C     KEYSTR2       SETLL     ENHETL01
     c                   end
     C     START1        TAG
     C     W2TEXT        IFEQ      *BLANKS
     C                   READ(N)   ENHETF                                 94
     c                   else
     C                   READ      ENHETL01                               94
     c                   end
      *
     C     START2        TAG
      *
     C  N94              DO
     C                   DO        14
     C                   ADD       1             ZRECNO
     C                   WRITE     SUBFIL
     C     W2TEXT        IFEQ      *BLANKS
     C                   READ(N)   ENHETF                                 94
     c                   else
     C                   READ      ENHETL01                               94
     c                   end
     C  N94              MOVE      ENKODE        YNKODE
     C  N94              END
     C                   END
     C*
     C                   Z-ADD     ZRECNO        YRECNO            5 0
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     7             POSNBR
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYWRK
     C                   ELSE
     C                   WRITE     BUNN
     C                   EXFMT     SUBCTL
     C                   END
     C*
     C***
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C                   Z-ADD     WSSUBN        XRECNO            4 0
     C     XRECNO        IFEQ      0
     C                   Z-ADD     ZRECNO        XRECNO            4 0
     C                   END
     C***
     C*
     C                   SETOFF                                       9899
      *
     C     w2kode        ifne      *blanks
     C     w2text        orne      *blanks
     c                   goto      start
     c                   end
     C*
     C*** AVSLUTT
     C   03              GOTO      SLUTT
     C*
     C*
     C*** tilbake
     C   12              GOTO      SLUTT
     C*
     C*** FRISK OPP
     C   05              GOTO      START
     C*
     C*** LAGE NY
     C   06              DO
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     06            POSNBR
     C                   MOVE      *BLANKS       YNKODE
     C                   GOTO      START5
     C                   END
     C*** SØK FRA
     C   08              GOTO      START6
     C*
     C*** BLA VIDERE
     C   25              DO
     C                   SETOFF                                       25
     C                   Z-ADD     YRECNO        ZRECNO
     C     W2TEXT        IFEQ      *BLANKS
     C  N94KEYBLA        SETLL     ENHETF
     C  N94              READ(N)   ENHETF                                 94
     C                   else
     C  N94KEYBLA2       SETLL     ENHETL01
     C  N94              READ      ENHETL01                               94
     C                   end
     C                   GOTO      START2
     C                   END
     C*
     C     ZRECNO        IFNE      0
     C                   EXSR      HENTSR
     C                   END
     C   98XRECNO        IFNE      0
     C                   Z-ADD     XRECNO        ZRECNO
     C                   END
     C                   GOTO      START3
     C*
     C*** LAGE NY
     C     START5        TAG
     C                   EXFMT     VARELI
     C                   SETOFF                                       309089
     C   12              EXSR      MOVB2
     C  N12              EXSR      TESTB1
     C  N12
     CAN 30              GOTO      START5
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     5             POSNBR
     C                   GOTO      START3
      *
      *** SØK FRA
      *
     C     START6        TAG
     C                   EXFMT     VAREL2
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     5             POSNBR
     C   12              GOTO      START3
     C                   GOTO      START
     C*
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
     C*
     C***********************************************
     C     TESTB1        BEGSR
     C***********************************************
     C  N40              DO
     C  N90KEYWS         CHAIN     ENHETF                             33
     C  N90
     CANN33              DO
     C                   UPDATE    ENHETFR
     C                   SETON                                        90
     C                   MOVE      'SPK0024'     MSGNR
     C                   END
     C   90              Z-ADD     07            POSNBR
     C   90              SETON                                        30
     C                   END
     C*
     C*
     C   30              goto      sltb
     C*
     C*** OPPDATERE PÅ REGISTER OG SUBFIL
     C     KEYWS         CHAIN     ENHETF                             33
     C                   EXSR      MOVWSF
     C  N33              UPDATE    ENHETFR
     C   33              WRITE     ENHETFR
     C   40              UPDATE    SUBFIL
     C  N40              ADD       1             YRECNO
     C  N40              Z-ADD     YRECNO        ZRECNO
     C  N40              WRITE     SUBFIL
     C                   WRITE     SUBCTL
     C                   EXSR      MOVB2
     C*
     C     SLTB          ENDSR
     C*
     C***********************************************
     C     HENTSR        BEGSR
     C***********************************************
     C                   Z-ADD     0             LINNBR
     C                   SETOFF                                       919299
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C   98              DO
     C   99              SETOFF                                       98
     C                   Z-ADD     1             LINNBR
     C                   GOTO      SLHENT
     C                   END
     C     ZRECNO        DIV       14            LINNBR
     C                   MVR                     XLIN              2 0
     C     XLIN          IFEQ      0
     C                   Z-ADD     14            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
      *
      *   1  = plukk
      *
     C   27WSNR          IFEQ      '1'                                          ENDRE
     C                   MOVE      ENKODE        UKODE
     c                   seton                                        lr
     c                   return
     C                   END
      *
      *   2  = endre
      *
     C  N27WSNR          IFEQ      '2'                                          ENDRE
     C                   MOVE      ' '           WSNR
     C     KOSFN7        COMP      '*DELETE'                              98
     C   98              GOTO      SLHENT
     C                   EXSR      CHGSF
     C   12              GOTO      SLHENT
     C                   END
      *
      *   4  = slett
      *
     C  N27WSNR          IFEQ      '4'                                          SLETTE
     C                   MOVE      ' '           WSNR
     C     KOSFN7        COMP      '*DELETE'                              98
     C   98              GOTO      SLHENT
     C                   EXSR      DLTSF
     C   12              GOTO      SLHENT
     C                   END
     C                   END
     C*
     C     SLHENT        TAG
     C                   ADD       5             LINNBR
     C                   Z-ADD     5             POSNBR
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     DLTSF         BEGSR
     C***********************************************
     C                   SETON                                        99
     C                   EXFMT     VAREL3
     C  N12              DO
     C     KEYFIL        CHAIN     ENHETF                             33
     C  N33              DELETE    ENHETFR
     C                   EXSR      MOVB1
     C                   END
     C     XLIN          IFEQ      0
     C                   Z-ADD     14            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C                   UPDATE    SUBFIL
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     CHGSF         BEGSR
     C***********************************************
     C                   EXSR      MOVSFW
     C                   SETON                                        99
     C                   MOVE      14            posnbr
     C     STCHG         TAG
     C                   EXFMT     VARELI
     C                   SETOFF                                       309089
     C   12              DO
     C                   UPDATE    SUBFIL
     C                   EXSR      MOVB2
     C                   END
     C  N12              EXSR      TESTB1
     C  N12
     CAN 30              GOTO      STCHG
     C     XLIN          IFEQ      0
     C                   Z-ADD     14            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVB1         BEGSR
     C***********************************************
     C                   MOVE      *BLANKS       ENKODE
     C                   MOVE      *BLANKS       ENTEXT
     C                   MOVE      *zeros        ENLEN
     C                   MOVE      *zeros        ENBRD
     C                   MOVE      *zeros        ENHOY
     C                   MOVE      *zeros        ENLM2
     C                   MOVE      *zeros        ENLM
     C                   MOVEL     '*DELETE'     KOSFN7
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVB2         BEGSR
     C***********************************************
     C                   SETOFF                                       40
     C*
     C                   MOVE      *BLANKS       WSKODE
     C                   MOVE      *BLANKS       WSTEXT
     C                   MOVE      *zeros        WSLEN
     C                   MOVE      *zeros        WSBRD
     C                   MOVE      *zeros        WSHOY
     C                   MOVE      *zeros        WSLM2
     C                   MOVE      *zeros        WSLM
     C                   ENDSR
     C*
     C***********************************************
     C     MOVSFW        BEGSR
     C***********************************************
     C                   MOVE      ENKODE        WSKODE
     C                   MOVE      ENTEXT        WSTEXT
     C                   MOVE      ENLEN         WSLEN
     C                   MOVE      ENBRD         WSBRD
     C                   MOVE      ENHOY         WSHOY
     C                   MOVE      ENLM2         WSLM2
     C                   MOVE      ENLM          WSLM
     C                   SETON                                        40
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     18            POSNBR
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVWSF        BEGSR
     C***********************************************
     C   33              DO
     C                   MOVE      WSKODE        ENKODE
     C                   END
     C*
     C                   MOVE      WSKODE        ENKODE
     C                   MOVE      WSTEXT        ENTEXT
     C                   MOVE      WSLEN         ENLEN
     C                   MOVE      WSBRD         ENBRD
     C                   MOVE      WSHOY         ENHOY
     C                   MOVE      WSLM2         ENLM2
     C                   MOVE      WSLM          ENLM
     C                   ENDSR
     C*
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     *entry        plist
     c                   parm                    umode             1
     c                   parm                    ukode             7
      * p = plukk
     c     umode         comp      'P'                                    27
     C     KEYWS         KLIST
     C                   KFLD                    WSKODE
     C     KEYSTR        KLIST
     C                   KFLD                    W2KODE
     C     KEYSTR2       KLIST
     C                   KFLD                    W2TEXT
     C     KEYFIL        KLIST
     C                   KFLD                    ENKODE
     C     KEYBLA        KLIST
     C                   KFLD                    YNKODE            7
     C*
     C     KEYBLA2       KLIST
     C                   KFLD                    YNTEXT           30
     C                   KFLD                    YNKODE
     C*
     C                   ENDSR
     C*
