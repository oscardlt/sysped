      *
      ***********************************************************
      *** PROGRAM SKAL SENDE UniversalEvent til CW1 (DDI)     ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01-26                            ***
      *** LEDIGE INDIKATORER 27-99                            ***
      ***********************************************************
     H DFTACTGRP(*NO)
     FFIRM      IF   E           K DISK
     FFRISOK    IF   E           K DISK
     FEDIC      UF A E             DISK
     FEDIS      UF A E           K DISK
     FEDIM      O  A E             DISK
     FFIRMARC   IF   E           K DISK
     FARKIVL02  IF   E           K DISK
     FARKTXT    IF   E           K DISK
     FARKEXT    IF   E           K DISK
     FTRACKF    O  A E             DISK
      /copy ifsio_h
     D WCMD            S            500A
      *
     D FILE_o          s             10I 0
     D String          s            512a
     D rc              s             10i 0
     D Idx             s              5u 0
     D FILNAM          s            100A
     D FILNAM1         s            100A
     D FILNAM2         s            100A
     D FILNAM2A        s            100A
     D FILNAM2X        s            100A
     D FILNAM3         s            100A
     D FILNAM3LF       s            100A
     D FILNAM4         s            100A
     D XCHGAUT         s            200A
     D LF              c                   x'25'
     D RunCmd          PR                  EXTPGM('QCMDEXC')
     D  Cmd                         500A   CONST
     D  Len                          15P 5 CONST
     D                SDS
     D  ZUSER                254    263
      *
     C                   EXSR      INIT
      *
     C                   EXSR      SRSNDST
      *
     C                   EXSR      SRUNIINT
      *
     C                   EXSR      OPDEDIM
      *
     C                   EXSR      OPDTRACK
      *
     C                   EXSR      SRCLOSE
      *
     C                   EXSR      SRSNDSL
      *
     C                   MOVE      *ON           *INLR
     C                   RETURN
      *
      ***********************************************
     C     SRSNDST       BEGSR
      ***********************************************
      *
     C     1             CHAIN     EDIC                               33
     C                   ADD       1             CSN
     C                   UPDATE    EDICR
      *
     C                   MOVE      *BLANKS       SDATA          1024
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
      *
     C                   EVAL      S0004 = X0004
     C                   EVAL      S0010 = X0010
     C                   EVAL      S0020 = %TRIM(X0020) + '-' + %TRIM(UTYPE)
     C                   Z-ADD     1             S0036
     C                   EVAL      SSN   = CSN
     C                   EVAL      SSR   = 'S'
     C                   EVAL      SST   = 'X'
     C                   MOVE      WDT           SDT
     C                   MOVE      WTM           STM
     C                   EVAL      SLIB  = *BLANKS
     C                   EVAL      SFILE = '*IFS'
     C                   EVAL      SMBR  = *BLANKS
     C                   EVAL      SMBRS  = 0
      *
     C                   EVAL      FILNAM = '/' + %TRIM(FILIBF) + 'X/'
     C                             + %TRIM(S0010) + '/EDISE1K/BACKUP/'
     C                             + %TRIM(S0020) + %TRIM(WDT)
     C                             + %TRIM(WTM) + '.xml'
     C                   EVAL      FILNAM1 = '/' + %TRIM(FILIBF) + 'X/'
     C                             + %TRIM(S0010) + '/EDISE1K/BACKUP/'
     C                             + %TRIM(S0020) + %TRIM(WDT)
     C                             + %TRIM(WTM) + '-1.xml'
     C                   EVAL      FILNAM2X = '/' + %TRIM(FILIBF) + 'X/'
     C                             + %TRIM(S0010) + '/EDISE1K/BACKUP/'
     C                             + %TRIM(S0020) + %TRIM(WDT)
     C                             + %TRIM(WTM) + '-X.xml'
     C                   EVAL      FILNAM3 = '/' + %TRIM(FILIBF) + 'X/'
     C                             + %TRIM(S0010) + '/EDISE1K/BACKUP/'
     C                             + %TRIM(S0020) + %TRIM(WDT)
     C                             + %TRIM(WTM) + '-BASE64-LF.enc'
     C                   EVAL      FILNAM3LF = '/' + %TRIM(FILIBF) + 'X/'
     C                             + %TRIM(S0010) + '/EDISE1K/BACKUP/'
     C                             + %TRIM(S0020) + %TRIM(WDT)
     C                             + %TRIM(WTM) + '-BASE64.enc'
     C                   EVAL      FILNAM4 = %TRIM(S0020) + %TRIM(WDT)
     C                             + %TRIM(WTM) + '.xml'
      * 1208 = UTF-8
     C                   eval      FILE_o = open( %TrimR( FilNam )
     C                               : O_CREAT+O_TRUNC+O_WRONLY
     C                                + O_TEXT_CREAT+O_CCSID+O_TEXTDATA
     C                               : S_IRUSR + S_IWUSR
     C                               : 1208
     C                               : 0)
      *
     C                   EVAL      SIFS   = FILNAM
     C                   WRITE     EDISR
      *
     C                   EVAL      XSSN = SSN
      *
     C                   EVAL      SDATA = '<?xml version="1.0" '
     C                             + 'encoding="UTF-8"?>'
     C                   EXSR      OPDFIL
      *
     C                   ENDSR
      *
      ***********************************************
     C     SRUNIINT      BEGSR
      ***********************************************
      * <UniversalInterchange>
     C                   EVAL      SDATA = '<UniversalInterchange '
     C                             + 'xmlns="http://www.cargowise.com/Schenas'
     C                             + '/Universal/2011/11" '
     C                             + 'version="1.1">'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '<header>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '<SenderID>' + %TRIM(S0004)
     C                             + '</SenderID>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '<RecipiantID>' + %TRIM(S0010)
     C                             + '</RecipiantID>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '</Header>'
     C                   EXSR      OPDFIL
      *
     C                   EVAL      SDATA = '<Body>'
     C                   EXSR      OPDFIL
      *
     C                   EXSR      SRUNIEVT
      *
     C                   EVAL      SDATA = '</Body>'
     C                   EXSR      OPDFIL
      *
     C                   EVAL      SDATA = '</UniversalInterchange>'
     C                   EXSR      OPDFIL
      *
     C                   ENDSR
      *
      ***********************************************
     C     SRUNIEVT      BEGSR
      ***********************************************
      * <UniversalEvent>
     C                   EVAL      SDATA = '<UniversalEvent>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '<Event>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '<EventTime>' + %SUBST(WDT:1:4) + '-'
     C                             + %SUBST(WDT:5:2) +'-'+ %SUBST(WDT:7:2) +'T'
     C                             + %SUBST(WTM:1:2) +':'+ %SUBST(WTM:3:2) +':'
     C                             + %SUBST(WTM:5:2) + '</EventTime>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '<EventType>' + %TRIM(UTYPE)
     C                             + '</EventType>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '<EventReference>Docs required'
     C                             + '</EventReference>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '<ContextCollection>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '<Context>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '<Type>EntryNumber</Type>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '<Value>' + %TRIM(UREF) + '</Value>' Løpenr/eksp.enhet
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '</Context>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '<Context>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '<Type>DeclarationReference</Type>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '<Value>' + %TRIM(X0020) +'</Value>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '</Context>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '<Context>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '<Type>EntryNumberType</Type>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '<Value>' + %TRIM(UIE) + '</Value>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '</Context>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '<Context>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '<Type>EntryNumberCountryOfIssue'
     C                             + '</Type>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '<Value>NO</Value>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '</Context>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '</ContextCollection>'
     C                   EXSR      OPDFIL
      *
     C                   EVAL      SDATA = '<AttachedDocumentCollection>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '<AttachedDocument>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '<FileName>' + %TRIM(FILNAM4)
     C                             + '</FileName>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '<Type>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '<Code>CAU</Code>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '<Description>Customs Authority'
     C                             + '</Description>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '</Type>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '<ImageData>'
     C                   EXSR      OPDFIL
      *
     C     ARKIVL02K1    CHAIN     ARKIVL02                           33
     C                   IF        *IN33 = *OFF                                 1<-B
      * FINN DOKUMENT
     C     ARTYPE        CHAIN     ARKTXT                             33
     C                   IF        ARKLAG <> *BLANKS                             2<-B
     C     ARKLAGK       CHAIN     ARKEXT                             33
     C                   ELSE                                                    2
     C     ARFIRM        CHAIN     FIRMARC                            33
     C                   END                                                     2<-E
     C                   IF        %SUBST(ARCANE:1:1) = '/'                      2<-B
     C                   EVAL      FILNAM2 = *BLANKS
     C                   ELSE                                                    2
     C                   EVAL      FILNAM2 = '/'
     C                   END                                                     2<-E
     C                   EVAL      FILNAM2 = %TRIM(FILNAM2) + %TRIM(ARCANE)
     C                             + '/' + %TRIM(ARLINK)
     C     '.'           SCAN      ARLINK                                 33
     C  N33              EVAL      FILNAM2 = %TRIM(FILNAM2) + '.pdf'
      *
      * SJEKK TILLEGGSARK
     C     ARKIVL02K2    CHAIN     ARKIVL02                           33
     C                   IF        *IN33                                         2<-B
      * TILLEGGSARK FINNES IKKE
     C                   EVAL      XKUNHOVED = 'J'
     C                   ELSE                                                    2
      * BÅDE HOVEDARK OG TILLEGGSARK FINNES --> SLÅ SAMMEN
     C     ARTYPE        CHAIN     ARKTXT                             33
     C                   IF        ARKLAG <> *BLANKS                              3<-B
     C     ARKLAGK       CHAIN     ARKEXT                             33
     C                   ELSE                                                     3
     C     FIFIRM        CHAIN     FIRMARC                            33
     C                   END                                                      3<-E
     C                   IF        %SUBST(ARCANE:1:1) = '/'                       3<-B
     C                   EVAL      FILNAM2A = *BLANKS
     C                   ELSE                                                     3
     C                   EVAL      FILNAM2A = '/'
     C                   END                                                      3<-E
     C                   EVAL      FILNAM2A = %TRIM(FILNAM2A) + %TRIM(ARCANE)
     C                             + '/' + %TRIM(ARLINK)
     C     '.'           SCAN      ARLINK                                 33
     C  N33              EVAL      FILNAM2A = %TRIM(FILNAM2A) + '.pdf'
      * SLÅ SAMMEN DOKUMENTER (HOVEDARK: FILNAM2, TILLEGGSARK: FILNAM2A)
      * SAMMENSLÅTTE DOKUMENT: FILNAM2X
     C                   Eval      WCMD = 'CONCATPDF INPDF('
     C                             + XAPS + %TRIM(FILNAM2) + XAPS + ' '
     C                             + XAPS + %TRIM(FILNAM2A) + XAPS
     C                             + ') OUTPDF(' + XAPS + %TRIM(FILNAM2X) + XAPS
     C                             + ') JDEBUG(' + XAPS + '*NO' + XAPS + ')'
     C                   CALLP     RUNCMD(WCMD:500)
     C                   END                                                     2<-E
      *
     C                   EXSR      SRHENTDOK
     C                   END                                                    1<-E
      *
     C                   EVAL      SDATA = '</ImageData>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '<IsPublished>true</IsPublished>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '</AttachedDocument>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '</AttachedDocumentCollection>'
     C                   EXSR      OPDFIL
      *
     C                   EVAL      SDATA = '</Event>'
     C                   EXSR      OPDFIL
     C                   EVAL      SDATA = '</UniversalEvent>'
     C                   EXSR      OPDFIL
      *
     C                   ENDSR
      *
      ***********************************************
     C     SRHENTDOK     BEGSR
      ***********************************************
     C                   callp     close( FILE_o )
      *
      * Konverter PDF (FILNAM2X) til base64 (FILNAM3)
     C                   IF        XKUNHOVED = 'J'
     C                   EVAL      WCMD = 'QSH CMD(' + XAPS + 'openssl e'
     C                             + 'nc -base64 -in ' + %trimr(FILNAM2)
     C                             + ' -out ' + %trimr(FILNAM3LF) + XAPS + ')'
     C                   ELSE
     C                   EVAL      WCMD = 'QSH CMD(' + XAPS + 'openssl e'
     C                             + 'nc -base64 -in ' + %trimr(FILNAM2X)
     C                             + ' -out ' + %trimr(FILNAM3LF) + XAPS + ')'
     C                   END
      *   openssl enc -base64 -in file.txt -out file.txt.enc
     C                   CALLP     RUNCMD(WCMD:300)
      * Fjern LF fra  base64-fil
     C                   CALL      'SYXML55A'
     C                   PARM                    FILNAM3LF
     C                   PARM                    FILNAM3
      *
      * Sy sammen XML og PDF
     C                   EVAL      WCMD = 'QSH CMD(' + XAPS + 'cat'
     C                             + ' '
     C                             + %trimr(FILNAM3)
     C                             + ' >> '
     C                             + %trimr(FILNAM)
     C                             + XAPS + ')'
     C                   CALLP     RUNCMD(WCMD:300)
      *
     C                   eval      FILE_o = open( %TrimR( FilNam1 )
     C                               : O_CREAT+O_TRUNC+O_WRONLY
     C                                + O_TEXT_CREAT+O_CCSID+O_TEXTDATA
     C                               : S_IRUSR + S_IWUSR
     C                               : 1208
     C                               : 0)
      *
     C                   ENDSR
      *
      ***********************************************
     C     SRCLOSE       BEGSR
      ***********************************************
      *
     C                   callp     close( FILE_o )
     C                   EVAL      XCHGAUT = 'CHGAUT OBJ(' + XAPS
     C                             + %TRIM(FILNAM) + XAPS
     C                             + ') USER(*PUBLIC) DTAAUT(*RWX) OBJAUT(*ALL)'
     C                   CALLP     RUNCMD(XCHGAUT:200)
      * SLÅ SAMMEN
     C                   EVAL      WCMD = 'QSH CMD(' + XAPS + 'cat'
     C                             + ' '
     C                             + %trimr(FILNAM1)
     C                             + ' >> '
     C                             + %trimr(FILNAM)
     C                             + XAPS + ')'
     C                   CALLP     RUNCMD(WCMD:300)
      * FJERN BUNN-FIL
     C                   EVAL      WCMD = 'RMVLNK OBJLNK(' + XAPS
     C                             + %TRIM(filnam1) + XAPS + ') '
     C                   CALLP     RUNCMD(WCMD:500)
      * SAMMENSLÅTTE DOKUMENT
     C                   IF        XKUNHOVED <> 'J'
     C                   EVAL      WCMD = 'RMVLNK OBJLNK(' + XAPS
     C                             + %TRIM(filnam2X) + XAPS + ') '
     C                   CALLP     RUNCMD(WCMD:500)
     C                   END
      * FJERN BASE64-LF FIL
     C                   EVAL      WCMD = 'RMVLNK OBJLNK(' + XAPS
     C                             + %TRIM(filnam3LF) + XAPS + ') '
     C                   CALLP     RUNCMD(WCMD:500)
      * FJERN BASE64 FIL
     C                   EVAL      WCMD = 'RMVLNK OBJLNK(' + XAPS
     C                             + %TRIM(filnam3) + XAPS + ') '
     C                   CALLP     RUNCMD(WCMD:500)
      * CHGATR OBJ('home/cb/a.xml') ATR(*CCSID) VALUE(1208)
     C                   EVAL      WCMD = 'CHGATR OBJ(' + XAPS
     C                             + %TRIM(FILNAM) + XAPS + ') '
     C                             + 'ATR(*CCSID) VALUE(1208)'
     C                   CALLP     RUNCMD(WCMD:500)
      *
     C                   ENDSR
      *
      ***********************************************
     C     SRSNDSL       BEGSR
      ***********************************************
      *
     C     XSSN          CHAIN     EDIS                               33
     C  N33              MOVE      'X'           SST
     C  N33              UPDATE    EDISR
      *
     C                   ENDSR
      *
      ***********************************************
     C     OPDEDIM       BEGSR
      ***********************************************
     C     1             CHAIN     EDIC                               33
     C                   ADD       1             CMN
     C                   UPDATE    EDICR
      *
     C                   EVAL      M0004 = S0004
     C                   EVAL      M0010 = S0010
     C                   MOVEL     CMN           M0062
     C                   EVAL      M0065 = 'UNIEVT'
     C                   EVAL      M0068 = X0020 + '-' + %TRIM(UTYPE)
     C                   EVAL      M1001 = UTYPE
     C                   EVAL      M1004 = X0020
     C                   EVAL      M1225 = %TRIM(UTYPE)
     C                   EVAL      M1N07 = *BLANKS
     C                   EVAL      MSN = SSN
     C                   EVAL      MMN = CMN
     C                   EVAL      MSR = 'S'
     C                   EVAL      MST = 'C'
     C                   MOVE      WDT           MDT
     C                   MOVE      WTM           MTM
     C                   MOVE      UAVD          MAVD
     C                   MOVE      UTDN          MTDN
     C                   WRITE     EDIMR
      *
     C                   ENDSR
      *
      ***********************************************
     C     OPDTRACK      BEGSR
      ***********************************************
      *
     C                   EVAL      TTAVD  = MAVD
     C                   EVAL      TTOPD  = MTDN
     C                   EVAL      TTACTI = 'CW1'
     C                   EVAL      TTDATE = MDT
     C                   EVAL      TTTIME = MTM
     C                   EVAL      TTUSER = ZUSER
     C                   EVAL      TTDATL = MDT
     C                   EVAL      TTTIML = MTM
     C                   EVAL      TTEDEV = UTYPE
     C                   EVAL      TTMANU = 'E'
     C                   EVAL      TTTEXT = 'CW1-Shipment(' + %TRIM(X0020)
     C                             + ') is cleared by customs.'
     C                   EVAL      TTTEXL = 'CW1-Shipment(' + %TRIM(X0020)
     C                             + ') er ferdigbehandlet av TVINN.'
     C                   WRITE     TRACKFR
      *
     C                   ENDSR
      *
      ***********************************************
     C     OPDFIL        BEGSR
      ***********************************************
     C*                  EVAL      SDATA=(%trimr(SDATA))+ LF
     C                   EVAL      SDATA=(%trimr(SDATA))
     c                   callp     write(File_o : %addr(Sdata)
     c                                : %len(%trimr(Sdata)) )
      *
     C                   ENDSR
      *
      ***********************************************
     C     INIT          BEGSR
      ***********************************************
     C     FRISOKK       KLIST
     C                   KFLD                    FSAVD
     C                   KFLD                    FSOPD
     C                   KFLD                    FSKODE
     C     ARKIVL02K1    KLIST
     C                   KFLD                    ARSTAT
     C                   KFLD                    FSAVD
     C                   KFLD                    FSOPD
     C                   KFLD                    X1_ARTYPE
     C     ARKIVL02K2    KLIST
     C                   KFLD                    ARSTAT
     C                   KFLD                    FSAVD
     C                   KFLD                    FSOPD
     C                   KFLD                    X2_ARTYPE
     C     ARKLAGK       KLIST
     C                   KFLD                    ARFIRM
     C                   KFLD                    ARKLAG
      *
     C     *ENTRY        PLIST
     C                   PARM                    UAVD              4
     C                   PARM                    UTDN              7
     C                   PARM                    UTYPE             3
     C                   PARM                    UREF             17
     C                   PARM                    UIE               3
      *
     C     *LIKE         DEFINE    S0004         X0004
     C     *LIKE         DEFINE    S0010         X0010
     C     *LIKE         DEFINE    S0020         X0020
     C     *LIKE         DEFINE    SSN           XSSN
     C     *LIKE         DEFINE    ARTYPE        X1_ARTYPE
     C     *LIKE         DEFINE    ARTYPE        X2_ARTYPE
     C                   MOVE      'N'           XKUNHOVED         1
     C                   MOVE      X'7D'         XAPS              1
     C                   MOVE      UAVD          FSAVD
     C                   MOVE      UTDN          FSOPD
     C                   EVAL      ARSTAT = 'A'
     C                   SELECT
     C                   WHEN      UIE = 'IMP'
     C                   EVAL      X1_ARTYPE = 'si'
     C                   EVAL      X2_ARTYPE = 'sk'
     C                   WHEN      UIE = 'IMP'
     C                   EVAL      X1_ARTYPE = 'se'
     C                   EVAL      X2_ARTYPE = 'sg'
     C                   ENDSL
      *
     C     *LOVAL        SETLL     FIRM
     C                   READ      FIRM                                   33
     C                   EVAL      FSKODE = '*CR'
     C     FRISOKK       CHAIN     FRISOK                             33
     C                   MOVEL     FSSOK         X0020                          CW1 ORDRE NR
     C                   EVAL      FSKODE = '*CA'
     C     FRISOKK       CHAIN     FRISOK                             33
     C                   MOVEL     FSSOK         X0010                          CW1 AVSENDER
     C                   EVAL      FSKODE = '*CM'
     C     FRISOKK       CHAIN     FRISOK                             33
     C                   MOVE      FSSOK         X0004                          CW1 MOTTAKER
      *
     C                   ENDSR
      *
