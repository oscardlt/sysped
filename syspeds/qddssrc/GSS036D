     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPDOC('STD' 'HELPNU' 'HLP400')
     A                                      HELP
     A                                      ALTHELP(CA01)
      * "NOERASE" MÅ HA FOR Å IKKE TØMME BILDE SOM MAN KOMME FRA
     A          R NOERASE
     A                                      ASSUME
     A                                  1  2' '
      * MAN KUN DEFINNERE VINDU PÅ REKORDNIVÅ
     A          R WINDOWSZ
     A                                      WINDOW(*DFT 10 60 *NOMSGLIN)
     A                                      OVERLAY
      *
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  5  2VALUES(' ' '1' '4')
     A            ORONO          7  0O  5  5EDTCDE(4)
     A            ORBCD          5A  O  5 13
     A            ORLC           5A  O  5 19
     A            ORSNM         30A  O  5 25
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A                                      SFLSIZ(0006)
     A                                      SFLPAG(0005)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A                                      CA03(03 'Forrige')
     A                                      CA05(05 'Frisk')
     A                                      CA10(10 'Plukk alle')
     A                                      CA12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            ZRECNO         4S 0O  2 55SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'GSS036R'
     A                                      COLOR(BLU)
     A                                  1 10'Orders with same collino.'
     A                                      COLOR(WHT)
     A                                  2  2'Tripno.'
     A            UTNOI         11Y 0O  2 10DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A N28                              3  2'Select option:   1=Pick up'
     A                                      COLOR(BLU)
     A  28                              3  2'Select option:   4=Delete'
     A                                      COLOR(BLU)
     A                                  4  2'Op OrderNo BoatC Locat Supplier'
     A                                      COLOR(WHT)
      *
     A          R BUNN
     A                                      WINDOW(WINDOWSZ)
     A                                      SLNO(10)
     A                                      CLRL(01)
     A                                      OVERLAY
     A N30N28                           1  2'F3/F12=Cancel  F5=Refresh  F10=Pic-
     A                                      k up all'
     A                                      COLOR(BLU)
     A N30 28                           1  2'F3/F12=Cancel  F5=Refresh  F10=Del-
     A                                      ete all'
     A                                      COLOR(BLU)
      *
     A          R NYWRK
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A                                      CA03(03 'Forrige')
     A                                      CA05(05 'Frisk')
     A                                      CA12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'GSS036R'
     A                                      COLOR(BLU)
     A                                  1 10'Orders with same collino.'
     A                                      COLOR(WHT)
     A                                  2  2'Tripno.'
     A            UTNOI         11Y 0O  2 10DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A N28                              3  2'Select option:   1=Pick up'
     A                                      COLOR(BLU)
     A  28                              3  2'Select option:   4=Delete'
     A                                      COLOR(BLU)
     A                                  4  2'Op OrderNo BoatC Locat Supplier'
     A                                      COLOR(WHT)
     A N30                             10  2'F3/F12=Cancel  F5=Refresh'
     A                                      COLOR(BLU)
