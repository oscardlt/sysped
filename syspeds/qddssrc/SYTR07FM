     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A          R SUBFIL                    SFL
     A            WSFIRM    R        H      REFFLD(TRABR/TBFIRM *LIBL/TRAB)
     A            WSNRO          1A  B  6  2VALUES(' ' '1' '2' '4')
     A  56                                  DSPATR(PR)
     A  56                                  DSPATR(ND)
     A            TINR      R        O  6  4REFFLD(TILLR/TINR *LIBL/TILL)
     A  56                                  DSPATR(ND)
     A            TITYPE    R        O  6 21REFFLD(TILLR/TITYPE *LIBL/TILL)
     A  56                                  DSPATR(ND)
SV5  A*           TIDAY     R        O  6 31REFFLD(TILLR/TIDAY *LIBL/TILL)
SV5  A* 56                                  DSPATR(ND)
SV5  A*           TIMONT    R        O  6 34REFFLD(TILLR/TIMONT *LIBL/TILL)
SV5  A* 56                                  DSPATR(ND)
SV5  A*           TIYEAR    R        O  6 37REFFLD(TILLR/TIYEAR *LIBL/TILL)
SV5  A* 56                                  DSPATR(ND)
NV5  A            TIDATE    R        H      REFFLD(TILLR/TIDATE *LIBL/TILL)
NV5  A            WIDATE         6  0O  6 31EDTCDE(Y)
NV5  A  56                                  DSPATR(ND)
     A            XIAVD     R        O  6 40REFFLD(TILLR/TIAVD *LIBL/TILL)
     A N56                                  DSPATR(ND)
     A                                      EDTCDE(4)
     A                                      DSPATR(HI)
     A            XIPRO     R        O  6 45REFFLD(TILLR/TIPRO *LIBL/TILL)
     A N56                                  DSPATR(ND)
     A                                      EDTCDE(4)
     A                                      DSPATR(HI)
     A            TUSTEF         5   O  6 54COLOR(WHT)
     A N56                                  DSPATR(ND)
     A            TUDATF         6  0O  6 60COLOR(WHT)
     A N56                                  DSPATR(ND)
     A            TUSTET         5   O  6 68COLOR(WHT)
     A N56                                  DSPATR(ND)
     A            TUDATT         6  0O  6 74COLOR(WHT)
     A N56                                  DSPATR(ND)
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A            ZRECNO         4S 0O  2 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYSPED/SYTR07'
     A                                      COLOR(BLU)
     A  95                              1 19MSGCON(045 DIV3565 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A N55N95                           1 19MSGCON(045 DIV3565 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A N95 55                           1 19MSGCON(045 DIV4026 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                  1 67DATE
     A                                      EDTCDE(Y)
     A                                  2 67TIME
     A            TIAVD     R        O  2  2REFFLD(TILLR/TIAVD *LIBL/TILL)
     A            TIPRO     R        O  2  7REFFLD(TILLR/TIPRO *LIBL/TILL)
     A  55                              2 19MSGCON(045 DIV3726 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A N55                              3  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  55                              3 34MSGCON(16 DIV3727 SYMSGF)
     A                                      DSPATR(HI)
     A  55        GMAVD     R        O  3 51REFFLD(TILLR/TIAVD *LIBL/TILL)
     A  55        GMPRO     R        O  3 56REFFLD(TILLR/TIPRO *LIBL/TILL)
     A N55                              4  2MSGCON(030 DIV0017 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  55                              4 34MSGCON(030 DIV3725 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  2MSGCON(078 DIV3566 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A          R BUNN
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF03(03 'EXIT')
     A                                      CF12(03 'EXIT')
     A                                      CF05(05 'Frisk')
     A  55
     AO 95                                  CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                      CF10(10 'CHANGE MODE')
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  3  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  55
     AO 95                              3 42MSGCON(018 DIV0022 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 62MSGCON(018 DIV0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  98                              4  2MSGCON(078 DIV0012 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A N98                              4  2MSGCON(078 DIV3728 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A          R VARELI
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF12(12 'Forrige')
     A                                      CF03(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2MSGCON(078 DIV3566 *LIBL/SYMSGF)
     A            WSNR      R        B  2  4REFFLD(TILLR/TINR *LIBL/TILL)
     A  40                                  DSPATR(HI)
     A  40                                  DSPATR(PR)
     A  70                                  DSPATR(RI)
     A            WSTYPE    R        B  2 21REFFLD(TILLR/TITYPE *LIBL/TILL)
SV5  A*           WSDAY     R        B  2 31REFFLD(TILLR/TIDAY *LIBL/TILL)
SV5  A*                                     EDTCDE(4)
SV5  A*           WSMONT    R        B  2 34REFFLD(TILLR/TIMONT *LIBL/TILL)
SV5  A*                                     EDTCDE(4)
SV5  A*           WSYEAR    R        B  2 37REFFLD(TILLR/TIYEAR *LIBL/TILL)
SV5  A*                                     EDTCDE(4)
NV5  A            WSDATE         6  0B  2 31EDTCDE(4)
     A                                  3  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  4  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A          R VAREL2
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF12(12 'Forrige')
     A                                      CF03(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            W2NR      R        B  2  4REFFLD(TILLR/TINR *LIBL/TILL)
     A  55                                  DSPATR(PR)
     A N55                                  DSPATR(PC)
     A            W2TYPE    R        B  2 21REFFLD(TILLR/TITYPE *LIBL/TILL)
     A N55                                  DSPATR(PR)
     A                                  3  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A          R VAREL3
     A                                      CF12(12 'Forrige')
     A                                      CF03(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A                                  1  2MSGCON(078 DIV3566 *LIBL/SYMSGF)
     A            TINR      R        O  2  4REFFLD(TILLR/TINR *LIBL/TILL)
     A                                      COLOR(WHT)
     A            TITYPE    R        O  2 21REFFLD(TILLR/TITYPE *LIBL/TILL)
     A                                      COLOR(WHT)
SV5  A*           TIDAY     R        O  2 30REFFLD(TILLR/TIDAY *LIBL/TILL)
SV5  A*                                     COLOR(WHT)
SV5  A*           TIMONT    R        O  2 33REFFLD(TILLR/TIMONT *LIBL/TILL)
SV5  A*                                     COLOR(WHT)
SV5  A*           TIYEAR    R        O  2 36REFFLD(TILLR/TIYEAR *LIBL/TILL)
SV5  A*                                     COLOR(WHT)
NV5  A            TIDATE    R        O  2 36REFFLD(TILLR/TIDATE *LIBL/TILL)
NV5  A            WIDATE         6  0O  2 30EDTCDE(Y)
NV5  A                                      COLOR(WHT)
     A                                  3  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A          R NYWRK
     A                                      CF03(03 'EXIT')
     A                                      CF12(03 'EXIT')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                      CF10(10 'MODE')
     A                                  1  2'SYSPED/SYTR07'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(045 DIV3565 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                  1 67DATE
     A                                      EDTCDE(Y)
     A            TIAVD     R        O  2  2REFFLD(TILLR/TIAVD *LIBL/TILL)
     A                                      COLOR(WHT)
     A            TIPRO     R        O  2  7REFFLD(TILLR/TIPRO *LIBL/TILL)
     A                                      COLOR(WHT)
     A  55                              2 19MSGCON(045 DIV3726 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                  2 67TIME
     A                                  3  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  55                              3 34MSGCON(16 DIV3727 SYMSGF)
     A                                      DSPATR(HI)
     A  55        GMAVD     R        O  3 51REFFLD(TILLR/TIAVD *LIBL/TILL)
     A  55        GMPRO     R        O  3 56REFFLD(TILLR/TIPRO *LIBL/TILL)
     A                                  4  2MSGCON(030 DIV0017 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  2MSGCON(078 DIV3566 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 42MSGCON(018 DIV0022 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 62MSGCON(018 DIV0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N98                             24  2MSGCON(078 DIV3728 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A          R BILDE1
     A                                      CF03(03 'EXIT')
     A                                      CF12(03 'EXIT')
     A                                      CF04(04 'SEARCH')
     A                                      CF06(06 'FRI TILLATELSE')
     A                                  1  2'SYSPED/SYTR07'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(045 DIV3565 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                  1 67DATE
     A                                      EDTCDE(Y)
     A                                  2 67TIME
     A                                  4  2MSGCON(024 DIV1497 *LIBL/SYMSGF)
     A            TIAVD     R        B  4 28REFFLD(TILLR/TIAVD *LIBL/TILL)
     A                                      EDTCDE(4)
     A                                  5  2MSGCON(024 DIV1499 *LIBL/SYMSGF)
     A            TIPRO     R        B  5 28REFFLD(TILLR/TIPRO *LIBL/TILL)
     A                                      EDTCDE(4)
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(018 DIV0032 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 42MSGCON(018 DIV3724 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  44        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
