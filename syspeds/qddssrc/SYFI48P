********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 YBC PTF10 UNDERSTREKK, IKKE STREKK
********************************************************************
      * *IN96 = FRA E-SENDINGER
      * *IN97 = FRA SJØOPPDRAG HEUR F / G
      * *IN98 = FRA FLYOPPDRAG HEUR C / D / E
      * *IN99 = FRA ANNET(LAND)HEUR A / B / H
     A          R HEAD
     A            KFMT1         75      1  1
     A            KFMT2         75      1  1
     A  40        HP1           80      1  1
     A  40        HP2           80      2  1
     A  40        HP3           80      3  1
     A  40        HP4           80      4  1
     A  40        HP5           80      5  1
     A  40        HP6           80      6  1
     A  40        HP7           80      7  1
     A  80N90N96  AUPRO          8     10 60BARCODE(CODE128 3 *HRITOP *HRZ +
     A                                      (*WIDTH 0.022) (*RATIO 3) X'02')
     A N96                             10  3MSGCON(24 PRI0024 SYMSGF)
     A                                      HIGHLIGHT
     A  96                             10  3MSGCON(30 JOV1477 SYMSGF)
     A                                      HIGHLIGHT
     A N96        HEPRO          8  0  10 24
     A                                      HIGHLIGHT
     A  98N96                          10 34MSGCON(4 DIV1098 SYMSGF)
     A* 98N96     HERFA         15     10 44
     A  98N96     HERFA15       15     10 44
     A  99N96                          10 33MSGCON(10 JOV0246 SYMSGF)
     A  99N96     HEHAWB         9     10 44
     A  96        EDSSOK        35     10 35
     A                                      HIGHLIGHT
     A                                 10103'Side:'
     A                                 10114PAGNBR
                                            EDTCDE(Z)
     A                                 11  3MSGCON(12 JOV1026 SYMSGF)
     A            ADATO          6  0  11 16EDTCDE(Y)
     A  99N96                          11 33MSGCON(7 DIV2130 SYMSGF)
     A  99N96     HEGN          15     11 44
     A  96        XXNAA         30     11 35
     A                                      HIGHLIGHT
     A                                 11103MSGCON(5 DIV0062 SYMSGF)
     A            IDAG           6  0  11110EDTCDE(Y)
     A  97                             12  3MSGCON(10 JOV1027 SYMSGF)
     A  97        BLOB           5     12 34
     A  97        BLBBNA        30     12 40
     A                                 12103MSGCON(7 JOV0691 SYMSGF)
     A            TID            6  0  12110EDTWRD('  .  .  ')
     A N96                             13  3MSGCON(7 DIV0084 SYMSGF)
     A N96        HENAA         30     13 12
     A                                 13103MSGCON(7 JOV0690 SYMSGF)
     A            ZUSER         10     13110
     A          R HEAD2
     A                                 14  1'------------------------'
     A                                 14 25'------------------------'
     A                                 14 49'------------------------'
     A                                 14 73'------------------------'
     A                                 14 97'------------------------'
     A                                 15  3MSGCON(10 JOV0563 SYMSGF)
     A                                 15 15MSGCON(24 PRI0026 SYMSGF)
     A                                 15 47MSGCON(8 DIV1916 SYMSGF)
     A                                 15 78MSGCON(7 DIV1440 SYMSGF)
     A                                 15 86MSGCON(11 PRI0027 SYMSGF)
     A                                 15106MSGCON(16 JOV0355 SYMSGF)
     A                                 16  1'------------------------'
     A                                 16 25'------------------------'
     A                                 16 49'------------------------'
     A                                 16 73'------------------------'
     A                                 16 97'------------------------'
N06  A          R HEAD2SS                   HIGHLIGHT
N06  A                                      SPACEB(1)
N06  A                                     1'************************'
N06  A                                    25'************************'
N06  A                                    49'************************'
N06  A                                    73'**'
N06  A          R HEAD2F                    HIGHLIGHT
N06  A                                      SPACEB(1)
N06  A                                     1'*'
N06  A            FRTTXT        70         3
N06  A                                    74'*'
     A          R DET                       SPACEB(1)
     A            HEPOS1         7         3
     A                                    10'/'
     A            HEPOS2         4  0     11EDTCDE(Z)
     A            HENAS         30        16
     A            HENAK         30        47
     A            HENT           7  0     78EDTCDE(Z)
     A            HEVS1         25        86
     A            HEVKT          9  0    109EDTCDE(Z)
     A                                      SPACEA(1)
     A            HEGM1         15        23
     A            HEADK1        30        47
     A            HEFBV          9  0    109EDTCDE(Z)
     A                                      SPACEA(1)
     A            HEGM2         15        23
     A            HEADK2        30        47
     A                                    79MSGCON(5 PRI0029 SYMSGF)
     A            HESDL         20        85
     A                                   107'LM='
     A            HELM           4  2    113EDTCDE(3)
     A                                      SPACEA(1)
     A N96        HEGN          15         3
     A N96                                19'/'
     A N96        HEPRO          8  0     20EDTCDE(Z)
     A N96                                28'/'
     A  96        HXSNDN        20         3HIGHLIGHT
     A            HEAVD          4  0     29EDTCDE(Z)
     A                                    33'/'
     A            HEOPD          7  0     34EDTCDE(Z)
     A            HEADK3        30        47
     A            HESDF          5        79
     A                                    86'->'
     A            HESDT          5        89
     A            HEOT           2        96
     A            HEFR           3       100
     A                                   107'M3='
     A            HEM3           7  3    110EDTCDE(3)
     A                                      SPACEA(1)
     A          R DETFS                     SPACEB(1)
     A            KFSOTX        30         3
     A                                    33':'
     A            FSSOK         35        35SPACEA(1)
     A          R DETUFB                    SPACEB(1)
N09  A                                      HIGHLIGHT
     A                                     3MSGCON(24 YBC0491 SYMSGF)
     A            HEFT1A        30        32
     A            HEFT1B        15        65HIGHLIGHT
N09  A                                      SPACEA(1)
     A          R DET2                      SPACEA(1)
     A            FRTTXT        70         3
     A          R BARLEFT
     A            HXSNDNB       20        10
N09  A            DISTXT1       50        36
     A            HXSNDN        20         5BARCODE(CODE128 2 *NOHRI *HRZ +
     A                                      (*WIDTH 0.015) X'02')
E09  A                                      SPACEA(1)
N09  A            DISTXT2       50        36SPACEA(1)
N09  A            DISTXT3       50        36SPACEA(1)
     A          R BARRIGHT
E09  A            HXSNDNB       20        93
N09  A            DISTXT1       50        36
E09  A            HXSNDN        20        88BARCODE(CODE128 2 *NOHRI *HRZ +
     A                                      (*WIDTH 0.015) X'02')
E09  A                                      SPACEA(1)
N09  A            DISTXT2       50        36SPACEA(1)
N09  A            DISTXT3       50        36SPACEA(1)
     A          R BARLEFTO
     A            HEAVDOPDB     12        13
     A            HEAVDOPD      12         8BARCODE(CODE128 2 *NOHRI *HRZ +
     A                                      (*WIDTH 0.015) X'02')
     A                                      SPACEA(2)
     A          R BARRIGHTO
     A            HEAVDOPDB     12        85
     A            HEAVDOPD      12        80BARCODE(CODE128 2 *NOHRI *HRZ +
     A                                      (*WIDTH 0.015) X'02')
     A                                      SPACEA(2)
     A          R BARSPACE                  SPACEB(1)
     A                                     3' '
     A          R DET3                      SPACEA(1)
S01  A*                                    1'------------------------'
S01  A*                                   25'------------------------'
S01  A*                                   49'------------------------'
S01  A*                                   73'------------------------'
S01  A*                                   97'------------------------'
N01  A                                     1'________________________'
N01  A                                    25'________________________'
N01  A                                    49'________________________'
N01  A                                    73'________________________'
N01  A                                    97'________________________'
     A          R PTOT                      SPACEB(2)
     A                                     3MSGCON(24 PRI0012 SYMSGF)
     A                                    29MSGCON(14 PRI0013 SYMSGF)
     A            XTPOS          5  0     44EDTCDE(Z)
     A                                    51MSGCON(17 PRI0014 SYMSGF)
     A            XTNT           7  0     69EDTCDE(Z)
     A                                    77MSGCON(15 PRI0015 SYMSGF)
     A            XTVKT          9  0     90EDTCDE(Z)
     A                                   100MSGCON(10 PRI0010 SYMSGF)
     A            XTVKT2         9  0    110EDTCDE(Z)
     A          R H1
     A                                 14  1'------------------------'
     A                                 14 25'------------------------'
     A                                 14 49'------------------------'
     A                                 14 73'------------------------'
     A                                 14 97'------------------------'
     A                                 15  2MSGCON(80 JOV0692 SYMSGF)
     A                                 16  1'------------------------'
     A                                 16 25'------------------------'
     A                                 16 49'------------------------'
     A                                 16 73'------------------------'
     A                                 16 97'------------------------'
     A          R D1                        SPACEB(1)
     A            ASMVK          3         3
     A            KGENOT        20         9
     A            ASMVAL         3        30
     A            ASMBLV        13  2     35EDTCDE(J)
     A            ASMBLN        13  2     55EDTCDE(J)
     A            ASMNT          3  0     76
     A          R T1                        SPACEB(2)
     A                                     3MSGCON(24 PRI0025 SYMSGF)
     A            XTOTAL        13  2     55EDTCDE(J)
