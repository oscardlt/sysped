     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HELP
     A                                      ALTHELP(CA01)
      * "NOERASE" MÅ HA FOR Å IKKE TØMME BILDE SOM MAN KOMME FRA
     A          R NOERASE
     A                                      ASSUME
     A                                  1  2' '
      * MAN KUN DEFINNERE VINDU PÅ REKORDNIVÅ
     A          R WINDOWSZ
     A                                      WINDOW(*DFT 15 69 *NOMSGLIN)
     A                                      OVERLAY
      *
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  5  2VALUES(' ' '1')
     A            WSWNT0         7  0B  5  5EDTCDE(4)
     A  31                                  COLOR(WHT)
     A            LHMN           7  0H
     A            LLMN           7  0H
     A            LLWNT0         7  0H
     A            LHBNA         30A  O  5 13
     A  42                                  COLOR(YLW)
     A            LHTDT          8  0O  5 44EDTWRD('    .  .  ')
     A            LLANT0         7  0O  5 55EDTCDE(4)
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A                                      CA03(03 'Forrige')
     A                                      CA07(07 'Re-allokkering')
     A                                      CA12(12 'Forrige')
     A                                      CF23(23 'Bekreft')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            ZRECNO         4S 0O  2 28SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'LL080R'
     A                                      COLOR(BLU)
     A                                  1 10'Arbeid med fordeling av varer'
     A                                      COLOR(WHT)
     A                                  2  2'Opsjon:  1=Plukk'
     A                                      COLOR(BLU)
     A                                  2 22'Artikkel:'
     A            VAANR         15A  O  2 32DSPATR(UL)
     A                                      COLOR(WHT)
     A            VAVARE        35A  H
     A            VAVARE20      20A  O  2 48DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  3  2MSGCON(10 YBC1275 SYMSGF)
     A            VASALD         7  0O  3 13DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  3 21MSGCON(09 YBC1276 SYMSGF)
     A            XWNT0A         7  0O  3 31DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  3 39MSGCON(06 YBC1277 SYMSGF)
     A            XWNT0D         7  0O  3 46DSPATR(UL)
     A                                      EDTCDE(L)
     A                                      COLOR(WHT)
     A                                  4  2'Op Alc.stk Mottaker               -
     A                                             Lev.dato   Bes.stk'
     A                                      COLOR(WHT)
      *
     A          R BUNN
     A                                      WINDOW(WINDOWSZ)
     A                                      SLNO(15)
     A                                      CLRL(01)
     A                                      OVERLAY
     A                                  1  2'F7=Re-alloker   F12=Avbryt   F23=B-
     A                                      ekreft'
     A                                      COLOR(BLU)
     A  30        MSGTXT        50A  O  1  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
