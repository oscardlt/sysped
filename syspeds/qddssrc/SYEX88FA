*****A**************************************************************
     A* RETTINGER I DETTE PROGRAM:
PTFnrA*Sek Sig Vers Beskrivelse...........................
"""""A*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
P8068A* 01 TH  PTF8 NY HELPFUNKSJON  KUN DDS
10261 * 02 YBC PTF10 VEKT/KUBIKK/KOLLI I TURER ER ENDRET TIL 9.0, 7.3 OG 7.0
*****A**************************************************************
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPPNLGRP(HLPSCR SYEX88APNL)
     A                                      HLPTITLE('ARBEIDE MED TURER')
     A                                      HELP
     A                                      ALTHELP
     A          R BILDE
     A N55                                  CF03(03 'Avslutt')
     A N27                                  CF04(04 'SØKE KUNDE')
     A                                      CF12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A          H                           HLPPNLGRP(SYEX88A010 SYEX88APNL)
     A                                      HLPARA(01 001 04 080)
     A          H                           HLPPNLGRP(SYEX88A020 SYEX88APNL)
     A                                      HLPARA(05 001 05 080)
     A          H                           HLPPNLGRP(SYEX88A030 SYEX88APNL)
     A                                      HLPARA(06 001 06 080)
     A          H                           HLPPNLGRP(SYEX88A040 SYEX88APNL)
     A                                      HLPARA(07 001 07 080)
     A          H                           HLPPNLGRP(SYEX88A050 SYEX88APNL)
     A                                      HLPARA(08 001 08 080)
     A          H                           HLPPNLGRP(SYEX88A060 SYEX88APNL)
     A                                      HLPARA(09 001 12 080)
     A          H                           HLPPNLGRP(SYEX88A070 SYEX88APNL)
     A                                      HLPARA(13 001 13 080)
     A          H                           HLPPNLGRP(SYEX88A080 SYEX88APNL)
     A                                      HLPARA(14 001 14 080)
     A          H                           HLPPNLGRP(SYEX88A090 SYEX88APNL)
     A                                      HLPARA(15 001 15 080)
     A          H                           HLPPNLGRP(SYEX88A100 SYEX88APNL)
     A                                      HLPARA(16 001 16 080)
     A          H                           HLPPNLGRP(SYEX88A110 SYEX88APNL)
     A                                      HLPARA(17 001 17 080)
     A          H                           HLPPNLGRP(SYEX88A120 SYEX88APNL)
     A                                      HLPARA(18 001 18 080)
     A          H                           HLPPNLGRP(SYEX88A130 SYEX88APNL)
     A                                      HLPARA(19 001 19 080)
     A          H                           HLPPNLGRP(SYEX88A140 SYEX88APNL)
     A                                      HLPARA(20 001 20 080)
     A          H                           HLPPNLGRP(SYEX88A150 SYEX88APNL)
     A                                      HLPARA(21 001 24 080)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1  2'SYSPED/SYEX88A'
     A                                      COLOR(BLU)
     A  27                              1 20MSGCON(030 DIV0660 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A N27 31                           1 22MSGCON(030 DIV0661 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A N27N31                           1 22MSGCON(030 DIV0723 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 57DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  1 67TIME
     A                                      COLOR(BLU)
     A                                  3  2MSGCON(009 DIV0050 *LIBL/SYMSGF)
     A            UAVD           4Y 0O  3 22EDTCDE(4)
     A                                      COLOR(WHT)
     A N28                              3 30MSGCON(010 DIV0659 *LIBL/SYMSGF)
     A  28                              3 30MSGCON(010 DIV0677 *LIBL/SYMSGF)
     A                                      COLOR(RED)
     A N28        UPRO           8Y 0O  3 41EDTCDE(4)
     A                                      COLOR(WHT)
     A N31                              5  2MSGCON(015 DIV0662 *LIBL/SYMSGF)
     A  31                              5 10MSGCON(015 DIV5099 *LIBL/SYMSGF)
     A            WSDT           6Y 0B  5 22DSPATR(UL)
     A  99                                  DSPATR(PC)
     A  99                                  DSPATR(RI)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                  6  2MSGCON(015 DIV0663 *LIBL/SYMSGF)
     A            WSKNA          8Y 0B  6 22DSPATR(UL)
     A  98                                  DSPATR(PC)
     A  98                                  DSPATR(RI)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  7  2MSGCON(015 DIV0664 *LIBL/SYMSGF)
     A            WSNAA         30A  O  7 22DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  8  2MSGCON(015 DIV0665 *LIBL/SYMSGF)
     A            WSKNT          8Y 0B  8 22DSPATR(UL)
     A  97                                  DSPATR(PC)
     A  97                                  DSPATR(RI)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  9  2MSGCON(015 DIV0666 *LIBL/SYMSGF)
     A            WSNAT         30A  B  9 22DSPATR(UL)
     A  96                                  DSPATR(PC)
     A  96                                  DSPATR(RI)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                 10  2MSGCON(015 DIV0667 *LIBL/SYMSGF)
     A            WSAD1T        30A  B 10 22DSPATR(UL)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                 11  2MSGCON(015 DIV0668 *LIBL/SYMSGF)
     A            WSAD2T        30A  B 11 22DSPATR(UL)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                 12  2MSGCON(015 DIV0669 *LIBL/SYMSGF)
     A            WSAD3T        30A  B 12 22DSPATR(UL)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                 13  2MSGCON(015 JOV0628 *LIBL/SYMSGF)
     A            WSMÅTE         2Y 0B 13 22DSPATR(UL)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                 13 27MSGCON(015 SHU0451 *LIBL/SYMSGF)
     A            WSTSTE         2Y 0B 13 43EDTCDE(4)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A  27                             13 54MSGCON(012 DIV0704 *LIBL/SYMSGF)
     A  27        TOTFRA         9Y 0O 13 67DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A                                 14  2MSGCON(015 DIV0670 *LIBL/SYMSGF)
     A            WSBILN        10A  B 14 22DSPATR(UL)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                 14 33'/'
     A            WSHENG        10A  B 14 35DSPATR(UL)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A  27                             14 54MSGCON(012 DIV0705 *LIBL/SYMSGF)
     A  27        TOTFR1         9Y 0O 14 67DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A                                 15  2MSGCON(015 DIV0671 *LIBL/SYMSGF)
     A            WSTARF        15A  B 15 22DSPATR(UL)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A  27                             15 54MSGCON(012 DIV0706 *LIBL/SYMSGF)
     A  27        TOTFR2         9Y 0O 15 67DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A                                 16  2MSGCON(015 DIV0672 *LIBL/SYMSGF)
     A            WSLK           2A  B 16 22DSPATR(UL)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                 16 54MSGCON(012 SHU0732 *LIBL/SYMSGF)
     A  27        TOTFB          5Y 0O 16 67DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 17  2MSGCON(015 DIV0673 *LIBL/SYMSGF)
     A            WSSDF         20A  B 17 22DSPATR(UL)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A  27                             17 54MSGCON(012 DIV0707 *LIBL/SYMSGF)
S02  A* 27        TUTS           5Y 0O 17 67DSPATR(UL)
N02  A  27        TUTS           7Y 0O 17 67DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A                                 18  2MSGCON(015 DIV0674 *LIBL/SYMSGF)
     A            WSSDT         20A  B 18 22DSPATR(UL)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A  27                             18 54MSGCON(012 DIV0708 *LIBL/SYMSGF)
S02  A* 27        TUTVKT         5Y 0O 18 67DSPATR(UL)
N02  A  27        TUTVKT         9Y 0O 18 67DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A                                 19  2MSGCON(015 DIV0675 *LIBL/SYMSGF)
     A            TUAO           5Y 0O 19 22DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A  27                             19 54MSGCON(012 DIV0709 *LIBL/SYMSGF)
S02  A* 27        TUTM3          5Y 2O 19 67DSPATR(UL)
N02  A  27        TUTM3          7Y 3O 19 67DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A                                 20  2MSGCON(015 DIV0676 *LIBL/SYMSGF)
     A            WSBUSJ         5Y 0B 20 22DSPATR(UL)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A  27                             20 54MSGCON(012 DIV0710 *LIBL/SYMSGF)
     A  27        TUTLM          4Y 2O 20 67DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A  27N55                          21  2MSGCON(060 DIV0711 *LIBL/SYMSGF)
     A  27        IMPL           1A  B 22 38DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'X')
     A  55                                  DSPATR(ND)
     A  55                                  DSPATR(PR)
     A  27        EXPL           1A  B 22 44DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'X')
     A  55                                  DSPATR(ND)
     A  55                                  DSPATR(PR)
     A  27        IMPF           1A  B 22 51DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'X')
     A  55                                  DSPATR(ND)
     A  55                                  DSPATR(PR)
     A  27        EXPF           1A  B 22 57DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'X')
     A  55                                  DSPATR(PR)
     A  55                                  DSPATR(ND)
     A N55                             23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N27                             23 42MSGCON(018 DIV0032 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 62MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N31                              5 31MSGCON(015 TRO0510 *LIBL/SYMSGF)
     A            WSDTT          6Y 0B  5 47DSPATR(UL)
     A  95                                  DSPATR(PC)
     A  95                                  DSPATR(RI)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
