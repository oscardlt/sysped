      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/TBOK009R MODULE(*LIBL/TBOK009R *LIBL/INCGI)
CRTPGM*        ACTGRP(*NEW) AUT(*USE)
      *
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
12XXX * 01 JOV PTF12 Rydding i tidligere feilaktig PTF-merking + update zokufe
12XXX * 02 JOV PTF12 Skriv til T&T
********************************************************************
      *********************************************************************
      /copy SYSPEDS/qrpgsrcpy,hspecs
      /copy SYSPEDS/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FBRPARF    IF   E           K DISK    USROPN
     FZEADL05U  UF A E           K DISK    USROPN
     FZREETXT2  UF   E           K DISK    USROPN
     FZOKUFV2   UF   E           K DISK    USROPN
     FZOKUFVF2  UF   E           K DISK    USROPN
     FZOKUFM2   UF   E           K DISK    USROPN
N01  FZOKUFEL1  UF   E           K DISK    USROPN
N02  FZTRACKF   O  A E             DISK    USROPN
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy SYSPEDS/qrpgsrcpy,prototypeb
      /copy SYSPEDS/qrpgsrcpy,usec
      /copy SYSPEDS/qrpgsrcpy,variables3
      *
      * Varible for
     D WSUSER          S             10
     D WA              S             15
     D JSONstring      s          32000
     D Error           S            150
     D AntLest         S              9  0
     D mode            S              1

     D UPC             C                   CONST('ABCDEFGHIJKLMNOPQRST-
     D                                     UVWXYZÆØÅ')
     D LO              C                   CONST('abcdefghijklmnopqrst-
     D                                     uvwxyzæøå')

      *get input-string
      /copy syspeds/qrpgsrcpy,prolog3

      * Parse input
     C                   exsr      Parse
      * Open files etc
     C                   EXSR      INIT
      *
     c                   callp     gethtmlifs(
     C                             '/syspeddev/html/JSON.html':
     C                             '/CGITAG/')
     C                   callp     wrtsection('top')
      *
      * Hoved-rutine - Teste input og utfør
     C                   EXSR      MAIN
      * ............................................................................................
      * skriv JSON-Object start..(alltid '{' + x ant param. m/komma mellom (IKKE komma etter siste))
      * ............................................................................................
      *
      /free
        JSONstring='{'
        +'¬user¬ : ¬'+%trim(WSUSER)+'¬, '
        +'¬hereff¬ : ¬'+%trim(HEREFF)+'¬, '
        +'¬heunik¬ : ¬'+%trim(%editc(HEUNIK:'Z'))+'¬, '
        +'¬mode¬ : ¬'+%trim(MODE)+'¬, '
        +'¬errMsg¬ : ¬'+%trim(Error)+'¬}';
      /end-free
      * ............................................................................................
      * Skriv JSON-string Avsluttning
      * ............................................................................................
     c                   call      'JSONFIX'
     c                   parm                    JSONstring
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')
      *
      * Quit
     C                   exsr      Exit


      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Lukk fil
     C                   CLOSE     BRIDF
     C  N50              CLOSE     BRPARF
     C  N50              CLOSE     ZEADL05U
     C  N50              CLOSE     ZREETXT2
     C  N50              CLOSE     ZOKUFV2
     C  N50              CLOSE     ZOKUFVF2
     C  N50              CLOSE     ZOKUFM2
     C  N50              CLOSE     ZOKUFEL1
N02  C  N50              CLOSE     ZTRACKF

      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   MOVE      *ON           *INLR
     C                   return
     C                   endsr
      *
      *********************************************************
     C     Parse         Begsr
      *********************************************************
      * Get input
     C                   EVAL      WSUSER  = zhbgetvar('USER')
     C                   eval      WA      = zhbgetvar('HEUNIK')
     C                   eval      HEUNIK  = c2n2(WA)

     c                   endsr
      *
      *********************************************************
     C     INIT          Begsr
      *********************************************************
     C                   OPEN      BRIDF
      * Test innlogging
     C     WSUSER        CHAIN     BRIDF                              50
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C                   call      BILIBL
     C                   end

     C   50              eval      Error = 'Invalid userID'
     C  N50              OPEN      BRPARF
     C  N50              OPEN      ZEADL05U
     C  N50              OPEN      ZREETXT2
     C  N50              OPEN      ZOKUFV2
     C  N50              OPEN      ZOKUFVF2
     C  N50              OPEN      ZOKUFM2
N01  C  N50              OPEN      ZOKUFEL1
N02  C  N50              OPEN      ZTRACKF
      *
     C     ZeaL05Key     klist
     C                   kfld                    TRKNFA
     C                   kfld                    HEUNIK
     C                   eval      TRKNFA = BIKUNR
      *
     c     FirmKey       klist
     c                   kfld                    DFTUSER          10
     c                   kfld                    NULLKU            8 0
     c                   kfld                    PAID
     c                   Eval      DFTUSER = '*KUNDFT*'+BIFIRM
     c     GrupKey       klist
     c                   kfld                    DFTUSER
     c                   kfld                    BIKNG
     c                   kfld                    PAID
     c     KundKey       klist
     c                   kfld                    DFTUSER
     c                   kfld                    BIKUNR
     c                   kfld                    PAID
     C     BrParKey      klist
     C                   kfld                    PABRID
     C                   kfld                    PAKUNR
     C                   kfld                    PAID
     C                   MOVE      BIBRID        PABRID
     C                   MOVE      *zeros        PAKUNR
      *
     c                   endsr
      *
      *********************************************************
     C     MAIN          Begsr
      *********************************************************
     c     ZeaL05Key     chain     ZEADL05U
     c                   if        not %found (zeadl05U)
     c                             or HEST <> 'E'
     C                   eval      Error = 'Ordren finnes ikke eller har ikke '
     c                             +'korrekt status for denne handlingen.'
     c                   goto      UtMain
     C                   endif
      *
      *  Overfør - Mer eller mindre blåkopi av ESOP11-rutiner.
     C                   MOVE      'N'           XDIREKT           1
     C                   IF        ((HEUR <> ' ') AND (HEAVD <> 0))
     C                   MOVE      'HEDIR'       PAID
     C     BrParKey      CHAIN     BRPARF                             33
     C   33KundKey       chain     BRPARF                             33
     C   33GrupKey       chain     BRPARF                             33
     C   33FirmKey       chain     BRPARF                             33
     C  N33              IF        ((PAVRDA = 'J') OR (PAVRDA = 'j'))
     C                   MOVE      'J'           XDIREKT
     C                   END
     C                   END
      *
     C                   MOVE      'WEB'         HESG
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
N02  C*                  PARM                    WTD               6
N02  C                   PARM                    WTM               6
     C                   MOVE      WDT           HEDTR
     C                   MOVE      ' '           HEST
     C                   MOVE      ' '           HE01
     c                   if        HEPK1<>'P'                                   Fraktbr..
     c                   eval      HEPK1 =' '
     c                   endif
     c                   if        HEPK2 ='P'
     c                   eval      HEPK7 ='P'                                   Lånt for CMR
     c                   else
     c                   eval      HEPK7 =' '
     c                   endif
     c                   eval      HEPK2 =' '
     c                   eval      HEPK3 =' '                                   Lånt for label
     C                   MOVE      ' '           HE01
     C                   MOVE      WSUSER        HEFILE
      *
     C                   UPDATE    Headfr
N02   *
N02  C                   exsr      TTSR
      *
     C                   exsr      FixReff
      *
     C                   MOVE      HEUNIK        UUNIK             9
      *
      * E-POST TIL mailadresse registrert i bildet (XREETXT)
      * hvis til innland -utsett denne til oppdr.nr er tildelt (utføres i ESOP11D)
      * (burde kankje utsette på alle dersom XDIREKT = 'J' - men staretr enkelt)
     c                   if        HEAVD<>9999 and HEAVD<>9998 and HEAVD<>9997
     C                   MOVE      '*FTXT'       UTYPE             5
     C                   CALL      'ESOP11C'
     C                   PARM                    UUNIK
     C                   PARM                    UTYPE
     c                   endif
      *
      * E-POST TIL INTERBRUKER
     C                   MOVE      'HEUMI'       UTYPE             5
     C                   CALL      'ESOP11C'
     C                   PARM                    UUNIK
     C                   PARM                    UTYPE
      * E-POST TIL intern avhenger av oppdragstype
     C                   MOVE      'HEUMO'       UTYPE             5
     C                   CALL      'ESOP11C'
     C                   PARM                    UUNIK
     C                   PARM                    UTYPE
      *
      * E-POST TIL WEB-RBRUKER (KUNDE SOM REGISTRERER OPPDRAG)
     C                   MOVE      'HEUMS'       UTYPE             5
     C                   CALL      'ESOP11C'
     C                   PARM                    UUNIK
     C                   PARM                    UTYPE
      *
     C                   IF        XDIREKT = 'J'
     C                   CALL      'ESOP11D'
     C                   PARM                    HEREFF
     C                   PARM                    HEUNIK
     C                   END
      *
     c     UtMain        endsr
      *
      *********************************************************
     C     FixReff       Begsr
      *********************************************************
      * internt i Tbok... stoler en på at UNIK er 100% unik..
      * men oppdater alle underregister med REFF lik HEREFF (SYOP21 m.fl)
      * (SYOP21 m.fl leser på sammensatt key REFF / UNIK ...)
      *
     c     HEUNIK        setll     ZREETXT2
     c     HEUNIK        reade     ZREETXT2                               33
     c     *in33         doweq     '0'
     c                   eval      FRREFF = HEREFF
     c                   update    FREETXTR
     c     HEUNIK        reade     ZREETXT2                               33
     c                   enddo
      *
     c     HEUNIK        setll     ZOKUFV2
     c     HEUNIK        reade     ZOKUFV2                                33
     c     *in33         doweq     '0'
     c                   eval      FVREFF = HEREFF
     c                   update    DOKUFVR
     c     HEUNIK        reade     ZOKUFV2                                33
     c                   enddo
      *
     c     HEUNIK        setll     ZOKUFVF2
     c     HEUNIK        reade     ZOKUFVF2                               33
     c     *in33         doweq     '0'
     c                   eval      FFREFF = HEREFF
     c                   update    DOKUFVFR
     c     HEUNIK        reade     ZOKUFVF2                               33
     c                   enddo
      *
     c     HEUNIK        setll     ZOKUFM2
     c     HEUNIK        reade     ZOKUFM2                                33
     c     *in33         doweq     '0'
     c                   eval      FMREFF = HEREFF
     c                   update    DOKUFMR
     c     HEUNIK        reade     ZOKUFM2                                33
     c                   enddo
      *
N01  c     HEUNIK        setll     ZOKUFEL1
N01  c     HEUNIK        reade     ZOKUFEL1                               33
N01  c     *in33         doweq     '0'
N01  c                   eval      FE_REFF = HEREFF
N01  c                   update    DOKUFER
N01  c     HEUNIK        reade     ZOKUFEL1                               33
N01  c                   enddo
      *
     c                   endsr
N02   *************************************************************
N02  C     TTSR          BEGSR
N02   *************************************************************
N02   *
N02  C                   MOVE      HEAVD         TTAVD
N02  C                   MOVE      HEOPD         TTOPD
N02  C                   MOVE      WDT           TTDATE
N02  C                   MOVE      WTM           TTTIME
N02  C                   MOVE      WDT           TTDATL
N02  C                   MOVE      WTM           TTTIML
N02  C                   MOVE      'ED2'         TTACTI
N02  C                   MOVE      WSUSER        TTUSER
N02  C                   EVAL      TTTEXT = 'Incoming EDI recieved'
N02  C                   EVAL      TTTEXL = 'Inkommende EDI mottatt'
N02  C                   MOVE      'WEB'         TTEDEV
N02  C                   MOVE      HEUNIK        TTUNIK
N02  C                   MOVE      HEREFF        TTREFF
N02  C                   WRITE     TRACKFR
N02   *
N02  C                   ENDSR
