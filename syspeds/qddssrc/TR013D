********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10017 * 01 YBC PTF10 SKILLE TET-EKSPORT OG FORHÅNDSVARSLING IMPORT
xxxxxx*xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
11XXX * 02 YBC PTF11 FEIL NAVN
********************************************************************
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPPNLGRP(HLPSCR TR013PNL)
     A                                      HLPTITLE('BEKREFRE SENDING AV TET E-
     A                                      KSPORT')
     A                                      HELP
     A                                      ALTHELP
     A          R TR013D01
     A  27                                  CF04(04 'Søking')
     A                                      CA12(12 'Avbryt uten utskrift')
S01  A*                                 1 26'Bekrefte sending av TET      '
N01  A  28                              1 26'Bekrefte sending av forh.varsel'
N01  A                                      COLOR(WHT)
N01  A N28                              1 26'Bekrefte sending av NCTS     '
     A                                      COLOR(WHT)
     A                                  1 73'TR013R'
     A                                      COLOR(BLU)
     A                                  3  1'Trykk Enter for å bekrefte:'
     A                                      COLOR(BLU)
     A                                  5  3'Avdeling . . . . . . . . :'
     A            UAVD           4Y 0O  5 32EDTCDE(4)
     A                                  6  5'Tolldeklarasjonsnummer :'
     A            UTDN           7Y 0O  6 32EDTCDE(4)
     A                                  7  3'Selgers navn . . . . . . :'
S02  A*           THNAK         30A  O  7 32TEXT('Navn Selger')
N02  A            THNAS         30A  O  7 32TEXT('Navn Selger')
     A                                  8  3'Kjøpers navn . . . . . . :'
S02  A*           TRNAS         30A  O  8 32TEXT('Navn Kjøper')
N02  A            THNAK         30A  O  8 32TEXT('Navn Kjøper')
     A                                  9 48'014=Anmodning om kansellering'
     A                                 10  3'Meldings funksjon  . . . :'
     A            W1N07          3A  O 10 32
     A                                 10 48'015=Deklarasjonsdata'
     A                                 13  3'Ventegrupe . . . . . . . :'
     A            WVEN      R        B 13 32REFFLD(MVEN *LIBL/EDIM)
     A                                      TEXT('Ventekode')
     A                                      DSPATR(UL)
     A                                 13 48'Blank=Sende direkte '
     A                                 14  3'Testekode  . . . . . . . :'
     A            W0035     R        O 14 32REFFLD(M0035 *LIBL/EDIM)
     A                                 14 48'Blank=Produksjon'
     A  27                             16  3'Kanselleringsgrunn . . . :'
     A  27        WSFT1         70A  B 17  7DSPATR(UL)
     A  98                                  DSPATR(RI)
     A  27N97
     AO 98                                  DSPATR(PC)
     A  27        WSFT2         70A  B 18  7DSPATR(UL)
     A  27        WSFT3         70A  B 19  7DSPATR(UL)
     A  27        WSFT4         70A  B 20  7DSPATR(UL)
     A  27        WSFT5         70A  B 21  7DSPATR(UL)
     A  27                             22  3'Språkkode  . . . . . . . :'
     A  27        WSSK           2A  B 22 32DSPATR(UL)
     A  97                                  DSPATR(RI)
     A  97                                  DSPATR(PC)
     A                                 23  2'F12=Avbryt'
     A                                      COLOR(BLU)
     A  27                             23 47'F4=Søk'
     A  27                                  COLOR(BLU)
     A  98                             24  2'Du må oppgi kanselleringsgrunn.'
     A                                      COLOR(WHT)
     A  97                             24  2'Tastet språkkode er ugyldig'
     A                                      COLOR(WHT)
