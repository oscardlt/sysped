     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPDOC('VEDKFRA1' 'HELPNU' 'HLP400')
     A                                      HELP
     A                                      ALTHELP
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  5  4VALUES(' ' '2' '4')
     A            FTTIL          5A  O  5 12TEXT('DESTINASJON TIL')
     A            FTTYPE         2A  O  5 26
     A            FTPRIS         9Y 2O  5 39EDTCDE(4)
     A            FTVAL          3   O  5 57
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0016)
     A                                      SFLPAG(0015)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A            ZRECNO         4S 0O  3 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYSPED/SYSJ33'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(50 DIV1482 SYMSGF)
     A                                      COLOR(WHT)
     A                                  2  2MSGCON(18 DIV1483 SYMSGF)
     A            UFRA           5A  O  2 21COLOR(WHT)
     A                                  2 27MSGCON(18 DIV1484 SYMSGF)
     A            UFRAN          3A  O  2 46COLOR(WHT)
     A                                  2 50MSGCON(18 DIV1485 SYMSGF)
     A            UOPDT          2A  O  2 69COLOR(WHT)
     A                                  2 72'S/K:'
     A            USK            1A  O  2 77COLOR(WHT)
     A                                  3  2MSGCON(30 DIV0016 SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 34MSGCON(30 DIV0017 SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(78 DIV4220 SYMSGF)
     A                                      COLOR(WHT)
      *
     A          R BUNN
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF03(03 'Avslutt program')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                      CF12(12 'Forrige')
     A                                      CF24(24 'FLERE TASTER')
     A                                      SLNO(19)
     A                                      CLRL(06)
     A                                      OVERLAY
     A N31                              5  2MSGCON(18 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A N31                              5 22MSGCON(18 DIV0021 SYMSGF)
     A                                      COLOR(BLU)
     A N31                              5 42MSGCON(18 DIV0022 SYMSGF)
     A                                      COLOR(BLU)
     A  31                              5  2MSGCON(18 DIV0023 SYMSGF)
     A                                      COLOR(BLU)
     A  31                              5 22MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A                                  5 62MSGCON(18 DIV0029 SYMSGF)
     A                                      COLOR(BLU)
     A  98                              6  2MSGCON(78 DIV0012 SYMSGF)
     A                                      COLOR(WHT)
     A*
     A          R VARELI
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF04(04 'SØKING')
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1 11MSGCON(060 DIV4221 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            WSTIL          5A  B  2 12DSPATR(UL)
     A  40                                  DSPATR(PR)
     A  90                                  DSPATR(RI)
     A  40                                  COLOR(WHT)
     A N40                                  COLOR(YLW)
     A            WSTYPE         2A  B  2 26DSPATR(UL)
     A  40                                  DSPATR(PR)
     A  89                                  DSPATR(RI)
     A  40                                  COLOR(WHT)
     A N40                                  COLOR(YLW)
     A            WSPRIS         9Y 2B  2 39DSPATR(UL)
     A  88                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSVAL          3A  B  2 57DSPATR(UL)
     A  87                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                  3  2MSGCON(018 DIV0032 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 22MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  4  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A*
     A          R VAREL2
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                  2  2MSGCON(012 DIV0026 *LIBL/SYMSGF)
     A            W2TIL          5A  I  2 16DSPATR(UL)
     A                                      DSPATR(PC)
     A            W2TYPE         2A  I  2 23DSPATR(UL)
     A                                  3  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A*
     A          R VAREL3
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                  1 11MSGCON(060 DIV4221 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            FTTIL          5A  B  2 12DSPATR(UL)
     A                                      DSPATR(PC)
     A                                      COLOR(WHT)
     A            FTTYPE         2A  B  2 26DSPATR(UL)
     A                                      COLOR(WHT)
     A            FTPRIS         9Y 2B  2 39DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            FTVAL          3A  B  2 57DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  3 62MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A*
     A          R NYVARE
     A                                      CF03(03 'Avslutt program')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                      CF12(12 'Forrige')
     A                                      CF24(24 'FLERE TASTER')
     A                                  1  2'SYSPED/SYSJ33'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(50 DIV1482 SYMSGF)
     A                                      COLOR(WHT)
     A                                  2  2MSGCON(18 DIV1483 SYMSGF)
     A            UFRA           5A  O  2 21COLOR(WHT)
     A                                  2 27MSGCON(18 DIV1484 SYMSGF)
     A            UFRAN          3A  O  2 46COLOR(WHT)
     A                                  2 50MSGCON(18 DIV1485 SYMSGF)
     A            UOPDT          2A  O  2 69COLOR(WHT)
     A                                  2 72'S/K:'
     A            USK            1A  O  2 77COLOR(WHT)
     A                                  3  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 34MSGCON(030 DIV0017 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(078 DIV4220 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A N31                             23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N31                             23 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N31                             23 42MSGCON(018 DIV0022 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  31                             23  2MSGCON(018 DIV0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  31                             23 22MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 62MSGCON(018 DIV0029 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  98                             24  2MSGCON(078 DIV0012 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
