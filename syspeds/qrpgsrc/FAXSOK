      *
      ***********************************************************
      *** PROGRAM FOR ALFABETISK SØKING PÅ KUNDER             ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01 02 04 07 09-11 13-24          ***
      *** LEDIGE INDIKATORER 27-29 31 32 34-39 41-88 95-97    ***
      ***********************************************************
     FFIRM      IF   E           K DISK
     FFAXLOG    IF   E           K DISK
     FFAXLOG01  IF   E           K DISK
     F                                     RENAME(FAXLOGR:FAXL01R)
     FFAXLOG02  UF   E           K DISK
     F                                     RENAME(FAXLOGR:FAXL02R)
     FFAXQLOG   UF   E           K DISK
     FFAXMANL1  UF   E           K DISK
     FHDFMANL1  UF   E           K DISK
     FFAXSOKFM  CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     D                 DS
     D  LOFAXN                 1     21
     D  LOFA15                 1     15
     D                 DS
     D  LODOCN                 1     12
     D  LODOC8                 1      8
     D                 DS
     D  QFORIG                 1     40
     D  PJNAV                  1     10
     D  PUSER                 11     20
     D  PJNBR                 21     26
     D  PSPNA                 27     36
     D  PSPNB                 37     40
     D                 DS
     D  W2ALFA                 1     10
     D  W2ALF8                 1      8
     C*
     C                   EXSR      INIT
     C*
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C  N89USERKE        SETGT     FAXLOG
     C   89DATOKE        SETGT     FAXLOG01
     C     START1        TAG
     C  N89USERKE        READPE    FAXLOG                                 94
     C   89              READP     FAXLOG01                               94
     C   94              MOVE      *BLANKS       WSNR
      *
     C     START2        TAG
      *
     C  N94              DO
     C                   DO        16
     C                   ADD       1             ZRECNO
     C                   MULT(H)   -1            ODDE              1 042
     C*
     C     LOFAXN        IFNE      'MAIL'
     C     LOGKEY        CHAIN(N)  FAXQLOG                            78
     C     LOFSU         IFEQ      ' '
     C   78              MOVE      *BLANKS       QFSU
     C   78              MOVE      '** IN '      QFDATE
     C   78              MOVE      'WORK *'      QFTIME
     C                   ELSE
     C                   MOVEL     LODATE        QFDATE
     C                   MOVEL     LOTIME        QFTIME
     C                   MOVE      LOFSU         QFSU
     C                   END
     C                   ELSE
     C                   MOVE      LOUDAT        QFDATE
     C                   MOVEL     LOTIME        QFTIME
     C     LOTIME        IFEQ      '*CURRENT'
     C                   MOVEL     *BLANKS       QFTIME
     C                   END
     C                   MOVE      LOFSU         QFSU
     C                   END
     C     QFSU          COMP      'U'                                    90
     C                   WRITE     SUBFIL
     C  N89USERKE        READPE    FAXLOG                                 94
     C   89              READP     FAXLOG01                               94
     C  N94              END
     C                   END
     C*
     C                   Z-ADD     ZRECNO        YRECNO            4 0
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYVARE
     C                   ELSE
     C                   WRITE     SUBCTL
     C                   EXFMT     BUNN
     C                   END
     C                   SETOFF                                       9830
     C*
     C*** AVSLUTT
     C   03              GOTO      SLUTT
     C*
     C*
     C* TASTET NYTT ALFABEGREP
     C*
     C     W2ALFA        IFNE      *BLANKS
     C                   SETOFF                                       89
     C     W2ALF8        IFEQ      '*ALL    '
     C                   MOVE      W2ALFA        USER
     C                   SETON                                        89
     C     DATOKE        SETGT     FAXLOG01
     C                   ELSE
     C                   MOVE      W2ALFA        USER
     C     USERKE        SETGT     FAXLOG
     C                   END
     C                   GOTO      START
     C                   END
     C*
     C*
     C*
     C* F5 ER FORNY
     C*
     C   05              DO
     C  N89USERKE        SETGT     FAXLOG
     C   89DATOKE        SETGT     FAXLOG01
     C*
     C*
     C                   GOTO      START
     C                   END
     C*
     C*** BLA VIDERE
     C   25              DO
     C                   Z-ADD     YRECNO        ZRECNO
     C  N89USERKE        READE     FAXLOG                                 94
     C  N89USERKE        READPE    FAXLOG                                 94
     C   89              READ      FAXLOG01                               94
     C   89              READP     FAXLOG01                               94
     C                   SETOFF                                       25
     C                   GOTO      START2
     C                   END
     C*
     C     ZRECNO        IFEQ      0
     C                   GOTO      START3
     C                   ELSE
     C                   EXSR      HENT
     C   98              GOTO      START3
     C                   END
     C*
     C                   GOTO      START
     C*
     C*
     C*
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
     C*
     C***********************************************
     C     HENT          BEGSR
     C***********************************************
     C                   MOVEA     '00'          *IN(91)
     C                   SETOFF                                       9830
     C                   Z-ADD     0             ANTOPT            1 0
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C  N98              ADD       1             ANTOPT
     C* 4 = SLETTING
     C  N98WSNR          IFEQ      '4'
     C     LOUSER        IFNE      UUUSER
     C     UUUSER        IFNE      'QSECOFR '
     C                   MOVE      ' '           WSNR
     C                   UPDATE    SUBFIL
     C                   MOVE      'SHU0234'     MSGNR
     C                   SETON                                        9830
     C                   GOTO      SLHEN2
     C                   END
     C                   END
     C     QFSU          COMP      'U'                                    90
     C                   MOVE      ' '           WSNR
     C                   MOVEL     '** DEL *'    LONAVN
     C                   UPDATE    SUBFIL
     C     LOGKEY        CHAIN     FAXQLOG                            33
     C  N33              DELETE    LOGR
     C     LOGKE2        CHAIN     FAXQLOG                            33
     C  N33              DELETE    LOGR
     C     LOJONR        CHAIN     FAXLOG02                           33
     C  N33              DELETE    FAXL02R
     C     LOJON1        CHAIN     FAXMANL1                           33
     C   33LOJONR        CHAIN     FAXMANL1                           33
     C  N33              DELETE    FAXMANR
     C     LOJONR        CHAIN     HDFMANL1                           33
     C  N33              DELETE    HDFMANR
     C                   MOVEL     LODOCN        LODOC2            2
     C     LODOC2        IFEQ      'F0'
     C                   CALL      'FAXDEL'
     C                   PARM                    LODOCN
     C                   PARM                    LOMAPP
     C                   END
     C                   END
     C* 1 = RESEND
     C  N98WSNR          IFEQ      '1'
     C     QFSU          IFNE      'U'
     C                   MOVE      ' '           WSNR
     C                   UPDATE    SUBFIL
     C                   MOVE      'SHU0221'     MSGNR
     C                   SETON                                        9830
     C                   GOTO      SLHEN2
     C                   END
     C* SLUTT FEIL
     C     LOKODE        IFEQ      'M'
     C     LOJONR        CHAIN     FAXMANL1                           33
     C   33LOJON1        CHAIN     FAXMANL1                           33
     C     LOJONR        CHAIN(N)  HDFMANL1                           34
     C                   END
     C     NYFAX         TAG
     C                   EXFMT     BFAXNR
     C     *IN12         IFEQ      '0'
     C  N33              MOVE      *BLANKS       JOBNR
     C     LOKODE        IFEQ      'M'
     C  N33              UPDATE    FAXMANR
     C                   END
     C     LOFAXN        CABEQ     *BLANKS       NYFAX
     C                   MOVE      '*CURRENT'    PTIME             8
     C                   MOVE      '*CURRENT'    PDATE             8
     C     SETIME        IFNE      *ZEROS
     C     SEDATE        IFNE      *ZEROS
     C                   MOVE      *BLANKS       PTIME
     C                   MOVEL     *BLANKS       PDATE
     C                   MOVEL     SETIME        PTIME
     C                   MOVEL     SEDATE        PDATE
     C                   END
     C                   END
     C* START IKKE M
     C     LOKODE        IFNE      'M'
     C                   CALL      'FAXRETCL'
     C                   PARM                    PJNAV
     C                   PARM                    PUSER
     C                   PARM                    PJNBR
     C                   PARM                    PSPNA
     C                   PARM                    PSPNB
     C                   PARM                    LOFAXN
     C                   PARM                    PDATE
     C                   PARM                    PTIME
     C                   ELSE
     C* 1.SIDES FAX
     C  N33              DO
     C                   MOVE      UNIK          UNIKA             9
     C                   CALL      'FAXR22CL'
     C                   PARM                    UNIKA
     C                   PARM                    LOFAXN
     C                   PARM                    PDATE
     C                   PARM                    PTIME
     C                   PARM                    LONAVN
     C                   PARM                    PUSER
     C                   END
     C* HELPDESK FAX
     C  N34              DO
     C                   MOVE      UNIK          UNIKA             9
     C                   CALL      'HDF022CL'
     C                   PARM                    UNIKA
     C                   PARM                    LOFAXN
     C                   PARM                    PDATE
     C                   PARM                    PTIME
     C                   PARM                    LONAVN
     C                   END
     C                   END
     C* SLUTT M
     C     QFSU          COMP      'U'                                    90
     C                   MOVE      *BLANKS       QFSU
     C                   MOVE      '** IN '      QFDATE
     C                   MOVE      'WORK *'      QFTIME
     C                   MOVE      ' '           WSNR
     C                   UPDATE    SUBFIL
     C* OPPDATERE NYTT FAXNUMMER
     C                   MOVE      LOFAXN        NYFAXN           21
     C     LOJONR        CHAIN     FAXLOG02                           33
     C     LOKODE        IFNE      'M'
     C                   MOVE      NYFAXN        LOFAXN
     C                   MOVE      *BLANKS       LOFSU
     C  N33              UPDATE    FAXL02R
     C                   ELSE
     C  N33              DELETE    FAXL02R
     C                   END
     C     LOGKEY        CHAIN     FAXQLOG                            33
     C  N33              DELETE    LOGR
     C     LOGKE2        CHAIN     FAXQLOG                            33
     C  N33              DELETE    LOGR
     C                   ELSE
     C                   MOVE      ' '           WSNR
     C                   UPDATE    SUBFIL
     C                   END
     C                   END
     C*
     C* COPIER MANUELL FAX TIL ANDRE  MED 3
     C*
     C  N98WSNR          IFEQ      '3'
     C     LOKODE        IFEQ      'M'
     C                   MOVE      'C'           KODEM             1
     C                   MOVE      LOUDAT        ÆDATO6            6 0
     C                   CALL      'FAX021'
     C                   PARM                    LOJONR
     C                   PARM                    ÆDATO6
     C                   PARM                    KODEM
     C                   MOVE      KODEM         FINS
     C     QFSU          COMP      'U'                                    90
     C                   MOVE      ' '           WSNR
     C                   UPDATE    SUBFIL
     C                   GOTO      SLHEN2
     C                   END
     C                   END
     C*
     C*
     C* SE PÅ FAX
     C*
     C  N98WSNR          IFEQ      '5'
     C     LODOC8        IFEQ      'FAKTURA '
     C                   MOVE      ' '           WSNR
     C                   UPDATE    SUBFIL
     C                   MOVE      'SHU0402'     MSGNR
     C                   SETON                                        9830
     C                   GOTO      SLHEN2
     C                   END
     C     LOKODE        IFNE      'O'
     C     LOKODE        IFNE      'M'
     C     LOKODE        IFNE      'D'
     C     LOKODE        IFNE      'F'
     C                   MOVE      ' '           WSNR
     C                   UPDATE    SUBFIL
     C                   MOVE      'SHU0236'     MSGNR
     C                   SETON                                        9830
     C                   GOTO      SLHEN2
     C                   END
     C                   END
     C                   END
     C                   END
     C                   MOVE      'J'           FINS
     C     LOKODE        IFEQ      'O'
     C     LOKODE        OREQ      'D'
     C                   CALL      'FAXDSP'
     C                   PARM                    LODOCN
     C                   PARM                    LOMAPP
     C                   PARM                    FINS              1
     C                   MOVE      ' '           WSNR
     C                   ELSE
     C     LOKODE        IFEQ      'M'
     C                   MOVE      'S'           KODEM             1
     C                   MOVE      LOUDAT        ÆDATO6            6 0
     C                   CALL      'FAX021'
     C                   PARM                    LOJONR
     C                   PARM                    ÆDATO6
     C                   PARM                    KODEM
     C                   MOVE      KODEM         FINS
     C                   ELSE
     C     LOKODE        IFEQ      'F'
     C                   Z-ADD     LOTUR         XXFBN
     C     XXFBN         IFNE      *ZEROS
     C                   CALL      'SYFM21C'
     C                   PARM                    FIFIRM
     C                   PARM                    LOAVD
     C                   PARM                    LOOPD
     C                   PARM                    XXFBN             3 0
     C                   PARM                    UFLAGG
     C                   ELSE
     C                   MOVE      'N'           FINS
     C                   END
     C                   END
     C*
     C                   END
     C                   END
     C     QFSU          COMP      'U'                                    90
     C                   UPDATE    SUBFIL
     C     FINS          IFNE      'J'
     C                   MOVE      'SHU0225'     MSGNR
     C                   SETON                                        9830
     C                   GOTO      SLHEN2
     C                   END
     C                   END
     C*
     C* 6 = PRINT
     C*
     C  N98WSNR          IFEQ      '6'
     C     LOKODE        IFNE      'M'
     C                   MOVE      ' '           WSNR
     C                   UPDATE    SUBFIL
     C                   MOVE      'SHU0398'     MSGNR
     C                   SETON                                        9830
     C                   GOTO      SLHEN2
     C                   END
     C                   EXSR      SKRIV
     C     QFSU          COMP      'U'                                    90
     C                   MOVE      ' '           WSNR
     C                   UPDATE    SUBFIL
     C                   GOTO      SLHEN2
     C                   END
     C*
     C* 8 = SE OPPDRAG
     C*
     C  N98WSNR          IFEQ      '8'
     C     LOKODE        IFNE      'O'
     C     LOKODE        ANDNE     'F'
     C                   MOVE      'SHU0222'     MSGNR
     C                   SETON                                          9830
     C                   GOTO      SLHEN2
     C                   END
     C                   MOVE      *ZEROS        UFLAGG            1 0
     C                   CALL      'SYFM21A'
     C                   PARM                    FIFIRM
     C                   PARM                    LOAVD
     C                   PARM                    LOOPD
     C                   PARM                    UFLAGG
     C                   MOVE      ' '           WSNR
     C     QFSU          COMP      'U'                                    90
     C                   UPDATE    SUBFIL
     C                   END
     C                   END
     C*
     C     SLHENT        TAG
     C   98ANTOPT        IFEQ      0
     C                   MOVE      'DIV0012'     MSGNR
     C                   SETON                                            30
     C                   END
     C*
     C     SLHEN2        ENDSR
     C*
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     *ENTRY        PLIST
     C                   PARM                    UUUSER           10
     C*
     C                   MOVE      UUUSER        USER
     C     LOJON1        KLIST
     C                   KFLD                    LOJONR
     C                   KFLD                    LOUDAT
     C     USERKE        KLIST
     C                   KFLD                    USER
     C     DATOKE        KLIST
     C                   KFLD                    UUUDAT            8 0
     C                   Z-ADD     99999999      UUUDAT
     C     LOGKEY        KLIST
     C                   KFLD                    LOJONR
     C                   KFLD                    FASTKO            2
     C     LOGKE2        KLIST
     C                   KFLD                    LOJONR
     C                   KFLD                    FASTK2            2
     C                   MOVE      '01'          FASTK2
     C                   MOVE      '00'          FASTKO
     C                   READ      FIRM                                   33
     C                   ENDSR
     C*
     C*
     C*
     C***********************************************
     C     SKRIV         BEGSR
     C***********************************************
     C                   EXFMT     BSKRIV
     C*                    //////////
     C     *IN12         IFEQ      *OFF
     C     LOJONR        CHAIN(N)  FAXMANL1                           88
     C                   MOVE      UNIK          UNIKA             9
     C                   CALL      'FAX022C'
     C                   PARM                    UNIKA
     C                   PARM                    DEV
     C                   PARM                    HOLD
     C                   MOVE      'SHU0399'     MSGNR
     C                   MOVE      *ON           *IN30
     C                   ENDIF                                                  <- *IN12
     C                   MOVE      *OFF          *IN12
      *
      *
     C                   ENDSR
