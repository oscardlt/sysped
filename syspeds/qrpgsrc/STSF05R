      *********************************************************************
      *  After compiling this RPG MODULE,
      *  create the related program with the following command:
      *
CRTPG+*  CRTPGM SYSPED/STSF05R
CRTPG+*         MODULE(*LIBL/STSF05R *LIBL/STSF05RB *LIBL/INCGI)
CRTPGM*         ACTGRP(*NEW) AUT(*USE)
      *
      *
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
      *--------------------------------------------------------------------
      * Data base files
      *--------------------------------------------------------------------
     FBRIDF     if   e           k disk    usropn
     Fstsopd    uf   e           K DISK    usropn
     Fstsopd3   if   e           K DISK    usropn
     F                                     RENAME(STSOPDR:STSOPDR3)
     Fstscli    if   e           K DISK    usropn
     Fheadf     if   e           k disk    usropn
      *--------------------------------------------------------------------
      * Prototype defintions and standard system API error structure
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      *--------------------------------------------------------------------
      * Variables common to CGI programs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
     D OddEven         s             10
     D RowHead         s             10a   inz('#B0E0E6')
     D Odd             s             10a   inz('#F8F8FF')
     D Even            s             10a   inz('#E6E6FA')
      * Variables received from the input string
     DwsPro            s              8  0
     DTURNR            s              8
     Dpallv            s              1
     DSlett            s              1
     DAvd              s              4
     DWsAvd            s              4  0
     DOpd              s              7
     DWsOpd            s              7  0
     D quit            s              4
     D UserId          s             10
     D User            s             10
     D VisAlle         s              1
     D SEMI            s              1
     D Sort            s              1
      * andre variabler
     D Fn              s             10
     D Lib             s             10
     D Mbr             s             10
     D GmAvOp          s             10
     D antclk          s              5  0
     D antclm          s              5  0
     D CliFinnes       s              1
      *
     D                 DS
     D  HXSNDN                 1     20
     D  Sndnr1                 1     10
     D  Sndnr2                11     20
     D                 DS
     D  Henas                  1     30
     D  xxnas                  1      6
     D                 DS
     D  Henak                  1     30
     D  xxnak                  1     10
     D                 DS
     D  Headk3                 1     30
     D  xxadk3                 1     10
     D                 DS
     D  XDT                    1      8  0
     D  XDTÅÅ                  1      4  0
     D  XDTMM                  5      6  0
     D  XDTDD                  7      8  0
     D                 DS
     D  XTIME                  1     14  0
     D  XTID                   1      4  0
     D  XDD                    7      8  0
     D  XMM                    9     10  0
     D  XÅÅ                   11     14  0
      *
     D Spaces          s             12a   inz('&nbsp;&nbsp;')
      *--------------------------------------------------------------------
      * Prolog common to CGI programs
      * -Receive input from the remote browser
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prolog3
      *=====================================================================******
      * MAIN PROCESS LINE
      *=====================================================================******
      * Parse input string
     C                   eval      TURNR    = zhbgetvar('TURNR')
     C                   eval      WSPRO    = c2n2(TURNR)
     C                   eval      PALLV    = zhbgetvar('PALLV')
     C                   eval      YTYPE    = zhbgetvar('YTYPE')
     C                   eval      VisAlle  = zhbgetvar('Alle')
     C                   eval      userid   = zhbgetvar('user')
     C                   eval      SEMI    = zhbgetvar('SEMI')
     C                   eval      Slett    = zhbgetvar('Slett')
     C                   eval      AVD      = zhbgetvar('AVD')
     C                   eval      WSAVD    = c2n2(AVD)
     C                   eval      OPD      = zhbgetvar('OPD')
     C                   eval      WSOPD    = c2n2(OPD)
     C                   eval      Sort     = zhbgetvar('SORT')
      * Set libl/open files
     C                   exsr      Init
      * Set output skeleton html member name
     C                   exsr      SetSkl
      * Read skeleton output html, etc.
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
      *------------------
      * Write sections of HTML.
      *
      * Hent/vis sendinger
     C                   exsr      HSEND
      *
      *
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      *
     C                   close     BRIDF
     C                   close     stsopd
     C                   close     stsopd3
     C                   close     stscli
     C                   close     headf
     C                   eval      *inlr = *on
     C                   RETURN
      *
     C***********************************************
     C     HSEND         BEGSR
     C***********************************************
      * Repeat UserId for the next invocation
BODY C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updhtmlvar('USER':userid)
     C                   callp     updhtmlvar('TURNR':TURNR)
     C                   callp     updhtmlvar('YTYPE':YTYPE)
     C                   callp     updhtmlvar('PALLV':PALLV)
     C                   callp     updhtmlvar('ALLE':VisAlle)
     C                   callp     updHTMLvar('ROWHEAD':RowHead)
      *
     C                   callp     wrtsection('top')
      * tur 1
     C                   exsr      HSEND2
      *
     C                   callp     wrtsection('tablebot')
      *
     C                   ENDSR
      *
     C***********************************************
     C     HSEND2        BEGSR
     C***********************************************
      *
     c     Sort          ifeq      *blanks
     C     TurKey        SETLL     stsopd
     C     TurKey        READE(N)  stsopd                                 94
     c                   else
     c                   exsr      fixsort
     C     TurKey        SETLL     stsopd3
     C     TurKey        READE     stsopd3                                94
     c                   end
     C     *IN94         DOWEQ     '0'
     C     VisAlle       Ifne      'J'
     C     SOSTAT        Andne     ' '
     C                   goto      NEXT
     C                   end
     c     heakey        chain     headf                              33
     c     sndnr2        Ifeq      *blanks
     C                   callp     updhtmlvar('OpID':Sndnr1)
     c                   else
     C                   callp     updhtmlvar('OpID':Sndnr2)
     c                   end
     C     sostat        ifeq      *blanks
     C     SEMI          andne     'Y'
     C                   callp     updhtmlvar('STAT':spaces)
     C                   else
     C                   callp     updhtmlvar('STAT':SOSTAT)
     C                   end
     C                   callp     updhtmlvar('ANT':
     C                             %trim(%editc(HENT:'3')))
     C                   callp     updhtmlvar('TURNR':
     C                             %trim(%editc(sopro:'3')))
     C                   callp     updhtmlvar('AVD':
     C                             %trim(%editc(soavd:'3')))
     C                   callp     updhtmlvar('OPD':
     C                             %trim(%editc(soopd:'3')))
     C                   callp     updhtmlvar('TKAVS':henas)
     C                   callp     updhtmlvar('TKMOT':henak)
     C                   callp     updhtmlvar('TKMAD':headk1)
     C                   callp     updhtmlvar('TKMPNST':headk3)
     C                   callp     updhtmlvar('HXSNDN':HXSNDN)
      *
     C                   callp     updhtmlvar('AVS':xxnas)
     C                   callp     updhtmlvar('MOT':xxnak)
     C                   callp     updhtmlvar('PNR':xxadk3)
      * ant. clid lest
     c                   Move      *zeros        antclk
     c                   Move      *zeros        antclm
     C                   Move      'N'           clifinnes
     C     cliKey        SETLL     stscli
     C     cliKey        READE     stscli                                 34
     C  N34              Move      'J'           clifinnes
     C     *IN34         DOWEQ     '0'
     c     Scstat        ifne      ' '
     c                   add       1             antclk
     c                   end
     C     cliKey        READE     stscli                                 34
     C  N34              ENDDO
     c     CliFinnes     ifeq      'J'
     C     HENT          sub       antclk        antclm
     c     Antclm        iflt      *zeros
     C                   move      *zeros        antclm
     c                   end
     c                   end
      * I manko vises blank/ferdig-status når CLI ikke finnes.
     c     CliFinnes     ifne      'J'
     C     sostat        ifeq      *blanks
     C     SEMI          andne     'Y'
     C                   callp     updhtmlvar('ANTM':spaces)
     C                   else
     C                   callp     updhtmlvar('ANTM':SOSTAT)
     C                   end
      * Når CLI finnes vises reelt manko-tall ( 0 = ferdig lest...)
     c                   else
     C                   callp     updhtmlvar('ANTM':
     C                             %trim(%editc(antclm:'3')))
     c                   end
      *
     c     OddEven       IFEQ      ODD
     c                   eval      OddEven = EVEN
     c                   else
     c                   eval      OddEven = ODD
     c                   end
     C                   callp     updHTMLvar('ODDEVEN':OddEven)
     C                   callp     wrtsection('tablerow')
     C     Next          tag
     c     Sort          ifeq      *blanks
     C     TurKey        READE(N)  stsopd                                 94
     c                   else
     C     TurKey        READE     stsopd3                                94
     c                   end
     C  N94              ENDDO
      *
     C                   ENDSR
      *
      *=====================================================================******
      * FIX SORTERINGSBEGREP I STSOPD3
      *=====================================================================******
     C     FixSort       begsr
     C     TurKey        SETLL     stsopd
     C     TurKey        READE     stsopd                                 94
     C     *IN94         DOWEQ     '0'
     c     heakey        chain     headf                              33
     c                   movel     HENAK         Mot25             5
     c                   movel     HEADK3        Pnr5              5
     C                   select
     C     Sort          wheneq    'M'
     c                   eval      SOSORT = Mot25 + Pnr5
     C     Sort          wheneq    'P'
     c                   eval      SOSORT = Pnr5  + Mot25
     C     Sort          wheneq    'A'
     c                   movel     HENAS         Avs10            10
     c                   movel     HENAK         Mot15            15
     c                   eval      SOSORT = Avs10+ Mot15 + Pnr5
     c                   other
     c                   movel     heavd         avdopd           11
     c                   move      heopd         avdopd
     c                   movel     avdopd        SOSORT
     c                   endsl
     C                   update    stsopdr
     C     TurKey        READE     stsopd                                 94
     C                   END
      *
     C                   ENDSR
      *
      *=====================================================================******
      * Set html output skeleton member before its read into memory
      *=====================================================================******
     C     SetSkl        begsr
      *------------------
      * Set html output skeleton member
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC'
     C                   eval      Mbr = 'STSF05'
     C     SEMI          IFEQ      'Y'
     C                   eval      Mbr = 'STSF05T'
     C                   end
      *------------------
     C                   endsr
      *=====================================================================******
      * Set Keys mm
      *=====================================================================******
     C     init          begsr
      *=====================================================================******
      *
     C                   open      BRIDF
     C     UserID        CHAIN     BRIDF                              30
      * --------------------------------------
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C* (bedre ressursmessig). MODUL må da være med comp. av dette pgm!!
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
      * Kall eksternt prog for å utf. evt sletting før liste vises.
     C     Slett         IFEQ      'J'
     C                   callb     'STSF05RB'
     C                   parm                    YTYPE
     C                   parm                    wspro
     C                   parm                    wsavd
     C                   parm                    wsopd
     C                   end
     C                   open      stsopd
     C                   open      stsopd3
     C                   open      stscli
     C                   open      headf
      *
      *  keys mm
     C     *LIKE         DEFINE    SOTYPE        YTYPE
     C     TURKEY        KLIST
     C                   KFLD                    YTYPE
     C                   KFLD                    WSPRO
     C     CLIKEY        KLIST
     C                   KFLD                    sotype
     C                   KFLD                    sopro
     C                   KFLD                    soavd
     C                   KFLD                    soopd
     C     HEAKEY        KLIST
     C                   KFLD                    soavd
     C                   KFLD                    soopd
     C                   endsr
