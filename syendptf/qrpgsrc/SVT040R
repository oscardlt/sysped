     FSVT040D   CF   E             WORKSTN
     F                                     SFILE(SVT040DA:ZRECNO)
     FSVTX04L1  UF A E           K DISK
     FSVTX10F   UF A E           K DISK
      *//////////////
      *  Hodelogikk /
      *//////////////
     C                   EXSR      SRA
     C                   EXSR      SRB
     C                   MOVE      '1'           *INLR
      *////////////////////////////////////////////////////////////
      *  Initiering                                           SRA /
      *////////////////////////////////////////////////////////////
     C     SRA           BEGSR
     C     *LIKE         DEFINE    ZRECNO        WRECNO
     C     *LIKE         DEFINE    SVTX04_01     WSVTX04_01
     C     *LIKE         DEFINE    SVTX04_02     WSVTX04_02
     C     *LIKE         DEFINE    SVTX04_02     XSVTX04_02
     C     *LIKE         DEFINE    *IN03         WIN03
      *__________________
      * Definiere Nøkkler
      *""""""""""""""""""
     C     WKEY          KLIST
     C                   KFLD                    WSVTX04_01
     C                   KFLD                    WSVTX04_02
     C     ZKEY          KLIST
     C                   KFLD                    ZSVTX04_01
     C                   KFLD                    ZSVTX04_02
     C     XKEY          KLIST
     C                   KFLD                    XSVTX04_02
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Bearbeide Formater                                   SRB /
      *////////////////////////////////////////////////////////////
     C     SRB           BEGSR

     C                   EVAL      ZSVTX04_01='I'
     C                   EVAL      ZSVTX04_02=*BLANKS

     C     SRB1          TAG
      *______________
      * Tømme Subfile
      *""""""""""""""
     C                   MOVE      '1'           *IN93
     C                   WRITE     SVT040DB
     C                   MOVEA     '000'         *IN(93)
     C                   Z-ADD     *ZERO         ZRECNO
     C                   Z-ADD     *ZERO         WRECNO
     C                   MOVE      *BLANKS       WSVTX04_01
     C                   MOVE      *BLANKS       WSVTX04_02
     C                   MOVEA     '000'         *IN(61)
     C                   IF        ZSVTX04_01=*BLANKS
     C                   EVAL      ZSVTX04_01='I'
     C                   ENDIF
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     ZKEY          SETLL     SVTX04L1
     C     ZSVTX04_01    READE(N)  SVTX04L1                               94

     C     *IN94         DOWEQ     '0'
     C                   IF        ZRECNO > 999
     C                   LEAVE
     C                   ENDIF

     C                   MOVEL     SVTX04_02     XSVTX04_02
     C                   IF        SVTX04_01='E'
     C                   MOVE      '00'          XSVTX04_02
     C                   ENDIF
     C                   EVAL      WSVTX10_31=*BLANKS
     C     XKEY          CHAIN(N)  SVTX10F
     C                   IF        %FOUND
     C                   MOVEL     SVTX10_31     WSVTX10_31
     C                   ENDIF

     C                   ADD       1             ZRECNO
     C                   Z-ADD     ZRECNO        WRECNO
     C                   WRITE     SVT040DA

     C     ZSVTX04_01    READE(N)  SVTX04L1                               94
     C  N94              END

     C     ZRECNO        IFEQ      0
     C                   MOVE      ' '           SVTX04_01
     C                   MOVE      ' '           SVTX04_02
     C                   ADD       1             ZRECNO
     C                   WRITE     SVT040DA
     C                   ENDIF
     C                   Z-ADD     1             ZRECNO
      *_______________
      * Behandle bilde
      *"""""""""""""""
     C     SRB3          TAG

     C                   MOVEA     '11'          *IN(91)
     C                   MOVE      *BLANKS       ZSVTX04_01
     C                   MOVE      *BLANKS       ZSVTX04_02
     C  N30              MOVE      'SY00000'     ZMSGNO
     C                   WRITE     SVT040DØ
     C                   WRITE     SVT040DC
     C                   EXFMT     SVT040DB

     C     *IN03         IFEQ      '0'
     C     *IN12         ANDEQ     '0'

     C                   MOVEA     '00'          *IN(91)
      *_____________
      * F5=Frisk opp
      *"""""""""""""
     C     *IN05         CABEQ     '1'           SRB1
      *___________
      * F6=Lage ny
      *"""""""""""
     C     *IN06         IFEQ      '1'
     C                   EXSR      SRBA
     C                   EXSR      SRBA2
     C     *IN03         CABEQ     '1'           UT
     C                   GOTO      SRB3
     C                   END
      *_______
      * Valg ?
      *"""""""
     C                   MOVE      '0'           *IN52
     C                   MOVE      '0'           *IN95
     C     *IN52         DOWEQ     '0'

     C                   READC     SVT040DA                               52
     C     *IN52         IFEQ      '0'
     C     ZOP           CASEQ     '2'           SRBB                           Endre
     C     ZOP           CASEQ     '3'           SRBC                           Kopiere
     C     ZOP           CASEQ     '4'           SRBD                           Slette
     C     ZOP           CASEQ     '5'           SRBE                           Vise
N05  C     ZOP           CASEQ     '6'           SRB6                           Avvikende mappe
     C                   END
     C                   END
      *
     C  N52              END
      *
     C     ZSVTX04_01    CABNE     *BLANKS       SRB1
     C     ZSVTX04_02    CABNE     *BLANKS       SRB1
     C     *IN03         CABEQ     '1'           ut
     C     *IN95         CABEQ     '1'           SRB1
     C     *IN95         CABEQ     '0'           SRB3

     C                   END

     C     UT            ENDSR
      *////////////////////////////////////////////////////////////
      *  Lage ny                                             SRBA /
      *////////////////////////////////////////////////////////////
     C     SRBA          BEGSR
      *____________
      * Blanke felt
      *""""""""""""
     C                   MOVEA     '0000'        *IN(30)
     C                   CLEAR                   SVTX04R

     C     SRBA1         TAG

     C  N30              MOVE      'SY00000'     ZMSGNO
     C                   WRITE     SVT040DØ
     C                   EXFMT     SVT040DD
     C                   MOVEA     '0000'        *IN(30)
     C     *IN03         IFEQ      '0'
     C     *IN12         ANDEQ     '0'
      *____________
      * Teste input-1
      *""""""""""""
      * Post må ikke finnes fra før.
     C                   MOVE      ÅSVTX04_01    WSVTX04_01
     C                   MOVE      ÅSVTX04_02    WSVTX04_02
     C     WKEY          CHAIN(N)  SVTX04L1                           53
     C     *IN53         IFEQ      '0'
     C                   MOVE      'SY00100'     ZMSGNO
     C                   MOVEA     '1110'        *IN(30)
     C*                  GOTO      SRBA1
     C                   ENDIF
     C                   MOVEL     SVTX04_02     XSVTX04_02
     C                   IF        SVTX04_01='E'
     C                   MOVE      '00'          XSVTX04_02
     C                   ENDIF

     C     XKEY          CHAIN     SVTX10F
     C                   IF        %FOUND
     C                   MOVEL     SVTX10_31     ÅSVTX10_31
     C                   ELSE
     C                   EVAL      ÅSVTX10_31=*BLANKS
     C                   ENDIF
     C                   EVAL      ÅSVTX04_04='1972-01-01'
     C                   EVAL      ÅSVTX04_03='9999-12-31'

     C                   ENDIF

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Lage ny - 2                                        SRBA2 /
      *////////////////////////////////////////////////////////////
     C     SRBA2         BEGSR
      *____________
      * Blanke felt
      *""""""""""""
     C                   MOVE      '0'           *IN41
     C                   MOVE      '0'           *IN42
     C                   MOVEA     '0000'        *IN(30)
     C                   CLEAR                   SVTX04R

     C     SRBA21        TAG

     C  N30              MOVE      'SY00000'     ZMSGNO
     C                   WRITE     SVT040DØ
     C                   EXFMT     SVT040DE
     C                   MOVEA     '0000'        *IN(30)
     C     *IN03         IFEQ      '0'
     C     *IN12         ANDEQ     '0'
      *____________
      * Teste input-1
      *""""""""""""
      * Post må ikke finnes fra før.
     C                   MOVE      ÅSVTX04_01    WSVTX04_01
     C                   MOVE      ÅSVTX04_02    WSVTX04_02
     C     WKEY          CHAIN(N)  SVTX04L1                           53
     C     *IN53         IFEQ      '0'
     C                   MOVE      'SY00100'     ZMSGNO
     C                   MOVEA     '1110'        *IN(30)
     C*                  GOTO      SRBA21
     C                   ENDIF
      *___________________
      * Adda post til  FIL
      *"""""""""""""""""""
     C                   MOVE      ÅSVTX04_01    SVTX04_01
     C                   MOVE      ÅSVTX04_02    SVTX04_02
     C                   MOVE      ÅSVTX04_03    SVTX04_03
     C                   MOVE      ÅSVTX04_04    SVTX04_04
     C                   WRITE     SVTX04R

     C                   MOVEL     SVTX04_02     XSVTX04_02
     C                   IF        SVTX04_01='E'
     C                   MOVE      '00'          XSVTX04_02
     C                   ENDIF
     C     XKEY          CHAIN     SVTX10F
     C                   IF        %FOUND
     C                   MOVEL     ÅSVTX10_31    SVTX10_31
     C                   UPDATE    SVTX10R
     C                   ELSE
     C                   MOVEL     XSVTX04_02    SVTX10_01
     C                   MOVEL     ÅSVTX10_31    SVTX10_31
     C                   WRITE     SVTX10R
     C                   ENDIF

      *______________________
      * Adda post til  SUBFIL
      *""""""""""""""""""""""
     C                   ADD       1             WRECNO
     C                   Z-ADD     WRECNO        ZRECNO
     C                   EVAL      *IN60=*OFF
     C                   WRITE     SVT040DA

N05  C                   CALL      'SVT041R'
N05  C                   PARM                    SVTX04_01
N05  C                   PARM                    SVTX04_02
N05  C                   PARM                    SVTX10_31

     C                   ENDIF

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Endre                                               SRBB /
      *////////////////////////////////////////////////////////////
     C     SRBB          BEGSR
      *_________________
      * Oppdatere SUBFIL
      *"""""""""""""""""
     C                   MOVE      *BLANKS       ZOP
     C                   UPDATE    SVT040DA
      *____________
      * Flytte felt
      *""""""""""""
     C                   MOVE      SVTX04_01     WSVTX04_01
     C                   MOVE      SVTX04_02     WSVTX04_02
     C     WKEY          CHAIN(N)  SVTX04L1                           53
     C                   MOVE      '1'           *IN41
     C                   MOVE      '0'           *IN42
     C                   MOVEA     '0000'        *IN(30)
     C                   MOVE      SVTX04_01     ÅSVTX04_01
     C                   MOVE      SVTX04_02     ÅSVTX04_02
     C                   MOVE      SVTX04_03     ÅSVTX04_03
     C                   MOVE      SVTX04_04     ÅSVTX04_04

     C                   MOVEL     SVTX04_02     XSVTX04_02
     C                   IF        SVTX04_01='E'
     C                   MOVE      '00'          XSVTX04_02
     C                   ENDIF
     C                   EVAL      WSVTX10_31=*BLANKS
     C     XKEY          CHAIN(N)  SVTX10F
     C                   IF        %FOUND
     C                   MOVEL     SVTX10_31     ÅSVTX10_31
     C                   ENDIF


     C     SRBB1         TAG

     C  N30              MOVE      'SY00000'     ZMSGNO
     C                   WRITE     SVT040DØ
     C                   EXFMT     SVT040DE
     C                   MOVEA     '000'         *IN(30)
     C     *IN03         IFEQ      '0'
     C     *IN12         IFEQ      '0'
      *____________
      * Teste input
      *""""""""""""
     C   30              GOTO      SRBB1
      *______________
      * Oppdatere FIL
      *""""""""""""""
     C                   MOVE      ÅSVTX04_01    WSVTX04_01
     C                   MOVE      ÅSVTX04_02    WSVTX04_02
     C     WKEY          CHAIN     SVTX04L1                           53
     C                   MOVE      ÅSVTX04_03    SVTX04_03
     C                   MOVE      ÅSVTX04_04    SVTX04_04
     C                   UPDATE    SVTX04R

     C                   MOVEL     SVTX04_02     XSVTX04_02
     C                   IF        SVTX04_01='E'
     C                   MOVE      '00'          XSVTX04_02
     C                   ENDIF
     C     XKEY          CHAIN     SVTX10F
     C                   IF        %FOUND
     C                   MOVEL     ÅSVTX10_31    SVTX10_31
     C                   UPDATE    SVTX10R
     C                   ELSE
     C                   MOVEL     XSVTX04_02    SVTX10_01
     C                   MOVEL     ÅSVTX10_31    SVTX10_31
     C                   WRITE     SVTX10R
     C                   ENDIF

N05  C                   CALL      'SVT041R'
N05  C                   PARM                    SVTX04_01
N05  C                   PARM                    SVTX04_02
N05  C                   PARM                    SVTX10_31

     C                   ENDIF
     C                   ENDIF
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Kopiere                                             SRBC /
      *////////////////////////////////////////////////////////////
     C     SRBC          BEGSR
      *____________
      * Flytte felt
      *""""""""""""
     C                   MOVE      SVTX04_01     WSVTX04_01
     C                   MOVE      SVTX04_02     WSVTX04_02
     C     WKEY          CHAIN(N)  SVTX04L1                           53
     C                   MOVE      SVTX04_01     ÅSVTX04_01
     C                   MOVE      SVTX04_02     ÅSVTX04_02
     C                   MOVE      SVTX04_03     ÅSVTX04_03
     C                   MOVE      SVTX04_04     ÅSVTX04_04
      *_________________
      * Oppdatere SUBFIL
      *"""""""""""""""""
     C                   MOVE      *BLANKS       ZOP
     C                   MOVE      *BLANKS       ZSVTX04_01
     C                   MOVE      *BLANKS       ZSVTX04_02
     C                   UPDATE    SVT040DA
      *____________________________
      * Kalle på rutine for Lage Ny
      *""""""""""""""""""""""""""""
     C                   EXSR      SRBA
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Slette                                              SRBD /
      *////////////////////////////////////////////////////////////
     C     SRBD          BEGSR
      *______________
      * Oppdatere FIL
      *""""""""""""""
     C                   MOVE      SVTX04_01     WSVTX04_01
     C                   MOVE      SVTX04_02     WSVTX04_02
     C     WKEY          CHAIN     SVTX04L1                           53
     C                   DELETE    SVTX04R
      *_________________
      * Oppdatere SUBFIL
      *"""""""""""""""""
     C                   MOVE      *BLANKS       ZOP
     C                   MOVE      *BLANKS       ZSVTX04_01
     C                   MOVE      *BLANKS       ZSVTX04_02
     C                   UPDATE    SVT040DA
     C                   MOVE      '1'           *IN95
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Vise                                                SRBE /
      *////////////////////////////////////////////////////////////
     C     SRBE          BEGSR
      *____________
      * Flytte felt
      *""""""""""""
     C                   MOVE      '1'           *IN41
     C                   MOVE      '1'           *IN42
     C                   MOVEA     '0000'        *IN(30)
     C                   MOVE      SVTX04_01     WSVTX04_01
     C                   MOVE      SVTX04_02     WSVTX04_02
     C     WKEY          CHAIN(N)  SVTX04L1                           53
     C                   MOVE      SVTX04_01     ÅSVTX04_01
     C                   MOVE      SVTX04_02     ÅSVTX04_02
     C                   MOVE      SVTX04_03     ÅSVTX04_03
     C                   MOVE      SVTX04_04     ÅSVTX04_04

     C                   MOVEL     SVTX04_02     XSVTX04_02
     C                   IF        SVTX04_01='E'
     C                   MOVE      '00'          XSVTX04_02
     C                   ENDIF
     C                   EVAL      WSVTX10_31=*BLANKS
     C     XKEY          CHAIN(N)  SVTX10F
     C                   IF        %FOUND
     C                   MOVEL     SVTX10_31     ÅSVTX10_31
     C                   ENDIF

     C  N30              MOVE      'SY00000'     ZMSGNO
     C                   WRITE     SVT040DØ
     C                   EXFMT     SVT040DE
     C                   MOVEA     '000'         *IN(30)
      *_________________
      * Oppdatere SUBFIL
      *"""""""""""""""""
     C                   MOVE      *BLANKS       ZOP
     C                   MOVE      *BLANKS       ZSVTX04_01
     C                   MOVE      *BLANKS       ZSVTX04_02
     C                   UPDATE    SVT040DA

N05  C                   CALL      'SVT041R'
N05  C                   PARM                    SVTX04_01
N05  C                   PARM                    SVTX04_02
N05  C                   PARM                    ÅSVTX10_31
     C                   ENDSR
N05   *////////////////////////////////////////////////////////////
N05   *  Avvikende mappe                                     SRB6 /
N05   *////////////////////////////////////////////////////////////
N05  C     SRB6          BEGSR
N05  C                   CALL      'SVT041R'
N05  C                   PARM                    SVTX04_01
N05  C                   PARM                    SVTX04_02
N05  C                   PARM                    WSVTX10_31
N05   *_________________
N05   * Oppdatere SUBFIL
N05   *"""""""""""""""""
     C                   MOVE      *BLANKS       ZOP
     C                   MOVE      *BLANKS       ZSVTX04_01
     C                   MOVE      *BLANKS       ZSVTX04_02
N05  C                   UPDATE    SVT040DA
N05  C                   MOVE      '1'           *IN95
N05
N05  C                   ENDSR
