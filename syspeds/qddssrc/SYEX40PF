     A*01 IKKE FASTE LINJENUMMER **************************************
     A*02 78 ER FAX              **************************************
     A*****************************************************************
     A*   KJØREKVITTERING/OPPDRAGSFORMULAR                            *
     A* ANTALL LINJE=72,  ANTALL TEGN PR. LINJE=80, OVERFLOW=72       *
     A*****************************************************************
     A          R KJØPNYS                   SPACEB(2)
     A                                    50MSGCON(10 YBC0492 SYMSGF)
     A          R KJØP
NV5  A            KFMT1         75A     1  1
NV5  A            KFMT2         75A     1  1
     A            KOPXXX        35A     1  1
     A            HOHT1         80A     1  1
     A            HOHT2         80A     2  1
     A            HOHT3         80A     3  1
     A            HOHT4         80A     4  1
     A            HOHT5         80A     5  1
     A            HOHT6         80A     6  1
     A            HOHT7         80A     7  1
     A                                 09  2MSGCON(9 DIV0200 SYMSGF)
     A N77        KNAVN         30     10 13
     A  77        HENAS         30     10 13
     A N26                             10 54'************************'
     A  26                             10 54MSGCON(15 YBC0493 SYMSGF)
     A  26        XSIDE          3  0  10 70
     A  26                             10 75'***'
     A N77        ADR1          30     11 13
     A  77        HEADS1        30     11 13
     A                                 11 54MSGCON(24 PRI0023 SYMSGF)
     A N77        ADR2          30     12 13
     A  77        HEADS2        30     12 13
     A                                 12 54'************************'
     A N77        POSTNR         4  0  13 13
     A N77        ADR3          24     13 18
     A  77        HEADS3        30     13 13
     A                                 15 54MSGCON(8 DIV0199 SYMSGF)
     A            HEKNSF         8  0  15 69
     A                                 16  2MSGCON(9 DIV0197 SYMSGF)
     A                                 16 54MSGCON(5 DIV0062 SYMSGF)
     A            UDT1           2  0  16 69
     A                                 16 71'.'
     A            UDT2           2  0  16 72
     A                                 16 74'.'
     A            UDT3           2  0  16 75
     A            HENAK         30     17 13
     A            HEADK1        30     18 13
     A                                 18 54MSGCON(7 DIV0201 SYMSGF)
     A            HEAVD          4  0  18 62
     A                                 18 66'/'
     A            HEOPD          7  0  18 67
     A                                 18 74'/'
     A            HESG           3     18 75
     A            HEADK2        30     19 13
     A                                 19 54MSGCON(15 DIV0205 SYMSGF)
     A            HEPRO          8  0  19 69
     A            HEADK3        30     20 13
     A                                 21 54MSGCON(15 DIV0209 SYMSGF)
     A            HEGN          15A    21 63
     A                                 21  2MSGCON(9 PRI0021 SYMSGF)
     A            HESDL         20     21 13
     A                                 22  2MSGCON(9 DIV0208 SYMSGF)
     A            HEFR           3     22 13
     A                                 22 54MSGCON(9 DIV0206 SYMSGF)
     A            HEHAWB         9     22 69
     A                                 23  2MSGCON(7 DIV0084 SYMSGF)
     A            HEKNA          8  0  23 13EDTCDE(Z)
     A            HENAA         30     23 22
     A                                 24  1MSGCON(77 DIV1679 SYMSGF)
     A N78                                  UNDERLINE
     A            HENT           7  0  25  1EDTCDE(4)
     A            HEVS1         25A    25  9HIGHLIGHT
     A            HEVKT          9  0  25 35EDTCDE(4)
     A            HEM3           7  3  25 46EDTCDE(4)
     A            HEGM1         15A    25 66
     A  27        HEVS2         25A    26  9
     A                                 26 48'LM:'
     A            HELM           4  2  26 56EDTCDE(4)
     A  27        HEGM2         15A    26 66
     A                                 27  1'________________________'
     A                                 27 25'________________________'
     A                                 27 49'________________________'
     A                                 27 73'_______ '
     A                                 29  2' '
     A
     A          R KJØPFS
     A            KFSOTX        30         2
     A                                    32':'
     A            FSSOK         35        35SPACEA(1)
     A          R KJØP3                     SPACEB(1)
     A                                     2MSGCON(24 YBC0491 SYMSGF)
     A                                      HIGHLIGHT
     A            HEFT1A        30        30HIGHLIGHT
     A            HEFT1B        15        61HIGHLIGHT
     A          R KJØP40                    SPACEB(1)
     A                                     2MSGCON(24 DIV0215 SYMSGF)
     A          R KJØP4                     SPACEB(1)
     A            FRTTXT        70         2
     A          R KJØP5                     SPACEB(1)
     A                                     2MSGCON(24 PRI0022 SYMSGF)
     A            HENAS         30        13SPACEB(1)
     A            HEADS1        30        13SPACEB(1)
     A            HEADS2        30        13SPACEB(1)
     A            HEADS3        30        13SPACEB(1)
     A                                    13MSGCON(64 DIV0217 SYMSGF)
     A                                      SPACEB(1)
     A          R KJØPB                     SPACEB(3) HIGHLIGHT
     A                                    17MSGCON(64 DIV0217 SYMSGF)
N01  A  55                                 1' '
N01  A                                      SPACEB(4)
N01  A  55        SKANBAR       13        30BARCODE(CODE128 4 *HRITOP *HRZ +
N01  A                                      (*WIDTH 0.018) (*RATIO 3) X'02')
