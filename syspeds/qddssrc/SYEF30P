      *_____________________________________________
      * Format for hovedopplysninger manifest
      *"""""""""""""""""""""""""""""""""""""""""""""
     A          R LHEADH1
     A            KFMT1         75      1  1
     A          R LHEADH2
     A            KFMT2         75      1  1
     A          R LHEAD
     A  29        HOHT1         80      2  1
     A  29        HOHT2         80      3  1
     A  29        HOHT3         80      4  1
     A  29        HOHT4         80      5  1
     A  29        HOHT5         80      6  1
     A  29        HOHT6         80      7  1
     A  29        HOHT7         80      8  1
     A            KNAVN         30     11  9
     A                                 11 44'************************-
     A                                      ***********'
     A            ADR1          30     12  9
     A N27N28                          12 44'* M A N I F E S T / I N -
     A                                      V O I C E *'
     A  28                             12 44'* PRØVEUTSKRIFT   / ONLY-
     A                                         TEST   *'
     A  27                             12 44'*  M   A   N   I   F   E-
     A                                         S   T  *'
     A            ADR2          30     13  9
     A                                 13 44'************************-
     A                                      ***********'
     A N88        POSTNR         4  0  14  9
     A N88        ADR3          24     14 15
     A  88        ADR3A         24     14  9
     A                                 14 44'DATE:'
     A                                 14 62DATE EDTCDE(Y)
     A            KJLAND        30     15  9
     A                                 15 44'MANIFEST NO:'
     A            USD            3     15 64
     A            ULNR           3  0  15 67
     A                                 16 44'MAWB:'
     A            XFSN           3     16 58
     A            XGN8           8     16 62
     A                                 18 44'Customer no:'
     A            KUNDNR         8  0  18 62EDTCDE(Z)
     A N27N28                          19 44'Date of due:'
     A N27N28     XFFDT          6  0  19 69EDTCDE(Y)
     A                                 20  2'From:'
     A            SMSDF          3     20  8
     A                                 20 13'To:'
     A            SMSDA          3     20 17
     A                                 20 31'Routing:'
     A            SMROB1         6     20 41
     A                                 20 47'/'
     A            SMROC1         2  0  20 48
     A  71        SMROB2         6     20 51
     A  71                             20 57'/'
     A  71        SMROC2         2  0  20 58
     A  72        SMROB3         6     20 61
     A  72                             20 67'/'
     A  72        SMROC3         2  0  20 68
     A                                 21  2'__________________________________-
     A                                      ___________________________________-
     A                                      __________'
S01  A*N27                             22  2'Hawbno Dest   Pcs  Kiloes  Descrip-
N01  A N27                             22  2'Hawbno        Pcs  Kiloes  Descrip-
S01  A*                                     tion              Dest         CC-a-
N01  A                                      tion                           CC-a-
     A                                      mount     '
     A N77                                  UNDERLINE
S01  A* 27                             22  2'Hawbno Dest   Pcs  Kiloes  Descrip-
     A  27                             22  2'Hawbno        Pcs  Kiloes  Descrip-
s01  A*                                     tion                               -
N01  A                                      tion              Dest             -
     A                                                '
     A N77                                  UNDERLINE
     A                                 24  2' '
     A          R LDET
S01  A*           XHAWB          6         2
N01  A            SMSDF          3         2
S02  A*           XHAWB          6         5
N02  A            HEHAWB         9         5
S01  A*           XTIL2          3         9
     A            XNT            6  0     13EDTCDE(Z)
     A            XVKT           8  0     20EDTCDE(Z)
     A            HEVS1         25        29
N01  A            XTIL2          3        55
     A N27        XKDUA         13  2     58EDTCDE(J)
     A            HEVALK         3        77SPACEA(2)
     A                                     2'Shipper:'
     A                                    17'Phone:'
     A            DFTLFS        15        24SPACEA(1)
     A            HENAS         30        17
     A            HEADS1        30        49SPACEA(1)
     A            HEADS2        30        17
     A            HEADS3        30        49SPACEA(1)
     A                                     2'Consignee:'
     A                                    17'Phone:'
     A            DFTLFK        15        24SPACEA(1)
     A            HENAK         30        17
     A            HEADK1        30        49SPACEA(1)
     A            HEADK2        30        17
     A            HEADK3        30        49SPACEA(1)
     A                                     2'Remarks:'
     A            DFINF1        35        11
     A            DFINF2        35        46SPACEA(1)
     A                                     2'__________________________________-
     A                                      ___________________________________-
     A                                      __________'
     A                                      SPACEA(2)
     A          R LTOT0
     A                                     5'TOTALS:'
     A            XTANT          7  0     13EDTCDE(Z)
     A            XTVKT          9  0     20EDTCDE(Z)
     A                                      SPACEA(1)
     A  27                                 2' '
     A                                      SPACEA(1)
     A          R LTOT
     A N27                                34'Invoice  no:'
     A N27        SUFANR         7  0     47EDTCDE(Z)
     A N27        SUVALB        13  2     58EDTCDE(J)
     A N27        SUVAL          3        77SPACEA(1)
     A          R LTOT9
     A                                     5'==================================-
     A                                      ===================================-
     A                                      ======='
