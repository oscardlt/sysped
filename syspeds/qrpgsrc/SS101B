      *********************************************************************
      * VIS ORDRE SOM ER I PRODUKSJON
      * PROGRAM BEHANDLER INPUT FRA SS102
      *
      * OBS! LAG KUN MODUL (OPT 15) AV SS101B IKKE PROGRAM.
      *     (INNGÅR SOM MODUL I PGM SS101, og SS102 )
      *
      *********************************************************************
      * MAIN PROGRAM FLOW
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
     FGSSORL09  IF   E           K DISK    USROPN
     FGSSPOF    IF   E           K DISK    USROPN
     FGSSRFF    IF   E           K DISK    USROPN
     FGSSWW1F   IF A E           K DISK    USROPN
     FCUNDL01   IF   E           K DISK    USROPN
     FFIRM      IF   E           K DISK    USROPN
     FBRPARF    IF   E           K DISK    USROPN
     FBRIDF     IF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Prototype defintions and standard system API error structure
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      *--------------------------------------------------------------------
      * Variables common to CGI programs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
      * Variables received from the input string
     D lng             s              2
     D UsrSpcName      s             10
     D WSUSER          S             10
     D WSNA            S             30
     D WSST2           S              5
     D XXST2           S              5
     D WSKN            S              8
     D XXKN            S              8  0
     D WSPOF           S             20
     D lstbrd          s              1
      * Variables received from the input string
      * and converted to numeric
     D zeros           s             15a   inz('000000000000000')
     D NIER            s             20A   INZ('99999999999999999999')
     D l1              s              3s 0
     D l2              s              3s 0
      * Number of records matching search criteria
     DRecordCnt        s              5S 0
      *
     D Lib             s             10
     D Fn              s             10
     D Mbr             s             10
      *
     D Spaces          s             12a   inz('&nbsp;&nbsp;')
      *--------------------------------------------------------------------
      * Prolog common to CGI programs
      * -Receive input from the remote browser
      *--------------------------------------------------------------------
      */copy syspeds/qrpgsrcpy,prolog3
      *=====================================================================******
      * MAIN PROCESS LINE
      *=====================================================================******
      * Get program start time for calculating execution time
     C                   time                    timedata1
      *
     C                   EXSR      INIT
      * Set output skeleton html member name
     C                   exsr      SetSkl
      * Read skeleton output html, etc.
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
      * Retrieve environment variables
     C                   exsr      RtvEnvVar
      *------------------                                                   ******
      * Write HTML sections
      *
      * 1-Section /$top
     C                   exsr      SetTop
     C                   callp     wrtsection('top')
      *------------------                                                   ******
      * Go to the last Recd already shown (1. time = from top)
     C                   exsr      GotoRecord
      *------------------                                                   ******
      * Write HTML sections
      * 2a-Section /$0found
     C     XRRN          IFEQ      9999999
     C                   callp     wrtsection('0found')
     C                   exsr      DspKund
     C                   goto      endhtml
     C                   endif
      * 3-Section /$tablestr
     C                   exsr      SetTabStr
     C                   callp     wrtsection('tablestr')
      * Kunder -pulldown
     C                   exsr      DspKund
      *
     C                   IF        WSST2 = 'IPR'
     C                   callp     wrtsection('tablestrRP')
     C                   ENDIF
     C                   callp     wrtsection('tablestr2')
      *
      * 4-Sections /$tablerow
     C     GSSOR9K       SETLL     GSSORL09
     C     GSSOR9K       READE     GSSORL09                               50
      *
     C     *IN50         DOWEQ     '0'
     C     ORTNO         IFEQ      0
     C     XXKN          IFNE      0
     C     XXKN          ANDNE     ORCNO
     C                   GOTO      STNESTE
     C                   END
     C     ORONO         SETLL     GSSPOF
     C     ORONO         READE     GSSPOF                                 33
     C   33              MOVE      *BLANKS       POCONO
     C     ORONO         SETLL     GSSRFF
     C     ORONO         READE     GSSRFF                                 33
     C     *IN33         DOWEQ     '0'
     C     RFKD          IFEQ      'CPO'
     C                   LEAVE
     C                   ENDIF
     C     ORONO         READE     GSSRFF                                 33
     C  N33              ENDDO
     C   33              MOVE      *BLANKS       RFREF
     C     WSPOF         IFGT      RFREF
     C                   GOTO      STNESTE
     C                   ENDIF
     C     CUNDL1K       CHAIN     CUNDL01                            33
     C   33              EVAL      KNAVN = '*UNKNOW'
     C                   WRITE     GSSWW1FR
     C                   ENDIF
     C     STNESTE       TAG
     C     GSSOR9K       READE     GSSORL09                               50
     C  N50              ENDDO
      *
     C                   z-add     0             bseq              3 0
     C     *LOVAL        SETLL     GSSWW1F
     C                   READ      GSSWW1F                                50
      *
     C     *IN50         DOWEQ     '0'
     C                   eval      bseq=bseq + 1
     C                   exsr      SetTabRow
     C                   callp     wrtsection('tablerow')
     C                   IF        WSST2 = 'IPR'
     C                   callp     wrtsection('tablerowRP')
     C                   ENDIF
     C                   callp     wrtsection('tablerow2')
     C                   READ      GSSWW1F                                50
     C  N50              ENDDO
      *
      * 5-Section /$tableend
     C                   callp     wrtsection('tableend')
      *
      * 5a-Section /$foundsome
     C                   exsr      SetFndSome
     C                   callp     wrtsection('foundsome')
      * 6-Section /$endhtml
     C     endhtml       tag
     C                   exsr      SetEnd
     C     SLUTT         tag
     C                   callp     wrtsection('endhtml')
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
     C                   EXSR      EXIT
     C                   return
      *=====================================================================******
      *  Set output variables for section /$top
      *  (search criteria)
      *=====================================================================******
     C     SetTop        begsr
      * Repeat national language for the next invocation
     C                   callp     updhtmlvar('LNG':lng:
     C                             InitHTMLVars)
      * Color for text, background, links, visited links, active links
     C                   callp     updhtmlvar('TXTCOLOR':'black')
     C                   callp     updhtmlvar('BOLD':' ')
     C                   callp     updhtmlvar('BGCOLOR':BIFARG)
     C                   callp     updhtmlvar('LINK':'0000FF')
     C                   callp     updhtmlvar('VLINK':'FF0000')
     C                   callp     updhtmlvar('ALINK':'00FF00')
      * KUNDE
     C                   callp     updhtmlvar('WSNA':WSNA)
BODY C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updhtmlvar('USER':WSUSER)
     C                   callp     updHtmlVar('UsrSpcName':UsrSpcName)
     C                   callp     updHTMLvar('sdato':
     C                             %trim(%editw(BIDTV:'    .  .  ')))
     C                   callp     updHTMLvar('stid':
     C                             %trim(%editw(BITIDV:'  .  .  ')))
     C                   callp     updhtmlvar('XXST2':XXST2)
     C                   IF        WSST2 = 'IPR'
     C                   callp     updhtmlvar('WSST2':'     ')
     C                   ELSE
     C                   callp     updhtmlvar('WSST2':WSST2)
     C                   ENDIF
     C                   IF        XXST2 = *BLANKS
     C                   callp     updhtmlvar('SWITCH':'Mark ready')
     C                   callp     updhtmlvar('ReadyMark':'Not ready')
     C                   ELSE
     C                   callp     updhtmlvar('SWITCH':'Mark not ready')
     C                   callp     updhtmlvar('ReadyMark':'Ready')
     C                   ENDIF
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$tablestr (table start)
      *=====================================================================******
     C     SetTabStr     begsr
     C     lstbrd        ifeq      'Y'
     C                   callp     updhtmlvar('LB':'0')
     C                   else
     C                   callp     updhtmlvar('LB':'1')
     C                   endif
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$tablestrKU
      *=====================================================================******
     C     SetTabStrKU   begsr
     C     lstbrd        ifeq      'Y'
     C                   callp     updhtmlvar('LB':'0')
     C                   else
     C                   callp     updhtmlvar('LB':'1')
     C                   endif
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$tablerow (table row)
      *=====================================================================******
     C     SetTabRow     begsr
     c* Sequence of this Recd (numeric, converted to char)
     C                   move      bseq          bseqchar          3
     C                   callp     updhtmlvar('SEQ':bseqchar)
     C                   callp     updhtmlvar('KNAVN':KNAVN)
     C                   callp     updhtmlvar('ORONO':
     C                             %trim(%editc(ORONO:'Z')))
     C                   callp     updhtmlvar('POCONO':POCONO)
     C                   callp     updhtmlvar('RFREF':RFREF)
     C                   callp     updhtmlvar('WSDTL':
     C                             %trim(%editw(ORDTL:'    .  .  ')))
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$foundsome (some Recds were found)
      *=====================================================================******
     C     SetFndSome    begsr
      * Repeat background color
     C                   callp     updhtmlvar('BCKGND':bckgnd)
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$endhtml
      *=====================================================================******
     C     SetEnd        begsr
      * Server protocol
     C                   callp     updhtmlvar('PROTOCOL':S_Protocol)
      * Compute run time
     C                   time                    timedata2
     C     timedata2     subdur    timedata1     ms:*ms
     C                   eval      sec = ms / 1000000
     C                   callp     updhtmlvar('runtime':
     C                             %trim(%editw(sec:'     0 .   ')))
      *
     C                   endsr
      *=====================================================================******
     C*  Go to the last Recd aleady shown
      *=====================================================================******
     C     GotoRecord    begsr
     C     GSSOR9K       SETLL     GSSORL09
     C     GSSOR9K       READE     GSSORL09                               50
     C                   DOW       *IN50 = *OFF
     C                   IF        ORTNO = 0
     C                   IF        ((XXKN  = 0) OR (XXKN = ORCNO))
     C     ORONO         SETLL     GSSRFF
     C     ORONO         READE     GSSRFF                                 33
     C     *IN33         DOWEQ     '0'
     C     RFKD          IFEQ      'CPO'
     C                   LEAVE
     C                   ENDIF
     C     ORONO         READE     GSSRFF                                 33
     C  N33              ENDDO
     C   33              MOVE      *BLANKS       RFREF
     C     WSPOF         IFLE      RFREF
     C                   LEAVE
     C                   ENDIF
     C                   ENDIF
     C                   ENDIF
     C     GSSOR9K       READE     GSSORL09                               50
     C  N50              ENDDO
     C  N50              MOVE      *ZEROS        XRRN              7 0
     C   50              MOVE      999999999     XRRN              7 0
      *
     C                   endsr
      *=====================================================================******
      * Set html output skeleton member before its read into memory
      *=====================================================================******
     C     SetSkl        begsr
      *------------------
      * Set html output skeleton member
     C                   eval      lng = uppify(lng)
      * "HTMLSRC  " er dft = NORSK, ved å trykke på FLAGGET i inng.
      * kan dette endre stil "HTMLSRCXX" avh. av støttede språk
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC' + lng
     C                   eval      Mbr = 'SS101'
      *------------------
     C                   endsr
      *=====================================================================******
      *  Retrieve environment variables
      *=====================================================================******
     C     RtvEnvVar     begsr
      * Use getenvp to get this server's protocol and name
     C                   eval      S_Protocol =getenv('SERVER_PROTOCOL':
     C                             qusec)
     C                   eval      S_Name     =getenv('SERVER_NAME':
     C                             qusec)
      *
     C                   endsr
      *=====================================================================******
      *  Display Kunder        to select from
      *=====================================================================******
     C     DspKund       begsr
      *
     C                   MOVE      *blanks       PAKUNR
     C                   MOVE      'SSCUS'       PAID
     C     BrParKey      setll     BRPARF
     C     BrParKey      reade     BRPARF                                 40
     C     *in40         doweq     '0'
     C                   exsr      SetleRow
     C     XRRN          IFEQ      9999999
     C                   callp     wrtsection('tablerowKU2')
     C                   ELSE
     C                   callp     wrtsection('tablerowKU')
     C                   ENDIF
     C     BrParKey      reade     BRPARF                                 40
     C                   enddo
      *
     C                   callp     updhtmlvar('WSPOF':WSPOF)
     C     XRRN          IFEQ      9999999
     C                   callp     wrtsection('tableendKU2')
     C                   ELSE
     C                   callp     wrtsection('tableendKU')
     C                   ENDIF
      *
     C                   endsr
      *=====================================================================******
      *  Set variable data for section /$typsltrow
      *=====================================================================******
     C     SetleRow      begsr
     C                   Z-ADD     PAVRDN        KUNRN             8 0
     C                   callp     updhtmlvar('KUNDNR':
     C                             %trim(%editc(KUNRN:'Z')))
     C                   callp     updhtmlvar('KNAVN':PAVRDA)
      * Når kundenr alt er valgt (inp.param.) vis posten også i pulldown
     C     XXKN          IFNE      *ZEROS
     C     XXKN          ANDEQ     KUNRN
     C                   callp     updhtmlvar('KSEL':'SELECTED')
     c                   else
     C                   callp     updhtmlvar('KSEL':spaces)
     c                   end
     C                   endsr
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
     C     *ENTRY        PLIST
     C                   PARM                    WSNA             30
     C                   PARM                    WSUSER           10
     C                   PARM                    UsrSpcName
     C                   PARM                    WSST2             5
     C                   PARM                    BckGnd           10
     C                   PARM                    WSKN              8
     C                   PARM                    WSPOF            20
      *
     C                   OPEN      BRIDF
     C                   OPEN      BRPARF
     C                   open      GSSORL09                             50
     C                   open      GSSPOF                               50
     C                   open      GSSRFF                               50
     C                   open      CUNDL01                              50
     C                   open      FIRM                                 50
      *
     C     GSSOR9K       klist
     C                   kfld                    XXLC
     C                   kfld                    WSST2
     C     CUNDL1K       klist
     C                   kfld                    FIFIRM
     C                   kfld                    ORCNO
     C     BrParKey      klist
     C                   kfld                    WSUSER
     C                   kfld                    PAKUNR
     C                   kfld                    PAID
      *
     C     WSUSER        CHAIN     BRIDF                              50
     C     WSUSER        SETLL     BRPARF
     C     WSUSER        READE     BRPARF                                 50
     C                   DOW       *IN50 = *OFF
     C                   IF        PAID = 'SSFAB'
     C                   MOVEL     PAVRDA        XXLC              5
     C                   LEAVE
     C                   ENDIF
     C     WSUSER        READE     BRPARF                                 50
     C  N50              ENDDO
     C     *LOVAL        SETLL     FIRM
     C                   READ      FIRM                                   50
      *
     C                   EVAL      XXFIL = 'GSSWW1F'
     C                   callb     'SS101C'
     C                   PARM                    FILIBF
     C                   PARM                    XXFIL            10
     C                   open      GSSWW1F                              50
      *
     C                   EVAL      XXKN    = c2n2(WSKN)
     C                   IF        WSST2   = *BLANKS
     C                   EVAL      WSST2   = 'IPR'
     C                   EVAL      XXST2   = 'RFP'
     C                   ELSE
     C                   EVAL      XXST2   = *BLANKS
     C                   END
      *
     C                   endsr
      *=====================================================================******
      *  AVSLUTT
      *=====================================================================******
     C     EXIT          begsr
     C                   close     BRIDF
     C                   close     BRPARF
     C                   close     GSSORL09
     C                   close     GSSPOF
     C                   close     GSSRFF
     C                   close     GSSWW1F
     C                   close     CUNDL01
     C                   close     FIRM
     C                   eval      *inlr = *on
      *
     C                   endsr
