      *
      ***********************************************************
      *** PROGRAM SKAL VEDLIKEHOLDE BILAGSTEKSTER KOSTNAD     ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      ***********************************************************
      *
     FKODTKSB   UF A E           K DISK
     FKODTKSBL  IF   E           K DISK
     F                                     RENAME(KOKSBR:KOKSBLR)
     FSYKS52FM  CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D                 DS
     D  KOKSTX                 1     35
     D  KOSFN7                 1      7
     C*
     C     *ENTRY        PLIST
     C                   PARM                    UPICK             1
     C                   PARM                    UTXT             35
     C     UPICK         COMP      'Y'                                    55
      * STARTVERDI FOR ALFA??
     C                   MOVE      UTXT          W2KSTX
     C*
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
     C                   MOVE      W2KSKO        NEXTKO            3
     C                   MOVE      W2KSTX        NEXTTX           35
     C     W2KSTX        COMP      *BLANKS                            5050
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C  N50W2KSKO        SETLL     KODTKSB
     C  N50              READ(N)   KODTKSB                                94
     C   50W2KSTX        SETLL     KODTKSBL
     C   50              READ      KODTKSBL                               94
      *
     C     START2        TAG
      *
     C  N94              DO
     C                   DO        14
     C                   ADD       1             ZRECNO
     C                   WRITE     SUBFIL
     C  N50              READ(N)   KODTKSB                                94
     C   50              READ      KODTKSBL                               94
     C  N94              MOVE      KOKSKO        NEXTKO
     C  N94              MOVE      KOKSTX        NEXTTX
     C  N94              END
     C                   END
     C*
     C                   Z-ADD     ZRECNO        YRECNO            5 0
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     5             POSNBR
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYWRK
     C                   ELSE
     C                   WRITE     SUBCTL
     C                   EXFMT     BUNN
     C                   END
     C*
     C***
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C                   Z-ADD     WSSUBN        XRECNO            4 0
     C     XRECNO        IFEQ      0
     C                   Z-ADD     ZRECNO        XRECNO            4 0
     C                   END
     C***
     C*
     C                   SETOFF                                       9899
     C*
     C*** AVSLUTT
     C   03              GOTO      SLUTT
     C*
     C*** FRISK OPP
     C   05              GOTO      START
     C*
     C*** LAGE NY
     C   06              DO
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     10            POSNBR
     C                   MOVE      *BLANKS       WSKSKO
     C                   GOTO      START5
     C                   END
     C*** SØK FRA
     C   08              GOTO      START6
     C*
     C*** BLA VIDERE
     C   25              DO
     C                   SETOFF                                       25
     C                   Z-ADD     YRECNO        ZRECNO
     C  N94
     CANN50NEXTKO        SETLL     KODTKSB
     C  N94
     CANN50              READ(N)   KODTKSB                                94
     C  N94
     CAN 50NEXTTX        SETLL     KODTKSBL
     C  N94
     CAN 50              READ      KODTKSBL                               94
     C                   GOTO      START2
     C                   END
     C*
     C     ZRECNO        IFNE      0
     C                   EXSR      HENT
     C                   END
     C   98XRECNO        IFNE      0
     C                   Z-ADD     XRECNO        ZRECNO
     C                   END
     C                   GOTO      START3
     C*
     C*** LAGE NY
     C     START5        TAG
     C                   EXFMT     VARELI
     C                   SETOFF                                       309089
     C   12              EXSR      MOVB2
     C  N12              EXSR      TESTB1
     C  N12
     CAN 30              GOTO      START5
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     5             POSNBR
     C                   GOTO      START3
     C*
     C*** SØK FRA
     C     START6        TAG
     C                   EXFMT     VAREL2
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     5             POSNBR
     C   12              GOTO      START3
     C                   GOTO      START
     C*
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
     C*
     C***********************************************
     C     TESTB1        BEGSR
     C***********************************************
     C  N40              DO
     C     WSKSKO        COMP      *BLANKS                                90
     C   90              MOVE      'JOV0391'     MSGNR
     C  N90WSKSKO        CHAIN(N)  KODTKSB                            33
     C  N90
     CANN33              DO
     C                   SETON                                        90
     C                   MOVE      'JOV0393'     MSGNR
     C                   END
     C   90              Z-ADD     10            POSNBR
     C   90              SETON                                        30
     C                   END
     C*
     C  N30WSKSTX        COMP      *BLANKS                                89
     C   89
     CANN30              MOVE      'JOV0392'     MSGNR
     C   89
     CANN30              Z-ADD     15            POSNBR
     C   89              SETON                                        30
     C*
     C   30              GOTO      SLTB
     C*
     C*** OPPDATERE PÅ REGISTER OG SUBFIL
     C     WSKSKO        CHAIN     KODTKSB                            33
     C                   EXSR      MOVWSF
     C  N33              UPDATE    KOKSBR
     C   33              WRITE     KOKSBR
     C   40              UPDATE    SUBFIL
     C  N40              ADD       1             YRECNO
     C  N40              Z-ADD     YRECNO        ZRECNO
     C  N40              WRITE     SUBFIL
     C                   WRITE     SUBCTL
     C                   EXSR      MOVB2
     C*
     C     SLTB          ENDSR
     C*
     C***********************************************
     C     HENT          BEGSR
     C***********************************************
     C                   Z-ADD     0             LINNBR
     C                   SETOFF                                       919299
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C   98              DO
     C   99              SETOFF                                       98
     C                   Z-ADD     1             LINNBR
     C                   GOTO      SLHENT
     C                   END
     C     ZRECNO        DIV       14            LINNBR
     C                   MVR                     XLIN              2 0
     C     XLIN          IFEQ      0
     C                   Z-ADD     14            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C   55WSNR          IFEQ      '1'                                          ENDRE
     C                   MOVE      ' '           WSNR
     C     KOSFN7        COMP      '*DELETE'                              98
     C   98              GOTO      SLHENT
     C                   MOVE      KOKSTX        UTXT
     C                   MOVE      *ON           *INLR
     C                   RETURN
     C                   END
     C     WSNR          IFEQ      '2'                                          ENDRE
     C                   MOVE      ' '           WSNR
     C     KOSFN7        COMP      '*DELETE'                              98
     C   98              GOTO      SLHENT
     C                   EXSR      CHGSF
     C   12              GOTO      SLHENT
     C                   END
     C     WSNR          IFEQ      '4'                                          SLETTE
     C                   MOVE      ' '           WSNR
     C     KOSFN7        COMP      '*DELETE'                              98
     C   98              GOTO      SLHENT
     C                   EXSR      DLTSF
     C   12              GOTO      SLHENT
     C                   END
     C                   END
     C*
     C     SLHENT        TAG
     C                   ADD       5             LINNBR
     C                   Z-ADD     5             POSNBR
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     DLTSF         BEGSR
     C***********************************************
     C                   SETON                                        99
     C                   MOVE      KOKSKO        WSKSKO
     C                   EXFMT     VAREL3
     C  N12              DO
     C     WSKSKO        CHAIN     KODTKSB                            33
     C  N33              DELETE    KOKSBR
     C                   EXSR      MOVB1
     C                   END
     C     XLIN          IFEQ      0
     C                   Z-ADD     14            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C                   UPDATE    SUBFIL
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     CHGSF         BEGSR
     C***********************************************
     C                   EXSR      MOVSFW
     C                   SETON                                        99
     C     STCHG         TAG
     C                   EXFMT     VARELI
     C                   SETOFF                                       309089
     C   12              DO
     C                   UPDATE    SUBFIL
     C                   EXSR      MOVB2
     C                   END
     C  N12              EXSR      TESTB1
     C  N12
     CAN 30              GOTO      STCHG
     C     XLIN          IFEQ      0
     C                   Z-ADD     14            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVB1         BEGSR
     C***********************************************
     C                   MOVE      *BLANKS       KOKSKO
     C                   MOVE      *BLANKS       KOKSTX
     C                   MOVEL     '*DELETE'     KOSFN7
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVB2         BEGSR
     C***********************************************
     C                   SETOFF                                       40
     C                   MOVE      *BLANKS       WSKSKO
     C                   MOVE      *BLANKS       WSKSTX
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVSFW        BEGSR
     C***********************************************
     C                   MOVE      KOKSKO        WSKSKO
     C                   MOVE      KOKSTX        WSKSTX
     C                   SETON                                        40
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     15            POSNBR
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVWSF        BEGSR
     C***********************************************
     C   33              DO
     C                   MOVE      WSKSKO        KOKSKO
     C                   END
     C*
     C                   MOVE      WSKSTX        KOKSTX
     C*
     C                   ENDSR
     C*
