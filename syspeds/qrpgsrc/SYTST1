      *********************************************************************
      *  RPG ILE MODULE CGIDEV2/sytst1
      *
CRTPG+*  CRTPGM SYSPED/sytst1 MODULE(*LIBL/sytst1
CRTPGM*         *LIBL/INCGI) ACTGRP(CGI) AUT(*USE)
      *
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FHEADF     IF   E           K DISK    USROPN
     FHEAD17    IF   E           K DISK    USROPN
     F                                     RENAME(HEADFR:HEAF17R)
     FBRIDF     IF   E           K DISK    USROPN
      *********************************************************************
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      * Number of variables
     DnbrVars          s             10i 0
      *
      *
      * Saved query string
     Dsavedquerystring...
     D                 s          32767    varying
      *
      * Client input variables
     D cgiinp01        s             15
     D cgiinp02        s              4
     D cgiinp03        s              7
     D user            s             10
     D EditCode        s             50
      * Indicators for chknbr subprocedure
     D ChkNbrInds      ds
     D  Indicators                     n   dim(7)
      *
      * Constant for updHTMLvar subprocedure
     D initHTMLVars    c                   '0'
     D                 DS
     D  XXTAB1                 1      7
     D  XXTAB2                 1      7    DIM(7)
      *
      * Return code
     D rc              s             10i 0 inz(0)                               return code
      * Get input
     C                   eval      nbrVars =
     C                             zhbgetinput(savedquerystring:qusec)
      *
      * Parse variables from QUERY_STRING environment variable
      * Søkeref
     C                   eval      cgiinp01 = zhbgetvar('cgiinp01')
      * Avd..
     C                   eval      cgiinp02 = zhbgetvar('cgiinp02')
      * Oppd..
     C                   eval      cgiinp03 = zhbgetvar('cgiinp03')
      * Userid.....
     C                   eval      user = zhbgetvar('user')
      *
     C                   EXSR      INIT
      * Read output skeleton html member into memory
     C                   exsr      LoadHtml
      * Set section variables
     C                   exsr      SetAll
      * Send section
     C                   callp     wrtsection('all')
      * Quit
     C                   exsr      Exit
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
     C                   OPEN      BRIDF
     C     USER          CHAIN     BRIDF                              50
     C                   CALLB     'INCGI'
     C                   PARM                    BISTDL
      *
     c                   open      head17
     C                   open      HEADF
     c     opdkey        klist
     c                   kfld                    heavd
     c                   kfld                    heopd
      *
     C                   endsr
      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   CLOSE     HEADF
     C                   CLOSE     HEAD17
     C                   CLOSE     BRIDF
     C                   return
     C                   endsr
      *=====================================================================
      * Read output skeleton html member into memory
      *=====================================================================
     C     LoadHtml      begsr
     C                   callp     gethtml('HTMLSRC':
     C                             '*LIBL':
     C                             'SYTST1':'/CGITAG/')
     C                   endsr
      *=====================================================================
      *  Set variable data for section /$all
      *=====================================================================
     C     SetAll        begsr
     C     cgiinp01      ifne      *blanks
     C     cgiinp01      chain     head17                             33
     C                   else
     C     cgiinp02      ifne      *blanks
     C     cgiinp03      andne     *blanks
     C                   MOVEL     cgiinp02      XXTAB1
     C                   MOVE      *ZEROS        HEAVD
     C                   MOVE      *ZEROS        HEOPD
     C     1             DO        4             XN                1 0
     C                   IF        ((XXTAB2(XN)>='0') AND (XXTAB2(XN)<='9'))
     C                   MULT      10            HEAVD
     C                   MOVE      XXTAB2(XN)    HEAVD
     C                   ELSE
     C                   LEAVE
     C                   END
     C                   ENDDO
     C                   MOVEL     cgiinp03      XXTAB1
     C     1             DO        7             XN                1 0
     C                   IF        ((XXTAB2(XN)>='0') AND (XXTAB2(XN)<='9'))
     C                   MULT      10            HEOPD
     C                   MOVE      XXTAB2(XN)    HEOPD
     C                   ELSE
     C                   LEAVE
     C                   END
     C                   ENDDO
     C     opdkey        chain     headf                              33
     C                   ELSE
     C                   MOVE      *ON           *IN33
     C                   end
     C                   end
      *
     C                   IF        *IN33
     C                   MOVE      *ZEROS        HEAVD
     C                   MOVE      *ZEROS        HEOPD
     C                   MOVE      *BLANKS       HENAK
     C                   MOVE      *BLANKS       HEADK1
     C                   MOVE      *BLANKS       HEADK2
     C                   MOVE      *BLANKS       HEADK3
     C                   MOVE      *ZEROS        HENT
     C                   MOVE      *BLANKS       HEVS1
     C                   MOVE      *ZEROS        HEVKT
     C                   MOVE      *ZEROS        HEM3
     C                   MOVE      *BLANKS       HESGM
     C                   MOVE      *BLANKS       HEDTMO
     C                   END
      * ref
     C                   callp     updHTMLvar('REF':herfa:
     C                             InitHTMLVars)
      * avd/opd
     C                   callp     updHTMLvar('AVD':
     C                             %trim(%editc(HEAVD:'Z')))
     C                   callp     updHTMLvar('OPD':
     C                             %trim(%editc(HEOPD:'Z')))
      * mottaker
     C                   callp     updHTMLvar('MOTT':henak)
     C                   callp     updHTMLvar('ADR1':headk1)
     C                   callp     updHTMLvar('ADR2':headk2)
     C                   callp     updHTMLvar('ADR3':headk3)
      * ant vareslag vekt
     C                   callp     updHTMLvar('ANT':
     C                             %trim(%editc(HENT:'Z')))
     C                   callp     updHTMLvar('VS':HEVS1)
     C                   callp     updHTMLvar('VKT':
     C                             %trim(%editc(HEVKT:'Z')))
     C                   callp     updHTMLvar('M3':
     C                             %trim(%editw(HEM3:'    ,   ')))
      * Kvitt mottatt
     C                   MOVEL     HESGM         xxkvmo
     C                   MOVE      HEDTMO        xxkvmo           20
     C                   callp     updHTMLvar('KVMO':xxkvmo)
      *
BODY C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updHTMLvar('USER':USER)
      * Color for text, background, links, visited links, active links
     C                   callp     updhtmlvar('TXTCOLOR':'black')
     C                   callp     updhtmlvar('BOLD':' ')
     C                   callp     updhtmlvar('BGCOLOR':BIFARG)
     C                   callp     updhtmlvar('LINK':'0000FF')
     C                   callp     updhtmlvar('VLINK':'FF0000')
     C                   callp     updhtmlvar('ALINK':'00FF00')
     C                   endsr
