      *
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr *Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
11105 * 03 YBC PTF11 EPOSTADRESSE FOR MOTTAK AV TVINN-MELDING
XXXXXX*XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
12132 * 04 YBC PTF12 OPPHEVE VENTEKØ
********************************************************************
      *
     A                                      DSPSIZ(27 132 *DS4)
     A                                      PRINT
     A                                      HELP(01)
     A                                      CF01(01)
S02  A*           WSNR           1A  B  6  5VALUES(' ' '2' '4')
S02  A*           WSNR           1A  B  6  5VALUES(' ' '2' '4')
S02  A*           WSNR           1A  B  6  5VALUES(' ' '2' '4')
      *
     A          R SUBFIL                    SFL
S02  A*           WSNR           1A  B  6  5VALUES(' ' '2' '4')
     A            WSNR           1A  B  6  5VALUES(' ' '2' '4' '6' 'P' 'S')
     A            KSISIG         3A  O  6  8TEXT('SIGNATUR')
     A            KSINAV        16A  O  6 13TEXT('NAVN PÅ SIGNATUR')
     A            KSOVL          8A  O  6 31
     A            KSUSER        10A  O  6 41
     A            KSEGEN         2   O  6 63
N03  A            KSIXXX        42A  O  6 75
N04  A            KSISTA         1A  O  6118
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A            ZRECNO         4S 0O  2 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYSPED/SYFT10'
     A                                      COLOR(BLU)
     A                                  1 20MSGCON(045 DIV1252 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                  1 66DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  2 66TIME
     A                                      COLOR(BLU)
     A                                  3  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 48MSGCON(020 SHU1346 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(020 DIV0017 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 24'6=Kunde/avd-styring'
     A                                      COLOR(BLU)
     A                                  4 48MSGCON(025 SHU1347 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
E03  A                                  5  2MSGCON(072 SHU1284 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
N03  A                                  5 75MSGCON(040 YBC2018 *LIBL/SYMSGF)
N03  A                                      COLOR(WHT)
N04  A                                  5118'Opphev v-kø'
N04  A                                      COLOR(WHT)
      *
     A          R BUNN                      CSRLOC(LINNBR POSNBR)
     A                                      CF03(03 'EXIT')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            LINNBR         3  0H
     A            POSNBR         3  0H
     A                                  3  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 42MSGCON(018 DIV0022 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 62MSGCON(018 DIV0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  98                              4  2MSGCON(078 DIV0012 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
      *
     A          R VARELI
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1  2MSGCON(072 SHU1284 *LIBL/SYMSGF)
     A                                  1 75MSGCON(040 YBC2018 *LIBL/SYMSGF)
N04  A                                  1118'Opphev v-kø'
     A            WSISIG         3A  B  2  8DSPATR(UL)
     A  40                                  DSPATR(HI)
     A  40                                  DSPATR(PR)
     A  90                                  DSPATR(RI)
     A                                      TEXT('SIGNATUR')
     A            WSINAV        16A  B  2 13DSPATR(UL)
     A  89                                  DSPATR(RI)
     A                                      TEXT('NAVN PÅ SIGNATUR')
     A            WSOVL          8A  B  2 31
     A            WSUSER        10A  B  2 41
     A            WSEP          42A  B  2 75DSPATR(UL)
     A                                      CHECK(LC)
N04  A            WSSTA          1A  B  2118DSPATR(UL)
N04  A                                      VALUES(' ' 'J' 'N')
N04  A                                  2120'(J/N)'
     A                                  3  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  4  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
      *
     A          R VAREL2
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A                                  2  2MSGCON(012 DIV1216 *LIBL/SYMSGF)
     A            W2SIG          3A  I  2 16DSPATR(UL)
     A                                      DSPATR(PC)
     A                                  3  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
      *
     A          R VAREL3
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A                                  1 15MSGCON(060 DIV1254 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            KSISIG         3A  O  2 17DSPATR(UL)
     A                                      DSPATR(PC)
     A                                      COLOR(WHT)
     A            KSINAV        16A  O  2 39DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  3  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
      *
     A          R NYWRK
     A                                      CF03(03 'EXIT')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                  1  2'SYSPED/SYFT10'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(045 DIV1252 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                  1 65DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  2 65TIME
     A                                      COLOR(BLU)
     A                                  3  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(030 DIV0017 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
S03  A*                                 5  2MSGCON(078 DIV1253 *LIBL/SYMSGF)
N03  A                                  5  2MSGCON(072 SHU1284 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
N03  A                                  5 75MSGCON(040 YBC2018 *LIBL/SYMSGF)
N03  A                                      DSPATR(HI)
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 42MSGCON(018 DIV0022 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 62MSGCON(018 DIV0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
