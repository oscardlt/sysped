      *********************************************************************
CRTPG+*  CRTPGM SYSPED/SYOPKW5 MODULE(*LIBL/SYOPKW5 *LIBL/INCGI)
CRTPGM*        ACTGRP(CGI) AUT(*USE)
      *
      *********************************************************************
      * MAIN PROGRAM FLOW
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
     FBRIDF     IF   E           K DISK    USROPN
     FHENOP     UF   E           K DISK    USROPN
     FHENOS01   UF   E           K DISK    USROPN
     FHENOF01   UF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Prototype defintions and standard system API error structure
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      *--------------------------------------------------------------------
      * Variables common to CGI programs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
      * Variables received from the input string
     D lng             s              2
     D USER            S             10
     D LNR             s              7
      *
     D Lib             s             10
     D Fn              s             10
     D Mbr             s             10
      *
     D Spaces          s             12a   inz('&nbsp;&nbsp;')
      *
      *--------------------------------------------------------------------
      * Prolog common to CGI programs
      * -Receive input from the remote browser
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prolog3
      *=====================================================================******
      * MAIN PROCESS LINE
      *=====================================================================******
      * Get program start time for calculating execution time
     C                   time                    timedata1
      * Parse input
     C                   exsr      Parse
      *
     C                   EXSR      INIT
      * Blank passord=anonym, OK, Ellers må være "Inne"
     C     BIPO          IFNE      *BLANKS
     C                   IF        ((*IN50 = *ON) OR
     C                              (BIINN = *BLANKS))
     C                   exsr      SetSkl01B
     C                   GOTO      SLUTT
     C                   END
     C                   END
      * Set output skeleton html member name
     C                   exsr      SetSkl
      * Read skeleton output html, etc.
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
      *
      * 1-Section /$top
     C                   exsr      SetTop
     C                   callp     wrtsection('top')
      *
      * slett sending / reduser HEAD
     C     HFSNDN        setll     henoS01
     C     HFSNDN        reade     henoS01                                33
     c     *in33         DOWEQ     *OFF
     C     HSLNR         IFNE      HFLNR
     C                   update    HENOSR
     C                   else
     C                   delete    HENOSR
     C     HSLNR         CHAIN     HENOP                              33
     C     *IN33         IFEQ      '0'
     C                   SUB       1             HOMINT                          *ANT SND
     C                   SUB       HSANT         HOMINK                          *ANT KLL
     C                   SUB       HSVKT         HOVKT
     C                   SUB       HSM3          HOM3
     C                   SUB       HSLM          HOLM
     C     HOMINT        IFGT      0
     C                   UPDATE    HENOPR
     C                   ELSE
     C                   DELETE    HENOPR
     C                   END
     C                   END
     C                   END
     C     HFSNDN        reade     henoS01                                33
     c                   end
      *
      *  Slett evt fritekster
     C     HenofKey      setll     henof01
     C     HenofKey      reade     henof01                                33
     c     *in33         DOWEQ     *OFF
     C                   DELETE    HENOFR
     C     HenofKey      reade     henof01                                33
     c                   end
      *
     C     SLUTT         TAG
     C                   EXSR      EXIT
     C                   return
      *=====================================================================******
      * Parse input
      *=====================================================================******
     C     Parse         begsr
     C                   eval      LNR      = zhbgetvar('LNR')
     C                   eval      HFLNR    = c2n2(LNR)
     C                   MULT      -1            HFLNR
     C                   eval      HFSNDN   = zhbgetvar('sndn')
      * Userid.....
     C                   eval      user = zhbgetvar('user')
      *
     C                   endsr
      *=====================================================================******
      * Set html output skeleton member before its read into memory
      *=====================================================================******
     C     SetSkl01B     begsr
     C                   eval      lng = uppify(lng)
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC' + lng
     C                   eval      Mbr = 'SYMNB'
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
     C                   exsr      SetTop
     C                   callp     updHTMLvar('FEILTXT':'.')
     C                   callp     wrtsection('all')
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$top
      *  (search criteria)
      *=====================================================================******
     C     SetTop        begsr
     C                   callp     updHTMLvar('HFSNDN':HFSNDN)
      *
     C                   endsr
      *=====================================================================******
      * Set html output skeleton member before its read into memory
      *=====================================================================******
     C     SetSkl        begsr
      *------------------
      * Set html output skeleton member
     C                   eval      lng = uppify(lng)
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC' + lng
     C                   eval      Mbr = 'SYOPKW5'
      *
     C                   endsr
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
     C                   OPEN      BRIDF
     C     USER          CHAIN     BRIDF                              50
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
     C                   open      henop
     C                   open      henos01
     C                   OPEN      henof01
     C     HENOFKey      KLIST
     C                   KFLD                    HFLNR
     C                   KFLD                    HFSNDN
      *
     C                   endsr
      *=====================================================================******
      *  AVSLUTT
      *=====================================================================******
     C     EXIT          begsr
     C                   callp     wrtsection('endhtml')
     C                   callp     wrtsection('*fini')
     C                   close     BRIDF
     C                   close     henop
     C                   close     henos01
     C                   close     henof01
     C                   eval      *inlr = *on
      *
     C                   endsr
