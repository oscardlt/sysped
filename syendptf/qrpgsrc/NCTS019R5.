********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
12352 * 01 CB  PTF12 Legg inn externref i godsmerking hvis godsmerking er blank
********************************************************************
     FNCTSEC    O  A E             DISK
     FNCTSECAM  O  A E             DISK
     FNCTSEI    O  A E             DISK
     FSAEH      IF   E           K DISK
     FSAEHTL    IF   E           K DISK
     FSAEV      IF   E           K DISK
     FSAEV4     IF   E           K DISK    RENAME(SAEVR:SAEVR4)
     FFIRM      IF   E           K DISK
     FCUNDL01   IF   E           K DISK
     FFRISOK    IF   E           K DISK
     D                 DS
     D  SYRG                   1     14
     D  SYFOR9                 1      9
     D  SYFORE                 1     11
     C                   EXSR      SRINIT
     C                   IF        *IN51=*OFF
     C                   IF        SESAM = 'J'
     C                   EXSR      READSAEV4
     C                   ELSE
     C                   EXSR      READSAEV
     C                   END
     C                   ENDIF
     C                   EVAL      *INLR=*ON
      *_______________________________________________
      * Les SAEV                              READSAEV
      *"""""""""""""""""""""""""""""""""""""""""""""""
     C     READSAEV      BEGSR
     C     SAEHKEY       SETLL     SAEV
     C     SAEHKEY       READE     SAEV                                   53
     C                   DOW       *IN53 = *OFF
     C                             AND (SVNYL = 'S' OR SVNYL = 'D')             IKKE TA MED SLETTET
     C     SAEHKEY       READE     SAEV                                   53
     C                   ENDDO
      *
     C     FRISOKK       CHAIN     FRISOK                             33
     C   33              EVAL      FSSOK = *BLANKS
      *
     C                   ADD       1             ULI
     C                   EVAL      ULI2=*ZEROS
     C     *IN53         DOWEQ     *OFF

     C                   CLEAR                   NCTSECR
     C                   CLEAR                   NCTSECAMR
     C                   CLEAR                   NCTSEIR
      *
     C                   IF        SVNYL <> 'S' AND SVNYL <> 'D'

     C                   EVAL      TIAVD  = UTCAVD
     C                   EVAL      TITDN  = UTCTDN
     C                   EVAL      TILI   = ULI
     C                   ADD       1             ULI2              5 0
     C                   EVAL      TILI2  = ULI2
     C                   MOVEL     SVVNT         TICCH
     C                   EVAL      TICTX  = SVVT01
     C                   EVAL      TIVKB  = SVVKTB
     C                   ADD       SVVKTB        TCVKTB
     C                   EVAL      TIVKN  = SVVKTN
     C                   EVAL      TIPMRK = SVFT01
     C                   EVAL      TIPMRK2= SVFT02
     C                   EVAL      TIPMRK3= SVFT03
     C                   EVAL      TIPMRK4= SVFT04
     C                   EVAL      TIPMRK5= SVFT05
     C                   EVAL      TIPTY  = SVEH01
     C                   IF        TIPTY  = *BLANKS AND SVNT01 > 0
     C                   EVAL      TIPTY  ='CT'
     C                   ENDIF
     C                   EVAL      TIPNT  = SVNT01
     C                   EVAL      TINTS  = SVNTM
     C                   EVAL      TIAVD2 = UTCAVD2
     C                   EVAL      TITDN2 = UTCTDN2
     C*                  EVAL      TIPDTY = 'N830'
     C     SETLE         IFNE      *BLANKS
     C     SETLL         IFNE      *BLANKS
     C                   EVAL      TISDTY = 'NZZZ'
     C                   EVAL      TISDRF = %TRIM(SETLE) + ' ' + %TRIM(SETLL)
     C                   ENDIF
     C                   ENDIF
     C                   IF        FSSOK <> *BLANKS
     C                   EVAL      TIXEXT = FSSOK
N01  C                   IF        SVFT01 = *BLANKS
N01  C                   MOVEL     FSSOK         TIPMRK
N01  C                   ENDIF
     C                   ENDIF

     C                   WRITE     NCTSEIR

     C                   END

     C     SAEHKEY       READE     SAEV                                   53
     C                   ENDDO
      *
     C                   IF        TCVKTB>0
     C                   EVAL      TCAVD  = UTCAVD
     C                   EVAL      TCTDN  = UTCTDN
     C                   EVAL      TCAVD2  = UTCAVD2
     C                   EVAL      TCTDN2  = UTCTDN2
     C                   EVAL      TCLI   = ULI
     C                   MOVEL     FSSOK         TCUCR
     C                   MOVEL     FSSOK         TCXEXT
     C                   WRITE     NCTSECR
     C                   IF        UHC_AM = 'J'
     C                   EXSR      HADR2
     C                   ENDIF
     C                   ENDIF

     C                   ENDSR
      *_______________________________________________
      * Les SAEV4                            READSAEV4
      *"""""""""""""""""""""""""""""""""""""""""""""""
     C     READSAEV4     BEGSR
     C                   ADD       1             ULI
     C     SAEHKEY       SETLL     SAEV4
     C     SAEHKEY       READE     SAEV4                                  53
     C                   DOW       *IN53 = *OFF
     C                             AND (SVNYL = 'S' OR SVNYL = 'D')             IKKE TA MED SLETTET
     C     SAEHKEY       READE     SAEV4                                  53
     C                   ENDDO
     C     FRISOKK       CHAIN     FRISOK                             33
     C   33              EVAL      FSSOK = *BLANKS
     C                   EVAL      ULI2=*ZEROS
      *
     C     *IN53         DOWEQ     *OFF

     C                   CLEAR                   NCTSECR
     C                   CLEAR                   NCTSECAMR
     C                   CLEAR                   NCTSEIR
      *
     C                   IF        SVNYL <> 'S' AND SVNYL <> 'D'
     C                   EVAL      X_LN = SVLN
      *
     C                   EVAL      TIAVD  = UTCAVD
     C                   EVAL      TITDN  = UTCTDN
     C                   EVAL      TILI   = ULI
     C                   ADD       1             ULI2
     C                   EVAL      TILI2  = ULI2
      *
     C                   MOVEL     SVVNT         TICCH
     C                   EVAL      TICTX  = SVVT01
     C                   EVAL      TIVKB  = 0
     C                   EVAL      TIVKN  = 0
     C                   EVAL      TIPMRK = SVFT01
     C                   EVAL      TIPMRK2= SVFT02
     C                   EVAL      TIPMRK3= SVFT03
     C                   EVAL      TIPMRK4= SVFT04
     C                   EVAL      TIPMRK5= SVFT05
     C                   EVAL      TIPTY  = SVEH01
     C                   IF        TIPTY  = *BLANKS AND SVNT01 > 0
     C                   EVAL      TIPTY  ='CT'
     C                   ENDIF
     C                   EVAL      TIPNT  = 0
     C                   EVAL      TINTS  = 0
      *
     C                   EVAL      TIAVD2 = UTCAVD2
     C                   EVAL      TITDN2 = UTCTDN2
     C*                  EVAL      TIPDTY = 'N830'
     C     SETLE         IFNE      *BLANKS
     C     SETLL         IFNE      *BLANKS
     C                   EVAL      TISDTY = 'NZZZ'
     C                   EVAL      TISDRF = %TRIM(SETLE) + ' ' + %TRIM(SETLL)
     C                   ENDIF
     C                   ENDIF
     C                   IF        FSSOK <> *BLANKS
     C                   EVAL      TIXEXT = FSSOK
N01  C                   IF        SVFT01 = *BLANKS
N01  C                   MOVEL     FSSOK         TIPMRK
N01  C                   ENDIF
     C                   ENDIF
      *
     C                   DOW       *IN53 = *OFF
     C                   IF        X_LN <> SVLN
     C                   LEAVE
     C                   END
     C                   EVAL      TIVKB  = TIVKB  + SVVKTB
     C                   EVAL      TIVKN  = TIVKN  + SVVKTN
     C                   EVAL      TINTS  = TINTS  + SVNTM
     C                   EVAL      TIPNT  = TIPNT  + SVNT01
     C                   ADD       TIVKB         TCVKTB
     C     SAEHKEY       READE     SAEV4                                  53
     C                   ENDDO
      *
     C                   WRITE     NCTSEIR
     C                   GOTO      TAGSAEV4
      *
     C                   END

     C     SAEHKEY       READE     SAEV                                   53
     C     TAGSAEV4      TAG
     C                   ENDDO

     C                   IF        TCVKTB>0

     C                   EVAL      TCAVD  = UTCAVD
     C                   EVAL      TCTDN  = UTCTDN
     C                   EVAL      TCAVD2  = UTCAVD2
     C                   EVAL      TCTDN2  = UTCTDN2
     C                   EVAL      TCLI   = ULI
     C                   MOVEL     FSSOK         TCUCR
     C                   MOVEL     FSSOK         TCXEXT
     C                   WRITE     NCTSECR
     C                   IF        UHC_AM = 'J'
     C                   EXSR      HADR2
     C                   ENDIF
     C                   ENDIF
      *
      *
     C                   ENDSR
      *
      ***********************************************
     C     HADR2         BEGSR
      ***********************************************
     C                   MOVE      SEKNK         TCKNS
     C                   MOVE      SENAK         TCNAS
     C                   MOVE      SEADK1        TCAD1S
     C     TCAD1S        IFEQ      *BLANKS
     C                   MOVEL     '.'           TCAD1S
     C                   END
     C                   MOVEL     SEADK3        TCPNS
     C                   MOVEL     SEADK3        TCPSS
     C                   MOVEL     'NO'          TCLKS
     C     SEKNK         IFEQ      0                                            1<-B
     C                   MOVE      *BLANKS       TCTINS
     C                   ELSE                                                   1
     C                   MOVE      SEKNK         YKN
     C     KUND1K        CHAIN     CUNDL01                            34
     C                   SELECT                                                  2<-B
     C                   WHEN      EORI = *BLANKS                                2
     C                   IF        ((SYFOR9 <> *BLANKS) AND (SYFOR9 <> *ZEROS))   3<-B
     C                   IF        SYLAND = *BLANKS                                4<-B
     C                   EVAL      TCTINS = 'NO' + SYFOR9
     C                   ELSE                                                      4
     C                   EVAL      TCTINS = SYLAND + SYFOR9
     C                   END                                                       4<-E
     C                   END                                                      3<-E
     C                   OTHER                                                   2
     C                   EVAL      TCTINS = EORI
     C                   ENDSL                                                   2<-E
     C                   END                                                    1<-E
      *
     C                   MOVE      SEKNS         TCKNK
     C                   MOVE      SENAS         TCNAK
     C                   MOVE      SEADS1        TCAD1K
     C     TCAD1K        IFEQ      *BLANKS
     C                   MOVEL     '.'           TCAD1K
     C                   END
     C                   MOVEL     SEADS3        TCPNK
     C                   MOVEL     SEADS3        TCPSK
     C*                  MOVEL     SELKA         TCLKK
     C                   MOVEL     SELKB         TCLKK
     C     SEKNS         IFEQ      0                                            1<-B
     C                   MOVE      *BLANKS       TCTINK
     C                   ELSE                                                   1
     C                   MOVE      SEKNS         YKN
     C     KUND1K        CHAIN     CUNDL01                            34
     C                   SELECT                                                  2<-B
     C                   WHEN      EORI = *BLANKS                                2
     C                   IF        ((SYFOR9 <> *BLANKS) AND (SYFOR9 <> *ZEROS))   3<-B
     C                   IF        SYLAND = *BLANKS                                4<-B
     C                   EVAL      TCTINK = 'NO' + SYFOR9
     C                   ELSE                                                      4
     C                   EVAL      TCTINK = SYLAND + SYFOR9
     C                   END                                                       4<-E
     C                   END                                                      3<-E
     C                   OTHER                                                   2
     C                   EVAL      TCTINK = EORI
     C                   ENDSL                                                   2<-E
     C                   END

     C                   WRITE     NCTSECAMR
      *
     C                   ENDSR
      *________________________________________________
      * INIT                                     SRINIT
      *""""""""""""""""""""""""""""""""""""""""""""""""
     C     SRINIT        BEGSR
     C     *ENTRY        PLIST
     C                   PARM                    UTCAVD
     C                   PARM                    UTCTDN
     C                   PARM                    ULI               5 0
     C                   PARM                    UTCAVD2
     C                   PARM                    UTCTDN2
     C                   PARM                    UHC_AM            1

     C     *LIKE         DEFINE    TCAVD         UTCAVD
     C     *LIKE         DEFINE    TCTDN         UTCTDN
     C     *LIKE         DEFINE    TCAVD2        UTCAVD2
     C     *LIKE         DEFINE    TCTDN2        UTCTDN2
     C     *LIKE         DEFINE    SVLN          X_LN
     C                   Z-ADD     0             YKN               8 0

     C     SAEHKEY       KLIST
     C                   KFLD                    UTCAVD2
     C                   KFLD                    UTCTDN2
     C     KUND1K        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    YKN
     C     FRISOKK       KLIST
     C                   KFLD                    UTCAVD2
     C                   KFLD                    UTCTDN2
     C                   KFLD                    FSKODE
      *
     C                   EVAL      FSKODE = '*CE'
      *
     C     *LOVAL        SETLL     FIRM
     C                   READ      FIRM
      * Finnes eksportdeklarasjonen?
     C     SAEHKEY       CHAIN     SAEH                               51
     C  N51SAEHKEY       CHAIN     SAEHTL                             33
     C  N51
     CAN 33              EVAL      SESAM = 'N'
      *
     C                   ENDSR
      *
