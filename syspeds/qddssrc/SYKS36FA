********************************************************************
     A* RETTINGER I DETTE PROGRAM:
PTFNRA*SEK SIG VERS BESKRIVELSE.........
"""""A*"""""""""""""""""""""""""""""""""
P8059A* 01 CB  PTF8 NY PARAMETER
11XXX * 02 YBC PTF11 BEVARE VARIABLER
********************************************************************
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HELP
     A                                      ALTHELP
     A          R SUBFIL                    SFL
     A*%%TS  SD  20020228  155656  SYSTEMA     REL-V4R4M0  5769-PW1
     A            WSNR           1A  B  6  5VALUES(' ' '2' '4')
     A            CIKUNR         8  0O  6  7TEXT('KUNDE')
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
N02  A            CIPRT          1A  H
N02  A            CISEID        35A  H
N02  A            CIMOID        35A  H
     A            ZZKNAV        20A  O  6 16TEXT('NAVN')
     A                                      COLOR(WHT)
     A            ZZSEID        20A  O  6 38TEXT('AVS-ID')
     A            ZZMOID        20A  O  6 59TEXT('MOT-ID')
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A*%%TS  SD  20020228  211636  SYSTEMA     REL-V4R4M0  5769-PW1
S01  A*                                     SFLSIZ(0015)
S01  A*                                     SFLPAG(0014)
N01  A                                      SFLSIZ(0014)
N01  A                                      SFLPAG(0013)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                  1  2'SYSPED/SYKS36A'
     A                                      COLOR(BLU)
     A                                  1 19'Vedlikeh. kunder en SENDER INTERN--
     A                                      EDI FAKTURA TIL'
     A                                      COLOR(WHT)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                  2 70TIME
     A                                  3  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A            ZRECNO         4S 0O  3 75SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  4  2MSGCON(030 DIV0017 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  2'Option  Kunde   Navn              -
     A                                        EDI-ID avsender(vår) EDI-ID motta-
     A                                      ker(deres)'
     A                                      COLOR(WHT)
     A          R BUNN
     A*%%TS  SD  20020228  164547  SYSTEMA     REL-V4R4M0  5769-PW1
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF03(03 'EXIT')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  3  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 42MSGCON(018 DIV0022 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  98                              4  2MSGCON(078 DIV0012 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A N98                              4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A          R VARELI
     A*%%TS  SD  20051201  073042  CB          REL-V5R2M0  5722-WDS
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF04(04 'SØK')
     A                                      CF12(12 'Forrige')
     A                                      SLNO(20)
     A                                      CLRL(05)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1  2'Kundenr EDI-ID avsender(vår)      -
     A                                                EDI-ID mottaker(deres)   -
     A                                             '
     A                                      COLOR(WHT)
     A            WSKUNR         8Y 0B  2  1TEXT('KUNDE')
     A  40                                  DSPATR(HI)
     A                                      EDTCDE(4)
     A            WSSEID        35A  B  2 10TEXT('AVS-ID')
     A            WSMOID        35A  B  2 46TEXT('MOT-ID')
N01  A                                  3  2'SKRIV UT FAKTURA ?'
N01  A                                      COLOR(WHT)
N01  A            WSPRT          1A  B  3 21VALUES('J' 'N' 'Y')
N01  A                                  3 23'Y/J/N'
S01  A*                                 3  4'F4=Søk'
     A                                  4  4'F4=Søk'
     A                                      COLOR(BLU)
S01  A* 30        MSGTXT        78A  O  4  2MSGID(&MSGNR SYMSGF)
     A  30        MSGTXT        78A  O  5  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
S01  A*N30                              4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A N30                              5 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A          R VAREL3
     A*%%TS  SD  20020228  211636  SYSTEMA     REL-V4R4M0  5769-PW1
     A                                      CF12(12 'Forrige')
S01  A*                                     SLNO(21)
S01  A*                                     CLRL(04)
N01  A                                      SLNO(20)
N01  A                                      CLRL(05)
     A                                      OVERLAY
     A                                  1  2'Kundenr EDI-ID avsender(vår)      -
     A                                                EDI-ID mottaker(deres)   -
     A                                             '
     A                                      COLOR(WHT)
     A            WSKUNR         8Y 0O  2  1TEXT('KUNDE')
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            WSSEID        35A  O  2 10TEXT('AVS-ID')
     A                                      COLOR(WHT)
     A            WSMOID        35A  O  2 46TEXT('MOT-ID')
     A                                      COLOR(WHT)
N01  A                                  3  2'SKRIV UT FAKTURA ?'
N01  A                                      COLOR(WHT)
N01  A            WSPRT          1A  O  3 21
N01  A                                  3 23'Y/J/N'
S01  A*                                 3  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
N01  A                                  4  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
S01  A*                                 3 21'Tast Enter for å slette eller F12 -
N01  A                                  4 21'Tast Enter for å slette eller F12 -
     A                                      for å avbryte'
     A                                      COLOR(WHT)
S01  A*                                 4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
N01  A                                  5 40'(C) COPYRIGHT  SYSTEMA A/S  2005  '
     A                                      COLOR(WHT)
     A          R NYWRK
     A*%%TS  SD  20020228  155656  SYSTEMA     REL-V4R4M0  5769-PW1
     A                                      CF03(03 'EXIT')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                  1  2'SYSPED/SYKS36A'
     A                                      COLOR(BLU)
     A                                  1 19'Vedlikeh. kunder en SENDER INTERN--
     A                                      EDI FAKTURA TIL'
     A                                      COLOR(WHT)
     A                                  1 66DATE
     A                                      EDTCDE(Y)
     A                                  2 66TIME
     A                                  3  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(030 DIV0017 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 42MSGCON(018 DIV0022 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
