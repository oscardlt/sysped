*****A**************************************************************
     A* RETTINGER I DETTE PROGRAM:
PTFnrA*Sek Sig Vers Beskrivelse...........................
"""""A*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
P8062A* 01 CB  OPTION 8 = FRITEKST
10261 * 02 YBC PTF10 VEKT/KUBIKK/KOLLI I TURER ER ENDRET TIL 9.0, 7.3 OG 7.0
*****A**************************************************************
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPDOC('STD' 'HELPNU' 'HLP400')
     A                                      HELP
     A                                      ALTHELP
     A          R SUBFIL                    SFL
S01  A*           WSNR           1A  I  6  3VALUES(' ' '5')
N01  A            WSNR           1A  I  6  3VALUES(' ' '5' '8')
     A  98                                  DSPATR(RI)
     A            TUAVD          4Y 0O  6  6EDTCDE(4)
     A  27                                  COLOR(YLW)
     A            TUPRO          8Y 0O  6 11EDTCDE(4)
     A  27                                  COLOR(YLW)
     A            TUBILN        10A  O  6 20
     A  28                                  COLOR(YLW)
     A            TUSDF         20A  O  6 31
     A            TUSDT         20A  O  6 52
     A  29                                  COLOR(YLW)
     A            WSDT           6Y 0O  6 73
     A            TUAO           5Y 0O  7 19DSPATR(UL)
     A                                      EDTCDE(4)
S02  A*           TUTS           5Y 0O  7 28DSPATR(UL)
N02  A            TUTS           7Y 0O  7 28DSPATR(UL)
     A                                      EDTCDE(4)
S02  A*           TUTVKT         5Y 0O  7 36DSPATR(UL)
N02  A            TUTVKT         9Y 0O  7 36DSPATR(UL)
     A                                      EDTCDE(4)
S02  A*           TUM3           4Y 2O  7 44DSPATR(UL)
N02  A            TUTM3          7Y 3O  7 46DSPATR(UL)
     A                                      EDTCDE(4)
S02  A*           TULM           4Y 2O  7 55DSPATR(UL)
N02  A            TUTLM          4Y 2O  7 55DSPATR(UL)
     A                                      EDTCDE(4)
     A            TUST           1A  O  7 72
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0008)
     A                                      SFLPAG(0007)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A            ZRECNO         4S 0O  2 76SFLRCDNBR
     A                                      DSPATR(ND)
     A            MSG001        30A  O  1  2MSGID(DIV 0712 SYMSGF)
     A                                      COLOR(WHT)
     A            MSG002        22A  O  1 58MSGID(DIV 0047 SYMSGF)
     A                                      COLOR(TRQ)
     A  27        MSG003         9A  O  2  2MSGID(DIV 0050 SYMSGF)
     A  27        WSAVD          4Y 0O  2 12COLOR(BLU)
     A                                      EDTCDE(4)
     A            MSG004        26A  O  3  2MSGID(DIV 0016 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG005        15A  O  3 29MSGID(DIV 0713 SYMSGF)
     A                                      COLOR(BLU)
N01  A                                  3 45'8=Fritekst'
N01  A                                      COLOR(BLU)
     A            MSG006        78A  O  4  2MSGID(DIV 0984 SYMSGF)
     A                                      COLOR(WHT)
     A            MSG007        70A  O  5  6MSGID(DIV 0714 SYMSGF)
     A                                      COLOR(WHT)
      *
     A          R BUNN
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF03(03 'Avslutt')
     A                                      CF05(05 'Frisk')
     A                                      CF08(08 'SØKE TUR')
     A                                      SLNO(20)
     A                                      CLRL(05)
     A                                      OVERLAY
     A            MSG101        18A  O  4  2MSGID(DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG102        18A  O  4 22MSGID(DIV0021 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG103        18A  O  4 42MSGID(DIV0023 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  5  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R SØKTUR
     A                                      CF12(12 'FORRIGE')
     A                                      SLNO(20)
     A                                      CLRL(05)
     A                                      OVERLAY
     A            WSAVD          4Y 0B  1  2DSPATR(UL)
     A                                      DSPATR(PC)
     A  99                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSPRO          8Y 0B  1  7DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            MSG201        30A  O  1 17MSGID(DIV0658 SYMSGF)
     A            WSBILN        10A  B  2  2DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSDT1          6Y 0B  2 13DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            MSG202        30A  O  2 20MSGID(DIV0981 SYMSGF)
     A            WSSDT         20A  B  3  2DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSDT2          6Y 0B  3 23DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            MSG203        30A  O  3 30MSGID(DIV0982 SYMSGF)
     A            MSG204        26A  O  4  2MSGID(DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  5  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R NYWRK
     A                                      CF03(03 'Avslutt')
     A                                      CF05(05 'Frisk')
     A                                      CF08(08 'SØKE TUR')
     A            MSG301        30A  O  1  2MSGID(DIV0712 SYMSGF)
     A                                      COLOR(WHT)
     A            MSG302        22A  O  1 58MSGID(DIV0047 SYMSGF)
     A                                      COLOR(TRQ)
     A  27        MSG303         9A  O  2  2MSGID(DIV0050 SYMSGF)
     A  27        WSAVD          4Y 0O  2 12COLOR(BLU)
     A                                      EDTCDE(4)
     A            MSG304        26A  O  3  2MSGID(DIV0016 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG305        15A  O  3 29MSGID(DIV0713 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG306        78A  O  4  2MSGID(DIV0984 SYMSGF)
     A                                      COLOR(WHT)
     A            MSG307        70A  O  5  6MSGID(DIV0714 SYMSGF)
     A                                      COLOR(WHT)
     A            MSG308        18A  O 23  2MSGID(DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG309        18A  O 23 22MSGID(DIV0021 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG310        18A  O 23 42MSGID(DIV0023 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
