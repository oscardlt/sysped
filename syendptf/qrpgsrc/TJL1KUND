      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/TJL1KUND MODULE(*LIBL/TJL1KUND *LIBL/INCGI)
CRTPGM*        ACTGRP(*NEW) AUT(*USE)
      *
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
      * 01 JOV PTF12 KUNDEL1 = blank - sett til V = vis
********************************************************************
      *********************************************************************
      /copy SYSPEDS/qrpgsrcpy,hspecs
      /copy SYSPEDS/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FKUNDL01   UF A E           K DISK    USROPN
     FKKUNF     IF A E           K DISK    USROPN
     FSYSBL01   UF   E           K DISK    USROPN
     FFIRKU     UF   E           K DISK    USROPN
     FKHEDL01   IF   E           K DISK    USROPN
     FKONTL01   IF   E           K DISK    USROPN
     FKODKL01   IF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *---------------------------------------------------------firma------
      /copy SYSPEDS/qrpgsrcpy,prototypeb
      /copy SYSPEDS/qrpgsrcpy,usec
      /copy SYSPEDS/qrpgsrcpy,variables3
      *
      * Varible for
     D WSUSER          S             10
     D WA              S             15
     D Mode            S              1
     D KUNDEL1         S              1
     D JSONstring      s          32000
     D KhenvList       S           5000
     D KgrupList       S           5000
     D Error           S            150
     D AntLest         S              9  0
      *get input-string
      /copy syspeds/qrpgsrcpy,prolog3

      * MERK Parse KUN henter inn nøkkelverdier for chain i databasefil
      * henter IKKE inn til arbeidvariable som så flyttes til DB-variable
      * Fra sr HOVED hentes variaber reff fra CGI-input til DB-variable for write eller update
      * Parse input
     C                   EXSR      Parse
      * Open files etc
     C                   EXSR      INIT
      *
      *
     c                   if        mode = 'G'
      * må hente FØR oppslag/setting av verdier da L1 gjenbruker feltnavn
     C                   exsr      srGetKhenv
     C                   exsr      srGetKgrup
     c                   exsr      srGet
     c                   else
     c                   eval      KhenvList ='[]'
     c                   eval      KgrupList ='[]'
     c                   exsr      srAddUpdate
     c                   endif
      *
     C                   CALLP     gethtmlifs(
     C                             '/syspeddev/html/JSON.html':
     C                             '/CGITAG/')
     C                   CALLP     wrtsection('top')
      * ............................................................................................
      * skriv JSON-Object start..(alltid '{' + x ant param. m/komma mellom (IKKE komma etter siste))
      * ............................................................................................
      /free
        JSONstring='{'
        +'¬user¬ : ¬'+%trim(WSUSER)+'¬, '
        +'¬firma¬ : ¬'+%trim(FIRMA)+'¬, '
        +'¬kundnr¬ : ¬'+%trim(%editc(KUNDNR:'Z'))+'¬, '
        +'¬l1_Head¬ : ¬'+%trim(HEAD)+'¬, '                                 //   Språk Header
        +'¬l1_KundGr¬ : ¬'+%trim(KUNDGR)+'¬, '                             //   Kundegruppe
        +'¬l1_Feks¬ : ¬'+%trim(%editc(FEKS:'Z'))+'¬, '                     //   Ant Faktura
        +'¬l1_Rkod¬ : ¬'+%trim(RKOD)+'¬, '                                 //   Rendekode D,M,Blank
        +'¬l1_Pkod¬ : ¬'+%trim(PKOD)+'¬, '                                 //   Purrekode
        +'¬l1_Kutdr¬ : ¬'+%trim(KUTDR)+'¬, '                               //   KontoUtdr.Kode
        +'¬l1_Khenv¬ : ¬'+%trim(KHENV)+'¬, '                               //   KontoHenvisning
        +'¬l1_Pgebyr¬ : ¬'+%trim(PGEBYR)+'¬, '                             //   PurreGebyr
        +'¬l1_DaoAar¬ : ¬'+%trim(%editc(DAOAAR:'X'))+'¬, '                 //   Dato opprettet År
        +'¬l1_DaoMnd¬ : ¬'+%trim(%editc(DAOMND:'X'))+'¬, '                 //   Dato opprettet Mnd
        +'¬l1_DaoDag¬ : ¬'+%trim(%editc(DAODAG:'X'))+'¬, '                 //   Dato opprettet Dag
        +'¬l1_Ledi20¬ : ¬'+%trim(LEDI20)+'¬, '                             //   Oppr AV NTE-spesial
        +'¬l1_KhenvList¬ : '+%trim(KHENVList)+', '                         //   Liste gyldige KHENV
        +'¬l1_KgrupList¬ : '+%trim(KGRUPList)+', '                         //   Liste gyldige K-Grup
        +'¬errMsg¬ : ¬'+%trim(Error)+'¬}';
      /end-free
      * JSONfix escaper " i tekst,  for deretter å bytte ¬->"
     C                   CALL      'JSONFIX'
     C                   PARM                    JSONstring
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   CALLP     wrtsection('JSONlin')
      *
      * Quit
     C                   EXSR      EXIT
      *
     C                   MOVE      *ON           *INLR
     C                   RETURN
      *
      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     EXIT          BEGSR
      * Lukk fil
     C                   CLOSE     BRIDF
     C  N50              CLOSE     KUNDL01
     C  N50              CLOSE     KKUNF
     C  N50              CLOSE     SYSBL01
     C  N50              CLOSE     KHEDL01
     C  N50              CLOSE     KONTL01
     C  N50              CLOSE     KODKL01
     C  N50              CLOSE     FIRKU

      * Do not delete the CALL to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   CALLP     wrtsection('*fini')
      *
     C                   ENDSR
      *********************************************************
     C     Parse         BEGSR
      *********************************************************
      * Get input
     C                   EVAL      WSUSER  = zhbgetvar('USER')
     C                   EVAL      FIRMA   = zhbgetvar('FIRMA')
     C                   EVAL      WA      = zhbgetvar('KUNDNR')
     C                   EVAL      KUNDNR  = c2n2(WA)
     C                   EVAL      MODE    = zhbgetvar('MODE')                  G=Get L1data
     C                   EVAL      KUNDEL1 = zhbgetvar('KUNDEL1')               V=Vises/J=autoskap
      * TEMP parameter mangler - sett V
N01  C                   IF        KUNDEL1 = ' '
N01  C                   EVAL      KUNDEL1 = 'V'
N01  C                   endif
     C                   ENDSR
      *********************************************************
     C     Hoved         BEGSR
      *********************************************************
      * Get input
     C*                  EVAL      AKTKOD  = zhbgetvar('AKTKOD')
      * Firma/Kundnr ER alt hentet
     C*                  EVAL      DKUND   = zhbgetvar('DKUND ')
     C                   EVAL      KNAVN   = zhbgetvar('KNAVN ')
     C                   EVAL      ADR1    = zhbgetvar('ADR1  ')
     C                   EVAL      ADR2    = zhbgetvar('ADR2  ')
     C                   EVAL      WA      = zhbgetvar('POSTNR')
     C                   EVAL      POSTNR  = c2n2(WA)
     C                   EVAL      ADR3    = zhbgetvar('ADR3  ')
     C                   EVAL      KPERS   = zhbgetvar('KPERS ')
     C                   EVAL      TLF     = zhbgetvar('TLF   ')
     C                   EVAL      SONAVN  = zhbgetvar('SONAVN')
     C                   EVAL      LAND    = zhbgetvar('SYLAND')                cundc-variabel
     C                   EVAL      VALKOD  = zhbgetvar('VALKOD')
     C                   EVAL      SPRAAK  = zhbgetvar('SPRAAK')
     C                   EVAL      BANKG   = zhbgetvar('BANKG ')
     C                   EVAL      POSTG   = zhbgetvar('POSTG ')
     C                   EVAL      WA      = zhbgetvar('FMOT  ')
     C                   EVAL      FMOT    = c2n2(WA)
     C                   EVAL      BETBET  = zhbgetvar('BETBET')
     C                   EVAL      BETMAT  = zhbgetvar('BETMAT')
     C                   EVAL      SFAKT   = zhbgetvar('SFAKT ')
     C                   if        SFAKT <> ' ' and SFAKT <> ' '
     C                   EVAL      SFAKT  = 'J'
     c                   endif
     C                   if        SFAKT <> 'J'
     C                   EVAL      SFAKT  = ' '
     c                   endif
     C                   EVAL      WA      = zhbgetvar('KGRENS')
     C                   EVAL      KGRENS  = c2n2(WA)
     C                   EVAL      TFAXNR  = zhbgetvar('TFAXNR')
     C                   EVAL      WA      = zhbgetvar('SYRG  ')                CUNDF-variabel
     c                   if        %subst(WA:1:9) <> *blanks
     c                   if        %subst(WA:10:2) =  *blanks
     C                   EVAL      FORETN  = c2n2(WA)                           9 lang = foret.nr
     c                   else
     C                   EVAL      PERNKU  = c2n2(WA)                           11 la.= per.nr
     c                   endif
     c                   endif
     C                   EVAL      SELGER  = zhbgetvar('SYSELG')                selger hent fra SY
      **
      **
      * div L1-felt oppfateres KUN dersom L1-mode=V = Vise feltene i bildet
     c                   if        KundeL1 = 'V'
     C                   EVAL      HEAD    = zhbgetvar('HEAD')
     C                   EVAL      KUNDGR  = zhbgetvar('KUNDGR')
     C                   EVAL      WA      = zhbgetvar('FEKS')
     C                   EVAL      FEKS    = c2n2(WA)
     C                   EVAL      RKOD    = zhbgetvar('RKOD')
     C                   EVAL      PKOD    = zhbgetvar('PKOD')
     C                   EVAL      KUTDR   = zhbgetvar('KUTDR')
     C                   EVAL      KHENV   = zhbgetvar('KHENV')
     C                   EVAL      PGEBYR  = zhbgetvar('PGEBYR')
     c                   endif
      **
     c
      *
      *
     C                   ENDSR
      *********************************************************
     C     INIT          BEGSR
      *********************************************************
     C                   OPEN      BRIDF
      * Test innlogging
     C     WSUSER        CHAIN     BRIDF                              50
     C     BILIBL        IFEQ      *blanks
     C                   CALLb     'INCGI'
     C                   PARM                    BISTDL
     C                   else
     C                   CALL      BILIBL
     C                   end

     c                   if        FIRMA = *blanks
     c                   eval      FIRMA = BIFIRM
     c                   endif

     C     KunKey        klist
     C                   KFLD                    FIRMA
     C                   KFLD                    Kundnr
     C     KHEDkey       klist
     C                   KFLD                    FIRMA
     C                   KFLD                    RecTypD           1
     C                   move      'D'           RecTypD
     C     KONTkey       klist
     C                   KFLD                    FIRMA
     C                   KFLD                    KONTOH

     c                   if        *in50 = *on
     C   50              EVAL      Error = 'Invalid userID'
     c                   else
     C  N50              OPEN      KUNDL01
     C  N50              OPEN      KKUNF
     C  N50              OPEN      SYSBL01
     C  N50              OPEN      KHEDL01
     C  N50              OPEN      KONTL01
     C  N50              OPEN      KODKL01
     C  N50              OPEN      FIRKU
     c                   endif

     C                   ENDSR

      *********************************************************
     C     GetKnr        BEGSR
      *********************************************************
      *telleverk primært fra FIRKU (= benytter IKKE L1, men kan adde kunder i Sysped OG KUNDF/KKUNF)
      *  = bør legges om slik at KUNDF  droppes / tildele nummer i VKUND (ref f.eks TOTEN)
      * prinsipp i FIRKU - NESTE nr ligger lagret (add 1 etter bruk)
     C     FIRMA         CHAIN     FIRKU                              33
     C     *IN33         IFEQ      '0'
     C     FIKUFN        andne     *zeros
     C                   MOVE      FIKUFN        KUNDNR
     C                   ADD       1             FIKUFN
     C                   UPDATE    FIRKUR
     C                   ELSE
      * prinsipp i SYSBF - SIST brukte nr ligger lagret (add 1 før bruk)
     C     FIRMA         CHAIN     SYSBL01                            33
     C     *IN33         IFEQ      '0'
     C     SISKNR        IFNE      99999999
     C                   ADD       1             SISKNR
     C                   Z-ADD     SISKNR        KUNDNR
     C                   ELSE
     C                   Z-ADD     1             SISKNR
     C                   Z-ADD     1             KUNDNR
     C                   ENDIF
     C                   UPDATE    SYSBR
     C                   ENDIF
     C                   ENDIF
      *
     C                   ENDSR
      *
      ************************************************************
     c     srGet         BEGSR
      ************************************************************
      *
     c                   if        Kundnr<>*zeros
     c     KunKey        chain(n)  KUNDL01
     c                   if        not %found(KUNDL01)
     C                   EVAL      Error = 'Invalid customer no'
     c                   endif
     c                   else
      * Get defaults - hardkodet spesial ved andre ønsker
     c                   eval      HEAD  = '  '                                 Språk Ledet..
     c                   eval      KUNDGR= '  '                                 Kundegrup
     c                   eval      FEKS = 1                                     Ant Faktura
     c                   eval      RKOD = ' '                                   Rentekode
     c                   eval      PKOD = 'J'                                   Purrekode
     c                   eval      PGEBYR='J'                                   Purregebyr
     c                   eval      KUTDR= 'J'                                   KontoUtdr.Kode
     c                   eval      KHENV= 'KUN'                                 KontoHenvisning
     c                   endif
     c                   endsr
      ************************************************************
     c     srGetKhenv    BEGSR
      ************************************************************
     c                   move      *zeros        AntLest
     c     KHEDkey       setll     KHEDL01
     c     KHEDkey       reade     KHEDL01
     c                   dow       not(%eof)
     c                   if        AKTKOD='A'
     c                   Add       1             AntLest
     c                   if        AntLest=1
     c                   eval      KhenvList =' ['
     c                   else
     c                   eval      KhenvList =%trim(KhenvList)+', '
     c                   endif
     c                   eval      KONTNA = '* Ukjent konto *'
     c     KONTkey       chain     KONTL01
     c                   eval      KhenvList =%trim(KhenvList)+'{'
     c                             +'¬kode¬ : ¬'+%trim(KHENV)+'¬, '
     c                             +'¬tekst¬ : ¬'+%trim(KONTNA)+'¬}'
     c                   endif
     c     KHEDkey       reade     KHEDL01
     c                   enddo
     c                   eval      KhenvList =%trim(KhenvList)+']'
     c                   endsr
      *
      ************************************************************
     c     srGetKgrup    BEGSR
      ************************************************************
     c                   move      *zeros        AntLest
     c     FIRMA         setll     KODKL01
     c     FIRMA         reade     KODKL01
     c                   dow       not(%eof)
     c                   if        AKTKOD='A'
     c                   Add       1             AntLest
     c                   if        AntLest=1
     c                   eval      KgrupList =' ['
     c                   else
     c                   eval      KgrupList =%trim(KgrupList)+', '
     c                   endif
     c                   eval      KgrupList =%trim(KgrupList)+'{'
     c                             +'¬kode¬ : ¬'+%trim(KUNDGR)+'¬, '
     c                             +'¬tekst¬ : ¬'+%trim(KODTXT)+'¬}'
     c                   endif
     c     FIRMA         reade     KODKL01
     c                   enddo
     c                   eval      KgrupList =%trim(KgrupList)+']'
     c                   endsr
      *
      ************************************************************
     c     srAddUpdate   BEGSR
      ************************************************************
      *
     c                   if        Kundnr = *zeros
     c                   exsr      GetKnr
     c                   endif
      *
     c     KunKey        chain     KUNDL01
      * hent rett fra input til filvariable
     c                   exsr      Hoved
     c                   if        not %found(KUNDL01)
     c                   call      'SYGE15C'
     c                   parm                    WDT               8
     c                   parm                    WTM               6
     c                   eval      DAOAAR = %int(%subst(WDT:1:4))
     c                   eval      DAOMND = %int(%subst(WDT:5:2))
     c                   eval      DAODAG = %int(%subst(WDT:7:2))
     c                   if        KUNDEL1 = 'J'
     c                   eval      FEKS = 1                                     Ant Faktura
     c                   eval      PKOD = 'J'                                   Purrekode
     c                   eval      KUTDR= 'J'                                   KontoUtdr.Kode
     c                   eval      KHENV= 'KUN'                                 KontoHenvisning
     c                   endif
     C                   eval      AKTKOD='A'
     c                   write     KUNDR
     c                   else
     c                   update    KUNDR
     c                   endif

     c     KunKey        chain     KKUNF
     c                   if        not %found(KKUNF)
     c                   write     KKUNR
     c                   endif
     c                   endsr
