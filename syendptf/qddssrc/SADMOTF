      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
12371 * 01 CB  PTF12 NYE VARIABLER
********************************************************************
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
12371 * 01 CB  PTF12 NYE VARIABLER
********************************************************************
      *__________________________________
      * EKSP.MANIFEST (TRANSPORT)
      *""""""""""""""""""""""""""""""""""
     A                                      REF(SR)
     A          R SADMOTFR
     A            ETST      R               REFFLD(ST)
     A                                      COLHDG('Status')
      * ' ' --> NY REGISTRERT MANIFEST
      * 'C' --> TRANSPORT         ER SENDT TIL TOLLVESENET
      * 'D' --> TRANSPORT         ER SLETTET
      * 'M' --> TRANSPORT         ER FEIL VED SENDING TIL TOLLVESENET
      * 'P' --> TRANSPORT         ER AVSLUTTET
      * 'X' --> TRANSPORT         ER UNDERARBEID AV EN BRUKER
      *  "Første turnr" er key
     A            ETAVD     R               REFFLD(AVD)
     A                                      COLHDG('Avdeling')
     A            ETPRO     R               REFFLD(PRO)
     A                                      COLHDG('Turnummer')
      *  "LØPE-nummer" som key???
     A            ETLNRT         7S 0       COLHDG('Løpenummer')
     A            ETDTR     R               REFFLD(DT)
     A                                      COLHDG('Registreringsdato')
     A            ETSG      R               REFFLD(SG)
      *
     A            ETST2     R               REFFLD(ST)
     A                                      COLHDG('Status om manifest')
      * ' ' --> TRANSPORT         er ikke sendt til Tollvesenet
      * 'S' --> TRANSPORT         er sendt til Tollvesenet med POST
      * 'R' --> TRANSPORT         er sendt til Tollvesenet med PUT
      * 'D' --> TRANSPORT         er sendt til Tollvesenet med DELETE
      *
      * BRUKER LRN/MRN FOR Å LAGRE issue date (foreløpig/endelig...)
     A            ETUUID        36A         COLHDG('LRN')
     A            ETUUID_OWN    36A         COLHDG('LRN BACKUP')
     A            ETMID         18A         COLHDG('MRN')
     A            ETMID_OWN     18A         COLHDG('MRN BACKUP')
     A            ETENTVAL       1S 0       COLHDG('ent_vali')
     A            ETENTOFF      10          COLHDG('ent_office')
     A            ETENTTIM      14S 0       COLHDG('ent_time')
     A            ETST3     R               REFFLD(ST)
     A                                      COLHDG('Status om innpass.')
     A            ETDTIN    R               REFFLD(DT)
     A                                      COLHDG('Innsendingsdato')
     A            ETETAD    R               REFFLD(DT)
     A                                      COLHDG('Estimert ank. ETA')
     A            ETETAT    R               REFFLD(TM)
     A                                      COLHDG('ETA tid')
     A            ETSHED    R               REFFLD(DT)
     A                                      COLHDG('Sheduled avg-dt ')
     A            ETSHET    R               REFFLD(TM)
     A                                      COLHDG('Sheduled avg-tid')
      *
      * Representative
     A            ETKNR          8S 0       COLHDG('Representative')
     A            ETRGR         17A         COLHDG('Org.nr representativ')
     A            ETNAR         30A         COLHDG('Navn representative')
     A            ETNA2R        30A         COLHDG('SubDivvision repre.')
     A            ETAD1R        30A         COLHDG('Gateadr. repre.')
     A            ETNRR         15A         COLHDG('Husnr repre.')
     A            ETPNR          9A         COLHDG('Postnr representativ')
     A            ETPSR         24A         COLHDG('P.sted representativ')
     A            ETLKR          2A         COLHDG('L.kode representativ')
     A            ETPBR         15A         COLHDG('Postbox repre.')
     A            ETEMR         50A         COLHDG('Epostadr/tlf repre.')
N01  A            ETEMRX        50A         COLHDG('Epostadr/tlf repre.2')
     A            ETEMRT         2A         COLHDG('Kodetype repre.')
N01  A            ETEMRTX        2A         COLHDG('Kodetype repre.2')
      * EM = Epostadr. TE = Telefon
      * ActiveBorderTransportMeans
     A            ETKMRK        30A         COLHDG('Kjennemerke')
     A            ETKTYP         2A         COLHDG('Kjøretøy type')
     A            ETKTM          4A         COLHDG('Transportmiddel type')
     A            ETKLK          2A         COLHDG('Kjøretøynasjonalitet')
     A            ETCREF        17A         COLHDG('Convay/turRef/flight')
     A            ETKTKD         1A         COLHDG('Mode av transportKD')
      * Operator - Fører
     A            ETSJAF        50A         COLHDG('Sjåfør navn')
     A            ETEMS         50A         COLHDG('Epostadr/tlf repre.')
     A            ETEMST         2A         COLHDG('Kodetype repre.')
      *
      * carrier  - transportør
     A            ETKNT          8S 0       COLHDG('Transportør')
     A            ETRGT         17A         COLHDG('Org.nr transportør')
     A            ETNAT         30A         COLHDG('Navn transportør')
     A            ETNA2T        30A         COLHDG('SubDivvision trans.')
     A            ETAD1T        30A         COLHDG('Gateadr. trans.')
     A            ETNRT         15A         COLHDG('Husnr trans.')
     A            ETPNT          9A         COLHDG('Postnr transportør')
     A            ETPST         24A         COLHDG('P.sted transortør')
     A            ETLKT          2A         COLHDG('L.kode transportør')
     A            ETPBT         15A         COLHDG('Postbox trans.')
     A            ETEMT         50A         COLHDG('Epostadr/tlf trans.')
     A            ETEMTX        50A         COLHDG('Epostadr/tlf trans.2')
     A            ETEMTT         2A         COLHDG('Kodetype trans.')
     A            ETEMTTX        2A         COLHDG('Kodetype trans.2')
      * EM = Epostadr. TE = Telefon
      * transportDocumentMasterLevel
     A            ETDKM         50A         COLHDG('Master Dokumentnr')
     A            ETDKMT         4A         COLHDG('Master Dokumenttype')
      * ----- Passeringsinfo (customsOfficeOfFirstEntry) ------
     A            ETTSD          8A         COLHDG('Passeringstollsted')
      *
     A            EMERR         50A         COLHDG('Feilmelding ved SND')
