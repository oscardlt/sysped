     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPDOC('STD' 'HELPNU' 'HLP400')
     A                                      HELP
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  6  3VALUES(' ' '5')
     A            XKNR           8  0O  6  6COLOR(YLW)
     A                                      EDTCDE(4)
     A            PKKNR          8A  H
     A            PKAVTN         1A  O  6 16COLOR(YLW)
     A            KNAVN         30A  O  6 19
     A            ADR1          30A  O  6 50
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0016)
     A                                      SFLPAG(0015)
     A  41                                  SFLDSP
     A  42                                  SFLDSPCTL
     A  43                                  SFLCLR
     A  44                                  SFLEND
     A  41N43N44                            ROLLUP(25)
     A  41N43N44                            ROLLDOWN(26)
     A            ZRECNO         4S 0O  2 76SFLRCDNBR
     A                                      DSPATR(ND)
     A            MSG101        45A  O  1  2MSGID(DIV1315 SYMSGF)
     A                                      COLOR(WHT)
     A            MSG102        30A  O  3  2MSGID(DIV0016 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG103        20A  O  4  2MSGID(DIV1311 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG104        78A  O  5  2MSGID(DIV1314 SYMSGF)
     A                                      DSPATR(HI)
     A          R BUNN                      CSRLOC(LINNBR POSNBR)
     A                                      CF03(03 'EXIT')
     A                                      CF05(05 'REFRESH')
     A                                      CF08(08 'SEARCH')
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            LINNBR         3  0H
     A            POSNBR         3  0H
     A            MSG201        18A  O  3  2MSGID(DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG202        18A  O  3 22MSGID(DIV0021 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG203        18A  O  3 42MSGID(DIV0023 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSG204        78A  O  4  2MSGID(DIV0012 SYMSGF)
     A                                      COLOR(WHT)
     A          R VAREL2
     A                                      CF04(04 'PROMT')
     A                                      CF12(12 'CANCEL')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            MSG401        20A  O  1 18MSGID(DIV1316 SYMSGF)
     A            MSG402        15A  O  2  2MSGID(DIV1168 SYMSGF)
     A            W2KNR          8  0B  2 18DSPATR(UL)
     A                                      DSPATR(PC)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            W2AVTN         1A  B  2 31DSPATR(UL)
     A                                      COLOR(YLW)
     A            MSG403        18A  O  3  2MSGID(DIV0032 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG404        18A  O  3 22MSGID(DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A          R NYWRK
     A                                      CF03(03 'EXIT')
     A                                      CF05(05 'REFRESH')
     A                                      CF08(08 'SEARCH')
     A                                      CF12(12 'CANCEL')
     A            MSG601        45A  O  1  2MSGID(DIV1315 SYMSGF)
     A                                      COLOR(WHT)
     A            MSG602        30A  O  3  2MSGID(DIV0016 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG603        20A  O  4  2MSGID(DIV1311 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG604        78A  O  5  2MSGID(DIV1314 SYMSGF)
     A                                      DSPATR(HI)
     A            MSG605        18A  O 23  2MSGID(DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG606        18A  O 23 22MSGID(DIV0021 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG607        18A  O 23 42MSGID(DIV0023 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSG608        78A  O 24  2MSGID(DIV0012 SYMSGF)
     A                                      COLOR(WHT)
