             PGM
             DCL        VAR(&INST) TYPE(*LGL) LEN(1) VALUE('1')
             DCL        VAR(&LIB) TYPE(*CHAR) LEN(10) VALUE('          ')
             DCL        VAR(&STMF) TYPE(*CHAR) LEN(30) +
                            VALUE('                              ')
             DCL        VAR(&FILE) TYPE(*CHAR) LEN(15) VALUE('               ')

             CD /
             MONMSG     MSGID(CPF0000)
             RTVDTAARA  DTAARA(EDIDTA (171 10)) RTNVAR(&LIB)
             CHGVAR     VAR(&FILE) VALUE('AVGIFTER.TXT')
             CHGVAR     VAR(&STMF) VALUE(&LIB *TCAT '/TOLL/' *TCAT &FILE)
/*  HENT FRA TOLLVESENET    */
             CALL SYTIHENTC PARM(&STMF &FILE)
             DLTF       FILE(QTEMP/SYTIRCV)
             MONMSG     MSGID(CPF0000)
             CRTPF      FILE(QTEMP/SYTIRCV) RCDLEN(256) SIZE(25000)
/* Konverter fra PC-tekst-fil, til FLAT tekstfil  */
             QSYS/CPYFRMSTMF FROMSTMF(&STMF) +
                          TOMBR('QSYS.LIB/QTEMP.LIB/SYTIRCV.FILE/SYTI+
                          RCV.MBR') MBROPT(*REPLACE) CVTDTA(*TBL) +
                          TBL('qsys.lib/sysped.lib/QTCPEBCNO.tbl')
/* Filen finnes ikke - hopp ut  */
             MONMSG     MSGID(CPFA095) EXEC(DO)
             SNDPGMMSG  MSG('Aktuell txt-fil finnes ikke i IFS +
                          /home/sytoll/   Konvertering IKKE +
                          kjørt!!!') MSGTYPE(*INFO)
             GOTO       CMDLBL(SLUTT)
             ENDDO
 /* KONVERTER TIL KODTS8NY, SADSDNY */
             CLRPFM     FILE(KODTS8NY)
             CLRPFM     FILE(SADSDNY)

             CALL       PGM(SYTIAV1) PARM(&INST)
             CLRPFM     FILE(QTEMP/SADSDNYE)
             MONMSG     MSGID(CPF0000) EXEC(CRTDUPOBJ OBJ(SADSDNY) +
                          FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP) NEWOBJ(SADSDNYE))
             CPYF       FROMFILE(SADSDNY) TOFILE(SADSDNYE) +
                          MBROPT(*ADD) CRTFILE(*NO)

 /* MODULEN IKKE INSTALLERT?? ELLER 0 POSTER LEST. */
             IF         COND(&INST = '1') THEN(GOTO CMDLBL(SLUTT))

             OVRPRTF    FILE(LISTE) OUTQ(*JOB) HOLD(*YES)

 /* Oppdater GAMLE AVGIFTER, SLETT UTGÅTTE i KODTS8  */
             CALL       PGM(SYTIAV2)
 /* LEGG TIL HELT NYE NR i KODTS8   */
             CALL       PGM(SYTIAV3)

 /* Oppdater GAMLE AVGIFTER, SLETT UTGÅTTE i SADSD   */
             CALL       PGM(SYTIAV4)
/*           RGZPFM     FILE(SADSD)          */
/*           MONMSG     MSGID(CPF0000)       */
 /* LEGG TIL HELT NYE NR i SADSD    */
             CALL       PGM(SYTIAV5)

 /* Oppdater GAMLE AVGIFTER, SLETT UTGÅTTE i SADAVGE (FF)*/
             CALL       PGM(SYTIAV6)
 /* LEGG TIL HELT NYE NR i SADAVGE (FF)   */
             CALL       PGM(SYTIAV7)
 /* SETT SLUTTDATO PÅ ALLE POSTER SOM IKKE FINNES I SADSDNYE */
             CALL       PGM(SYTIAV8)
 /*          RGZPFM     FILE(SADAVGE)          */
 /*          MONMSG     MSGID(CPF0000)         */


             SNDPGMMSG  MSG('Konvertering av aktuell(e) fil(er) ER +
                          KJØRT. Sjekk utkrift "LISTE".......... +
                          (For hver konvertert fil: EN LISTE for +
                          nye/endrede. EN LISTE for slettede.)') +
                          MSGTYPE(*INFO)

SLUTT:       ENDPGM
