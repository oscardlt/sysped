     H Option( *SrcStmt ) DftActGrp( *No ) BndDir( 'QC2LE' )
      *__________
      * Variabler
      *""""""""""
     D FILE_i          s               *
     D FILE_o          s               *
     D ErrTxt          s            128a
     D String          s            256a
     D rc              s             10i 0

      *_________________
      * Feildefinisjoner
      *"""""""""""""""""
     D errno           Pr            10i 0

     D strerror        Pr           128a   Varying
      *___________________________
      * IFS Stream file funksjoner
      *"""""""""""""""""""""""""""
     Dopen             Pr              *   ExtProc( '_C_IFS_fopen' )
     D                                 *   Value Options( *String )
     D                                 *   Value Options( *String )

     Dfgets            Pr              *   ExtProc( '_C_IFS_fgets' )
     D                                 *   Value
     D                               10i 0 Value
     D                                 *   Value

     dfputs            pr            10i 0 extproc( '_C_IFS_fputs' )
     d                                 *   value options( *string )
     d                                 *   value

     Dclose            Pr            10i 0 ExtProc( '_C_IFS_fclose' )
     D                                 *   Value
      *______
      * Input
      *""""""
     C     *ENTRY        PLIST
     C                   PARM                    Filnam_i        100
     C                   PARM                    Filnam_o        100

      *________________________________________
      * Åpne fil, med tegnkonvertering for jobb
      *""""""""""""""""""""""""""""""""""""""""
     C                   EVAL      FILE_o = open( %TrimR( FilNam_o )
     C                             : 'w, codepage=819' )
     C                   EVAL      rc = close( FILE_o )
      * Utfil må åpnes først
     C                   EVAL      FILE_o = open( %TrimR( FilNam_o ) : 'w' )
      * Så åpnes innfil
     C                   EVAL      FILE_i = open( %TrimR( FilNam_i ) : 'r' )

     C                   IF        FILE_i = *Null
     C                   EVAL      ErrTxt = %Char( Errno ) + ': ' + Strerror
     C                   ELSE

     C                   DOW       fgets( %Addr( String ) : %Size( String )
     C                             : FILE_i ) <> *Null
      * Fjern LF
     C                   eval      String = %scanRpl(X'25': '' : String)
     C                   EVAL      rc = fputs( %Str( %Addr( String )) : FILE_o )
     C                   ENDDO

     C                   EVAL      rc = close( FILE_i )
     C                   ENDIF
     C                   EVAL      rc = close( FILE_o )

     C                   EVAL      *INLR = *On
     C                   RETURN

      *_________________
      * Hente feilnummer
      *"""""""""""""""""
     P Errno           B
     D                 PI            10i 0
     D sys_errno       PR              *    ExtProc( '__errno' )

     D Error           S             10i 0  Based( pError ) NoOpt

     C                   EVAL      pError = sys_errno
     C                   RETURN    Error

     P Errno           E
      *________________
      * Hente feiltekst
      *""""""""""""""""
     P Strerror        B
     D                 PI           128a   Varying
     D sys_strerror    PR              *   ExtProc( 'strerror' )
     D                               10i 0 Value

     C                   RETURN    %Str( sys_strerror( Errno ))

     P Strerror        E
