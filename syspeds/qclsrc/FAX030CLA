             PGM        (&UTSKRIFTNR &DOKNAVN &SVAR &FAX &PTIME +
                         &PDATE &NAVN &AVD &OPD &TUR &KODE)
             DCL        VAR(&AVD) TYPE(*CHAR) LEN(4) VALUE('0000')
             DCL        VAR(&OPD) TYPE(*CHAR) LEN(7) VALUE('0000000')
             DCL        VAR(&TUR) TYPE(*CHAR) LEN(8) VALUE('00000000')
             DCL        VAR(&KODE) TYPE(*CHAR) LEN(1) VALUE('T')
             DCL        VAR(&FAX) TYPE(*CHAR) LEN(21)
             DCL        VAR(&PTIME) TYPE(*CHAR) LEN(8)
             DCL        VAR(&PDATE) TYPE(*CHAR) LEN(8)
             DCL        VAR(&NAVN) TYPE(*CHAR) LEN(30)
             DCL        VAR(&UTSKRIFTNR) TYPE(*CHAR) LEN(10)
             DCL        VAR(&BRUKER) TYPE(*CHAR) LEN(10)
             DCL        VAR(&SVAR) TYPE(*CHAR) LEN(1)
             DCL        VAR(&DOKNAVN) TYPE(*CHAR) LEN(8)
             DCL        VAR(&WS) TYPE(*CHAR) LEN(10)
             DCL        VAR(&MAPPE) TYPE(*CHAR) LEN(20)
             /* **   NY    RELEASE    **  */
             ALCOBJ     OBJ((QAOFENRG *FILE *SHRRD))
             MONMSG     MSGID(CPF3141) EXEC(GOTO CMDLBL(SLUTT))
             CHKDLO     DLO(STDMAN) FLR(FAXSTD) OBJTYPE(*DOC) +
                          USRID(*CURRENT)
             MONMSG     MSGID(CPF8A82 CPF8A77) EXEC(GOTO CMDLBL(SLUTT))
    /* TEST OM MAN ER OFFICE-BRUKER */
    /* I SÅ FALL SPØR OM UTSKRIFT SKAL FLYTTES TIL MAPPE */
             RTVUSRPRF  RTNUSRPRF(&MAPPE)
             RTVUSRPRF  RTNUSRPRF(&BRUKER)
    /*       CALL       PGM(SYYA02A) PARM(&BRUKER &DOKNAVN &SVAR)  */
    /*       IF         COND(&SVAR = 'N') THEN(GOTO CMDLBL(SLUTT)) */
    /* */
             RTVJOBA    JOB(&WS) /* Hente arbeidstsjonskode WS */
             CLRPFM     FILE(FAXDOCC) MBR(&WS)
             MONMSG     MSGID(CPF3141) EXEC(ADDPFM FILE(FAXDOCC) +
                          MBR(&WS))
             OVRDBF     FILE(FAXDOCC) TOFILE(FAXDOCC) MBR(&WS)
             CLRPFM     FILE(FAXDOCD) MBR(&WS)
             MONMSG     MSGID(CPF3141) EXEC(ADDPFM FILE(FAXDOCD) +
                          MBR(&WS))
             OVRDBF     FILE(FAXDOCD) TOFILE(FAXDOCD) MBR(&WS)
    /* */
    /* LEGG UTSKRIFT I KØ PÅ SPOOLFILE                  */
             OVRPRTF    FILE(&UTSKRIFTNR) HOLD(*YES)
    /* KOPIER SPOOLFILE OVER I DATABASEFIL (4 FØRSTE=SKIP/SPACEINFO) */
             CPYSPLF    FILE(&UTSKRIFTNR) TOFILE(FAXDOCC) +
                          SPLNBR(*LAST) TOMBR(&WS) CTLCHAR(*PRTCTL)
             MONMSG     MSGID(CPF3309) EXEC(GOTO INTET)
             DLTSPLF    FILE(&UTSKRIFTNR) SPLNBR(*LAST)
             MONMSG     MSGID(CPF0000)
    /* SETTE INN BLANKLINJER ETTER INFO I SKIPP/SPACEB-FELT*/
             CALL       PGM(FAX002C)
    /* */
             MRGDOC     FROMDOC(STDMAN) FROMFLR(FAXSTD) +
                          TODOC(&DOKNAVN) TOFLR(&BRUKER) +
                          REPLACE(*YES) MRGTYPE(*FILE) +
                          DTAFILE(*LIBL/FAXDOCD) DTAMBR(&WS) +
                          JOBQ(*NO) ADJUST(*PAGES) MLTLINRPT(*NO) +
                          DSPOPT(*EDIT) EXITPNL(*NO)
    /* */
             ALCOBJ     OBJ((QSYS/TLX400B *LIB *SHRUPD))
             MONMSG     MSGID(CPF1085) EXEC(DO)
    /* SENDER VIA FAKSMILE */
             SBMJOB     CMD(CALL PGM(FAX030CLJ) PARM(&DOKNAVN &MAPPE +
                          &FAX &PTIME &PDATE &NAVN &AVD &OPD &TUR +
                          &KODE)) JOB(SNDFAXB) JOBD(*USRPRF) +
                          JOBQ(QFAX/QFAXJOBQ)
             GOTO       CMDLBL(CLEAR)
             ENDDO
    /* SENDER VIA FAX400 */
             SBMJOB     CMD(CALL PGM(FAX030CLJ2) PARM(&DOKNAVN +
                          &MAPPE &FAX &PTIME &PDATE &NAVN &AVD &OPD +
                          &TUR &KODE)) JOB(SNDFAXB) JOBD(*USRPRF) +
                          JOBQ(SYJOBQPRT)
             DLCOBJ     OBJ((QSYS/TLX400B *LIB *SHRUPD))
    /* TØM ARBEIDSFILER                          */
 CLEAR:      CLRPFM     FILE(FAXDOCC)
             MONMSG     MSGID(CPF0000)
             CLRPFM     FILE(FAXDOCD)
             MONMSG     MSGID(CPF0000)
             GOTO SLUTT
 INTET:      SNDPGMMSG  MSG('Ingen utskrift.')
 SLUTT:      ENDPGM
