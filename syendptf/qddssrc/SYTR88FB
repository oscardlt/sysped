     A*%%TS  SD  20191218  131410  SYSTEMA     REL-V7R1M0  5770-WDS
     A* BRINGTMS-PROSJEKT
*****A**************************************************************
     A* RETTINGER I DETTE PROGRAM:
PTFnrA*Sek Sig Vers Beskrivelse...........................
"""""A*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10261A* 01 YBC PTF10 VEKT/KUBIKK/KOLLI I TURER ER ENDRET TIL 9.0, 7.3 OG 7.0
10261A* 11 JOV PTF11 OPSJON 6=SE FAKTURA
10261A* 13 JOV PTF11 OPSJON W=SE PÅ WEB
     A* XXX    BRINGTMS-PROSJEKT
     A* 20 JOV PTF12 Y = SEND SMS
     A*    JOV PTF12 M = MERKELAPP/J=MAIL(TIDLIGERE M)
     A* 25 JOV PTF12 INNE MED HEPOS1 FLYTTE TTSTAT / BEDRE OVERSKRIFTER
*****A**************************************************************
     A*%%EC
     A                                      DSPSIZ(27 132 *DS4)
     A                                      HLPPNLGRP(HLPSCR SYTR88BPNL)
     A                                      HLPTITLE('VEDLIKEHOLD AV TURENS INN-
     A                                      HOLD (TRANSPORTMODULEN)')
     A                                      HELP
     A                                      ALTHELP
     A          R SUBFIL                    SFL
     A*%%TS  SD  20191218  131410  SYSTEMA     REL-V7R1M0  5770-WDS
     A            TRSLAG         2A  H
     A            TRSDFD         8S 0H
     A            WSNR           1A  I  9  2VALUES(' ' '2' '4' '5' '8' 'L' 'P' -
     A                                      'B' 'A' 'S' 'Q' '7' 'I' '6' 'W' 'U'-
     A                                       'H' 'M' 'Y' 'J')
S20  A*                                      'H' 'M')
S19  A*                                      'H')
S17  A*                                     'B' 'A' 'S' 'Q' '7' 'I' '6' 'W' 'U')
S14  A*                                     'B' 'A' 'S' 'Q' '7' 'I' '6' 'W')
S13  A*                                     'B' 'A' 'S' 'Q' '7' 'I' '6')
S11  A*                                     'B' 'A' 'S' 'Q' '7' 'I')
S09  A*                                     'B' 'A' 'S' 'Q' '7')
     A            IMCO           1A  O  9  4COLOR(RED)
     A            HEAVD          4Y 0O  9  6EDTCDE(4)
     A  75                                  COLOR(RED)
     A            HEOPD          7Y 0O  9 11EDTCDE(4)
     A            HEOT           2A  O  9 19
     A  59                                  DSPATR(RI)
     A            HEFR           3A  O  9 22
     A            HESDF          5A  O  9 26
     A            WRSDFD         6S 0O  9 32
     A            TRSTAF         1A  O  9 39DSPATR(HI)
     A            HESDT          5A  O  9 41
     A            WRSDTD         6S 0O  9 47
     A            TRSTAE         1A  O  9 54DSPATR(HI)
     A            HENT           7Y 0H
     A            HENT4          4Y 0O  9 56EDTCDE(4)
     A  73                                  DSPATR(RI)
     A  73                                  COLOR(PNK)
     A            HEVKT          9Y 0H
     A            HEVKTW         5Y 0O  9 61EDTCDE(4)
     A  73                                  DSPATR(RI)
     A  73                                  COLOR(PNK)
     A            HEM3           7Y 3H
     A            WSM3SU         4Y 1O  9 67EDTCDE(4)
     A  73                                  DSPATR(RI)
     A  73                                  COLOR(PNK)
     A            HELMLA         4Y 2O  9 73EDTCDE(4)
     A  73                                  DSPATR(RI)
     A  73                                  COLOR(PNK)
     A            HEST           1A  O  9 79
     A            HENAS         30A  O 10  5DSPATR(UL)
     A  55                                  DSPATR(RI)
     A  71                                  COLOR(PNK)
     A  72                                  COLOR(YLW)
     A            HENAK         30A  O 10 36DSPATR(UL)
     A            HEVS10        10   O 10 67DSPATR(UL)
     A  78                                  DSPATR(RI)
     A            ETTE           1A  O 10 78DSPATR(UL)
     A                                      COLOR(RED)
     A            FAREKL         7A  O 10 80COLOR(RED)
N25  A            HEPOS1         7A  O  9 81
S25  A*           TTSTAT         8   O  9 82
N25  A            TTSTAT         8   O  9 89
     A  76                                  COLOR(RED)
     A  77                                  COLOR(YLW)
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A*%%TS  SD  20150810  222117  JOVO        REL-V7R1M0  5770-WDS
     A                                      SFLSIZ(0007)
     A                                      SFLPAG(0006)
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(24)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A                                  1  3'SYSPED/SYTR88B'
     A                                      COLOR(BLU)
     A  27                              1 22MSGCON(030 DIV0681 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A N27                              1 22MSGCON(030 DIV0724 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 58MSGCON(022 DIV0047 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  2  2MSGCON(009 DIV0050 *LIBL/SYMSGF)
     A            TUAVD          4Y 0O  2 12EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  2 22MSGCON(011 DIV4023 *LIBL/SYMSGF)
     A            TUPRO          8Y 0O  2 34EDTCDE(4)
     A                                      COLOR(WHT)
     A            ZRECNO         4S 0O  2 35SFLRCDNBR
     A                                      DSPATR(ND)
     A            TURUND         8Y 0O  2 43EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  2 53MSGCON(006 DIV0683 *LIBL/SYMSGF)
     A            TUSDF         20A  O  2 60COLOR(WHT)
     A                                  3  2MSGCON(009 DIV4021 *LIBL/SYMSGF)
     A            TUBILN    R        O  3 12REFFLD(TURERR/TUBILN *LIBL/TURER)
     A                                      COLOR(WHT)
     A            TULK      R        O  3 23REFFLD(TURERR/TULK *LIBL/TURER)
     A                                      COLOR(WHT)
     A            TUHENG    R        O  3 27REFFLD(TURERR/TUHENG *LIBL/TURER)
     A                                      COLOR(WHT)
     A            TULKH     R        O  3 38REFFLD(TURERR/TULKH *LIBL/TURER)
     A                                      COLOR(WHT)
     A N70                              3 41'*PDA*'
     A                                      COLOR(PNK)
     A                                      DSPATR(RI)
     A N70        REFTID         4Y 0O  3 47EDTWRD('0  :  ')
     A                                      COLOR(PNK)
     A                                  3 53MSGCON(006 DIV0685 *LIBL/SYMSGF)
     A            TUSDT         20A  O  3 60COLOR(WHT)
     A                                  4  2MSGCON(008 DIV0686 *LIBL/SYMSGF)
     A            TUKNT2         8Y 0O  4 12EDTCDE(4)
     A                                      COLOR(WHT)
     A            TUNAT         30A  O  4 21COLOR(WHT)
     A                                  4 53MSGCON(006 DIV4022 *LIBL/SYMSGF)
     A            WSDAFR         6S 0O  4 60COLOR(WHT)
     A                                  4 67'----->'
     A            WSDATI         6S 0O  4 74COLOR(WHT)
     A N27                              5  1MSGCON(079 SHU1362 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  27                              5  1MSGCON(079 SHU1363 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
S25  A*                                 7  1MSGCON(130 YBC0093 *LIBL/SYMSGF)
N25  A                                  7  1MSGCON(130 JOV1798 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
S25  A*                                 8  5MSGCON(125 YBC0094 *LIBL/SYMSGF)
N25  A                                  8  5MSGCON(125 JOV1799 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
S24  A*                                 6  1MSGCON(130 SHU1447 *LIBL/SYMSGF)
N24  A                                  6  1MSGCON(130 JOV1794 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A          R BUNN
     A*%%TS  SD  20150217  095327  SVEIN       REL-V7R1M0  5770-WDS
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(24)
     A                                      CF03(03 'Avslutt')
     A                                      CF05(05 'FORNY')
     A N27                                  CF06(06 'TIL PLUKKING')
     A                                      CF12(12 'FORRIGE')
     A N27                                  CF07(07 'OMDISPONER')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            MSGNR          7A  P
S01  A*                                 1  2MSGCON(007 DIV4000 *LIBL/SYMSGF)
S01  A*           WSNT           5Y 0O  1 10COLOR(WHT)
     A                                  1  2'Kll'
     A            WSNT           7Y 0O  1  6COLOR(WHT)
     A                                      EDTCDE(4)
S01  A*                                 1 16MSGCON(006 DIV0694 *LIBL/SYMSGF)
S01  A*           WSVKT          5Y 0O  1 23COLOR(WHT)
     A                                  1 14'Vkt'
     A            WSVKT          9Y 0O  1 18COLOR(WHT)
     A                                      EDTCDE(4)
S01  A*           TUTARA         5Y 0O  1 29COLOR(WHT)
     A            TUTARA         5Y 0O  1 28COLOR(WHT)
     A                                      EDTCDE(4)
S01  A*           WSBER1         5Y 0O  1 35COLOR(WHT)
     A            WSBER1         9Y 0O  1 34COLOR(WHT)
     A                                      EDTCDE(4)
S01  A*                                 1 41MSGCON(006 DIV0695 *LIBL/SYMSGF)
S01  A*           WSM3           5Y 2O  1 48COLOR(WHT)
     A                                  1 44'M3'
     A            WSM3           7Y 3O  1 47COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  1 56'LML'
     A            WSLMLA         4Y 2O  1 60COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  1 66'LMF'
     A            WSLM           4Y 2O  1 70COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  1 76MSGCON(005 SHU1511 *LIBL/SYMSGF)
     A            WSPOEN         5S 0O  1 82COLOR(WHT)
     A                                  1 89MSGCON(006 DIV0697 *LIBL/SYMSGF)
     A            WSNTO          5Y 0O  1 96COLOR(WHT)
     A                                      EDTCDE(4)
     A            TUKVKT         9Y 0O  2 18COLOR(WHT)
     A                                      EDTCDE(4)
     A            TUBER2         9Y 0O  2 34COLOR(WHT)
     A                                      EDTCDE(4)
     A            TUKAM3         7Y 3O  2 47COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  2 56'LML'
     A            TUKALM         4Y 2O  2 60COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  3  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N27                              3 42MSGCON(018 DIV4066 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 62MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  4  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
      *
     A          R NYWRK
     A                                      CF03(03 'Avslutt')
     A                                      CF05(05 'FORNY')
     A N27                                  CF06(06 'TIL PLUKKING')
     A                                      CF12(12 'FORRIGE')
     A N27                                  CF07(07 'omdisponer')
     A                                  1  3'SYSPED/SYTR88B'
     A                                      COLOR(BLU)
     A  27                              1 22MSGCON(030 DIV0681 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A N27                              1 22MSGCON(030 DIV0724 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 58MSGCON(022 DIV0047 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  2  2MSGCON(009 DIV0050 *LIBL/SYMSGF)
     A            TUAVD          4Y 0O  2 12EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  2 19MSGCON(010 DIV0659 *LIBL/SYMSGF)
     A            TUPRO          8Y 0O  2 30EDTCDE(4)
     A                                      COLOR(WHT)
     A            TURUND         8Y 0O  2 40COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  2 53MSGCON(006 DIV0683 *LIBL/SYMSGF)
     A            TUSDF         20A  O  2 60COLOR(WHT)
     A                                  3  2MSGCON(008 DIV0684 *LIBL/SYMSGF)
     A            TUKNA          8Y 0O  3 11EDTCDE(4)
     A                                      COLOR(WHT)
     A            TUNAA         30A  O  3 21COLOR(WHT)
     A                                  3 53MSGCON(006 DIV0685 *LIBL/SYMSGF)
     A            TUSDT         20A  O  3 60COLOR(WHT)
     A                                  4  2MSGCON(008 DIV0686 *LIBL/SYMSGF)
     A            TUKNT          8Y 0O  4 11EDTCDE(4)
     A                                      COLOR(WHT)
     A            TUNAT         30A  O  4 21COLOR(WHT)
     A                                  4 53MSGCON(006 DIV0687 *LIBL/SYMSGF)
     A            TUTARF        15A  O  4 60COLOR(WHT)
     A                                  5  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N27                              5 35MSGCON(012 DIV0688 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5 48MSGCON(012 DIV0693 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
S25  A*                                 6  1MSGCON(130 YBC0093 *LIBL/SYMSGF)
N25  A                                  6  1MSGCON(130 JOV1798 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
S25  A*                                 7  5MSGCON(125 YBC0094 *LIBL/SYMSGF)
N25  A                                  7  5MSGCON(125 JOV1799 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
S01  A*                                22  2MSGCON(015 DIV0691 *LIBL/SYMSGF)
S01  A*           WSNT           5Y 0O 22 18COLOR(WHT)
N01  A                                 22  2'Kll'
N01  A            WSNT           7Y 0O 22  6COLOR(WHT)
     A                                      EDTCDE(4)
S01  A*                                22 26MSGCON(006 DIV0694 *LIBL/SYMSGF)
S01  A*           WSVKT          5Y 0O 22 33COLOR(WHT)
N01  A                                 22 14'Vkt'
N01  A            WSVKT          9Y 0O 22 18COLOR(WHT)
     A                                      EDTCDE(4)
S01  A*                                22 40MSGCON(006 DIV0695 *LIBL/SYMSGF)
S01  A*           WSM3           5Y 2O 22 48COLOR(WHT)
N01  A                                 22 44'M3'
N01  A            WSM3           7Y 3O 22 47COLOR(WHT)
     A                                      EDTCDE(4)
S01  A*                                22 55MSGCON(006 DIV0696 *LIBL/SYMSGF)
S01  A*           WSLM           4Y 2O 22 62COLOR(WHT)
N01  A                                 22 56'LM'
N01  A            WSLM           4Y 2O 22 59COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 22 69MSGCON(006 DIV0697 *LIBL/SYMSGF)
     A            WSNTO          5Y 0O 22 76COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 62MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N27                             23 42MSGCON(018 DIV4066 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A          R FELWIN
     A                                      OVERLAY
     A                                      WINDOW(*DFT 13 70)
     A                                      CF10(10 'FAKT-BILDE')
     A                                  1  2'SYSPED/SYTR88B'
     A                                      COLOR(BLU)
     A                                  1 21'Det ER ført felles-kostnader...'
     A                                      COLOR(WHT)
     A                                  3  1'Oppdraget du nå fjernet fra turen -
     A                                      hadde felleskostnader på seg.      '
     A                                  4  1'(basert på fordeling via turnummer-
     A                                      t det var på)                      '
     A                                  5  1'Disse bør via et korreksjonsbilag -
     A                                      tas ut av oppdraget og refordeles  '
     A                                  6  1'på de gjenværende posisjonene på t-
     A                                      uren.                              '
     A                                  8  1'Avd:'
     A            HEAVD          4Y 0O  8  6COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  8 11'Oppdrag:'
     A            HEOPD          7Y 0O  8 20EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  9  1'Verdi av oppdragets kostnadslinjer-
     A                                       med turkode: '
     A            FELKOS        11Y 2B  9 51EDTCDE(J)
     A                                      DSPATR(PR)
     A                                      COLOR(WHT)
     A                                 10  1'Verdi av øvrige kostn.linjer (alt -
     A                                      tilbakeført?)'
     A            ANDKOS        11Y 2B 10 51EDTCDE(J)
     A                                      DSPATR(PR)
     A                                 12  1'F10=Se oppdrags faktura-bilde'
     A                                      COLOR(BLU)
     A                                 12 51'Enter=Videre'
     A                                      COLOR(BLU)
