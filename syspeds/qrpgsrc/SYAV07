********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 sh  PTF10 ny parameter syge03
********************************************************************
      **************************************************************
      *** PROGRAM SKAL OMREGNE FRAKTBREGNINGSVEKT                ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER       ***
      *** LEDIGE INDIKATORER                                     ***
      **************************************************************
      *
     FHEADF     UF   E           K DISK
     FFAKT      UF   E           K DISK
     FKODTV     IF   E           K DISK
     FFRISOL01  IF   E           K DISK
      *
     C                   EXSR      INIT
      *
     C     FSGMLK        SETLL     FRISOL01
     C     FSGMLK        READE     FRISOL01                               33
     C     *IN33         CABEQ     '1'           SLUTT
     C     *IN33         DOWEQ     '0'
     C     HEGMLK        CHAIN     HEADF                              34
     C                   ADD       HEVKT         XVKT
     C     HEM3          IFNE      0
     C     HEM3          MULT(H)   KOVKKG        XFBV2
     C     HEFBV         IFLT      XFBV2
     C                   MOVE      XFBV2         HEFBV
     C                   END
     C                   END
     C                   ADD       HEFBV         XFBV
     C                   ADD       HEM3          XM3
     C                   UPDATE    HEADFR
     C     FSGMLK        READE     FRISOL01                               33
     C  N33              ENDDO
      *
      * OPPDATER FRAKTBEREGNINGSVEKT PÅ NYTT OPPDRAG
     C     HENYTK        CHAIN     HEADF                              33
     C                   MOVE      XVKT          HEVKT
     C     XM3           IFEQ      0
     C                   MOVE      *ZEROS        HEM3
     C                   ELSE
     C     XFBV          DIV(H)    KOVKKG        HEM3
     C                   END
     C                   MOVE      XFBV          HEFBV
     C                   UPDATE    HEADFR
      * OPPDATER NYTT BELØP PÅ FAKTURALINJE
     C     FAKTK         CHAIN     FAKT                               33
     C                   EXSR      BERAVR
     C                   Z-SUB     AVRBEL        FABELN
     C                   Z-SUB     AVRBEL        FABELV
     C                   UPDATE    FAKTR
      *
     C     SLUTT         TAG
     C                   MOVE      *ON           *INLR
     C                   RETURN
      *
      *****************************************************************
     C     BERAVR        BEGSR
      *****************************************************************
     C                   MOVE      'FRA'         UGEBTY            3
     C                   MOVE      *BLANKS       UUVAL             3
     C                   MOVE      *ZEROS        AVRBEL           13 2
     C                   MOVE      *ZEROS        URABAT
     C                   MOVE      FAKUNR        UKUNDE
     C                   CALL      'SYGE03'
     C                   PARM                    UGEBTY
     C                   PARM                    UAVD              4 0
     C                   PARM                    UOPD              7 0
     C                   PARM                    UUVAL
     C                   PARM                    AVRBEL
     C                   PARM                    USONE             5
     C                   PARM                    URABAT            5 2
     C                   PARM                    N5                5 0
     C                   PARM                    N5                5 0
     C                   PARM                    N5                5 0
     C                   PARM                    N5                5 0
     C                   PARM                    UKUNDE            8 0
     C                   PARM                    UAVNR             9 0
n01  C                   PARM                    afdum             1
      *
     C                   ENDSR
      *
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     *LIKE         DEFINE    HEVKT         XVKT
     C     *LIKE         DEFINE    HEFBV         XFBV
     C     *LIKE         DEFINE    HEFBV         XFBV2
     C     *LIKE         DEFINE    HEM3          XM3
     C*--------------------------------------------------------------
     C* DEFINERER NØKKEL TIL HEADF
     C*--------------------------------------------------------------
     C     HEGMLK        KLIST
     C                   KFLD                    FSAVD
     C                   KFLD                    FSOPD
     C     HENYTK        KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    UOPD
     C     FAKTK         KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    UOPD
     C                   KFLD                    ULI
     C     FSGMLK        KLIST
     C                   KFLD                    FSKODE
     C                   KFLD                    FSSOK
     C                   MOVE      'SAA'         FSKODE
     C     KODTVK        KLIST
     C                   KFLD                    KOVUNI
     C                   KFLD                    UAVD
     C                   MOVE      'V'           KOVUNI
      *
     C     *ENTRY        PLIST
     C                   PARM                    UAVD              4 0
     C                   PARM                    UOPD              7 0
     C                   PARM                    ULI               5 0
      *
     C     KODTVK        CHAIN     KODTV                              33
     C                   MOVEL     UAVD          XOPDNR           11
     C                   MOVE      UOPD          XOPDNR
     C                   MOVEL     XOPDNR        FSSOK
      *
     C                   ENDSR
      *
