********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
12XXX * 01 CB  PTF12 Avrunda vikter på varuposter
********************************************************************
      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/TSVI062R MODULE(*LIBL/TSVI062R *LIBL/INCGI)
CRTPGM*        ACTGRP(*NEW) AUT(*USE)
      *
      *********************************************************************
      /copy SYSPEDS/qrpgsrcpy,hspecs
      /copy SYSPEDS/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FSVIFZL01  UF A E           K DISK    USROPN
     F                                     RENAME(SVIFR:SVIFZR)
     FSVIVZL01  UF A E           K DISK    USROPN
     F                                     RENAME(SVIVR:SVIVZR)
     FSVIF      O  A E             DISK    USROPN
     FSVIV      IF A E           K DISK    USROPN
     FSVTVK     IF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy SYSPEDS/qrpgsrcpy,prototypeb
      /copy SYSPEDS/qrpgsrcpy,usec
      /copy SYSPEDS/qrpgsrcpy,variables3
      *
      * Varible for
     D P               S              5U 0
     D WSUSER          S             10
     D SYAVA           S              4
     D SYAV            S              4  0
     D SYOPA           S              7
     D SYOP            S              7  0
     D WMODE           S              1
     D UNIKA           S              9
     D REFF            S             17
     D JSONstring      S          32000
     D Error           S            150
N01  D W110            S             11  0

      *get input-string
      /copy syspeds/qrpgsrcpy,prolog3

      * Parse input
     C                   EXSR      Parse
      * Open files etc
     C                   EXSR      INIT
      *
     c                   callp     gethtmlifs(
     C                             '/syspeddev/html/JSON.html':
     C                             '/CGITAG/')
     C                   callp     wrtsection('top')
      * Teste input og utfør
     C  N50              EXSR      TEST

      * ............................................................................................
      * skriv JSON-Object start..(alltid '{' + x ant param. m/komma mellom (IKKE komma etter siste))
      * ............................................................................................
      *
      /free
        JSONstring='{'
        +'¬user¬ : ¬'+%trim(WSUSER)+'¬, '
        +'¬unik¬ : ¬'+%trim(UNIKA)+'¬, '
        +'¬reff¬ : ¬'+%trim(REFF)+'¬, '
        +'¬avd¬ : ¬'+%trim(SYAVA)+'¬, '
        +'¬opd¬ : ¬'+%trim(SYOPA)+'¬, '
        +'¬mode¬ : ¬'+%trim(WMODE)+'¬, '
        +'¬errmsg¬ : ¬'+%trim(Error)+'¬, ';
      /end-free
      * JSONfix escaper " i tekst,  for deretter å bytte ¬->"
     c                   call      'JSONFIX'
     c                   parm                    JSONstring
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      *
      * ............................................................................................
      * Skriv JSON-LISTE Start (en liste er EN parameter(fra [ til   )
      * ............................................................................................
     c                   eval      JSONstring ='"updexternfakt" : ['
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      * ............................................................................................
      * Skriv JSON-Liste/Array  Avslutning
      * ............................................................................................
     c                   eval      JSONstring =']'
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      * ............................................................................................
      * Skriv JSON-string Avsluttning
      * ............................................................................................
     c                   eval      JSONstring ='}'
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      *
      * Quit
     C                   exsr      Exit


      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Lukk fil
     C                   CLOSE     BRIDF
     C  N50              CLOSE     SVIFZL01
     C  N50              CLOSE     SVIVZL01
     C  N50              CLOSE     SVIF
     C  N50              CLOSE     SVIV
     C  N50              CLOSE     SVTVK

      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   MOVE      *ON           *INLR
     C                   return
     C                   endsr
      *
      *********************************************************
     C     Parse         Begsr
      *********************************************************
      * Get input
     C                   EVAL      WSUSER  = zhbgetvar('USER')
     C                   EVAL      SYAVA = zhbgetvar('AVD')
     C                   EVAL      SYAV  = c2n2(SYAVA)
     C                   EVAL      SYOPA = zhbgetvar('OPD')
     C                   EVAL      SYOP  = c2n2(SYOPA)
     C                   EVAL      WMODE  = zhbgetvar('MODE')
     C                   EVAL      REFF  = zhbgetvar('REFF')
     C                   EVAL      UNIKA   = zhbgetvar('UNIK')
     C                   EVAL      UNIK  = c2n2(UNIKA)
     c                   ENDSR
      *********************************************************
     C     INIT          Begsr
      *********************************************************
     C                   OPEN      BRIDF
      * Test innlogging
     C     WSUSER        CHAIN     BRIDF                              50
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C                   call      BILIBL
     C                   end
     C     *LIKE         DEFINE    SVIF_UNIK     UNIK
     C     *LIKE         DEFINE    SVVK_DTS      ZVVK_DTS
     C     *LIKE         DEFINE    SVIV_SYLI     ZVIV_SYLI
     C                   EVAL      ZVVK_DTS=99999999
     C     SVIFKEY       KLIST
     C                   KFLD                    UNIK
     C                   KFLD                    REFF
     C     SVTVKKEY      KLIST
     C                   KFLD                    SVIF_VAKD
     C                   KFLD                    ZVVK_DTS
     C     SVIVKEY       KLIST
     C                   KFLD                    SVIF_SYAV
     C                   KFLD                    SVIF_SYOP

      *
     C   50              eval      Error = 'Invalid userID'
     C  N50              OPEN      SVIFZL01
     C  N50              OPEN      SVIVZL01
     C  N50              OPEN      SVIF
     C  N50              OPEN      SVIV
     C  N50              OPEN      SVTVK

     c                   endsr
      *______________________________________________________
      * TEST                                             TEST
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     TEST          BEGSR
      * Hæmta extern faktura
     C     SVIFKEY       CHAIN     SVIFZL01                           55
      * MODE D=Delete  U=Update
     C                   SELECT
     C     WMODE         WHENEQ    'D'
     C                   IF        *IN55=*ON
     C                   eval      Error = 'Invoice not found'
     C                   ELSE
     C                   eval      Error = *blanks
     C                   EXSR      SRD
     C                   ENDIF
     C     WMODE         WHENEQ    'U'
     C                   IF        *IN55=*ON
     C                   eval      Error = 'Invoice not found'
     C                   ELSE
     C                   eval      Error = *blanks
     C                   EXSR      SRU
     C                   ENDIF
     C                   OTHER
     C                   eval      Error = 'Wrong mode'
     C                   ENDSL
     C                   ENDSR
      *______________________________________________________
      * Delete                                            SRD
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     SRD           BEGSR
     C                   DELETE    SVIFZR

     C     SVIFKEY       SETLL     SVIVZL01
     C     SVIFKEY       READE     SVIVZL01                               56
     C     *IN56         DOWEQ     *OFF
     C                   DELETE    SVIVZR
     C     SVIFKEY       READE     SVIVZL01                               56
     C                   ENDDO
     C                   ENDSR
      *______________________________________________________
      * Update                                            SRU
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     SRU           BEGSR
     C                   EVAL      SVIF_SYAV=SYAV
     C                   EVAL      SVIF_SYOP=SYOP
     C                   IF        SVIF_FATY=*BLANKS
     C                   EVAL      SVIF_FATY='N380'
     C                   ENDIF
     C     SVTVKKEY      SETGT     SVTVK
     C                   READP     SVTVK                                  57
     C     *IN57         IFEQ      *OFF
     C                   EVAL      SVIF_VAKU=SVVK_KRS
     C                   ENDIF
     C                   UPDATE    SVIFZR
     C                   WRITE     SVIFR
      * Hitta høgsta varupostnummret
     C                   EVAL      *IN57=*OFF
     C     SVIVKEY       SETLL     SVIV
     C     SVIVKEY       READE     SVIV                                   57
     C     *IN57         DOWEQ     *OFF
     C                   EVAL      ZVIV_SYLI=SVIV_SYLI
     C     SVIVKEY       READE     SVIV                                   57
     C                   ENDDO
      * Skriv til produktionsfil

     C     SVIFKEY       SETLL     SVIVZL01
     C     SVIFKEY       READE     SVIVZL01                               56
     C     *IN56         DOWEQ     *OFF
     C                   IF        SVIV_SYAV=*ZEROS
     C                   EVAL      SVIV_SYAV=SYAV
     C                   EVAL      SVIV_SYOP=SYOP
     C                   ADD       1             ZVIV_SYLI
     C                   EVAL      SVIV_SYLI=ZVIV_SYLI
     C                   EVAL      SVIV_VANO=ZVIV_SYLI
     C                   EVAL      SVIV_CALL='00'
     C                   UPDATE    SVIVZR
     C                   EXSR      SRUP
     C                   ENDIF
     C     SVIFKEY       READE     SVIVZL01                               56
     C                   ENDDO
     C                   ENDSR
      *______________________________________________________
      * Update, skriv till produktionsfiler              SRUP
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     SRUP          BEGSR
N01  C                   IF        SVIV_BRUT > 1
N01  C                   EVAL(H)   W110=SVIV_BRUT
N01  C                   EVAL      SVIV_BRUT=W110
N01  C                   ENDIF
N01  C                   IF        SVIV_NETO > 1
N01  C                   EVAL(H)   W110=SVIV_NETO
N01  C                   EVAL      SVIV_NETO=W110
N01  C                   ENDIF
     C                   WRITE     SVIVR
     C                   ENDSR
