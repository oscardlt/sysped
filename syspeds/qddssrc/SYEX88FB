*****A**************************************************************
     A* RETTINGER I DETTE PROGRAM:
PTFnrA*Sek Sig Vers Beskrivelse...........................
"""""A*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10261 * 01 YBC PTF10 VEKT/KUBIKK/KOLLI I TURER ER ENDRET TIL 9.0, 7.3 OG 7.0
*****A**************************************************************
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPPNLGRP(HLPSCR SYEX88BPNL)
     A                                      HLPTITLE('ARBEIDE MED TURPLANLEGGIN-
     A                                      G')
     A                                      HELP
     A                                      ALTHELP
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  8  3VALUES(' ' '4' '5')
     A  98                                  DSPATR(RI)
     A            HEAVD          4Y 0O  8  5EDTCDE(4)
     A            HEOPD          7Y 0O  8 10EDTCDE(4)
     A            HENAS         30A  O  8 18
     A            SFNAVN        30A  O  8 49
     A            HENT           7Y 0O  9  6EDTCDE(4)
     A            HEVS1         25A  O  9 14
     A            HEVKT          9Y 0H
     A            W2VKT          5Y 0O  9 40EDTCDE(4)
     A            HEM3           7Y 3H
     A            W2M3           4Y 2O  9 46EDTCDE(4)
     A            HELM           4Y 2O  9 53EDTCDE(4)
     A            HESDF          5A  O  9 59
     A            HESDT          5A  O  9 65
     A            HEOT           2A  O  9 71
     A            HEFR           3A  O  9 75
     A            HEKDPL         1   O  9 79COLOR(WHT)
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0008)
     A                                      SFLPAG(0007)
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A                                  1  2'SYSPED/SYEX88B'
     A                                      COLOR(BLU)
     A  27                              1 22MSGCON(030 DIV0681 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A N27 31                           1 22MSGCON(030 DIV0682 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A N27N31                           1 22MSGCON(030 DIV0724 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  1 69TIME
     A                                      COLOR(BLU)
     A                                  2  2MSGCON(009 DIV0050 *LIBL/SYMSGF)
     A            TUAVD          4Y 0O  2 15EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  2 21MSGCON(010 DIV0659 *LIBL/SYMSGF)
     A            TUPRO          8Y 0O  2 32EDTCDE(4)
     A                                      COLOR(WHT)
     A            ZRECNO         4S 0O  2 35SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  2 53MSGCON(006 DIV0683 *LIBL/SYMSGF)
     A            TUSDF         20A  O  2 60COLOR(WHT)
     A                                  3  2MSGCON(008 DIV0684 *LIBL/SYMSGF)
     A            TUKNA          8Y 0O  3 11EDTCDE(4)
     A                                      COLOR(WHT)
     A            TUNAA         30A  O  3 21COLOR(WHT)
     A                                  3 53MSGCON(006 DIV0685 *LIBL/SYMSGF)
     A            TUSDT         20A  O  3 60COLOR(WHT)
     A                                  4  2MSGCON(008 DIV0686 *LIBL/SYMSGF)
     A            TUKNT          8Y 0O  4 11EDTCDE(4)
     A                                      COLOR(WHT)
     A            TUNAT         30A  O  4 21COLOR(WHT)
     A                                  4 53MSGCON(006 DIV0687 *LIBL/SYMSGF)
     A            TUTARF        15A  O  4 60COLOR(WHT)
     A                                  5  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5 35MSGCON(020 JOV0714 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5 60MSGCON(012 DIV0693 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  6  2MSGCON(078 DIV0689 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  7  6MSGCON(074 DIV0690 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A          R BUNN
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF03(03 'Avslutt')
     A N27                                  CF06(06 'TIL PLUKKING')
     A                                      CF12(12 'FORRIGE')
     A N27                                  CF16(16 'Hurtigplukk')
     A                                      SLNO(22)
     A                                      CLRL(03)
     A                                      OVERLAY
S01  A*                                 1  2MSGCON(015 DIV0691 *LIBL/SYMSGF)
S01  A*           WSNT           5Y 0O  1 18COLOR(WHT)
N01  A                                  1  2MSGCON(013 YBC1994 *LIBL/SYMSGF)
N01  A            WSNT           7Y 0O  1 16COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  1 24MSGCON(006 DIV0694 *LIBL/SYMSGF)
     A            WSVKT          9Y 0O  1 31COLOR(WHT)
     A                                      EDTCDE(4)
S01  A*                                 1 41MSGCON(006 DIV0695 *LIBL/SYMSGF)
S01  A*           WSM3           5Y 2O  1 48COLOR(WHT)
N01  A                                  1 41MSGCON(004 YBC1995 *LIBL/SYMSGF)
N01  A            WSM3           7Y 3O  1 46COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  1 55MSGCON(006 DIV0696 *LIBL/SYMSGF)
     A            WSLM           4Y 2O  1 62COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  1 69MSGCON(006 DIV0697 *LIBL/SYMSGF)
     A            WSNTO          5Y 0O  1 76COLOR(WHT)
     A                                      EDTCDE(4)
     A N27                              2 34MSGCON(018 DIV0692 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  3  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A                                  2 22MSGCON(010 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  2 70MSGCON(011 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  2 53MSGCON(015 TRO0201 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A          R NYWRK
     A                                      CF03(03 'Avslutt')
     A N27                                  CF06(06 'TIL PLUKKING')
     A                                      CF12(12 'FORRIGE')
     A N27                                  CF16(16 'Hurtigplukk')
     A                                  1  2'SYSPED/SYEX88B'
     A                                      COLOR(BLU)
     A  27                              1 22MSGCON(030 DIV0681 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A N27 31                           1 22MSGCON(030 DIV0682 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A N27N31                           1 22MSGCON(030 DIV0724 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  1 71TIME
     A                                      COLOR(BLU)
     A                                  2  2MSGCON(009 DIV0050 *LIBL/SYMSGF)
     A            TUAVD          4Y 0O  2 15EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  2 21MSGCON(010 DIV0659 *LIBL/SYMSGF)
     A            TUPRO          8Y 0O  2 32EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  2 53MSGCON(006 DIV0683 *LIBL/SYMSGF)
     A            TUSDF         20A  O  2 60COLOR(WHT)
     A                                  3  2MSGCON(008 DIV0684 *LIBL/SYMSGF)
     A            TUKNA          8Y 0O  3 11EDTCDE(4)
     A                                      COLOR(WHT)
     A            TUNAA         30A  O  3 21COLOR(WHT)
     A                                  3 53MSGCON(006 DIV0685 *LIBL/SYMSGF)
     A            TUSDT         20A  O  3 60COLOR(WHT)
     A                                  4  2MSGCON(008 DIV0686 *LIBL/SYMSGF)
     A            TUKNT          8Y 0O  4 11EDTCDE(4)
     A                                      COLOR(WHT)
     A            TUNAT         30A  O  4 21COLOR(WHT)
     A                                  4 53MSGCON(006 DIV0687 *LIBL/SYMSGF)
     A            TUTARF        15A  O  4 60COLOR(WHT)
     A                                  5  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5 35MSGCON(020 JOV0714 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5 60MSGCON(012 DIV0693 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  6  2MSGCON(078 DIV0689 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  7  6MSGCON(078 DIV0690 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
S01  A*                                22  2MSGCON(015 DIV0691 *LIBL/SYMSGF)
S01  A*           WSNT           5Y 0O 22 18COLOR(WHT)
N01  A                                 22  2MSGCON(013 YBC1994 *LIBL/SYMSGF)
N01  A            WSNT           7Y 0O 22 16COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 22 24MSGCON(006 DIV0694 *LIBL/SYMSGF)
     A            WSVKT          9Y 0O 22 31COLOR(WHT)
     A                                      EDTCDE(4)
S01  A*                                22 41MSGCON(006 DIV0695 *LIBL/SYMSGF)
S01  A*           WSM3           5Y 2O 22 48COLOR(WHT)
N01  A                                 22 41MSGCON(004 YBC1995 *LIBL/SYMSGF)
N01  A            WSM3           7Y 3O 22 48COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 22 55MSGCON(006 DIV0696 *LIBL/SYMSGF)
     A            WSLM           4Y 2O 22 62COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 22 69MSGCON(006 DIV0697 *LIBL/SYMSGF)
     A            WSNTO          5Y 0O 22 76COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 23 70MSGCON(011 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 34MSGCON(018 DIV0692 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(010 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 53MSGCON(015 TRO0201 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A          R BILDE01
     A                                      CF03(03 'Avslutt')
     A                                      CF12(12 'FORRIGE')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/SYEX88B'
     A                                      COLOR(BLU)
     A                                  1 20MSGCON(030 TRO0198 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  1 71TIME
     A                                      COLOR(BLU)
     A                                  2  2MSGCON(009 DIV0050 *LIBL/SYMSGF)
     A            TUAVD          4Y 0O  2 15EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  2 21MSGCON(010 DIV0659 *LIBL/SYMSGF)
     A            TUPRO          8Y 0O  2 33EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  2 53MSGCON(006 DIV0683 *LIBL/SYMSGF)
     A            TUSDF         20A  O  2 60COLOR(WHT)
     A                                  3  2MSGCON(008 DIV0684 *LIBL/SYMSGF)
     A            TUKNA          8Y 0O  3 11EDTCDE(4)
     A                                      COLOR(WHT)
     A            TUNAA         30A  O  3 21COLOR(WHT)
     A                                  3 53MSGCON(006 DIV0685 *LIBL/SYMSGF)
     A            TUSDT         20A  O  3 60COLOR(WHT)
     A                                  4  2MSGCON(008 DIV0686 *LIBL/SYMSGF)
     A            TUKNT          8Y 0O  4 11EDTCDE(4)
     A                                      COLOR(WHT)
     A            TUNAT         30A  O  4 21COLOR(WHT)
     A                                  4 53MSGCON(006 DIV0687 *LIBL/SYMSGF)
     A            TUTARF        15A  O  4 60COLOR(WHT)
     A                                 11 19MSGCON(011 TRO0199 *LIBL/SYMSGF)
     A            WFOAVD         4Y 0B 11 32EDTCDE(4)
NV5  A                                 11 48MSGCON(030 JOV0795 *LIBL/SYMSGF)
     A                                 13 19MSGCON(011 TRO0200 *LIBL/SYMSGF)
     A            WFOOPD         7Y 0B 13 31EDTCDE(4)
NV5  A                                 13 48MSGCON(030 JOV0796 *LIBL/SYMSGF)
NV5  A                                 15  3MSGCON(078 JOV0797 *LIBL/SYMSGF)
NV5  A                                 17 19MSGCON(011 JOV0798 *LIBL/SYMSGF)
NV5  A            WFOFBN         3Y 0B 17 31EDTCDE(4)
     A                                 23 25MSGCON(010 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 69MSGCON(011 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
