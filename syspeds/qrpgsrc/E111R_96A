     H DATEDIT(*YMD)
     FEDIM      UF   E           K DISK
     FEDIMD     IF   E           K DISK
     F                                     RENAME(EDIMR:EDIMRD)
     FEUNH      IF   E           K DISK
     FEDTM96A   IF   E           K DISK
     FENAD96A   IF   E           K DISK
     FELOC96A   IF   E           K DISK
     FECNT96A   IF   E           K DISK
     FETDT96A   IF   E           K DISK
     FHEADF     UF   E           K DISK
     FHEAD8     IF   E           K DISK
     F                                     RENAME(HEADFR:HEAD8R)
     FFIRM      IF   E             DISK
     FKUNKO9    IF   E           K DISK
     FCUNDL01   IF   E           K DISK
     FTELL      IF   E           K DISK
     FKODTSF    IF   E           K DISK
     FTURER     IF   E           K DISK
     FE111D     CF   E             WORKSTN
     F                                     SFILE(E111DB:ZRECNO)
     F                                     SFILE(E111DG:XRECNO)
     D CMD             S             80    DIM(1) CTDATA PERRCD(1)
     D LDADTA         UDS            26
     D  UFIRM                  1      2
     D  UAVD                  15     18
     D  USG                   19     21
     D  UOPD                  22     26
      */////////////
      * MAIN LOGIC /
      */////////////
      *___________
      * Initiering
      *"""""""""""
     C                   EXSR      INIT
      *______________
      * Clear Subfil
      *""""""""""""""
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     E111DC
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     0             ZRECNO
      *_____________
      * Fyll Subfil
      *"""""""""""""
     C     KEYTVD        SETLL     EDIMD
     C     KEYTVD        READE     EDIMD                                  94

     C     *IN94         DOWEQ     '0'

     C                   MOVE      *BLANKS       ZAVS
     C                   MOVE      *BLANKS       ZDEP
     C                   MOVE      *BLANKS       ZMOTT
     C                   MOVE      *BLANKS       ZDST
     C                   MOVE      *BLANKS       ZDT
     C                   MOVE      *BLANKS       ZNOSH
     C                   MOVE      *BLANKS       WMS

     C                   MOVE      '0'           *IN51
     C                   Z-ADD     MMN           ZMN
     C     ZMN           CHAIN     EUNH                               20
     C     *IN20         IFEQ      '0'
     C                   MOVEL     U1004         ZREF
     C                   ENDIF

     C                   MOVE      '0'           *IN51
     C                   Z-ADD     5             ZGN
     C     KEYMSG        CHAIN     ETDT96A                            20
     C     *IN20         IFEQ      '0'
     C                   MOVEL     T8212         ZBIL
     C                   ENDIF

     C                   MOVE      '0'           *IN51
     C                   Z-ADD     MMN           ZMN
     C                   Z-ADD     9             ZGN
     C     KEYMSG        SETLL     ENAD96A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMSG        READE     ENAD96A                                51
     C     *IN51         DOWEQ     '0'
     C     N3035         IFEQ      'MS '
     C                   MOVEL     N3036A        ZAVS
     C                   MOVEL     N3039         WMS
     C                   ELSE
     C     N3035         IFEQ      'MR '
     C                   MOVEL     N3036A        ZMOTT
     C                   ENDIF
     C                   ENDIF
     C     KEYMSG        READE     ENAD96A                                51
     C  N51              ENDDO
     C                   ENDIF

     C                   MOVE      '0'           *IN51
     C                   Z-ADD     5             ZGN
     C     KEYMSG        SETLL     ELOC96A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMSG        READE     ELOC96A                                51
     C     *IN51         DOWEQ     '0'
     C     L3227         IFEQ      '5  '
     C                   MOVEL     L3224         ZDEP
     C                   ELSE
     C     L3227         IFEQ      '8  '
     C                   MOVEL     L3224         ZDST
     C                   ENDIF
     C                   ENDIF
     C     KEYMSG        READE     ELOC96A                                51
     C  N51              ENDDO
     C                   ENDIF

     C                   MOVE      '0'           *IN51
     C                   Z-ADD     5             ZGN
     C     KEYMSG        SETLL     EDTM96A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMSG        READE     EDTM96A                                51
     C     *IN51         DOWEQ     '0'
     C     D2005         IFEQ      '132'
     C                   MOVEL     D2380         ZDT
     C                   ENDIF
     C     KEYMSG        READE     EDTM96A                                51
     C  N51              ENDDO
     C                   ENDIF

     C                   MOVE      '0'           *IN51
     C                   Z-ADD     0             ZGN
     C     KEYMSG        SETLL     ECNT96A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMSG        READE     ECNT96A                                51
     C     *IN51         DOWEQ     '0'
     C     C6069         IFEQ      '10 '
     C                   MOVEL     C6066         ZNOSH
     C                   ENDIF
     C     KEYMSG        READE     ECNT96A                                51
     C  N51              ENDDO
     C                   ENDIF

     C                   MOVE      '0'           *IN41
     C                   ADD       1             ZRECNO
     C                   WRITE     E111DB
     C     KEYTVD        READE     EDIMD                                  94
     C  N94              ENDDO

     C                   Z-ADD     ZRECNO        YRECNO            5 0
      *________________
      * Display Subfil
      *""""""""""""""""
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C     VISDE         TAG
     C                   MOVE      '0'           *IN91
     C                   MOVE      '1'           *IN93
     C                   EXFMT     E111DC
     C     *IN03         IFNE      '1'
     C     *IN12         ANDNE     '1'
     C                   GOTO      VISDE
     C                   END                                                    <*IN12

     C                   ELSE                                                   <ZRECNO
     C                   WRITE     E111DD
     C                   EXFMT     E111DC
     C                   END                                                    <ZRECNO

     C                   MOVEA     '0000000'     *IN(30)
     C                   MOVE      'SY00000'     MSGNR

     C                   SETOFF                                       9998
      *** F3=Gå ut
     C     *IN03         IFEQ      '1'
     C     *IN12         OREQ      '1'
     C                   GOTO      ENDPGM
     C                   END                                                    <*IN03
      *** F5=Frisk opp
     C     *IN05         IFEQ      '1'
     C                   GOTO      START
     C                   END                                                    <*IN03
      *** Finn valg
     C     ZRECNO        IFNE      0
     C                   EXSR      GETALT
      *** F3=Gå ut
     C     *IN03         IFEQ      '1'
     C                   GOTO      ENDPGM
     C                   ENDIF                                                  <*IN03
      *** F12=Annuller
     C     *IN12         IFEQ      '1'
     C                   GOTO      START3
     C                   ENDIF                                                  <*IN12
     C                   ENDIF                                                  <ZRECNO

     C                   GOTO      START3

     C     ENDPGM        TAG
     C                   MOVE      '1'           *INLR
     C                   RETURN
      *////////////////////////////////////////////////////////////
      *                                                    GETALT /
      *////////////////////////////////////////////////////////////
     C     GETALT        BEGSR

     C                   Z-ADD     0             OPTNBR            2 0
     C                   SETOFF                                       919299
     C     *IN98         DOWEQ     '0'
     C                   READC     E111DB                                 98
     C     *IN98         IFEQ      '1'
     C     *IN99         IFEQ      '1'
     C                   MOVE      '0'           *IN98
     C                   ENDIF                                                  <*IN99
     C                   GOTO      ENDGET
     C                   ENDIF                                                  <*IN98
     C                   MOVE      *OFF          *IN68
     C                   MOVE      *OFF          *IN70
      *** 1=Bekreft
     C     ZALT          IFEQ      '1'
     C                   MOVE      ZAVS          XAVS
     C                   MOVE      ZDEP          XDEP
     C                   MOVE      ZMOTT         XMOTT
     C                   MOVE      ZDST          XDST
     C                   MOVE      *BLANK        ZALT
     C                   MOVE      *BLANKS       ZAVS
     C                   MOVE      *BLANKS       ZDEP
     C                   MOVE      *BLANKS       ZMOTT
     C                   MOVE      *BLANKS       ZDST
     C                   MOVE      *BLANKS       ZDT
     C                   MOVE      *BLANKS       ZNOSH
     C                   MOVE      *BLANKS       WMS
     C                   MOVE      '1'           *IN41
     C                   UPDATE    E111DB
     C                   EXSR      CONFIR
     C                   Z-ADD     0             MMN
     C                   GOTO      ENDGET
     C                   ENDIF                                                  <WSNR
      *** 4=Avvis
     C     ZALT          IFEQ      '4'
     C                   MOVE      *BLANK        ZALT
     C                   MOVE      *BLANKS       ZAVS
     C                   MOVE      *BLANKS       ZDEP
     C                   MOVE      *BLANKS       ZMOTT
     C                   MOVE      *BLANKS       ZDST
     C                   MOVE      *BLANKS       ZDT
     C                   MOVE      *BLANKS       ZNOSH
     C                   MOVE      *BLANKS       WMS
     C                   MOVE      '1'           *IN41
     C                   UPDATE    E111DB
     C                   EXSR      AVVIS
     C                   GOTO      ENDGET
     C                   ENDIF                                                  <WSNR
      *** 5=Vis
     C     ZALT          IFEQ      '5'
     C     ZALT          OREQ      '6'
     C                   UPDATE    E111DB
     C     MMN           CHAIN(N)  EDIM                               90
     C                   MOVEL     'E112R_'      ZPGM             10
     C                   MOVE      '96A '        ZPGM
     C                   Z-ADD     MMN           ZMN
     C                   CALL      ZPGM
     C                   PARM                    ZMN
     C                   PARM                    ZALT

     C                   MOVE      *BLANK        ZALT
     C                   ENDIF

     C     *IN03         IFEQ      '1'
     C                   GOTO      ENDGET
     C                   ENDIF

     C                   ENDDO

     C     ENDGET        TAG
     C                   MOVE      *OFF          *IN54
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *                                                      INIT /
      *////////////////////////////////////////////////////////////
     C     INIT          BEGSR

     C     KEYTVD        KLIST
     C                   KFLD                    ZST
     C                   KFLD                    ZSR
     C                   KFLD                    Z0065
     C     KEYMSG        KLIST
     C                   KFLD                    ZMN
     C                   KFLD                    ZGN
     C     TURKEY        KLIST
     C                   KFLD                    ÆEAVD
     C                   KFLD                    ÆEPRO
     C     HEAKEY        KLIST
     C                   KFLD                    XEAVD
     C                   KFLD                    XEOPD
     C     KUNKEY        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    WKUNDN
     C     KODSFK        KLIST
     C                   KFLD                    WSSFUN
     C                   KFLD                    ÆSSG
     C     *LIKE         DEFINE    MST           ZST
     C     *LIKE         DEFINE    MSR           ZSR
     C     *LIKE         DEFINE    M0065         Z0065
     C     *LIKE         DEFINE    NMN           ZMN
     C     *LIKE         DEFINE    NGN           ZGN
     C     *LIKE         DEFINE    HEAVD         ÆEAVD
     C     *LIKE         DEFINE    HEPRO         ÆEPRO
     C     *LIKE         DEFINE    KUINTN        WUINTN
     C     *LIKE         DEFINE    KUNDNR        WKUNDN
     C                   Z-ADD     0             PPUNIK            9 0
     C                   MOVEL     *BLANKS       PPREFF           17
     C                   MOVE      ' '           ZST
     C                   MOVE      'R'           ZSR
     C                   MOVE      'IFCSUM'      Z0065
     C                   MOVE      'SF'          WSSFUN            2
     C     *DTAARA       DEFINE    *LDA          LDADTA
     C                   READ      FIRM                                   20
     C                   MOVE      FIFIRM        UFIRM
     C                   IN        LDADTA
     C                   MOVE      UAVD          ÆSAVD
     C                   MOVE      USG           ÆSSG
     C                   OUT       LDADTA
      * OVERRIDE MSGF
     C                   MOVE      CMD(1)        QCMDA            80
     C                   Z-ADD     80            QCMDL            15 5
     C                   CALL      'QCMDEXC'
     C                   PARM                    QCMDA
     C                   PARM                    QCMDL

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *                                                    CONFIR /
      *////////////////////////////////////////////////////////////
     C     CONFIR        BEGSR

     C     CON01         TAG

     C                   EXFMT     E111DF
     C     *IN03         IFEQ      '1'
     C     *IN12         OREQ      '1'
     C                   GOTO      CON99
     C                   ENDIF
     C     *IN04         IFEQ      '1'
     C                   EXSR      SOEK
     C                   GOTO      CON01
     C                   ENDIF

     C                   MOVEA     '000000'      *IN(30)
     C                   MOVE      'SY00000'     MSGNR

     C     ÆSAVD         CHAIN     TELL                               30
     C     *IN30         IFEQ      '1'
     C                   ENDIF

     C     KODSFK        CHAIN     KODTSF                             30
     C     *IN30         IFEQ      '1'
     C                   MOVE      '1'           *IN32
     C                   MOVE      'SPS0001'     MSGNR
     C                   GOTO      CON01
     C                   ENDIF

     C     ÆTURNR        IFEQ      0
     C     ÆAGNR         ANDEQ     0
     C                   MOVEL     WMS           WUINTN
     C     WUINTN        CHAIN     KUNKO9                             20
     C     *IN20         IFEQ      '0'
     C                   Z-ADD     KUKUN2        ÆAGNR
     C                   ENDIF
     C     ÆAGNR         IFEQ      0
     C                   MOVEA     '100001'      *IN(30)
     C                   MOVE      'DIV2547'     MSGNR
     C                   GOTO      CON01
     C                   ENDIF
     C                   Z-ADD     ÆAGNR         WKUNDN
     C     KUNKEY        CHAIN     CUNDL01                            30
     C     *IN30         IFEQ      '1'
     C                   MOVEA     '100001'      *IN(30)
     C                   MOVE      'DIV2548'     MSGNR
     C                   GOTO      CON01
     C                   ENDIF
     C                   ENDIF

     C     ÆTURNR        IFEQ      0
     C                   Z-ADD     ÆAGNR         WKUNDN
     C     KUNKEY        CHAIN     CUNDL01                            30
     C     *IN30         IFEQ      '1'
     C                   MOVEA     '100001'      *IN(30)
     C                   MOVE      'DIV2548'     MSGNR
     C                   GOTO      CON01
     C                   ENDIF
     C                   ENDIF

     C     ÆTURNR        IFGT      0
     C                   Z-ADD     ÆSAVD         ÆEAVD
     C                   Z-ADD     ÆTURNR        ÆEPRO
     C     TURKEY        CHAIN     TURER                              30
     C     *IN30         IFEQ      '1'
     C                   MOVE      '1'           *IN34
     C                   MOVE      'DIV2422'     MSGNR
     C                   GOTO      CON01
     C                   ENDIF
     C                   ENDIF
      *
      *_____________________
      * CONVERT TO SYSPED400
      *"""""""""""""""""""""
     C                   MOVEL     'E113R_'      WPGM             10
     C                   MOVE      '96AB'        WPGM
     C     ÆWRKOP        IFEQ      'Y'
     C     ÆWRKOP        OREQ      'J'
     C                   MOVEL     ÆSSG          ÆSSG
     C                   ELSE
     C                   MOVEL     'EDI'         ÆSSG
     C                   ENDIF
     C                   Z-ADD     ÆSAVD         WEAVD             4 0
     C                   Z-ADD     ÆTURNR        WEPRO             8 0
     C                   Z-ADD     ÆAGNR         WAGNR             8 0
     C                   MOVEL     'E113R_'      ZPGM
     C                   MOVE      '96AB'        ZPGM
     C                   Z-ADD     MMN           ZMN
     C                   CALL      ZPGM
     C                   PARM                    ZMN
     C                   PARM                    WEAVD
     C                   PARM                    WEPRO
     C                   PARM                    ÆSSG
     C                   PARM                    WAGNR
     C                   PARM                    ÆEGN
     C                   Z-ADD     WEPRO         ÆTURNR
      *____________
      * Update EDIM
      *""""""""""""
     C     MMN           CHAIN     EDIM                               90
     C     *IN90         IFEQ      '0'
     C                   MOVEL     'C'           MST
     C                   Z-ADD     HEAVD         MAVD
     C                   Z-ADD     0             MTDN
     C                   UPDATE    EDIMR
     C                   ENDIF                                                  <*IN90
      *_______________________________
      * CALL HEADF Maintance program ?
      *""""""""""""""""""""""""""""""""
     C     ÆWRKOP        IFEQ      'Y'
     C     ÆWRKOP        OREQ      'J'
     C                   IN        LDADTA
     C                   MOVE      FIFIRM        UFIRM
     C                   MOVE      ÆSAVD         UAVD
     C                   MOVE      ÆSSG          USG
     C                   OUT       LDADTA

     C  N90
     CAN 90              EXSR      WRKTU
     C                   EXSR      WRKOP

     C                   ENDIF
      *
     C     CON99         ENDSR
      *////////////////////////////////////////////////////////////
      *                                                     AVVIS /
      *////////////////////////////////////////////////////////////
     C     AVVIS         BEGSR
      *____________
      * Update EDIM
      *""""""""""""
     C     MMN           CHAIN     EDIM                               90
     C     *IN90         IFEQ      '0'
     C                   MOVEL     'C'           MST
     C                   UPDATE    EDIMR
     C                   END

     C     AVV99         ENDSR
      *////////////////////////////////////////////////////////////
      * Arbeide med oppdrag                                 WRKOP /
      *////////////////////////////////////////////////////////////
     C     WRKOP         BEGSR
      *______________
      * Clear Subfil
      *""""""""""""""
     C     WRKOP1        TAG
     C                   MOVE      '1'           *IN83
     C                   WRITE     E111DH
     C                   MOVEA     '00'          *IN(83)
     C                   Z-ADD     0             XRECNO
      *_____________
      * Fyll Subfil
      *"""""""""""""
     C                   Z-ADD     WEAVD         ÆEAVD
     C                   Z-ADD     WEPRO         ÆEPRO
     C     TURKEY        SETLL     HEAD8
     C     TURKEY        READE     HEAD8                                  84

     C     *IN84         DOWEQ     '0'

     C     HEST          IFNE      'S'
     C                   MOVE      ' '           XALT
     C                   MOVE      HEAVD         XEAVD
     C                   MOVE      HEOPD         XEOPD
     C                   MOVE      HEPOS1        XEPOS
     C                   MOVEL     HENAK         XENAK
     C                   MOVEL     HENAS         XENAS
     C                   ADD       1             XRECNO
     C                   WRITE     E111DG
     C                   ENDIF
     C     TURKEY        READE     HEAD8                                  84
     C  N84              ENDDO

     C                   Z-ADD     XRECNO        ØRECNO            5 0
      *________________
      * Display Subfil
      *""""""""""""""""
     C                   MOVEA     '11'          *IN(81)
     C     ØRECNO        IFEQ      0
     C                   GOTO      WRKOP9
     C                   ELSE                                                   <XRECNO
     C                   Z-ADD     1             XRECNO
     C                   WRITE     E111DI
     C                   EXFMT     E111DH
     C                   ENDIF                                                  <XRECNO

     C                   MOVEA     '00'          *IN(30)

     C                   SETOFF                                       8889
      *** F12 FORRIGE
     C   12              GOTO      WRKOP9
      *** Finn valg
     C                   SETOFF                                       818299
     C                   SETOFF                                       88
     C     *IN88         DOWEQ     '0'

     C                   READC     E111DG                                 88
     C     *IN88         IFEQ      '0'

     C     XALT          IFEQ      '2'
     C                   IN        LDADTA
     C                   MOVE      XEOPD         UOPD
     C                   OUT       LDADTA
     C                   CALL      'SYFA10'
     C                   PARM                    XEAVD
     C                   PARM                    XEOPD
     C                   PARM                    ÆSSG
     C                   PARM      'CHG'         UTYPE             3
     C                   PARM      ' '           UKODE             1
     C                   PARM      ' '           UDUMMY            1
     C                   PARM                    UAVDNY            4 0
     C                   PARM                    PPUNIK
     C                   PARM                    PPREFF
     C     HEAKEY        CHAIN     HEADF                              20
     C     *IN20         IFEQ      '0'

     C                   MOVE      ' '           HEST
     C                   UPDATE    HEADFR

     C                   ENDIF
     C                   ENDIF

     C     XALT          IFEQ      '4'
     C     HEAKEY        CHAIN     HEADF                              20
     C     *IN20         IFEQ      '0'

     C                   MOVE      'S'           HEST
     C                   UPDATE    HEADFR

     C                   ENDIF
     C                   ENDIF

     C                   ENDIF
     C  N88              ENDDO

     C                   GOTO      WRKOP1

     C     WRKOP9        ENDSR
      *////////////////////////////////////////////////////////////
      * Arbeide med tur                                     WRKTU /
      *////////////////////////////////////////////////////////////
     C     WRKTU         BEGSR
     C                   CALL      'SYEX88A'
     C                   PARM                    FIFIRM
     C                   PARM                    WEAVD
     C                   PARM                    WEPRO
     C                   PARM      'N'           USPØR             1
     C                   PARM      'J'           UIMP              1
     C                   PARM      0             UFLAGG            1 0
     C                   ENDSR
      ***********************************************
     C     SOEK          BEGSR
      ***********************************************
     C                   Z-ADD     ÆAGNR         UAGNR             8 0
     C                   CALL      'SOEKKUN'
     C                   PARM                    UAGNR
     C                   PARM                    FIFIRM
     C                   Z-ADD     UAGNR         ÆAGNR
     C                   ENDSR
**
OVRMSGF MSGF(QUSERMSG) TOMSGF(SYMSGF)
