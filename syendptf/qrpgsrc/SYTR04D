      *
      ***********************************************************
      *** PROGRAM FOR VEDL AVD/AGENT (TRANSP)
      ***********************************************************
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
12XXX * 01 JOV PTF12 visning far record 1 ...
********************************************************************
      *
     FKODTAG2   UF A E           K DISK
     FFIRM      IF   E           K DISK
     FTRAN      IF   E           K DISK
     FSYTR04FD  CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D                 DS
     D  VMNAV1                 1     15
     D  VMNAV7                 1      7
     C*
     C     TRANKE        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    WSTRNR
     C     TRANKE2       KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    WSDFTR
     C     AVDTRK        KLIST
     C                   KFLD                    WSAVD
     C                   KFLD                    WSTRNR
     C     *LOVAL        SETLL     FIRM
     C                   READ      FIRM                                   33
     C*
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     *LOVAL        SETLL     KODTAG2
     C                   READ(N)   KODTAG2                                94
     C     *IN94         DOWEQ     '0'
     C                   ADD       1             ZRECNO
     C                   MOVE      AGTRNR        WSTRNR
     C     TRANKE        CHAIN     TRAN                               33
     C   33              MOVE      *BLANKS       VMNAVN
     C                   MOVEL     VMNAVN        VMNAV1
     C                   MOVE      AGDFTR        WSDFTR
     C     TRANKE2       CHAIN     TRAN                               33
     C   33              MOVE      *BLANKS       VMNAVN
     C                   MOVEL     VMNAVN        VMNAV2
     C                   WRITE     SUBFIL
     C                   READ(N)   KODTAG2                                94
     C  N94              END
     C*
     C                   Z-ADD     ZRECNO        YRECNO            5 0
S01  C**                 IF        ZRECNO = 1
     C                   Z-ADD     1             ZRECNO
S01  C**                 END
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     3             POSNBR
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYWRK
     C                   ELSE
     C                   WRITE     SUBCTL
     C                   EXFMT     BUNN
     C                   END
     C*
     C***
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C                   Z-ADD     WSSUBN        XRECNO            4 0
     C     XRECNO        IFEQ      0
     C                   Z-ADD     ZRECNO        XRECNO            4 0
     C                   END
     C***
     C*
     C                   SETOFF                                       9899
     C*
     C*** AVSLUTT
     C   03              GOTO      SLUTT
     C*
     C*** FRISK OPP
     C   05              GOTO      START
     C*
     C*** LAGE NY
     C   06              DO
     C                   Z-ADD     22            LINNBR
     C*                  Z-ADD     1             POSNBR
     C                   Z-ADD     5             POSNBR
     C                   MOVE      *ZEROS        WSAVD
     C                   MOVE      *ZEROS        WSTRNR
     C                   GOTO      START5
     C                   END
     C*
     C     ZRECNO        IFNE      0
     C                   EXSR      HENT
     C                   END
     C   98XRECNO        IFNE      0
     C                   Z-ADD     XRECNO        ZRECNO
     C                   END
     C                   GOTO      START3
     C*
     C*** LAGE NY
     C     START5        TAG
     C                   EXFMT     VARELI
     C                   SETOFF                                       309089
     C                   SETOFF                                       88
     C     *IN04         IFEQ      '1'
     C                   EXSR      SØK
     C                   GOTO      START5
     C                   END
     C   12              EXSR      MOVB2
     C  N12              EXSR      TESTB1
     C  N12
     CAN 30              GOTO      START5
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     3             POSNBR
     C                   GOTO      START3
     C*
     C*
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
     C*
     C***********************************************
     C     TESTB1        BEGSR
     C***********************************************
     c                   movea     '0000'        *in(87)
      * *IN40 OFF = NY
     C     TRANKE        CHAIN     TRAN                               90
      * Agent 0 = ok / (posten er da kun dft fransp. på gitt avdeling)
     c     *in90         IFEQ      *ON
     c     WSDFTR        andne     *zeros
     c     WSTRNR        andeq     *zeros
     c                   goto      tsttra2
     c                   end
     C  N40              DO
     C     WSAVD         IFLT      9998
     C     WSAVD         CHAIN     KODTAG2                            89
     C                   END
     C   90              MOVE      'JOV1411'     MSGNR
     C   90              Z-ADD     10            POSNBR
     C  N90AVDTRK        CHAIN(N)  KODTAG2                            33
     C  N90
     CANN33              DO
     C                   SETON                                        90
     C                   MOVE      'JOV1215'     MSGNR
     C                   Z-ADD     5             POSNBR
     C                   END
     C   90              SETON                                        30
     C                   END
     C     *IN90         IFEQ      *OFF
     C                   MOVE      VMTRKU        WSTRKU
     C                   MOVE      VMTRLE        WSTRLE
     C                   ELSE
     C                   MOVE      *BLANKS       VMNAVN
     C                   END
     c                   movel     VMNAVN        VMNAV1
     c     tsttra2       tag
     C     WSDFTR        ifne      *zeros
     C     TRANKE2       CHAIN     TRAN                               87
     C     *in87         IFEQ      *ON
     C                   MOVE      'JOV1412'     MSGNR
     C                   Z-ADD     60            POSNBR
     C                   SETON                                        30
     c                   end
     c                   end
     C*
     C   30              GOTO      SLTB
     C*
     C*** OPPDATERE PÅ REGISTER OG SUBFIL
     C     AVDTRK        CHAIN     KODTAG2                            33
     C                   EXSR      MOVWSF
     C  N33              UPDATE    KODTAGR
     C   33              WRITE     KODTAGR
     C   40              UPDATE    SUBFIL
     C  N40              ADD       1             YRECNO
     C  N40              Z-ADD     YRECNO        ZRECNO
     C  N40              WRITE     SUBFIL
     C                   WRITE     SUBCTL
     C                   EXSR      MOVB2
     C*
     C     SLTB          ENDSR
     C*
     C***********************************************
     C     HENT          BEGSR
     C***********************************************
     C                   Z-ADD     0             LINNBR
     C                   SETOFF                                       919299
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C   98              DO
     C   99              SETOFF                                       98
     C                   Z-ADD     1             LINNBR
     C                   GOTO      SLHENT
     C                   END
     C     ZRECNO        DIV       14            LINNBR
     C                   MVR                     XLIN              2 0
     C     XLIN          IFEQ      0
     C                   Z-ADD     14            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C     WSNR          IFEQ      '2'                                          ENDRE
     C                   MOVE      ' '           WSNR
     C     VMNAV7        COMP      '*DELETE'                              98
     C   98              GOTO      SLHENT
     C                   EXSR      CHGSF
     C   12              GOTO      SLHENT
     C                   END
     C     WSNR          IFEQ      '4'                                          SLETTE
     C                   MOVE      ' '           WSNR
     C     VMNAV7        COMP      '*DELETE'                              98
     C   98              GOTO      SLHENT
     C                   EXSR      DLTSF
     C   12              GOTO      SLHENT
     C                   END
     C                   END
     C*
     C     SLHENT        TAG
     C                   ADD       5             LINNBR
     C                   Z-ADD     3             POSNBR
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     DLTSF         BEGSR
     C***********************************************
     C                   SETON                                        99
     C                   EXFMT     VAREL3
     C  N12              DO
     C                   MOVE      AGAVD         WSAVD
     C                   MOVE      AGTRNR        WSTRNR
     C     AVDTRK        CHAIN     KODTAG2                            33
     C  N33              DELETE    KODTAGR
     C                   EXSR      MOVB1
     C                   END
     C     XLIN          IFEQ      0
     C                   Z-ADD     14            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C                   UPDATE    SUBFIL
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     CHGSF         BEGSR
     C***********************************************
     C                   EXSR      MOVSFW
     C                   SETON                                        99
     C     STCHG         TAG
     C                   EXFMT     VARELI
     C                   SETOFF                                       309089
     C                   SETOFF                                       88
     C     *IN04         IFEQ      '1'
     C                   EXSR      SØK
     C                   GOTO      STCHG
     C                   END
     C   12              DO
     C                   UPDATE    SUBFIL
     C                   EXSR      MOVB2
     C                   END
     C  N12              EXSR      TESTB1
     C  N12
     CAN 30              GOTO      STCHG
     C     XLIN          IFEQ      0
     C                   Z-ADD     14            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     SØK           BEGSR
     C***********************************************
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C*
     C     POSNBR        IFLE      8
     C     *IN40         ANDEQ     '0'                                           KUN V/NY
     C                   CALL      'SYFA14B'
     C                   PARM                    WSAVD
     C                   Z-ADD     11            POSNBR
     C                   GOTO      UTSØK
     C                   ENDIF
      *
     C     POSNBR        IFGE      11
     C     POSNBR        ANDLE     50
     C     *IN40         ANDEQ     '0'                                           KUN V/NY
     C                   CALL      'SYTR04A'
     C                   PARM                    WSTRNR
     C                   Z-ADD     53            POSNBR
     C                   GOTO      UTSØK
     C                   ENDIF
     C*
     C     POSNBR        IFGE      60
     C     POSNBR        ANDLE     79
     C                   CALL      'SYTR04A'
     C                   PARM                    WSDFTR
     C                   Z-ADD     60            POSNBR
     C                   GOTO      UTSØK
     C                   ENDIF
      *
     C     UTSØK         ENDSR
     C*
     C***********************************************
     C     MOVB1         BEGSR
     C***********************************************
     C                   MOVE      *ZEROS        AGAVD
     C                   MOVE      *ZEROS        AGTRNR
     C                   MOVE      *BLANKS       VMNAV1
     C                   MOVEL     '*DELETE'     VMNAV7
     C                   MOVE      *ZEROS        AGTRKU
     C                   MOVE      *ZEROS        AGTRLE
     C                   MOVE      *BLANKS       AGPNRT
     C                   MOVE      *ZEROS        AGDFTR
     C                   MOVE      *BLANKS       VMNAV2
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVB2         BEGSR
     C***********************************************
     C                   SETOFF                                       40
     C                   MOVE      *ZEROS        WSAVD
     C                   MOVE      *ZEROS        WSTRNR
     C                   MOVE      *BLANKS       VMNAV1
     C                   MOVE      *ZEROS        WSTRKU
     C                   MOVE      *ZEROS        WSTRLE
     C                   MOVE      *BLANKS       WSPNRT
     C                   MOVE      *ZEROS        WSDFTR
     C                   MOVE      *BLANKS       VMNAV1
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVSFW        BEGSR
     C***********************************************
     C                   MOVE      AGAVD         WSAVD
     C                   MOVE      AGTRNR        WSTRNR
     C     TRANKE        CHAIN     TRAN                               33
     C   33              MOVE      *BLANKS       VMNAVN
     C                   movel     VMNAVN        VMNAV1
     C                   MOVE      AGTRKU        WSTRKU
     C                   MOVE      AGTRLE        WSTRLE
     C                   MOVE      AGPNRT        WSPNRT
     C                   MOVE      AGDFTR        WSDFTR
     C     TRANKE2       CHAIN     TRAN                               33
     C   33              MOVE      *BLANKS       VMNAVN
     C                   movel     VMNAVN        VMNAV2
     C                   SETON                                        40
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     53            POSNBR
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVWSF        BEGSR
     C***********************************************
     C   33              DO
     C                   MOVE      WSAVD         AGAVD
     C                   MOVE      WSTRNR        AGTRNR
     C                   END
     C*
     C                   MOVE      WSTRKU        AGTRKU
     C                   MOVE      WSTRLE        AGTRLE
     C                   MOVE      WSPNRT        AGPNRT
     C                   MOVE      WSDFTR        AGDFTR
     C                   MOVE      *BLANKS       VMNAVN
     C     TRANKE        CHAIN     TRAN
     C                   MOVEL     VMNAVN        VMNAV1
     C                   MOVE      *BLANKS       VMNAVN
     C     TRANKE2       CHAIN     TRAN
     C                   MOVEL     VMNAVN        VMNAV2
     C*
     C                   ENDSR
     C*
