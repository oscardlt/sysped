      *********************************************************************
      *
      *
CRTPG+*  CRTPGM SYSPED/SYOPKW1 MODULE(*LIBL/SYOPKW1 *LIBL/INCGI)
CRTPGM*         ACTGRP(CGI) AUT(*USE)
      *
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FHENOP02   IF   E           K DISK    USROPN
     FHENOF     IF   E           K DISK    USROPN
     FHENOS04   IF   E           K DISK    USROPN
     FHENOS05   IF   E           K DISK    USROPN
     F                                     RENAME(HENOSR:HENOS05R)
      *********************************************************************
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      /copy syspeds/qrpgsrcpy,variables3
      *
OddEvD OddEven         s             10
      * Client input variables
     D user            s             10
     D Term            s              4
     D dager           s              3
     D BlankIco        s             20a   inz('blank.gif')
     D MeldIco         s             20a   inz('melding.gif')
     D FarlIco         s             20a   inz('farlig.gif')
     D VarmIco         s             20a   inz('varme.gif')
      *
     D Spaces          s             12a   inz('&nbsp;&nbsp;')
     D ItemCount       s             10i 0
     D i               s             10i 0
      *
      /copy syspeds/qrpgsrcpy,prolog3
      *
      * Get program start time for calculating execution time
     C                   time                    timedata1
      * Parse input / cvt to numeric
     C                   exsr      Parse
      * File open / keys
     C                   EXSR      INIT
      * Read output skeleton html member into memory
     C                   exsr      LoadHtml
      * Set section variables
     C                   exsr      Settop
     C                   callp     wrtsection('top')
      *
      * HOVEDRUTINE
      *
      * Vis liste over registrete leveringer
      *
     c     TermSta       setLL     HENOP02
     c     TermSta       ReadE     HENOP02                                35
      *
     c     *IN35         doweq     '0'
      * Ok - skriv
     C     HOMINT        ifne      *zeros
     C                   exsr      Setrow
     C                   callp     wrtsection('row')
     c                   end
     c     TermSta       ReadE     HENOP02                                35
      *
     c     NextRec       Tag
     c                   end
     C     UTLES         TAG
      * Quit
      * Compute run time
     C                   time                    timedata2
     C     timedata2     subdur    timedata1     ms:*ms
     C                   eval      sec = ms / 1000000
     C                   callp     updhtmlvar('runtime':
     C                             %trim(%editw(sec:'     0 .   ')))
      *
     C                   callp     wrtsection('bot')
      *
     C                   exsr      Exit
      *
     C                   eval      *inlr = *on
     C                   return
      *
      *
      *=====================================================================
      * Parse input / cvt to numeric
      *=====================================================================
     C     Parse         begsr
      * Parse variables from QUERY_STRING environment variable
     C                   eval      term     = zhbgetvar('term')
     c     term          IFEQ      '1000'
     c                   move      'Y'           TermSta           1
     c                   else
     c                   move      'Z'           TermSta           1
     c                   end
     C                   eval      dager   = zhbgetvar('dager')
      * Userid.....
     C                   eval      user = zhbgetvar('user')
     C                   endsr
      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   CLOSE     BRIDF
     C                   CLOSE     HENOP02
     C                   CLOSE     HENOF
     C                   CLOSE     HENOS04
     C                   CLOSE     HENOS05
     C                   endsr
      *=====================================================================
      * Read output skeleton html member into memory
      *=====================================================================
     C     LoadHtml      begsr
     C                   callp     gethtml('HTMLSRC':
     C                             '*LIBL':
     C                             'SYOPKW1':'/CGITAG/')
     C                   endsr
      *=====================================================================
      *  Set variable data for section /$top
      *=====================================================================
     C     SetTop        begsr
      *
OddEvC                   callp     updHTMLvar('ROWHEAD':RowHead)
OddEvC                   callp     updHTMLvar('LogoImg':LogoImg)
     C                   callp     updHTMLvar('USER':USER)
     C                   callp     updHTMLvar('TERM':TERM)
      *
     C                   endsr
      *=====================================================================
      *  Set variable data for section /$row
      *=====================================================================
     C     Setrow        begsr
OddEvc     OddEven       IFEQ      ODD
OddEvc                   eval      OddEven = EVEN
OddEvc                   else
OddEvc                   eval      OddEven = ODD
OddEvc                   end
OddEvC                   callp     updHTMLvar('ODDEVEN':OddEven)
OddEv *
     C                   callp     updHTMLvar('HOKNS':
     C                             %trim(%editc(HOKNS:'Z')))
     C                   callp     updHTMLvar('dager':dager)
     C                   callp     updHTMLvar('HONAS':HONAS)
      * HOMINT "lånt" for ant. sendinger
     C                   callp     updHTMLvar('HOMINT':
     C                             %trim(%editc(HOMINT:'Z')))
      * HOMINK "lånt" for ant. Kolli
     C                   callp     updHTMLvar('HOMINK':
     C                             %trim(%editc(HOMINK:'Z')))
     C                   callp     updHTMLvar('HOVKT':
     C                             %trim(%editc(HOVKT:'Z')))
     C                   callp     updHTMLvar('HOM3':
     C                             %trim(%editc(HOM3:'3')))
     C                   callp     updHTMLvar('HOLM':
     C                             %trim(%editc(HOLM:'3')))
      * Melding til transportør?? på minst en av sendingene..
     c     HOLNR         chain     henof                              33
     C     *in33         ifeq      *ON
     C                   callp     updHTMLvar('MELDICO':BlankIco)
     c                   else
     C                   callp     updHTMLvar('MELDICO':MeldIco)
     c                   end
      * Farlig gods ??            på minst en av sendingene..
     c     HOLNR         chain     henos04                            33
     C     *in33         ifeq      *ON
     C                   callp     updHTMLvar('FARLICO':BlankIco)
     c                   else
     C                   callp     updHTMLvar('FARLICO':FarlIco)
     c                   end
      * Varmegods ??              på minst en av sendingene..
     c     HOLNR         chain     henos05                            33
     C     *in33         ifeq      *ON
     C                   callp     updHTMLvar('VARMICO':BlankIco)
     c                   else
     C                   callp     updHTMLvar('VARMICO':VarmIco)
     c                   end
      *
     C                   endsr
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
     C                   OPEN      BRIDF
     C     USER          CHAIN     BRIDF                              50
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
      *
OddEv * hent Parametre som går igjen i mange prog:
OddEv *      Odd/Even/Rowhead-farger,Logobilde,Språk,Intern/Ekstern bruker,  mm
OddEvc                   call      'ESGETPAR'
OddEvc                   parm                    BIBRID
OddEvc                   parm                    BIFIRM
OddEvc                   parm                    BIKUNR
OddEvc                   parm                    BIKNG
OddEvc                   parm                    RowHead          10
OddEvc                   parm                    Odd              10
OddEvc                   parm                    Even             10
OddEvc                   parm                    LogoImg          50
OddEvc                   parm                    XSPRÅK            2
OddEvc                   parm                    XINTERN           1
OddEvc                   parm                    ParmDS1        1024
OddEvc                   parm                    ParmDS2        1024
OddEvc                   parm                    ParmDS3        1024
      *
     C                   OPEN      HENOP02
     C                   OPEN      HENOF
     C                   OPEN      HENOS04
     C                   OPEN      HENOS05
      *
     C                   endsr
      *
