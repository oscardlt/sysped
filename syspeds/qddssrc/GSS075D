      *
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr *Sek Sig Vers Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
P00XX * 01 JOV VER5 REKKEFØLGE PÅ KOSTN.KONTOER I SUBFILA FEIL..
P8068 * 02 TH  PTF8 NY HELPFUNKSJON  KUN DDS
********************************************************************
      *
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPPNLGRP(HLPSCR SYFA04PNL)
     A                                      HLPTITLE('VEDLIKEHOLD AV GEBYR/FAKT-
     A                                      URAKODER')
     A                                      HELP
     A                                      ALTHELP
      *
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  8  2VALUES(' ' '2' '4')
     A            DGLOP          3S 0H      TEXT('LOPENO')
     A            DGPSN         60A  H      TEXT('PSN')
     A            DGPSN2        60A  H      TEXT('PSN2')
     A            DGADD         40A  H      TEXT('ADD INFO')
     A            DGST      R        O  8  4REFFLD(GSSDGFR/DGST *LIBL/GSSDGF)
     A            DGONO     R        O  8  6REFFLD(GSSDGFR/DGONO *LIBL/GSSDGF)
     A            DGUNNR    R        O  8 14REFFLD(GSSDGFR/DGUNNR *LIBL/GSSDGF)
     A            DGPS40        40A  O  8 19
     A            DGCOG     R        O  8 60REFFLD(GSSDGFR/DGCOG *LIBL/GSSDGF)
     A            DGPG      R        O  8 69REFFLD(GSSDGFR/DGPG *LIBL/GSSDGF)
     A            DGTR      R        O  8 74REFFLD(GSSDGFR/DGTR *LIBL/GSSDGF)
     A            DGNOP     R        O  9  1REFFLD(GSSDGFR/DGNOP *LIBL/GSSDGF)
     A            DGKOP     R        O  9  5REFFLD(GSSDGFR/DGKOP *LIBL/GSSDGF)
     A            DGGWK     R        O  9 26REFFLD(GSSDGFR/DGGWK *LIBL/GSSDGF)
     A                                      EDTWRD('      0,  ')
     A            DGNW      R        O  9 37REFFLD(GSSDGFR/DGNW *LIBL/GSSDGF)
     A                                      EDTWRD('      0,  ')
     A            DGNWT     R        O  9 48REFFLD(GSSDGFR/DGNWT *LIBL/GSSDGF)
     A            DGPFRT    R        O  9 50REFFLD(GSSDGFR/DGPFRT *LIBL/GSSDGF)
     A            DGCLC     R        O  9 56REFFLD(GSSDGFR/DGCLC *LIBL/GSSDGF)
     A            DGLC      R        O  9 60REFFLD(GSSDGFR/DGLC *LIBL/GSSDGF)
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0008)
     A                                      SFLPAG(0007)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A            ZRECNO         4S 0O  2 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1 52DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  1 63TIME
     A                                      COLOR(BLU)
     A                                  1  3'SHIPSYS/GSS075D'
     A                                      COLOR(BLU)
     A                                  1 20'Work with dangerous goods'
     A                                      COLOR(WHT)
     A                                  4  3'Options:  2=Change   4=Delete'
     A                                      COLOR(BLU)
     A                                  6  1'Op S Orderno UnNo Proper shipping -
     A                                      name                     ClGo     P-
     A                                      aGr TuRe  '
     A                                      COLOR(WHT)
     A                                  7  1'Pcs Kind of packages     Gross wei-
     A                                      ght net weight points              -
     A                                               '
     A                                      COLOR(WHT)
     A          R BUNN
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF03(03 'AVSLUTT')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                      SLNO(22)
     A                                      CLRL(03)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  2  2MSGCON(18 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A                                  2 22MSGCON(18 DIV0021 SYMSGF)
     A                                      COLOR(BLU)
     A                                  2 42MSGCON(18 DIV0022 SYMSGF)
     A                                      COLOR(BLU)
     A                                  2 62MSGCON(18 DIV0023 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  3  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                              3 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R VARELI
     A                                      WINDOW(*DFT 15 76 *NOMSGLIN)
     A                                      OVERLAY
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF04(04 'SØK')
     A                                      CA12(12 'Forrige')
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A N30                             15  1MSGCON(018 DIV0032 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N30                             15 22MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        58A  O 15  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A            WGLOP          3S 0H      TEXT('LOPENO')
     A                                  1  1'SHIPSYS/GSS075D'
     A                                  1 19'Dangerous goods'
     A                                      COLOR(WHT)
     A                                  1 36'Order:'
     A            WGONO     R        O  1 45REFFLD(GSSDGFR/DGONO *LIBL/GSSDGF)
     A                                      EDTCDE(Z)
     A                                      COLOR(WHT)
     A            WGST      R        O  1 53REFFLD(GSSDGFR/DGST *LIBL/GSSDGF)
     A                                      COLOR(WHT)
     A                                  2  1'UN-NO.......:'
     A            WGUNNR    R        B  2 15REFFLD(GSSDGFR/DGUNNR *LIBL/GSSDGF)
     A  71                                  DSPATR(RI)
     A                                      EDTCDE(Z)
     A                                      COLOR(YLW)
     A                                  3  1'Prop.Sh Name:'
     A            WGPSN     R        B  3 15REFFLD(GSSDGFR/DGPSN *LIBL/GSSDGF)
     A  72                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WGPSN2    R        B  4 15REFFLD(GSSDGFR/DGPSN *LIBL/GSSDGF)
     A                                      COLOR(YLW)
     A                                  5  1'Cls of goods:'
     A            WGCOG     R        B  5 15REFFLD(GSSDGFR/DGCOG *LIBL/GSSDGF)
     A  73                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                  5 25'Pack group..:'
     A            WGPG      R        B  5 42REFFLD(GSSDGFR/DGPG *LIBL/GSSDGF)
     A  74                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                  6  1'Tunnel regim:'
     A            WGTR      R        B  6 15REFFLD(GSSDGFR/DGTR *LIBL/GSSDGF)
     A  75                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                  6 25'Nbr of pcks.:'
     A            WGNOP     R        B  6 42REFFLD(GSSDGFR/DGNOP *LIBL/GSSDGF)
     A  76                                  DSPATR(RI)
     A                                      EDTCDE(Z)
     A                                      COLOR(YLW)
     A                                  7  1'Kind of pcks:'
     A            WGKOP     R        B  7 15REFFLD(GSSDGFR/DGKOP *LIBL/GSSDGF)
     A  77                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                  8  1'Gross weight:'
     A            WGGWK     R        B  8 15REFFLD(GSSDGFR/DGGWK *LIBL/GSSDGF)
     A  78                                  DSPATR(RI)
     A                                      EDTWRD('      0,  ')
     A                                      COLOR(YLW)
     A                                  9  1'Nett weight.:'
     A            WGNW      R        B  9 15REFFLD(GSSDGFR/DGNW *LIBL/GSSDGF)
     A  79                                  DSPATR(RI)
     A                                      EDTWRD('      0,  ')
     A                                      COLOR(YLW)
     A            WGNWT     R        B  9 31REFFLD(GSSDGFR/DGNWT *LIBL/GSSDGF)
     A  80                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                  9 33'(K/L)'
     A                                 10  1'Addit. Info.:'
     A            WGADD     R        B 10 15REFFLD(GSSDGFR/DGADD *LIBL/GSSDGF)
     A                                      COLOR(YLW)
     A                                 11  1'Points road.:'
     A            WGPFRT    R        B 11 15REFFLD(GSSDGFR/DGPFRT *LIBL/GSSDGF)
     A                                      EDTCDE(Z)
     A                                      COLOR(YLW)
     A                                 12  1'Classific...:'
     A            WGCLC     R        B 12 15REFFLD(GSSDGFR/DGCLC *LIBL/GSSDGF)
     A                                      COLOR(YLW)
     A                                 13  1'Location....:'
     A            WGLC      R        B 13 15REFFLD(GSSDGFR/DGLC *LIBL/GSSDGF)
     A                                      COLOR(YLW)
      *
     A          R VAREL3
     A                                      WINDOW(*DFT 12 70 *NOMSGLIN)
     A                                      OVERLAY
     A                                      CA12(12 'Forrige')
     A                                  1  1'SHIPSYS/GSS075D'
     A                                  1 19'Dangerous goods'
     A                                      COLOR(WHT)
     A            DGLOP          3 00H      TEXT('LOPENO')
     A                                  1 36'Order:'
     A            DGONO     R        O  1 45REFFLD(GSSDGFR/DGONO *LIBL/GSSDGF)
     A                                      EDTCDE(Z)
     A                                      COLOR(WHT)
     A            DGST      R        O  1 53REFFLD(GSSDGFR/DGST *LIBL/GSSDGF)
     A                                      COLOR(WHT)
     A                                  2  1'UN-NO..........:'
     A            DGUNNR    R        O  2 19REFFLD(GSSDGFR/DGUNNR *LIBL/GSSDGF)
     A                                      EDTCDE(Z)
     A                                      COLOR(WHT)
     A                                  3  1'Proper Sh. Name:'
     A*           DGPSN     R        O  3 19REFFLD(GSSDGFR/DGPSN *LIBL/GSSDGF)
     A*                                     COLOR(WHT)
     A                                  4  1'Class of goods:'
     A            DGCOG     R        O  4 19REFFLD(GSSDGFR/DGCOG *LIBL/GSSDGF)
     A                                      COLOR(WHT)
     A                                  4 25'Packing group: '
     A            DGPG      R        O  4 42REFFLD(GSSDGFR/DGPG *LIBL/GSSDGF)
     A                                      COLOR(WHT)
     A                                  5  1'Tunnel regime..:'
     A            DGTR      R        O  5 19REFFLD(GSSDGFR/DGTR *LIBL/GSSDGF)
     A                                      COLOR(WHT)
     A                                  5 25'Number of pcks:'
     A            DGNOP     R        O  5 42REFFLD(GSSDGFR/DGNOP *LIBL/GSSDGF)
     A                                      EDTCDE(Z)
     A                                      COLOR(WHT)
     A                                  6  1'Kind of pcks...:'
     A            DGKOP     R        O  6 19REFFLD(GSSDGFR/DGKOP *LIBL/GSSDGF)
     A                                      COLOR(WHT)
     A                                  7  1'Gross weight...:'
     A            DGGWK     R        O  7 19REFFLD(GSSDGFR/DGGWK *LIBL/GSSDGF)
     A                                      EDTWRD('      0,  ')
     A                                      COLOR(WHT)
     A                                  8  1'Nett weight....:'
     A            DGNW      R        O  8 19REFFLD(GSSDGFR/DGNW *LIBL/GSSDGF)
     A                                      EDTWRD('      0,  ')
     A                                      COLOR(WHT)
     A            DGNWT     R        O  8 31REFFLD(GSSDGFR/DGNWT *LIBL/GSSDGF)
     A                                      COLOR(WHT)
     A                                  8 33'(K/L)'
     A                                  9  1'Additional info:'
     A            DGADD     R        O  9 19REFFLD(GSSDGFR/DGADD *LIBL/GSSDGF)
     A                                      COLOR(WHT)
     A                                 10  1'Points road trs:'
     A            DGPFRT    R        O 10 19REFFLD(GSSDGFR/DGPFRT *LIBL/GSSDGF)
     A                                      EDTCDE(Z)
     A                                      COLOR(WHT)
     A  30        MSGTXT        58A  O 12  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                             12  2MSGCON(018 DIV0032 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N30                             12 22MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A            MSGNR          7A  P
     A          R NYWRK
     A                                      CF03(03 'AVSLUTT')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                  1 52DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  1 63TIME
     A                                      COLOR(BLU)
     A                                  1  3'SHIPSYS/GSS075D'
     A                                      COLOR(BLU)
     A                                  1 20'Work with dangerous goods'
     A                                      COLOR(WHT)
     A                                  4  3'Options:  2=Change   4=Delete'
     A                                      COLOR(BLU)
     A                                  6  1'Op S Orderno UnNo Proper shipping -
     A                                      name                     ClGo PaGr -
     A                                      TuRe Pcs  '
     A                                      COLOR(WHT)
     A                                  7  1'Kind og packages     Gross wght Ne-
     A                                      t wght   T Point                   -
     A                                               '
     A                                      COLOR(WHT)
     A                                 23  2MSGCON(18 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(18 DIV0021 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 42MSGCON(18 DIV0022 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 62MSGCON(18 DIV0023 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                             24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A          R SØKGEB
     A                                      CA12(12 'Forrige')
     A                                      SLNO(20)
     A                                      CLRL(05)
     A                                      OVERLAY
     A                                  2  2MSGCON(030 DIV1032 *LIBL/SYMSGF)
     A            W2GNOT        20A  I  2 33DSPATR(UL)
     A                                      DSPATR(PC)
     A                                  3  2MSGCON(022 SHU0403 *LIBL/SYMSGF)
     A            WSSORT         1Y 0B  3 26EDTCDE(Z)
     A                                  4  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
