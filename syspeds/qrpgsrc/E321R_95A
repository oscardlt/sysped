     FCUNDL01   IF   E           K DISK
     FTURER14   IF   E           K DISK
     FFIRM      UF   E           K DISK
     FDOKUF     UF   E           K DISK
     FDOKUFV    IF   E           K DISK
     FDOK29     IF   E           K DISK
     FDOK36     IF   E           K DISK
     FHEADF     UF   E           K DISK
      *
     FECNT95A   O  A E             DISK
     FECPI95A   O  A E             DISK
     FEEQD95A   O  A E             DISK
     FEEQN95A   O  A E             DISK
     FEFTX95A   O  A E             DISK
     FEGID95A   O  A E             DISK
     FELOC95A   O  A E             DISK
     FEMEA95A   O  A E             DISK
     FEMOA95A   O  A E             DISK
     FENAD95A   O  A E             DISK
     FERFF95A   O  A E             DISK
     FETCC95A   O  A E             DISK
     FETDT95A   O  A E             DISK
     FETOD95A   O  A E             DISK
     D                 DS
     D  TX                     1    490
     D                                     DIM(14)
     D  Z2901                  1     35
     D  Z2902                 36     70
     D  Z2903                 71    105
     D  Z2904                106    140
     D  Z2905                141    175
     D  Z2906                176    210
     D  Z2907                211    245
     D  Z2908                246    280
     D  Z2909                281    315
     D  Z2910                316    350
     D  Z2911                351    385
     D  Z2912                386    420
     D  Z2913                421    455
     D  Z2914                456    490
     D  ZW2901                 1     35
     D  ZW2902                36     70
     D  ZW2903                71    105
     D  ZW2904               106    140
     D  ZW2905               141    175
     D  ZW2906               176    210
     D  ZW2907               211    245
     D  ZW2908               246    280
     D  ZW2909               281    315
     D  ZW2910               316    350
     D  ZW2911               351    385
     D  ZW2912               386    420
     D  ZW2913               421    455
     D  ZW2914               456    490
      *____________________________________
      * DS for leftjustify numericel fields
      *""""""""""""""""""""""""""""""""""""
     D                 DS
     D  HEOPD                  1      7  0
     D  WEOPD                  1      7
      *________________________________________
      * DS for å venstrestille numeriske felter
      *""""""""""""""""""""""""""""""""""""""""
     D                 DS
     D  ØN152                  1     15  2
     D  ØA152H                 1     13
     D  ØA152X                 5     13
     D  ØA152D                14     15
     D                 DS
     D  ØN153                  1     15  3
     D  ØA153H                 1     12
     D  ØA153X                 5     12
     D  ØA153D                13     15
     D                 DS
     D  ØA12                   1     12
     D  Ø12                    1     12
     D                                     DIM(12)
      */////////////
      * MAIN LOGIC /
      */////////////
     C                   EXSR      SRINIT
     C  N33              EXSR      SRA
     C                   MOVE      '1'           *INLR
      *
      *////////////////////////////////////////////////////////////
      * SR for init                                        SRINIT /
      *////////////////////////////////////////////////////////////
     C     SRINIT        BEGSR
      *
     C     *ENTRY        PLIST
     C                   PARM                    UAVD              4 0
     C                   PARM                    UOPD              7 0
     C                   PARM                    UFBN              3 0
     C                   PARM                    UMN2              9
     C                   MOVE      UMN2          UMN               9 0
      *
      * INIT AV ARBEIDSFELT
     C     *LIKE         DEFINE    FREP0         WREP0
     C     *LIKE         DEFINE    FREP1         WREP1
     C     *LIKE         DEFINE    FREP2         WREP2
     C                   Z-ADD     0             ØX                2 0
     C                   MOVE      *BLANKS       ØAN              12
      * DEF AV KEYS
     C     HEADFK        KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    UOPD
     C     DOKUFK        KLIST
     C                   KFLD                    DFRI
     C                   KFLD                    UAVD
     C                   KFLD                    UOPD
     C                   KFLD                    UFBN
     C                   MOVE      'F'           DFRI
     C     DOKUFVK       KLIST
     C                   KFLD                    DFRI
     C                   KFLD                    UAVD
     C                   KFLD                    UOPD
     C                   KFLD                    UFBN
     C     DOK29K        KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    UOPD
     C                   KFLD                    UFBN
     C     DOK36K        KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    UOPD
     C                   KFLD                    UFBN
     C     CUND1K        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    KUNDNR
     C*    TRANK         KLIST
     C*                  KFLD                    FIRMA
     C*                  KFLD                    DFTRAN
      *_____________________
      * LES NØDVENDIGE FILER
      *"""""""""""""""""""""
     C     FIFIRM        SETLL     FIRM
     C                   READ      FIRM                                   33
     C  N33HEADFK        CHAIN     HEADF                              33
     C  N33DOKUFK        CHAIN(N)  DOKUF                              33
     C  N33HEPRO         CHAIN     TURER14                            33
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      * SR                                                    SRA /
      *////////////////////////////////////////////////////////////
     C     SRA           BEGSR
      * ADD TO EDI-DB
     C                   EXSR      FTX00
     C                   EXSR      TOD02
     C                   EXSR      RFF03
     C                   EXSR      TDT08
     C                   EXSR      NAD11A
     C                   EXSR      NAD11B
     C                   EXSR      NAD11C
     C                   EXSR      GID18
     C                   EXSR      CNT00
      *
     C     HEADFK        CHAIN     HEADF                              33
     C                   MOVE      'P'           HEPK1
     C                   UPDATE    HEADFR
     C     DOKUFK        CHAIN     DOKUF                              33
     C                   MOVE      'V'           DFST
     C                   UPDATE    DOKUFR
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      *  Kontrolltotaler                                    CNT00 /
      *////////////////////////////////////////////////////////////
     C     CNT00         BEGSR
     C                   Z-ADD     0             WREP0
     C                   Z-ADD     0             WREP1
      *
     C                   CLEAR                   CNTR
     C                   Z-ADD     UMN           CMN
     C                   Z-ADD     0             CGN
     C                   ADD       1             WREP0
     C                   Z-ADD     WREP0         CREP0
      *
     C                   MOVEL     '11'          C6069
     C                   Z-ADD     DFNT          ØN152
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           C6066
     C                   MOVEL     'NMB'         C6411
     C                   WRITE     CNTR
      *
      *
     C                   CLEAR                   CNTR
     C                   Z-ADD     UMN           CMN
     C                   Z-ADD     0             CGN
     C                   ADD       1             WREP0
     C                   Z-ADD     WREP0         CREP0
      *
     C                   MOVEL     '7'           C6069
     C                   Z-ADD     DFVKT         ØN152
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           C6066
     C                   MOVEL     'KGM'         C6411
      *
     C                   WRITE     CNTR
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Fritekst                                           FTX00 /
      *////////////////////////////////////////////////////////////
     C     FTX00         BEGSR
      *
     C                   Z-ADD     0             WREP0
      * Ordinær fritekst
     C     DOK29K        SETLL     DOK29
     C     DOK29K        READE     DOK29                                  90
     C     *IN90         DOWEQ     *OFF
      *
     C                   CLEAR                   FTXR
     C                   Z-ADD     UMN           FMN
     C                   Z-ADD     0             FGN
     C                   ADD       1             WREP0
     C                   Z-ADD     WREP0         FREP0
     C                   MOVEL     'ICN'         F4451
     C                   MOVEL     D29TXT        F4440A
     C                   WRITE     FTXR
     C     DOK29K        READE     DOK29                                  90
     C                   ENDDO
      *
     C                   Z-ADD     0             X                 3 0
     C     DOK36K        SETLL     DOK36
     C     DOK36K        READE     DOK36                                  90
     C     *IN90         DOWEQ     *OFF
      *
     C                   ADD       1             X
     C                   MOVEL     D36TXT        TX(X)
     C     DOK36K        READE     DOK36                                  90
     C                   ENDDO
     C                   CLEAR                   FTXR
     C                   Z-ADD     UMN           FMN
     C                   Z-ADD     0             FGN
     C                   MOVEL     'SIC'         F4451
     C     TX(1)         IFNE      *BLANKS
     C     TX(2)         ORNE      *BLANKS
     C     TX(3)         ORNE      *BLANKS
     C     TX(4)         ORNE      *BLANKS
     C     TX(5)         ORNE      *BLANKS
     C                   ADD       1             WREP0
     C                   Z-ADD     WREP0         FREP0
     C                   MOVEL     TX(1)         F4440A
     C                   MOVEL     TX(2)         F4440B
     C                   MOVEL     TX(3)         F4440C
     C                   MOVEL     TX(4)         F4440D
     C                   MOVEL     TX(5)         F4440E
     C                   WRITE     FTXR
     C                   ENDIF
     C     TX(6)         IFNE      *BLANKS
     C     TX(7)         ORNE      *BLANKS
     C     TX(8)         ORNE      *BLANKS
     C     TX(9)         ORNE      *BLANKS
     C     TX(10)        ORNE      *BLANKS
     C                   ADD       1             WREP0
     C                   Z-ADD     WREP0         FREP0
     C                   MOVEL     TX(6)         F4440A
     C                   MOVEL     TX(7)         F4440B
     C                   MOVEL     TX(8)         F4440C
     C                   MOVEL     TX(9)         F4440D
     C                   MOVEL     TX(10)        F4440E
     C                   WRITE     FTXR
     C                   ENDIF
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Godsdetaljer                                       GID18 /
      *////////////////////////////////////////////////////////////
     C     GID18         BEGSR
      *
     C                   Z-ADD     0             WREP0
     C                   Z-ADD     0             WREP1
     C                   CLEAR                   GIDR
     C                   Z-ADD     UMN           GMN
     C                   Z-ADD     18            GGN
      *____________
      * Varelinje-1
      *""""""""""""
      *
     C     DOKUFVK       SETLL     DOKUFV
     C     DOKUFVK       READE     DOKUFV                                 33
     C     *IN33         DOWEQ     *OFF
      *
     C                   ADD       1             WREP0
     C                   Z-ADD     WREP0         GREP0
      *
     C                   Z-ADD     WREP0         ØN152
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           G1496
     C                   Z-ADD     FVANT         ØN152
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           G7224A
     C                   MOVEL     FVVT          G7064A
      *
     C                   WRITE     GIDR
      * Other segments
     C                   EXSR      MEA20
      *
     C     DOKUFVK       READE     DOKUFV                                 33
     C  N33              ENDDO
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Steder                                             LOC09 /
      *////////////////////////////////////////////////////////////
     C     LOC09         BEGSR
      *_____________
      * Førtransport
      *"""""""""""""
     C                   Z-ADD     0             WREP1
      *
     C                   CLEAR                   LOCR
     C                   Z-ADD     UMN           LMN
     C                   Z-ADD     9             LGN
     C                   ADD       1             WREP1
     C                   Z-ADD     WREP0         LREP0
     C                   Z-ADD     WREP1         LREP1
      *
     C                   MOVEL     '80'          L3227
     C                   MOVEL     DFPNLS        L3225
      *
     C                   WRITE     LOCR
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Mål                                                MEA20 /
      *////////////////////////////////////////////////////////////
     C     MEA20         BEGSR
      *___________
      * Bruttovekt
      *"""""""""""
     C                   Z-ADD     0             WREP1
      *
     C                   CLEAR                   MEAR
     C                   Z-ADD     UMN           MMN
     C                   Z-ADD     20            MGN
     C                   Z-ADD     WREP0         MREP0
     C                   IF        FVVKT > 0
     C                   ADD       1             WREP1
     C                   Z-ADD     WREP1         MREP1
      *
     C                   MOVEL     'GRO'         M6311
     C                   MOVEL     'KGM'         M6411
     C                   Z-ADD     FVVKT         ØN152
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           M6314
      *
     C                   WRITE     MEAR
     C                   ENDIF
      *______
      * Volum
      *""""""
     C                   IF        FVVOL > 0
     C                   ADD       1             WREP1
     C                   Z-ADD     WREP1         MREP1
      *
     C                   MOVEL     'VOL'         M6311
     C                   MOVEL     'DMQ'         M6411
     C                   Z-ADD     FVVOL         ØN152
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           M6314
      *
     C                   WRITE     MEAR
     C                   ENDIF
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      *  Pengebeløp                                         MOA11 /
      *////////////////////////////////////////////////////////////
     C     MOA11         BEGSR
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Navn og adresser Godsavsender                     NAD11A /
      *////////////////////////////////////////////////////////////
     C     NAD11A        BEGSR
     C                   Z-ADD     0             WREP0
      *
     C                   CLEAR                   NADR
     C                   Z-ADD     UMN           NMN
     C                   Z-ADD     11            NGN
     C                   ADD       1             WREP0
     C                   Z-ADD     WREP0         NREP0
      *
     C                   MOVE      *ZEROS        POSTNR
     C                   MOVE      DFKNSS        KUNDNR
     C     CUND1K        CHAIN     CUNDL01                            51
     C                   MOVEL     'CZ'          N3035
     C                   MOVEL     DFNAVS        N3036A
     C                   MOVEL     DFAD1S        N3042A
     C                   MOVEL     DFAD2S        N3042B
     C     POSTNR        IFGT      0
     C                   MOVEL     POSTNR        N3251
     C                   MOVEL     'NO'          N3207
     C                   MOVEL     ADR3          N3164
     C                   ELSE
     C                   MOVEL     DFAD3S        N3164
     C                   END
      *
     C                   WRITE     NADR
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      *  Navn og adresser MOTTAKER                         NAD11B /
      *////////////////////////////////////////////////////////////
     C     NAD11B        BEGSR
      *
     C                   CLEAR                   NADR
     C                   Z-ADD     UMN           NMN
     C                   Z-ADD     11            NGN
     C                   ADD       1             WREP0
     C                   Z-ADD     WREP0         NREP0
      *
     C                   MOVEL     'CN'          N3035
     C                   MOVEL     DFNAVM        N3036A
     C                   MOVEL     DFAD1M        N3042A
     C                   MOVEL     DFAD2M        N3042B
     C                   MOVEL     DFAD3M        N3164
     C                   MOVEL     DFPNUM        N3251
     C                   MOVEL     DFLKM         N3207
      *
     C                   WRITE     NADR
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      *  Navn og adresser Leveringsadresse                 NAD11C /
      *////////////////////////////////////////////////////////////
     C     NAD11C        BEGSR
      *
     C                   CLEAR                   NADR
     C                   Z-ADD     UMN           NMN
     C                   Z-ADD     11            NGN
     C                   ADD       1             WREP0
     C                   Z-ADD     WREP0         NREP0
      *
     C                   MOVEL     'DP'          N3035
     C                   MOVEL     DFNAVL        N3036A
     C                   MOVEL     DFAD1L        N3042A
     C                   MOVEL     DFAD2L        N3042B
     C                   MOVEL     DFADUL        N3164
     C                   MOVEL     DFPNUL        N3251
     C                   MOVEL     DFLKL         N3207
      *
     C                   WRITE     NADR
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Referanser                                         RFF03 /
      *////////////////////////////////////////////////////////////
     C     RFF03         BEGSR
     C                   Z-ADD     0             WREP0
      *
     C                   CLEAR                   RFFR
     C                   Z-ADD     UMN           RMN
     C                   Z-ADD     3             RGN
      *
      * Eksportørs ref.
     C                   ADD       1             WREP0
     C                   Z-ADD     WREP0         RREP0
     C                   MOVEL     'ERN'         R1153
     C                   MOVEL     HERFA         R1154
     C                   WRITE     RFFR
      *
      * Avsenders ref./KUNDENR
      *KUNDENR MOTTAKER BURDE SELVSAGT SENDES I NAD-SEGMENT(CN) TAG3039??
      *OG DETTE RFF-SEGMENTET KUNNE VÆRT DROPPET
     C                   ADD       1             WREP0
     C                   Z-ADD     WREP0         RREP0
     C                   MOVEL     'CU '         R1153
     C                   MOVE      *BLANKS       R1154
     C                   MOVEL     DFKNM         R1154
     C                   WRITE     RFFR
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      *  Venstrestill numeriske verdier                      SRNE /
      *////////////////////////////////////////////////////////////
     C     SRNE          BEGSR
      * Minus value ?
     C     ØN152         IFLT      0
     C                   Z-SUB     ØN152         ØN152
     C                   MOVE      '-'           ØSIGN             1
     C                   ELSE
     C                   MOVE      '+'           ØSIGN
     C                   ENDIF
      * Flytte in rett verdi i Alfa 12-felt
     C                   MOVE      *ZEROS        ØA12
     C     ØA152D        IFEQ      '00'
     C                   MOVE      ØA152H        ØA12
     C                   ELSE
     C                   MOVEL     ØA152X        ØA12
     C                   MOVE      ',  '         ØA12
     C                   MOVE      ØA152D        ØA12
     C                   END
      * lete fra venstre og finn foerste siffer over 0
     C     1             DO        11            ØX
     C     Ø12(ØX)       IFNE      '0'
     C                   GOTO      SRNE1
     C                   END
     C                   END
     C     SRNE1         TAG
      * Venstrestill til et nytt felt
     C                   MOVE      *BLANKS       ØAN
     C     ØSIGN         IFEQ      '-'
     C                   SUB       1             ØX
     C                   MOVE      '-'           Ø12(ØX)
     C                   ENDIF
     C                   MOVEA     Ø12(ØX)       ØAN
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Transportrateberegning                             TCC14 /
      *////////////////////////////////////////////////////////////
     C     TCC14         BEGSR
     C                   Z-ADD     0             WREP1
      *______
      * Frakt
      *""""""
     C     DFBELA        IFEQ      'S'
      *
     C                   CLEAR                   TCCR
     C                   Z-ADD     UMN           TMN
     C                   Z-ADD     14            TGN
     C                   ADD       1             WREP1
     C                   Z-ADD     WREP0         TREP0
     C                   Z-ADD     WREP1         TREP1
      *
      *
     C                   WRITE     TCCR
      *
     C                   END
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Transportdetaljer                                  TDT08 /
      *////////////////////////////////////////////////////////////
     C     TDT08         BEGSR
     C                   Z-ADD     0             WREP0
     C                   Z-ADD     0             WREP1
     C                   ADD       1             WREP0
      * Other segments
     C** CONDITIONAL     EXSR      LOC09
      *
     C                   CLEAR                   TDTR
     C                   Z-ADD     UMN           TMN
     C                   Z-ADD     8             TGN
     C                   Z-ADD     WREP0         TREP0
      *
     C                   MOVEL     '20'          T8051
     C                   MOVEL     TUNAT         T3128
     C                   MOVEL     TUBILN        T8213
      *
     C                   WRITE     TDTR
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Leveringsbetingelser                               TOD02 /
      *////////////////////////////////////////////////////////////
     C     TOD02         BEGSR
     C                   Z-ADD     0             WREP0
     C                   Z-ADD     0             WREP1
      *
     C                   CLEAR                   TODR
     C                   Z-ADD     UMN           TMN
     C                   Z-ADD     0             TGN
     C                   ADD       1             WREP0
     C                   Z-ADD     WREP0         TREP0
      *
     C                   MOVEL     '6'           T4055
     C                   MOVEL     HEFR          T4053
     C                   MOVEL     '106'         T1131
      *
     C                   WRITE     TODR
     C                   ENDSR
