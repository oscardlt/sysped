      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/TJETUR05AD MODULE(*LIBL/TJETUR05AD *LIBL/INCGI)
CRTPGM*        ACTGRP(CGI) AUT(*USE)
      *
      *********************************************************************
      /copy SYSPEDS/qrpgsrcpy,hspecs
      /copy SYSPEDS/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FTURER14   UF   E           K DISK    USROPN
     FDISP      UF   E           K DISK    USROPN
     FHEADF     UF   E           K DISK    USROPN
     FTRACKF    O  A E             DISK    USROPN
      /copy SYSPEDS/qrpgsrcpy,prototypeb
      /copy SYSPEDS/qrpgsrcpy,usec
      /copy SYSPEDS/qrpgsrcpy,variables3
     D USER            S             10
     D WSTUR           S              8
     D WBTUR           S              8  0
     D WSAVD           S              4
     D WBAVD           S              4  0
     D WSOPD           S              7
     D WBOPD           S              7  0
     D WMODE           S              1
N02  D TUTM3X          S              9  3
N03  D TUTLMX          S              6  2
     D JSONstring      S          32000
     D Error           S            150
     D AntLest         S              9  0
      *get input-string
      /copy syspeds/qrpgsrcpy,prolog3

     C                   EXSR      PARSE

     C                   EXSR      INIT
     c                   callp     gethtmlifs(
     C                             '/syspeddev/html/JSON.html':
     C                             '/CGITAG/')
     C                   callp     wrtsection('top')

      /free
        JSONstring='{'
        +'¬user¬ : ¬'+%trim(USER)+'¬, '
        +'¬wmode¬ : ¬'+%trim(WMODE)+'¬, '
        +'¬wstur¬ : ¬'+%trim(WSTUR)+'¬, '
        +'¬wsavd¬ : ¬'+%trim(WSAVD)+'¬, '
        +'¬wsopd¬ : ¬'+%trim(WSOPD)+'¬, '
        +'¬error¬ : ¬'+%trim(ERROR)+'¬, ';
      /end-free
      * JSONfix escaper " i tekst,  for deretter å bytte ¬->"
     c                   call      'JSONFIX'
     c                   parm                    JSONstring
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')

     C                   EVAL      JSONstring ='"tripadddel" : ['
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')
      * Find/send lines
     c     Error         IFEQ      *blanks

     C                   SELECT
     C                   WHEN      WMODE='D'
     C                   EXSR      SRREMOVE
     C                   WHEN      WMODE='A'
     C                   EXSR      SRPICKUP
     C                   ENDSL
     C                   ENDIF
     c                   eval      JSONstring =']'
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')
     c                   eval      JSONstring ='}'
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')
     C                   EXSR      EXIT
      *=====================================================================
      * Parse input / cvt to numeric
      *=====================================================================
     C     PARSE         BEGSR
     C                   EVAL      USER     = zhbgetvar('USER')
     C                   EVAL      WMODE    = zhbgetvar('WMODE')
     C                   EVAL      WSTUR    = zhbgetvar('WSTUR')
     C                   EVAL      WBTUR    = c2n2(WSTUR)
     C                   EVAL      WSAVD    = zhbgetvar('WSAVD')
     C                   EVAL      WBAVD    = c2n2(WSAVD)
     C                   EVAL      WSOPD    = zhbgetvar('WSOPD')
     C                   EVAL      WBOPD    = c2n2(WSOPD)
     C                   ENDSR
      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     EXIT          BEGSR
     C                   callp     wrtsection('*fini')
     C                   CLOSE     BRIDF
     C  N50              CLOSE     TURER14
     C  N50              CLOSE     DISP
     C  N50              CLOSE     HEADF
     C  N50              CLOSE     TRACKF
     C                   MOVE      *ON           *INLR
     C                   return
     C                   ENDSR
      *=====================================================================******
      *  SRREMOVE
      *=====================================================================******
     C     SRREMOVE      BEGSR

     C     HEADFK        CHAIN     HEADF                              33
     C                   IF        *IN33 = *OFF
     C                   MOVE      *ZEROS        HEPRO
     C                   MOVE      *BLANKS       HEPOS1
     C                   UPDATE    HEADFR
     C     HEADFK        CHAIN     DISP                               33
     C                   IF        *IN33 = *OFF
     C                   MOVE      *ZEROS        DITUAV
     C                   MOVE      *ZEROS        DITUR
     C                   UPDATE    DISPR
     C                   END

     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVE      HEAVD         TTAVD
     C                   MOVE      HEOPD         TTOPD
     C                   EVAL      TTTEXT='Order is removed from trip '
     C                             + %TRIM(WSTUR) + ' by '
     C                             + %TRIM(BIBRID)
     C                   EVAL      TTTEXL='Ordre er fjernet fra turen '
     C                             + %TRIM(WSTUR) + ' av '
     C                             + %TRIM(BIBRID)
     C                   MOVE      WDT           TTDATE
     C                   MOVE      WTM           TTTIME
     C                   MOVE      WDT           TTDATL
     C                   MOVE      WTM           TTTIML
     C                   MOVE      'RMV'         TTACTI
     C                   MOVEL     USER          TTUSER
     C                   EVAL      TTDEPO = 'ESOVERS5'
     C                   WRITE     TRACKFR
     C                   END

     C     WBTUR         CHAIN     TURER14                            33
     C                   SUB       1             TUAO
     C                   SUB       HENT          TUTS
     C                   SUB       HEVKT         TUTVKT
     C                   SUB       HEM3          TUTM3
     C                   SUB       HELM          TUTLM
     C                   UPDATE    TURERR

     C                   ENDSR
      *=====================================================================******
      *  SRPICKUP
      *=====================================================================******
     C     SRPICKUP      BEGSR
     C     HEADFK        CHAIN     HEADF                              33
     C                   IF        *IN33 = *OFF
     C                   MOVE      WBTUR         HEPRO
     C                   UPDATE    HEADFR
     C     HEADFK        CHAIN     DISP                               33
     C                   IF        *IN33 = *OFF
     C                   MOVE      TUAVD         DITUAV
     C                   MOVE      WBTUR         DITUR
     C                   UPDATE    DISPR
     C                   ENDIF
     C     WBTUR         CHAIN     TURER14                            33
     C                   ADD       1             TUAO
     C                   ADD       HENT          TUTS
     C                   ADD       HEVKT         TUTVKT
n02  C                   z-add     TUTM3         TUTM3X
n02  C                   ADD       HEM3          TUTM3X
N08  C                   IF        TUTM3X > 9999.999
N08  C                   EVAL      TUTM3  = 9999.999
n02  C                   ELSE
n02  C                   EVAL      TUTM3  = TUTM3X
n02  C                   ENDIF
n03  c                   z-add     tutlm         tutlmx
n03  C                   ADD       HELM          TUTLMX
N03  C                   IF        TUTLMX > 99.99
n03  c                   eval      tutlm  = 99.99
n03  C                   ELSE
n03  C                   EVAL      TUTLM  = TUTLMX
n03  C                   ENDIF
     C                   UPDATE    TURERR
     C                   ENDIF

     C                   ENDSR
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          BEGSR
     C                   OPEN      BRIDF
     C     USER          CHAIN     BRIDF                              50
      * En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        IFEQ      *blanks
     C                   CALLB     'INCGI'
     C                   PARM                    BISTDL
     C                   ELSE
      * Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   CALL      BILIBL
     C                   ENDIF

     C   50              eval      Error = 'Invalid userID'
     C  N50              OPEN      TURER14
     C  N50              OPEN      DISP
     C  N50              OPEN      HEADF
     C  N50              OPEN      TRACKF

     C     HEADFK        KLIST
     C                   KFLD                    WBAVD
     C                   KFLD                    WBOPD
     C                   ENDSR
