     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      REF(*LIBL/EDIJ)
     A                                      PRINT
     A                                      HLPDOC('STD' 'HELPNU' 'HLP400')
     A                                      HELP
      *
     A          R EDI51DA                   SFL
     A            J0004     R        H
     A            J0010     R        H
     A            ZOP            1A  I  7  2VALUES(' ' '2' '4')
     A            JCLFE     R        O  7 13
     A            JLI5      R        O  7  6EDTCDE(4)
     A            JCLC      R        O  7 16
      *
     A          R EDI51DB                   SFLCTL(EDI51DA)
     A                                      SFLSIZ(0007)
     A                                      SFLPAG(0006)
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A                                      CF03(03 'Gå ut')
     A                                      CF05(05 'Frisk opp')
     A                                      CF06(06 'Lage Ny')
     A                                      CF12(12 'Forrige')
     A                                  1  2'EDI'
     A                                      COLOR(BLU)
     A                                  1 36'JCL 1/2'
     A                                      COLOR(WHT)
     A                                  1 75'EDI51D'
     A                                      COLOR(BLU)
     A                                  3  2MSGCON(023 EDI0602 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  4MSGCON(007 EDI0702 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 14MSGCON(008 EDI0704 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A            ZRECNO         4S 0O  5 68SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  6  1MSGCON(080 EDI0173 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
      *
     A          R EDI51DC
     A                                      OVERLAY
     A                                 21  1'                                  -
     A                                                                         -
     A                                                 '
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
     A                                 22  2MSGCON(010 EDI0803 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 22 15MSGCON(010 EDI0805 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 22 29MSGCON(010 EDI0806 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 22 43MSGCON(010 EDI0812 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
      *
     A          R EDI51DD
     A                                      OVERLAY
     A                                      CF12(12 'Forrige')
     A                                 19 16'....+...10....+...20....+...30....-
     A                                      +...40....+...50....+...60....+'
     A                                      COLOR(WHT)
     A            ÅLI5      R     D  B 20  6REFFLD(JLI5)
     A  41                                  DSPATR(PR)
     A                                      RANGE(1 99999)
     A N41                                  DSPATR(PC)
     A            ÅCLFE     R     A  B 20 13REFFLD(JCLFE)
     A  41                                  DSPATR(PR)
     A                                      VALUES('E' 'F')
     A            ÅCLC      R        B 20 16REFFLD(JCLC)
     A  41                                  DSPATR(PC)
     A                                 22  2MSGCON(010 EDI0812 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
      *
     A          R EDI51DE
     A                                      OVERLAY
     A                                      CF03(03 'Gå ut')
     A                                      CF12(12 'Forrige')
     A                                  1  2'EDI'
     A                                      COLOR(BLU)
     A                                  1 39'JCL'
     A                                      COLOR(WHT)
     A                                  1 73'EDI51D E'
     A                                  2 34'Send/Receive   '
     A                                      COLOR(WHT)
     A                                  4  2MSGCON(023 EDI0602 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  6  4MSGCON(037 EDI0176 *LIBL/SYMSGF)
     A            WSR            1A  B  6 44VALUES('S' 'R')
     A                                      DFTVAL('S')
     A                                  6 48MSGCON(024 EDI0179 *LIBL/SYMSGF)
     A                                 21 76MSGCON(005 EDI0180 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                 22  2MSGCON(010 EDI0803 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 22 16MSGCON(010 EDI0812 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A          R EDI51DØ                   OVERLAY
     A            ZMSGTX        79A  O 23  2MSGID(&ZMSGNO SYMSGF)
     A N30                                  COLOR(WHT)
     A  30                                  COLOR(RED)
     A            ZMSGNO         7A  P
