      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/TNOI010R MODULE(*LIBL/TNOI010R *LIBL/INCGI)
CRTPGM*        ACTGRP(*NEW) AUT(*USE)
      *
      *********************************************************************
      /copy SYSPEDS/qrpgsrcpy,hspecs
      /copy SYSPEDS/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FSADV4     IF   E           K DISK    USROPN
     FSADV4E    IF   E           K DISK    USROPN
     F                                     RENAME(SADVR:SADVR4)
     FSADV2     IF   E           K DISK    USROPN
     F                                     RENAME(SADVR:SADVR2)
     FSADH      IF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy SYSPEDS/qrpgsrcpy,prototypeb
      /copy SYSPEDS/qrpgsrcpy,usec
      /copy SYSPEDS/qrpgsrcpy,variables3
      *
      * Varible for
     D WSUSER          S             10
     D SVAVDA          S              4
     D SVAVD           S              4  0
     D SVTDNA          S              7
     D SVTDN           S              7  0
     D LINA            S              5
     D LIN             S              5  0
     D VARENRA         S              8
     D VARENR          S              8  0
     D WMODE           S              2
     D JSONstring      S          32000
     D Error           S            150
     D AntLest         S              9  0
     D                 DS
     D  SVVNT                  1      8S 0
     D  SVVNTA                 1      8
      *get input-string
      /copy syspeds/qrpgsrcpy,prolog3

      * Parse input
     C                   exsr      Parse
      * Open files etc
     C                   EXSR      INIT
      * Angre
     C                   IF        WMODE = 'R'
     C     *LIKE         DEFINE    SVAVD         UAVDOMS
     C     *LIKE         DEFINE    SVTDN         UTDNOMS
     C                   MOVE      SVAVD         UAVDOMS
     C                   MOVE      SVTDN         UTDNOMS
     C                   MOVE      'OMS'         UTYPEOMS          3
     C                   CALL      'SAD064'
     C                   PARM                    UAVDOMS
     C                   PARM                    UTDNOMS
     C                   PARM                    SISG
     C                   PARM                    UTYPEOMS
     C                   END
      *
     c                   callp     gethtmlifs(
     C                             '/syspeddev/html/JSON.html':
     C                             '/CGITAG/')
     C                   callp     wrtsection('top')
      *
      * ............................................................................................
      * skriv JSON-Object start..(alltid '{' + x ant param. m/komma mellom (IKKE komma etter siste))
      * ............................................................................................
      *
      /free
        JSONstring='{'
        +'¬user¬ : ¬'+%trim(WSUSER)+'¬, '
        +'¬avd¬ : ¬'+%trim(%editc(SVAVD:'Z'))+'¬, '
        +'¬opd¬ : ¬'+%trim(%editc(SVTDN:'M'))+'¬, '
        +'¬lin¬ : ¬'+%trim(%editc(LIN:'Z'))+'¬, '
        +'¬varenr¬ : ¬'+%trim(%editc(VARENR:'Z'))+'¬, '
        +'¬errMsg¬ : ¬'+%trim(Error)+'¬, ';
      /end-free
      * JSONfix escaper " i tekst,  for deretter å bytte ¬->"
     c                   call      'JSONFIX'
     c                   parm                    JSONstring
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')
      *
      * ............................................................................................
      * Skriv JSON-LISTE Start (en liste er EN parameter(fra [ til   )
      * ............................................................................................
     c                   eval      JSONstring ='"orderList" : ['
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')
      *
      * Liste Ærende
      *
     c     Error         ifeq      *blanks
     C     SADVkey2      setll     SADV4
     C     SADVkey       setll     SADV4E
     C     SADVkey3      setll     SADV2
     C                   IF        *IN61
     C     SADVkey       READE     SADV2                                  55
     C                   ELSE
     C                   IF        LIN > 1
     C     SADVkey       READE     SADV4                                  55
     C                   ELSE
     C     SADVkey       READE     SADV4E                                 55
     C                   ENDIF
     C                   ENDIF
     C     *IN55         DOWEQ     '0'
     C*                  IF        SVVKTB > *ZEROS
     C                   Add       1             AntLest
      * ............................................................................................
      * Skriv en JSON-Array-linje pr menypunkt - komma før hvert nytt element
      * ............................................................................................
      /free
       if AntLest=1;
          JSONstring=*blanks;
          else;
          JSONstring=', ';
          endif;
       JSONstring=%trim(JSONstring)+'{'
          +'¬sv01¬ : ¬'+%trim(sv01)+'¬, '
          +'¬svavd¬ : ¬'+%trim(%editc(svavd:'Z'))+'¬, '
          +'¬svtdn¬ : ¬'+%trim(%editc(svtdn:'M'))+'¬, '
          +'¬svli¬ : ¬'+%trim(%editc(svli:'Z'))+'¬, '
          +'¬svdp¬ : ¬'+%trim(%editc(svdp:'Z'))+'¬, '
          +'¬svlk¬ : ¬'+%trim(svlk)+'¬, '
          +'¬svvnt¬ : ¬'+%trim(svvnta)+'¬, '
          +'¬svtn¬ : ¬'+%trim(svtn)+'¬, '
          +'¬svpre¬ : ¬'+%trim(svpre)+'¬, '
N01       +'¬svas¬ : ¬'+%trim(%editc(svas:'4'))+'¬, '
          +'¬svpva¬ : ¬'+%trim(svpva)+'¬, '
          +'¬svmfr¬ : ¬'+%trim(svmfr)+'¬, '
          +'¬svvf¬ : ¬'+%trim(svvf)+'¬, '
N03       +'¬svvktb¬ : ¬'+%trim(%editc(svvktb:'4'))+'¬, '
N04       +'¬svntm¬ : ¬'+%trim(%editc(svntm:'Z'))+'¬, '
          +'¬svbelt¬ : ¬'+%trim(%editc(svbelt:'4'))+'¬, '
          +'¬svbels¬ : ¬'+%trim(%editc(svbels:'4'))+'¬, '
N03       +'¬svvktn¬ : ¬'+%trim(%editc(svvktn:'4'))+'¬, '
N02       +'¬wa1¬ : ¬'+%trim(svft01)+'¬, '
          +'¬wb1¬ : ¬'+%trim(%editc(svnt01:'Z'))+'¬, '
          +'¬wc1¬ : ¬'+%trim(sveh01)+'¬, '
N03       +'¬wd1¬ : ¬'+%trim(svvt01)+'¬, '
          +'¬we1¬ : ¬'+%trim(svcre1)+'¬, '
          +'¬wf1¬ : ¬'+%trim(svtop1)+'¬, '
          +'¬svtob¬ : ¬'+%trim(svtob)+'¬, '
          +'¬wg1¬ : ¬'+%trim(SVAKD1)+'¬, '
          +'¬wh1¬ : ¬'+%trim(SVASV1)+'¬, '
          +'¬wi1¬ : ¬'+%trim(%editc(SVABL1:'4'))+'¬, '
          +'¬wj1¬ : ¬'+%trim(%editc(SVAGR1:'4'))+'¬, '
          +'¬wk1¬ : ¬'+%trim(%editc(SVASA1:'4'))+'¬, '
          +'¬sveh2¬ : ¬'+%trim(sveh21)+'¬, '
          +'¬svpreae¬ : ¬'+%trim(svpreÆ)+'¬, '
          +'¬sveh2ae¬ : ¬'+%trim(SVEH2Æ)+'¬, '
          +'¬svln¬ : ¬'+%trim(%editc(svln:'Z'))+'¬, '
          +'¬svrefl¬ : ¬'+%trim(%editc(svrefl:'Z'))+'¬, '
N03       +'¬svexr01¬ : ¬'+%trim(svexr01)+'¬, '
N03       +'¬svexr02¬ : ¬'+%trim(svexr02)+'¬, '
N03       +'¬svexr03¬ : ¬'+%trim(svexr03)+'¬, '
N03       +'¬svexr04¬ : ¬'+%trim(%editc(svexr04:'4'))+'¬, '
N03       +'¬svexr05¬ : ¬'+%trim(%editc(svexr05:'4'))+'¬, '
          +'¬svexr06¬ : ¬'+%trim(svexr06)+'¬, '
          +'¬svexr07¬ : ¬'+%trim(%editc(svexr07:'4'))+'¬, '
          +'¬sverr¬ : ¬'+%trim(sverr)+'¬}';
      /end-free
     C                   call      'JSONFIX'
     C                   parm                    JSONstring
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')

     C*                  ENDIF

     C                   IF        *IN61
     C     SADVkey       READE     SADV2                                  55
     C                   ELSE
     C                   IF        LIN > 1
     C     SADVkey       READE     SADV4                                  55
     C                   ELSE
     C     SADVkey       READE     SADV4E                                 55
     C                   ENDIF
     C                   ENDIF
     C* N55ANTLEST       COMP      100                                    55
     C                   ENDDO
     c                   ENDIF
      * ............................................................................................
      * Skriv JSON-Liste/Array  Avslutning
      * ............................................................................................
     c                   eval      JSONstring =']'
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')
      * ............................................................................................
      * Skriv JSON-string Avsluttning
      * ............................................................................................
     c                   eval      JSONstring ='}'
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')
      *
      * Quit
     C                   exsr      Exit


      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Lukk fil
     C                   CLOSE     BRIDF
     C  N50              CLOSE     SADV4
     C  N50              CLOSE     SADV4E
     C  N50              CLOSE     SADV2
     C  N50              CLOSE     SADH

      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   MOVE      *ON           *INLR
     C                   return
     C                   endsr
      *
      *********************************************************
     C     Parse         Begsr
      *********************************************************
      * Get input
     C                   EVAL      WSUSER = zhbgetvar('USER')
     C                   EVAL      SVAVDA = zhbgetvar('AVD')
     C                   EVAL      SVAVD  = c2n2(SVAVDA)
     C                   EVAL      SVTDNA = zhbgetvar('OPD')
     C                   EVAL      SVTDN  = c2n2(SVTDNA)
     C                   EVAL      LINA   = zhbgetvar('LIN')
     C                   EVAL      LIN    = c2n2(LINA)
     C                   EVAL      VARENRA= zhbgetvar('VARENR')
     C                   EVAL      VARENR = c2n2(VARENRA)
     C                   EVAL      WMODE  = zhbgetvar('MODE')
     C                   IF        LIN=*ZEROS
     C                   EVAL      LIN=1
     C                   ENDIF
     C     VARENR        COMP      *ZEROS                             61
     c                   endsr
      *
      *********************************************************
     C     INIT          Begsr
      *********************************************************
     C                   OPEN      BRIDF
      * Test innlogging
     C     WSUSER        CHAIN     BRIDF                              50
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C                   call      BILIBL
     C                   end
      *
     C     SADVkey       klist
     C                   kfld                    SVAVD
     C                   kfld                    SVTDN
     C     SADVkey2      klist
     C                   kfld                    SVAVD
     C                   kfld                    SVTDN
     C                   kfld                    LIN
     C     SADVkey3      klist
     C                   kfld                    SVAVD
     C                   kfld                    SVTDN
     C                   kfld                    VARENR
     C                   Z-ADD     1             X                 3 0
      *
     C   50              eval      Error = 'Invalid userID'
     C  N50              OPEN      SADV4
     C  N50              OPEN      SADV4E
     C  N50              OPEN      SADV2
     C  N50              OPEN      SADH
     C  N50SADVKEY       CHAIN     SADH                               51

     c                   endsr
