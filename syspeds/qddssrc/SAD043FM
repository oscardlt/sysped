     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPDOC('SAD04301' 'SAD043' 'SY400-
     A                                      LO/SADTVINN')
     A                                      HELP
     A                                      ALTHELP
      *
     A          R BILDE01
     A                                      CF03(03 'Avslutt program.')
     A                                  2  7'SAD043'
     A                                      COLOR(BLU)
     A                                  4 23'Endre status på SAD-transitt'
     A                                      COLOR(WHT)
     A                                  8 23'Avdeling:'
     A            WSAVD          4Y 0B  8 40DSPATR(UL)
     A  99                                  DSPATR(RI)
     A  99                                  DSPATR(PC)
     A                                      EDTCDE(4)
     A                                 10 23'Signatur:'
     A            WSSG           3A  B 10 40TEXT('Signatur')
     A                                      DSPATR(UL)
     A  98                                  DSPATR(RI)
     A  98                                  DSPATR(PC)
     A  98                                  ERRMSG('Tastet tariffør er ikke reg-
     A                                      istrert.' 98)
     A                                 12 23'Oppdragsnr.'
     A            WSOPD          7Y 0B 12 40TEXT('Oppdragsnummer')
     A                                      DSPATR(UL)
     A  99                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                 15 23'Ny status:'
     A            WSST           1A  B 15 40TEXT('Status ny')
     A                                      DSPATR(UL)
     A                                 23 23'F3 = Avslutt'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R BILDE02
     A                                      CF05(05 'Endre status')
     A                                  1 10'SAD-transitt'
     A                                      COLOR(WHT)
     A                                  1 41'1.Dekl.'
     A                                  1 59'4.L.liste'
     A            SEDTY          2A  O  1 49TEXT('Deklarasjonstype')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SEDK           2A  O  1 55TEXT('DEKLARASJONSKODE')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SELSTL         2Y 0O  1 69TEXT('LASTELISTE')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  2  2'2.Avsender'
     A            SEKNK          8Y 0O  2 13TEXT('Kundenr Kjøper')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  2 23'Regnr'
     A            SERG          11A  O  2 29TEXT('Regn Fylk.skatt.sjef')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  2 41'5.V.post'
     A            SEVPOS         3Y 0O  2 50TEXT('VAREPOST')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  2 54'6.Kolli'
     A            SENTK          7Y 0O  2 62TEXT('Antall kolli')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            SENAK         30A  O  3  2TEXT('Navn Kjøper')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  3 41'Brt vekt'
     A            SEVKB          9Y 0O  3 50TEXT('Bruttovekt')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  3 61'Ref'
     A            SEAVD          4Y 0O  3 65TEXT('AVDELING')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            SETDN          7Y 0O  3 70TEXT('Tolldeklarasjonsnr')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            SESG           3A  O  3 78TEXT('Signatur')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SEADK1        30A  O  4  2TEXT('Adresse-1 Kjøper')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  4 40'15.Avs/utf.land'
     A            SELKA          2A  O  4 56TEXT('Landk avs/utf-land')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SELKAT        17A  O  4 59TEXT('F.T. AVS/UTF.LAND')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SEADK2        30A  O  5  2TEXT('Adresse-2 Kjøper')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  5 40'17.Be.land'
     A            SELKB          2A  O  5 56TEXT('LANDK TIL B.LAND')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SEFTB         17A  O  5 59TEXT('FRITEKST TIL B.LAND')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SEADK3        30A  O  6  2TEXT('Adresse-3 Kjøper')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  6 40'18.Mid.land'
     A            SELKM          2A  O  6 56TEXT('LK MIDLETS ID LAND')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SEFTM         17A  O  6 59TEXT('F.T. MIDLETS ID LAND')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  7  2'8.Mottaker'
     A            SEKNS          8Y 0O  7 13TEXT('Kundenr Selger')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  7 40'19.Cont.'
     A            SEKDC          1A  O  7 49TEXT('Container 0/1')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SENAS         30A  O  8  2TEXT('Navn Selger')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  8 40'21.Ident.'
     A            SETRID        17A  O  8 50TEXT('Transport ID')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SELKT          2A  O  8 68TEXT('Landk Tr.middel')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SEADS1        30A  O  9  2TEXT('Adresse-1 Selger')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  9 40'25.Tr.måte'
     A            SETRM          2S 0O  9 51TEXT('Transportmåte')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SEADS2        30A  O 10  2TEXT('Adresse-2 Selger')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SEADS3        30A  O 11  2TEXT('Adresse-3 Selger')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 12  2'Fritekst ved dataliste'
     A                                      COLOR(WHT)
     A                                 12 52'Stempel (rubr. C)'
     A                                 13  1'31.'
     A            SEFT1         45A  O 13  5TEXT('Fritekst-1')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SECLK          2A  O 13 52DSPATR(UL)
     A                                      COLOR(YLW)
     A            SECDS         19A  O 13 55DSPATR(UL)
     A                                      COLOR(YLW)
     A            SEFT2         45A  O 14  5TEXT('Fritekst-2')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SECTN          5Y 0O 14 52DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSCDT          6Y 0O 14 62DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(Y)
     A                                 15  1'44.'
     A            SEFT3         45A  O 15  5TEXT('Fritekst-3')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SECNV         15A  O 15 52DSPATR(UL)
     A                                      COLOR(YLW)
     A            SECAN          6A  O 15 68DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 16  1'50.Hovedansvarlig'
     A            SEKNA          8Y 0O 16 19TEXT('HOVEDANSVARLIG')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 16 39'51.'
     A            SESTG1         8A  O 16 43TEXT('GARANTITOLLSTED-1')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SELKG1         2A  O 16 52TEXT('GARANTI-land-1')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SESTG2         8A  O 16 56TEXT('GARANTITOLLSTED-2')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SELKG2         2A  O 16 65TEXT('GARANTI-land-2')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SESTG3         8A  O 16 69TEXT('GARANTITOLLSTED-3')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SELKG3         2A  O 16 78TEXT('GARANTI-land-3')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SENAA         30A  O 17  2TEXT('Navn ANSVARLIG')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SESTG4         8A  O 17 43TEXT('GARANTITOLLSTED-4')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SELKG4         2A  O 17 52TEXT('GARANTI-land-4')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SESTG5         8A  O 17 56TEXT('GARANTITOLLSTED-5')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SESTG6         8A  O 17 69TEXT('GARANTITOLLSTED-6')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SELKG5         2A  O 17 65TEXT('GARANTI-land-5')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SELKG6         2A  O 17 78TEXT('GARANTI-land-6')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SEADA1        30A  O 18  2TEXT('Adresse-1 ANSVARLIG')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 18 39'52.Garanti'
     A            SEGFT1        30A  O 18 50TEXT('GARATI FRITEKST-1')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SEADA3        30A  O 19  2TEXT('Adresse-3 ANSVARLIG')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SEGKD          1S 0O 19 40TEXT('GARATIKODE')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SEGFT2        30A  O 19 50TEXT('GARATI FRITEKST-2')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 20  1'Rubr. D/J'
     A            SEDFT1        15A  O 20 11DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 20 39'53.Tollsted'
     A            SETLKB         2A  O 20 51TEXT('LANDK TIL B.T.STED')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SETSTB        20A  O 20 54TEXT('BESTEMMELSESTOLLSTED')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SEDFT2        22A  O 21 11DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 21 39'54.Sted'
     A            SEDST         15A  O 21 47TEXT('Deklarantens sted')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 21 63'Reg.dato'
     A            WSDT           6Y 0O 21 72TEXT('Registreringsdato')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(Y)
     A            WSDDT          6Y 0O 22 11DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(Y)
     A                                 22 34'Dato'
     A            SEDTG         10A  O 22 39TEXT('Dekl. godkj.dato')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 22 50'Løpenr'
     A            SETLL         10A  O 22 57TEXT('Tollnr Løpenr')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 22 68'Eksp.'
     A            SETLE          6A  O 22 74TEXT('Tollnr Ekspsted')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 23 48'Tariffør'
     A                                 23  1'Gammel status'
     A                                      COLOR(TRQ)
     A            SEST           1A  O 23 15TEXT('Status')
     A                                      DSPATR(UL)
     A                                      COLOR(PNK)
     A                                 23 18'Ny status'
     A                                      COLOR(TRQ)
     A            WSST           1A  O 23 28TEXT('Status')
     A                                      DSPATR(UL)
     A                                      COLOR(RED)
     A                                 23 33'Innstikk'
     A                                      COLOR(TRQ)
     A            SEMI           1A  O 23 42TEXT('Status')
     A                                      DSPATR(UL)
     A                                      COLOR(RED)
     A            SETARF        16A  O 23 57TEXT('TARIFFØRSNAVN')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 24  2'F5=Endre status'
     A                                      COLOR(BLU)
