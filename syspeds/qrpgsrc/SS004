      *********************************************************************
      * PROGRAM BEHANDLER INPUT FRA SS002 ELLER SS003 OG VISER ORDRE
      *
CRTPG+*  CRTPGM SYSPED/SS004 MODULE(*LIBL/SS004
CRTPGM*        *LIBL/CHECKUSR *LIBL/INCGI) ACTGRP(CGI) AUT(*USE)
      *
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 YBC PTF10 VIS SYSPED OPPDRAG
********************************************************************
      *
      *********************************************************************
      * MAIN PROGRAM FLOW
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
     FGSSTRF    IF   E           K DISK    USROPN
     FGSSORF    IF   E           K DISK    USROPN
     FGSSORL21  IF   E           K DISK    USROPN
     F                                     RENAME(GSSORFR:GSSORFR21)
     FGSSORL24  IF   E           K DISK    USROPN
     F                                     RENAME(GSSORFR:GSSORFR24)
     FGSSORL25  IF   E           K DISK    USROPN
     F                                     RENAME(GSSORFR:GSSORFR25)
     FGSSPOF    IF   E           K DISK    USROPN
     FGSSPAF    IF   E           K DISK    USROPN
     FGSSOLL01  IF   E           K DISK    USROPN
     FGSSCGF    IF   E           K DISK    USROPN
     FGSSBXF    IF   E           K DISK    USROPN
     FHEADF     IF   E           K DISK    USROPN
     FBRIDF     UF   E           K DISK    USROPN
     FBRPARF    IF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Prototype defintions and standard system API error structure
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      *--------------------------------------------------------------------
      * Variables common to CGI programs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
      * Variables received from the input string
     D lng             s              2
     D UsrSpcName      s             10
     D WSUSER          S             10
     D WINT            S              6
     D WSNA            S             30
     D WSONO           S              7
     D WSTNO           S             11
     D WSBOX           S              3S 0
     D WKEY            S            150
     D lstbrd          s              1
     D PGM             C                   CONST('SYSPED/CHECKUSR')
      * Variables received from the input string
     D zeros           s             15a   inz('000000000000000')
      * Number of records matching search criteria
     DRecordCnt        s              5S 0
      *
     D Lib             s             10
     D Fn              s             10
     D Mbr             s             10
      *
     D Spaces          s             12a   inz('&nbsp;&nbsp;')
     D                 DS
     D  TRTNO                  1     11  0
     D  FRTAVD                 1      4  0
     D  FRTOPD                 5     11  0
      *--------------------------------------------------------------------
      * Prolog common to CGI programs
      * -Receive input from the remote browser
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prolog3
      *=====================================================================******
      * MAIN PROCESS LINE
      *=====================================================================******
      * Get program start time for calculating execution time
     C                   time                    timedata1
      * Parse input
     C                   exsr      Parse
      *
     C                   EXSR      INIT
     C                   CALLB     PGM
     C                   PARM                    BIBRID
     C                   PARM                    UsrSpcName
     C                   PARM                    UINN              1
     C                   IF        UINN = 'N'
     C                   GOTO      SLUTT
     C                   END
     C     WSUSER        CHAIN     BRIDF                              50
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVE      WDT           BIDTF
     C                   MOVE      WTM           BITIDF
     C                   UPDATE    BRIDFR
      * Set output skeleton html member name
     C                   exsr      SetSkl
      * Read skeleton output html, etc.
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
      * Retrieve environment variables
     C                   exsr      RtvEnvVar
      *------------------                                                   ******
      * Write HTML sections
      *
      * 1-Section /$top
     C                   exsr      SetTop
     C                   callp     wrtsection('top')
      * Vis turens opplyninger
     C                   exsr      Vishead1
     C                   callp     wrtsection('header1')
      *------------------                                                   ******
      * Write HTML sections
      * 3-Section /$tablestr1
     C                   exsr      SetTabStr
      * Vis turens innhold
     C                   callp     wrtsection('tablestr1')
      *
     C                   MOVE      *ZEROS        XONO1             7 0
     C                   MOVE      *ZEROS        XONO2             7 0
     C                   MOVE      *ZEROS        XONO3             7 0
     C                   MOVE      *BLANKS       XBCD1             5
     C                   MOVE      *BLANKS       XBCD2             5
     C                   MOVE      *BLANKS       XBCD3             5
      *
     C     TRTNO         SETLL     GSSORL21
     C     TRTNO         READE     GSSORL21                               50
     C   50              Z-ADD     9999999       XONO1
     C  N50              MOVE      ORONO         XONO1
     C   50              MOVE      '99999'       XBCD1
     C  N50              MOVE      ORBCD         XBCD1
     C     TRTNO         SETLL     GSSORL24
     C     TRTNO         READE     GSSORL24                               51
     C   51              Z-ADD     9999999       XONO2
     C  N51              MOVE      ORONO         XONO2
     C   51              MOVE      '99999'       XBCD2
     C  N51              MOVE      ORBCD         XBCD2
     C     TRTNO         SETLL     GSSORL25
     C     TRTNO         READE     GSSORL25                               52
     C   52              Z-ADD     9999999       XONO3
     C  N52              MOVE      ORONO         XONO3
     C   52              MOVE      '99999'       XBCD3
     C  N52              MOVE      ORBCD         XBCD3
      *
     C     *IN50         DOWEQ     '0'
     C     *IN51         OREQ      '0'
     C     *IN52         OREQ      '0'
      *
     C                   SELECT
     C     XBCD3         WHENLT    XBCD1
     C     XBCD3         ANDLT     XBCD2
     C                   MOVE      XONO3         XONO
     C     XBCD2         WHENLT    XBCD1
     C     XBCD2         ANDLT     XBCD3
     C                   MOVE      XONO2         XONO
     C     XBCD1         WHENLT    XBCD2
     C     XBCD1         ANDLT     XBCD3
     C                   MOVE      XONO1         XONO
     C     XBCD1         WHENEQ    XBCD2
     C     XBCD1         ANDLT     XBCD3
     C     XONO2         IFLT      XONO1
     C                   MOVE      XONO2         XONO
     C                   ELSE
     C                   MOVE      XONO1         XONO
     C                   END
     C     XBCD2         WHENEQ    XBCD3
     C     XBCD2         ANDLT     XBCD1
     C     XONO3         IFLT      XONO2
     C                   MOVE      XONO3         XONO
     C                   ELSE
     C                   MOVE      XONO2         XONO
     C                   END
     C     XBCD3         WHENEQ    XBCD1
     C     XBCD3         ANDLT     XBCD2
     C     XONO1         IFLT      XONO3
     C                   MOVE      XONO1         XONO
     C                   ELSE
     C                   MOVE      XONO3         XONO
     C                   END
     C                   OTHER
     C                   SELECT
     C     XONO3         WHENLT    XONO1
     C     XONO3         ANDLT     XONO2
     C                   MOVE      XONO3         XONO              7 0
     C     XONO2         WHENLT    XONO1
     C                   MOVE      XONO2         XONO
     C                   OTHER
     C                   MOVE      XONO1         XONO
     C                   ENDSL
     C                   ENDSL
     C     XONO          CHAIN     GSSORF                             33
      *
     C     ORONO         SETLL     GSSPOF
     C     ORONO         READE     GSSPOF                                 33
     C     ORONO         CHAIN     GSSPAF                             33
     C                   IF        XAGENT <> 'J'
     C                   IF        ORCNO <> BIKUNR
     C                   IF        XCG  = '  '
     C                   GOTO      XNESTE
     C                   ELSE
     C     ORCNO         CHAIN     GSSCGF                             34
     C  N34XCG           COMP      CGCG                               3434
     C  N34              GOTO      XNESTE
     C                   END
     C                   END
     C                   END
     C                   IF        *IN33
     C                   MOVE      'C'           PAPKD
     C                   MOVE      *BLANKS       PATXT1
     C                   MOVE      *BLANKS       PATXT2
     C                   MOVE      *BLANKS       PATXT3
     C                   MOVE      *BLANKS       PATXT4
     C                   MOVE      *BLANKS       PATXT5
     C                   END
     C                   exsr      SetTabRow1
     C                   callp     wrtsection('tablerow1')
      *
     C     XNESTE        TAG
     C                   SELECT
     C     XONO1         WHENEQ    XONO
     C  N50TRTNO         READE     GSSORL21                               50
     C  N50              IF        ORCNO <> BIKUNR
     c                   GOTO      XNESTE
     c                   end
     C   50              Z-ADD     9999999       XONO1
     C  N50              MOVE      ORONO         XONO1
     C   50              MOVE      '99999'       XBCD1
     C  N50              MOVE      ORBCD         XBCD1
     C     XONO2         WHENEQ    XONO
     C  N51TRTNO         READE     GSSORL24                               51
     C  N51              IF        ORCNO <> BIKUNR
     c                   GOTO      XNESTE
     c                   end
     C   51              Z-ADD     9999999       XONO2
     C  N51              MOVE      ORONO         XONO2
     C   51              MOVE      '99999'       XBCD2
     C  N51              MOVE      ORBCD         XBCD2
     C                   OTHER
     C  N52TRTNO         READE     GSSORL25                               52
     C  N52              IF        ORCNO <> BIKUNR
     c                   GOTO      XNESTE
     c                   end
     C   52              Z-ADD     9999999       XONO3
     C  N52              MOVE      ORONO         XONO3
     C   52              MOVE      '99999'       XBCD3
     C  N52              MOVE      ORBCD         XBCD3
     C                   ENDSL
     C  N50
     CORN51
     CORN52              ENDDO                                                  *hival
      *
     C                   Z-ADD     -1            WSBOX
     C                   MOVE      *ZEROS        XANTB             3 0
      *
     C     GSSOLL01K     SETLL     GSSOLL01
     C     TRTNO         READE     GSSOLL01                               50
     C     *IN50         DOWEQ     '0'
     C                   IF        OLBOX <> WSBOX
     C     BXGSL         IFEQ      '*'
     C                   EVAL      OLBOX = WSBOX +1
     C                   END
     C                   MOVE      OLBOX         WSBOX
      * Vis box heading
     C                   exsr      Vishead2
     C                   callp     wrtsection('header2')
     C                   callp     wrtsection('tablestr2')
     C                   END
     C     OLONO         SETLL     GSSPOF
     C     OLONO         READE     GSSPOF                                 33
     C                   exsr      SetTabRow2
     C                   callp     wrtsection('tablerow2')
     C     TRTNO         READE     GSSOLL01                               50
     C  N50              ENDDO                                                  *hival
      *
      * 5-Section /$tableend
     C                   callp     wrtsection('tableend')
      *
      * 5a-Section /$foundsome
     C                   exsr      SetFndSome
     C                   IF        WKEY = *BLANKS
     C                   callp     wrtsection('foundsome')
     C                   ELSE
     C                   callp     wrtsection('foundsome2')
     C                   END
      * 6-Section /$endhtml
     C     endhtml       tag
     C                   exsr      SetEnd
     C     SLUTT         tag
     C                   callp     wrtsection('endhtml')
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
     C                   EXSR      EXIT
     C                   return
      *=====================================================================******
      * Parse input
      *=====================================================================******
     C     Parse         begsr
     C                   MOVE      'N'           XFEIL             1
     C                   eval      lng     = zhbgetvar('lng')
     C                   eval      lstbrd  = zhbgetvar('lstbrd')
     C                   EVAL      WSUSER  = zhbgetvar('USER')
     C                   EVAL      WINT    = zhbgetvar('WINT')
     C                   EVAL      UsrSpcName  = zhbgetvar('UsrSpcName')
     C                   EVAL      WSNA    = zhbgetvar('WSNA')
     C                   EVAL      WSTNO   = zhbgetvar('WSTNO')
     C                   MOVE      WSTNO         TRTNO
     C                   EVAL      WKEY    = zhbgetvar('WKEY')
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$top
      *  (search criteria)
      *=====================================================================******
     C     SetTop        begsr
      * Repeat national language for the next invocation
     C                   callp     updhtmlvar('LNG':lng:
     C                             InitHTMLVars)
      * Repeat background color for the next invocation
     C                   callp     updhtmlvar('TXTCOLOR':'black')
     C                   callp     updhtmlvar('BOLD':' ')
     C                   callp     updhtmlvar('BGCOLOR':BIFARG)
     C                   callp     updhtmlvar('LINK':'0000FF')
     C                   callp     updhtmlvar('VLINK':'FF0000')
     C                   callp     updhtmlvar('ALINK':'00FF00')
      * KUNDE
     C                   callp     updhtmlvar('WSNA':WSNA)
BODY C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updhtmlvar('USER':WSUSER)
     C                   callp     updhtmlvar('WINT':WINT)
     C                   callp     updHtmlVar('UsrSpcName':UsrSpcName)
     C                   callp     updhtmlvar('WKEY':WKEY)
     C                   callp     updHTMLvar('sdato':
     C                             %trim(%editw(BIDTV:'    .  .  ')))
     C                   callp     updHTMLvar('stid':
     C                             %trim(%editw(BITIDV:'  .  .  ')))
N01  C                   callp     updhtmlvar('HEAVD':
N01  C                             %trim(%editc(FRTAVD:'Z')))
N01  C                   callp     updhtmlvar('HEOPD':
N01  C                             %trim(%editc(FRTOPD:'Z')))
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$tablestr (table start)
      *=====================================================================******
     C     SetTabStr     begsr
     C     lstbrd        ifeq      'Y'
     C                   callp     updhtmlvar('LB':'0')
     C                   else
     C                   callp     updhtmlvar('LB':'1')
     C                   endif
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for header1
      *=====================================================================******
     C     Vishead1      begsr
      *
     C     TRTNO         CHAIN     GSSTRF                             33
     C                   callp     updhtmlvar('TRAD1':TRAD1)
     C                   callp     updhtmlvar('TRAD2':TRAD2)
     C                   callp     updhtmlvar('TRAD3':TRAD3)
     C                   callp     updhtmlvar('TRAD4':TRAD4)
     C                   callp     updhtmlvar('TRAD5':TRAD5)
     C                   callp     updhtmlvar('TRAD6':TRAD6)
     C                   callp     updhtmlvar('TRDTS':
     C                             %trim(%editW(TRDTS:'    .  .  ')))
     C                   callp     updhtmlvar('TRDTL':
     C                             %trim(%editW(TRDTL:'    .  .  ')))
     C                   callp     updhtmlvar('TRTML':
     C                             %trim(%editW(TRTML:'0  :  ')))
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$tablerow (table row)
      *=====================================================================******
     C     SetTabRow1    begsr
      *
     C                   callp     updhtmlvar('ORONO':
     C                             %trim(%editc(ORONO:'Z')))
     C                   MOVE      ORONO         WSONO
     C                   callp     updhtmlvar('WSONO':WSONO)
     C                   callp     updhtmlvar('POCONO':POCONO)
     C                   IF        ORTNO3 = TRTNO
     C                   callp     updhtmlvar('ORLC':ORLC2)
     C                   ELSE
     C                   callp     updhtmlvar('ORLC':ORLC)
     C                   END
     C                   MOVE      *BLANKS       ORSTAT           30
     C                   SELECT
     C                   WHEN      ORST = 'P'
     C                   EVAL      ORSTAT = 'PRE-REGISTRATIONS'
     C                   WHEN      ((ORST = '0') AND (PAPKD = 'C'))
     C                   EVAL      ORSTAT = 'IN STOCK'
     C                   WHEN      ((ORST = '0') AND (PAPKD = 'P'))
     C                   EVAL      ORSTAT = 'IN STOCK (PARTIAL)'
     C                   WHEN      ((ORST = '0') AND (PAPKD = ' '))
     C                   EVAL      ORSTAT = 'IN STOCK (PARTIAL, NOT COMPL.)'
     C                   WHEN      ((ORST = '1') OR (ORST = '2'))
     C                   IF        PAPKD = 'C'
     C                   EVAL      ORSTAT = 'IN WORK'
     C                   ELSE
     C                   EVAL      ORSTAT = 'IN WORK (PARTIAL)'
     C                   END
     C                   WHEN      ORST > '2'
     C                   IF        PAPKD = 'C'
     C                   EVAL      ORSTAT = 'SHIPPED'
     C                   ELSE
     C                   EVAL      ORSTAT = 'SHIPPED (PARTIAL)'
     C                   END
     C                   ENDSL
     C                   callp     updhtmlvar('ORSTAT':ORSTAT)
     C                   IF        ORDTL = 0
     C                   callp     updhtmlvar('ORDTL':
     C                             %trim(%editW(ORDTWL:'    .  .  ')))
     C                   ELSE
     C                   callp     updhtmlvar('ORDTL':
     C                             %trim(%editW(ORDTL:'    .  .  ')))
     C                   END
     C                   callp     updhtmlvar('ORCLI':
     C                             %trim(%editc(ORCLI:'Z')))
     C                   callp     updhtmlvar('ORWT':
     C                             %trim(%editc(ORWT:'4')))
     C                   callp     updhtmlvar('ORSNM':ORSNM)
     C                   callp     updhtmlvar('ORCURC':ORCURC)
     C                   callp     updhtmlvar('ORAMT':
     C                             %trim(%editc(ORAMT:'4')))
     C     OPDKEY        CHAIN     HEADF                              33
     C  N33HEUR          COMP      'D'                                3333
     C   33              MOVE      *BLANKS       HEGN
     C                   callp     updhtmlvar('HEGN':HEGN)
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for header2
      *=====================================================================******
     C     Vishead2      begsr
      *
     C                   callp     updhtmlvar('WSBOX':
     C                             %trim(%editc(WSBOX:'Z')))
      *
     C                   MOVE      *BLANKS       WSCID            17
     C                   MOVE      WSBOX         XWSBOX            3 0
     C     TRTNO         SETLL     GSSBXF
     C     TRTNO         READE     GSSBXF                                 33
     C     *IN33         DOWEQ     '0'
     C     XWSBOX        IFEQ      BXBOX
     C                   ADD       1             XANTB
     C                   callp     updhtmlvar('WSCLNO':
     C                             %trim(%editc(XANTB:'Z')))
     C                   callp     updhtmlvar('WSBOXT':
     C                             %trim(%editc(BXTOT:'Z')))
     C                   callp     updhtmlvar('WSBWT':
     C                             %trim(%editc(BXVKT:'4')))
     C                   callp     updhtmlvar('WSBM3':
     C                             %trim(%editc(BXM3:'4')))
     C                   MOVE      *BLANKS       ZDIM             30
     C                   MOVEL     BXLEN         ZDIM
     C                   CAT       'X':0         ZDIM
     C                   MOVE      BXBRE         XAN05             5
     C                   CAT       XAN05:0       ZDIM
     C                   CAT       'X':0         ZDIM
     C                   MOVE      BXHØY         XAN05             5
     C                   CAT       XAN05:0       ZDIM
     C                   callp     updhtmlvar('WSDIM':ZDIM)
     C                   IF        BXID = *BLANKS
     C                   callp     updhtmlvar('WSCID':BXCN)
     C                   ELSE
     C                   callp     updhtmlvar('WSCID':BXID)
     C                   END
     C                   LEAVE
     C                   END
     C     TRTNO         READE     GSSBXF                                 33
     C  N33              ENDDO
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$tablerow (table row)
      *=====================================================================******
     C     SetTabRow2    begsr
      *
     C                   callp     updhtmlvar('OLONO':
     C                             %trim(%editc(OLONO:'Z')))
     C                   callp     updhtmlvar('OLLNO':
     C                             %trim(%editc(OLLNO:'Z')))
     C                   callp     updhtmlvar('POCONO':POCONO)
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$foundsome (some Recds were found)
      *=====================================================================******
     C     SetFndSome    begsr
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$endhtml
      *=====================================================================******
     C     SetEnd        begsr
      * Server protocol
     C                   callp     updhtmlvar('PROTOCOL':S_Protocol)
      * Compute run time
     C                   time                    timedata2
     C     timedata2     subdur    timedata1     ms:*ms
     C                   eval      sec = ms / 1000000
     C                   callp     updhtmlvar('runtime':
     C                             %trim(%editw(sec:'     0 .   ')))
      *
     C                   endsr
      *=====================================================================******
      * Set html output skeleton member before its read into memory
      *=====================================================================******
     C     SetSkl        begsr
      *------------------
      * Set html output skeleton member
     C                   eval      lng = uppify(lng)
      * "HTMLSRC  " er dft = NORSK, ved å trykke på FLAGGET i inng.
      * kan dette endre stil "HTMLSRCXX" avh. av støttede språk
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC' + lng
     C                   eval      Mbr = 'SS004'
      *------------------
     C                   endsr
      *=====================================================================******
      *  Retrieve environment variables
      *=====================================================================******
     C     RtvEnvVar     begsr
      * Use getenvp to get this server's protocol and name
     C                   eval      S_Protocol =getenv('SERVER_PROTOCOL':
     C                             qusec)
     C                   eval      S_Name     =getenv('SERVER_NAME':
     C                             qusec)
      *
     C                   endsr
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
     C                   OPEN      BRIDF
     C     WSUSER        CHAIN(N)  BRIDF                              50
     C     BILIBL        ifeq      *blanks
     C                   CALLB     'INCGI'
     C                   PARM                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
     C                   open      GSSORF                               50
     C                   open      GSSORL21                             50
     C                   open      GSSORL24                             50
     C                   open      GSSORL25                             50
     C                   open      GSSPOF                               50
     C                   open      GSSPAF                               50
     C                   open      GSSTRF                               50
     C                   open      GSSOLL01                             50
     C                   open      GSSCGF                               50
     C                   open      GSSBXF                               50
     C                   open      HEADF                                50
     C                   open      BRPARF                               50
      *
     C     BrParKey      klist
     C                   kfld                    WSUSER
     C                   kfld                    PAKUNR
     C                   kfld                    PAID
     C     GSSOLL01K     klist
     C                   kfld                    TRTNO
     C                   kfld                    XBOX
     C     OPDKEY        klist
     C                   kfld                    FRTAVD
     C                   kfld                    FRTOPD
     C                   Z-ADD     1             XBOX              3 0
     C     BIKNG         CHAIN     GSSCGF                             50
     C                   MOVE      CGCG          XCG               2
     C                   MOVE      'SSAGT'       PAID
     C     BrParKey      CHAIN     BRPARF                             33
     C  N33              MOVE      'J'           XAGENT            1
      *
     C                   endsr
      *=====================================================================******
      *  AVSLUTT
      *=====================================================================******
     C     EXIT          begsr
     C                   close     BRIDF
     C                   close     GSSORF
     C                   close     GSSORL21
     C                   close     GSSORL24
     C                   close     GSSORL25
     C                   close     GSSPOF
     C                   close     GSSPAF
     C                   close     GSSTRF
     C                   close     GSSOLL01
     C                   close     GSSCGF
     C                   close     GSSBXF
     C                   close     HEADF
     C                   close     BRPARF
     C                   eval      *inlr = *on
      *
     C                   endsr
