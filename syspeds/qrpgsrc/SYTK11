      *
      ************************************************************
      *** PROGRAM for å generere importfil for Autoroute       ***
      ***         Alle biler i et gitt område                  ***
      ************************************************************
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 JOV PTF10 kode Google = Y svar skal til google maps
10XXX * 02 JOV PTF10 Endrin i JSON-objekt som returneres til Google
      * 03 JOV PTF10 GOOGLE-LOGIKK NÅ FLYTTET TIL ESTK11G-Dvs denne er nå KUN AUTOROUTE
      *              (bruker ikke tid på å rydde her, men fremtidig vedlikehold unødig)
********************************************************************
      *
     FTKGPS     IF   E           K DISK
     FTKUSER    IF   E           K DISK
     FTKAREA    IF   E           K DISK
     FUNITL04   IF   E           K DISK
     FQ1024     O  A F 1024        DISK
     FSYTK11FM  CF   E             WORKSTN USROPN
     D Path            s            100
     D Post1           s              1
     D FeilOmr         s              1
     D FeilUnty        s              1
     D Streng          s           1024
     D SJNR            s              8  0
     D Info1           s            100
     D Info2           s            100
     D Info3           s            100
     D Info4           s            100
N01  D SeTur           s            500
     D CurrTime        s               z
     D MinDs           ds
     D  Min                            z
     D  MinYear                       4    overlay(Min:1)
     D  MinMonth                      2    overlay(Min:6)
     D  MinDay                        2    overlay(Min:9)
     D  MinHour                       2    overlay(Min:12)
     D  MinMin                        2    overlay(Min:15)
     D  MinSec                        2    overlay(Min:18)
      *
     D MiniDs          ds
     D MiniDtTi                1     14  0
     D MiniYear                1      4  0
     D MiniMonth               5      6  0
     D MiniDay                 7      8  0
     D Today                   1      8  0
     D MiniHour                9     10  0
     D MiniMin                11     12  0
     D MiniSec                13     14  0
     D TodayA                  1      8
     D TodayHMSA               9     14
      *
     D TkdtDs          ds
     D TKDTTI                  1     14  0
     D TKDT                    1      8  0
     D TKTI                    9     14  0
      *___________________________
      * Finner username
      *"""""""""""""""""""""""""""
     D                SDS
     D  ZJOB                 244    253
     D  ZUSER                254    263
     D  ZJOBNR               264    269  0
     D                 DS
     D  ES_PATH                1    100
     D  Path1_5                1      5
     D  Path6_50               6     50
     D  Path51_60             51     60
     D  Path61_63             61     63
     D  Path64_65             64     65
     D  Path66_69             66     69
     D                 DS
     D  TKBRE_GD               1      8  0
     D  TKBRE_G                1      2
     D  TKBRE_D                3      8
     D                 DS
     D  TKLEN_GD               1      9  0
     D  TKLEN_G                1      3
     D  TKLEN_D                4      9
      *
N01   * Definere  hex appostrof
N01  D Ap              c                   x'7D'
      *
     C     *entry        Plist
     C                   Parm                    PATH
     C                   move      Path          ES_PATH
      *
     C     KEY           KLIST
     C                   KFLD                    XXBRID           10
     C                   KFLD                    XXDT              8 0
     c                   move      '9999999999'  XXBRID
     c                   move      99999999      XXDT
      * Defaulter
     c                   move      ZUSER         MAUSER
     C     PATH          IFNE      *BLANKS
     c                   move      '*DFT      '  MAUSER
     C                   END
     C     MAUSER        Chain     TKUSER                             33
     C   33'*DFT      '  Chain     TKUSER                             33
     c                   move      MAMAPP        WSMAPP
     c     Path1_5       Ifeq      '*ES* '
N01  c     Path1_5       oreq      '*ES*G'
N02  c     Path1_5       oreq      '*ES*C'
     c                   eval      WsMapp  = Path6_50
     c                   move      Path51_60     WSOMR
     c                   move      Path61_63     WSTIM
     c                   move      Path64_65     WSMIN
     c                   move      Path66_69     WSUNTY
     c                   end
      * låner MiniDS for å finne "Today"
     C                   time                    Min
     c                   move      MinYear       MiniYear
     c                   move      MinMonth      MiniMonth
     c                   move      MinDay        MiniDay
     c                   move      MinHour       MiniHour
     c                   move      MinMin        MiniMin
     c                   move      MinSec        MiniSec
     c                   eval      WSFIL = 'Biler_'+TodayA+'_'+TodayHMSA
     C*
     C     PATH          IFEQ      *BLANKS
     c                   open      SYTK11FM
     C     STBILD        TAG
     C                   EXFMT     BILDE01
     C* Fraktbrev 999 = CHG bekreftet håndtert
     C     *in12         cabeq     *ON           Slutt
     c                   exsr      TestBi
     c     *IN30         cabeq     '1'           STBILD
     c                   close     SYTK11FM
     C                   else
     c                   exsr      TestBi
     C                   end
      * Lag fil:
     C                   Exsr      LagFil
     c                   eval      Path = %trim(WSMAPP)+
     c                                    %trim(WSFIL)+'_' +
     c                                    %trim(WSOMR)+'.csv'
      *
     C     Slutt         Tag
     C                   SETON                                        LR
     C                   RETURN
     C*
      ***************************************************************************
     C     TESTBI        BEGSR
      ***************************************************************************
     C                   move      *off          *IN30
     C                   ENDSR
      ***************************************************************************
     C     Lagfil        BEGSR
      ***************************************************************************
     C     WsTim         ifne      *zeros
     C     WsMin         Orne      *zeros
     C                   time                    CurrTime
     C     CurrTime      subdur    WSTim:*h      Min
     C     Min           subdur    WSMin:*mn     Min
      * eks: CURRTIME = '2006-08-22-18.23.52.354000'   minus 2 t. 10 min ->
      *           MIN = '2006-08-22-16.13.52.354000'
      * Flytt til 14 langt numesrisk felt for enkel sammenligning.
     c                   move      MinYear       MiniYear
     c                   move      MinMonth      MiniMonth
     c                   move      MinDay        MiniDay
     c                   move      MinHour       MiniHour
     c                   move      MinMin        MiniMin
     c                   move      MinSec        MiniSec
     C                   end
      * 1.post - kolonneheadere...
     C                   move      'J'           Post1
      *
     C     StartLes      tag
     C     Key           setgt     TKGPS
     C                   ReadP     TKGPS                                  50
     c     *in50         Cabeq     '1'           SluttLes
      * Post funnet,
      *  test om den er for gammel...
     C     WsTim         ifne      *zeros
     C     WsMin         Orne      *zeros
     C     TKDtTi        cablt     MiniDtTi      Skipp
     C                   else
      * timer/minutter ikke angit - må være dagens dato..
     C     TKDT          cablt     Today         Skipp
     C                   end
      *  test om den er i rette område
     C     WsOmr         IFNE      *blanks
     C                   exsr      TstOmr
     C     FeilOmr       cabeq     'J'           Skipp
     C                   end
      *  test unittype (og hent evt bilnr alltid..)
     C                   exsr      TstUnty
     C     FeilUnty      cabeq     'J'           Skipp
      ***
N01  c     Path1_5       ifne      '*ES*G'
N02  c     Path1_5       andne     '*ES*C'
     C                   exsr      EnPost
N01  c                   else
N01  C                   exsr      EnPostG
N01  c                   end
      ***
     C     Skipp         tag
     C                   move      TKBRID        XXBRID
     C                   move      TKBRID        SJNR
     c                   sub       1             SJNR
     C                   move      SJNR          XXBRID
     c     SJNR          cable     0             Sluttles
     C                   goto      StartLes
     C     SluttLes      tag
      *
     C     wsomr         ifeq      *blank
     c                   movel     'ALL'         WSOMR
     c                   end
N01  c     Path1_5       ifeq      '*ES*G'
N02  c     Path1_5       oreq      '*ES*C'
S02  C*    Post1         andeq     'N'                                          minst en skrevet
N02  C     Post1         ifeq      'N'                                          minst en skrevet
N01  C                   eval      Streng = ']}'
N01  C                   EXCEPT    Qdata
N02  C                   end
N01  C                   end
      *
     C                   ENDSR
      ***************************************************************************
     C     TstOmr        BEGSR
      ***************************************************************************
     C                   Move      'J'           FeilOmr
      *
     C     WsOmr         setll     TKAREA
     C     WsOmr         READE     TKAREA                                 35
     C     *IN35         DOWEQ     '0'
      * Funnet område definert av Vestre/Nordre/Ørstre/Søndre linje i et rektangel
      * (breddegrader = nord/sør - lengdegrader=Øst-vest)
      * ER bilen innenfor dette området?
     C     TKLEN         ifge      OMLENV
     C     TKBRE         andle     OMBREN
     C     TKLEN         andle     OMLENØ
     C     TKBRE         andge     OMBRES
     C                   Move      'N'           FeilOmr
     C                   goto      UtTstOmr
     C                   end
     C     WsOmr         READE     TKAREA                                 35
     C                   END
     C     UtTstOmr      ENDSR
      ***************************************************************************
     C     TstUnty       BEGSR
      ***************************************************************************
     C                   move      TKBRID        SJÅFNR            8
     C                   eval      UNRETU = 'PDA:'+SJÅFNR
     c                   move      *blanks       UNBILN
     C     UNRETU        chain     UnitL04                            33
     C     WsUnTy        IFNE      *blanks
     C                   Move      'J'           FeilUnty
     C  N33UNUNTY        COMP      WSUNTY                             3333
     C  N33              Move      'N'           FeilUnty
     C                   end
     C                   ENDSR
      ***************************************************************************
     C     EnPost        BEGSR
      ***************************************************************************
     C                   move      TKBRID        SJNR
      * Name,Address,Latitude,Longitude,Annet
      * DA12345(Navn) dette er en bli med..,Her er adr,59.90403,10.74397,annen info
      * HZ98765(Navn) her er en annen,Adresse,60.81711,11.16040,annen info her også
     C     Post1         Ifeq      'J'
     C                   eval      Streng = 'Name,Name 2,'+
     C                                      'Latitude,Longitude,'+
     C                                      'Province'
     C                   EXCEPT    Qdata
     C                   move      'N'           Post1
     C                   end
      *
     c     Path1_5       Ifeq      '*ES* '
     c                   move      TKDT          TKDTA             8
     C                   eval      Info1 ='##IP##'+'/sycgip/estk21.pgm'+
     C                                    '?user=NN&SJ='+
     c                                    %trim(%editc(SJNR:'Z'))+
     C                                    '&DT='+TKDTA
     c                   else
     C                   eval      Info1 ='http://10.13.1.22/sycgip/syfra.pgm'+
     C                                    '?user=NN'
     c                   end
     c                   move      '+'           BreFor            1
     c                   move      '+'           LenFor            1
     c     TKLEN         iflt      0
     c                   mult      -1            TKLEN
     c                   move      '-'           LenFor
     c                   end
     c     TKBRE         iflt      0
     c                   mult      -1            TKBRE
     c                   move      '-'           BreFor
     c                   end
     C                   MOVE      TKLEN         TKLEN_GD
     C                   MOVE      TKBRE         TKBRE_GD
     C                   eval      Info1 =%trim(info1)+
     c                             '&L='+LenFor+TKLEN_G+'.'+TKLEN_D +
     c                             '&B='+BreFor+TKBRE_G+'.'+TKBRE_D
     C                   eval      Streng = %trim(UNBILN) + ' ' +
     C                             %trim(TKBRID) + ',' +
     C                             %trim(%editw(TKTI:'  :  :  ')) + ',' +
     c                             BreFor+TKBRE_G + '.' + TKBRE_D + ',' +
     c                             LenFor+TKLEN_G + '.' + TKLEN_D + ',' +
     C                             %trim(Info1)
     C                   EXCEPT    Qdata
     C                   ENDSR
N01
N01   ***************************************************************************
N01  C     EnPostG       BEGSR
N01   ***************************************************************************
N02  c                   add       1             postID            5 0
N01  c                   move      *blanks       Streng
N01  C     Post1         Ifeq      'J'
N01  C                   eval      Streng = '{"points": [ '
N01  C                   move      'N'           Post1
N01  C                   else
N01  c                   movel     ','           Streng                         komma mellom hver
N01  C                   end
N01   *
N01   *
N01  C                   move      TKBRID        SJNR
N01  c                   move      TKDT          TKDTA             8
N01
N01  c                   move      '+'           BreFor            1
N01  c                   move      '+'           LenFor            1
N01  c     TKLEN         iflt      0
N01  c                   mult      -1            TKLEN
N01  c                   move      '-'           LenFor
N01  c                   end
N01  c     TKBRE         iflt      0
N01  c                   mult      -1            TKBRE
N01  c                   move      '-'           BreFor
N01  c                   end
N01  C                   MOVE      TKLEN         TKLEN_GD
N01  C                   MOVE      TKBRE         TKBRE_GD
N01   /free
N01    // Ap=Hexavedi for single Appostrof for å kunne bruke inne i tekstkonst.(mellom '       ')
N01
N01    // Definer url i hjelpevariabel som skal inn i Infobobla
N01     SeTur  = '<a href='+Ap+'#'+Ap+' OnClick=\"window.open('+Ap
N01     +'../../sycgip/estk21.pgm?user=NN&SJ='
N01     +%trim(%editc(SJNR:'Z'))+'&DT='+TKDTA
N01    //+Ap+','+Ap+'GMwind'+Ap+','+Ap+'width=1245,height=800,left=15,top=30,' ****MED WIND-NAME??
N01     +Ap+','+Ap+Ap+','+Ap+'width=1245,height=800,left=15,top=30,'
N01     +'scrollbars,resizable'+Ap+')\">Se tur(er)</A>';
N01
N01     //Definer ETT punkt / en boble
N01     //Midlertidig ta høyde for lokal kjøring
N02     If  (Path1_5 =  '*ES*G');
N01     Streng = %trim(Streng)+'{'
N01     +' "timestamp": "' + %trim(%editw(TKTI:'  :  :  ')) + '",'
N01     +' "pos":{"lat": "'+BreFor+TKBRE_G + '.' + TKBRE_D +'", '
N01             +'"lng": "'+LenFor+TKLEN_G + '.' + TKLEN_D +'"},'
N01     +' "icon": {"url":"images/truck.png", '
                   +'"offset": { "X":"3", "Y":"31"},'
                   +'"labeloffset": { "X":"2", "Y":"-22"},'
                   +'"title": "Title linje 1 \n'
                   +'Title linje 2", '
N01                +'"size":{"width":"64", "height":"32"}},'
N01     +' "unit": "'+%trim(UNBILN)+'", '
N01     +' "info":"'
N01     +'<h1>'+%trim(UNBILN)+'</h1>'
N01     +'SjåførID: '+ %trim(TKBRID)+'<br>'
N01     +'Tid: '+   %trim(%editw(TKDT:'    .  .  '))  +' '
N01     + %trim(%editw(TKTI:'  :  :  ')) +'<br>'
N01     //+%trim(SeTur)
N01     //+'"}';
N02      +%trim(SeTur) +'" ,'
N02      +' "unit": "'+%trim(UNBILN)+'", '
N02      +' "type":"car", '
N02      +' "group":""'
N02      +'}';
N01     //+'"}';
N02     else;
N02      Streng = %trim(Streng)+'{'
N02      +' "id": "' + %trim(%editc(PostID:'Z')) + '",'
N02      +' "timestamp": "' + %trim(%editw(TKTI:'  :  :  ')) + '",'
N02      +' "pos": {"lat": "'+BreFor+TKBRE_G + '.' + TKBRE_D +'", '
N02              +'"lng": "'+LenFor+TKLEN_G + '.' + TKLEN_D +'"},'
N02      +' "address": {"address": "" ,"contrycode":""},'
N02      +' "icon": {"url":"images/truck.png", '
N02                 +'"size": {"width":"64", "height":"32"}, '
N02                 +'"title": "Title linje 1 \n'+'Title linje 2", '
N02                 +'"offset": { "X":"3", "Y":"31"}},'
N02      +' "label": {"text":"'+%trim(UNBILN)+'", '
N02                 +'"offset":{"X":"2","Y":"-22"}},'
N02      +' "info":"'
N02      +'<h1>'+%trim(UNBILN)+'</h1>'
N02      +'SjåførID: '+ %trim(TKBRID)+'<br>'
N02      +'Tid: '+   %trim(%editw(TKDT:'    .  .  '))  +' '
N02      + %trim(%editw(TKTI:'  :  :  ')) +'<br>'
N02      +%trim(SeTur) +'" ,'
N02      +' "unit": "'+%trim(UNBILN)+'", '
N02      +' "type":"car", '
N02      +' "group":""'
N02      +'}';
N02     endif;
N02   /end-free
N02  C                   EXCEPT    Qdata
N02  C                   ENDSR
     OQ1024     EADD         Qdata
     O                       STRENG            1024
