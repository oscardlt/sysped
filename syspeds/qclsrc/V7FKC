             PGM
             DCL        VAR(&FRALIB) TYPE(*CHAR) LEN(10) /* +
                          Driftbibliotek */
             DCL        VAR(&TILLIB) TYPE(*CHAR) LEN(10) /* +
                          Bibliotek for oppbevaring av filer fra V7. */
             DCL        VAR(&HIST) TYPE(*CHAR) LEN(1) /* om +
                          konvertering av historikk bibliotek */
             DCL        VAR(&SAD) TYPE(*CHAR) LEN(1) /* Om +
                          konvertering av SAD */
             DCL        VAR(&TOMME) TYPE(*CHAR) LEN(1) /* Om tømming +
                          av gamle filer etter konvertering. */
             DCL        VAR(&JOBQ) TYPE(*CHAR) LEN(1) /* Om benytt +
                          av jobbkø. */
             DCL        VAR(&FLAGG) TYPE(*CHAR) LEN(1)
             DCL        VAR(&FB) TYPE(*CHAR) LEN(1) /* Om +
                          forbehandling (oppbygging av nye/endrede +
                          logiske filer). */
             DCLF       FILE(V7FKD00) RCDFMT(BILDE01) /* Hente +
                          driftbibliotek og info. for konvertering. */
             CHGJOB     JOB(*) LOG(4 00 *SECLVL) LOGCLPGM(*YES)
             OVRDBF     FILE(V7FNF) TOFILE(V7SYSF/V7FNF)
             OVRDBF     FILE(V7DSPDBR) TOFILE(V7SYSF/V7DSPDBR)

LOOP1:
             SNDRCVF    DEV(*FILE) RCDFMT(*FILE)
             IF         COND(&IN03 = '1') THEN(GOTO CMDLBL(SLUTT))
             CHGVAR     VAR(&IN30) VALUE('0')
             CHGVAR     VAR(&IN31) VALUE('0')

             CHKOBJ     OBJ(QSYS/&FRALIB) OBJTYPE(*LIB) /* Test om +
                          inntastet driftbibliotek finnes fra før. */
             MONMSG     MSGID(CPF9801 CPF9810) EXEC(CHGVAR +
                          VAR(&IN30) VALUE('1'))

             IF         COND(&IN30 = '1' *OR &IN31 = '1') THEN(GOTO +
                          CMDLBL(LOOP1))

             IF         COND(&IN10 = '1') THEN(CHGVAR VAR(&JOBQ) +
                          VALUE('J'))

             IF         COND(&HIST = 'J') THEN(DO)
             CHGCURLIB  CURLIB(V7SYSO)
             ADDLIBLE   LIB(&FRALIB)
             CHGDTAARA  DTAARA(V7SYSF/V7FKDTA (1 10)) VALUE(&FRALIB)
             CHGDTAARA  DTAARA(V7SYSF/V7FKDTA (1 4)) VALUE('V7O.')
             ENDDO
             ELSE       CMD(DO)
             CHGCURLIB  CURLIB(V7SYSF)
             CHGDTAARA  DTAARA(V7SYSF/V7FKDTA (1 10)) VALUE(&FRALIB)
             CHGDTAARA  DTAARA(V7SYSF/V7FKDTA (1 4)) VALUE('V7F.')
             CPYF       FROMFILE(V7SYSF/TVINE) TOFILE(&FRALIB/TVINE) +
                          MBROPT(*ADD)
             ENDDO
             RTVDTAARA  DTAARA(V7SYSF/V7FKDTA (1 10)) RTNVAR(&TILLIB)
             CHKOBJ     OBJ(QSYS/&TILLIB) OBJTYPE(*LIB)
             MONMSG     MSGID(CPF9801 CPF9810) EXEC(DO)
             CALL       PGM(V7FKRTOM)
             CRTLIB     LIB(&TILLIB) TEXT('Datafiler fra V7')
             CHGDTAARA  DTAARA(V7SYSF/V7FKDTA (11 1)) VALUE('N')
             ENDDO

             CALL       PGM(V7FKR) PARM(&HIST &SAD &FLAGG)
             IF         COND(&FLAGG = '1') THEN(GOTO CMDLBL(SLUTT))

             RTVDTAARA  DTAARA(V7SYSF/V7FKDTA (11 1)) RTNVAR(&FB)
             IF         COND(&FB = 'N') THEN(DO)
             /* BYGG NYE/ENDREDE LOGISKEFILER SOM FYSISKEFILER IKKE */
             /* ER ENDRET.                                          */
             IF         COND(&HIST *EQ 'J') THEN(CALL PGM(V7FKC91A) +
                          PARM(&FRALIB))
             ELSE       CMD(DO)
             CALL       PGM(V7FKC91) PARM(&FRALIB)
             CALL       PGM(V7FKC92) PARM(&FRALIB)
             ENDDO
             CHGDTAARA  DTAARA(V7SYSF/V7FKDTA (11 1)) VALUE('J')
             ENDDO

             IF         COND(&JOBQ = 'J') THEN(DO)
             ADDLIBLE   LIB(V7SYSF)
             MONMSG     MSGID(CPF0000)
             SBMJOB     CMD(CALL PGM(V7FKR2) PARM(&HIST &SAD &FRALIB +
                          &TILLIB &TOMME &JOBQ))
             ENDDO
             ELSE       CMD(DO)
             CHGDSPF    FILE(V7FKD2) WAITRCD(*IMMED)
             CALL       PGM(V7FKR2) PARM(&HIST &SAD &FRALIB &TILLIB +
                          &TOMME &JOBQ)
             ENDDO

             IF         COND(&HIST = 'J') THEN(RMVLIBLE LIB(&FRALIB))

 SLUTT:      RCLRSC
             CHGMSGQ    MSGQ(SYSPED/PRINTERAUT) DLVRY(*DFT)
             CHGCURLIB  CURLIB(*CRTDFT)
             ENDPGM
