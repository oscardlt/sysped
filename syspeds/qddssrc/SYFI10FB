     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPDOC('FLI00' 'HELPNU' 'HLP400')
     A                                      HELP
     A                                      ALTHELP
     A          R BILDE01
     A                                      CF03(03 'Avslutt')
     A                                      CF04(04 'Søke selger')
     A                                      CF05(05 'Videre')
     A N31                                  CF16(16 'Neste posisjon')
     A                                      CF07(07 'Tildele AWBnummer')
     A                                      CF09(09 'Neste flyfraktbrev')
     A                                      CF12(12 'Avbryt')
     A                                      CF18(18 'Adverseringer')
     A                                      CF20(20 'Dir. booking')
     A                                      CF21(21 'Booking')
     A                                      CF22(22 'Se på logg')
     A                                      CF24(24 'Flere taster')
     A                                      CF23(23 'Send Scedule Request')
     A                                      CSRLOC(LINNBR     POSNBR)
     A          H                           HLPDOC('FLI01' 'HELPNU' 'HLP400')
     A                                      HLPARA(01 001 03 080)
     A          H                           HLPDOC('FLI02' 'HELPNU' 'HLP400')
     A                                      HLPARA(04 001 04 080)
     A          H                           HLPDOC('FLI03' 'HELPNU' 'HLP400')
     A                                      HLPARA(06 001 06 080)
     A          H                           HLPDOC('FLI04' 'HELPNU' 'HLP400')
     A                                      HLPARA(08 001 24 080)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A            MSGLN1         7A  P
     A            MSGLN2         7A  P
     A            MSGLN3         7A  P
     A            MSGLN4         7A  P
     A            MSGLN5         7A  P
     A            MSGLN6         7A  P
     A            MSGLN7         7A  P
     A                                  1  2MSGCON(030 DIV1070 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 34MSGCON(003 DIV1071 *LIBL/SYMSGF)
     A            WSFBNR         3Y 0O  1 38TEXT('Fraktbrevnummer')
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  1 42MSGCON(011 DIV1072 *LIBL/SYMSGF)
     A            UOPD           7Y 0O  1 54TEXT('Oppdragsnummer')
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  1 62MSGCON(005 DIV1073 *LIBL/SYMSGF)
     A            WSAWB         11S 0O  1 68TEXT('HAWB-NR')
     A                                      COLOR(WHT)
     A                                  2  2MSGCON(007 DIV1074 *LIBL/SYMSGF)
     A            WSST           1A  B  2 10TEXT('Status')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  2 18MSGCON(008 BBA0044 *LIBL/SYMSGF)
     A            WSCMN     R     A  B  2 27REFFLD(DOKUIR/DICMN *LIBL/DOKUI)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  2 29MSGCON(005 BBA0002 *LIBL/SYMSGF)
     A                                  2 42MSGCON(010 DIV0465 *LIBL/SYMSGF)
     A            WSSG           3A  B  2 54COLOR(YLW)
     A                                  3  2MSGCON(007 DIV1075 *LIBL/SYMSGF)
     A            WSFRA          5A  B  3 10TEXT('FRA-DESTINASJON')
     A                                      DSPATR(UL)
     A  99                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                  3 17MSGCON(003 DIV1076 *LIBL/SYMSGF)
     A            WSTIL          5A  B  3 21TEXT('TIL-DESTINASJON')
     A                                      DSPATR(UL)
     A  98                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                  3 27MSGCON(007 DIV1077 *LIBL/SYMSGF)
     A            WSRUNR         6A  B  3 35TEXT('RUNR')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSDAG1         2Y 0B  3 42TEXT('DAG1')
     A                                      DSPATR(UL)
     A                                      EDTCDE(9)
     A                                      COLOR(YLW)
     A                                  3 45'/'
     A            WSRUN2         6A  B  3 47TEXT('RUN2')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSDAG2         2Y 0B  3 54TEXT('DAG2')
     A                                      DSPATR(UL)
     A                                      EDTCDE(9)
     A                                      COLOR(YLW)
     A                                  3 57'/'
     A            WSRUN3         6A  B  3 59TEXT('RUN3')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSDAG3         2Y 0B  3 66TEXT('DAG3')
     A                                      DSPATR(UL)
     A                                      EDTCDE(9)
     A                                      COLOR(YLW)
     A                                  3 69MSGCON(005 DIV1078 *LIBL/SYMSGF)
     A            WSCCPP         2A  B  3 75TEXT('CCPP')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  4  2MSGCON(007 DIV1079 *LIBL/SYMSGF)
     A            WSPREF         3Y 0B  4 10TEXT('PREF')
     A                                      DSPATR(UL)
     A  97                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                      COLOR(YLW)
     A            WSFNAV        30A  O  4 15TEXT('FNAV')
     A                                      COLOR(WHT)
     A                                  4 47MSGCON(005 BBA0043 *LIBL/SYMSGF)
     A            DIXXV1         3A  B  4 53TEXT('Via sted nummer 1')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  4 59MSGCON(005 BBA0043 *LIBL/SYMSGF)
     A            DIXXV2         3A  B  4 65TEXT('Via sted nummer 2')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  5  2MSGCON(007 DIV1080 *LIBL/SYMSGF)
     A            WSSKNR         8Y 0B  5 10TEXT('SKNR')
     A                                      DSPATR(UL)
     A  96                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                      COLOR(YLW)
     A            WSSNAV        30A  B  5 19TEXT('SNAV')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSSAD1        30A  B  5 50TEXT('SAD1')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSSAD2        30A  B  6 19TEXT('SAD2')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSSAD3        30A  B  6 50TEXT('SAD3')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  7 10MSGCON(008 DIV1081 *LIBL/SYMSGF)
     A            WSSTLF        15A  B  7 19TEXT('STLF')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  8  2MSGCON(007 DIV1082 *LIBL/SYMSGF)
     A            WSKNAV        30A  B  8 19TEXT('KNAV')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSKAD1        30A  B  8 50TEXT('KAD1')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSKAD2        30A  B  9 19TEXT('KAD2')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSKAD3        30A  B  9 50TEXT('KAD3')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 10 10MSGCON(008 DIV1081 *LIBL/SYMSGF)
     A            WSKTLF        15A  B 10 19TEXT('KTLF')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 12  2MSGCON(078 DIV1083 *LIBL/SYMSGF)
     A            WSANT          7Y 0B 13  2TEXT('ANT')
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(YLW)
     A            WSVEKT         5Y 0B 13 10TEXT('VEKT')
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(YLW)
     A            WSSLAG        20A  B 13 16TEXT('SLAG')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSVSL2        20A  B 13 38TEXT('VSL2')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSFVKT         5Y 0B 13 61TEXT('FVKT')
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(YLW)
     A            WSTARI         1A  B 13 69TEXT('TARI')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSPRIS         5Y 2B 13 73TEXT('PRIS')
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(YLW)
     A                                 14  2MSGCON(007 TRO0504 *LIBL/SYMSGF)
     A            WSFT          41A  B 14 18TEXT('MERK')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 14 61'beløp'
     A            WSFUEL         9Y 2B 14 68COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 15  2MSGCON(007 DIV1084 *LIBL/SYMSGF)
     A            WSMERK        50A  B 15 18TEXT('MERK')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 16  2MSGCON(007 DIV1085 *LIBL/SYMSGF)
     A            WSFORS         9Y 0B 16 18TEXT('FORS')
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(YLW)
     A                                 16 30MSGCON(007 DIV1086 *LIBL/SYMSGF)
     A            WSFORA        25A  B 16 38TEXT('FORA')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 17  2MSGCON(007 DIV1087 *LIBL/SYMSGF)
     A            WSFRAK         7Y 0B 17 18TEXT('FRAK')
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(YLW)
     A                                 17 35MSGCON(045 DIV1088 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            WSTXUT        15A  B 18  2COLOR(YLW)
     A                                      CHECK(LC)
     A            WSFS           7Y 0B 18 18TEXT('FS')
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(YLW)
     A                                 18 35MSGCON(045 DIV1090 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                 19  2MSGCON(007 DIV1091 *LIBL/SYMSGF)
     A            WSFPRE         7Y 0B 19 18TEXT('FPRE')
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(YLW)
     A                                 20  2MSGCON(007 DIV1092 *LIBL/SYMSGF)
     A            WSVGEB         7Y 0O 20 10TEXT('VGEB')
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A                                 20 19MSGCON(006 DIV1093 *LIBL/SYMSGF)
     A            WSMVA          9Y 2O 20 26TEXT('MVA')
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 20 39MSGCON(010 DIV1094 *LIBL/SYMSGF)
     A            WSMLAP         1A  B 20 50TEXT('MLAP')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'J' 'P' 'B' 'S')
     A            MSGL1         18A  O 21  2MSGID(&MSGLN1 SYMSGF)
     A                                      COLOR(BLU)
     A            MSGL2         18A  O 21 22MSGID(&MSGLN2 SYMSGF)
     A                                      COLOR(BLU)
     A            MSGL3         18A  O 21 42MSGID(&MSGLN3 SYMSGF)
     A                                      COLOR(BLU)
     A            MSGL4         18A  O 21 62MSGID(&MSGLN4 SYMSGF)
     A                                      COLOR(BLU)
     A            MSGL5         18A  O 22  2MSGID(&MSGLN5 SYMSGF)
     A                                      COLOR(BLU)
     A            MSGL6         18A  O 22 22MSGID(&MSGLN6 SYMSGF)
     A                                      COLOR(BLU)
     A            MSGL7         18A  O 22 42MSGID(&MSGLN7 SYMSGF)
     A                                      COLOR(BLU)
     A                                 22 62MSGCON(018 DIV0029 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A            UMSG          62A  O 23  2COLOR(WHT)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
