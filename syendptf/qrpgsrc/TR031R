********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
11XXX * 01 YBC PTF11 FJERN TEST PÅ TET00004
12321 * 02 CB  PTF12 Ikke oppdater hvis brukt beløp er endret i bakgrunnen
********************************************************************
      *
      ***********************************************************
      *** PROGRAM SKAL ARBEIDE MED GARANTI                    ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01 02 05-09 11 13-26
      *** LEDIGE INDIKATORER 29 31 32 34-85
      ***********************************************************
      *
     FTRUGH     UF A E           K DISK
     FTRUGHL01  IF   E           K DISK
     F                                     RENAME(TRUGHR:TRUGHR01)
     FTRUGD     IF A E           K DISK
     FTRUGLG    IF A E           K DISK
     FTRKODL01  IF   E           K DISK
     FCUNDL01   IF   E           K DISK
     FSYPTFF    IF   E           K DISK
     FKODTS2    IF   E           K DISK
     FKODTVAL2  IF   E           K DISK
     FTR031D    CF   E             WORKSTN
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               382    383B 0
     D  WSSUBN               378    379B 0
     D                 DS
     D  WSDTD                  1      2  0
     D  WSDTM                  3      4  0
     D  WSDTÅ                  5      6  0
     D  WSDT                   1      6  0
     D                 DS
     D  TGDTÅ                  1      4  0
     D  TGDTM                  5      6  0
     D  TGDTD                  7      8  0
     D  TGDT                   1      8  0
     D                 DS
     D  WSDTRD                 1      2  0
     D  WSDTRM                 3      4  0
     D  WSDTRÅ                 5      6  0
     D  WSDTR                  1      6  0
     D                 DS
     D  TGDTRÅ                 1      4  0
     D  TGDTRM                 5      6  0
     D  TGDTRD                 7      8  0
     D  TGDTR                  1      8  0
      *
     D                 DS
     D  SYRG                   1     14
     D  SYFOR9                 1      9
     D  SYFORE                 1     11
     D                SDS
     D  UUSR                 254    263
     D                UDS
     D  UFIRM                  1      2
      *
     C                   EXSR      INIT
      *
     C     START         TAG
      *
     C                   EXFMT     TR031D01
     C                   MOVEA     '000000000000'*IN(88)
     C                   MOVEA     '00'          *IN(87)
     C                   MOVE      '0'           *IN30
      *
      *  04 = SØK HOVEDANSVARLIG
     C   04              DO
     C                   EXSR      SØKDT
     C                   GOTO      START
     C                   END
      *
      *  10 = SE OPPLYSNIGER OM GODKJENNELSE FRA TOLLVESENET (KUN KOMMUNIKASJON)
     C   10              DO
     C                   EXSR      VISTOL
     C                   GOTO      START
     C                   END
      *
     C   12              GOTO      SLUTT
      *
     C                   IF        UTYPE = 'DLT'
     C     WSGNR         CHAIN     TRUGH                              33
     C                   IF        TGST <> 'S'
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVE      TGGNR         TLGNR
     C                   MOVE      TGRN          TLRN
     C                   MOVE      UUSR          TLUSR
     C                   MOVE      WDT           TLDT
     C                   MOVE      WTM           TLTM
     C                   MOVE      TGST          TLFST
     C                   MOVE      TGGTY         TLFGTY
     C                   MOVE      TGGVK         TLFVK
     C                   MOVE      TGGBL         TLFBL
     C                   MOVE      TGGBLB        TLFBLB
     C                   MOVE      TGAKNY        TLFKNY
     C                   MOVE      TGAKGM        TLFKGM
     C                   MOVE      TGPRM         TLFPRM
     C                   MOVE      TGGFV         TLFFV
     C                   MOVE      'S'           TLEST
     C                   MOVE      TGGTY         TLEGTY
     C                   MOVE      TGGVK         TLEVK
     C                   MOVE      TGGBL         TLEBL
     C                   MOVE      TGGBLB        TLEBLB
     C                   MOVE      TGAKNY        TLEKNY
     C                   MOVE      TGAKGM        TLEKGM
     C                   MOVE      TGPRM         TLEPRM
     C                   MOVE      TGGFV         TLEFV
     C                   WRITE     TRUGLGR
     C                   END
     C                   MOVE      'S'           TGST
     C                   UPDATE    TRUGHR
     C                   GOTO      SLUTT
     C                   END
      *
     C                   IF        UTYPE <> 'DSP'
     C                   EXSR      TESTB
     C   30              GOTO      START
      *
      * ----- INGEN FEIL HITTIL     OPPDATER TRUGH -----
     C                   EXSR      OPDTRH
     C   03              GOTO      SLUTT
     C                   END
      *
      *
     C                   EXSR      TOTRV
     C   12              GOTO      START
      *
     C                   IF        UTYPE <> 'DSP'
     C                   CALL      'TR033R'
     C                   PARM                    TGGNR
     C                   END
      *
     C     SLUTT         TAG
     C                   MOVE      '1'           *INLR
     C                   RETURN
      *
     C***********************************************
     C     SØKDT         BEGSR
     C***********************************************
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
      * HOVEDANSVARLIGNR
     C     LINNBR        IFEQ      3
     C     POSNBR        ANDGE     20
     C     POSNBR        ANDLE     27
     C                   MOVE      *BLANKS       USØKKN           30
     C                   CALL      'SOEKKUN'
     C                   PARM                    WSKNA
     C                   PARM                    UFIRM
     C                   PARM                    USØKKN
     C                   Z-ADD     4             LINNBR
     C                   Z-ADD     20            POSNBR
     C                   GOTO      SLSØK
     C                   END
      * LANDKODE TIL HOVEDANSVARLIG
     C     LINNBR        IFEQ      8
     C     POSNBR        ANDGE     20
     C     POSNBR        ANDLE     21
     C                   MOVE      'N'           USPED             1
     C                   CALL      'SYFI10L'
     C                   PARM                    WSLKA
     C                   PARM                    USPED
     C                   Z-ADD     9             LINNBR
     C                   Z-ADD     20            POSNBR
     C                   GOTO      SLSØK
     C                   END
      * GARANTITOLLSTED
     C     LINNBR        IFEQ      9
     C     POSNBR        ANDGE     20
     C     POSNBR        ANDLE     27
     C                   MOVE      '106'         UUNIK             3
     C                   MOVE      *BLANKS       UKODE            10
     C                   MOVEL     WSTSD         UKODE
     C                   MOVE      'AVG'         UTYPE2            3
     C                   CALL      'TR002R'
     C                   PARM                    UUNIK
     C                   PARM                    UKODE
     C                   PARM                    UTYPE2
     C                   MOVEL     UKODE         WSTSD
     C                   Z-ADD     10            LINNBR
     C                   Z-ADD     20            POSNBR
     C                   GOTO      SLSØK
     C                   END
      *
     C     LINNBR        IFEQ      12
     C     POSNBR        ANDGE     40
     C     POSNBR        ANDLE     42
     C                   CALL      'SAD061F'
     C                   PARM                    WSGVK
     C                   PARM                    UDT
     C                   Z-ADD     13            LINNBR
     C                   Z-ADD     20            POSNBR
     C                   GOTO      SLSØK
     C                   END
      *
     C                   MOVE      'DIV1037'     MSGNR
     C                   SETON                                        30
      *
     C     SLSØK         ENDSR
      *
     C***********************************************
     C     TESTB         BEGSR
     C***********************************************
      *
     C     WSKNA         IFNE      0                                            1<-B
     C     CUNDL01K      CHAIN     CUNDL01                            99
     C     *IN99         IFEQ      '1'                                           2<-B
     C                   MOVE      'SPK0001'     MSGNR
     C                   Z-ADD     3             LINNBR
     C                   Z-ADD     20            POSNBR
     C                   SETON                                        30
     C                   ELSE                                                    2
     C                   MOVE      KNAVN         WSNAA
     C                   MOVE      ADR1          WSADA1
     C     SYPOGE        IFEQ      *BLANKS                                        3<-B
     C                   MOVE      *BLANKS       WSPNA
     C                   MOVEL     POSTNR        WSPNA
     C                   ELSE                                                     3
     C                   MOVEL     SYPOGE        WSPNA
     C                   END                                                      3<-E
     C                   MOVE      ADR3          WSPSA
     C     SYLAND        IFNE      *BLANKS                                        3<-B
     C                   MOVE      SYLAND        WSLKA
     C                   ELSE                                                     3
     C                   MOVE      'NO'          WSLKA
     C                   END                                                      3<-E
     C                   IF        WSTINA = *BLANKS                               3<-B
     C                   SELECT                                                    4<-B
S01  C*                  WHEN      PTFDT > UDT                                     4
S01  C*                  IF        ((SYFOR9 <> *BLANKS) AND (SYFOR9 <> *ZEROS))     5<-B
S01  C*                  EVAL      WSTINA = SYFOR9
S01  C*                  END                                                        5<-E
     C                   WHEN      EORI = *BLANKS                                  4
     C                   IF        ((SYFOR9 <> *BLANKS) AND (SYFOR9 <> *ZEROS))     5<-B
     C                   EVAL      WSTINA = WSLKA + SYFORE
     C                   END                                                        5<-E
     C                   OTHER                                                     4
     C                   EVAL      WSTINA = EORI
     C                   ENDSL                                                     4<-E
     C                   END                                                      3<-E
     C                   END                                                     2<-E
     C                   END                                                    1<-E
      *
     C     WSTINA        COMP      *BLANKS                                98
     C  N30              IF        *IN98
     C                   MOVE      'YBC0812'     MSGNR
     C                   Z-ADD     4             LINNBR
     C                   Z-ADD     20            POSNBR
     C                   SETON                                        30
     C                   END
      *
     C     WSNAA         COMP      *BLANKS                                97
     C  N30              IF        *IN97
     C                   MOVE      'YBC1889'     MSGNR
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     20            POSNBR
     C                   SETON                                        30
     C                   END
      *
     C     WSPNA         COMP      *BLANKS                                96
     C  N30              IF        *IN96
     C                   MOVE      'YBC1890'     MSGNR
     C                   Z-ADD     7             LINNBR
     C                   Z-ADD     20            POSNBR
     C                   SETON                                        30
     C                   END
      *
     C     WSPSA         COMP      *BLANKS                                95
     C  N30              IF        *IN95
     C                   MOVE      'YBC1891'     MSGNR
     C                   Z-ADD     7             LINNBR
     C                   Z-ADD     30            POSNBR
     C                   SETON                                        30
     C                   END
      *
     C                   IF        WSLKA = *BLANKS
     C                   MOVE      'NO'          WSLKA
     C                   ELSE
     C     KODTS2K       CHAIN     KODTS2                             94
     C  N30              IF        *IN94
     C                   MOVE      'SPL0001'     MSGNR
     C                   Z-ADD     8             LINNBR
     C                   Z-ADD     20            POSNBR
     C                   SETON                                        30
     C                   END
     C                   END
      *
     C                   MOVEL     WSTSD         TKKODE
     C     TRK01K        CHAIN     TRKODL01                           93
     C  N30              IF        *IN93
     C                   MOVE      'YBC1768'     MSGNR
     C                   Z-ADD     9             LINNBR
     C                   Z-ADD     20            POSNBR
     C                   SETON                                        30
     C                   END
      *
     C                   IF        *IN27 = *OFF                                 1<-B
     C     WSGNR         COMP      *BLANKS                                92
     C                   IF        *IN92 = *OFF                                  2<-B
     C     WSGNR         CHAIN(N)  TRUGH                              33
     C                   IF        *IN33 = *OFF                                   3<-B
     C                   SETON                                        92
     C  N30              IF        *IN92                                           4<-B
     C                   MOVE      'YBC1892'     MSGNR
     C                   Z-ADD     10            LINNBR
     C                   Z-ADD     20            POSNBR
     C                   SETON                                        30
     C                   END                                                       4<-E
     C                   END                                                      3<-E
     C                   ELSE                                                    2
     C  N30              IF        *IN92                                          3<-B
     C                   MOVE      'YBC1891'     MSGNR
     C                   Z-ADD     10            LINNBR
     C                   Z-ADD     20            POSNBR
     C                   SETON                                        30
     C                   END                                                      3<-E
     C                   END                                                     2<-E
     C                   END                                                    1<-E
      *
     C     WSAKNY        COMP      *BLANKS                                91
     C  N30              IF        *IN91
     C                   MOVE      'YBC1893'     MSGNR
     C                   Z-ADD     11            LINNBR
     C                   Z-ADD     20            POSNBR
     C                   SETON                                        30
     C                   END
      *
     C     WSAKGM        COMP      *BLANKS                                90
     C  N30              IF        *IN90
     C                   MOVE      'YBC1895'     MSGNR
     C                   Z-ADD     11            LINNBR
     C                   Z-ADD     48            POSNBR
     C                   SETON                                        30
     C                   END
      *
     C     WSGBL         COMP      0                                      89
     C  N30              IF        *IN89
     C                   MOVE      'YBC1896'     MSGNR
     C                   Z-ADD     12            LINNBR
     C                   Z-ADD     20            POSNBR
     C                   SETON                                        30
     C                   END
      *
     C     WSGVK         CHAIN     KODTVAL2                           88
     C  N30              IF        *IN88
     C                   MOVE      'SPV0001'     MSGNR
     C                   Z-ADD     12            LINNBR
     C                   Z-ADD     40            POSNBR
     C                   SETON                                        30
     C                   END
      *
     C     WSGBL         COMP      WSGBLB                               87
     C  N30              IF        *IN88
     C                   MOVE      'YBC1897'     MSGNR
     C                   Z-ADD     13            LINNBR
     C                   Z-ADD     20            POSNBR
     C                   SETON                                        30
     C                   END
N02   *
N02  C                   IF        UTYPE='CHG'
N02  C     WSGNR         CHAIN(N)  TRUGH
N02  C                   IF        %FOUND(TRUGH)
N02  C                   EVAL      YTGGBLB=TGGBLB
N02  C                   IF        YTGGBLB <> XTGGBLB
N02  C                   MOVE      'CBA0413'     MSGNR
N02  C                   Z-ADD     13            LINNBR
N02  C                   Z-ADD     20            POSNBR
N02  C                   SETON                                        30
N02  C                   ENDIF
N02  C                   ENDIF
N02  C                   ENDIF
      *
     C     WSPRM         COMP      100                                86  86
     C  N30              IF        *IN86
     C                   MOVE      'YBC1898'     MSGNR
     C                   Z-ADD     14            LINNBR
     C                   Z-ADD     20            POSNBR
     C                   SETON                                        30
     C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     MOVW2         BEGSR
     C***********************************************
     C                   IF        UTYPE <> 'CRT'
     C                   MOVE      TGGNR         WSGNR
     C                   MOVE      TGGTY         WSGTY
     C                   MOVE      TGGVK         WSGVK
     C                   MOVE      TGGBL         WSGBL
     C                   MOVE      TGGBLB        WSGBLB
     C                   MOVE      TGKNA         WSKNA
     C                   MOVE      TGTINA        WSTINA
     C                   MOVE      TGNAA         WSNAA
     C                   MOVE      TGADA1        WSADA1
     C                   MOVE      TGPNA         WSPNA
     C                   MOVE      TGPSA         WSPSA
     C                   MOVE      TGLKA         WSLKA
     C                   MOVE      TGTSD         WSTSD
     C                   MOVE      TGAKNY        WSAKNY
     C                   MOVE      TGAKGM        WSAKGM
     C                   MOVE      TGPRM         WSPRM
     C                   MOVE      TGUSR         WSUSR
     C                   MOVE      TGDTD         WSDTD
     C                   MOVE      TGDTM         WSDTM
     C                   MOVE      TGDTÅ         WSDTÅ
     C                   MOVE      TGDTRD        WSDTRD
     C                   MOVE      TGDTRM        WSDTRM
     C                   MOVE      TGDTRÅ        WSDTRÅ
     C                   MOVE      TGGFV         WSGFV
     C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     MOVTRH        BEGSR
     C***********************************************
     C                   MOVE      WSGNR         TGGNR
     C                   MOVE      WSGTY         TGGTY
     C                   MOVE      WSGVK         TGGVK
     C                   MOVE      WSGBL         TGGBL
     C                   MOVE      WSGBLB        TGGBLB
     C                   MOVE      WSKNA         TGKNA
     C                   MOVE      WSTINA        TGTINA
     C                   MOVE      WSNAA         TGNAA
     C                   MOVE      WSADA1        TGADA1
     C                   MOVE      WSPNA         TGPNA
     C                   MOVE      WSPSA         TGPSA
     C                   MOVE      WSLKA         TGLKA
     C                   MOVE      WSTSD         TGTSD
     C                   MOVE      WSAKNY        TGAKNY
     C                   MOVE      WSAKGM        TGAKGM
     C                   MOVE      WSPRM         TGPRM
     C                   MOVE      WSUSR         TGUSR
     C                   MOVE      WSGFV         TGGFV
     C                   MOVE      WDT           TGDT
      *
     C                   ENDSR
      *
     C***********************************************
     C     OPDTRH        BEGSR
     C***********************************************
     C                   IF        *IN27 = *OFF
     C     XMINRN        SETLL     TRUGHL01
     C                   READ      TRUGHL01                               33
     C                   IF        *IN33
     C                   Z-SUB     1             XGRN              9 0
     C                   ELSE
     C                   EVAL      XGRN = TGRN - 1
     C                   END
     C                   END
      *
     C     WSGNR         CHAIN     TRUGH                              33
N02  C     *LIKE         DEFINE    TGGBLB        YTGGBLB
N02  C  N33              EVAL      YTGGBLB=TGGBLB
      *
     C                   MOVE      'N'           XLOGG             1
     C                   SELECT
     C                   WHEN      *IN33
     C                   CLEAR                   TRUGLGR
     C                   MOVE      WSGNR         TLGNR
     C                   MOVE      XGRN          TLRN
     C                   MOVE      'J'           XLOGG
     C     WSGTY         WHENNE    TGGTY
     C     WSGVK         ORNE      TGGVK
     C     WSGBL         ORNE      TGGBL
     C     WSGBLB        ORNE      TGGBLB
     C     WSAKNY        ORNE      TGAKNY
     C     WSAKGM        ORNE      TGAKGM
     C     WSPRM         ORNE      TGPRM
     C     WSGFV         ORNE      TGGFV
     C                   MOVE      TGGNR         TLGNR
     C                   MOVE      TGRN          TLRN
     C                   MOVE      TGST          TLFST
     C                   MOVE      TGGTY         TLFGTY
     C                   MOVE      TGGVK         TLFVK
     C                   MOVE      TGGBL         TLFBL
     C                   MOVE      TGGBLB        TLFBLB
     C                   MOVE      TGAKNY        TLFKNY
     C                   MOVE      TGAKGM        TLFKGM
     C                   MOVE      TGPRM         TLFPRM
     C                   MOVE      TGGFV         TLFFV
     C                   MOVE      TGST          TLEST
     C                   MOVE      WSGTY         TLEGTY
     C                   MOVE      WSGVK         TLEVK
     C                   MOVE      WSGBL         TLEBL
     C                   MOVE      WSGBLB        TLEBLB
     C                   MOVE      WSAKNY        TLEKNY
     C                   MOVE      WSAKGM        TLEKGM
     C                   MOVE      WSPRM         TLEPRM
     C                   MOVE      WSGFV         TLEFV
     C                   MOVE      'J'           XLOGG
     C                   ENDSL
     C                   IF        XLOGG = 'J'
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVE      UUSR          TLUSR
     C                   MOVE      WDT           TLDT
     C                   MOVE      WTM           TLTM
     C                   WRITE     TRUGLGR
     C                   END
      *
     C                   EXSR      MOVTRH
      *
     C                   IF        *IN33
     C                   MOVE      ' '           TGST
     C                   MOVE      XGRN          TGRN
     C                   MOVE      WDT           TGDTR
     C                   MOVE      TGDTRD        WSDTRD
     C                   MOVE      TGDTRM        WSDTRM
     C                   MOVE      TGDTRÅ        WSDTRÅ
     C                   WRITE     TRUGHR
     C                   MOVE      TGGNR         UGNR
     C  N27              IF        UTYPE = 'CPY'
     C                   EXSR      CPYTRV
     C                   END
     C                   MOVE      *ON           *IN27
     C                   ELSE
N02  C  N33              IF        YTGGBLB=XTGGBLB
     C                   UPDATE    TRUGHR
N02  C                   ENDIF
     C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     CPYTRV        BEGSR
     C***********************************************
     C     XGNRG         SETLL     TRUGD
     C     XGNRG         READE(N)  TRUGD                                  33
     C                   DOW       *IN33 = *OFF
     C                   MOVE      TGGNR         TDGNR
     C                   WRITE     TRUGDR
     C     XGNRG         READE(N)  TRUGD                                  33
     C  N33              ENDDO
      *
     C                   ENDSR
      *
     C***********************************************
     C     TOTRV         BEGSR
     C***********************************************
      *
     C                   CALL      'TR032R'
     C                   PARM                    TGGNR
     C                   PARM                    TGAKNY
     C                   PARM                    UTYPE
     C                   PARM                    UFLAGG            1 0
      *
     C     UFLAGG        COMP      1                                      12
     C     UFLAGG        COMP      3                                      03
      *
     C                   ENDSR
      *
     C***********************************************
     C     VISTOL        BEGSR
     C***********************************************
      *________________________
      * Call på eksternt CL-pgm
      *""""""""""""""""""""""""
     C                   MOVEL     TGRN          UAVD              4 0
     C                   MOVE      TGRN          UTDN              7 0
     C                   MOVE      '2'           WIN03             1
     C                   CALL      'SAD003'
     C                   PARM                    UAVD
     C                   PARM                    UTDN
     C                   PARM                    WIN03
      *
     C                   ENDSR
      *
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     CUNDL01K      KLIST
     C                   KFLD                    UFIRM
     C                   KFLD                    WSKNA
     C     TRK01K        KLIST
     C                   KFLD                    TKUNIK
     C                   KFLD                    TKKODE
     C                   MOVE      '106'         TKUNIK
     C     KODTS2K       KLIST
     C                   KFLD                    KS2UNI
     C                   KFLD                    WSLKA
     C                   MOVE      'S2 '         KS2UNI
      *
     C     *ENTRY        PLIST
     C                   PARM                    UGNR             24
     C                   PARM                    UTYPE             3
     C                   PARM                    UFLAGG            1 0
      *
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVE      WDT           UDT               8 0
S01  C*    'TET00004'    CHAIN     SYPTFF                             33
     C     UTYPE         COMP      'CHG'                                  27
     C     UTYPE         COMP      'DSP'                                  28
     C  N28UTYPE         COMP      'DLT'                                  28
     C                   IF        UTYPE = 'CRT'
     C                   MOVE      *ZEROS        WSDT
     C                   MOVE      *ZEROS        WSDTR
     C                   ELSE
     C     UGNR          CHAIN(N)  TRUGH                              33
N02  C     *LIKE         DEFINE    TGGBLB        XTGGBLB
N02  C  N33              EVAL      XTGGBLB=TGGBLB
     C                   EXSR      MOVW2
     C                   IF        UTYPE = 'CPY'
     C                   MOVE      UGNR          XGNRG            24
     C                   MOVE      *BLANKS       UGNR
     C                   MOVE      *BLANKS       WSGNR
     C                   MOVE      *ZEROS        WSGBL
     C                   MOVE      *ZEROS        WSGBLB
     C                   MOVE      *ZEROS        WSDT
     C                   MOVE      *ZEROS        WSDTR
     C                   Z-ADD     10            LINNBR
     C                   Z-ADD     20            POSNBR
     C                   END
     C                   END
     C                   MOVE      UUSR          WSUSR
     C                   Z-SUB     99999999999   XMINRN           11 0
      *
     C                   WRITE     WINDOWSZ
      *
     C                   ENDSR
      *
