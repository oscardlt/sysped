     FLLAGJL01  UF A E           K DISK
     FLLTELL    UF   E             DISK
     FLLLSDF    IF   E           K DISK
     FLL067D    CF   E             WORKSTN
     F                                     INFDS(FBAREA)
     F                                     SFILE(SUBFIL:ZRECNO)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D                SDS
     D  UUSER                254    263
      *
     C                   EXSR      INIT
      *
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
      *
     C                   Z-ADD     ZRECNO        YRECNO            4 0
     C                   MOVE      '1'           *IN94
     C                   WRITE     NYWRK
     C                   MOVE      '1'           *IN06
     C                   GOTO      START5
      *
     C     START3        TAG
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     2             POSNBR
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYWRK
     C                   ELSE
     C                   WRITE     SUBCTL
     C                   EXFMT     BUNN
     C                   END
      *
     C                   SETOFF                                       9830
      *
     C   12              MOVE      '1'           UFLAGG
     C   12              GOTO      SLUTT
     C   23              IF        XANTR = 0
     C                   EXSR      OPPDAT
     C                   GOTO      SLUTT
     C                   END
      *
      *** LAG NY
     C     START5        TAG
     C     *IN06         IFEQ      '1'
     C     XANTR         CABLE     0             START3
     C                   MOVEA     '100'         *IN(50)
     C                   EXSR      POST
     C                   GOTO      START3
     C                   END
      *
     C     ZRECNO        IFGT      0
     C                   EXSR      HENT
     C                   END
     C                   GOTO      START3
      *
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
      *
     C***********************************************
     C     HENT          BEGSR
     C***********************************************
     C                   SETOFF                                       9998
     C                   MOVEA     '00'          *IN(91)
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C  N98WSNR          IFEQ      '2'
     C     WSLOK2        ANDNE     '*DELETE'
     C                   MOVEA     '010'         *IN(50)
     C                   EXSR      POST
     C                   MOVE      *BLANKS       WSNR
     C                   UPDATE    SUBFIL
     C                   END
     C  N98WSNR          IFEQ      '4'
     C     WSLOK2        ANDNE     '*DELETE'
     C                   MOVEA     '001'         *IN(50)
     C                   EXSR      POST
     C                   MOVE      *BLANKS       WSNR
     C                   UPDATE    SUBFIL
     C                   END
     C                   END
      *
     C     SLHENT        TAG
     C   98
     CANN99              DO
     C                   MOVE      'DIV0012'     MSGNR
     C                   MOVE      '1'           *IN30
     C                   END
     C                   MOVE      '0'           *IN99
     C                   ENDSR
      *
     C***********************************************
     C     POST          BEGSR
     C***********************************************
      * IN50= NY POST,  IN51= ENDRE  52= SLETT (WSNR='4')
      *   99= MINST EN OPTION ER UTFØRT
     C                   MOVE      '1'           *IN99
      *
     C     *IN50         IFEQ      '1'
     C                   MOVE      *ZEROS        W2ANT
     C                   MOVE      XANTR         XANTR2            7 0
     C                   MOVEL     ULOK          W2LOK1
     C                   IF        YRECNO = 0
     C                   MOVE      ULOK          W2LOK2
     C                   ELSE
     C                   MOVE      *BLANKS       W2LOK2
     C                   END
     C                   ELSE
     C   51              EVAL      XANTR2 = XANTR + WSANT
     C                   MOVE      WSANT         W2ANT
     C                   MOVE      WSLOK1        W2LOK1
     C                   MOVE      WSLOK2        W2LOK2
     C                   END
      *
     C                   Z-ADD     14            LINNBR
     C                   Z-ADD     2             POSNBR
      *
     C     BILTAG        TAG
     C                   EXFMT     BILDE
     C                   MOVEA     '000'         *IN(88)
     C                   MOVE      '0'           *IN30
      *
     C   12              GOTO      ENDPOS
      * SLETT
     C                   IF        *IN52
     C                   SUB       W2ANT         XANT
     C                   ADD       W2ANT         XANTR
     C                   MOVE      *ZEROS        WSANT
     C                   MOVE      *BLANKS       WSLOK1
     C                   EVAL      WSLOK2 = '*DELETE'
     C                   GOTO      ENDPOS
     C                   ELSE
      * LAGE ELLER ENDRE
     C                   EXSR      TESTB
     C   30              GOTO      BILTAG
      *
     C                   MOVE      W2LOK1        WSLOK1
     C                   MOVE      W2LOK2        WSLOK2
     C                   IF        *IN50
     C                   MOVE      W2ANT         WSANT
     C                   SUB       W2ANT         XANTR
     C                   ADD       W2ANT         XANT
     C                   ADD       1             YRECNO
     C                   MOVE      YRECNO        ZRECNO
     C                   WRITE     SUBFIL
     C                   ELSE
     C                   SUB       WSANT         XANT
     C                   ADD       WSANT         XANTR
     C                   MOVE      W2ANT         WSANT
     C                   SUB       W2ANT         XANTR
     C                   ADD       W2ANT         XANT
     C                   END
      *
     C                   END
      *
     C     ENDPOS        TAG
     C                   ENDSR
      *
     C***********************************************
     C     TESTB         BEGSR
     C***********************************************
     C                   IF        ((W2ANT <= 0) OR (W2ANT > XANTR2))
     C                   MOVE      '1'           *IN90
     C                   MOVE      '1'           *IN30
     C                   MOVE      'YBC1916'     MSGNR
     C                   Z-ADD     14            LINNBR
     C                   Z-ADD     2             POSNBR
     C                   GOTO      SLTB
     C                   END
      *
     C                   IF        W2LOK2 = *BLANKS
     C                   MOVE      '1'           *IN89
     C                   MOVE      '1'           *IN30
     C                   MOVE      'YBC0674'     MSGNR
     C                   Z-ADD     14            LINNBR
     C                   Z-ADD     10            POSNBR
     C                   GOTO      SLTB
     C                   END
      *
     C     W2LOK1        CHAIN     LLLSDF                             88
     C                   IF        *IN88
     C                   MOVE      '1'           *IN30
     C                   MOVE      'YBC0674'     MSGNR
     C                   Z-ADD     14            LINNBR
     C                   Z-ADD     31            POSNBR
     C                   GOTO      SLTB
     C                   END
      *
     C     SLTB          ENDSR
      *
     C***********************************************
     C     OPPDAT        BEGSR
     C***********************************************
     C     LAGJ1K        CHAIN     LLAGJL01                           33
     C                   Z-ADD     1             ZRECNO
     C     ZRECNO        CHAIN     SUBFIL                             33
     C                   MOVE      WSANT         VAOANT
     C                   MOVE      WSANT         VASAE0
     C                   MOVE      WSANT         VASALD
     C                   EVAL      VAVKTS = WSANT * VAVKT0
     C                   EVAL      VAKUBS = WSANT * VAKUB0
     C                   MOVE      WSLOK2        VALLOK
     C                   MOVEL     WSLOK1        VALLOK
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVE      UUSER         VABUSE
     C                   MOVE      WDT           VABDAT
     C                   MOVE      WTM           VABKL
     C                   UPDATE    LLAGJR
      *
     C     2             DO        YRECNO        ZRECNO
     C     ZRECNO        CHAIN     SUBFIL                             33
     C     3             CHAIN     LLTELL                             33
     C                   ADD       1             LLREC1
     C                   UPDATE    LLTELLR
     C                   MOVE      LLREC1        VAMN
     C                   MOVE      WSANT         VAOANT
     C                   MOVE      WSANT         VASAE0
     C                   MOVE      WSANT         VASALD
     C                   EVAL      VAVKTS = WSANT * VAVKT0
     C                   EVAL      VAKUBS = WSANT * VAKUB0
     C                   MOVE      WSLOK2        VALLOK
     C                   MOVEL     WSLOK1        VALLOK
     C                   WRITE     LLAGJR
     C                   ENDDO
      *
     C                   ENDSR
      *
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     *ENTRY        PLIST
     C                   PARM                    UGRUP             8 0
     C                   PARM                    UKJED             8 0
     C                   PARM                    UMN               7 0
     C                   PARM                    UANT              7 0
     C                   PARM                    ULOK             25
     C                   PARM                    UFLAGG            1
      *
     C     LAGJ1K        KLIST
     C                   KFLD                    UGRUP
     C                   KFLD                    UKJED
     C                   KFLD                    UMN
      *
     C                   MOVE      UANT          XANTR             7 0
     C                   MOVE      *ZEROS        XANT
     C                   MOVE      ' '           UFLAGG
     C                   WRITE     WINDOWSZ
      *
     C                   ENDSR
      *
