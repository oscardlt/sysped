********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 SH  PTF10 ny dangerous goods logikk
10XXX * 02 YBC PTF10 SJEKK VEKT PÅ ORDRE OG BOX
10XXX * 03 YBC PTF10 SKRIV ORDRETYPE PÅ MERKELAPP
10XXX * 04 YBC PTF10 NYTT PARAMETER VED KALL PÅ GSS034C
10XXX * 05 SH  PTF10 godtar fly oppdrag og ikke bare kode ''AIR''
10217 * 06 YBC PTF10 FLERE DEFAULT PRINTER
12217 * 07 SH  PTF12 vise messsurement på tur
********************************************************************
      *
      **************************************************************
      *** PROGRAM SKAL ARBEIDE MED TUR
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER
      *** LEDIGE INDIKATORER 01 02 04 07-22 24
      *** LEDIGE INDIKATORER 27-29 31 32 34-40 48-89 95-97
      **************************************************************
      *
N05  FHEADF     IF   E           K DISK
     FGSSTRF    IF   E           K DISK
     FGSSTRL01  IF   E           K DISK
     F                                     RENAME(GSSTRFR:GSSTRF01)
     FGSSTRL02  IF   E           K DISK
     F                                     RENAME(GSSTRFR:GSSTRF02)
     FGSSTRL03  IF   E           K DISK
     F                                     RENAME(GSSTRFR:GSSTRF03)
     FGSSTRL04  IF   E           K DISK
     F                                     RENAME(GSSTRFR:GSSTRF04)
     FGSSTRL05  IF   E           K DISK
     F                                     RENAME(GSSTRFR:GSSTRF05)
     FGSSTRL06  IF   E           K DISK
     F                                     RENAME(GSSTRFR:GSSTRF06)
     FGSSBIF    IF   E           K DISK
     FGSSCGF    IF   E           K DISK
     FGSSORL22  IF   E           K DISK
     FGSSPOF    IF   E           K DISK
     FCDBOAT    IF   E           K DISK
     FCUNDL01   IF   E           K DISK
     FFIRM      IF   E           K DISK
     FGSS020D   CF   E             WORKSTN
     F                                     SFILE(GSS020DA:ZRECNO)
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D                 DS
     D  TRAD1                  1     30
     D  XTRAD1                 1     28
     D                 DS
EV5  D  TRDTLÅ                 1      4  0
EV5  D  TRDTLM                 5      6  0
EV5  D  TRDTLD                 7      8  0
EV5  D  TRDTL                  1      8  0
     D                 DS
     D  WXDTLD                 1      2  0
     D  WXDTLM                 3      4  0
     D  WXDTLÅ                 5      6  0
     D  WXDTL                  1      6  0
     D                 DS
EV5  D  TRDTSÅ                 1      4  0
EV5  D  TRDTSM                 5      6  0
EV5  D  TRDTSD                 7      8  0
EV5  D  TRDTS                  1      8  0
     D                 DS
     D  WXDTSD                 1      2  0
     D  WXDTSM                 3      4  0
     D  WXDTSÅ                 5      6  0
     D  WXDTS                  1      6  0
     D                 DS
     D  ORDTLÅ                 1      4  0
     D  ORDTLM                 5      6  0
     D  ORDTLD                 7      8  0
     D  ORDTL                  1      8  0
     D                 DS
     D  UPDTLD                 1      2
     D  UPDTLM                 3      4
     D  UPDTLÅ                 5      6
     D  UPDTL                  1      6
     D                 DS
     D  UPO                    1   1000    DIM(50)
     D  UPCON1                 1     20
     D  UPCON2                21     40
     D  UPCON3                41     60
     D  UPCON4                61     80
     D  UPCON5                81    100
     D  UPCON6               101    120
     D                SDS
     D  UUSER                254    263
     D SAVUDS         UDS
     D  UFIRM                  1      2
     D  UCURR                  3      5
     D  UBIAVD                15     18  0
     D  UBIUSI               101    110
     D  UBICGR               111    112
     D  UBICNO               113    120  0
     D  UBIFC                121    123
     D  UBIPRT               124    133
     D  UBIPRM               134    143
     D  UBILOC               144    148
N06  D  UBIPRP               149    158
N06  D  UBIPRI               159    168
      *
     C                   EXSR      INIT
     C   33              GOTO      SLUTT
      *
     C     START         TAG
     C                   MOVE      '1'           *IN(93)
     C                   WRITE     GSS020DB
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
     C                   MOVE      *BLANKS       WSNR
     C                   MOVE      *ZEROS        XTNO             11 0
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C                   EXSR      SETKEY
     C     START1        TAG
     C                   EXSR      LESREC
      *
     C     START2        TAG
      *
     C  N94              DO
     C                   DO        15
     C                   ADD       1             ZRECNO
     C                   MOVE      TRDTLD        WXDTLD
     C                   MOVE      TRDTLM        WXDTLM
     C                   MOVE      TRDTLÅ        WXDTLÅ
     C                   MOVE      TRDTSD        WXDTSD
     C                   MOVE      TRDTSM        WXDTSM
     C                   MOVE      TRDTSÅ        WXDTSÅ
     C                   WRITE     GSS020DA
     C                   MOVE      ZRECNO        XRECNO            5 0
     C                   EXSR      LESREC
     C  N94              END
     C                   END
      *
     C     START3        TAG
     C  N30              Z-ADD     5             LINNBR
     C  N30              Z-ADD     2             POSNBR
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     GSS020DG
     C                   ELSE
     C                   WRITE     GSS020DC
     C                   EXFMT     GSS020DB
     C                   END
      *
      *** AVSLUTT
     C                   MOVE      '0'           *IN30
     C                   MOVEA     '00'          *IN(98)
      *
     C   03              GOTO      SLUTT
     C   04              DO
     C                   EXSR      SØKDTA
     C     W2BCD         IFEQ      *BLANKS
     C     W2CNO         ANDEQ     0
     C                   GOTO      START3
     C                   END
     C                   END
     C   05              GOTO      START
     C     START4        TAG
     C   06              DO
     C                   EXSR      CRTTR
     C                   GOTO      START3
     C                   END
      *
      *** BLA VIDERE
     C   25              DO
     C                   Z-ADD     XRECNO        ZRECNO
     C  N94XTNO          CHAIN     GSSTRF                             25
     C                   SETOFF                                       25
     C                   GOTO      START2
     C                   END
      *
     C     W2NR          IFNE      *BLANKS                                      1<-B
     C     W2TNO         ORNE      0
     C     W2BCD         ORNE      *BLANKS
     C     W2CNO         ORNE      0
     C     W2NR          IFEQ      *BLANKS                                       2<-B
     C                   EXSR      NYSØK
     C                   GOTO      START
     C                   ELSE                                                    2
     C     W2NR          COMP      '1'                                    06
     C   06              DO                                                       3<-B
     C                   MOVE      *BLANKS       W2NR
     C                   GOTO      START4
     C                   END                                                      3<-E
     C     W2TNO         IFEQ      0                                              3<-B
     C                   SETON                                        9830
     C                   MOVE      'GSS0047'     MSGNR
     C                   ELSE                                                     3
     C                   EXSR      NYOPT
     C                   END                                                      3<-E
     C                   MOVE      *ZEROS        W2TNO
     C                   MOVE      *BLANKS       W2BCD
     C                   MOVE      *ZEROS        W2CNO
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     2             POSNBR
     C                   GOTO      START3
     C                   END                                                     2<-E
     C                   END                                                    1<-E
      *
     C     ZRECNO        IFNE      0
     C                   EXSR      HHENT
     C                   END
     C                   GOTO      START3
      *
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
      *
     C***********************************************
     C     TESTB         BEGSR
     C***********************************************
     C     YTNO          COMP      0                                  4141
     C   41              GOTO      SLTB2
     C     YBCD          COMP      *BLANKS                            4242
     C   42              GOTO      SLTB2
     C     YCNO          COMP      0                                  4343
     C   43              GOTO      SLTB2
      *
     C     SLTB2         TAG
     C   41UBICNO        IFNE      0
     C                   MOVE      '0'           *IN41
     C                   MOVE      '1'           *IN44
     C                   ELSE
     C     UBICGR        IFNE      *BLANKS
     C                   MOVE      '0'           *IN41
     C                   MOVE      '1'           *IN46
     C                   END
     C                   END
     C   42UBICNO        IFNE      0
     C                   MOVE      '0'           *IN42
     C                   MOVE      '1'           *IN45
     C                   ELSE
     C     UBICGR        IFNE      *BLANKS
     C                   MOVE      '0'           *IN42
     C                   MOVE      '1'           *IN47
     C                   END
     C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     NYSØK         BEGSR
     C***********************************************
     C                   MOVE      *ZEROS        YTNO
     C                   MOVE      *BLANKS       YBCD
     C                   MOVE      *ZEROS        YCNO
     C     W2TNO         IFNE      0
     C                   MOVE      W2TNO         YTNO
     C                   END
     C     W2BCD         IFNE      *BLANKS
     C                   MOVE      W2BCD         YBCD
     C                   END
     C     W2CNO         IFNE      YCNO
     C     UBICNO        IFNE      0
     C                   MOVE      UBICNO        W2CNO
     C                   END
     C                   MOVE      W2CNO         YCNO
     C                   END
      *
     C                   MOVE      *ZEROS        W2TNO
     C                   MOVE      *BLANKS       W2BCD
     C                   MOVE      *ZEROS        W2CNO
      *
     C                   MOVEA     '0000000'     *IN(41)
     C                   EXSR      TESTB
      *
     C                   ENDSR
      *
     C***********************************************
     C     NYOPT         BEGSR
     C***********************************************
     C     W2TNO         CHAIN     GSSTRF                             98
     C  N98TRST          IFNE      ' '
     C     TRST          COMP      '1'                                9898
     C                   END
     C   98              MOVE      'GSS0048'     MSGNR
     C   98              GOTO      SLOPT
     C     UBICGR        IFNE      *BLANKS                                      1<-B
     C     TRCNO         CHAIN     GSSCGF                             98
     C  N98CGCG          COMP      UBICGR                             9898
     C   98              MOVE      'GSS0029'     MSGNR
     C   98              GOTO      SLOPT
     C                   END                                                    1<-E
     C     UBICNO        IFNE      0                                            1<-B
     C     UBICNO        ANDNE     TRCNO
     C                   MOVE      '1'           *IN98
     C                   MOVE      'GSS0030'     MSGNR
     C                   GOTO      SLOPT
     C                   END                                                    1<-E
     C                   MOVE      '1'           *IN90
      *
     C                   SELECT
     C     W2NR          WHENEQ    '2'
     C     TRST          IFNE      ' '
     C     TRST          ANDNE     '1'
     C                   MOVE      '1'           *IN98
     C                   MOVE      'GSS0049'     MSGNR
     C                   GOTO      SLOPT
     C                   END
     C                   EXSR      CHGTR
     C     W2NR          WHENEQ    '5'
     C                   EXSR      DSPTR
     C     W2NR          WHENEQ    '6'
     C                   EXSR      PRTTR
     C     W2NR          WHENEQ    '9'
     C     TRST          ANDEQ     '1'
     C                   EXSR      CFMTR
     C     W2NR          WHENEQ    'P'
     C                   EXSR      PLKTR
N01  C     W2NR          WHENEQ    'D'
N01  C                   EXSR      DGSR
     C     W2NR          WHENEQ    'A'
     C                   EXSR      arkivsr
     C     W2NR          WHENEQ    'I'
     C                   EXSR      BOXINF
     C     W2NR          WHENEQ    'L'
     C                   EXSR      PRTML
     C     W2NR          WHENEQ    'C'
     C                   EXSR      BERFAK
N07  C     W2NR          WHENEQ    'M'
N07  C                   EXSR      MESSURE
     C                   OTHER
     C                   MOVE      'GSS0009'     MSGNR
     C                   MOVE      '1'           *IN98
     C                   GOTO      SLOPT
     C                   ENDSL
      *
     C     SLOPT         TAG
     C   98              MOVE      '1'           *IN30
     C                   MOVE      '0'           *IN90
     C                   MOVE      *BLANKS       W2NR
      *
     C                   ENDSR
      *
     C***********************************************
     C     HHENT         BEGSR
     C***********************************************
     C                   MOVE      *BLANKS       MSGNR
     C                   MOVEA     '000'         *IN(90)
     C                   MOVE      '0'           *IN99
     C     *IN98         DOWEQ     '0'
     C                   READC     GSS020DA                               98
     C   98              DO
     C   99              MOVE      '0'           *IN98
     C                   GOTO      SLHENT
     C                   END
      *
     C     ZRECNO        DIV       15            LINNBR
     C                   MVR                     XLIN              1 0
     C     XLIN          IFEQ      0
     C                   Z-ADD     15            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
      *
     c     trtno         chain     gsstrf                             33
     C                   SELECT
     C     WSNR          WHENEQ    '2'
     C     TRST          IFNE      *BLANKS
     C     TRST          ANDNE     '1'
     C                   MOVE      '1'           *IN98
     C                   MOVE      'GSS0049'     MSGNR
     C                   MOVE      *BLANKS       WSNR
     C                   UPDATE    GSS020DA
     C                   GOTO      SLHENT
     C                   END
     C                   EXSR      CHGTR
     C     WSNR          WHENEQ    '5'
     C                   EXSR      DSPTR
     C     WSNR          WHENEQ    '6'
     C                   EXSR      PRTTR
     C     WSNR          WHENEQ    '9'
     C     TRST          ANDEQ     '1'
     C                   EXSR      CFMTR
     C     WSNR          WHENEQ    'P'
     C                   EXSR      PLKTR
N01  C     WSNR          WHENEQ    'D'
N01  C                   EXSR      DGSR
     C     WSNR          WHENEQ    'A'
     C                   EXSR      arkivsr
     C     WSNR          WHENEQ    'I'
     C                   EXSR      BOXINF
     C     WSNR          WHENEQ    'L'
     C                   EXSR      PRTML
     C     WSNR          WHENEQ    'C'
     C                   EXSR      BERFAK
N07  C     WSNR          WHENEQ    'M'
N07  C                   EXSR      MESSURE
     C                   OTHER
     C                   MOVE      '1'           *IN98
     C                   MOVE      *BLANKS       WSNR
     C                   UPDATE    GSS020DA
     C                   GOTO      SLHENT
     C                   ENDSL
     C                   MOVE      '1'           *IN99
      *
     C                   END
      *
     C     SLHENT        TAG
     C                   MOVE      '0'           *IN99
     C   98MSGNR         IFEQ      *BLANKS
     C                   MOVE      'GSS0009'     MSGNR
     C                   END
     C   98              MOVE      '1'           *IN30
     C  N30              Z-ADD     5             LINNBR
     C   30              ADD       5             LINNBR
     C                   Z-ADD     2             POSNBR
      *
     C                   ENDSR
      *
     C***********************************************
     C     SETKEY        BEGSR
     C***********************************************
     C   41YTNO          IFEQ      99999999999
     C     GTRK01        SETGT     GSSTRL01
     C                   ELSE
     C     GTRK01        SETLL     GSSTRL01
     C                   END
     C   42GTRK02        SETGT     GSSTRL02
     C   43GTRK03        SETGT     GSSTRL03
     C   44YTNO          IFEQ      99999999999
     C     GTRK04        SETGT     GSSTRL03
     C                   ELSE
     C     GTRK04        SETLL     GSSTRL03
     C                   END
     C   45GTRK05        SETGT     GSSTRL04
     C   46YTNO          IFEQ      99999999999
     C     GTRK06        SETGT     GSSTRL05
     C                   ELSE
     C     GTRK06        SETLL     GSSTRL05
     C                   END
     C   47GTRK07        SETGT     GSSTRL06
      *
     C                   ENDSR
      *
     C***********************************************
     C     LESREC        BEGSR
     C***********************************************
     C     STLF          TAG
     C   41YTNO          IFEQ      99999999999
     C                   READP     GSSTRL01                               94
     C                   ELSE
     C                   READ      GSSTRL01                               94
     C                   END
     C   42YBCD          READPE    GSSTRL02                               94
     C   43YCNO          READPE    GSSTRL03                               94
     C   44YTNO          IFEQ      99999999999
     C     UBICNO        READPE    GSSTRL03                               94
     C                   ELSE
     C     UBICNO        READE     GSSTRL03                               94
     C                   END
     C   45GTRX05        READPE    GSSTRL04                               94
     C   46YTNO          IFEQ      99999999999
     C     UBICGR        READPE    GSSTRL05                               94
     C                   ELSE
     C     UBICGR        READE     GSSTRL05                               94
     C                   END
     C   47GTRX07        READPE    GSSTRL06                               94
     C  N94              MOVE      TRTNO         XTNO
      *
     C                   ENDSR
      *
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
N05  C     HEADK         KLIST
N05  C                   KFLD                    heavd
N05  C                   KFLD                    heopd
     C     GTRK01        KLIST
     C                   KFLD                    YTNO
     C     *LIKE         DEFINE    TRTNO         YTNO
     C     GTRK02        KLIST
     C                   KFLD                    YBCD
     C                   KFLD                    YTNO2
     C     *LIKE         DEFINE    TRBCD         YBCD
     C     *LIKE         DEFINE    TRTNO         YTNO2
     C                   Z-ADD     99999999999   YTNO2
     C     GTRK03        KLIST
     C                   KFLD                    YCNO
     C                   KFLD                    YTNO2
     C     *LIKE         DEFINE    TRCNO         YCNO
     C     GTRK04        KLIST
     C                   KFLD                    UBICNO
     C                   KFLD                    YTNO
     C     GTRK05        KLIST
     C                   KFLD                    UBICNO
     C                   KFLD                    YBCD
     C                   KFLD                    YTNO2
     C     GTRX05        KLIST
     C                   KFLD                    UBICNO
     C                   KFLD                    YBCD
     C     GTRK06        KLIST
     C                   KFLD                    UBICGR
     C                   KFLD                    YTNO
     C     GTRK07        KLIST
     C                   KFLD                    UBICGR
     C                   KFLD                    YBCD
     C                   KFLD                    YTNO2
     C     GTRX07        KLIST
     C                   KFLD                    UBICGR
     C                   KFLD                    YBCD
     C     CUNDL01K      KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    ORCNO
      *
     C                   MOVE      '1'           UMODE             1
     C                   MOVE      '1'           UMOD              1
     C                   MOVE      'N'           WSFAX
      *
     C     UBIUSI        CHAIN     GSSBIF                             33
     C     *IN33         IFEQ      '0'
     C     FIFIRM        SETLL     FIRM
     C                   READ      FIRM                                   33
     C                   Z-ADD     1             WSCPY
     C                   MOVE      *ZEROS        WXDTL
     C                   MOVE      *ZEROS        WXDTS
     C                   MOVE      *ZEROS        TRDTL
     C                   MOVE      *ZEROS        TRDTS
     C     *DTAARA       DEFINE    *LDA          SAVUDS
     C                   IN        SAVUDS
     C                   MOVE      FIFIRM        UFIRM
     C                   MOVE      FICURR        UCURR
     C     BIAVD         IFNE      0
     C                   MOVE      BIAVD         UBIAVD
     C                   END
     C                   MOVE      BICGR         UBICGR
     C                   MOVE      BICNO         UBICNO
     C                   MOVE      BIFC          UBIFC
     C                   MOVE      BIPRT         UBIPRT
     C                   MOVE      BIPRTM        UBIPRM
N06  C                   MOVE      BIPRTP        UBIPRP
N06  C                   MOVE      BIPRTI        UBIPRI
     C                   MOVE      BIPRT         WSPRT
     C                   MOVE      BILOC         UBILOC
     C                   MOVE      BIPRTM        WSPRT2
     C                   OUT       SAVUDS
     C                   Z-ADD     99999999999   YTNO
     C                   EXSR      TESTB
     C                   END
     C                   MOVE      UBIPRM        WSPRTM
      *
     C                   ENDSR
      *
     C***********************************************
     C     CRTTR         BEGSR
     C***********************************************
     C                   MOVE      'CRT'         UTYPE             3
     C                   MOVE      *ZEROS        UTNO
     C     *LIKE         DEFINE    TRTNO         UTNO
      *
     C                   CALL      'GSS021R'
     C                   PARM                    UTNO
     C                   PARM                    UTYPE
     C                   PARM                    UMODE
      *
     C                   MOVE      '1'           *IN90
     C                   EXSR      VISTR
     C                   MOVE      '0'           *IN90
      *
     C                   ENDSR
      *
     C***********************************************
     C     VISTR         BEGSR
     C***********************************************
     C     UTNO          IFNE      0                                            1<-B
     C     UTNO          CHAIN     GSSTRF                             33
     C     *IN33         IFEQ      '0'                                           2<-B
     C                   MOVE      TRDTLD        WXDTLD
     C                   MOVE      TRDTLM        WXDTLM
     C                   MOVE      TRDTLÅ        WXDTLÅ
     C                   MOVE      TRDTSD        WXDTSD
     C                   MOVE      TRDTSM        WXDTSM
     C                   MOVE      TRDTSÅ        WXDTSÅ
     C   90              ADD       1             XRECNO
     C   90              MOVE      XRECNO        ZRECNO
     C   90              WRITE     GSS020DA
     C                   END                                                     2<-E
     C                   END                                                    1<-E
      *
     C  N90              MOVE      *BLANKS       WSNR
     C  N90              UPDATE    GSS020DA
      *
     C                   ENDSR
      *
     C***********************************************
     C     CHGTR         BEGSR
     C***********************************************
     C  N90              MOVE      '1'           *IN99
     C                   MOVE      'CHG'         UTYPE             3
     C                   MOVE      TRTNO         UTNO
      *
     C                   CALL      'GSS021R'
     C                   PARM                    UTNO
     C                   PARM                    UTYPE
     C                   PARM                    UMODE
      *
     C  N90              EXSR      VISTR
      *
     C                   ENDSR
      *
     C***********************************************
     C     DSPTR         BEGSR
     C***********************************************
     C  N90              MOVE      '1'           *IN99
     C  N90              MOVE      *BLANKS       WSNR
     C  N90              UPDATE    GSS020DA
      *
     C                   MOVE      'DSP'         UTYPE             3
     C                   MOVE      TRTNO         UTNO
     C                   CALL      'GSS021R'
     C                   PARM                    UTNO
     C                   PARM                    UTYPE
     C                   PARM                    UMODE
      *
     C                   ENDSR
      *
     C***********************************************
     C     PRTTR         BEGSR
     C***********************************************
     C                   MOVE      '0'           *IN99
     C                   WRITE     WINDOWSZ
     C     STPRT         TAG
     C                   EXFMT     GSS020DX
     C                   MOVE      '0'           *IN99
     C   12              GOTO      SLPRT
     C     WSPRT         COMP      *BLANKS                                99
     C   99              GOTO      STPRT
      *
     C                   MOVE      'PRT'         UTYPE
     C                   MOVE      TRCNO         UCNO2             8
     C                   MOVE      TRTNO         UTNO2            11
      *
     C     WSFAX         IFEQ      'Y'
     C                   CALL      'GSS034C'
     C                   PARM                    UCNO2
     C                   PARM                    UUTFAX            1
     C                   PARM                    UTNO2
     C                   PARM                    UTYPE
N04  C                   PARM                    UORDTYP           1
     C                   PARM                    UVALK             3
     C                   PARM                    UMOD              1
     C                   PARM                    UCOMM             1
     C                   PARM                    UKNC              8
     C     UUTFAX        CABEQ     'J'           STPRT2
     C                   GOTO      STPRT
     C                   END
      *
     C     WSCPY         IFEQ      0
     C                   Z-ADD     1             WSCPY
     C                   END
     C                   MOVE      WSCPY         UCPY              1
      *
     C                   CALL      'GSS022R'
     C                   PARM                    UTNO2
     C                   PARM                    UTYPE
     C                   PARM                    UCPY
     C                   PARM                    WSPRT
     C                   PARM                    WSFAX
      *
     C     STPRT2        TAG
     C  N90              MOVE      '1'           TRST
     C  N90              MOVE      '1'           *IN99
     C  N90              MOVE      *BLANKS       WSNR
     C  N90              UPDATE    GSS020DA
      *
     C     SLPRT         ENDSR
      *
     C***********************************************
     C     CFMTR         BEGSR
     C***********************************************
     C  N90              MOVE      '1'           *IN99
     C   23              DO
     C                   MOVE      'CFM'         UTYPE
     C                   MOVE      TRTNO         UTNO2
     C                   MOVE      'N'           WSFAX
     C                   CALL      'GSS022R'
     C                   PARM                    UTNO2
     C                   PARM                    UTYPE
     C                   PARM                    UCPY
     C                   PARM                    WSPRT
     C                   PARM                    WSFAX
     C                   MOVE      '2'           TRST
     C                   END
      *
     C  N90              MOVE      *BLANKS       WSNR
     C  N90              UPDATE    GSS020DA
      *
     C                   ENDSR
      *
     C***********************************************
     C     PLKTR         BEGSR
     C***********************************************
     C                   MOVE      '0'           *IN99
     C                   WRITE     WINDOWSZ
     C                   MOVE      'L'           WSSORT
     C                   MOVE      'N'           WSFAX
     C                   MOVE      'N'           WSCLAB
N06  C                   EVAL      WSPRT = BIPRTP
     C     STPLK         TAG
     C                   EXFMT     GSS020DY
     C                   MOVE      '0'           *IN99
N02  C   03              GOTO      SLPLK
     C   12              GOTO      SLPLK
     C     WSPRT         COMP      *BLANKS                                99
     C   99              GOTO      STPLK
      *
     C                   MOVE      'PRT'         UTYPE
     C                   MOVE      TRCNO         UCNO2             8
     C                   MOVE      TRTNO         UTNO2            11
      *
     C                   SELECT
     C     WSSORT        WHENEQ    'L'
     C     WSSORT        OREQ      'O'
     C     WSSORT        OREQ      'B'
     C                   MOVE      'N'           UKOPI             1
     C                   CALL      'GSS025R'
     C                   PARM                    UTNO2
     C                   PARM                    WSPRT
     C                   PARM                    WSSORT
     C                   PARM                    UKOPI
     C                   PARM                    UUSER
      * Merkelapp for ny transport-etappe. (UFROM=G, fra "Grønnskjerm")
     C     WSCLAB        IFEQ      'Y'
     C                   MOVE      'J'           UUKOPI            1
     C                   MOVE      'G'           UFROM             1
      * FORELØPIG KUN ZEBRA
     C                   MOVE      'Z'           ULABTYP           1
     C                   CALL      'GSS028R'
     C                   PARM                    UFIRM
     C                   PARM                    UTNO2
     C                   PARM                    ULABTYP
     C                   PARM                    WSPRT2
     C                   PARM                    UFROM
     C                   PARM                    UUKOPI
     C                   END
     C     WSSORT        WHENEQ    'P'
     C     WSSORT        OREQ      'F'
     C     WSSORT        OREQ      'T'
     C     WSSORT        IFEQ      'P'
     C                   MOVE      'N'           WSFAX
     C                   END
N02  C                   IF        WSSORT = 'F'
N02  C     'AIR'         SCAN      TRDW                                   33
N05   * godtar også HEUR='D'
N05  C     *in33         ifeq      '0'
N05  C                   movel     trtno         heavd
N05  C                   move      trtno         heopd
N05  C     headk         chain     headf                              34
N05  C  n34heur          comp      'D'                                    33
N05  c                   end
N02  C                   IF        *IN33
N02  C                   CALL      'GSS053R'
N02  C                   PARM                    TRTNO
N02  C                   PARM                    UFLAGG            1 0
N02  C     UFLAGG        CABNE     0             STPLK
N02  C                   END
N02  C                   END
     C                   CALL      'GSS026R'
     C                   PARM                    UTNO2
     C                   PARM                    WSPRT
     C                   PARM                    WSPRT2
     C                   PARM                    WSSORT
     C                   PARM                    WSFAX
     C                   PARM                    UMOD
     C                   PARM                    UQJOB            10
     C                   PARM                    UQUSER           10
     C                   PARM                    UQNBR             6
     C                   PARM                    UUSER
     C     WSFAX         IFEQ      'Y'
     C                   MOVE      TRCNO         UCNO2             8
     C                   MOVE      '5'           UMOD              1
     C                   CALL      'GSS034C'
     C                   PARM                    UCNO2
     C                   PARM                    UUTFAX            1
     C                   PARM                    UTNO2
     C                   PARM                    UTYPE
N04  C                   PARM                    UORDTYP           1
     C                   PARM                    UVALK             3
     C                   PARM                    UMOD              1
     C                   PARM                    UCOMM             1
     C                   PARM                    UKNC              8
     C                   MOVE      '1'           UMOD              1
     C                   END
      * Evt. statusendr. returneres gjennom WSSORT
     C     WSSORT        IFEQ      '1'
     C     WSSORT        OREQ      '2'
     C                   MOVE      WSSORT        TRST
     C                   END
     c                   ENDSL
      *
     C  N90              MOVE      '1'           *IN99
     C  N90              MOVE      *BLANKS       WSNR
     C  N90              UPDATE    GSS020DA
      *
     C     SLPLK         ENDSR
N01  C***********************************************
N01  C     DGSR          BEGSR
N01  C***********************************************
N01   *
N01  C                   CALL      'GSS076C'
N01  C                   PARM                    WSPRT
N01  C                   PARM                    TRTNO
N01  C  N90              MOVE      '1'           *IN99
N01  C  N90              MOVE      *BLANKS       WSNR
N01  C  N90              UPDATE    GSS020DA
N01   *
N01  C                   ENDSR
      *
     C***********************************************
     C     arkivsr       BEGSR
     C***********************************************
     C                   MOVEl     TRTNO         uavd              4 0
     C                   MOVE      TRTNO         uopd              7 0
     C                   CALL      'ARC003'
     C                   PARM                    uavd
     C                   PARM                    uopd
     C                   PARM                    URUNDE           10
     C  N90              MOVE      '1'           *IN99
     C  N90              MOVE      *BLANKS       WSNR
     C  N90              UPDATE    GSS020DA
     C                   ENDSR
      *
     C***********************************************
     C     BOXINF        BEGSR
     C***********************************************
     C                   MOVE      TRTNO         UTNO
     C                   CALL      'GSS047R'
     C                   PARM                    UTNO
     C  N90              MOVE      '1'           *IN99
     C  N90              MOVE      *BLANKS       WSNR
     C  N90              UPDATE    GSS020DA
      *
     C                   ENDSR
      *
     C***********************************************
     C     PRTML         BEGSR
     C***********************************************
     C                   MOVE      '0'           *IN99
     C     STPML         TAG
     C                   EXFMT     GSS020DH
     C                   MOVE      '0'           *IN89
     C   12              GOTO      SLPML
     C     WSPRTM        COMP      *BLANKS                                89
     C   89              GOTO      STPML
      *
     C     TRTNO         SETLL     GSSORL22
     C     TRTNO         READE     GSSORL22                               33
     C                   DOW       *IN33 = *OFF
     C                   MOVE      ORONO         UPONO             7
     C                   MOVE      ORDTLD        UPDTLD
     C                   MOVE      ORDTLM        UPDTLM
     C                   MOVE      ORDTLÅ        UPDTLÅ
     C     ORBCD         CHAIN     CDBOAT                             33
     C     CUNDL01K      CHAIN     CUNDL01                            33
     C                   MOVE      *BLANKS       UPO
     C                   MOVE      *ZEROS        XN                3 0
     C     ORONO         SETLL     GSSPOF
     C     ORONO         READE     GSSPOF                                 33
     C                   DOW       *IN33 = *OFF
     C                   ADD       1             XN                3 0
     C                   MOVE      POCONO        UPO(XN)
     C     ORONO         READE     GSSPOF                                 33
     C  N33              ENDDO
     C                   CALL      'GSS011RP'
     C                   PARM                    WSPRTM                         PRINTNAVN
     C                   PARM                    UPONO                          INTER ORDRENR
N03  C                   PARM                    ORTYPE                         ORDRETYPE
     C                   PARM                    CBNAB                          BÅTNAVN
     C                   PARM                    KNAVN                          KUNDENAVN
     C                   PARM                    ORSNM                          LEVERANDØR
     C                   PARM                    UPDTL                          LEV.DATO
     C                   PARM                    ORCLI                          ANTALL KOLLI
     C                   PARM                    UPCON1                         P.O.NR 1
     C                   PARM                    UPCON2                         P.O.NR 2
     C                   PARM                    UPCON3                         P.O.NR 3
     C                   PARM                    UPCON4                         P.O.NR 4
     C                   PARM                    UPCON5                         P.O.NR 5
     C                   PARM                    UPCON6                         P.O.NR 6
     C     TRTNO         READE     GSSORL22                               33
     C  N33              ENDDO
      *
     C  N90              MOVE      '1'           *IN99
     C  N90              MOVE      *BLANKS       WSNR
     C  N90              UPDATE    GSS020DA
      *
     C     SLPML         ENDSR
      *
     C***********************************************
     C     BERFAK        BEGSR
     C***********************************************
     C                   MOVE      TRTNO         UTNO
     C                   CALL      'GSS049C'
     C                   PARM                    PAR07             7 0
     C                   PARM                    UTNO             11 0
      *
     C  N90              MOVE      *BLANKS       WSNR
     C  N90              UPDATE    GSS020DA
     C                   ENDSR
     C***********************************************
     C     SØKDTA        BEGSR
     C***********************************************
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C                   Z-ADD     WSSUBN        GRECNO            4 0
     C                   Z-ADD     1             UFLAGG            1 0
      *
     C     LINNBR        IFEQ      5
     C     POSNBR        ANDGE     48
     C     POSNBR        ANDLE     52
     C                   CALL      'SYSJ11A'
     C                   PARM                    W2BCD
     C                   GOTO      SLSØK
     C                   END
      *
     C     LINNBR        IFEQ      5
     C     POSNBR        ANDGE     54
     C     POSNBR        ANDLE     61
     C                   MOVE      *BLANKS       USØKKN           30
     C                   CALL      'SOEKKUN'
     C                   PARM                    W2CNO
     C                   PARM                    UFIRM
     C                   PARM                    USØKKN           30
     C                   GOTO      SLSØK
     C                   END
      *
     C                   MOVE      'GSS0013'     MSGNR
     C                   SETON                                        30
      *
     C     SLSØK         ENDSR
      *
N07  C***********************************************
N07  C     MESSURE       BEGSR
N07  C***********************************************
N07  C                   MOVE      TRTNO         UTNOAL
N07  C                   CALL      'GSS082TC'
N07  C                   PARM                    UTNOAL           11
N07   *
N07  C  N90              MOVE      *BLANKS       WSNR
N07  C  N90              UPDATE    GSS020DA
N07  C                   ENDSR
