     FSVEHH     IF   E           K DISK
     FSVEH      IF   E           K DISK
     FKODTP     IF   E           K DISK
     FKODTFM    IF   E           K DISK
     FSVE012P   O    E             PRINTER OFLIND(*IN27) USROPN
      *//////////////
      *  Hodelogikk /
      *//////////////
     C                   EXSR      SRA
     C                   EXSR      SRB
     C                   MOVE      '1'           *INLR
      *////////////////////////////////////////////////////////////
      *  Initiering                                           SRA /
      *////////////////////////////////////////////////////////////
     C     SRA           BEGSR
      *___________________
      * Definiere Workfelt
      *"""""""""""""""""""
     C     *LIKE         DEFINE    SVEH_SYAV     UVEH_SYAV
     C     *LIKE         DEFINE    SVEH_SYOP     UVEH_SYOP
     C     *LIKE         DEFINE    KOPUNI        UOPUNI
     C     *LIKE         DEFINE    KOPAVD        UOPAVD
     C     *LIKE         DEFINE    KOPLNR        UOPLNR
     C     *LIKE         DEFINE    KFMSD         UFMSD
     C     *LIKE         DEFINE    SVEHH_DTH     UVEHH_DTH
     C     *LIKE         DEFINE    SVEHH_TMH     UVEHH_TMH
      *__________________
      * Definiere Nøkkler
      *""""""""""""""""""
     C     KEYA          KLIST
     C                   KFLD                    UVEH_SYAV
     C                   KFLD                    UVEH_SYOP
     C     KEYP          KLIST
     C                   KFLD                    UOPUNI
     C                   KFLD                    UOPAVD
     C                   KFLD                    UOPLNR
     C     KEYF          KLIST
     C                   KFLD                    KOPFM
     C                   KFLD                    UFMSD
      *_____________
      * Init av felt
      *"""""""""""""
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVE      WDT           WDT80             8 0
     C                   EVAL      UOPUNI='P'
     C                   EVAL      UOPAVD=UVEH_SYAV
     C                   EVAL      UOPLNR=253
     C                   EVAL      UFMSD=1
     C                   Z-ADD     1024          UCMDL            15 5
     C                   MOVE      *BLANKS       UCMD           1024
      *_________________
      * Input parametrer
      *"""""""""""""""""
     C     *ENTRY        PLIST
     C                   PARM                    UVEH_SYAV
     C                   PARM                    UVEH_SYOP
     C                   PARM                    UVEHH_DTH
     C                   PARM                    UVEHH_TMH
      *_____________________
      * Hente printerstyring
      *"""""""""""""""""""""
     C     KEYP          CHAIN     KODTP
     C     KEYF          CHAIN     KODTFM
      *_______________
      * Printerstyring
      *"""""""""""""""
     C                   EVAL      UCMD='OVRPRTF FILE(SVE012P) OUTQ('
     C     UCMD          CAT       KOPNVN:0      UCMD
     C     UCMD          CAT       ')':0         UCMD
     C                   CALL      'QCMDEXC'                            33
     C                   PARM                    UCMD
     C                   PARM                    UCMDL
     C                   OPEN      SVE012P
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Læs fakturor                                         SRB /
      *////////////////////////////////////////////////////////////
     C     SRB           BEGSR
     C     KEYA          CHAIN     SVEH
     C                   WRITE     SVE012PA

     C     KEYA          SETLL     SVEHH
     C     KEYA          READE     SVEHH                                  51
     C     *IN51         DOWEQ     *OFF
     C     UVEHH_DTH     IFEQ      SVEHH_DTH
     C     UVEHH_TMH     ANDEQ     SVEHH_TMH

     C                   IF        *IN27
     C                   EVAL      *IN27=*OFF
     C                   WRITE     SVE012PA
     C                   ENDIF
     C                   WRITE     SVE012PB

     C                   ENDIF
     C     KEYA          READE     SVEHH                                  51
     C                   ENDDO

     C                   WRITE     SVE012PZ
     C                   ENDSR
