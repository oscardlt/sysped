      *****************************************************************
      * UTSKRIFT AV DIFFERANSE AV LAGEROPPTELLING                    *
      * ANTALL LINJE=72,  ANTALL TEGN PR. LINJE=80 OVERFLOW=66       *
      *****************************************************************
      * HEADING
     A          R LL047P1
     A                                  2  2'LL047R'
     A                                  2 12MSGCON(50 YBC1567 SYMSGF)
     A                                      HIGHLIGHT
     A                                  2 68MSGCON(05 YBC1536 SYMSGF)
     A                                  2 74PAGNBR EDTCDE(Z)
     A                                  3  2MSGCON(14 YBC1537 SYMSGF)
     A            NVNGRU        30A     3 17
     A                                      HIGHLIGHT
     A                                  4  2MSGCON(14 YBC1538 SYMSGF)
     A            NVNKJE        30A     4 17
     A                                      HIGHLIGHT
     A                                  4 41MSGCON(08 YBC1539 SYMSGF)
     A            WSLNR          7S 0   4 60EDTCDE(Z)
     A                                  5  2MSGCON(16 YBC1540 SYMSGF)
     A            WSLTDT         8S 0   5 20EDTWRD('    .  .  ')
     A                                      HIGHLIGHT
     A                                  5 42MSGCON(14 YBC1541 SYMSGF)
     A            XIDAG          8S 0   5 60EDTWRD('    .  .  ')
     A                                  7  2MSGCON(77 YBC1568 SYMSGF)
     A                                  8  2'-------------------------'
     A                                  8 27'-------------------------'
     A                                  8 52'-------------------------'
     A                                  8 77'--'
     A                                  9  2' '
      *DETALJER
     A          R LL047P2                   SPACEB(1)
     A            LTLOK1         5A  O     2
     A            LTLOK2        20A  O     8
     A            LTANR         15A  O    29
     A            LTSALD         7  0O    45EDTCDE(Z)
     A            WSOPTS         7  0O    53EDTCDE(Z)
     A            LTDIFF         7  0O    61EDTCDE(L)
     A                                      HIGHLIGHT
     A            LTBAT2        10A  O    69
      *
     A          R LL047P3                   SPACEB(2)
     A                                     2MSGCON(77 YBC1551 SYMSGF)
