********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
      * N01/N02 - bedre overskrifter
11XXX * 03 SH  PTF11 flytt mobil til SMS hvis blank
********************************************************************
     FDOKUFE    UF A E           K DISK
     FFIRM      IF   E           K DISK
     FCUNDC     IF   E             DISK
     FSYCTA01D  CF   E             WORKSTN
      *//////////////
      *  Hodelogikk /
      *//////////////
     C                   EXSR      SRA
     C                   EXSR      SRB
     C     *IN12         IFEQ      *OFF
     C                   EXSR      SRC
     C                   ENDIF
     C                   MOVE      '1'           *INLR
      *////////////////////////////////////////////////////////////
      *  Initiering                                           SRA /
      *////////////////////////////////////////////////////////////
     C     SRA           BEGSR
      *_________________
      * Input parametere
      *"""""""""""""""""
     C     *ENTRY        PLIST
     C                   PARM                    ZE_DFAVD
     C                   PARM                    ZE_DFOPD
     C                   PARM                    ZE_DFFBNR
     C                   PARM                    WE_N3035
     C                   PARM                    WE_KUNDNR         8 0
      *_______________
      * Definiere felt
      *"""""""""""""""
     C     *LIKE         DEFINE    FE_N3035      WE_N3035
      *__________________
      * Definiere Nøkkler
      *""""""""""""""""""
     C     DOKUFEKEY     KLIST
     C                   KFLD                    ZE_DFAVD
     C                   KFLD                    ZE_DFOPD
     C                   KFLD                    ZE_DFFBNR
     C                   KFLD                    WE_N3035
      * Hetn firmakode
     C     *LOVAL        SETLL     FIRM
     C                   READ      FIRM
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Bearbeide Formater                                   SRB /
      *////////////////////////////////////////////////////////////
     C     SRB           BEGSR
      * Sjekker først om det finnes noen verdier å vise
     C     DOKUFEKEY     CHAIN(N)  DOKUFE                             90
     C     *IN90         IFEQ      *OFF
     C                   EVAL      ZE_C3413=FE_C3413
     C                   EVAL      ZE_C3412=FE_C3412
     C                   EVAL      ZE_C3148A=FE_C3148A
     C                   EVAL      ZE_C3148B=FE_C3148B
     C                   EVAL      ZE_C3148C=FE_C3148C
     C                   EVAL      ZE_C3148D=FE_C3148D
     C                   EVAL      ZE_C3148E=FE_C3148E
     C                   ENDIF
      * Kvalifikator (Type kundekontakt)
     C                   SELECT
N01  C     WE_N3035      WHENEQ    'CZ '
N02  C                   EVAL      ZE_TYP='Avsenderadresse'
N02  C     WE_N3035      WHENEQ    'PW '
N01  C                   EVAL      ZE_TYP='Henteadresse'
N01  C     WE_N3035      WHENEQ    'CN '
N01  C                   EVAL      ZE_TYP='Mottakeradresse'
     C     WE_N3035      WHENEQ    'DP '
     C                   EVAL      ZE_TYP='Leveringsadresse'
     C                   OTHER
S02  C*                  EVAL      ZE_TYP='Leveringsadresse'
N02  C                   EVAL      ZE_TYP='Part NAD+'+WE_N3035
     C                   ENDSL
      * Viser bilde
     C     TAG01         TAG
N03  C     ZE_C3148D     IFEQ      *blanks
N03  C                   EVAL      ZE_C3148D=ZE_C3148B
N03  C                   end
     C                   EXFMT     SYCTA01DA
     C   03              GOTO      TAG02
     C   04              EXSR      SRBA
     C   12              GOTO      TAG02
     C                   GOTO      TAG01
     C     TAG02         TAG
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Søk kontakter                                       SRBA /
      *////////////////////////////////////////////////////////////
     C     SRBA          BEGSR
     C                   CALL      'VCUNDCR'
     C                   PARM                    FIFIRM
     C                   PARM                    WE_KUNDNR
     C                   PARM                    URRN             10 0
     C                   PARM                    UEMFA             1
     C                   PARM                    UCONTA           30
     C     URRN          CHAIN     CUNDC                              91
     C                   IF        *IN91=*OFF
     C                   EVAL      ZE_C3412=UCONTA
     C                   EVAL      ZE_C3148A=CPHONE
     C                   EVAL      ZE_C3148B=CMOBIL
     C                   EVAL      ZE_C3148C=CFAX
     C                   EVAL      ZE_C3148D=CMOBIL
     C                   EVAL      ZE_C3148E=CEMAIL
     C                   ENDIF
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Summering                                            SRC /
      *////////////////////////////////////////////////////////////
     C     SRC           BEGSR
     C     DOKUFEKEY     CHAIN     DOKUFE                             90
     C   90              CLEAR                   DOKUFER
     C   90              EVAL      FE_DFAVD=ZE_DFAVD
     C   90              EVAL      FE_DFOPD=ZE_DFOPD
     C   90              EVAL      FE_DFFBNR=ZE_DFFBNR
     C   90              EVAL      FE_N3035=WE_N3035
     C                   EVAL      FE_C3413=ZE_C3413
     C                   EVAL      FE_C3412=ZE_C3412
     C                   EVAL      FE_C3148A=ZE_C3148A
     C                   EVAL      FE_C3148B=ZE_C3148B
     C                   EVAL      FE_C3148C=ZE_C3148C
     C                   EVAL      FE_C3148D=ZE_C3148D
     C                   EVAL      FE_C3148E=ZE_C3148E
     C     FE_C3413      IFEQ      *BLANKS
     C     FE_C3412      ANDEQ     *BLANKS
     C     FE_C3148A     ANDEQ     *BLANKS
     C     FE_C3148B     ANDEQ     *BLANKS
     C     FE_C3148C     ANDEQ     *BLANKS
     C     FE_C3148D     ANDEQ     *BLANKS
     C     FE_C3148E     ANDEQ     *BLANKS
     C  N90              DELETE    DOKUFER
     C                   GOTO      SRC9
     C                   ENDIF
     C   90              WRITE     DOKUFER
     C  N90              UPDATE    DOKUFER
     C     SRC9          TAG
     C                   ENDSR
