     FEDIRE     IF   E           K DISK
     FQ         O    F   80        DISK    USROPN
      *   Input Parametere
     D DirName         S            100A
     D FullName        S            256A

      *   Arbeidsvariabler
     D Outfile         DS
     D  OutFilNam                    10    INZ('Q         ')
     D  OutFilLib                    10    INZ('QTEMP     ')

     D ObjVar          S             90
     D ObjVarLen       S             10I 0 Inz(%size(ObjVar))
     D ObjVarFmt       S              8
     D ObjTyp          S             10
     D CmdLine         S            512
     D CmdLen          S             15  5

     D APIERR          DS
     D  ERRSIZ                 1      4B 0 INZ(256)
     D  ERRLEN                 5      8B 0 INZ(0)
     D  ERRMIC                 9     15
     D  ERRNBR                16     16
     D  ERRDTA                17    272


      * Finnes ouput fil ?
     C                   CALL      'QUSROBJD'
     C                   PARM                    ObjVar
     C                   PARM                    ObjVarLen
     C                   PARM      'OBJD0100'    ObjVarFmt
     C                   PARM                    OutFile
     C                   PARM      '*FILE'       ObjTyp
     C                   PARM                    APIERR
      * Lag outfile hvis nødvendig
     C                   IF        ERRMIC = 'CPF9812'
     C                   EVAL      CmdLine = 'CRTPF FILE('
     C                             + %trimr(OutFilLib) + '/'
     C                             + %trimr(OutFilNam) + ')'
     C                             + ' RCDLEN(80)'
     C                   EVAL      CmdLen = %len(%trim(CmdLine))
     C                   CALL      'QCMDEXC'     Qcmdexc
     C                   ENDIF
      * Tøm outfile
     C                   EVAL      CmdLine = 'CLRPFM FILE('
     C                             + %trimr(OutFilLib) + '/'
     C                             + %trimr(OutFilNam) + ')'
     C                   EVAL      CmdLen = %len(%trim(CmdLine))
     C                   CALL      'QCMDEXC'     Qcmdexc
      * Åpne fil
     C                   OPEN      Q
      * Kopiere fra EDIRE til QTEMP/Q
     C     WMBR          SETLL     EDIRE
     C     WMBR          READE     EDIRE                                  51
     C     *IN51         DOWEQ     *OFF
     C                   EXCEPT    ADDEDI
     C     WMBR          READE     EDIRE                                  51
     C                   ENDDO
      * Lukk QTEMP/Q
     C                   CLOSE     Q
      * Kopiere fra qtemp/q til *LIBL/EDI79F
     C                   EVAL      CmdLine = 'CPYF FROMFILE(QTEMP/Q)'
     C                             + ' TOFILE(EDI79F) TOMBR('
     C                             + WMBR
     C                             + ') MBROPT(*REPLACE) FMTOPT(*NOCHK)'
     C                   EVAL      CmdLen = %len(%trim(CmdLine))
     C                   CALL      'QCMDEXC'     Qcmdexc
      * Last record
     C                   EVAL      *INLR = *ON

      *////////////////////////////////////////////////////////////
      *  Init av PGM                                       *INZSR /
      *////////////////////////////////////////////////////////////
     C     *INZSR        BEGSR

     C     *ENTRY        PLIST
     C                   PARM                    WMBR             10

     C     Qcmdexc       PLIST
     C                   PARM                    CmdLine
     C                   PARM                    Cmdlen

     C                   ENDSR
     OQ         E            ADDEDI
     O                       RDATA               80
