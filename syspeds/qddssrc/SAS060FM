     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      HLPDOC('VEDLSAK' 'HELPNU' 'HLP400')
     A                                      HELP
     A                                      ALTHELP
      *
     A          R SUBFIL                    SFL
     A            WSNR           1A  B  6  5VALUES(' ' '2' '4')
     A            SASONE         2S 0O  6  8
     A            SGMINI         5Y 0O  6 15EDTCDE(4)
     A            SGNRAT         5Y 2O  6 21EDTCDE(1)
     A            SGQ045         5Y 2O  6 28EDTCDE(1)
     A            SGQ100         5Y 2O  6 35EDTCDE(1)
     A            SGQ500         5Y 2O  6 42EDTCDE(1)
     A            SPMINI         5Y 0O  6 53EDTCDE(4)
     A            SPQ045         5Y 2O  6 66EDTCDE(1)
     A            SPQ100         5Y 2O  6 73EDTCDE(1)
     A            SPNRAT         5Y 2O  6 59EDTCDE(1)
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A            ZRECNO         4S 0O  3 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYSPED/SAS060'
     A                                      COLOR(BLU)
     A            MSG102        47A  O  1 19MSGID(TRO 0507 SYMSGF)
     A                                      DSPATR(HI)
     A                                  1 68DATE
     A                                      EDTCDE(Y)
     A                                  2 68TIME
     A            MSG103        30A  O  3  2MSGID(DIV 0016 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG104        30A  O  4  2MSGID(DIV 0017 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG105        78A  O  5  2MSGID(TRO 0506 SYMSGF)
     A                                      DSPATR(HI)
      *
     A          R BUNN
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF03(03 'EXIT')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSG201        18A  O  3  2MSGID(DIV 0020 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG202        18A  O  3 22MSGID(DIV 0021 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG203        18A  O  3 42MSGID(DIV 0022 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG204        18A  O  3 62MSGID(DIV 0023 SYMSGF)
     A                                      COLOR(BLU)
     A  98        MSG205        78A  O  4  2MSGID(DIV 0012 SYMSGF)
     A                                      COLOR(WHT)
     A N98                              4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
      *
     A          R VARELI
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSG301        78A  O  1  2MSGID(TRO 0506 SYMSGF)
     A                                      COLOR(WHT)
     A            WSSONE         2S 0B  2  8DSPATR(UL)
     A  40                                  DSPATR(HI)
     A*N40                                  DSPATR(PR)
     A  90                                  DSPATR(RI)
     A            WGMINI         5Y 0B  2 15DSPATR(UL)
     A                                      EDTCDE(4)
     A  40                                  DSPATR(HI)
     A*N40                                  DSPATR(PR)
     A  90                                  DSPATR(RI)
     A            WGNRAT         5Y 2B  2 21DSPATR(UL)
     A                                      EDTCDE(1)
     A  40                                  DSPATR(HI)
     A*N40                                  DSPATR(PR)
     A  90                                  DSPATR(RI)
     A            WGQ045         5Y 2B  2 28DSPATR(UL)
     A                                      EDTCDE(1)
     A  40                                  DSPATR(HI)
     A*N40                                  DSPATR(PR)
     A  90                                  DSPATR(RI)
     A            WGQ100         5Y 2B  2 35DSPATR(UL)
     A                                      EDTCDE(1)
     A  40                                  DSPATR(HI)
     A*N40                                  DSPATR(PR)
     A  90                                  DSPATR(RI)
     A            WGQ500         5Y 2B  2 42DSPATR(UL)
     A                                      EDTCDE(1)
     A  40                                  DSPATR(HI)
     A*N40                                  DSPATR(PR)
     A  90                                  DSPATR(RI)
     A            WPMINI         5Y 0B  2 53DSPATR(UL)
     A                                      EDTCDE(4)
     A  40                                  DSPATR(HI)
     A*N40                                  DSPATR(PR)
     A  90                                  DSPATR(RI)
     A            WPQ045         5Y 2B  2 66DSPATR(UL)
     A                                      EDTCDE(1)
     A  40                                  DSPATR(HI)
     A*N40                                  DSPATR(PR)
     A  90                                  DSPATR(RI)
     A            WPQ100         5Y 2B  2 73DSPATR(UL)
     A                                      EDTCDE(1)
     A  40                                  DSPATR(HI)
     A*N40                                  DSPATR(PR)
     A  90                                  DSPATR(RI)
     A            WPNRAT         5Y 2B  2 59DSPATR(UL)
     A                                      EDTCDE(1)
     A  40                                  DSPATR(HI)
     A*N40                                  DSPATR(PR)
     A  90                                  DSPATR(RI)
     A            MSG302        18A  O  3  2MSGID(DIV 0024 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  4  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                              4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R VAREL2
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            MSG401        12A  O  2  2MSGID(DIV 1216 SYMSGF)
     A            XSONE          2S 0I  2 16DSPATR(UL)
     A                                      DSPATR(PC)
     A                                      COLOR(BLU)
     A            MSG302        18A  O  3  2MSGID(DIV 0024 SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
      *
     A          R VAREL3
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            MSG401        78A  O  1  2MSGID(TRO 0506 SYMSGF)
     A                                      COLOR(WHT)
     A            SASONE         2S 0O  2 08DSPATR(UL)
     A                                      DSPATR(PC)
     A                                      COLOR(WHT)
     A            SGMINI         5Y 0O  2 15EDTCDE(4)
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            SGNRAT         5Y 2O  2 21EDTCDE(1)
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            SGQ045         5Y 2O  2 28EDTCDE(1)
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            SGQ100         5Y 2O  2 35EDTCDE(1)
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            SGQ500         5Y 2O  2 42EDTCDE(1)
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            SPMINI         5Y 0O  2 53EDTCDE(4)
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            SPQ045         5Y 2O  2 66EDTCDE(1)
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            SPQ100         5Y 2O  2 73EDTCDE(1)
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            SPNRAT         5Y 2O  2 59EDTCDE(1)
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            MSG402        18A  O  3  2MSGID(DIV 0024 SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
      *
     A          R NYWRK
     A                                      CF03(03 'EXIT')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                  1  2'SYSPED/SYFA60'
     A                                      COLOR(BLU)
     A            MSG0502       47A  O  1 19MSGID(TRO 0507 SYMSGF)
     A                                      DSPATR(HI)
     A                                  1 67DATE
     A                                      EDTCDE(Y)
     A                                  2 67TIME
     A            MSG503        30A  O  3  2MSGID(DIV 0016 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG504        30A  O  4  2MSGID(DIV 0017 SYMSGF)
     A                                      COLOR(BLU)
     A            TR0506        78A  O  5  2MSGID(DIV 1253 SYMSGF)
     A                                      DSPATR(HI)
     A            MSG506        18A  O 23  2MSGID(DIV 0020 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG507        18A  O 23 22MSGID(DIV 0021 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG508        18A  O 23 42MSGID(DIV 0022 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG509        18A  O 23 62MSGID(DIV 0023 SYMSGF)
     A                                      COLOR(BLU)
     A                                 24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
