     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      HELP
     A                                      ALTHELP
      *
     A          R BILDE
     A                                      CA03(03 'Gå til inngangsbilde')
     A                                  1  2'SYSPED/TR077R'
     A                                      COLOR(BLU)
     A                                  1 20'Vis NCTS som er avsluttet etter da-
     A                                      g 1 fra godsdato'
     A                                      COLOR(WHT)
     A                                  3  2'Fra og med dato:'
     A            WSDTF          8Y 0B  3 20DSPATR(UL)
     A  97
     AO 99                                  DSPATR(PC)
     A  97
     AO 99                                  COLOR(WHT)
     A                                      EDTCDE(Z)
     A                                  3 30'(CCAAMMDD)'
     A                                  5  2'Til og med dato:'
     A            WSDTT          8Y 0B  5 20DSPATR(UL)
     A  98                                  DSPATR(PC)
     A  98                                  COLOR(WHT)
     A                                      EDTCDE(Z)
     A                                  5 30'(CCAAMMDD)'
     A                                  7  2'Arbeidsdager ?'
     A            WSADJN         1A  B  7 20DSPATR(UL)
     A                                      VALUES('J' 'N')
     A                                  7 23'(J/N)'
     A                                 23  2'F3=Avbryt'
     A                                      COLOR(BLU)
     A  99                             23  2'Fra og med dato er ugyldig.'
     A                                      COLOR(WHT)
     A  98                             23  2'Til og med dato er ugyldig.'
     A                                      COLOR(WHT)
     A  97                             23  2'Fra og med dato kan ikke være sene-
     A                                      re enn til og med dato.'
     A                                      COLOR(WHT)
      *
     A          R SUBFIL                    SFL
     A            WSNR           2Y 0B  5  2EDTCDE(4)
     A            TIAVD          4Y 0   5  5EDTCDE(Z)
     A            TITDN          7Y 0   5 10EDTCDE(Z)
     A            TISG           3A  H
     A            TIGN15        15A     5 18
     A            WSGDT          8Y 0   5 34EDTWRD('    .  .  ')
     A            WSADT          8Y 0   5 45EDTWRD('    .  .  ')
     A            TITRNR        18A     5 56
     A            WSANTD         3Y 0   5 75EDTCDE(Z)
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0017)
     A                                      SFLPAG(0016)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                      CA03(03 'Avslutt')
     A                                      CA05(05 'Frisk')
     A                                      CA12(12 'Avbryt')
     A                                  1  2'SYSPED/TR077R'
     A                                      COLOR(BLU)
     A                                  1 20'Vis NCTS som er avsluttet etter da-
     A                                      g 1 fra godsdato'
     A                                      COLOR(WHT)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                  2 70TIME
     A                                  3  2'Velg opsjon:   5=Vis ankomstmeld. -
     A                                        9=Vis logg   45=Vis lossemerknad'
     A                                      COLOR(BLU)
     A            ZRECNO         4S 0O  3 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  4  2'Op Avd. Oppd.nr Godsnr          Go-
     A                                      dsdato   IE44-dato  MRN-nr'
     A                                      COLOR(WHT)
      *
     A          R BUNN
     A                                 22  2'F3=Avslutt  F5=Frisk  F12=Forrige'
     A                                      COLOR(BLU)
