     A*%%TS  SD  20180924  073139  SVEIN       REL-V7R1M0  5770-WDS
*****A**************************************************************
     A* RETTINGER I DETTE PROGRAM:
PTFnrA*Sek Sig Vers Beskrivelse...........................
"""""A*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10261A* 01 YBC PTF10 VEKT/KUBIKK I TURER ER ENDRET TIL 9.0, 7.3
     A*              INGEN MERKING
*****A**************************************************************
     A*%%EC
     A                                      DSPSIZ(27 132 *DS4)
     A                                      PRINT
     A          R SUBFIL                    SFL
     A*%%TS  SD  20180924  073139  SVEIN       REL-V7R1M0  5770-WDS
     A            WSNR           1A  B  6  2VALUES(' ' '2' '4' '6' '9')
     A            WSFIRM    R        H      REFFLD(TRABR/TBFIRM *LIBL/TRAB)
     A            UNBILN    R        O  6  4REFFLD(UNITR/UNBILN *LIBL/UNIT)
     A            UNTILH    R        O  6 15REFFLD(UNITR/UNTILH *LIBL/UNIT)
     A            UNLAND    R        O  6 26REFFLD(UNITR/UNLAND *LIBL/UNIT)
     A            UNTRME    R        O  6 29REFFLD(UNITR/UNTRME *LIBL/UNIT)
     A            UNVEKT    R        O  6 35REFFLD(UNITR/UNVEKT *LIBL/UNIT)
     A                                      EDTCDE(Z)
     A            UNTARA    R        O  6 45REFFLD(UNITR/UNTARA *LIBL/UNIT)
     A                                      EDTCDE(Z)
     A            UNM3      R        O  6 51REFFLD(UNITR/UNM3 *LIBL/UNIT)
     A                                      EDTCDE(4)
     A            UNLM      R        O  6 60REFFLD(UNITR/UNLM *LIBL/UNIT)
     A                                      EDTCDE(4)
     A            UNHØYD    R        O  6 66REFFLD(UNITR/UNHØYD *LIBL/UNIT)
     A                                      EDTCDE(4)
     A            UNDATE    R        H      REFFLD(UNITR/UNDATE *LIBL/UNIT)
     A            WNDATE         6Y 0O  6 72EDTCDE(4)
     A            UNRETU    R        H      REFFLD(UNITR/UNRETU *LIBL/UNIT)
     A            UNSAVD    R        H      REFFLD(UNITR/UNSAVD *LIBL/UNIT)
     A            UNSPRO    R        H      REFFLD(UNITR/UNSPRO *LIBL/UNIT)
     A            UNRUND    R        H      REFFLD(UNITR/UNRUND *LIBL/UNIT)
     A            UNTRMA    R        O  6 79REFFLD(UNITR/UNTRMA *LIBL/UNIT)
     A            UNUNTY    R        H      REFFLD(UNITR/UNUNTY *LIBL/UNIT)
     A            UNAVD     R        O  6 83REFFLD(UNITR/UNAVD *LIBL/UNIT)
     A  67                                  COLOR(RED)
     A            UNVAL          3A  O  6 89
     A            UNKMP          5Y 2O  6 93EDTWRD('   ,  ')
     A            UNVALI         3A  O  6100
     A            UNKMPI         5Y 2O  6104EDTWRD('   .  ')
     A            UNVALE         3A  O  6111
     A            UNKMPE         5Y 2O  6115EDTWRD('   .  ')
     A*
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A*%%TS  SD  20180924  073139  SVEIN       REL-V7R1M0  5770-WDS
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A            ZRECNO         4S 0O  2 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYSPED/SYTR06'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(045 DIV3564 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                  1 67DATE
     A                                      EDTCDE(Y)
     A            WSFIRM    R        O  2  2REFFLD(TRABR/TBFIRM *LIBL/TRAB)
     A                                      COLOR(WHT)
     A            WSNAVN        30A  O  2 11COLOR(WHT)
     A                                  2 67TIME
     A                                  3  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(030 DIV0017 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 33MSGCON(015 JOV0787 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A*                                 5  2MSGCON(078 DIV3563 *LIBL/SYMSGF)
     A                                  5  2MSGCON(130 YBC2006 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                  4 50MSGCON(021 SHU1890 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A          R BUNN
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF03(03 'EXIT')
     A                                      CF12(12 'EXIT')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  3  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 42MSGCON(018 DIV0022 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 62MSGCON(018 DIV0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  98                              4  2MSGCON(078 DIV0012 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A          R VARELI
     A*%%TS  SD  20160602  073548  SVEIN       REL-V7R1M0  5770-WDS
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF04(04 'SØK')
     A                                      CF12(12 'Forrige')
     A                                      CF03(12)
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A*                                 1  2MSGCON(078 DIV3563 *LIBL/SYMSGF)
     A                                  1  2MSGCON(130 YBC2006 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            WSBILN    R        B  2  4REFFLD(UNITR/UNBILN *LIBL/UNIT)
     A  80                                  DSPATR(RI)
     A            WSTILH    R        B  2 15REFFLD(UNITR/UNTILH *LIBL/UNIT)
     A            WSLAND    R        B  2 26REFFLD(UNITR/UNLAND *LIBL/UNIT)
     A  81                                  DSPATR(RI)
     A            WSTRME    R        B  2 29REFFLD(UNITR/UNTRME *LIBL/UNIT)
     A  82                                  DSPATR(RI)
     A            WSVEKT    R        B  2 35REFFLD(UNITR/UNVEKT *LIBL/UNIT)
     A                                      EDTCDE(4)
     A            WSTARA    R        B  2 45REFFLD(UNITR/UNTARA *LIBL/UNIT)
     A                                      EDTCDE(4)
     A            WSM3      R        B  2 51REFFLD(UNITR/UNM3 *LIBL/UNIT)
     A                                      EDTCDE(4)
     A            WSLM      R        B  2 60REFFLD(UNITR/UNLM *LIBL/UNIT)
     A                                      EDTCDE(4)
     A            WSHØYD         4Y 2B  2 66EDTCDE(4)
     A            WSDATE         6Y 0B  2 72EDTCDE(4)
     A            WSTRMA    R        B  2 79REFFLD(UNITR/UNTRMA *LIBL/UNIT)
     A                                      EDTCDE(4)
     A            WSAVD     R        B  2 83REFFLD(UNITR/UNAVD *LIBL/UNIT)
     A                                      EDTCDE(4)
     A            WSVAL          3A  B  2 89
     A            WSKMP          5Y 2B  2 93EDTCDE(4)
     A                                  3  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 21MSGCON(013 JOV1360 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            WSRETU    R        B  3 35REFFLD(UNITR/UNRETU *LIBL/UNIT)
     A                                  3 66'Unit typ'
     A                                      COLOR(WHT)
     A            WSUNTY         4A  B  3 75
     A  83                                  DSPATR(RI)
     A  30        MSGTXT        78A  O  4  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                              4  2MSGCON(018 DIV0032 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A            WSVALI         3   B  2100
     A            WSKMPI         5Y 2B  2104EDTWRD('   ,  ')
     A            WSVALE         3   B  2111
     A            WSKMPE         5Y 2B  2115EDTWRD('   ,  ')
     A          R VAREL2
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF12(12 'Forrige')
     A                                      CF03(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            W2BILN    R        B  2  4REFFLD(UNITR/UNBILN *LIBL/UNIT)
     A                                      COLOR(WHT)
     A                                  3  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A          R VAREL3
     A*%%TS  SD  20160602  073548  SVEIN       REL-V7R1M0  5770-WDS
     A                                      CF12(12 'Forrige')
     A                                      CF03(12)
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A                                  1  2MSGCON(130 YBC2006 *LIBL/SYMSGF)
     A            UNBILN    R        O  2  4REFFLD(UNITR/UNBILN *LIBL/UNIT)
     A                                      COLOR(WHT)
     A            UNTILH    R        O  2 15REFFLD(UNITR/UNTILH *LIBL/UNIT)
     A                                      COLOR(WHT)
     A            UNLAND    R        O  2 26REFFLD(UNITR/UNLAND *LIBL/UNIT)
     A                                      COLOR(WHT)
     A            UNTRME    R        O  2 29REFFLD(UNITR/UNTRME *LIBL/UNIT)
     A                                      COLOR(WHT)
     A            UNVEKT    R        O  2 35REFFLD(UNITR/UNVEKT *LIBL/UNIT)
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A            UNTARA    R        O  2 45REFFLD(UNITR/UNTARA *LIBL/UNIT)
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A            UNM3      R        O  2 51REFFLD(UNITR/UNM3 *LIBL/UNIT)
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A            UNHØYD    R        O  2 59REFFLD(UNITR/UNLM *LIBL/UNIT)
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A            UNLM      R        O  2 60REFFLD(UNITR/UNLM *LIBL/UNIT)
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A            WNDATE         6Y 0O  2 72EDTCDE(4)
     A                                      COLOR(WHT)
     A            UNTRMA    R        O  2 79REFFLD(UNITR/UNTRMA *LIBL/UNIT)
     A                                      COLOR(WHT)
     A            UNAVD     R        O  2 83REFFLD(UNITR/UNAVD *LIBL/UNIT)
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A            UNVAL          3A  O  2 89COLOR(WHT)
     A            UNKMP          5Y 2O  2 93EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  3  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A            UNRETU    R        O  3 28REFFLD(UNITR/UNRETU *LIBL/UNIT)
     A                                      COLOR(WHT)
     A            UNVALI         3   O  2100COLOR(WHT)
     A            UNKMPI         5Y 2O  2104EDTWRD('   ,  ')
     A                                      COLOR(WHT)
     A            UNVALE         3   O  2111COLOR(WHT)
     A            UNKMPE         5Y 2O  2115EDTWRD('   ,  ')
     A                                      COLOR(WHT)
     A          R NYWRK
     A                                      CF03(03 'EXIT')
     A                                      CF12(12 'EXIT')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                  1  2'SYSPED/SYTR06'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(045 DIV3564 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                  1 67DATE
     A                                      EDTCDE(Y)
     A            WSFIRM    R        O  2  2REFFLD(TRABR/TBFIRM *LIBL/TRAB)
     A                                      COLOR(WHT)
     A            WSNAVN        30A  O  2 11COLOR(WHT)
     A                                  2 67TIME
     A                                  3  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(030 DIV0017 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A*                                 5  2MSGCON(078 DIV3563 *LIBL/SYMSGF)
     A                                  5  2MSGCON(130 YBC2006 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 42MSGCON(018 DIV0022 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 62MSGCON(018 DIV0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A          R BILDE1
     A                                      CF03(03 'EXIT')
     A                                      CF12(03)
     A                                      CF04(04 'SEARCH')
     A                                  1  2'SYSPED/SYTR06'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(045 DIV3564 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                  1 67DATE
     A                                      EDTCDE(Y)
     A                                  2 67TIME
     A                                  4  1MSGCON(018 DIV3303 *LIBL/SYMSGF)
     A            WSFIRM    R        B  4 20REFFLD(TRABR/TBFIRM *LIBL/TRAB)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            WSNAVN        30A  O  4 32COLOR(WHT)
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(018 DIV0032 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  44        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
