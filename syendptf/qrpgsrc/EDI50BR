********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
12XXX * 01 JOV PTF12
********************************************************************
      *
      ***********************************************************
      *** PROGRAM SKAL VEDLIKEHOLDE AVVIKENDE MAPPER ftp      ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01 02 07 09-11 13-24             ***
      *** LEDIGE INDIKATORER 27-29 31 32 34-39 41-87 95-97    ***
      ***********************************************************
      *
     FEDII      IF   E           K DISK
     FEDIIAM    UF A E           K DISK
     FEDI50BD   CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D                 DS
     D  INTCRD                 1     64
     D  XDLT                   1      7
      *
     C                   EXSR      INIT
      *
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     INID          SETLL     EDIIAM
     C     START1        TAG
     C     INID          READE(N)  EDIIAM                                 94
      *
     C     START2        TAG
      *
     C  N94              DO
     C                   DO        10
     C                   ADD       1             ZRECNO
     C                   WRITE     SUBFIL
     C     INID          READE(N)  EDIIAM                                 94
     C  N94              END
     C                   END
     C                   Z-ADD     ZRECNO        YRECNO            4 0
     C                   Z-ADD     8             LINNBR
     C                   Z-ADD     2             POSNBR
      *
     C     START3        TAG
     C                   SETOFF                                       9998
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYVARE
     C                   ELSE
     C                   WRITE     SUBCTL
     C                   EXFMT     BUNN
     C                   END
      *
      ***
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C                   Z-ADD     WSSUBN        XRECNO            4 0
      ***
      *
     C                   SETOFF                                       9830
     C*** AVSLUTT
     C   03              GOTO      SLUTT
     C*** FRISK OPP
     C   05              GOTO      START
     C*** LAGE NY
     C   06              DO
     C                   Z-ADD     19            LINNBR
     C                   Z-ADD     4             POSNBR
     C                   GOTO      START5
     C                   END
      *
      *** BLA VIDERE
     C   25              DO
     C                   Z-ADD     YRECNO        ZRECNO
     C                   SETOFF                                       25
     C                   GOTO      START2
     C                   END
      *
     C     ZRECNO        IFNE      0
     C                   EXSR      HENT
     C                   END
     C   98XRECNO        IFNE      0
     C                   Z-ADD     XRECNO        ZRECNO
     C                   END
     C                   GOTO      START3
      *
      *** LAGE
     C     START5        TAG
     C                   EXSR      MOVB2
     C                   EXFMT     VARELI
     C                   MOVEA     '000'         *IN(88)
     C                   SETOFF                                           30
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
      *
      *
     C   12              EXSR      MOVB2
     C  N12              EXSR      TESTB1
     C  N12
     CAN 30              GOTO      START5
     C                   Z-ADD     8             LINNBR
     C                   Z-ADD     2             POSNBR
     C                   GOTO      START3
      *
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
      *
      ***********************************************
     C     TESTB1        BEGSR
      ***********************************************
     C  N40              DO
      * TEST KUNDENR.
     C     KEY           CHAIN(N)  EDIIAM                             33
     C                   IF        *IN33 = *off
     C                   MOVE      'SPK0024'     MSGNR
     C                   SETON                                        90
     C                   SETON                                        30
     C                   Z-ADD     19            LINNBR
     C                   Z-ADD     04            POSNBR
     C                   END
     C                   ENDDO
     C   30              GOTO      SLTB1
      *
      *
      *** OPPDATERE PÅ REGISTER OG SUBFIL
     c     KEY           CHAIN     EDIIAM                             33
     C                   EXSR      MOVWCE
     C  N33              UPDATE    EDIIAMR
     C   33              WRITE     EDIIAMR
     C   40              UPDATE    SUBFIL
     C  N40              ADD       1             YRECNO
     C  N40              Z-ADD     YRECNO        ZRECNO
     C  N40              WRITE     SUBFIL
     C                   WRITE     SUBCTL
     C                   EXSR      MOVB2
      *
     C     SLTB1         ENDSR
      *
      ***********************************************
     C     HENT          BEGSR
      ***********************************************
     C                   Z-ADD     0             LINNBR
     C                   SETOFF                                       919299
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C   98              DO
     C   99              SETOFF                                       98
     C                   Z-ADD     1             LINNBR
     C                   GOTO      SLHENT
     C                   END
     C     ZRECNO        DIV       9             LINNBR
     C                   MVR                     XLIN              2 0
     C     XLIN          IFEQ      0
     C                   Z-ADD     9             LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C     WSNR          IFEQ      '2'
     C                   MOVE      ' '           WSNR
     C     XDLT          COMP      '*DELETE'                              98
     C   98              GOTO      SLHENT
     C                   EXSR      CHGCE
     C   12              GOTO      SLHENT
     C                   END
     C     WSNR          IFEQ      '4'
     C                   MOVE      ' '           WSNR
     C     XDLT          COMP      '*DELETE'                              98
     C   98              GOTO      SLHENT
     C                   EXSR      DLTCE
     C   12              GOTO      SLHENT
     C                   END
     C                   END
      *
     C     SLHENT        TAG
     C                   ADD       8             LINNBR
     C                   Z-ADD     2             POSNBR
     C                   SETOFF                                       99
     C   98              DO
     C                   MOVE      'DIV0012'     MSGNR
     C                   SETON                                            30
     C                   END
      *
     C                   ENDSR
      *
      ***********************************************
     C     CHGCE         BEGSR
      ***********************************************
     C                   EXSR      MOVCEW
     C                   SETON                                        99
     C     STCHG         TAG
     C                   EXFMT     VARELI
     C                   MOVEA     '000'         *IN(88)
     C                   SETOFF                                         30
      *
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
      *
     C   12              DO
     C                   UPDATE    SUBFIL
     C                   EXSR      MOVB2
     C                   END
     C  N12              EXSR      TESTB1
     C  N12
     CAN 30              GOTO      STCHG
     C     XLIN          IFEQ      0
     C                   Z-ADD     9             LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
      *
     C                   ENDSR
      *
      ***********************************************
     C     DLTCE         BEGSR
      ***********************************************
     C                   SETON                                        99
     C                   EXFMT     VAREL3
     C                   MOVE      IN0065        ws0065
     C  N12              DO
     C     key           CHAIN     EDIIAM                             33
     C  N33              DELETE    EDIIAMR
     C                   EXSR      MOVB1
     C                   END
     C     XLIN          IFEQ      0
     C                   Z-ADD     09            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C                   UPDATE    SUBFIL
      *
     C                   ENDSR
      *
      ***********************************************
     C     MOVWCE        BEGSR
      ***********************************************
     C                   MOVE      ws0065        IN0065
     C                   MOVE      wsTCRD        INTCRD
     C                   MOVE      wsTCRDT       INTCRDT
     C                   MOVE      wsTCRF        INTCRF
      *
     C                   ENDSR
      *
      ***********************************************
     C     MOVCEW        BEGSR
      ***********************************************
     C                   MOVE      IN0065        ws0065
     C                   MOVE      INTCRD        wsTCRD
     C                   MOVE      INTCRDT       wsTCRDT
     C                   MOVE      INTCRF        wsTCRF
     C                   SETON                                        40
     C                   Z-ADD     19            LINNBR
     C                   Z-ADD     12            POSNBR
      *
     C                   ENDSR
      *
      ***********************************************
     C     MOVB1         BEGSR
      ***********************************************
     C                   EVAL      IN0065 = *BLANKS
     C                   EVAL      INTCRD = '*DELETE'
     C                   EVAL      INTCRDT= *BLANKS
     C                   EVAL      INTCRF = *BLANKS
      *
     C                   ENDSR
      *
      ***********************************************
     C     MOVB2         BEGSR
      ***********************************************
     C                   SETOFF                                       40
     C                   EVAL      WS0065 = *BLANKS
     C                   EVAL      WSTCRD = *BLANKS
     C                   EVAL      WSTCRDT= *BLANKS
     C                   EVAL      WSTCRF = *BLANKS
      *
     C                   ENDSR
      *
      ***********************************************
     C     INIT          BEGSR
      ***********************************************
      *
      *   INID            Interchange ID
      *   IN0065          Msg ID, IFTMIN,..
      *   INTCRD          FTP Remote dir
      *   INTCRDT         FTP Remote dir, temp
      *   INTCRF          FTP Remote file
      *
     c     *entry        plist
     c                   parm                    INID
      *  hent standardmapper
     c     INID          Chain     EDII
     c                   eval      STD_RD  = INTCRD
     c                   eval      STD_RDT = INTCRDT
     c                   eval      STD_RF  = INTCRF
      *
     C     KEY           KLIST
     C                   KFLD                    INID
     C                   KFLD                    WS0065
      *
     C                   WRITE     WINDOWSZ
      *
     C                   ENDSR
      *
