********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
12XXX * 01 YBC PTF12 NYE FILER FOR REORGANISERING
********************************************************************
     FEDIM2     UF   E           K DISK
     FEDIS      UF   E           K DISK
     FSAEF      UF   E           K DISK
     FSAEHD     UF   E           K DISK
     FSAEV      UF   E           K DISK
N01  FSAEHTL    UF   E           K DISK
N01  FSAEHOM    UF   E           K DISK    RENAME(SAEHR:SAEHOMR)
N01  FSAEOMT    UF   E           K DISK
N01  FSAEOMTF   UF   E           K DISK
N01  FSAEOMTH   UF   E           K DISK
N01  FSAEOMTV   UF   E           K DISK
     FTVINF     UF   E           K DISK
      *
     FEDIMOLD   O    E             DISK    RENAME(EDIMR:EDIMRO)
     FEDISOLD   O    E             DISK    RENAME(EDISR:EDISRO)
     FSAEFOLD   O    E             DISK    RENAME(SADFR:SADFRO)
     FSAEHOLD   O    E             DISK    RENAME(SAEHR:SAEHRO)
     FSAEVOLD   O  A E             DISK    RENAME(SAEVR:SAEVRO)
N01  FSAEHTLOLD O  A E             DISK    RENAME(SAEHTLR:SAEHTLRO)
N01  FSAEHOMOLD O  A E             DISK    RENAME(SAEHR:SAEHOMRO)
N01  FSAEOMTOLD O  A E             DISK    RENAME(SAEOMTR:SAEOMTRO)
N01  FSAEOMTFOLDO  A E             DISK    RENAME(SAEOMTFR:SAEOMTFRO)
N01  FSAEOMTHOLDO  A E             DISK    RENAME(SAEOMTHR:SAEOMTHRO)
N01  FSAEOMTVOLDO  A E             DISK    RENAME(SAEOMTVR:SAEOMTVRO)
      *
     D                 DS
     D  XDT2                   1      6  0
     D  XD                     1      2  0
     D  XM                     3      4  0
     D  XÅ                     5      6  0
     D                 DS
     D  WDT                    1      8  0
     D  WÅ                     1      4  0
     D  WM                     5      6  0
     D  WD                     7      8  0
      *
     C                   EXSR      INIT
      *
      *__________________________________________
      * SETLL, READ AND DELETE RECORDS FROM SAEHD
      *""""""""""""""""""""""""""""""""""""""""""
     C     SEDT          SETLL     SAEHD
     C                   READ      SAEHD                                  51
     C  N51SEDT          COMP      WDT                                51
     C                   DOW       *IN51 = *OFF
     C                   IF        ((XAVD = 0) OR (SEAVD = XAVD))
     C                   EXSR      SRA
     C                   EXSR      SRB
     C                   EXSR      SRC
N01  C                   EXSR      SROM
N01  C     SAEHK         CHAIN     SAEHTL                             33
N01  C  N33              WRITE     SAEHTLRO
N01  C  N33              DELETE    SAEHTLR
     C                   WRITE     SAEHRO
     C                   DELETE    SAEHR
     C                   END
     C                   READ      SAEHD                                  51
     C  N51SEDT          COMP      WDT                                51
     C  N51              END
      *
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
      *
      *_________________________________________
      * SETLL, READ AND DELETE RECORDS FROM SAEV
      *"""""""""""""""""""""""""""""""""""""""""
     C     SRA           BEGSR
     C     SAEHK         SETLL     SAEV
     C     SAEHK         READE     SAEV                                   52
     C                   DOW       *IN52 = *OFF
     C                   WRITE     SAEVRO
     C                   DELETE    SAEVR
     C     SAEHK         READE     SAEV                                   52
     C                   END
     C                   ENDSR
      *_________________________________________
      * SETLL, READ AND DELETE RECORDS FROM SAEF
      *"""""""""""""""""""""""""""""""""""""""""
     C     SRB           BEGSR
     C     SAEHK         SETLL     SAEF
     C     SAEHK         READE     SAEF                                   52
     C                   DOW       *IN52 = *OFF
     C                   WRITE     SADFRO
     C                   DELETE    SADFR
     C     SAEHK         READE     SAEF                                   52
     C                   END
     C                   ENDSR
      *_____________________________________________________
      * SETLL, READ AND DELETE RECORDS FROM EDIM2/EDIS/TVINF
      *"""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     SRC           BEGSR
     C     SAEHK         SETLL     EDIM2
     C     SAEHK         READE     EDIM2                                  52
     C                   DOW       *IN52 = *OFF
     C                   IF        M1001 = '830'
     C     MMN           SETLL     TVINF
     C     MMN           READE     TVINF                                  53
     C                   DOW       *IN53 = *OFF
     C                   DELETE    TVINFF
     C     MMN           READE     TVINF                                  53
     C                   END
     C     MSN           CHAIN     EDIS                               53
     C  N53              WRITE     EDISRO
     C  N53              DELETE    EDISR
     C                   WRITE     EDIMRO
     C                   DELETE    EDIMR
     C                   ELSE
     C                   UPDATE    EDIMR
     C                   END
     C     SAEHK         READE     EDIM2                                  52
     C                   END
     C                   ENDSR
N01   *_________________________________________
N01   * SETLL, READ AND DELETE RECORDS FROM SAEHOM, SAEOMT, SAEOMTF, SAEOMTH, SAEOMTV
N01   *"""""""""""""""""""""""""""""""""""""""""
N01  C     SROM          BEGSR
N01  C     SAEHK         SETLL     SAEOMTF
N01  C     SAEHK         READE     SAEOMTF                                52
N01  C                   DOW       *IN52 = *OFF
N01  C                   WRITE     SAEOMTFRO
N01  C                   DELETE    SAEOMTFR
N01  C     SAEHK         READE     SAEOMTF                                52
N01  C                   END
N01   *
N01  C     SAEHK         SETLL     SAEOMTH
N01  C     SAEHK         READE     SAEOMTH                                52
N01  C                   DOW       *IN52 = *OFF
N01  C                   WRITE     SAEOMTHRO
N01  C                   DELETE    SAEOMTHR
N01  C     SAEHK         READE     SAEOMTH                                52
N01  C                   END
N01   *
N01  C     SAEHK         SETLL     SAEOMTV
N01  C     SAEHK         READE     SAEOMTV                                52
N01  C                   DOW       *IN52 = *OFF
N01  C                   WRITE     SAEOMTVRO
N01  C                   DELETE    SAEOMTVR
N01  C     SAEHK         READE     SAEOMTV                                52
N01  C                   END
N01   *
N01  C     SAEHK         SETLL     SAEOMT
N01  C     SAEHK         READE     SAEOMT                                 52
N01  C                   DOW       *IN52 = *OFF
N01  C                   WRITE     SAEOMTRO
N01  C                   DELETE    SAEOMTR
N01  C     SAEHK         READE     SAEOMT                                 52
N01  C                   END
N01   *
N01  C     SAEHK         CHAIN     SAEHOM                             52
N01  C  N52              WRITE     SAEHOMRO
N01  C  N52              DELETE    SAEHOMR
N01   *
N01  C                   ENDSR
N01   *
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     SAEHK         KLIST
     C                   KFLD                    SEAVD
     C                   KFLD                    SETDN
      *_______________
      * PARAMETER LIST
      *"""""""""""""""
     C     *ENTRY        PLIST
     C                   PARM                    XAVD
     C     XDT2          PARM                    XDT
      *____________
      * WORK FIELDS
      *""""""""""""
     C     *LIKE         DEFINE    SEAVD         XAVD
     C     *LIKE         DEFINE    XDT2          XDT
      *____________
      * INIT FIELDS
      *""""""""""""
     C                   Z-ADD     XÅ            WÅ
     C                   Z-ADD     XM            WM
     C                   Z-ADD     XD            WD
     C                   IF        XÅ > 50
     C                   MOVEL     '19'          WÅ
     C                   ELSE
     C                   MOVEL     '20'          WÅ
     C                   ENDIF
      *
     C                   ENDSR
      *
