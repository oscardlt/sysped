     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      REF(*LIBL/TELLA)
     A                                      CF03(03 'Avslutt')
     A                                      PRINT
     A                                      HLPDOC('STD' 'HELPNU' 'HLP400')
     A                                      HELP
     A          R SUBFIL                    SFL
     A            TEAACC    R   10   H
     A            WSNR           1A  I  6  2VALUES(' ' '2' '4')
     A  98                                  DSPATR(RI)
     A  98                                  DSPATR(PC)
     A            TEAFNR         3Y 0O  6  4TEXT('FLYSELSKAPSNR.')
     A                                      EDTCDE(4)
     A            TEATAB         1   O  6  8
     A            XFSNAV        20A  O  6 10TEXT('FLYSELSKAPSNAVN')
     A            TEAFR1    R    9 00O  6 31EDTCDE(4)
     A            TEATI1    R    9 00O  6 41EDTCDE(4)
     A            TEAFR2    R    9 00O  6 51EDTCDE(4)
     A            TEATI2    R    9 00O  6 61EDTCDE(4)
     A            WSDIFF         9Y 0O  6 71EDTCDE(4)
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A  91N93                               CF05(05 'Frisk')
     A  91N93                               CF06(06 'LAGE NY')
     A  91N93                               CF08(08 'SØK')
     A  91N93                               CF12(12 'BAK')
     A            ZRECNO         4S 0O  2 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYSPED/SYFA42'
     A                                      COLOR(BLU)
     A            MSG001        40A  O  1 19MSGID(DIV 1574 SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 61DATE
     A                                      EDTCDE(Y)
     A                                  1 70TIME
     A            WSTXT         11A  O  2 19COLOR(WHT)
     A            MSG002        30A  O  3  2MSGID(DIV 0016 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG003        30A  O  4  2MSGID(DIV 0017 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG004        79A  O  5  1MSGID(DIV 1575 SYMSGF)
     A                                      DSPATR(HI)
     A          R BUNN
     A            MSG005        18A  O 23  2MSGID(DIV 0020 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG006        18A  O 23 21MSGID(DIV 0021 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG007        18A  O 23 40MSGID(DIV 0022 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG008        18A  O 23 60MSGID(DIV 0023 SYMSGF)
     A                                      COLOR(BLU)
     A  98                             24  2'INVALID OPTION'
     A                                      COLOR(WHT)
     A                                 24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A          R VARELI
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A            WSFNR          3Y 0B  2  8TEXT('FLYSELSKAPSNR.')
     A                                      DSPATR(UL)
     A  40                                  DSPATR(HI)
     A  40                                  DSPATR(PR)
     A N40                                  DSPATR(PC)
     A                                      EDTCDE(4)
     A  70                                  DSPATR(RI)
     A            WSFR1          9Y 0B  2 22DSPATR(UL)
     A  40                                  DSPATR(PC)
     A                                      EDTCDE(4)
     A  69                                  DSPATR(RI)
     A            WSTI1          9Y 0B  2 32DSPATR(UL)
     A                                      EDTCDE(4)
     A            WSFR2          9Y 0B  2 47DSPATR(UL)
     A                                      EDTCDE(4)
     A            WSTI2          9Y 0B  2 57DSPATR(UL)
     A                                      EDTCDE(4)
     A            MSG009        18A  O  3  2MSGID(DIV 0020 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG010        18A  O  3 22MSGID(DIV 0024 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG011        75   O  1  4MSGID(DIV 1576 SYMSGF)
     A                                      COLOR(WHT)
     A            WSTAB          1A  B  2 17
     A  30        MSGTXT        78A  O  4  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                              4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A            WSACC         10   B  2 68
     A          R VAREL2
     A                                      SLNO(21)
     A                                      CLRL(04)
     A            MSG043        12A  O  2  2MSGID(DIV 0026 SYMSGF)
     A            W2FNR          3S 0I  2 16DSPATR(UL)
     A                                      DSPATR(PC)
     A            MSG012        18A  O  3  2MSGID(DIV 0020 SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A          R VAREL3
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A            MSG013        75A  O  1  3MSGID(DIV 1576 SYMSGF)
     A                                      COLOR(WHT)
     A            TEAFNR    R    3Y00O  2  8DSPATR(UL)
     A                                      DSPATR(PC)
     A                                      DSPATR(HI)
     A            TEATAB         1A  O  2 16
     A            TEAFR1    R    9 00B  2 22DSPATR(UL)
     A                                      DSPATR(HI)
     A            TEATI1    R    9 00B  2 33DSPATR(UL)
     A                                      DSPATR(HI)
     A            TEAFR2    R    9 00B  2 47DSPATR(UL)
     A                                      DSPATR(HI)
     A            TEATI2    R    9 00B  2 58DSPATR(UL)
     A                                      DSPATR(HI)
     A            MSG030        18A  O  3  3MSGID(DIV 0024 SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A          R NYWRK
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                  1  2'SYSPED/SYFA42'
     A                                      COLOR(BLU)
     A            MSG021        40A  O  1 19MSGID(DIV 1574 SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                  1 70TIME
     A            WSTXT         11A  O  2 19COLOR(WHT)
     A            MSG022        30A  O  3  2MSGID(DIV 0016 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG023        30A  O  4  2MSGID(DIV 0017 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG024        79A  O  5  1MSGID(DIV 1575 SYMSGF)
     A                                      DSPATR(HI)
     A            MSG025        18A  O 23  2MSGID(DIV 0020 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG026        18A  O 23 21MSGID(DIV 0021 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG027        18A  O 23 40MSGID(DIV 0022 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG028        18A  O 23 60MSGID(DIV 0023 SYMSGF)
     A                                      COLOR(BLU)
     A                                 24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A          R BILDE01
     A                                      CF12(12 'Avslutt program')
     A            MSGNR          7A  P
     A                                  1  2'SYSPED/SYFA42'
     A                                      COLOR(BLU)
     A                                  1 20MSGCON(045 DIV1574 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 67DATE
     A                                      EDTCDE(Y)
     A                                  2 67TIME
     A                                  9 20MSGCON(008 SHU0639 *LIBL/SYMSGF)
     A            WSPROD         1   B  9 30
     A                                  9 35MSGCON(012 SHU0640 *LIBL/SYMSGF)
     A                                 10 35MSGCON(012 SHU0641 *LIBL/SYMSGF)
     A                                 11 35MSGCON(012 SHU0642 *LIBL/SYMSGF)
     A                                 12 35MSGCON(040 SHU0643 *LIBL/SYMSGF)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                             24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
