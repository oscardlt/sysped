     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPDOC('STD' 'HELPNU' 'HLP400')
     A                                      HELP
     A                                      ALTHELP
      *
     A          R BILDE
     A                                      CF01(01 'BYTT AVDELINGER')
     A                                      CF03(03 'Avslutt program')
     A                                      CF12(12 'Gå til forrige bilde (SADH-
     A                                      VARE)')
     A                                  1 29'SAD-transitt'
     A                                      COLOR(WHT)
     A                                  3 11'Avdeling'
     A            SEAVD          4Y 0O  3 20TEXT('AVDELING')
     A                                      EDTCDE(4)
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A                                  3 28'Tolldek.nr.'
     A            SETDN          7Y 0O  3 40TEXT('Tolldeklarasjonsnr')
     A                                      EDTCDE(4)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  3 49'Tarifør'
     A            SESG           3A  O  3 57TEXT('Signatur')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  4 11'Registreringsdato'
     A            WSDT           6Y 0O  4 29TEXT('Registreringsdato')
     A                                      DSPATR(UL)
     A                                      EDTCDE(Y)
     A                                      COLOR(YLW)
     A                                  6  9'2.Avsender'
     A            SEKNK          8Y 0O  6 26TEXT('Kundenr Kjøper')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            SENAK         30A  O  6 35TEXT('Navn Kjøper')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  7 11'Reg.nr.'
     A            SERG          11A  O  7 26TEXT('Regn Fylk.skatt.sjef')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  8  9'8.Mottaker'
     A            SEKNS          8Y 0O  8 26TEXT('Kundenr Selger')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            SENAS         30A  O  8 35TEXT('Navn Selger')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  9  8'50.Hovedansvarlig'
     A            SEKNA          8Y 0O  9 26TEXT('HOVEDANSVARLIG')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            SENAA         30A  O  9 35TEXT('Navn ANSVARLIG')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 11  9'6.Kolli'
     A            SENTK          7Y 0O 11 30TEXT('Antall kolli')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 12 11'Brutto vekt'
     A            SEVKB          9Y 0O 12 30TEXT('Bruttovekt')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 18  8'Transitteringsnr.'
     A            SECTN          5S 0O 18 26DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 18 38'Dato'
     A            WSCDT          6Y 0O 18 43DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(Y)
     A                                 18 59'Tidsfrist'
     A            WSDDT          6Y 0O 18 69DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(Y)
     A                                 20  8'Godgjenelsesdato'
     A            SEDTG         10A  O 20 25TEXT('Dekl. godkj.dato')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 20 38'Løpenr.'
     A            SETLL         10A  O 20 46TEXT('Tollnr Løpenr')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 20 59'Eksp.sted'
     A            SETLE          6A  O 20 69TEXT('Tollnr Ekspsted')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 23  2MSGCON(078 DIV1952 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
