********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
11XXX * 01 sh  PTF11 full priis på høydetillegg ved Express
11XXX * 02 JOV PTF11 skille på fkatturamottaker/fraktbet i fro m av avtaleoppslag
11XXX * 03 JOV PTF11 Ved tillegg som høyde/farlig gods mm - MOMSKODE etter fraktlinja
********************************************************************
     FFAKTIL    IF   E           K DISK
     FHEADF     IF   E           K DISK
     FFAKT      UF A E           K DISK
     FDOKUFV    IF   E           K DISK
     FDOKUFVF   IF   E           K DISK
     FDOKUFVG   UF A E           K DISK
     FCUNDL01   IF   E           K DISK
     FFIRM      IF   E           K DISK
      *
     D                 DS
     D  HXHEAD                 1    256
     D  HEUR                   1      1
     D  HEFR                   2      4
     D  HEKDPL                 5      5
     D  HELKA                  6      7
     D  HESDF                  8     12
     D  HETRI                 13     14
     D  HESDT                 15     19
     D  HELKS                 20     21
     D  HESDFF                22     26
     D  HELKK                 27     28
     D  HESDVT                29     33
     D  XXVKT                 34     42  0
     D  XXFBV                 43     51  0
     D  XXM3                  52     58  3
     D  XXLM                  59     62  2
     D  XXNT                  63     69  0
     D  XXVERV                70     72
     D  XXVERB                73     75  2
N01  D                 DS
N01  D  FTG                    1      5
N01  D                                     DIM(5)
N01  D  HESTN1                 1      1
N01  D  HESTN2                 2      2
N01  D  HESTN3                 3      3
N01  D  HESTN4                 4      4
N01  D  HESTN5                 5      5
     D  WTGX                   1      5
     c                   EXSR      INIT
      * Sjekk om oppdrag er fakturert - skipp i så fall
     C                   EXSR      Fakturert
     c                   if        Skipp =  'J'
     c                   goto      SLUTT
     c                   endif
      *
     C                   EXSR      DelAuto
     C                   EXSR      CrtAuto
      *
     C     Slutt         tag
     C                   seton                                        LR
     C                   return
      *
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     *entry        PLIST
     C                   PARM                    UUAVD             4 0
     C                   PARM                    UUOPD             7 0
      *
     C                   z-add     UUAVD         HEAVD
     C                   z-add     UUOPD         HEOPD
     C     HeadKey       KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C     FakKey        KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C                   KFLD                    FGFALI
N03   * hent dft momskode fra fakt linje nr 0 = Hovedfrakten
N03  c     FakKey        chain(N)  FAKT                               33
N03  c  N33              move      FAKDM         FRAKTkdm          1
     C     DokufvKey     KLIST
     C                   KFLD                    FVRI
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C                   KFLD                    FVFBNR
     C                   MOVE      'F'           FVRI
     C                   Z-ADD     1             FVFBNR
     C     DokufvGKey    KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C                   KFLD                    FVFBNR
     C     FTKey         KLIST
     C                   KFLD                    FTKUND
     C                   KFLD                    FTNIV
     C                   move      'L'           FTNIV
     C     LinKey        KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C                   KFLD                    FVFBNR
     C                   KFLD                    FVLINR
     C     LinKey2Max    KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C                   KFLD                    FVFBNR
     C                   KFLD                    FVLINR
     C                   KFLD                    maxSub
     C                   z-add     99            maxSub            2 0
     C     KunKey        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    FraktBet
      *
     c     *loval        setll     FIRM
     c                   read      FIRM
      *
     C                   ENDSR
      *
     C***********************************************
     C     Fakturert     BEGSR
     C***********************************************
     c                   move      'N'           Skipp             1
     C     HeadKey       setll     FAKT
     C     HeadKey       reade(N)  FAKT                                   34
     C     *in34         doweq     '0'
     c                   if        FAOPKO > 'C'
     c                   move      'J'           Skipp
     c                   leave
     c                   endif
     C     HeadKey       reade(N)  FAKT                                   34
     C                   enddo
     C                   ENDSR
     C***********************************************
     C     DelAuto       BEGSR
     C***********************************************
      * slett autoskapte linjer
     C     DokufvGKey    setll     DOKUFVG
     C     DokufvGKey    reade     DOKUFVG                                34
     C     *in34         doweq     '0'
     c                   if        FGKDA = 'A'
     c                   delete    DOKUFVGR
     c                   if        FGFALI <> 0
     c     FakKey        chain     FAKT                               33
     c  N33              delete    FAKTR
     c                   endif
     c                   else
     c                   update    DOKUFVGR
     c                   endif
     C     DokufvGKey    reade     DOKUFVG                                34
     C                   enddo
     C                   ENDSR
     C***********************************************
     C     CrtAuto       BEGSR
     C***********************************************
      *
     c* Finn fraktbet i oppdraget.
     c*   Om det finnes minst en styrings-Record på denne foventes alt å være på kundenivå
     c                   move      *zeros        FraktBet          8 0
N02  c                   move      *zeros        FraktBetF         8 0
     c     HeadKey       chain     HEADF                              33
N01  c* ekspress
N01  c     'E'           scan      WTGX                                   35
n01  c   35              move      'J'           ekspress          1
N01  c*
     c                   if        TRKDFF = 'S'
     c                   eval      FraktBet = HEKNSF
     c                   else
     c                   eval      FraktBet = HEKNKF
     c                   endif
N02  c                   move      FraktBet      FraktBetF
     c     KunKey        chain     Cundl01                            33
     c                   if        *in33=*off and AKNRKU <> 0
     c                   eval      FraktBet = AKNRKU                            Avtalekundenr ??
     c                   endif
     c                   eval      FTKUND = FraktBet
     c     FTKey         chain     FAKTIL                             33
     c   33              move      *zeros        FTKUND
      *
     c     FTKey         setll     FAKTIL
     c     FTKey         reade     FAKTIL                                 34
     c     *in34         doweq     '0'
     c                   select
     c                   when      %subst(FTTYPE:1:5)='HØYDE'
     c                   exsr      TstHoyde
     c                   when      %subst(FTTYPE:1:3)='ADR'
     c                   exsr      TstADR
     c                   endsl
     c     FTKey         reade     FAKTIL                                 34
     c                   enddo
     c                   ENDSR
     C***********************************************
     C     TstHoyde      BEGSR
     C***********************************************
     C                   move      *blanks       XXVT             20
     C     DokufvKey     setll     DOKUFV
     C     DokufvKey     reade     DOKUFV                                 34
     C     *in34         doweq     '0'
     c                   if        FVHOY > FTMAX
     c                   move      FVLINR        XXLN              2
     C                   eval      XXVT ='Lin '+ xxLN + ' Høyde='
     c                                    + %trimr(%editc(FVHOY:'Z')) +' cm'
     c                   exsr      CrtTilLin
     c                   endif
     C     DokufvKey     reade     DOKUFV                                 34
     C                   enddo
     C                   ENDSR
     C***********************************************
     C     TstAdr        BEGSR
     C***********************************************
     C                   move      *blanks       XXVT             20
     C     DokufvKey     setll     DOKUFV
     C     DokufvKey     reade     DOKUFV                                 34
     C     *in34         doweq     '0'
     C     Linkey        setll     DOKUFVF
     C     Linkey        reade     DOKUFVF                                34
     C     *in34         doweq     '0'
     c                   movel     FFSEDD        ADKL              1
     c     ADKL          ifeq      *blanks                                      Noen få mangler.
     c                   movel     '7'           ADKL                           fleste=7-Radioaktiv
     c                   endif
     c     ADKL          scan      FTLFK                                  34
     c                   if        *in34 = *on
     c                   move      FVLINR        XXLN              2
     C                   eval      XXVT ='Lin '+ xxLN + ' ADR-klasse='+ADKL
     c                   exsr      CrtTilLin
     c                   endif
     C     Linkey        reade     DOKUFVF                                34
     C                   enddo
     C     DokufvKey     reade     DOKUFV                                 34
     C                   enddo
     C                   ENDSR
     C***********************************************
     C     CrtTilLin     BEGSR
     C***********************************************
     C                   MOVE      *ZEROS        XXVERB
     C                   MOVE      *ZEROS        N5                5 0
     C                   MOVE      *ZEROS        NAVD              4 0
     C                   MOVE      *ZEROS        NOPD              7 0
     C                   MOVE      *ZEROS        uavnr
N01  C*
      * finn kolliets/linjens  pris
      * ved ekspress sett lm til 12.99
     C                   move      *zeros        XXFBV
     C                   Z-ADD     FVANT         XXNT
     C                   Z-ADD     FVVKT         XXVKT
     C                   Z-ADD     FVVOL         XXM3
     C                   Z-ADD     FVLM          XXLM
      * høydetillegg av fulload ved ekspress
N01  C     ekspress      ifeq      'J'
N01  c                   if        %subst(FTTYPE:1:5)='HØYDE'
N01  c                   z-add     12.99         xxlm
N01  c                   end
N01  c                   end
      * høydetillegg av fulload ved ekspress
N01  C     ekspress      ifeq      'J'
N01  c                   if        %subst(FTTYPE:1:3)='ADR'
N01  c                   z-add     12.99         xxlm
N01  c                   end
N01  c                   end
     C                   Z-ADD     FraktBet      UUKN              8 0
     C                   MOVE      *BLANKS       UUVAL             3
     C                   MOVE      'FRA'         UGEB              3
     C                   MOVE      'FRA'         UGEBKO            3
     C                   MOVE      *ZEROS        UUBELØ           13 2
     C                   MOVE      HXHEAD        UHEAD           256
     C                   CALL      'SYGE14'
     C                   PARM                    UGEBKO
     C                   PARM                    UGEB
     C                   PARM                    NAVD
     C                   PARM                    NOPD
     C                   PARM                    UUKN
     C                   PARM                    UUVAL
     C                   PARM                    UUBELØ
     C                   PARM                    UAVNR             9 0
     C                   PARM                    UAVMSG           50
     C                   PARM                    USKA              1
     C                   PARM                    UHEAD
      * brt
     C     UUBELØ        ifeq      *zeros
     C                   MOVE      *ZEROS        XXFBV
     C                   MOVE      *ZEROS        NAVD
     C                   MOVE      *ZEROS        N5
     C                   MOVE      *ZEROS        UUBELØ           13 2
     C                   MOVE      *BLANKS       USONE
     C                   MOVE      *BLANKS       UUVAL
     C                   MOVE      *ZEROS        URABAT
     C                   MOVE      'FRA'         UGEB
     C                   MOVE      HXHEAD        UHEAD           256
     C                   CALL      'SYGE13'
     C                   PARM                    UGEB
     C                   PARM                    NAVD
     C                   PARM                    NOPD
     C                   PARM                    UUVAL
     C                   PARM                    UUBELØ
     C                   PARM                    USONE             5
     C                   PARM                    URABAT            5 2
     C                   PARM                    N5                5 0
     C                   PARM                    N5                5 0
     C                   PARM                    N5                5 0
     C                   PARM                    N5                5 0
     C                   PARM                    UUKN
     C                   PARM                    UHEAD
     C                   PARM                    atdum             1
     C                   ENDIF
     c
      * legg ut linje i FAKT
     c                   CLEAR                   FAKTR
     C                   MOVE      TRKDFF        FASK
     C                   MOVE      FVAVD         FAAVD
     C                   MOVE      FVOPD         FAOPD
     C                   MOVE      FTLVK         FAVK
     C                   MOVE      FICURR        FAVAL
S02  C*                  MOVE      FraktBet      FAKUNR
N02  C                   MOVE      FraktBetF     FAKUNR
S03  C*                  MOVE      'J'           FAKDM
N01  c                   eval      FAKDM = FRAKTkdm
     C                   MOVE      'M'           FAKDA
     C                   eval      FABELV = UUBELØ * FTLPRO / 100
     C                   eval      FABELN = FABELV + 0.50
     c                   move      00            FABELN                         avrund hel krone
     C                   move      XXVT          FAVT
     C                   CALL      'SYGE06'
     C                   PARM                    FAAVD
     C                   PARM                    FAOPD
     C                   PARM                    FALI
     C                   WRITE     FAKTR
      * legg ut linje i DOKUFVG
     C                   MOVE      *ZEROS        FGLIN2
     C     LinKey2Max    SETGT     DOKUFVG
     C     LinKey        READPE(N) DOKUFVG                                33
     C                   eval      FGLIN2=FGLIN2+1
     C                   MOVE      FVAVD         FGAVD
     C                   MOVE      FVOPD         FGOPD
     C                   MOVE      FVFBNR        FGFBNR
     C                   MOVE      FVLINR        FGLINR
     C                   MOVE      FALI          FGFALI
     C                   MOVE      TRKDFF        FGSKX
     C                   MOVE      FTLVK         FGVK
     C                   z-add     FABELV        FGBELV
     C                   MOVE      FICURR        FGVAL
s03  C*                  MOVE      'J'           FGKDM
N03  c                   eval      FGKDM = FRAKTkdm
     C                   z-add     FABELN        FGBELN
     C                   move      XXVT          FGVT
     c                   MOVE      'A'           FGKDA
     c                   write     DOKUFVGR
      *
     C                   ENDSR
