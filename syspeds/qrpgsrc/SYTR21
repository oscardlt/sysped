********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 SH  PTF10 sjekk at gebyrkode er opprettet som gebyr
********************************************************************
      *
      **************************************************************
      *** PROGRAM SKAL VEDLIKEHOLDE UNITTYPER                    ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER       ***
      *** LEDIGE INDIKATORER 01 02 04 07 09-11 13-24             ***
      *** LEDIGE INDIKATORER 27-29 31 32 34-39 41-88 95-97       ***
      **************************************************************
      *
N01  FKODTGE    IF   E           K DISK
     FBILK      UF A E           K DISK
     FSYTR21FM  CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D                 DS
     D  BKTXT                  1     20
     D  XDLT                   1      7
     C*
     C                   EXSR      INIT
     C*
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
     C                   MOVE      W2KODE        YSIG
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     KODSI1        SETLL     BILK
     C     START1        TAG
     C                   READ      BILK                                   94
     C  N94              UPDATE    BILKR
     C  N94              MOVE      BKKODE        YSIG
      *
     C     START2        TAG
      *
     C  N94              DO
     C                   DO        14
     C                   ADD       1             ZRECNO
     C                   WRITE     SUBFIL
     C                   READ      BILK                                   94
     C  N94              UPDATE    BILKR
     C  N94              MOVE      BKKODE        YSIG
     C  N94              END
     C                   END
     C*
     C                   Z-ADD     ZRECNO        YRECNO            5 0
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     5             POSNBR
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYWRK
     C                   ELSE
     C                   WRITE     SUBCTL
     C                   EXFMT     BUNN
     C                   END
     C*
     C***
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C                   Z-ADD     WSSUBN        XRECNO            4 0
     C     XRECNO        IFEQ      0
     C                   Z-ADD     ZRECNO        XRECNO            4 0
     C                   END
     C***
     C*
     C                   SETOFF                                       9899
     C*
     C*** AVSLUTT
     C   03              GOTO      SLUTT
     C*
     C*** FRISK OPP
     C   05              GOTO      START
     C*
     C*** LAGE NY
     C   06              DO
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     8             POSNBR
     C                   MOVE      *BLANKS       WSTXT
     C                   GOTO      START5
     C                   END
     C*** SØK FRA
     C   08              GOTO      START6
     C*
     C*** BLA VIDERE
     C   25              DO
     C                   SETOFF                                       25
     C                   Z-ADD     YRECNO        ZRECNO
     C  N94KODSI3        SETLL     BILK
     C  N94              READ      BILK                                   94
     C  N94              UPDATE    BILKR
     C                   GOTO      START2
     C                   END
     C*
     C     ZRECNO        IFNE      0
     C                   EXSR      HENT
     C                   END
     C   51UUKODE        IFNE      *BLANKS
     C                   GOTO      SLUTT
     C                   END
     C   98XRECNO        IFNE      0
     C                   Z-ADD     XRECNO        ZRECNO
     C                   END
     C                   GOTO      START3
     C*
     C*** LAGE NY
     C     START5        TAG
     C                   EXFMT     VARELI
     C                   SETOFF                                       309089
     C   12              EXSR      MOVB2
     C  N12              EXSR      TESTB1
     C  N12
     CAN 30              GOTO      START5
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     5             POSNBR
     C                   GOTO      START3
     C*
     C*** SØK FRA
     C     START6        TAG
     C                   EXFMT     VAREL2
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     5             POSNBR
     C   12              GOTO      START3
     C                   GOTO      START
     C*
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
     C*
     C***********************************************
     C     TESTB1        BEGSR
     C***********************************************
     C  N40              DO
     C     WSTXT         COMP      *BLANKS                                90
     C   90              MOVE      'DIV4011'     MSGNR
     C  N90KODSIK        CHAIN     BILK                               33
     C  N90
     CANN33              DO
     C                   UPDATE    BILKR
     C                   SETON                                        90
     C                   MOVE      'DIV4012'     MSGNR
     C                   END
     C   90              Z-ADD     8             POSNBR
     C   90              SETON                                        30
     C                   END
     C*
     C     WSTXT         COMP      *BLANKS                                89
     C   89
     CANN30              MOVE      'DIV4013'     MSGNR
     C   89
     CANN30              Z-ADD     15            POSNBR
     C   89              SETON                                        30
     C*
N01  C  N30              DO
N01  c                   movel     wskode        kgekod
N01  C     kodtgek       chain     kodtge                             90
N01  C   90              MOVE      'SHU1310'     MSGNR
N01  C   90              seton                                        30
N01  C   90              Z-ADD     8             POSNBR
N01  C                   end
     C*
     C   30              GOTO      SLTB
     C*
     C*** OPPDATERE PÅ REGISTER OG SUBFIL
     C     KODSIK        CHAIN     BILK                               33
     C                   EXSR      MOVWSI
     C  N33              UPDATE    BILKR
     C   33              WRITE     BILKR
     C   40              UPDATE    SUBFIL
     C  N40              ADD       1             YRECNO
     C  N40              Z-ADD     YRECNO        ZRECNO
     C  N40              WRITE     SUBFIL
     C                   WRITE     SUBCTL
     C                   EXSR      MOVB2
     C*
     C     SLTB          ENDSR
     C*
     C***********************************************
     C     HENT          BEGSR
     C***********************************************
     C                   Z-ADD     0             LINNBR
     C                   SETOFF                                       919299
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C   98              DO
     C   99              SETOFF                                       98
     C                   Z-ADD     1             LINNBR
     C                   GOTO      SLHENT
     C                   END
     C     ZRECNO        DIV       14            LINNBR
     C                   MVR                     XLIN              2 0
     C     XLIN          IFEQ      0
     C                   Z-ADD     14            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C   51WSNR          IFEQ      '1'                                          PLUKK
     C                   MOVE      ' '           WSNR
     C                   MOVE      BKKODE        UUKODE
     C                   GOTO      SLHENT
     C                   END
     C     WSNR          IFEQ      '2'                                          ENDRE
     C                   MOVE      ' '           WSNR
     C     XDLT          COMP      '*DELETE'                              98
     C   98              GOTO      SLHENT
     C                   EXSR      CHGSI
     C   12              GOTO      SLHENT
     C                   END
     C     WSNR          IFEQ      '4'                                          SLETTE
     C                   MOVE      ' '           WSNR
     C     XDLT          COMP      '*DELETE'                              98
     C   98              GOTO      SLHENT
     C                   EXSR      DLTSI
     C   12              GOTO      SLHENT
     C                   END
     C                   END
     C*
     C     SLHENT        TAG
     C                   ADD       5             LINNBR
     C                   Z-ADD     5             POSNBR
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     DLTSI         BEGSR
     C***********************************************
     C                   SETON                                        99
     C                   MOVE      BKKODE        WSKODE
     C                   EXFMT     VAREL3
     C  N12              DO
     C     KODSIK        CHAIN     BILK                               33
     C  N33              DELETE    BILKR
     C                   EXSR      MOVB1
     C                   END
     C     XLIN          IFEQ      0
     C                   Z-ADD     14            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C                   UPDATE    SUBFIL
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     CHGSI         BEGSR
     C***********************************************
     C                   EXSR      MOVSIW
     C                   SETON                                        99
     C     STCHG         TAG
     C                   EXFMT     VARELI
     C                   SETOFF                                       309089
     C   12              DO
     C                   UPDATE    SUBFIL
     C                   EXSR      MOVB2
     C                   END
     C  N12              EXSR      TESTB1
     C  N12
     CAN 30              GOTO      STCHG
     C     XLIN          IFEQ      0
     C                   Z-ADD     14            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVB1         BEGSR
     C***********************************************
     C                   MOVE      *BLANKS       BKKODE
     C                   MOVE      *BLANKS       BKTXT
     C                   MOVEL     '*DELETE'     XDLT
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVB2         BEGSR
     C***********************************************
     C                   SETOFF                                       40
     C                   MOVE      *BLANKS       WSKODE
     C                   MOVE      *BLANKS       WSTXT
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVSIW        BEGSR
     C***********************************************
     C                   MOVE      BKKODE        WSKODE
     C                   MOVE      BKTXT         WSTXT
     C                   SETON                                        40
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     15            POSNBR
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVWSI        BEGSR
     C***********************************************
     C   33              DO
     C                   MOVE      WSKODE        BKKODE
     C                   END
     C*
     C                   MOVE      WSTXT         BKTXT
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     *ENTRY        PLIST
     C     UUKODE        PARM      UUKODE        UUKODE            3
     C     SOK           PARM      SOK           SOK               1
     C     SOK           COMP      'J'                                    51
     C     KODSIK        KLIST
     C                   KFLD                    WSKODE
     C     KODSI1        KLIST
     C                   KFLD                    W2KODE
     C     KODSI3        KLIST
     C                   KFLD                    YSIG              3
N01  C     KODTGEK       KLIST
N01  C                   KFLD                    kgeuni
N01  C                   KFLD                    kgekod
N01  c                   move      'GE'          kgeuni
     C*
     C*
     C                   ENDSR
     C*
