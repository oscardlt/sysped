********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 YBC PTF10 TOLLSTEDSKODE ER ENDRET TIL 4.0
xxxxxx*xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
11XXX * 02 YBC PTF11 KUN ENDRE STATUS FRA 'E' ELLER BLANK
11XXX * 03 YBC PTF11 TILLATE NEGATIV OPPDRAGSNR
********************************************************************
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HELP(01)
     A                                      CF01(01)
      *
     A          R BILDE01
     A                                      CF03(03 'Avslutt program.')
     A                                  2  7'SAD029'
     A                                      COLOR(BLU)
     A                                  4 23'Endre status på SAD-eksport'
     A                                      COLOR(WHT)
     A                                  8 23'Avdeling:'
     A            WSAVD          4Y 0B  8 40DSPATR(UL)
     A  99                                  DSPATR(RI)
     A  99                                  DSPATR(PC)
     A                                      EDTCDE(4)
     A                                 10 23'Signatur:'
     A            WSSG           3A  B 10 40TEXT('Signatur')
     A                                      DSPATR(UL)
     A  98                                  DSPATR(RI)
     A  98                                  DSPATR(PC)
     A  98                                  ERRMSG('Tastet tariffør er ikke reg-
     A                                      istrert.' 98)
     A                                 12 23'Oppdragsnr.'
     A            WSOPD          7Y 0B 12 40TEXT('Oppdragsnummer')
     A                                      DSPATR(UL)
     A  99                                  DSPATR(RI)
S03  A*                                     EDTCDE(4)
N03  A                                      EDTCDE(M)
     A                                 15 23'Ny status:'
     A            WSST           1A  B 15 40TEXT('Status ny')
     A                                      DSPATR(UL)
     A                                 23 23'F3 = Avslutt'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R BILDE02
S02  A*                                     CF05(05 'Endre status')
N02  A N28                                  CF05(05 'Endre status')
S03  A*                                     CF07(07 'Fjern innstikksmerke')
S03  A*                                     CF09(09 'Sett inn innstikksmerke')
N03  A N29                                  CF07(07 'Fjern innstikksmerke')
N03  A N29                                  CF09(09 'Sett inn innstikksmerke')
     A                                  1 10'TOLLDEKLARASJON'
     A                                      COLOR(WHT)
     A                                  1 41'1.Dekl.'
     A                                  1 59'4.L.liste'
     A            SEDTY          2A  O  1 49TEXT('Deklarasjonstype')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SEDP           2S 0O  1 52TEXT('Dekl.prosedyre')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  2  2'2.Avsender'
     A            SEKNK          8Y 0O  2 13TEXT('Kundenr Kjøper')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  2 23'Regnr'
     A            SERG          11A  O  2 29TEXT('Regn Fylk.skatt.sjef')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  2 54'6.Kolli'
     A            SENTK          7Y 0O  2 62TEXT('Antall kolli')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  2 70'S/K'
     A            SESKI          1A  O  2 74TEXT('Selg,Kjøper,Ingen')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  2 76'D/K'
     A            SEKDDK         1A  O  2 80TEXT('Dagsoppgjør/Kontant')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SENAK         30A  O  3  2TEXT('Navn Kjøper')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  3 41'Brt vekt'
     A            SEVKB          9Y 0O  3 50TEXT('Bruttovekt')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  3 61'Ref'
     A            SEAVD          4Y 0O  3 65TEXT('AVDELING')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            SETDN          7Y 0O  3 70TEXT('Tolldeklarasjonsnr')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            SESG           3A  O  3 78TEXT('Signatur')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SEADK1        30A  O  4  2TEXT('Adresse-1 Kjøper')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  4 40'15.Avs/utf.land'
     A            SELKA          2A  O  4 56TEXT('Landk avs/utf-land')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SELKAT        17A  O  4 59TEXT('F.T. AVS/UTF.LAND')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SEADK2        30A  O  5  2TEXT('Adresse-2 Kjøper')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  5 40'17.Be.land'
     A            SELKB          2A  O  5 56TEXT('LANDK TIL B.LAND')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SEFTB         17A  O  5 59TEXT('FRITEKST TIL B.LAND')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SEADK3        30A  O  6  2TEXT('Adresse-3 Kjøper')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  7  2'8.Mottaker'
     A            SEKNS          8Y 0O  7 13TEXT('Kundenr Selger')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  7 40'19.Cont.'
     A            SEKDC          1A  O  7 49TEXT('Container 0/1')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  7 52'20.Lev.'
     A            SELV           3A  O  7 60TEXT('Leveringsvilkår kod')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SELVT         17A  O  7 64TEXT('Leveringsvilkår txt')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SENAS         30A  O  8  2TEXT('Navn Selger')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  8 40'21.Ident.'
     A            SETRID        17A  O  8 50TEXT('Transport ID')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SELKT          2A  O  8 68TEXT('Landk Tr.middel')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SEADS1        30A  O  9  2TEXT('Adresse-1 Selger')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  9 40'22.Faktura'
     A            SEVAL1         3A  O  9 51TEXT('Val.kode Faktsum')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SEBEL1        13Y 2O  9 55TEXT('Beløp Faktsum')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  9 70'23'
     A            SEVKU          7Y 3O  9 73TEXT('Valutakurs')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            SEADS2        30A  O 10  2TEXT('Adresse-2 Selger')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 10 40'A.kostnad'
     A            SEVAL2         3A  O 10 51TEXT('Valutakode A.KOST')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SEBEL2        11Y 2O 10 55TEXT('Beløp A. kost')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            SEFTG2         1A  O 10 68TEXT('Fortegn +/- a.kost')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 10 70'24.Tr.typ'
     A            SETST          1S 0O 10 80TEXT('Transaksjonstype')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SEADS3        30A  O 11  2TEXT('Adresse-3 Selger')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 11 35'25.Tr.måte'
     A            SETRM          2S 0O 11 46TEXT('Transportmåte')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
S01  A*                                11 49'29.Utpass'
S01  A*           SEKDH          2S 0O 11 59TEXT('Havnekode')
N01  A                                 11 49'29.Utpa'
N01  A            SEKDH          4S 0O 11 57TEXT('Havnekode')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SEKDFT        17A  O 11 62TEXT('FRITEKST TOLLSTED')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 12  1'14.Deklarant'
     A            SETLF         12A  O 12 14TEXT('Telefon Deklarant')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 12 35'28.Finans'
     A            WSFID          6S 0O 12 46DSPATR(UL)
     A                                      COLOR(YLW)
     A            SEFIF         17A  O 12 53DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 13 35'30.Lag.sted'
     A            SENAD         30A  O 13  2TEXT('Navn Deklarant')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SELS          16A  O 13 47TEXT('Lagringssted tekst')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SEKDLS         2A  O 13 64TEXT('Lagringssted kode')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 14  2'Fritekst ved dataliste'
     A                                      COLOR(WHT)
     A                                 14 32'48.Konto'
     A            SEKTA          5S 0O 14 41TEXT('KONTONR')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SEKTB          2S 0O 14 47TEXT('KONTONR')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 14 50'49.Godsn'
     A            SEGN          15A  O 14 59TEXT('Godsnummer')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SEPOS          8A  O 15 59TEXT('POSISJON')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 15  1'31.'
     A            SEFT1         45A  O 15  5TEXT('Fritekst-1')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SEFT2         45A  O 16  5TEXT('Fritekst-2')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 17  1'44.'
     A            SEFT3         45A  O 17  5TEXT('Fritekst-3')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 18 39'52.Garanti'
     A            SEGFT1        30A  O 18 50TEXT('GARATI FRITEKST-1')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SEGKD          1S 0O 19 40TEXT('GARATIKODE')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SEGFT2        30A  O 19 50TEXT('GARATI FRITEKST-2')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 21 39'54.Sted'
     A            SEDST         15A  O 21 47TEXT('Deklarantens sted')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 21 63'Reg.dato'
EV5  A            SEDT           8Y 0H
NV5  A            WSDT           6Y 0O 21 72TEXT('Registreringsdato')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(Y)
     A                                 22  1'Dato'
     A            SEDTG         10A  O 22  6TEXT('Dekl. godkj.dato')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            SETARF        16A  O 22 63TEXT('TARIFFØRSNAVN')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 22 17'Løpenr'
     A            SETLL         10A  O 22 24TEXT('Tollnr Løpenr')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 22 38'Eksp.'
     A            SETLE          6A  O 22 44TEXT('Tollnr Ekspsted')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 22 54'Tariffør'
     A                                 23  1'Gammel status'
     A                                      COLOR(TRQ)
     A            SEST           1A  O 23 15TEXT('Status')
     A                                      DSPATR(UL)
     A                                      COLOR(PNK)
     A                                 23 18'Ny status'
     A                                      COLOR(TRQ)
     A            WSST           1A  O 23 28TEXT('Status')
     A                                      DSPATR(UL)
     A                                      COLOR(RED)
     A                                 23 33'Innstikk'
     A                                      COLOR(TRQ)
     A            SEMI           1A  O 23 42TEXT('Status')
     A                                      DSPATR(UL)
     A                                      COLOR(RED)
S02  A*                                24  2'F5=Endre status   F7=Fjerne innsti-
S03  A*N28                             24  2'F5=Endre status   F7=Fjerne innsti-
S03  A*                                     kkmerk   F9=Sette innstikkmerk'
N03  A N28                             24  2'F5=Endre status'
     A                                      COLOR(BLU)
S03  A* 28                             24  2'F7=Fjerne innstikkmerk   F9=Settei-
N03  A N29                             24  2'F7=Fjerne innstikkmerk   F9=Settei-
N02  A                                      nnstikkmerk'
N02  A                                      COLOR(BLU)
