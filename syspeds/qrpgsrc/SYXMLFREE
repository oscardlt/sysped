      * VISER HVORDAN EN MED BRUK AV RPG free FÅR MER OVERSIKTLIG KODE
      * NÅR LANGE TEKSTSTRENGER SKAL BYGGES..
      * (CL KOPIERER SÅ INNHOLD TIL IFS-FIL  /SYSPEDDEV/TEMP/XMLTEST.XML FOR Å SE I IEXPLORE)
      *
      * OBS! BIBLIOTEK LIBHTTP INNEHOLDER MYE SNADDER  VEDR XML (INN)  MM . (Scott Klement )
      *
      *
     FHEADF     IF   E           K DISK
      * Fila QTEMP/Q vil vel være EDESE1K i en reell situasjon...
     FQ         O  A F 1024        DISK
      *
     c                   move      *blanks       DATA           1024
      *
     c     KEY           KLIST
     c                   KFLD                    HEAVD
     c                   KFLD                    HEOPD
     c                   Z-ADD     1             HEAVD
     c                   Z-ADD     54144         HEOPD
      * div nødvendige felt i eksempelet (hentes normalt fra div register i prosesseringen.)
     c                   movel     *blank        REF              35
     c                   movel     'NO'          SYLAND            2
     c                   movel     '923609016'   SYRG             14
     c                   movel     '55557200'    TLF              15
     c                   movel     '55557311'    TFAXNR           15
      *  INIT
     c                   exsr      SeksjonINI
     C                   EXCEPT    QData
      *
      * Hele programlogikken med looper og diverse seksjons-subrutiner følger så..
     c     KEY           CHAIN     HEADF                              35
     c     *IN35         IFEQ      *OFF
     c                   exsr      SeksjonXXX
     C                   EXCEPT    QData
     c                   END
      *  AVSLUTT
     c                   exsr      SeksjonEND
     C                   EXCEPT    QData
     C*
     c                   seton                                        LR
     c                   return
      *

      ***********************************************
     C     SeksjonINI    BEGSR
      ***********************************************
      /free
       DATA =
        '<?xml version="1.0" encoding="ISO-8859-1" ?>'
       +'<PostenInterchange>'
       +'<InterchangeType>PostenInterchange</InterchangeType>'
       +'<Version>1.4-rev3</Version>'
       +'<Originator>'
       +'<Address>932725967</Address>'
       +'<SubAddress/>'
       +'<AddressQualifier>OrgNr-Avd</AddressQualifier>'
       +'</Originator>';
        // OSV...
      /end-free

     C                   ENDSR


      ***********************************************
     C     SeksjonXXX    BEGSR
      ***********************************************
     C                   MOVEL     HEADK3        POSTN             4
     C                   MOVE      HEADK3        STED             25
      * MAX VED BRUK AV EVAL =
     C**                 EVAL      DATA = ' LKHJLKJLKJLKJLKJLK' +
     C**                           'KJHKSDJHKJSDKJHSADFKJHFDLIULKULKWEJJKLEKJJK'
      /free
       DATA =
        '<Invoicee>'
       +'<Identifications>'
       +'<CompanyRegNo>' + %trim(SYRG) + ' </CompanyRegNo>'
       +'<CompanyVATNo>NO' + %trim(SYRG) + 'MVA</CompanyVATNo>'
       +'<CompanyPartyNo>' + %trim(%editc(HEKNK:'Z')) + '</CompanyPartyNo>'
       +'</Identifications>'
       +'<Name>' + %trim(HENAK) + '</Name>'
       +'<PostalAddress>'
       +'<Address1>' + %trim(HEADK1) + '</Address1>'
       +'<Address2>' + %trim(HEADK2) + '</Address2>'
       +'<City>' + %trim(STED) + '</City>'
       +'<PostCode>' + %trim(POSTN) + '</PostCode>'
       +'<CountryCode>' + %trim(SYLAND) + '</CountryCode>'
       +'</PostalAddress>'
       +'<Contact>'
       +'<Reference></Reference>'
       +'<Phone>' + %trim(TLF) + '</Phone>'
       +'<Fax>' + %trim(TFAXNR) + '</Fax>'
       +'</Contact>'
       +'</Invoicee>';
      /end-free

     C                   ENDSR


      ***********************************************
     C     SeksjonEND    BEGSR
      ***********************************************
      /free
        // evt. flere avslutningstagger for å matche start-tagger i innledningen...
       DATA =
        '</PostenInterchange>';

      /end-free

     C                   ENDSR
      *
     OQ         EADD         Qdata
     O                       DATA              1024
