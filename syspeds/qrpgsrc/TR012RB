********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
11128 * 01 YBC PTF11 VEKT ER ENDRET FRA 9.0 TIL 11.3. INGEN MERKING
11176 * 02 YBC PTF11 TILLEGGSLINJER MED SAMME POST
********************************************************************

     ‚***********************************************************
     ‚*** PROGRAM SKAL OVERFØRE SAD-LINJER TIL TET            ***
     ‚*** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
     ‚*** LEDIGE INDIKATORER 01-26                            ***
     ‚*** LEDIGE INDIKATORER 27-32 34-99                      ***
     ‚***********************************************************
     H DFTACTGRP(*NO)
     fsaeh      if   e           k disk
     fsaev4     if   e           k disk
     ftruv      o  a e             disk
     ftruvam    o  a e             disk
     fcundl01   if   e           k disk
     d                 ds
     d syrg                          14
     d  syfore                       11    overlay(syrg)
     d   syfor9                       9    overlay(syfore)
     ‚*  Prototype for tr012rb
     d tr012rb         pr
     d ufirm_                              like(ufirm)
     d uavd_                               like(uavd)
     d utdn_                               like(utdn)
     d uli_                                like(uli)
     d uavd2_                              like(uavd2)
     d utdn2_                              like(utdn2)
     ‚*  *ENTRY Interface for Main Procedure
     d tr012rb         pi
     d ufirm                          2
     d uavd                           4
     d utdn                           7
     d uli                            5
     d uavd2                          4
     d utdn2                          7
     ‚*----------------------------------------------------------------------
     ‚* Stand Alone Fields - TOP
     ‚*----------------------------------------------------------------------
     d xli             s              5  0
     d xln             s              5  0
     ‚*----------------------------------------------------------------------
     ‚* Stand Alone Fields - BOTTOM
     ‚*----------------------------------------------------------------------

      /free
       exsr init;

       xln = *zeros;
       setll ( seavd : setdn ) saev4;
       reade ( seavd : setdn ) saev4;
       dow not %eof;
         select;
         when xln = 0;
           exsr srhent;                                                         // HENT DATA FRA SAE
         when svln = xln;
           exsr srsum;                                                          // SUMMERE VEKT
         other;
           write truvr;
           exsr srhent;                                                         // HENT DATA FRA SAE
         endsl;
         reade ( seavd : setdn ) saev4;
       enddo;

       if tvli <> 0;
         write truvr;
       endif;

      /end-free
     C                   MOVE      XLI           ULI
      /free

       *inlr = *on;

       //**********************************************
       begsr srhent;
         //**********************************************
         xln = svln;
         xli = xli +1;
         tvli = xli;
         tvvnt = svvnt;
         tvvktb = svvktb;
         tvvktn = svvktn;
         tvnteh = svntm;
E02      tvmn = svft01;
E02      tvnt = svnt01;
E02      tveh = sveh01;
E02      %subst(tvvt:1:30) = svvt01;
         tvdty = '830';
         tvdref = %trim(setle) + ' ' + %trim(setll);
         exsr hadr12;

       endsr;

       //**********************************************
       begsr srsum;
         //**********************************************
           tvvktb = tvvktb + svvktb;
           tvvktn = tvvktn + svvktn;
           tvnteh = tvnteh + svntm;
       endsr;

       //**********************************************
       begsr hadr12;
         //**********************************************
         tvkns = seknk;
         tvnas = senak;
         tvads1 = seadk1;
         tvpns = seadk3;
         tvpss = seadk3;
         tvsks = 'NO';
         tvlks = 'NO';
         if seknk = 0;                                                          // 1<-B
           tvtins = *blanks;
         else;                                                                  // 1
           kundnr = seknk;
           chain ( ufirm : kundnr ) cundl01;
           *in34 = not %found;
           tvtins = *blanks;
          if %found;
           select;                                                              // 2<-B
           when eori = *blanks;                                                 // 2
             if ((syfor9 <> *blanks) and (syfor9 <> *zeros));                   //   3<-B
               tvtins = tvlks + syfor9;
             else;                                                              //  3
               tvtins = *blanks;
             endif;                                                             //   3<-E
           other;                                                               // 2
             tvtins = eori;
           endsl;                                                               //  2<-E
          endif;
         endif;                                                                 // 1<-E

         tvknk = sekns;
         tvnak = senas;
         tvadk1 = seads1;
         tvpnk = seads3;
         tvpsk = seads3;
         tvskk = 'NO';
         tvlkk = selkb;
         if sekns = 0;                                                          // 1<-B
           tvtink = *blanks;
         else;                                                                  // 1
           kundnr = sekns;
           chain ( ufirm : kundnr ) cundl01;
           *in34 = not %found;
           tvtink = *blanks;
          if %found;
           select;                                                              // 2<-B
           when eori = *blanks;                                                 // 2
             if ((syfor9 <> *blanks) and (syfor9 <> *zeros));                   //   3<-B
               tvtink = tvlkk + syfor9;
             else;                                                              //  3
               tvtink = *blanks;
             endif;                                                             //   3<-E
           other;                                                               // 2
             tvtink = eori;
           endsl;                                                               //  2<-E
          endif;
         endif;                                                                 // 1<-E

         if tvads1 = *blanks;                                                   // 1<-B
           %subst(tvads1:1:1) = '.';
         endif;                                                                 // 1<-E
         if tvadk1 = *blanks;                                                   // 1<-B
           %subst(tvadk1:1:1) = '.';
         endif;                                                                 // 1<-E

         write truvamr;

       endsr;

       //**********************************************
       begsr init;
         //**********************************************

         seavd = %dec(uavd2:4:0);
         setdn = %dec(utdn2:7:0);
         tvavd = %dec(uavd:4:0);
         tvtdn = %dec(utdn:7:0);
         xli   = %dec(uli:5:0);
         tvavd2 = %dec(uavd2:4:0);
         tvtdn2 = %dec(utdn2:7:0);

         chain ( seavd : setdn ) saeh;

       endsr;
      /end-free
