     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      REF(*LIBL/EDIM)
     A                                      CF03(03 'Avslutt program')
     A                                      CF05(05 'Forny')
     A                                      CF12(12 'Annuller')
     A                                      PRINT
     A                                      HLPPNLGRP(HLPSCR SVI014PNL)
     A                                      HLPTITLE('TDS IMPORT HISTORIK')
     A                                      HELP
     A                                      ALTHELP
      *
     A          R SVI014DA                  SFL
     A            MSN       R        H
     A            MMN       R        H
     A            MSR       R        H
     A            M0004     R        H
     A            M0010     R        H
     A            MVEN      R        H
     A            M0068     R        H
     A            M0035     R        H
     A            M0065     R        H
     A            ZOP            1A  I  9  2VALUES(' ' '5' '6' '7' '9' 'P')
     A            M1225     R        O  9  4
     A  27                                  COLOR(PNK)
     A            MDT       R        O  9  8
     A  27                                  COLOR(PNK)
     A            MTM       R        O  9 17
     A  27                                  COLOR(PNK)
     A            WTXT          56A  O  9 24
     A  27                                  COLOR(PNK)
      *
     A          R SVI014DB                  SFLCTL(SVI014DA)
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0012)
     A                                      TEXT('EDI MESSAGE-LOG, Heading')
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A                                  1  2'SVI014D A'
     A                                      COLOR(BLU)
     A                                  1 34'TDS Import'
     A                                      COLOR(WHT)
     A                                  2 35'Historik'
     A                                      COLOR(WHT)
     A                                  3  2'Avdelning:'
     A            WAVD      R        O  3 13REFFLD(MAVD)
     A                                      EDTCDE(4)
     A                                  3 19'Ärende:'
     A            WTDN      R        O  3 27REFFLD(MTDN)
     A                                      EDTCDE(4)
     A                                  5  2'Välj och tryck Enter:'
     A                                      COLOR(BLU)
     A                                  6  2'5=Visa meddelande   6=Visa fel   7-
     A                                      =Visa komponent   9=Visa edifact   -
     A                                      P=Skriv'
     A                                      COLOR(BLU)
     A            ZRECNO         4S 0O  7 68SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  8  2'A'
     A                                      COLOR(WHT)
     A                                  8  4'Typ'
     A                                      COLOR(WHT)
     A                                  8  8'Datum'
     A                                      COLOR(WHT)
     A                                  8 17'Tid'
     A                                      COLOR(WHT)
     A                                  8 24'Text'
     A                                      COLOR(WHT)
      *
     A          R SVI014DC
     A                                      OVERLAY
     A                                 23  2'F3=Avsluta   F5=Uppdatera   F12=Av-
     A                                      bryt'
     A                                      COLOR(BLU)
      *
     A          R SVI014DD
     A                                      OVERLAY
     A                                      ROLLUP(25)
     A                                  1  2'SVI014D D'
     A                                      COLOR(BLU)
     A                                  1 34'TDS Import'
     A                                      COLOR(WHT)
     A                                  2 35'Historik'
     A                                      COLOR(WHT)
     A                                  3  2'Tryck Enter för att fortsätta:'
     A                                      COLOR(BLU)
     A                                  5  2'Avdelning. . . . . . . . . . . . .-
     A                                       . :'
     A            MAVD      R        O  5 43EDTCDE(4)
     A                                  6  4'Ärende . . . . . . . . . . . . . .-
     A                                       :'
     A            MTDN      R        O  6 43EDTCDE(4)
     A                                  8  6'UNB ID Avsändare . . . . . . . . :'
     A            M0004     R        O  8 43
     A                                  9  6'UNB ID Mottagare . . . . . . . . :'
     A            M0010     R        O  9 43
     A                                 10  6'Testindikator  . . . . . . . . . :'
     A            M0035     R        O 10 43
     A                                 11  6'Meddelande referans  . . . . . . :'
     A            M0062     R        O 11 43
     A                                 12  6'Meddelande id  . . . . . . . . . :'
     A            M0065     R        O 12 43
     A                                 13  6'Tullid . . . . . . . . . . . . . :'
     A            M1004     R        O 13 43
     A                                 15  6'Sändningsnummer  . . . . . . . . :'
     A            MSN       R        O 15 43EDTCDE(4)
     A                                 16  8'Meddelandenummer . . . . . . . :'
     A            MMN       R        O 16 43EDTCDE(4)
     A                                 17  6'Send/Recive  . . . . . . . . . . :'
     A            MSR       R        O 17 43
     A                                 18  6'Status . . . . . . . . . . . . . :'
     A            MST       R        O 18 43
     A                                 19  8'Status Datum . . . . . . . . . :'
     A            MDT       R        O 19 43EDTWRD('    .  .  ')
     A                                 20 10'Status Tid . . . . . . . . . :'
     A            MTM       R        O 20 43EDTWRD('  :  :  ')
     A                                 21  6'Väntkod  . . . . . . . . . . . ..:'
     A            MVEN      R        O 21 43
     A                                 23  2'F3=Gå ut   F12=Annuller'
     A                                      COLOR(BLU)
      *
     A          R SVI014DE                  SFL
     A            F0078A    R        O  8 11REFFLD(TVINFF/F0078A *LIBL/TVINF)
     A            F0078B    R        O  9 11REFFLD(TVINFF/F0078B *LIBL/TVINF)
     A            F0078C    R        O 10 11REFFLD(TVINFF/F0078C *LIBL/TVINF)
     A            F0078D    R        O 11 11REFFLD(TVINFF/F0078D *LIBL/TVINF)
     A            F0078E    R        O 12 11REFFLD(TVINFF/F0078E *LIBL/TVINF)
      *
     A          R SVI014DF                  SFLCTL(SVI014DE)
     A                                      SFLSIZ(0004)
     A                                      SFLPAG(0003)
     A  81N83N84                            ROLLUP(25)
     A  81N83N84                            ROLLDOWN(26)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  84                                  SFLEND
     A                                  1  2'SVI014D E'
     A                                      COLOR(BLU)
     A                                  1 31'TDS Import'
     A                                      COLOR(WHT)
     A                                  2 21'Historik - Felmeddelanden - Text'
     A                                      COLOR(WHT)
     A                                  3  2'Avdelning:'
     A            MAVD      R        O  3 13EDTCDE(4)
     A                                  3 23'Ärende:'
     A            MTDN      R        O  3 31EDTCDE(4)
     A                                  5  2'Tryck Enter för att fortsätta.'
     A                                      COLOR(BLU)
     A            ÆRECNO         4S 0O  5 68SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  7 11'Text '
     A                                      COLOR(WHT)
      *
     A          R SVI014DØ
     A                                      OVERLAY
     A            ZMSGTX        79A  O 24  2MSGID(&ZMSGNO SYMSGF)
     A N30                                  COLOR(WHT)
     A  30                                  COLOR(RED)
     A            ZMSGNO         7A  P
