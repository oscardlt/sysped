      * OBS! logikken rundt 'AGDIST..' ikke i bruk ???
      *      mener vagt å huske at det var ideer om at agenter(BHR) kunne bygge sine egne turer
      *      totalt uavhengig av normale tursystemer... ble aldri gjennomført, bare forberedt
      *      (fil  AGDIST.. brukes KUN i dette programmet)
      * ER NÅ FYSIsK FJERNET FRA source uten merking, samt fil AGDIST.. slettet
      *********************************************************************
      *               TURLISTE/TURVELGER FOR EN SJÅFØR                    *
      *  Finne eller LAGE tur knyttet til aktuell sjåfør/TKsign PDA-ident *
      *  NÅ:                                                              *
      * Turen må tilhøre sjåføren                                         *
      *       KAN IKKE være hentet til TKSIGN - MÅ VÆRE ÅPEN (blank/P)    *
      *  BØR bli fullt interaktivt valg av inntil 3 turer.                *
      *   (mulig at også de so ER plukket til pda (kode 1) bør vises ?)   *
      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/STSWMTL MODULE(*LIBL/STSWMTL *LIBL/INCGI)
CRTPGM*         ACTGRP(*NEW) AUT(*USE)
      *
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 JOV PTF10 ved kall turvelger i TKsign så LAGES NY tur.   AGDIST-logikk slettet
********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
      *--------------------------------------------------------------------
      * Data base files
     FBRIDF     if   e           k disk    usropn
     FTURER20   if   e           k disk    usropn
     FBRPARF    if   e           k disk    usropn
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      *--------------------------------------------------------------------
      * Variables common to CGI programs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
      * Varibles
     DWSUSER           S             10
     DTN1              s              8
     DTN2              s              8
     DTN3              s              8
     DSJNr             s              8
     D LAGBUT          s            200
     D MTUR            s              8
     D MTURNr          s              8  0
     D TKId            s             10
     D SEMI            s              1
     D Fn              s             10
     D Lib             s             10
     D Mbr             s             10
     D Spaces          s             12a   inz('&nbsp;&nbsp;')
      *--------------------------------------------------------------------
      * Prolog common to CGI programs   (C-specs)
      * -Receive input from the remote browser
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prolog3
      *=====================================================================******
      *
      *=====================================================================
      * MAIN PROCESS LINE
      *=====================================================================
      * Hent / repeter UserID
     C                   EVAL      WSUSER   = zhbgetvar('user')
     C                   eval      Mtur     = zhbgetvar('Mtur  ')
     C                   eval      MTurNr   = c2n2(MTur  )
     C                   eval      TKId     = zhbgetvar('TKId  ')
     C                   move      TKId          SJNR
     C                   eval      TUSJA1   = c2n2(SJNR  )
     C                   eval      SEMI    = zhbgetvar('SEMI')
N01  c     WSUSER        ifeq      *blanks
N01  C                   EVAL      WSUSER   = TKID
N01  c                   endif
      * set libl
     C                   OPEN      BRIDF
     C     WSUSER        CHAIN     BRIDF                              50
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
     C                   OPEN      TURER20
     c     TURER20K      KLIST
     c                   KFLD                    TUSJA1
     c                   KFLD                    XXTST4            1
      *
N01   * Kommer inn med 0 i Mtur= kall fra TKsign (ikke TKterm)  LAG NY tur
N01  c     MturNr        ifeq      *zeros
N01  c     WSUSER        andeq     TKID
N01  c                   exsr      NyTur
N01  c                   else
      * Turen må tilhøre sjåføren - KAN IKKE være hentet til TKSIGN - MÅ VÆRE ÅPEN (blank/P)
     c                   exsr      NormTur
N01  c                   endif
      *
      * RETURNER til den manuelle velgeren med TN1 fylt ut
      * Set/Read skeleton output html, etc.
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC'
     C     SEMI          IFNE      'Y'
     C                   eval      Mbr = 'STSWM'
     C                   else
     C                   eval      Mbr = 'STSWT2'
     C                   end
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
      * sett html-variable
     C                   callp     updhtmlvar('USER':WSUSER:
     C                             InitHTMLVars)
     C                   callp     updhtmlvar('TN1':TN1)
     C                   callp     updhtmlvar('TN2':TN2)
     C                   callp     updhtmlvar('TN3':TN3)
     C                   callp     updhtmlvar('LAGBUT':LAGBUT)
     C                   callp     updhtmlvar('MTUR':MTUR)
     C                   callp     updhtmlvar('TKID':TKID)
      * Write HTML-section
     C                   callp     wrtsection('all')
      * dummy fini-section
     C                   callp     wrtsection('*fini')
      * Quit
     C                   close     BRIDF
     C                   close     TURER20
     C                   MOVE      *ON           *INLR
     C                   return
      *************************************************************
     C     NormTur       begsr
      *************************************************************
      * innhold FLYTTET fra main-logikk
     c                   Move      ' '           XXTST4
     c     TURER20K      setll     TURER20
     C     Next1         tag
     c     TURER20K      reade     TURER20                                35
     c     *in35         ifeq      '0'
     c     Tupro         cabeq     MturNr        Next1
     c     Tust          Ifne      ' '
     c     Tust          andne     'P'
     c                   goto      Next1
     c                   end
     c                   end
      * OM IKKE en slik tur finnes - skap basert på mønster-turen knyttet til bruker
     c     *in35         ifeq      '1'
     c                   call      'TURADD'
     c                   parm                    Mtur
      * prøv så igjen
     c                   Move      ' '           XXTST4
     c     TURER20K      setll     TURER20
     C     Next2         tag
     c     TURER20K      reade     TURER20                                35
     c     *in35         ifeq      '0'
     c     Tupro         cabeq     MturNr        Next2
     c     Tust          Ifne      ' '
     c     Tust          andne     'P'
     c                   goto      Next2
     c                   end
     c                   end
     c                   end
      * Hvis tur funnet - legg denn ut i TN1
     c  N35              move      TUPRO         TN1
     c                   endsr
N01   *************************************************************
N01  C     NYTUR         begsr
N01   *************************************************************
N01  C                   OPEN      BRPARF
N01  C     BrParKey      klist
N01  C                   kfld                    BIBRID
N01  C                   kfld                    PAKUNR
N01  C                   kfld                    PAID
N01  C                   MOVE      'TUTUR'       PAID
N01  C     BrParKey      Chain     BRPARF                             33
N01  C                   CLOSE     BRPARF
N01  C     *in33         cabeq     '1'           UtNyTur
N01  C                   z-add     PAVRDN        MTurNr
N01  C                   move      MTurNr        Mtur
N01  C                   move      'T'           MtFLAGG                        Fra TKsign
N01  c                   call      'TURADDN'
N01  c                   parm                    Mtur
N01  c                   parm                    MtFLAGG           1
N01  C     MTFLAGG       cabeq     '1'           UtNyTur
N01  c                   move      Mtur          TN1
N01   *
N01  c     UtNyTur       endsr
