     A*%%TS  SD  20180815  080023  SVEIN       REL-V7R1M0  5770-WDS
*****A**************************************************************
     A* RETTINGER I DETTE PROGRAM:
PTFnrA*Sek Sig Vers Beskrivelse...........................
"""""A*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
P9XXXA* 01 YBC PTF9 LEVERANDØRSFAKT.NR  ER ØKT FRA 9A TIL 13A
P9XXXA* 02 SH  PTF10 KREDITERE BILAG
P10XXA* 03 TH  PTF10 HJELPETEKST
*****A**************************************************************
     A*%%EC
     A                                      DSPSIZ(27 132 *DS4)
     A                                      PRINT
     A                                      HLPPNLGRP(HLPSCR SYKS21PNL)
     A                                      HLPTITLE('ARBEIDE MED KOSTANDSBILAG-
     A                                      ')
     A                                      HELP
     A                                      ALTHELP
     A          R SUBFIL                    SFL
     A*%%TS  SD  20170304  163159  SVEIN       REL-V7R1M0  5770-WDS
     A            KAFNR         13A  H
     A            KABNR          7S 0H
     A            KAJOUR         7S 0H
     A            KAVK           3A  H
     A            KABDT          8S 0H
     A            WSNR           1A  I  6  2
     A            WSATT          3A  I  6  4
     A            SABNR          7Y 0O  6  8EDTCDE(4)
     A  41                                  DSPATR(RI)
     A  61                                  COLOR(RED)
     A            KAST           1A  O  6 16
     A  49                                  DSPATR(RI)
     A            KAPMN          2S 0O  6 18
     A            KAPÅR          2S 0O  6 21
S01  A*           WSF01         15A  O  6 18
     A            WSF01         16A  O  6 25
     A  42
     AO 51
     AO 52
     AO 56                                  DSPATR(RI)
     A            KALNR          8Y 0O  6 42EDTCDE(4)
     A  43
     AO 44
     AO 45                                  DSPATR(RI)
     A            LNAVN         30A  O  6 51
     A            KASG           3A  O  6 82
     A  46
     AO 47
     AO 48                                  DSPATR(RI)
     A            WSBDT          6Y 0O  6 87EDTCDE(4)
     A  50                                  DSPATR(RI)
     A  61                                  COLOR(RED)
     A            KAVAL          3A  O  6 94
     A            KABL          13Y 2O  6 99EDTCDE(M)
     A            KAMVA          1A  O  6115
     A            KABLST         1A  O  6117
     A            KABB           2A  O  6119
     A            WSFDT          6Y 0O  6122EDTCDE(4)
     A            KBREKL         2A  O  6129COLOR(RED)
     A  78                                  DSPATR(RI)
     A* *DS3                                SFLSIZ(0016)
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A*%%TS  SD  20170304  162647  SVEIN       REL-V7R1M0  5770-WDS
     A                                      SFLSIZ(0016)
     A                                      SFLPAG(0015)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                      OVERLAY
     A                                      CF03(03 'AVSLUTT')
     A                                      CF04(04 'SØK')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/SYKS21'
     A                                      COLOR(BLU)
     A                                  1 20MSGCON(020 TBR0209 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  1 70TIME
     A                                      COLOR(BLU)
     A                                  2  2MSGCON(128 YBC0050 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A            ZRECNO         4S 0O  2 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  3  2MSGCON(128 YBC0051 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N88                              4  2MSGCON(131 JOV0429 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A  88                              4  2MSGCON(131 YBC0056 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A            W2NR           1A  B  5  2DSPATR(UL)
     A            W2ATT          3A  B  5  4
     A            W2BNR          7Y 0B  5  8EDTCDE(4)
     A                                      DSPATR(UL)
     A            W2ST           1A  B  5 16DSPATR(UL)
S01  A*           W2FNR          9A  B  5 18DSPATR(UL)
     A            W2FNR         13A  B  5 25DSPATR(UL)
     A            W2LNR          8Y 0B  5 42EDTCDE(4)
     A                                      DSPATR(UL)
     A            W2SG           3A  B  5 82DSPATR(UL)
     A            W2DT           6Y 0B  5 87EDTCDE(4)
     A                                      DSPATR(UL)
     A          R BUNN
     A                                      SLNO(22)
     A                                      CLRL(03)
     A                                      OVERLAY
     A                                      ERASE(BILDE02)
     A                                      ERASE(BUNNKR)
     A                                  2  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  2 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  2 42MSGCON(018 DIV0022 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  2 62MSGCON(018 DIV0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  3  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                              3 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A          R BUNNKR
     A                                      SLNO(22)
     A                                      CLRL(03)
     A                                      OVERLAY
     A                                      CF12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1  2MSGCON(011 DIV2150 *LIBL/SYMSGF)
     A            WSBSR          1A  B  1 14TEXT('BILAGSSERIE')
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A                                  1 17MSGCON(020 SHU1253 *LIBL/SYMSGF)
     A            WSBNR2         7Y 0B  1 40TEXT('BILAGSNUMMER')
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
     A                                  2  2MSGCON(022 DIV2557 *LIBL/SYMSGF)
     A            WSTXT         35A  B  2 25DSPATR(UL)
     A                                      COLOR(YLW)
     A  30        MSGTXT        78A  O  3  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                              3 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A          R NYVARE
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF03(03 'AVSLUTT')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/SYKS21'
     A                                      COLOR(BLU)
     A                                  1 20MSGCON(020 TBR0209 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                  1 69TIME
     A                                  2  2MSGCON(128 YBC0050 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3  2MSGCON(128 YBC0051 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N88                              4  2MSGCON(131 JOV0429 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A  88                              4  2MSGCON(131 YBC0056 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A            W2NR           1A  B  5  2DSPATR(UL)
     A            W2BNR          7Y 0B  5  7EDTCDE(4)
     A                                      DSPATR(UL)
     A            W2ST           1A  B  5 15DSPATR(UL)
S01  A*           W2FNR          9A  B  5 18DSPATR(UL)
N01  A            W2FNR         13A  B  5 25DSPATR(UL)
     A            W2LNR          8Y 0B  5 42EDTCDE(4)
     A                                      DSPATR(UL)
     A            W2SG           3A  B  5 82DSPATR(UL)
     A            W2DT           6Y 0B  5 87EDTCDE(4)
     A                                      DSPATR(UL)
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 42MSGCON(018 DIV0022 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 62MSGCON(018 DIV0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                             24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A          R BILDE01
     A*%%TS  SD  20170309  072319  SVEIN       REL-V7R1M0  5770-WDS
     A                                      CF03(03 'Avslutt')
     A                                      CF04(04 'SØK')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1  2'SYSPED/SYKS21'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(017 DIV2563 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                  1 69TIME
     A                                  5  2MSGCON(028 DIV2562 *LIBL/SYMSGF)
     A                                  6  2MSGCON(030 SHU1471 *LIBL/SYMSGF)
     A            WSREKL         1A  B  6 41VALUES('S' 'O' ' ')
     A                                  6 50MSGCON(030 SHU1472 *LIBL/SYMSGF)
     A                                  7  2MSGCON(030 DIV2558 *LIBL/SYMSGF)
     A            WSBNR          7Y 0B  7 41DSPATR(UL)
     A  99                                  DSPATR(PC)
     A  99                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A            BNRTYP         1A  B  7 50
     A                                  7 52MSGCON(013 DIV2564 *LIBL/SYMSGF)
     A                                  8  2MSGCON(030 DIV2164 *LIBL/SYMSGF)
     A            WSFNR         13A  B  8 41DSPATR(UL)
     A                                  9  2MSGCON(030 DIV2559 *LIBL/SYMSGF)
     A            WSLNR          8Y 0B  9 41DSPATR(UL)
     A  98                                  DSPATR(PC)
     A  98                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                 10  2MSGCON(030 DIV2168 *LIBL/SYMSGF)
     A            WSSG           3A  B 10 41DSPATR(UL)
     A                                 11  2MSGCON(030 TBR0196 *LIBL/SYMSGF)
     A            WSBKOM        35A  B 11 41
     A                                 12  2MSGCON(035 TBR0212 *LIBL/SYMSGF)
     A            WSFSOK         3A  B 12 41
     A            WSFTXT        15A  B 12 46
     A                                 13  2MSGCON(035 SHU1425 *LIBL/SYMSGF)
     A            WS12           1A  B 13 41VALUES('1' '2' ' ')
     A                                 14  2'Status  . . . . . . . . . . .:'
     A            WSST           1A  B 14 41DSPATR(UL)
     A                                 14 55MSGCON(008 DIV2561 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 15  2MSGCON(030 DIV2560 *LIBL/SYMSGF)
     A            WSDT           6Y 0B 15 41DSPATR(UL)
     A                                      EDTWRD('0     ')
     A                                 16  2MSGCON(030 SHU1712 *LIBL/SYMSGF)
     A            WSMNDF         2Y 0B 16 41EDTCDE(Z)
     A                                 16 44'/'
     A            WSAARF         2Y 0B 16 46EDTCDE(Z)
     A                                 16 49'->'
     A            WSMNDT         2Y 0B 16 52EDTCDE(Z)
     A                                 16 55'/'
     A            WSAART         2Y 0B 16 57EDTCDE(Z)
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(018 DIV0032 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(RED)
     A*
     A N30                             24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A          R BILDE02
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            MSGNR          7A  P
     A                                  1  2MSGCON(029 TBR0213 *LIBL/SYMSGF)
     A            KABNR          7Y 0O  1 33DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  2  2MSGCON(022 DIV2557 *LIBL/SYMSGF)
     A            WSTXT         35A  B  2 33DSPATR(UL)
     A                                      DSPATR(PC)
     A  30                                  DSPATR(RI)
     A                                  3  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  4  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(RED)
     A N30                              4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
