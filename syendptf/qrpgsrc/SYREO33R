     H DFTACTGRP(*NO)
     FEDIM      UF   E           K DISK
     FEDIS      UF   E           K DISK
     FEDISE     UF   E           K DISK
     FEDIRE     UF   E           K DISK
     FEDISE1K   UF   E           K DISK    RENAME(EDISER:EDISER1K)
     F                                     PREFIX(K_)
     FEDIRE1K   UF   E           K DISK    RENAME(EDIRER:EDIRER1K)
     F                                     PREFIX(K_)
     FEDISX     UF   E           K DISK
     FXMLRCVF   UF   E           K DISK
     FXMLRCAF   UF   E           K DISK
     FXMLRCLF   UF   E           K DISK
     FSVEF      UF   E           K DISK
     FSVEFZ     UF   E           K DISK    RENAME(SVEFR:SVEFZR)
     FSVEH      UF   E           K DISK
     FSVEHE     UF   E           K DISK
     FSVEHF     UF   E           K DISK
     FSVEHG     UF   E           K DISK
     FSVEHH     UF   E           K DISK
     FSVEHZ     UF   E           K DISK    RENAME(SVEHR:SVEHZR)
     FSVEV      UF   E           K DISK
     FSVEVA     UF   E           K DISK
     FSVEVX     UF   E           K DISK
     FSVEVZ     UF   E           K DISK    RENAME(SVEVR:SVEVZR)
     FSVEX      UF   E           K DISK
     FSVEZ      UF   E           K DISK
     FTVINF     UF   E           K DISK
     FFRISOK    UF   E           K DISK
     FTRACKF    UF   E           K DISK
      *
     FARKTXT    IF   E           K DISK
     FARKEXT    IF   E           K DISK
     FFIRMARC   IF   E           K DISK
     FFIRM      IF   E           K DISK
     FARKIVL17  UF   E           K DISK

     d                 ds
     d  usn_n                  1      9  0
     d  usn                    1      9
     d                 ds
     d  smbr                   1     10A
     d  smbr_1                 1      1A
     d  smbr_2                 2      8A
     d  smbr_3                 9     10A
     d  smbr_4                 4     10A

     d WhereC          s            512a
     d FirstW          s              1A
     d cQuote          C                   const('''')
     d SqlStm          s            512a
     d myrows0         s             10i 0
     d myrows          s             10i 0
     d myrows2         s             10i 0
     D XDTF            S              8  0
     D XDTP            S              8  0
     D XPOS            S              3  0
     d XAPS            C                   const(X'7D')
     D XMBR            S             10A
     D Error           S            150
     d X               S             10  0
     d X2              S             10  0
     D ufil            S            256A
     D uok             S              1A
     D XSLUTT          S              1
     D XSLUTT2         S              1
     D XSLT            S              1
      *
      *  Prototype for 'SYREO01C3'
     d SYREO01C3       pr                  extpgm('SYREO01C3')
     d ufil_                               like(ufil)
      *  Prototype for 'SYREO33R2'
     d syreo33r2       pr                  extpgm('SYREO33R2')
     d usn_                                like(usn)
     d udtp_                               like(udtp)
     d uok_                                like(uok)
      *  Prototype for syreo33r
     d syreo33r        pr
     d udtf_                               like(udtf)
     d udtp_                               like(udtp)
      *  *ENTRY Interface for Main Procedure
     d syreo33r        pi
     d udtf                           8
     d udtp                           8
      *
     d MyPoDS0         ds                  Qualified  dim(1)
     d  FIFIRM                        2A
     d MyPoDS          ds                  Qualified  dim(10000)
     d  SVEH_SYAV                     4S 0
     d  SVEH_SYOP                     7S 0
     d  SVEH_SYSG                     3A
     d  SVEH_SYDT                     8S 0
     d MyPoDS2         ds                  Qualified  dim(1000)
     d  MAVD                          4S 0
     d  MTDN                          7S 0
     d  MSR                           1A
     d  MMN                           9S 0
     d  MSN                           9S 0
      /free

         exsr init;

         dow xslutt <> 'J';
           exsr srhoved;
         enddo;

       *inlr = *on;
       return;

       //=====================================================================
       // Hoved logikk
       //=====================================================================
       begsr srhoved;

       clear MyPoDS;
       SqlStm = 'Select SVEH_SYAV, SVEH_SYOP, SVEH_SYSG, SVEH_SYDT ' +
                'from SVEH ' +
                'Where SVEH_SYOP > 0 AND SVEH_SYDT <= ' + %trim(udtf);
       Exec SQL PREPARE DynSel from :SqlStm;
       EXEC SQL DECLARE  A33  cursor for DynSel;
       Exec SQL OPEN A33;
       Myrows = %elem(MyPoDS);
       exec sql FETCH A33   for :MyRows rows
                  INTO :MyPoDs;
       Exec SQL CLOSE A33;

       if myrows > 0;
         for x = 1 to myrows;
           if mypods(x).sveh_sysg = *blanks;
             xslutt = 'J';
             leave;
           endif;

       //  if %subst(mypods(x).sirg:10:2) = *blanks or                          //Firma 5 år
       //     %subst(mypods(x).sirg:10:2) <> *blanks and mypods(x).sidt < xdtp; //Personkunde 10 år
             sveh_syav = mypods(x).sveh_syav;
             sveh_syop = mypods(x).sveh_syop;

             exsr sra;
             exsr srb;
             xslutt2 = 'N';
             dow xslutt2 <> 'J';
               exsr src;
             enddo;
             exsr srf;

             exec sql
             delete from SVEHE
             where svehe_sya = :sveh_syav and svehe_syo= :sveh_syop
             with NC;
             exec sql
             delete from SVEHF
             where svehf_sya = :sveh_syav and svehf_syo= :sveh_syop
             with NC;
             exec sql
             delete from SVEHG
             where svehg_sya = :sveh_syav and svehg_syo= :sveh_syop
             with NC;
             exec sql
             delete from SVEHH
             where svehh_sya = :sveh_syav and svehh_syo= :sveh_syop
             with NC;
             exec sql
             delete from SVEHZ
             where sveh_syav = :sveh_syav and sveh_syop= :sveh_syop
             with NC;
             exec sql
             delete from SVEX
             where svex_syav = :sveh_syav and svex_syop= :sveh_syop
             with NC;
             exec sql
             delete from SVEZ
             where svez_syav = :sveh_syav and svez_syop= :sveh_syop
             with NC;

           // slett hovedpost
             exec sql
             delete from SVEH
             where sveh_syav = :sveh_syav and sveh_syop = :sveh_syop
             with NC;
        // endif;  // End of "if %subst(mypods(x).sveh_sysg:10:2) = *blanks or"

         endfor;
       endif;

       endsr;
      //*_________________________________________
      //* SETLL, READ AND DELETE RECORDS FROM SVEV
      //*"""""""""""""""""""""""""""""""""""""""""
       begsr sra;

       exec sql
       delete from svev
       where svev_syav=:sveh_syav and svev_syop=:sveh_syop
       with NC;

       exec sql
       delete from sveva
       where sveva_sav=:sveh_syav and sveva_sop=:sveh_syop
       with NC;

       exec sql
       delete from svevx
       where svevx_syav=:sveh_syav and svevx_syop=:sveh_syop
       with NC;

       exec sql
       delete from svevz
       where svev_syav=:sveh_syav and svev_syop=:sveh_syop
       with NC;

       endsr;

      //*_________________________________________
      //* SETLL, READ AND DELETE RECORDS FROM SVEF
      //*"""""""""""""""""""""""""""""""""""""""""
       begsr srb;

       exec sql
       delete from svef
       where svef_syav=:sveh_syav and svef_syop=:sveh_syop
       with NC;

       exec sql
       delete from svefz
       where svef_syav=:sveh_syav and svef_syop=:sveh_syop
       with NC;

       endsr;

      //*_____________________________________________________
      //* SETLL, READ AND DELETE RECORDS FROM EDIM/EDIS/TVINF
      //*"""""""""""""""""""""""""""""""""""""""""""""""""""""
       begsr src;

       clear MyPoDS2;
       SqlStm = 'Select MAVD, MTDN, MSR, MMN, MSN ' +
                'from EDIM ' +
                'Where MAVD = ' + %trim(%editc(sveh_syav:'Z')) +
                ' and MTDN = ' + %trim(%editc(sveh_syop:'Z')) +
                ' and M1001 <> ' + xaps + '929' + xaps +
                ' and M1001 <> ' + xaps + '830' + xaps +
                ' and M1001 <> ' + xaps + 'TEI' + xaps +
                ' and M1001 <> ' + xaps + 'TET' + xaps +
                ' and M1001 <> ' + xaps + 'SVI' + xaps +
                ' and M1N07 <> ' + xaps + 'SVI' + xaps +
                ' and M1001 <> ' + xaps + 'STI' + xaps +
                ' and M1N07 <> ' + xaps + 'STI' + xaps +
                ' and M1001 <> ' + xaps + 'STE' + xaps +
                ' and M1N07 <> ' + xaps + 'STE' + xaps +
                ' and M1001 <> ' + xaps + 'DKI' + xaps +
                ' and M1N07 <> ' + xaps + 'DKI' + xaps +
                ' and M1001 <> ' + xaps + 'DKE' + xaps +
                ' and M1N07 <> ' + xaps + 'DKE' + xaps;
       Exec SQL PREPARE DynSel2 from :SqlStm;
       EXEC SQL DECLARE  A34  cursor for DynSel2;
       Exec SQL OPEN A34;
       Myrows2 = %elem(MyPoDS2);
       exec sql FETCH A34 for :MyRows2 rows
                  INTO :MyPoDs2;
       Exec SQL CLOSE A34;

       if myrows2 > 0;
         for x2 = 1 to myrows2;
           if mypods2(x2).msr = *blanks;
             xslutt2 = 'J';
             leave;
           endif;

           mmn = mypods2(x2).mmn;
           msn = mypods2(x2).msn;

           exec sql
           delete from TVINF
           where fmn = :mmn
           with NC;
           exec sql
           delete from XMLRCVF
           where xrsn = :msn
           with NC;
           exec sql
           delete from XMLRCAF
           where xrsn = :msn
           with NC;
           exec sql
           delete from XMLRCLF
           where xrsn = :msn
           with NC;

           chain (msn) edis;
           if %found;
            // slett komponent
             exsr src2;
            // slett fil i IFS
             if sifs <> *blanks;
               if %subst(sifs:1:1) <> '/';
                 sifs = '/' + %trim(sifs);
               endif;
               ufil = sifs;
               syreo01c3 (ufil);
             endif;
             delete edisr;
           endif;

         // slett hovedpost
           exec sql
           delete from EDIM
           where mmn = :mmn
           with NC;

         endfor;
       endif;

       endsr;

      //*_____________________________________________________
      //* SETLL, READ AND DELETE RECORDS FROM EDIRE/EDISE/EDIRE1K/EDISE1K/XMLRCVF/AF/LF/EDISX
      //*"""""""""""""""""""""""""""""""""""""""""""""""""""""
       begsr src2;

       exec sql
       delete from edire
       where rembr = :smbr
       with NC;

       exec sql
       delete from edise
       where sembr = :smbr
       with NC;

       exec sql
       delete from edire1k
       where rembr = :smbr
       with NC;

       exec sql
       delete from edise1k
       where sembr = :smbr
       with NC;

       chain smbr edisx;
       if %found;
         if sxifsobj <> *blanks and %subst(sxifsobj:1:1) <> '/';
           sxifsobj = '/' + %trim(sxifsobj);
         endif;
         syreo01c3 (sxifsobj);
         delete edisxr;
       else;
         if smbr_3 = ' ';
         // MBR = R1234567 -> PRØV R001234567
           xmbr = smbr_1 + '00' + smbr_2;
         else;
         // MBR = R001234567 -> PRØV R1234567
           xmbr = smbr_1 + smbr_4 + '  ';
         endif;
         chain xmbr edisx;
         if %found;
           if sxifsobj <> *blanks and %subst(sxifsobj:1:1) <> '/';
             sxifsobj = '/' + %trim(sxifsobj);
           endif;
           syreo01c3 (sxifsobj);
           delete edisxr;
         endif;
       endif;

       endsr;

      //*_________________________________________
      //* SETLL, READ AND DELETE RECORDS FROM FRISOK/TRACKF
      //*"""""""""""""""""""""""""""""""""""""""""
       begsr srf;

       exec sql
       delete from frisok
       where fsavd = :sveh_syav and fsopd = :sveh_syop
       with NC;

       exec sql
       delete from trackf
       where ttavd = :sveh_syav and ttopd = :sveh_syop
       with NC;

       setll (sveh_syav:sveh_syop) arkivl17;
       reade (sveh_syav:sveh_syop) arkivl17;
       dow not %eof;
         chain (fifirm) firmarc;
         chain (artype) arktxt;
         if %found and arklag <> *blanks;
           chain (fifirm:arklag) arkext;
         endif;
         if arbhis <> *blanks;
         // Bane for historikk
           arcane = arbhis;
         endif;
         xpos = %scan ('.' : arlink : 1);
         if xpos = 0;
           ufil = '/' + %TRIM(ARCANE) + '/' + %TRIM(ARLINK) + '.pdf';
         else;
           ufil = '/' + %TRIM(ARCANE) + '/' + %TRIM(ARLINK);
         endif;
         syreo01c3 (ufil);

         delete arkivr;
         reade (sveh_syav:sveh_syop) arkivl17;
       enddo;

       endsr;

      //***********************************************
       begsr init;
      //***********************************************
       xdtf = %float(udtf);
       xdtp = %float(udtp);

       SqlStm = 'Select * ' +
                'from FIRM ' +
                'FOR READ ONLY ';
       Exec SQL PREPARE DynSel0 from :SqlStm;
       EXEC SQL DECLARE  A30  cursor for DynSel0;
       Exec SQL OPEN A30;
       Myrows0 = %elem(MyPoDS0);
       exec sql FETCH A30   for :MyRows0 rows
                  INTO :MyPoDs0;
       Exec SQL CLOSE A30;
       FIFIRM = mypods0(1).fifirm;

       endsr;

      /end-free
