      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/EPOMS16 MODULE(*LIBL/EPOMS16
CRTPGM*        *LIBL/CHECKUSR *LIBL/INCGI) ACTGRP(CGI) AUT(*USE)
      *
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
      * 01 JOV PTF10 Title sette i RPG
10XXX * 02 YBC PTF10 TEST FRA/TIL STEDSKODE PÅ LINJE-NIVÅ
10XXX * 03 JOV PTF10 linje slettet e/shipped:vis med status Deleted (fins kun i epopos)
xxxxxx*xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
10XXX * 04 YBC PTF11 SJEKK STATUS KUN PÅ HEAD VED PLUKK PÅ HEAD-NIVÅ
********************************************************************
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     UF   E           K DISK    USROPN
     FEPOPOS    IF   E           K DISK    USROPN
     FEPOH      IF   E           K DISK    USROPN
     FEPOL      IF   E           K DISK    USROPN
     FHEADF     IF   E           K DISK    USROPN
     FTURER14   IF   E           K DISK    USROPN
      *********************************************************************
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      /copy syspeds/qrpgsrcpy,variables3
      * Indicators for GetHtmlIfsMult subprocedure
     D IfsMultIndicators...
     d                 ds
     D  NoErrors                       n
     D  NameTooLong                    n
     D  NotAccessible                  n
     D  NoFilesUsable                  n
     D  DupSections                    n
     D  FileIsEmpty                    n

      *
     D OddEven         s             10
     0* Client input variables
     D user            s             10
     D UsrSpcName      s             10
     D WSKN            S              8
     D BSKN            S              8  0
     D WSAVD           s              4
     D WBAVD           s              4  0
     D WSOPD           s              7
     D WBOPD           s              7  0
     D VisAChecked     s             10
     D VisAlle         s              1
     D DATO            s              8  0
     D ZZVKt           s             11  2
     D ZZM3            s              4  0
     D From            s              8
     D To              s              8
     D STATUS          S            100
     D REASON          S            100
     D STATUSL         S            100
     D REASONL         S            100
     D Title           S            100
     D PGM             C                   CONST('SYSPED/CHECKUSR')
      *
     D ANTSND          s              5  0
     D MaxSN           s              5
     D MaxSND          s              5  0
     D ANTRCD          s              5  0
     D MaxRC           s              5
     D MaxRCD          s              5  0
     D Spaces          s             12a   inz('&nbsp;&nbsp;')
     D ItemCount       s             10i 0
     D i               s             10i 0
     D UPC             C                   CONST('ABCDEFGHIJKLMNOPQRST-
     D                                     UVWXYZÆØÅ')
     D LO              C                   CONST('abcdefghijklmnopqrst-
     D                                     uvwxyzæøå')
     D                 DS
     D  FraDs                  1      8
     D   EHFLK                 1      2
     D   StrFra                3      3    inz('-')
     D   EHFSTE                4      8
     D                 DS
     D  TilDs                  1      8
     D   EHTLK                 1      2
     D   StrTil                3      3    inz('-')
     D   EHTSTE                4      8
     D                 DS
     D  TstFrTi                1      8
     D   TstStr                3      3
      *
      /copy syspeds/qrpgsrcpy,prolog3
      *
      * Get program start time for calculating execution time
     C                   time                    timedata1
      * Parse input / cvt to numeric
     C                   exsr      Parse
      * File open / keys
     C                   EXSR      INIT
      *
     C                   CALLB     PGM
     C                   PARM                    BIBRID
     C                   PARM                    UsrSpcName
     C                   PARM                    UINN              1
     C                   IF        UINN = 'N'
     C                   GOTO      SLUTT
     C                   END
      * dato/tid siste vellykkede operasjon.
     C     USER          CHAIN     BRIDF                              50
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVE      WDT           BIDTF
     C                   MOVE      WTM           BITIDF
     C                   UPDATE    BRIDFR
      * Read output skeleton html member into memory
     C                   exsr      LoadHtml
      * Set section variables
     C                   exsr      Settop
     C                   callp     wrtsection('StdtopTab')
     C                   callp     wrtsection('top')                            Programmets html
      *
      * HOVEDRUTINE
      *
      * Vis liste over registrete saker
      *
     C     MaxSnd        IFEQ      *ZEROS
     C                   Z-add     500           MaxSnd
     C                   END
     C     MaxRcd        IFEQ      *ZEROS
     C                   Z-add     2000          MaxRcd
     C                   END
      *
     C                   Move      *zeros        AntSnd
     C                   Move      *zeros        AntRcd
      *
     c     HeaKey        setll     EPOPOS
     c     HeaKey        Reade     EPOPOS                                 35
      *
     c     *IN35         doweq     '0'
     C                   Add       1             AntRcd
     C     AntRcd        CabGe     MaxRcd        Utles
      * div filter
     C     EPOHK         CHAIN     EPOH                               33
     c     EPLIN         ifne      *zeros
     C     EPOLK         CHAIN     EPOL                               33
     C                   exsr      Setrow
     C                   callp     wrtsection('row')
     C                   Add       1             AntSnd
     c                   goto      NextRec
     c                   else
      * linjenivå (når alle)
     C     EPOHK         SETLL     EPOL
     C     EPOHK         READE     EPOL                                   33
     C                   DOW       *IN33 = *OFF
     C                   exsr      Setrow
     C                   callp     wrtsection('row')
     C                   Add       1             AntSnd
     C     AntSnd        CabGe     MaxSnd        Utles
     C     EPOHK         READE     EPOL                                   33
     C                   ENDDO
     c                   endif
      *
     c     NextRec       Tag
     c     HeaKey        Reade     EPOPOS                                 35
     c                   enddo
      *
      *
     C     UTLES         TAG
     C                   callp     updHTMLvar('ANTSND':
     C                             %trim(%editc(ANTSND:'3')))
     C                   callp     updHTMLvar('MaxSnd':
     C                             %trim(%editc(MaxSnd:'3')))
     C                   callp     updHTMLvar('ANTRCD':
     C                             %trim(%editc(ANTRCD:'3')))
     C                   callp     updHTMLvar('MaxRcd':
     C                             %trim(%editc(MaxRcd:'3')))
      * Quit
      * Compute run time
     C                   time                    timedata2
     C     timedata2     subdur    timedata1     ms:*ms
     C                   eval      sec = ms / 1000000
     C                   callp     updhtmlvar('runtime':
     C                             %trim(%editw(sec:'     0 .   ')))
      *
     C                   callp     wrtsection('bot')                            programmets Html
     C                   callp     wrtsection('botStd')                         Body/htmlavsl
      *
     C     SLUTT         TAG
     C                   exsr      Exit
      *
     C                   eval      *inlr = *on
     C                   return
      *
      *
      *=====================================================================
      * Parse input / cvt to numeric
      *=====================================================================
     C     Parse         begsr
      * Parse variables from QUERY_STRING environment variable
     C                   eval      MAXSN    = zhbgetvar('MaxSn')
     C                   eval      MaxSnd   = c2n2(MAXSN)
     C                   eval      MAXRC    = zhbgetvar('MaxRc')
     C                   eval      MaxRcd   = c2n2(MAXRC)
      * Userid.....
     C                   eval      user = zhbgetvar('user')
     C                   EVAL      UsrSpcName  = zhbgetvar('UsrSpcName')
     C                   eval      WSKN    = zhbgetvar('WSKN')
     C                   eval      BSKN    = c2n2(WSKN)
     C                   eval      WSAVD    = zhbgetvar('WSAVD')
     C                   EVAL      WBAVD    = c2n2(WSAVD)
     C                   eval      WSOPD    = zhbgetvar('WSOPD')
     C                   EVAL      WBOPD    = c2n2(WSOPD)
      * Checkboks for Vis alle
      *    ved KLIKK på checkboks kommer verdi Y/blank,
      *    ellers kommer verdi fra siste valg via "VisAlle" som checked / blank(N)
     C     VisAchecked   Ifeq      'checked'
     C                   eval      VisAlle  = 'Y'
     C                   else
     C                   eval      VisAlle  = zhbgetvar('VisAlle')
     C                   end
     C                   endsr
      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   CLOSE     BRIDF
     C                   CLOSE     EPOPOS
     C                   CLOSE     EPOH
     C                   CLOSE     EPOL
     C                   CLOSE     HEADF
     C                   CLOSE     TURER14
     C                   endsr
      *=====================================================================
      * Read output skeleton html member into memory
      *=====================================================================
     C     LoadHtml      begsr
cccccC                   eval      IfsMultIndicators = gethtmlifsmult(
cccccC                             '/syspeddev/html/eSpedTop.html -
cccccC                             /syspeddev/html/EPOMS16.html -
cccccC                             /syspeddev/html/eSpedBot.html':
cccccC                             '/CGITAG/')
     C                   endsr
      *=====================================================================
      *  Set variable data for section /$top
      *=====================================================================
     C     SetTop        begsr
      *
N01  c                   eval      Title='POMS Purchase Order Management System'
N01  C                   callp     updhtmlvar('TITLE':Title)
     c
     C                   callp     updHTMLvar('ROWHEAD':RowHead)
     C                   callp     updHTMLvar('LogoImg':LogoImg)
     C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updHTMLvar('USER':USER)
     C                   callp     updHtmlVar('UsrSpcName':UsrSpcName)
     C                   callp     updHTMLvar('BESK':BIBESK)
     C                   callp     updHtmlVar('KUNDE':
     C                             %trim(%editc(BIKUNR:'Z')))
     C                   callp     updHTMLvar('KNAVN':BIBESK)
     C                   callp     updHTMLvar('WSAVD':WSAVD)
     C                   callp     updHTMLvar('WSOPD':WSOPD)
     c*
     C     HeaKey        CHAIN     HEADF
     C     HEPRO         CHAIN     TURER14
     C                   callp     updHtmlVar('TUCON1':TUCON1)
     C                   callp     updHtmlVar('TUBLNR':TUBLNR)
     C                   callp     updHtmlVar('TUBNV':TUBNV)
N09  C                   callp     updhtmlvar('TUPRO':
N09  C                             %trim(%editc(TUPRO:'Z')))
     C                   callp     updHtmlVar('HENAK':HENAK)
     C                   callp     updHTMLvar('TUETD':
     C                             %trim(%editc(TUETD:'Z')))
     C                   callp     updHTMLvar('TUATD':
     C                             %trim(%editc(TUATD:'Z')))
     C                   callp     updHTMLvar('TUETA':
     C                             %trim(%editc(TUETA:'Z')))
     C                   endsr
      *=====================================================================
      *  Set variable data for section /$row
      *=====================================================================
     C     Setrow        begsr
      *
      * HEADER/PO LEVEL
     C                   callp     updHTMLvar('EPPOS':
     C                             %trim(%editc(EPPOS:'Z')))
     C                   callp     updHTMLvar('EHBES':EHBES)
     C                   callp     updHTMLvar('ELLIN':
     C                             %trim(%editc(ELLIN:'Z')))
     C                   callp     updHTMLvar('ELULIN':
     C                             %trim(%editc(ELULIN:'Z')))
     c                   z-add     EHDATO        DATO
     C                   callp     updHTMLvar('DATO':
     C                             %trim(%editc(DATO:'Z')))
     C                   callp     updHTMLvar('EHLNAV':EHLNAV)
     C                   callp     updHTMLvar('EHBLNR':EHBLNR)
     C                   callp     updHTMLvar('EHORIG':EHORIG)
     c*                  eval      From = EHFLK +'-'+EHFSTE
N02  C                   IF        ELFSTE = *BLANKS
     c                   eval      From = EHFSTE
N02  C                   ELSE
N02  c                   eval      From = ELFSTE
N02  C                   END
     C                   callp     updHTMLvar('FROM':FROM)
     c*                  eval      To   = EHTLK +'-'+EHTSTE
N02  C                   IF        ELTSTE = *BLANKS
     c                   eval      To   = EHTSTE
N02  C                   ELSE
N02  c                   eval      To   = ELTSTE
N02  C                   END
     C                   callp     updHTMLvar('EHTRKO':EHTRKO)
     C                   callp     updHTMLvar('TO':TO)
     c                   z-add     EHVKT         ZZVKT
     C                   callp     updHTMLvar('EHVKT':
     C                             %trim(%editc(ZZVKT:'Z')))
     c                   z-add     EHM3          ZZM3
     C                   callp     updHTMLvar('EHM3':
     C                             %trim(%editc(ZZM3:'Z')))
     c                   select
     c     EHSTA         wheneq    ' '
     c     EHSTA         oreq      '0'
     c                   eval      STATUS = 'Pending'
     c     EHSTA         wheneq    '1'
     c                   eval      STATUS = 'Contacted'
     c     EHSTA         wheneq    '15'
     c                   eval      STATUS = 'Ready for stuffing'
     c     EHSTA         wheneq    '2'
     c                   eval      STATUS = 'Booked'
     c     EHSTA         wheneq    '3'
     c                   eval      STATUS = 'Confirmed'
     c     EHSTA         wheneq    '4'
     c                   eval      STATUS = 'Shipped'
     c     EHSTA         wheneq    '5'
     c                   eval      STATUS = 'Arrived'
     c     EHSTA         wheneq    '6'
     c                   eval      STATUS = '6*Undefined*'
     c                   endsl
N03  c                   if        EHOPD = -9999999
N03  c                   eval      STATUS= '**Deleted**'
N03  c                   endif
     C                   callp     updHTMLvar('STATUS':STATUS)
      *
     c                   select
     c     EHREAS        wheneq    ' '
     c                   eval      REASON = *blanks
     c     EHREAS        wheneq    'xxx'
     c                   eval      REASON = EHREAS + ' xxxxxxxxxxxxx'
     c     EHREAS        wheneq    'AAAA'
     c                   eval      REASON = EHREAS + ' YYYYYYYYYYYYY'
     c     EHREAS        wheneq    'XYX..'
     c                   eval      REASON = EHREAS + ' ZZZZZZZZZZZZZ'
     c                   other
     c                   eval      REASON = EHREAS
     c                   endsl
     C                   callp     updHTMLvar('REASON':REASON)
      * LINE LEVEL
     C                   callp     updHTMLvar('ELLIN':
     C                             %trim(%editc(ELLIN:'Z')))
     C                   callp     updHTMLvar('ELULIN':
     C                             %trim(%editc(ELULIN:'Z')))
     C                   callp     updHTMLvar('ELART':ELART)
     C                   callp     updHTMLvar('ELARTL':ELARTL)
     C*                  callp     updHTMLvar('ETD':
     C*                            %trim(%editc(ELETD:'Z')))
     C                   callp     updHTMLvar('ELABES':ELABES)
     C                   callp     updHTMLvar('ELBLNR':ELBLNR)
     C                   callp     updHTMLvar('ELORIG':ELORIG)
     C                   callp     updHTMLvar('ELNTSL':
     C                             %trim(%editc(ELNTSL:'Z')))
     c                   z-add(H)  ELNTKL        XXNTKL            6 0
     C                   callp     updHTMLvar('ELNTKL':
     C                             %trim(%editc(xxNTKL:'Z')))
     c                   z-add     ELVKTL        ZZVKT
     C                   callp     updHTMLvar('ELVKTL':
     C                             %trim(%editc(ZZVKT:'4')))
     C                   callp     updHTMLvar('ELM3L':
     C                             %trim(%editc(ELM3L:'4')))
     c                   select
N04  C                   WHEN      EHOPD <> 0 AND EHOPD <> 9999999 AND
N04  C                             EHOPD <> -9999999 AND ELOPD = 0
N04  c                   eval      STATUSL= STATUS
     c     ELSTA         wheneq    ' '
     c     ELSTA         oreq      '0'
     c                   eval      STATUSL= 'Pending'
     c     ELSTA         wheneq    '1'
     c                   eval      STATUSL= 'Contacted'
     c     ELSTA         wheneq    '15'
     c                   eval      STATUSL= 'Ready for stuffing'
     c     ELSTA         wheneq    '2'
     c                   eval      STATUSL= 'Booked'
     c     ELSTA         wheneq    '3'
     c                   eval      STATUSL= 'Confirmed'
     c     ELSTA         wheneq    '4'
     c                   eval      STATUSL= 'Shipped'
     c     ELSTA         wheneq    '5'
     c                   eval      STATUSL= 'Arrived'
     c     ELSTA         wheneq    '6'
     c                   eval      STATUSL= '6*Undefined*'
     c                   endsl
N03  c                   if        ELOPD = -9999999
N03  c                   eval      STATUSL= '**Deleted**'
N03  c                   endif
     C                   callp     updHTMLvar('STATUSL':STATUSL)
      *
     c                   select
     c     ELREAS        wheneq    ' '
     c                   eval      REASONL= *blanks
     c     ELREAS        wheneq    'xxx'
     c                   eval      REASONL= ELREAS + ' xxxxxxxxxxxxx'
     c     ELREAS        wheneq    'AAAA'
     c                   eval      REASONL= ELREAS + ' YYYYYYYYYYYYY'
     c     ELREAS        wheneq    'XYX..'
     c                   eval      REASONL= ELREAS + ' ZZZZZZZZZZZZZ'
     c                   other
     c                   eval      REASONL= ELREAS
     c                   endsl
     C                   callp     updHTMLvar('REASONL':REASONL)
     C                   endsr
      *
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
     C                   OPEN      BRIDF
     C     USER          CHAIN(N)  BRIDF                              50
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
      *
     C                   OPEN      EPOPOS
     C                   OPEN      EPOH
     C                   OPEN      EPOL
     C                   OPEN      HEADF
     C                   OPEN      TURER14
      *
      * hent Parametre som går igjen i mange prog:
      *      Odd/Even/Rowhead-farger,Logobilde,Språk,Intern/Ekstern bruker,  mm
     c                   call      'ESGETPAR'
     c                   parm                    BIBRID
     c                   parm                    BIFIRM
     c                   parm                    BIKUNR
     c                   parm                    BIKNG
     c                   parm                    RowHead          10
     c                   parm                    Odd              10
     c                   parm                    Even             10
     c                   parm                    LogoImg          50
     c                   parm                    XSPRÅK            2
     c                   parm                    XINTERN           1
     c                   parm                    ParmDS1        1024
     c                   parm                    ParmDS2        1024
     c                   parm                    ParmDS3        1024
      *
     c     HeaKey        klist
     C                   kfld                    WBAVD
     C                   kfld                    WBOPD
     c     EPOHK         klist
     C                   kfld                    EPKUND
     c                   kfld                    EPBES
     c     EPOLK         klist
     C                   kfld                    EPKUND
     c                   kfld                    EPBES
     c                   kfld                    EPLIN
     c                   kfld                    EPULIN
      *
     C                   endsr
      *
