********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10244 * 01 YBC PTF10 VARENR ER UTVIDERT FRA 15A TIL 28A
      *              MYE ENDRING. INGEN MERKING
10262 * 02 YBC PTF10 SATS ER UTVIDERT TIL 7.2, INGEN MERKING
10286 * 03 YBC PTF10 VEKT ER ENDRET FRA 5.0 TIL 12.3
********************************************************************
     A                                      DSPSIZ(27 132 *DS4)
     A                                      PRINT
     A                                      CF03(03 'Avslutt program')
     A                                      HLPDOC('STD' 'HELPNU' 'HLP400')
     A                                      HELP
     A                                      ALTHELP
      *
     A          R BILDE01
     A                                      CF04(04 'Søk kundenr.')
     A                                  1  2'SYSPED/SAD004'
     A                                      COLOR(BLU)
     A                                  1 20MSGCON(035 DIV0015 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  2 60TIME
     A                                      COLOR(BLU)
     A                                 11  2MSGCON(010 DIV0027 *LIBL/SYMSGF)
     A            WSKUNR         8Y 0B 11 13DSPATR(UL)
     A  99                                  DSPATR(PC)
     A  99                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                 26  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 26 22MSGCON(018 DIV0636 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 27  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  5  2VALUES(' ' '2' '4')
     A  98                                  DSPATR(RI)
     A  98                                  DSPATR(PC)
     A            SLKNR          8  0H
     A            SLALFA        28   O  5  5TEXT('ALFASØKING')
     A            SLTXT2        20   O  5 34TEXT('BESKRIVELSE')
     A            SLR31          1A  O  5 56TEXT('OM TEKST TIL RUBR. 31')
     A            SLOPPL         2   O  5 59TEXT('OPPRINGELSE LAND')
     A            SLTANR         8  0O  5 62TEXT('TARIFFNR.')
     A                                      EDTCDE(4)
N03  A  66                                  DSPATR(RI)
     A            SLTAR          3A  H
     A            SLTN           1   O  5 71TEXT('TOLLNEDSETTELSE')
     A            SLPRE          1   O  5 84TEXT('PREFERANSE')
S03  A*           SLVEKT         5  0O  5 83TEXT('VEKT')
N03  A            SLVEKT        12  3O  5 76TEXT('VEKT')
     A                                      EDTCDE(4)
     A            SLPVA          1   O  5 90TEXT('PVA')
S02  A*           SLSATS         5  2O  5 92TEXT('PVA SATS')
N02  A            SLSATS         7  2O  5 93TEXT('PVA SATS')
     A                                      EDTCDE(4)
S02  A*           SLMFR          1   O  5 99TEXT('MOMS')
N02  A            SLMFR          1   O  5102TEXT('MOMS')
S02  A*           SLKDAÆ         2A  H      TEXT('AVGIFTSKODE')
S02  A*           SLKDSÆ         3A  H      TEXT('AVGIFTS SEKVENS')
N02  A            SLKDAÆ         2A  O  5105TEXT('AVGIFTSKODE')
N02  A            SLKDSÆ         3A  O  5108TEXT('AVGIFTS SEKVENS')
     A            SLTO          45A  H      TEXT('TILLEGGSOPPLYSNINGER')
     A            SLCREF         3A  H      TEXT('KOMMUNIKASJONREF.')
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0019)
     A                                      SFLPAG(0018)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                  1  2'SYSPED/SAD004'
     A                                      COLOR(BLU)
     A                                  1 20MSGCON(040 DIV0015 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 65DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  2  2MSGCON(010 DIV0018 *LIBL/SYMSGF)
     A            WSKUNR         8  0O  2 20EDTCDE(4)
     A                                      COLOR(WHT)
     A            KNAVN         30   O  2 30COLOR(WHT)
     A                                  2 65TIME
     A                                      COLOR(BLU)
     A            ZRECNO         4S 0O  2 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  3  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 34MSGCON(030 DIV0017 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(002 DIV0025 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  4  5MSGCON(120 YBC1823 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
      *
     A          R BUNN
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                      CF12(12 'FORRIGE')
     A                                      CF24(24 'FLERE TASTER')
     A                                      SLNO(26)
     A                                      CLRL(02)
     A                                      OVERLAY
     A                                  1  2MSGCON(18 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A                                  1 22MSGCON(18 DIV0021 SYMSGF)
     A                                      COLOR(BLU)
     A                                  1 42MSGCON(18 DIV0022 SYMSGF)
     A                                      COLOR(BLU)
     A                                  1 62MSGCON(18 DIV0023 SYMSGF)
     A                                      COLOR(BLU)
     A                                  1 82MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  2  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R VARELI
     A                                      CF12(12 'Forrige')
     A                                      SLNO(22)
     A                                      CLRL(06)
     A                                  1  5MSGCON(120 YBC1823 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            WSALFA        28A  B  2  5TEXT('ALFASØKING')
     A                                      DSPATR(UL)
     A  40                                  DSPATR(HI)
     A  40                                  DSPATR(PR)
     A N40N30
     AO 89                                  DSPATR(PC)
     A  89                                  DSPATR(RI)
     A            WSTXT2        20A  B  2 34TEXT('BESKRIVELSE')
     A  40N30                               DSPATR(PC)
     A                                      DSPATR(UL)
     A            WSR31          1A  B  2 56TEXT('TEKST TIL RUBR. 31')
     A                                      DSPATR(UL)
     A                                      VALUES(' ' 'J' 'N')
     A            WSOPPL         2A  B  2 59TEXT('OPPRINGELSE LAND')
     A                                      DSPATR(UL)
     A  88                                  DSPATR(PC)
     A  88                                  DSPATR(RI)
     A            WSTANR         8Y 0B  2 62TEXT('TARIFFNR.')
     A                                      DSPATR(UL)
     A  87                                  DSPATR(PC)
     A  87                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A            WSTN           1A  B  2 71TEXT('TOLLNEDSETTELSE')
     A                                      DSPATR(UL)
     A            WSPRE          1A  B  2 74TEXT('PREFERANSE')
     A                                      DSPATR(UL)
S03  A*           WSVEKT         5Y 0B  2 76TEXT('VEKT')
N03  A            WSVEKT        12Y 3B  2 76TEXT('VEKT')
     A                                      DSPATR(UL)
     A  86                                  DSPATR(PC)
     A  86                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A            WSPVA          1A  B  2 90TEXT('TOLL')
     A                                      DSPATR(UL)
S02  A*           WSSATS         5Y 2B  2 85TEXT('PVA SATS')
N02  A            WSSATS         7Y 2B  2 93TEXT('PVA SATS')
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
S02  A*           WSMFR          1A  B  2 92TEXT('MOMS FRI')
N02  A            WSMFR          1A  B  2102TEXT('MOMS FRI')
     A                                      DSPATR(UL)
N02  A            WSKDAÆ         2A  B  2105TEXT('AVGIFTSKODE')
N02  A                                      DSPATR(UL)
N02  A            WSKDSÆ         3A  B  2108TEXT('AVGIFT SEKVENS')
N02  A                                      DSPATR(UL)
     A                                  3  5'T.O'
     A                                      COLOR(WHT)
     A            WSTO          45A  B  3  9TEXT('TILLEGGSOPPLYSNINGER')
     A                                      DSPATR(UL)
     A  81                                  DSPATR(PC)
     A  81                                  DSPATR(RI)
S02  A*           WSCREF         3A  B  3 55TEXT('KOMMUNIKASJONREF.')
N02  A                                  3 55'Ref.'
N02  A            WSCREF         3A  B  3 60TEXT('KOMMUNIKASJONREF.')
     A                                      DSPATR(UL)
     A  82                                  DSPATR(PC)
     A  82                                  DSPATR(RI)
S02  A*                                 3 63'Avg'
S02  A*                                     COLOR(WHT)
S02  A*           WSKDAÆ         2A  B  3 67TEXT('AVGIFTSKODE')
S02  A*                                     DSPATR(UL)
S02  A* 83                                  DSPATR(PC)
S02  A* 83                                  DSPATR(RI)
S02  A*           WSKDSÆ         3A  B  3 70TEXT('AVGIFT SEKVENS')
S02  A*                                     DSPATR(UL)
S02  A* 84                                  DSPATR(PC)
S02  A* 84                                  DSPATR(RI)
     A                                  5  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5 22MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  6  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R VAREL2
     A                                      CF12(12 'Forrige')
     A                                      SLNO(24)
     A                                      CLRL(04)
     A                                  2  2MSGCON(12 DIV0026 SYMSGF)
     A            W2ALFA        28A  I  2 16DSPATR(UL)
     A                                      DSPATR(PC)
     A                                  3  2MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
      *
     A          R VAREL3
     A                                      CF12(12 'Forrige')
     A                                      SLNO(24)
     A                                      CLRL(04)
     A                                  1  5MSGCON(120 YBC1823 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            SLALFA        28A  O  2  5TEXT('ALFASØKING')
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                      DSPATR(PC)
     A            SLTXT2        20A  O  2 34TEXT('BESKRIVELSE')
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            SLR31          1A  O  2 56TEXT('TEKST TIL RUBR. 31')
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            SLOPPL         2A  O  2 59TEXT('OPPRINGELSE LAND')
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            SLTANR         8Y 0O  2 62TEXT('TARIFFNR.')
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
     A            SLTN           1A  O  2 71TEXT('TOLLNEDSETTELSE')
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            SLPRE          1A  O  2 74TEXT('PREFERANSE')
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
S03  A*           SLVEKT         5Y 0O  2 76TEXT('VEKT')
N03  A            SLVEKT        12Y 3O  2 76TEXT('VEKT')
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
     A            SLPVA          1A  O  2 90TEXT('TOLL')
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
S02  A*           SLSATS         5Y 2O  2 85TEXT('PVA SATS')
N02  A            SLSATS         7Y 2O  2 93TEXT('PVA SATS')
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
S02  A*           SLMFR          1A  O  2 92TEXT('MOMS FRI')
N02  A            SLMFR          1A  O  2102TEXT('MOMS FRI')
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
N02  A            SLKDAÆ         2A  B  2105TEXT('AVGIFTSKODE')
N02  A                                      DSPATR(HI)
N02  A                                      DSPATR(UL)
N02  A            SLKDSÆ         3A  B  2108TEXT('AVGIFT SEKVENS')
N02  A                                      DSPATR(HI)
N02  A                                      DSPATR(UL)
     A                                  3  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A          R NYVARE
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                      CF12(12 'FORRIGE')
     A                                  1  2'SYSPED/SAD004'
     A                                      COLOR(BLU)
     A                                  1 20MSGCON(040 DIV0015 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 62DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  2  2MSGCON(010 DIV0018 *LIBL/SYMSGF)
     A            WSKUNR         8  0O  2 20EDTCDE(4)
     A                                      COLOR(WHT)
     A            KNAVN         30   O  2 30COLOR(WHT)
     A                                  2 62TIME
     A                                      COLOR(BLU)
     A                                  3  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 34MSGCON(030 DIV0017 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(002 DIV0025 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  4  5MSGCON(120 YBC1823 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
