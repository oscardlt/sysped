     FSVEH      UF   E           K DISK
     FSVEHE     UF   E           K DISK
     FSVTHA     IF   E           K DISK
     FEDIC      UF   E             DISK
     FEDIM      O  A E             DISK
     FSVTFI     IF   E             DISK
      *//////////////
      *  Hodelogikk /
      *//////////////
     C                   EXSR      SRA
     C                   EXSR      SRB
     C                   MOVE      '1'           *INLR
      *////////////////////////////////////////////////////////////
      *  Initiering                                           SRA /
      *////////////////////////////////////////////////////////////
     C     SRA           BEGSR
      *____________________
      * Definiere variabler
      *""""""""""""""""""""
     C     *LIKE         DEFINE    SVEH_SYAV     ZVEH_SYAV
     C     *LIKE         DEFINE    SVEH_SYOP     ZVEH_SYOP
     C     *LIKE         DEFINE    SVEH_SGDK     ZVEH_SGDK
     C     *LIKE         DEFINE    SVEH_SGUT     ZVEH_SGUT
      *_________________
      * Input parametere
      *"""""""""""""""""
     C     *ENTRY        PLIST
     C                   PARM                    ZVEH_SYAV
     C                   PARM                    ZVEH_SYOP
      *__________________
      * Definiere Nøkkler
      *""""""""""""""""""
     C     SVEH_KEY      KLIST
     C                   KFLD                    ZVEH_SYAV
     C                   KFLD                    ZVEH_SYOP
      *_________________________________
      * Hæmta dagens datum och klockslag
      *"""""""""""""""""""""""""""""""""
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
      *________________________
      * Hæmta firmaupplysningar
      *""""""""""""""""""""""""
     C     1             CHAIN     SVTFI                              51
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Bearbeide Formater                                   SRB /
      *////////////////////////////////////////////////////////////
     C     SRB           BEGSR
     C     SVEH_KEY      CHAIN(N)  SVEH                               90
     C                   EVAL      ZVEH_SGDK=SVEH_SGDK
     C                   EVAL      ZVEH_SGUT=SVEH_SGUT
      * Sigiller via keypad eller automatsik ?
     C     SVEH_SYSG     CHAIN     SVTHA
     C*    SVTH_DKEY     IFNE      *BLANKS
     C*                  EXSR      SRCC
     C*                  ENDIF
     C     SVEH_KEY      CHAIN     SVEH                               90
     C                   EVAL      SVEH_SGDK=ZVEH_SGDK
     C                   EVAL      SVEH_SGUT=ZVEH_SGUT
     C                   UPDATE    SVEHR
      * Slett edifact
     C     SVEH_KEY      SETLL     SVEHE
     C     SVEH_KEY      READE     SVEHE                                  51
     C     *IN51         DOWEQ     *OFF
     C                   DELETE    SVEHER
     C     SVEH_KEY      READE     SVEHE                                  51
     C                   ENDDO
      * Lag edifact pånytt med riktig sigill
     C     SVEH_SGDK     IFNE      *BLANKS
     C     SVEH_SGDK     ANDNE     '000000'
     C                   MOVEL     SVEH_MTYP     WVIH_MTYP1        1
     C                   SELECT
     C     SVEH_MTYP     WHENEQ    'UBK'
     C                   CALL      'SVE022RK'
     C                   PARM                    ZVEH_SYAV
     C                   PARM                    ZVEH_SYOP
     C                   PARM                    ZVEH_SGDK
     C                   PARM                    ZVEH_SGUT
     C     SVEH_MTYP     WHENEQ    'UGO'
     C     SVEH_MTYP     OREQ      'UKO'
     C                   CALL      'SVE022RH'
     C                   PARM                    ZVEH_SYAV
     C                   PARM                    ZVEH_SYOP
     C                   PARM                    ZVEH_SGDK
     C                   PARM                    ZVEH_SGUT
     C                   OTHER
     C                   CALL      'SVE022RN'
     C                   PARM                    ZVEH_SYAV
     C                   PARM                    ZVEH_SYOP
     C                   PARM                    ZVEH_SGDK
     C                   PARM                    ZVEH_SGUT
     C                   ENDSL

     C                   ENDIF
      *_____________
      * Ændra status
      *"""""""""""""
     C     SVEH_KEY      CHAIN     SVEH                               52
     C                   EVAL      SVEH_SYST='A'
     C                   UPDATE    SVEHR
      *_____________________________
      * Hæmta næsta meddelandenummer
      *"""""""""""""""""""""""""""""
     C     1             CHAIN     EDIC                               52
     C                   ADD       1             CMN
     C                   UPDATE    EDICR
      *_______________
      * Skriv til EDIM
      *"""""""""""""""
     C                   CLEAR                   EDIMR
     C                   EVAL      M0004=SVTF_0004
     C                   EVAL      M0010=SVTF_0010
     C                   EVAL      M0035=SVTF_0035
     C                   MOVEL     SVEH_SYAV     W0062            11
     C                   MOVE      SVEH_SYOP     W0062
     C                   EVAL      M0062=W0062
     C                   EVAL      M0065='CUSDEC'
     C                   EVAL      M1001=SVEH_MTYP
     C                   EVAL      M1004=SVEH_TUID
     C                   EVAL      M1225=SVEH_MTYP
     C                   EVAL      M1N07='SVE'
     C                   EVAL      MMN=CMN
     C                   EVAL      MSR='S'
     C                   MOVEL     WDT           MDT
     C                   MOVEL     WTM           MTM
     C                   EVAL      MAVD=SVEH_SYAV
     C                   EVAL      MTDN=SVEH_SYOP

     C                   WRITE     EDIMR

     C     SRB9          TAG
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Berækna utstællarsigill                             SRCC /
      *////////////////////////////////////////////////////////////
     C     SRCC          BEGSR
      * Send oppdrag til sigillering
     C                   MOVEL     ZVEH_SYAV     QTFIEL
     C                   MOVE      ZVEH_SYOP     QTFIEL
     C                   CALL      'QSNDDTAQ'
     C                   PARM      'SVGSRQ'      QTNAME           10
     C                   PARM      '*LIBL'       QTLIB            10
     C                   PARM      11            QTLEN             5 0
     C                   PARM                    QTFIEL           11
      * Sigillering
     C                   CALL      'SEALCTL'
     C                   PARM      'SVG003RE  '  QPARM            10
      * Retur fra sigillering
     C                   MOVEL     ZVEH_SYAV     QKEY
     C                   MOVE      ZVEH_SYOP     QKEY
     C                   CALL      'QRCVDTAQ'
     C                   PARM      'SVGSSQ'      QRNAME           10
     C                   PARM      '*LIBL'       QRLIB            10
     C                   PARM      6             QRLEN             5 0
     C                   PARM                    ZVEH_SGUT
     C                   PARM      99            QWAIT             5 0
     C                   PARM      'EQ'          QORDER            2
     C                   PARM      11            QKILEN            3 0
     C                   PARM                    QKEY             11
     C                   PARM      10            QSNDRL            3 0
     C                   PARM                    QSNDR            10
     C                   ENDSR
