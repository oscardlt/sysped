     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HELP
     A                                      ALTHELP(CA01)
      * "NOERASE" MÅ HA FOR Å IKKE TØMME BILDE SOM MAN KOMME FRA
     A          R NOERASE
     A                                      ASSUME
     A                                  1  2' '
      * MAN KUN DEFINNERE VINDU PÅ REKORDNIVÅ
     A          R WINDOWSZ
     A                                      WINDOW(*DFT 12 65 *NOMSGLIN)
     A                                      OVERLAY
      *
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  4  2VALUES(' ' '2' '4')
     A  27                                  DSPATR(PR)
     A            TVTDT          6A  O  4  5
     A            TVTDR         20A  O  4 12
     A            TVTDSK         2A  O  4 33
     A            TVTDO         26A  O  4 36
     A            TVTDOS         2A  O  4 63
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0006)
     A                                      SFLPAG(0005)
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A                                      CA03(03 'Forrige')
     A                                      CA05(05 'Frisk')
     A                                      CA06(06 'Lag ny')
     A                                      CA12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            ZRECNO         4S 0O  2 28SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SVX023R'
     A                                      COLOR(BLU)
     A                                  1 10'Underhåll tidigare dok(rubr. 40)'
     A                                      COLOR(WHT)
     A N27                              2  2'2=Ændra 4=Radera'
     A                                      COLOR(BLU)
     A                                  3  2'Op Tidigare dokument  typ/ref/SK  -
     A                                      Upplysningar               SK'
     A                                      COLOR(WHT)
      *
     A          R BUNN
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A                                  9  2'   '
     A                                 11  2'F3/12=Avbryt F5=Uppdatera F6=Skapa-
     A                                       ny'
     A                                      COLOR(BLU)
     A  30        MSGTXT        30A  O 12  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R VARELI
     A                                      WINDOW(WINDOWSZ)
     A                                      CF04(04 'Søk')
     A                                      CA12(12 'Forrige')
     A                                      SLNO(09)
     A                                      CLRL(04)
     A                                      OVERLAY
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'   Tidigare dokument  typ/ref/SK  -
     A                                      Upplysningar               SK'
     A                                      COLOR(WHT)
     A            WSTDT          6A  B  2  5DSPATR(UL)
     A  90                                  DSPATR(RI)
     A            WSTDR         20A  B  2 12DSPATR(UL)
     A  89                                  DSPATR(RI)
     A            WSTDSK         2A  B  2 33DSPATR(UL)
     A  88                                  DSPATR(RI)
     A            WSTDO         26A  B  2 36DSPATR(UL)
     A            WSTDOS         2A  B  2 63DSPATR(UL)
     A  87                                  DSPATR(RI)
     A                                  3  2'F12=Avbryt'
     A                                      COLOR(BLU)
     A  30        MSGTXT        30A  O  4  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R VAREL3
     A                                      WINDOW(WINDOWSZ)
     A                                      CA12(12 'Forrige')
     A                                      SLNO(09)
     A                                      CLRL(04)
     A                                      OVERLAY
     A                                  1  2'   Tidigare dokument  typ/ref/SK  -
     A                                      Upplysningar               SK'
     A                                      COLOR(WHT)
     A            TVTDT          6A  O  2  5DSPATR(UL)
     A                                      DSPATR(HI)
     A                                      DSPATR(PC)
     A            TVTDR         20A  O  2 12DSPATR(UL)
     A                                      DSPATR(HI)
     A            TVTDSK         2A  O  2 33DSPATR(UL)
     A                                      DSPATR(HI)
     A            TVTDO         26A  O  2 36DSPATR(UL)
     A                                      DSPATR(HI)
     A            TVTDOS         2A  O  2 63DSPATR(UL)
     A                                      DSPATR(HI)
     A N30                              3  2'F12=Avbryt'
     A                                      COLOR(BLU)
      *
     A          R NYWRK
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A                                      CA03(03 'Forrige')
     A N27                                  CF04(04 'Søking')
     A                                      CA05(05 'Frisk')
     A                                      CA12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SVX023R'
     A                                      COLOR(BLU)
     A                                  1 10'Underhåll tidigare dok(rubr. 40)'
     A                                      COLOR(WHT)
     A N27                              2  2'2=Ændra 4=Radera'
     A                                      COLOR(BLU)
     A                                  3  2'Op Tidigare dokument  typ/ref/SK  -
     A                                      Upplysningar               SK'
     A                                      COLOR(WHT)
     A N30                             11  2'F3/12=Avbryt F5=Uppdatera F6=Skapa-
     A                                       ny'
     A                                      COLOR(BLU)
