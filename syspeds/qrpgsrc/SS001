      *********************************************************************
      * Arbeid med plukkliste
      * PROGRAM BEHANDLER INPUT FRA SYMN1
      *
CRTPG+*  CRTPGM SYSPED/SS001 MODULE(*LIBL/SS001
CRTPGM*        *LIBL/CHECKUSR *LIBL/INCGI) ACTGRP(CGI) AUT(*USE)
      *
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 YBC PTF10 KUN SPØR PÅ BEGRENSET BÅTKODER
10XXX * 02 YBC PTF10 ERSTATTER & MED BLANK PÅ KUNDENAVN
10XXX * 03 YBC PTF10 VIS BÅTKODER VIA GRUPPE
********************************************************************
      *
      *********************************************************************
      * MAIN PROGRAM FLOW
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
     FCUNDL01   IF   E           K DISK    USROPN
     FWSSORF    UF   E           K DISK    USROPN
     FBRPARF    UF   E           K DISK    USROPN
     FBRIDF     UF   E           K DISK    USROPN
     FCDBOATLF  IF   E           K DISK    USROPN
N01  F                                     RENAME(CDBOATR:CDBOATRL)
N01  FCDBOAT    IF   E           K DISK    USROPN
     FGSSCGF    IF   E           K DISK    USROPN
N03  FGSSCGL01  IF   E           K DISK    USROPN
N03  F                                     RENAME(GSSCGFR:GSSCGFR1)
     FGSSORL29  IF   E           K DISK    USROPN
     Fsted2     IF   E           K DISK    USROPN
     FBATGR     IF   E           K DISK    USROPN
     FBATGRLF   IF   E           K DISK    USROPN
     F                                     RENAME(BATGRR:BATGR2)
      *--------------------------------------------------------------------
      * Prototype defintions and standard system API error structure
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      *--------------------------------------------------------------------
      * Variables common to CGI programs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
      * Variables received from the input string
OddEvD OddEven         s             10
     D IKKEOK          s              1a
     D wsboagr         s              7
     D lng             s              2
     D BckGnd          s             10
     D lstbrd          s              1
     D UsrSpcName      s             10
     D WSUSER          S             10
     D RDATF           S             10
     D RDATFN          S              8  0
     D RDATT           S             10
     D RDATTN          S              8  0
     D SDATF           S             10
     D SDATFN          S              8  0
     D SDATT           S             10
     D SDATTN          S              8  0
     D WINT            S              6
     D WINT2           S              6  0
     D PGM             C                   CONST('SYSPED/CHECKUSR')
      * Variables received from the input string
      * and converted to numeric
     D zeros           s             15a   inz('000000000000000')
      * Number of records matching search criteria
     DRecordCnt        s              5S 0
      *
     D Lib             s             10
     D Fn              s             10
     D Mbr             s             10
      *
     D Spaces          s             12a   inz('&nbsp;&nbsp;')
N02  D ST1             C                   CONST('&')
N02  D ST2             C                   CONST(' ')
N01  D                 DS
N01  D  BCD                    1     50    DIM(10)
N01  D  BCD50                  1     50
      *
      *--------------------------------------------------------------------
      * Prolog common to CGI programs
      * -Receive input from the remote browser
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prolog3
      *=====================================================================******
      * MAIN PROCESS LINE
      *=====================================================================******
      * Get program start time for calculating execution time
     C                   time                    timedata1
      * Parse input
     C                   exsr      Parse
      *
     C                   EXSR      INIT
     C                   CALLB     PGM
     C                   PARM                    BIBRID
     C                   PARM                    UsrSpcName
     C                   PARM                    UINN              1
     C                   IF        UINN = 'N'
     C                   GOTO      SLUTT
     C                   END
     C     WSUSER        CHAIN     BRIDF                              50
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVE      WDT           BIDTF
     C                   MOVE      WTM           BITIDF
     C                   UPDATE    BRIDFR
      * Set output skeleton html member name
     C                   exsr      SetSkl
      * Read skeleton output html, etc.
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
      * Retrieve environment variables
     C                   exsr      RtvEnvVar
      *------------------                                                   ******
      * Write HTML sections
      *
      * 1-Section /$top
     C                   exsr      SetTop
     C                   callp     wrtsection('top')
      *
     C                   exsr      SetFound
      *
      * skjekker om det finnes grupper
      *
     c     wsboagr       ifeq      *blanks
     C                   exsr      gruppetst
     c                   end
      *
      * hvis grupper finnes skrives FORM = FORMG
      *
N01  c                   IF        BCD50 = *BLANKS
     c     grujn         ifeq      'J'
     c     wsboagr       orne      *blanks
     C                   callp     wrtsection('foundg')
     c                   exsr      gruppe
     c                   end
N01  c                   END
      *
      * lager sectbuttom for båtkodene
      *
     C                   callp     wrtsection('found')
N01  c                   IF        BCD50 = *BLANKS
     C                   exsr      HENTBKD
N01  c                   ELSE
N01  C                   exsr      HENTBKD2
N01  c                   END
     c                   exsr      locsr
     C                   callp     wrtsection('bunn')
     C                   EXSR      TØMWFIL
      * 6-Section /$endhtml
     C                   exsr      SetEnd
     C     SLUTT         TAG
     C                   callp     wrtsection('endhtml')
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
     C                   EXSR      EXIT
     C                   return
      *=====================================================================******
      * Parse input
      *=====================================================================******
     C     Parse         begsr
     C                   eval      IKKEOK     = zhbgetvar('IKKEOK    ')
     C                   eval      wsboagr   = zhbgetvar('wsboagr')
     C                   eval      wsboagr = uppify(wsboagr)
     C                   eval      lng     = zhbgetvar('lng')
     C                   eval      BckGnd  = zhbgetvar('bckgnd')
     C                   eval      lstbrd  = zhbgetvar('lstbrd')
     C                   eval      WSUSER  = zhbgetvar('USER')
     C                   eval      WINT    = zhbgetvar('WINT')
     C                   eval      WINT2   = c2n2(WINT)
     C                   IF        WINT2   = 0
     C                   eval      WINT2    = random(1:999999)
     C                   MOVE      WINT2         WINT
     C                   END
     C                   EVAL      UsrSpcName  = zhbgetvar('UsrSpcName')
      * Ved Nytt søk foreslås evt dato-filter beholdt.
     C                   eval      RDATF   = zhbgetvar('RDATF')
     C                   eval      RDATFN  = c2n2(RDATF)
     C                   eval      RDATT   = zhbgetvar('RDATT')
     C                   eval      RDATTN  = c2n2(RDATT)
     C                   eval      SDATF   = zhbgetvar('SDATF')
     C                   eval      SDATFN  = c2n2(SDATF)
     C                   eval      SDATT   = zhbgetvar('SDATT')
     C                   eval      SDATTN  = c2n2(SDATT)
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$top
      *  (search criteria)
      *=====================================================================******
     C     SetTop        begsr
      * Repeat national language for the next invocation
     C                   callp     updhtmlvar('LNG':lng:
     C                             InitHTMLVars)
      * Color for text, background, links, visited links, active links
     C                   callp     updhtmlvar('TXTCOLOR':'black')
     C                   callp     updhtmlvar('BOLD':' ')
     C                   callp     updhtmlvar('BGCOLOR':BIFARG)
     C                   callp     updhtmlvar('LINK':'0000FF')
     C                   callp     updhtmlvar('VLINK':'FF0000')
     C                   callp     updhtmlvar('ALINK':'00FF00')
      * KUNDE
     C                   callp     updhtmlvar('WSNA':KNAVN)
OddEvC                   callp     updHTMLvar('ROWHEAD':RowHead)
OddEvC                   callp     updHTMLvar('LogoImg':LogoImg)
BODY C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updHtmlVar('USER':WSUSER)
     C                   callp     updHtmlVar('WINT':WINT)
     C                   callp     updHtmlVar('UsrSpcName':UsrSpcName)
     C     RDATFN        Ifeq      *Zeros
     C                   callp     updHTMLvar('RDATF':' ')
     c                   else
     C                   callp     updHTMLvar('RDATF':
     C                             %trim(%editw(RDATFN:'    .  .  ')))
     c                   end
     C     RDATTN        Ifeq      *Zeros
     C                   callp     updHTMLvar('RDATT':' ')
     c                   else
     C                   callp     updHTMLvar('RDATT':
     C                             %trim(%editw(RDATTN:'    .  .  ')))
     c                   end
     C     SDATFN        Ifeq      *Zeros
     C                   callp     updHTMLvar('SDATF':' ')
     c                   else
     C                   callp     updHTMLvar('SDATF':
     C                             %trim(%editw(SDATFN:'    .  .  ')))
     c                   end
     C     SDATTN        Ifeq      *Zeros
     C                   callp     updHTMLvar('SDATT':' ')
     c                   else
     C                   callp     updHTMLvar('SDATT':
     C                             %trim(%editw(SDATTN:'    .  .  ')))
     c                   end
     C                   callp     updHTMLvar('sdato':
     C                             %trim(%editw(BIDTV:'    .  .  ')))
     C                   callp     updHTMLvar('stid':
     C                             %trim(%editw(BITIDV:'  .  .  ')))
     C                   callp     updHtmlVar('wsboagr':wsboagr)
      *
     C                   endsr
      *=====================================================================
      * Set html output variables for section /$found (display a boat)
      *=====================================================================
     C     SetFound      begsr
      *
      * Sett inn i HTMLvariabler
     C                   callp     updHtmlVar('USER':WSUSER)
     C                   callp     updHtmlVar('UsrSpcName':UsrSpcName)
     C                   callp     updhtmlvar('BIKUNR':
     C                             %trim(%editc(BIKUNR:'Z')))
     C                   callp     updHtmlVar('KNAVN':KNAVN)
      *
     C                   endsr
      *=====================================================================
      * Read output skeleton html member into memory
      *=====================================================================
     c***********************************************************************
     C     HENTBKD       begsr
     c***********************************************************************
     c     grukey        klist
     c                   kfld                    wsboagr
     c                   kfld                    CBKDB
     c     grukey2       klist
     c                   kfld                    CBKDB
      *
     C                   callp     updhtmlvar('CBNAB':'ALL')
     C                   callp     updhtmlvar('CBKDB':'ALL')
     C                   callp     wrtsection('typsltrow')
      *
     C     *LOVAL        SETLL     CDBOATLF
     C                   READ      CDBOATLF                               40
     C     *IN40         DOWEQ     '0'
     C                   SELECT
     C                   WHEN      XAGRP <> *BLANKS
     C                   IF        XAGRP = '**'
     C                   callp     updhtmlvar('CBNAB':CBNAB)
     C                   callp     updhtmlvar('CBKDB':CBKDB)
     C                   callp     wrtsection('typsltrow')
     C                   ELSE
N03  C     CBKN          SETLL     GSSCGL01
N03  C     CBKN          READE     GSSCGL01                               33
N03  C                   DOW       *IN33 = *OFF
N03  C                   IF        XAGRP = CGCG
N03  C                   callp     updhtmlvar('CBNAB':CBNAB)
N03  C                   callp     updhtmlvar('CBKDB':CBKDB)
N03  C                   callp     wrtsection('typsltrow')
N03  C                   LEAVE
N03  C                   END
N03  C     CBKN          READE     GSSCGL01                               33
N03  C                   ENDDO
     C                   END
     C                   WHEN      ((CBKN = BIKUNR) AND (CBLK <> '  '))
     c     wsboagr       ifne      *blanks
     c     grukey        chain     batgr                              33
     c     *in33         ifeq      '0'
     C                   callp     updhtmlvar('CBNAB':CBNAB)
     C                   callp     updhtmlvar('CBKDB':CBKDB)
     C                   callp     wrtsection('typsltrow')
     c                   end
     c                   else
     C                   callp     updhtmlvar('CBNAB':CBNAB)
     C                   callp     updhtmlvar('CBKDB':CBKDB)
     C                   callp     wrtsection('typsltrow')
     c                   end
     C                   WHEN      ((CBKN <> BIKUNR) AND (wsboagr <> '       '))
     c                   move      'N'           kuok              1
     c                   exsr      tstku2
     c     kuok          ifeq      'J'
     C                   callp     updhtmlvar('CBNAB':CBNAB)
     C                   callp     updhtmlvar('CBKDB':CBKDB)
     C                   callp     wrtsection('typsltrow')
     c                   end
     C                   WHEN      XCG <> *BLANKS
     C     CBKN          CHAIN     GSSCGF                             33
     C  N33              IF        ((XCG = CGCG) AND (CBLK <> '  '))
     C                   callp     updhtmlvar('CBNAB':CBNAB)
     C                   callp     updhtmlvar('CBKDB':CBKDB)
     C                   callp     wrtsection('typsltrow')
     C                   END
     C                   ENDSL
     C                   READ      CDBOATLF                               40
     C  N40              ENDDO
      *
     C                   callp     wrtsection('typsltend')
      *
     c                   endsr
N01   ***********************************************************************
N01  C     HENTBKD2      begsr
N01   ***********************************************************************
N01   *
N01  C                   callp     updhtmlvar('CBNAB':'ALL')
N01  C                   callp     updhtmlvar('CBKDB':'ALL')
N01  C                   callp     wrtsection('typsltrow')
N01   *
N01  C     1             DO        10            XN                3 0
N01  C                   IF        BCD(XN) = *BLANKS
N01  C                   LEAVE
N01  C                   END
N01  C                   EVAL      CBKDB = BCD(XN)
N01  C     CBKDB         CHAIN     CDBOAT                             33
N01  C  N33              IF        CBKN  = BIKUNR
N01  C                   callp     updhtmlvar('CBNAB':CBNAB)
N01  C                   callp     updhtmlvar('CBKDB':CBKDB)
N01  C                   callp     wrtsection('typsltrow')
N01  C                   END
N01  C                   ENDDO
N01   *
N01  C                   callp     wrtsection('typsltend')
N01   *
N01  c                   endsr
N01   *
     c****************************************************************************
     c     locsr         begsr
     c****************************************************************************
      * Selection box for Locations
     C                   callp     wrtsection('locstart')
     C                   IF        XALOC = *BLANKS
     C                   callp     updhtmlvar('LOCKOD':'ALL')
     C                   callp     updhtmlvar('LOCNAV':'ALL')
     C                   callp     wrtsection('LOCsltrow')
     C     GSSORL29K     SETGT     GSSORL29
     C     BIKUNR        READE     GSSORL29                               40
     C     *IN40         DOWEQ     '0'
     c     ORLC          chain     sted2                              33
     c                   eval      LOCNAV = ORLC
     c  n33              CAT       '-':1         LOCNAV           40
     c  n33              CAT       st2nvn:1      LOCNAV
     C                   callp     updhtmlvar('LOCKOD':ORLC)
     C                   callp     updhtmlvar('LOCNAV':LOCNAV)
     C                   callp     wrtsection('LOCsltrow')
     C     GSSORL29K     SETGT     GSSORL29
     C     BIKUNR        READE     GSSORL29                               40
     c                   end
      *
     C                   ELSE
      *
     C                   callp     updhtmlvar('LOCKOD':XALOC)
     C                   callp     updhtmlvar('LOCNAV':XALOC2)
     C                   callp     wrtsection('LOCsltrow')
     C                   END
      *
     C                   callp     wrtsection('LOCsltend')
      *
     C                   endsr
      *=====================================================================
      * TØM ARBEIDSFIL
      *=====================================================================
     C     TØMWFIL       begsr
      *
     C     WSUSER        SETLL     WSSORF
     C     WSUSER        READE     WSSORF                                 40
     C     *IN40         DOWEQ     '0'
     C                   DELETE    WSSORFR
     C     WSUSER        READE     WSSORF                                 40
     C  N40              ENDDO
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$endhtml
      *=====================================================================******
     C     SetEnd        begsr
      * Server protocol
     C                   callp     updhtmlvar('PROTOCOL':S_Protocol)
      * Compute run time
     C                   time                    timedata2
     C     timedata2     subdur    timedata1     ms:*ms
     C                   eval      sec = ms / 1000000
     C                   callp     updhtmlvar('runtime':
     C                             %trim(%editw(sec:'     0 .   ')))
      *
     C                   endsr
      *=====================================================================******
      * Set html output skeleton member before its read into memory
      *=====================================================================******
     C     SetSkl        begsr
      *------------------
      * Set html output skeleton member
     C                   eval      lng = uppify(lng)
      * "HTMLSRC  " er dft = NORSK, ved å trykke på FLAGGET i inng.
      * kan dette endre stil "HTMLSRCXX" avh. av støttede språk
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC' + lng
     C                   eval      Mbr = 'SS001'
      *------------------
     C                   endsr
      *=====================================================================******
      *  Retrieve environment variables
      *=====================================================================******
     C     RtvEnvVar     begsr
      * Use getenvp to get this server's protocol and name
     C                   eval      S_Protocol =getenv('SERVER_PROTOCOL':
     C                             qusec)
     C                   eval      S_Name     =getenv('SERVER_NAME':
     C                             qusec)
      *
     C                   endsr
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
     C                   OPEN      BRIDF
     C     WSUSER        CHAIN(N)  BRIDF                              50
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   CALLB     'INCGI'
     C                   PARM                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
OddEv * hent Parametre som går igjen i mange prog:
OddEv *      Odd/Even/Rowhead-farger,Logobilde,Språk,Intern/Ekstern bruker,  mm
OddEvc                   call      'ESGETPAR'
OddEvc                   parm                    BIBRID
OddEvc                   parm                    BIFIRM
OddEvc                   parm                    BIKUNR
OddEvc                   parm                    BIKNG
OddEvc                   parm                    RowHead          10
OddEvc                   parm                    Odd              10
OddEvc                   parm                    Even             10
OddEvc                   parm                    LogoImg          50
OddEvc                   parm                    XSPRÅK            2
OddEvc                   parm                    XINTERN           1
OddEvc                   parm                    ParmDS1        1024
OddEvc                   parm                    ParmDS2        1024
OddEvc                   parm                    ParmDS3        1024
     C                   open      BRPARF                               50
     C                   open      CUNDL01                              50
N01  C                   open      CDBOAT                               50
     C                   open      CDBOATLF                             50
     C                   open      BATGR                                50
     C                   open      BATGRLF                              50
     C                   open      WSSORF                               50
     C                   open      GSSCGF                               50
N03  C                   open      GSSCGL01                             50
     C                   open      GSSORL29                             50
     C                   open      sted2                                50
      *
     C     GSSORL29K     klist
     C                   kfld                    BIKUNR
     C                   kfld                    ORLC
     C     CUND1K        klist
     C                   kfld                    BIFIRM
     C                   kfld                    BIKUNR
     C     CUND1K        CHAIN     CUNDL01                            50
N02  C     ST1:ST2       XLATE     KNAVN         KNAVN
     C                   IF        BIKNG <> 0
     C     BIKNG         CHAIN     GSSCGF                             50
     C  N50              MOVE      CGCG          XCG               2
     C                   END
      *
     C                   MOVE      BIBRID        PABRID
     C                   MOVE      *zeros        PAKUNR
     C                   MOVE      'SSAGT'       PAID
     C     BrParKey      CHAIN(N)  BRPARF                             33
     C                   IF        *IN33 = *OFF
     C                   MOVE      '**'          XAGRP             2
     C                   MOVE      'SSKGR'       PAID
     C     BrParKey      CHAIN(N)  BRPARF                             33
     C  N33              MOVEL     PAVRDA        XAGRP
     C                   MOVE      'SSLOC'       PAID
     C     BrParKey      CHAIN(N)  BRPARF                             33
     C  N33              MOVEL     PAVRDA        XALOC             3
     C  N33              MOVE      PAVRDA        XALOC2           45
     C                   END
N01   *
N01  C                   MOVE      'SSBCD'       PAID
N01  C     BrParKey      CHAIN(N)  BRPARF                             33
N01  C  N33              MOVEL     PAVRDA        BCD50
N01  C   33              MOVEL     *BLANKS       BCD50
      *
     C                   endsr
      *=====================================================================******
      *  AVSLUTT
      *=====================================================================******
     C     EXIT          begsr
     C                   close     BRIDF
     C                   close     CUNDL01
     C                   close     BRPARF                               50
     C                   close     CDBOATLF
N01  C                   close     CDBOAT
     C                   close     BATGR
     C                   close     BATGRLF
     C                   close     WSSORF
     C                   close     GSSCGF
N03  C                   close     GSSCGL01
     C                   close     GSSORL29
     C                   close     sted2
     C                   eval      *inlr = *on
      *
     C                   endsr
      *=====================================================================
      * Read output skeleton html member into memory
      *=====================================================================
     C********************************************************************
     C     GRUPPE        begsr
     C********************************************************************
     C                   setoff                                       40
     C     BATGRKEY      klist
     C                   kfld                    wsboagr
     c                   move      wsboagr       wsboagrf          7
     C                   callp     updhtmlvar('wsboagr':wsboagr)
     C                   callp     updhtmlvar('wsboagrn':'           ')
     C                   callp     wrtsection('typsltrowgr')
     c                   eval      wsboagr=*blanks
     c     wsboagrf      ifne      *blanks
     C                   callp     updhtmlvar('wsboagr':wsboagr)
     C                   callp     updhtmlvar('wsboagrn':'           ')
     C                   callp     wrtsection('typsltrowgr')
     c                   end
      *
     C     BATGRKEY      SETLL     BATGR
     C     *IN40         DOWEQ     '0'
     C                   READ      BATGR                                  40
     c     *in40         ifeq      '0'
     c     bagrup        andne     wsboagrf
     C     BAkunr        ifeq      bikunr
     C                   callp     updhtmlvar('wsboagr':bagrup)
     C                   callp     updhtmlvar('wsboagrn':banavn)
     C                   callp     wrtsection('typsltrowgr')
     C     BAGRUP        SETGT     BATGR
     c                   else
     c                   exsr      kutst
     C                   END
     C                   END
     C  N40              ENDDO
     c                   move      wsboagrf      wsboagr
     C                   callp     updHtmlVar('wsboagr':wsboagr)
      *
     C                   callp     wrtsection('typsltendgr')
     C                   endsr
      *
     C********************************************************************
     C     GRUPPETST     begsr
     C********************************************************************
      * skjekker først om kunde har grupper
     C                   setoff                                       40
     C                   move      'N'           grujn             1
     C     BATGRKEY      SETLL     BATGR
     C     *IN40         DOWEQ     '0'
     C                   READ      BATGR                                  40
     c     *in40         ifeq      '0'
     C     BAkunr        ifeq      bikunr
     C                   move      'J'           grujn
     C                   end
     C     BAGRUP        SETGT     BATGR
     C                   end
     C  N40              ENDDO
     C                   endsr
     c*******************************************************************
     c     kutst         begsr
     c*******************************************************************
     c                   setoff                                       34
     C     BrParKey      klist
     C                   kfld                    PABRID
     C                   kfld                    PAKUNR
     C                   kfld                    PAID
     C                   MOVE      BIBRID        PABRID
     C                   MOVE      *zeros        PAKUNR
     c
     C                   MOVE      'SSCUS'       PAID
     C     BrParKey      setll     BRPARF
     c     *in34         doweq     '0'
     C     BrParKey      reade     BRPARF                                 34
     c  n34pavrdn        ifeq      bakunr
     C                   callp     updhtmlvar('wsboagr':bagrup)
     C                   callp     updhtmlvar('wsboagrn':banavn)
     C                   callp     wrtsection('typsltrowgr')
     c                   goto      utku
     c                   end
     c                   end
     c     utku          tag
     C     BAGRUP        SETGT     BATGR
     c                   endsr
     c*******************************************************************
     c     tstku2        begsr
     c*******************************************************************
     c                   setoff                                       3435
     C                   MOVE      BIBRID        PABRID
     C                   MOVE      *zeros        PAKUNR
     c
     C                   MOVE      'SSCUS'       PAID
     C     BrParKey      setll     BRPARF
     c     *in34         doweq     '0'
     C     BrParKey      reade     BRPARF                                 34
     c  N34pavrdn        ifeq      cbkn
     c     wsboagr       ifne      *blanks
     c     grukey        chain     batgr                              35
     C                   ELSE
     C     GRUKEY2       CHAIN     BATGRLF                            35
     c                   end
     C  n35              move      'J'           kuok
     c  n35              goto      utku2
     c                   end
     c                   end
     c     utku2         tag
     c                   endsr
