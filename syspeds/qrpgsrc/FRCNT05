      *********************************************************************
      *  RPG ILE MODULE *LIBL/FRCNT05
      *
      *  After compiling this RPG MODULE,
      *  create the related program with the following command:
      *
CRTPG+*  CRTPGM SYSPED/FRCNT05 MODULE(*LIBL/FRCNT05)
CRTPGM*         ACTGRP(CGI) AUT(*USE)
      *
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
      * Order details
     FHEAD11    if   e           K DISK    USROPN
     FTURER1    if   e           K DISK    USROPN
      *=====================================================================
      * Includes to be used in all CGIs
      *=====================================================================
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
      *  Fields used for parsing the input string
     D lng             s              2a
     D Tur             s              8a
     D Turnr           s              8  0
     D Avd             s              4a
     D Avdnr           s              4  0
      *
     D htmlsrclib      s             10a
     D htmlsrcfil      s             10a
     D htmlsrcmbr      s             10a
      * "InitSW"- is blank only the first time pgm is called
     DInitSW           S              1a
      *
     D PNRSTED         s             30a
     D TOTPos          s              5s 0
     D TOTNT           s              5s 0
     D TotVKT          s              5s 0
     D TotM3           s              7s 3
     D RtvSW           s              1a
      *=====================================================================
     C                   exsr      SetTimeStr
      *=====================================================================
      * Read remote browser input string and parse it
      *=====================================================================
      /copy syspeds/qrpgsrcpy,prolog3
     C                   exsr      Parse
      *=====================================================================
      * Main line
      *=====================================================================
      * Ask the service program to load into core
      * the appropriate output skeleton html member
     C                   exsr      LoadHtml
      * Open database files
     C                   exsr      OpenDbf
      * Start response html
     C                   callp     wrtsection('top')
     C                   eval      wrotetop = *on                               For pssr
      *------------------
      * Hvis avdeling er gitt
      * list åpne turer på avdelingen
     C                   if        Avdnr  > 0
     C                   exsr      ListTur
     C                   endif
      * Hvis tur for planlegging er valgt
      * list innhold kortversjon.
     C                   if        Turnr  > 0
     C                   exsr      ListItems
     C                   endif
      * =================
      * Flush the html buffer and exit
     C                   exsr      Exit
      *
      *  END OF MAIN PROG
      *
      *=====================================================================
      * List the order items
      *=====================================================================
     C     ListItems     begsr
     C                   eval      TotPos = 0
     C                   eval      TotNT  = 0
     C                   eval      TotVKT  =0
     C                   eval      TotM3   =0
     C                   callp     updhtmlvar('TUR':
     C                             %trim(%editc(TURNR:'Z')))
     C                   callp     wrtsection('tabstr')
      *----------------
     C     TurNr         setll     Head11
      *
     C     TurNr         reade     Head11
     C                   dow       not%eof
     C                   exsr      SetTabRow
     C                   callp     wrtsection('tabrow')
     C     TurNr         reade     Head11
     C                   enddo
      *
     C                   exsr      SetTabEnd
     C                   callp     wrtsection('tabend')
      *
     C                   endsr
      *=====================================================================
      * Set variables in html section "tabrow"
      *=====================================================================
     C     SetTabRow     begsr
     C                   movel     HELKA         LKF               2
     C                   movel     HETRI         LKT               2
     C                   eval      PNRSTED   = 'Fra: '+LKF+' '+ HESDF +
     C                             '  Til: '+LKT+' '+ HESDT
     C                   callp     updHTMLvar('PNRSTED':PNRSTED)
      *
      * Quantity
     C                   callp     updhtmlvar('ANt':
     C                             %trim(%editc(HeNT:'Z')))
     C                   callp     updhtmlvar('VKT':
     C                             %trim(%editc(Hevkt:'Z')))
     C                   callp     updhtmlvar('M3':
     C                             %trim(%editc(HEM3:'3')))
      * Score for bottom line
     C                   eval      TotPos = TotPos + 1
     C                   eval      TotNT  = TotNT  + HENT
     C                   eval      TotVKT = TotVKT + Hevkt
     C                   eval      TotM3  = TotM3  + HeM3
      *
     C                   endsr
      *=====================================================================
      * Set variables in html section "tabend"
      *=====================================================================
     C     SetTabEnd     begsr
      * Total posisjoner
     C                   callp     updhtmlvar('TOTPos':
     C                             %trim(%editc(TotPos:'3')))
      * Total kolli
     C                   callp     updhtmlvar('TOTNT':
     C                             %trim(%editc(TotNt:'Z')))
      * Total vekt
     C                   callp     updhtmlvar('TOTVKT':
     C                             %trim(%editc(TotVKT:'Z')))
      * Total vekt
     C                   callp     updhtmlvar('TOTM3':
     C                             %trim(%editc(TotM3:'3')))
      *
     C                   endsr
      *=====================================================================
      * List TURER
      *=====================================================================
     C     ListTur       begsr
     C                   callp     updhtmlvar('AVD':
     C                             %trim(%editc(AvdNR:'Z')))
     C                   callp     wrtsection('avturtabstr')
      *----------------
     C     TurKey        setll     TURER1
      *
     C     TurKey        reade     TURER1
     C                   dow       not%eof
     C                   exsr      SetTabRowAT
     C                   callp     wrtsection('avturtabrow')
     C     TurKey        reade     TURER1
     C                   enddo
      *
     C                   callp     wrtsection('avturtabend')
      *
     C                   endsr
      *=====================================================================
      * Set variables in html section "Avturtabrow"
      *=====================================================================
     C     SetTabRowAT   begsr
     C                   MOVE      TUPRO         TUPROA            8
     C                   callp     updHTMLvar('TUPRO':TUPROA)
     C                   callp     updHTMLvar('TUBILN':TUBILN)
     C                   callp     updhtmlvar('TUAO':
     C                             %trim(%editc(TUAO:'Z')))
     C                   callp     updhtmlvar('TUTVKT':
     C                             %trim(%editc(TUTVKT:'Z')))
     C                   callp     updhtmlvar('TUTM3':
     C                             %trim(%editc(TUTM3:'3')))
     C                   endsr
      *=====================================================================
      * Ask the service program to load into core
      * the appropriate output skeleton html member
      *=====================================================================
     C     LoadHtml      begsr
      * Read externally defined output html
     C                   eval      HtmlSrcLib = '*LIBL'
     C                   eval      HtmlSrcFil = 'HTMLSRC' + lng
     C                   eval      HtmlSrcMbr = 'FRCNT05'
     C                   callp     gethtml(HtmlSrcFil:HtmlSrcLib:HtmlSrcMbr:
     C                             '/CGITAG/')
      *
     C                   endsr
      *=====================================================================
      * Open database files
      *=====================================================================
     C     OpenDbf       begsr
     c     TurKey        KLIST
     C                   kfld                    BlStat            1
     C                   kfld                    AvdNr
      *
     C                   call      'INCGIFULL'
     C                   open      HEAD11
     C                   open      TURER1
      *
     C                   endsr
      *=====================================================================
      * Close database files
      *=====================================================================
     C     CloseDbf      begsr
     C                   close     HEAD11
     C                   close     TURER1
     C                   endsr
      *=====================================================================
      *  Time started
      *=====================================================================
     C     SetTimeStr    begsr
     C                   time                    TimeStart
     C                   endsr
      *=====================================================================
      *  Time ended and time elapsed elapsed
      *=====================================================================
     C     SetTimeEnd    begsr
     C                   time                    TimeEnd
     C     TimeEnd       subdur    TimeStart     MsElaps:*ms
     C                   eval      SecElaps = MsElaps / 1000000
     C                   endsr
      *=====================================================================
      * Send response html and quit
      *=====================================================================
     C     Exit          begsr
      * Close database files
     C                   exsr      CloseDbf
      *
     C                   exsr      SetTimeEnd
     C                   callp     updhtmlvar('RUNTIME':
     C                             %trim(%editw(SecElaps:'     0 .   ')))
     C                   callp     wrtsection('runtime')
     C                   callp     wrtsection('end')
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   return
     C                   endsr
      *=====================================================================
      * Parse the input string
      *=====================================================================
     C     Parse         begsr
     C                   eval      lng        = zhbgetvar('lng       ')
     C                   eval      tur        = zhbgetvar('tur       ')
     C                   eval      Turnr   = c2n2(tur)
     C                   eval      avd        = zhbgetvar('avd       ')
     C                   eval      Avdnr   = c2n2(avd)
     C                   endsr
