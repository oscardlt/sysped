********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 CB  PTF10 Hent foretaksnummer isteden for EORI
********************************************************************
     FSVIH      IF   E             DISK
     FSVIV      IF   E           K DISK
N01  FSVIF2     IF   E           K DISK
     FSVI202W   UF A E           K DISK
     FQ         O  A F  256        DISK
     D                 DS
     D  DATOFN                 1      8S 0
     D  DATOTN                 9     16S 0
     D                 DS
     D  WVIV_FABL              1      9
      *____________________________________
      * DS for leftjustify numericel fields
      *""""""""""""""""""""""""""""""""""""
     D                 DS
     D  ØN152                  1     15S 2
     D  ØA152H                 1     13
     D  ØA152X                 5     13
     D  ØA152D                14     15
     D                 DS
     D  ØA12                   1     12
     D  Ø12                    1     12
     D                                     DIM(12)
      *//////////////
      *  Hodelogikk /
      *//////////////
     C                   EXSR      SRA
     C                   EXSR      SRB
     C                   MOVE      '1'           *INLR
      *////////////////////////////////////////////////////////////
      *  Initiering                                           SRA /
      *////////////////////////////////////////////////////////////
     C     SRA           BEGSR
      *_________________
      * Input parametrer
      *"""""""""""""""""
     C     *ENTRY        PLIST
     C                   PARM                    UORG             12
     C                   PARM                    UKVART            4
     C                   PARM                    UNAMN            30
     C                   PARM                    UTEL             15
     C                   PARM                    UEPOST           40
     C                   PARM                    UDATOF            8
     C                   PARM                    UDATOT            8
     C                   MOVEL     UDATOF        DATOFN
     C                   MOVEL     UDATOT        DATOTN
      *__________________
      * Definiere Nøkkler
      *""""""""""""""""""
     C     KEYA          KLIST
     C                   KFLD                    SVIH_SYAV
     C                   KFLD                    SVIH_SYOP
N01  C     KEYF          KLIST
N01  C                   KFLD                    SVIH_SYAV
N01  C                   KFLD                    SVIH_SYOP
N01  C                   KFLD                    WVIF_FATY
N01  C     KEYF2         KLIST
N01  C                   KFLD                    SVIH_SYAV
N01  C                   KFLD                    SVIH_SYOP
N01  C                   KFLD                    WVIF_FATY2
     C     KEYW          KLIST
     C                   KFLD                    SVIH_MOEO
N01  C     *LIKE         DEFINE    SVIF_FATY     WVIF_FATY
N01  C     *LIKE         DEFINE    SVIF_FATY     WVIF_FATY2
N01  C                   EVAL      WVIF_FATY='2MEG'
N01  C                   EVAL      WVIF_FATY2='Y041'
     C                   MOVE      *BLANKS       ØAN              12
     C                   Z-ADD     0             ØX                2 0
      *______________________________
      * Slett alle poster i arbetsfil
      *""""""""""""""""""""""""""""""
     C                   EVAL      *IN53=*OFF
     C     *LOVAL        SETLL     SVI202W
     C                   READ      SVI202W                                53
     C     *IN53         DOWEQ     *OFF
     C                   DELETE    SVI202WR
     C                   READ      SVI202W                                53
     C                   ENDDO
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Start av file                                        SRB /
      *////////////////////////////////////////////////////////////
     C     SRB           BEGSR
     C                   MOVEL     UORG          UREC            256
     C                   CAT       ';':0         UREC
     C                   CAT       UKVART:0      UREC
     C                   CAT       ';':0         UREC
     C                   CAT       UNAMN:0       UREC
     C                   CAT       ';':0         UREC
     C                   CAT       UTEL:0        UREC
     C                   CAT       ';':0         UREC
     C                   CAT       UEPOST:0      UREC
     C                   CAT       ';':0         UREC
     C                   EXCEPT    UTH
     C                   READ      SVIH                                   51
     C     *IN51         DOWEQ     *OFF
     C     SVIH_SYDT     IFGE      DATOFN
     C     SVIH_SYDT     ANDLE     DATOTN
     C     SVIH_EUP1     IFEQ      '4200'
     C     SVIH_SYST     IFEQ      'G'
     C                   EXSR      SRBA
     C                   ENDIF
     C                   ENDIF
     C                   ENDIF
      * Læs næsta varulinje
     C                   READ      SVIH                                   51
     C  N51              ENDDO
      *________________________
      * Legg ut alle varelinjer
      *""""""""""""""""""""""""
     C                   EVAL      *IN53=*OFF
     C     *LOVAL        SETLL     SVI202W
     C                   READ      SVI202W                                53
     C     *IN53         DOWEQ     *OFF
     C                   MOVE      *BLANKS       UREC
     C                   MOVEL     SVI2_MOEO     UREC
     C                   CAT       ';':0         UREC
     C                   EVAL      ØN152=SVI2_FABL
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           WVIV_FABL
     C                   CAT       WVIV_FABL:0   UREC
     C                   CAT       ';':0         UREC
     C                   EXCEPT    UTD
     C                   READ      SVI202W                                53
     C                   ENDDO
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Les varelinjer                                      SRBA /
      *////////////////////////////////////////////////////////////
     C     SRBA          BEGSR
N01   * Hent foretaksnummer fra egen fil.
N01  C     KEYF          CHAIN     SVIF2                              55
N01  C   55KEYF2         CHAIN     SVIF2                              55
N01  C                   IF        *IN55=*OFF
N01  C                   MOVEL     SVIF_FATX     SVIH_MOEO
N01  C                   ENDIF

     C                   EVAL      *IN52=*OFF
     C     KEYA          SETLL     SVIV
     C     KEYA          READE     SVIV                                   52
     C     *IN52         DOWEQ     *OFF
     C     KEYW          CHAIN     SVI202W                            54
     C                   IF        *IN54
     C                   CLEAR                   SVI202WR
     C                   EVAL      SVI2_MOEO=SVIH_MOEO
     C                   EVAL      SVI2_FABL=SVIV_STVA
     C                   WRITE     SVI202WR
     C                   ELSE
     C                   ADD       SVIV_stva     SVI2_FABL
     C                   UPDATE    SVI202WR
     C                   ENDIF
     C     KEYA          READE     SVIV                                   52
     C  N52              ENDDO
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Venstrestill numeriske verdier                      SRNE /
      *////////////////////////////////////////////////////////////
     C     SRNE          BEGSR
      * Minus value ?
     C     ØN152         IFLT      0                                            <-------------I
     C                   Z-SUB     ØN152         ØN152                                        I
     C                   MOVE      '-'           ØSIGN             1                          I
     C                   ELSE                                                   <-------------I
     C                   MOVE      '+'           ØSIGN                                        I
     C                   ENDIF                                                  <-------------I
      * Flytte in rett verdi i Alfa 12-felt
     C                   MOVE      *ZEROS        ØA12
     C     ØA152D        IFEQ      '00'                                         <-------------I
     C                   MOVE      ØA152H        ØA12                                         I
     C                   ELSE                                                   <-------------I
     C                   MOVEL     ØA152X        ØA12                                         I
     C                   MOVE      ',  '         ØA12                                         I
     C                   MOVE      ØA152D        ØA12                                         I
     C                   END                                                    <-------------I
      * lete fra venstre og finn foerste siffer over 0
     C     1             DO        11            ØX                             <-------------I
     C     Ø12(ØX)       IFNE      '0'                                           <-----------II
     C     Ø12(ØX)       IFEQ      ','                                           <-----------II
     C                   SUB       1             ØX
     C                   ENDIF                                                   <-----------II
     C                   GOTO      SRNE1                                                     II
     C                   END                                                     <-----------II
     C                   END                                                    <-------------I
     C     SRNE1         TAG
      * Venstrestill til et nytt felt
     C                   MOVE      *BLANKS       ØAN
     C     ØSIGN         IFEQ      '-'                                          <-------------I
     C                   SUB       1             ØX                                           I
     C                   MOVE      '-'           Ø12(ØX)                                      I
     C                   ENDIF                                                  <-------------I
     C                   MOVEA     Ø12(ØX)       ØAN
      *
     C                   ENDSR
     OQ         EADD         UTH
     O                                            9 'SKV574005'
     O                                           10 ';'
     O          EADD         UTH
     O                       UREC               256
     O          EADD         UTD
     O                       UREC               256
