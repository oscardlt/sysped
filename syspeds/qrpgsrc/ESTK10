      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/ESTK10 MODULE(*LIBL/ESTK10 *LIBL/INCGI)
CRTPGM*         ACTGRP(*NEW) AUT(*USE)
      *
      *********************************************************************
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 JOV PTF10 kode Google = Y svar skal til google maps
10XXX * 02 JOV PTF10 Param for å gjøre cross domain kall
********************************************************************
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FTKAREA    IF   E           K DISK    USROPN
     FUNITl01   IF   E           K DISK    USROPN
     FDRIV      IF   E           K DISK    USROPN
      *********************************************************************
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      /copy syspeds/qrpgsrcpy,variables3
      *
     D SJ              S             50    DIM(1000)
     D UT              S              4    DIM(100)
      * Client input variables
     D user            s             10
N02  D CallBack        s           1000
N02  D Data            s           1000
     D LINTXT          s            256
N01  D GOOGLE          s              1
      *
     D Spaces          s             12a   inz('&nbsp;&nbsp;')
     D ItemCount       s             10i 0
     D i               s             10i 0
     D                 DS
     D  UNRETU                 1     30
     D  UNPDA                  1      4
     D  UNSJNR                 5     12
      /copy syspeds/qrpgsrcpy,prolog3
      *
      * Get program start time for calculating execution time
     C                   time                    timedata1
      * Parse input / cvt to numeric
     C                   exsr      Parse
      * File open / keys
     C                   EXSR      INIT
      * Read output skeleton html member into memory
     C                   exsr      LoadHtml
      * Set section variables
     C                   exsr      SetAll
      * Quit
     C                   exsr      Exit
      *
     C                   eval      *inlr = *on
     C                   return
      *
      *
      *=====================================================================
      * Parse input / cvt to numeric
      *=====================================================================
     C     Parse         begsr
      * Parse variables from QUERY_STRING environment variable
     C                   eval      user = zhbgetvar('user')
N01  C                   eval      Google = zhbgetvar('GOOGLE')
N02  C                   eval      CallBack = zhbgetvar('CallBack')
     C                   endsr
      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   CLOSE     BRIDF
     C                   CLOSE     TKAREA
     C                   CLOSE     UNITL01
     C                   CLOSE     DRIV
     C                   endsr
      *=====================================================================
      * Read output skeleton html member into memory
      *=====================================================================
     C     LoadHtml      begsr
N01  c     Google        ifne      'Y'
     C                   callp     gethtml('HTMLSRC':
     C                             '*LIBL':
     C                             'ESTK11':'/CGITAG/')
N01  c                   else
N01  C                   callp     gethtml('HTMLSRC':
N01  C                             '*LIBL':
N01  C                             'ESTK11G':'/CGITAG/')
N01  c                   end
     C                   endsr
      *=====================================================================
      *  Set variable data for section /$top , lin , Bot
      *=====================================================================
     C     SetAll        begsr
      * Send section top
     C                   callp     wrtsection('top')
      * cross domain oppkall ??
N02  c     CallBack      ifne      *blanks
N02  c                   eval      Data=%trim(CallBack)+'('
N02  C                   callp     updHTMLvar('LINTXT':Data)
N02  C                   callp     wrtsection('lin')
N02  C                   end
      *  1 = OMRÅDER
n01  C     Google        ifeq      'Y'
n01  C                   eval      Lintxt='{"parameters": {"areaCode":  ['
n01  C                   callp     updHTMLvar('LINTXT':LINTXT)
n01  c                   else
     C                   callp     updHTMLvar('LINTXT':'##OMRAADE')
n01  c                   end
     C                   callp     wrtsection('lin')
     C                   move      *Blanks       OMKODE
N01  C                   move      *zeros        AntCount          5 0
     C     OMKODE        setgt     TKAREA
     C                   READ      TKAREA                                 35
     C     *IN35         DOWEQ     *OFF
     C                   eval      LinTxt = OMKode+' '+ OMBesk
N01  C                   add       1             AntCount
N01  C     Google        ifeq      'Y'
N01  C                   eval      LinTxt = *blanks
N01  c     AntCount      ifgt      1
N01  C                   eval      LinTxt = ','
N01  c                   endif
N01  C                   eval      LinTxt = %trim(LinTxt)
N01  C                             +'{"value":"'+%trim(OMKode)+'",'
N01  C                             +' "text":"'+%trim(OMBesk)+'"}'
N01  c                   endif
     C                   callp     updHTMLvar('LINTXT':LINTXT)
     C                   callp     wrtsection('lin')
     C     OMKODE        setgt     TKAREA
     C                   READ      TKAREA                                 35
     C                   END
n01  C     Google        ifeq      'Y'
n01  C                   callp     updHTMLvar('LINTXT':'],')
n01  C                   callp     wrtsection('lin')
n01  c                   end
      *  2 = BILNUMMER
n01  C     Google        ifeq      'Y'
n01  C                   eval      Lintxt='"carNr":  ['
n01  C                   callp     updHTMLvar('LINTXT':LINTXT)
n01  c                   else
     C                   callp     updHTMLvar('LINTXT':'##BILNR')
n01  c                   end
     C                   callp     wrtsection('lin')
N01  C                   move      *zeros        AntCount
     C     *LOVAL        setLL     UNITL01
     C                   READ      UNITL01                                35
     C     *IN35         DOWEQ     *OFF
     C     UNPDA         IFEQ      'PDA:'
     C                   TESTN                   UNSJNR               33
     C     *IN33         IFEQ      *ON
     C                   MOVE      *BLANKS       DRNAVN
     C                   MOVE      UNSJNR        DRANSA
     C     DRANSA        CHAIN     DRIV                               33
      * PDA/SJÅF VIA ARRAY (skrives sortert lenger nede...)
     C                   ADD       1             Snr               5 0
     C                   eval      SJ(Snr) = 'PD'+UNSJNR+' '+ DRNAVN
      * skriv bil.
     C                   eval      LinTxt = UNBILN +'/ '+ SJ(Snr)
N01  C                   add       1             AntCount
N01  C     Google        ifeq      'Y'
N01  C                   eval      LinTxt = *blanks
N01  c     AntCount      ifgt      1
N01  C                   eval      LinTxt = ','
N01  c                   endif
N01  C                   eval      LinTxt = %trim(LinTxt)
N01  C                             +'{"value":"'+%trim(SJ(Snr))+'",'
N01  C                             +' "text":"'+%trim(UNBILN)+' '
N01  C                             +%trim(SJ(Snr))+'"}'
N01  c                   endif
     C                   callp     updHTMLvar('LINTXT':LINTXT)
     C                   callp     wrtsection('lin')
      * BILTYPE - KUN EN POST PR...    (skrives sortert lenger nede...)
     C     UNUNTY        IFNE      *BLANKS
     C     UNUNTY        LOOKUP    UT                                     41
     C  N41              ADD       1             Unr               5 0
     C  N41              MOVE      UNUNTY        UT(Unr)
     C                   END
     C                   END
     C                   END
     C                   READ      UNITL01                                35
     C                   END
n01  C     Google        ifeq      'Y'
n01  C                   callp     updHTMLvar('LINTXT':'],')
n01  C                   callp     wrtsection('lin')
n01  c                   end
      *
      * 3 - SJÅFØRER  (sorter= alle blanke kommer først...)
n01  C     Google        ifeq      'Y'
n01  C                   eval      Lintxt='"driverNr":  ['
n01  C                   callp     updHTMLvar('LINTXT':LINTXT)
n01  c                   else
     C                   callp     updHTMLvar('LINTXT':'##SJAAF')
n01  c                   end
     C                   callp     wrtsection('lin')
N01  C                   move      *zeros        AntCount
     C                   SORTA     SJ
     C                   EVAL      Snr  = 1001 - Snr
     C     Snr           DOWLE     1000
     C                   callp     updHTMLvar('LINTXT':SJ(Snr))
N01  C                   add       1             AntCount
N01  C     Google        ifeq      'Y'
N01  C                   eval      LinTxt = *blanks
N01  c     AntCount      ifgt      1
N01  C                   eval      LinTxt = ','
N01  c                   endif
n01  c                   movel     SJ(Snr)       XXSJAF           10
n01  c                   move      XXSJAF        DRANSA
n01  c                   move      SJ(Snr)       XXSJBesk         39
N01  C                   eval      LinTxt = %trim(LinTxt)
N01  C                             +'{"value":"'+%trim(XXSJAF)+'",'
N01  C                             +' "text":"'+%trim(%editc(DRANSA:'Z'))+' '
N01  C                             +%trim(XXSJBesk)+'"}'
N01  C                   callp     updHTMLvar('LINTXT':LINTXT)
N01  c                   endif
     C                   callp     wrtsection('lin')
     C                   ADD       1             Snr               5 0
     C                   END
n01  C     Google        ifeq      'Y'
n01  C                   callp     updHTMLvar('LINTXT':'],')
n01  C                   callp     wrtsection('lin')
n01  c                   end
      *
      * 4 - UNITTYPE  (sorter= alle blanke kommer først...)
n01  C     Google        ifeq      'Y'
n01  C                   eval      Lintxt='"carType":  ['
n01  C                   callp     updHTMLvar('LINTXT':LINTXT)
n01  c                   else
     C                   callp     updHTMLvar('LINTXT':'##UNITTYPE')
n01  c                   end
     C                   callp     wrtsection('lin')
N01  C                   move      *zeros        AntCount
     C                   SORTA     UT
     C                   EVAL      Unr  = 101 - Unr
     C     Unr           DOWLE     100
     C                   callp     updHTMLvar('LINTXT':UT(Unr))
N01  C                   add       1             AntCount
N01  C     Google        ifeq      'Y'
N01  C                   eval      LinTxt = *blanks
N01  c     AntCount      ifgt      1
N01  C                   eval      LinTxt = ','
N01  c                   endif
N01  C                   eval      LinTxt = %trim(LinTxt)
N01  C                             +'{"value":"'+%trim(UT(Unr))+'",'
N01  C                             +' "text":"'+%trim(UT(Unr))+'"}'
N01  C                   callp     updHTMLvar('LINTXT':LINTXT)
N01  c                   endif
     C                   callp     wrtsection('lin')
     C                   ADD       1             Unr               5 0
     C                   END
n01  C     Google        ifeq      'Y'
n01  C                   callp     updHTMLvar('LINTXT':']}}')
n01  C                   callp     wrtsection('lin')
n01  c                   end
     C
     C
N01  c     Google        ifne      'Y'
     C                   callp     wrtsection('bot')
n01  c                   end
N02  c     CallBack      ifne      *blanks
N02  C                   callp     updHTMLvar('LINTXT':')')
N02  C                   callp     wrtsection('lin')
N02  C                   end
     C
     C                   endsr
     C
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
     C                   OPEN      BRIDF
     C     USER          CHAIN     BRIDF                              50
      * Ugyldig userID
NUSR c     *in50         ifeq      '1'
NUSR C                   callp     gethtml('HTMLSRC':'*LIBL':'ERRUSR':
NUSR c                             '/CGITAG/')
NUSR C                   callp     updhtmlvar('User':USER)
NUSR C                   callp     wrtsection('all')
NUSR C                   callp     wrtsection('*fini')
NUSR C                   close     BRIDF
NUSR C                   eval      *inlr = *on
NUSR C                   return
NUSR c                   endif
NUSR  *
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
     C                   OPEN      TKAREA
     C                   OPEN      UNITL01
     C                   OPEN      DRIV
     C     UtInit        endsr
