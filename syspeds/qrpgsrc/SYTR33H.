      * OBS!!! innhente EDI-logikk far RAMBERG!!!
      ***********************************************************
      *** Håndtere henting                                    ***
      ***********************************************************
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10172 * 01 JOV PTF10 F21=SMARTPHONE HENTESMS,F22=SMARTPHONE-GENERELL URL
********************************************************************
     FSYTR33HD  CF   E             WORKSTN
     FFIRM      IF   E           K DISK
     FTRAN      IF   E           K DISK
xx   FCUNDC04   IF   E           K DISK
     FTRACKL02  UF   E           K DISK
     FDOKUFE    IF   E           K DISK
      *___________________________
      * Finner username
      *"""""""""""""""""""""""""""
     D                SDS
     D  ZJOB                 244    253
     D  ZUSER                254    263
     D  ZJOBNR               264    269  0
     D                 DS
     D  TTDATE                 1      8  0
     D   TTDM                  5      6
     D   TTDD                  7      8
     D                 DS
     D  TTTIME                 1      6  0
     D   TTTT                  1      2
     D   TTTM                  3      4
      *
     C                   EXSR      INIT
      *
     c     Start0        tag
     c                   exsr      HentDta
     c     Start         tag
     c                   exsr      HentDta2
     C                   EXFMT     BILDE
     C   03              GOTO      SLUTT
     C   12              GOTO      SLUTT
     C*
     c   05              goto      Start0
      *
     c     *IN04         ifeq      *ON
     C                   CALL      'SYTR04A'
     C                   PARM                    WSTRAN
     C                   GOTO      START
     C                   end
     C     *IN07         ifeq      *ON
     C                   CALL      'SYCTA01R'
     C                   PARM                    UAVD
     C                   PARM                    UOPD
     C                   PARM                    FBNR1
     C                   PARM      'CZ '         UQAULIF           3
     C                   PARM                    WSKNSM            8 0
     C                   GOTO      START
     C                   end
      * F11 ER TRACK&TRACE HENDELSESLOG
     C     *IN11         ifeq      *ON
     C                   Z-ADD     1             UTTFLG
     c                   z-add     0             UTTUR             8 0
     C                   CALL      'SYTT01CL'
     C                   PARM      UAVD          UTTAVD            4 0
     C                   PARM      UOPD          UTTOPD            7 0
     C                   PARM                    UTTUR
     C                   PARM                    UTTFLG            1 0
     C                   GOTO      START
     C                   END
      *
      * F08 ER send avs-mail
     C     *IN08         ifeq      *ON
     c     AvsMail       ifeq      *blanks
     c                   goto      start
     c                   endif
      *
     C                   move      Uavd          HentAvd
     C                   move      Uopd          HentOpd
     C                   eval      HenMail = AvsMail
     C                   eval      HenTLF  = AvsTLF
     C                   eval      HenSMS  = AvsSMS
     C                   eval      ANNEN   = ' '
     C                   eval      AnnSMS  = *blanks
     C                   MOVE      '00000000'    TRAA
     C                   eval      TRANAV  = *blanks
     C                   eval      Merknad = WSMERK
     C                   eval      HenUSR  = ZUSER
     c                   exsr      SendMail
     C                   endif
      *
      * F21 = send hente-sms (SMARTPHONE-APP)
S01  C*    *IN19         ifeq      *ON
N01  C     *IN21         ifeq      *ON
N01  C     *IN22         oreq      *ON
E01  c     WSTSMS        ifeq      *blanks
     C                   seton                                        30
     C*                  MOVE      'JOV1598'     MSGNR
     c                   goto      start
N01  c                   endif
     c                   endif
      * F09 ER send for Tildelt Transportør
     C     *IN09         ifeq      *ON
     c     WSTMail       ifeq      *blanks
     C                   seton                                        30
     C*                  MOVE      'JOV1598'     MSGNR
     c                   goto      start
     c                   endif
      * Transportør
     C     TranKey       CHAIN     TRAN                               33
     C     *in33         ifeq      '1'
     C                   seton                                        30
     C*                  MOVE      'JOV1598'     MSGNR
     c                   goto      start
     c                   endif
      * slett evt tidligere tildeling
     c                   exsr      DelPrev
      *
     C                   move      Uavd          HentAvd
     C                   move      Uopd          HentOpd
     C                   eval      HenMail = WSTMail
     C                   eval      HenTLF  = AvsTLF
     C                   eval      HenSMS  = AvsSMS
     C                   eval      ANNEN   = 'T'
     C                   eval      AnnSMS  = WSTSMS
     C                   MOVE      WSTRAN        TRAA
     C                   eval      TRANAV  = VMNAVN
     C                   eval      Merknad = WSMERK
     C                   eval      HenUSR  = ZUSER
     c                   exsr      SendMail
     C                   endif
      *
      * F10 ER send TIL ANNEN / AD HOC-PART
     C     *IN10         ifeq      *ON
     c     WSAMail       ifeq      *blanks
     C                   seton                                        30
     C*                  MOVE      'JOV1598'     MSGNR
     c                   goto      start
     c                   endif
      *
     C                   move      Uavd          HentAvd
     C                   move      Uopd          HentOpd
     C                   eval      HenMail = WSAMail
     C                   eval      HenTLF  = AvsTLF
     C                   eval      HenSMS  = AvsSMS
     C                   eval      ANNEN   = 'A'
     C                   eval      AnnSMS  = WSASMS
     C                   MOVE      '00000000'    TRAA
     C                   eval      TRANAV  = *blanks
     C                   eval      Merknad = WSMERK
     C                   eval      HenUSR  = ZUSER
     c                   exsr      SendMail
     C                   endif
      *
      *
S01  C*    *IN19         ifeq      *ON
N01  C     *IN21         ifeq      *ON
N01  C     *IN22         oreq      *ON
N01  C     *IN21         ifeq      *ON                                          21=Url Hent DETTE
N01  c                   move      'COL'         H_L
N01  c                   else                                                   22=Url gener/oppst.
N01  c                   move      *blanks       H_L
N01  c                   endif
     C                   move      Uavd          HentAvd
     C                   move      Uopd          HentOpd
     C                   eval      AnnSMS  = WSTSMS
     c                   call      'ESOP11HS'
     c                   parm                    HentAvd           4
     c                   parm                    HentOpd           7
s01  c*                  parm      'H'           H_L               1
n01  c                   parm                    H_L               3
     c                   parm                    AnnSMS           15
     c                   parm                    ZUSER            10
     c                   goto      slutt
     c                   endif
      * Ingen valg og ikke F12/F3 - vis på ny
     c                   if        *in08='0' and *in09='0' and *in10='0'
     c                             and *in19='0' and *in21='0' and *in22='0'
     c                   goto      start
     c                   endif
      *
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
     C***********************************************
     C     HentDta       BEGSR
     C***********************************************
     C* Finn hentestatus
N23  c                   call      'SYTR33HS'
N23  c                   parm                    UAVD
N23  c                   parm                    UOPD
N23  c                   parm      '  '          UHSTAT            2
     c                   movea     '000000000000'*in(50)
N23  c                   if        UHStat>='10' and UHStat<='99'
N23  c                   move      *on           *in50
N23  c                   select
N23  c     UHStat        wheneq    '10'
N23  c                   move      *on           *in51                      Rød=ikke klar
     c                   eval      HENSTAT='10 Ikke bekreftet klart'
N23  c     UHStat        wheneq    '20'
N23  c                   move      *on           *in52                      Pink=Klar for hentin
     c                   eval      HENSTAT='20 Bekretet klart for h'
N23  c     UHStat        wheneq    '30'
N23  c                   move      *on           *in53                      Gul =Tildelet transp
     c                   eval      HENSTAT='30 Tildelt transportør '
N23  c     UHStat        wheneq    '40'
N23  c                   move      *on           *in54                      Hvit=BekreftetTransp
     c                   eval      HENSTAT='40 Bekrefet transportør'
N23  c     UHStat        wheneq    '50'
N23  c                   move      *on           *in55                      Turkis=Hentet h.Avs
     c                   eval      HENSTAT='50 Hentet hos avsender '
N23  c     UHStat        wheneq    '60'
N23  c                   move      *on           *in56                      Blå=Ankommet TERM
     c                   eval      HENSTAT='60 Ankommet terminal   '
N23  c     UHStat        wheneq    '99'
N23  c                   move      *off          *in50                      TEI + på tur- OK!!
     c                   eval      HENSTAT='99 Ankommet/satt på tur'
N23  c                   endsl
N23  c                   endif
     C*
     c                   move      *blanks       CMS01
     c                   move      *blanks       CMS02
     c                   move      *blanks       CMS03
     c                   move      *zeros        WSTRAN
     c                   movea     '00'          *IN(72)
      * Vis inntil 3 sendte mail  (..++ om det finnes flere)  AVSENDER/ADHOC
     C                   eval      XXACTI ='CMS'
     c     TrackKEY      setgt     TRACKL02
     c     TrackKEY      readpe(N) TRACKL02                               33
     c     *in33         doweq     '0'
     c                   select
     c     CMS01         wheneq    *blanks
     c                   eval      CMS01=TTDD+'/'+TTDM+' '+TTTT+':'+TTTM+' '
     c                             +TTTEXT
     c     CMS02         wheneq    *blanks
     c                   eval      CMS02=TTDD+'/'+TTDM+' '+TTTT+':'+TTTM+' '
     c                             +TTTEXT
     c     CMS03         wheneq    *blanks
     c                   eval      CMS03=TTDD+'/'+TTDM+' '+TTTT+':'+TTTM+' '
     c                             +TTTEXT
     c                   other
     c                   move      '+..(se F11)' CMS03
     c                   leave
     c                   endsl
     c     TrackKEY      readpe(N) TRACKL02                               33
     c                   enddo
      * Tildel Transportør ?? (skal finnes kun 1 )
     C                   eval      XXACTI ='CAT'
     c     TrackKEY      setgt     TRACKL02
     c     TrackKEY      readpe(N) TRACKL02                               33
     c     *in33         ifeq      '0'
     c                   movea     '10'          *IN(72)                        Gul = Tildelt
     c                   movel     TTDEPO        WSTRAN
     c                   select
     c     CMS02         wheneq    *blanks
     c                   eval      CMS02=TTDD+'/'+TTDM+' '+TTTT+':'+TTTM+' '
     c                             +TTTEXT
     c     CMS03         wheneq    *blanks
     c                   eval      CMS03=TTDD+'/'+TTDM+' '+TTTT+':'+TTTM+' '
     c                             +TTTEXT
     c                   other
     c                   move      '+..(se F11)' CMS03
     c                   endsl
     c                   endif
      * Bekreftet av Transp  (skal finnes kun 1 )
     C                   eval      XXACTI ='CCT'
     c     TrackKEY      setgt     TRACKL02
     c     TrackKEY      readpe(N) TRACKL02                               33
     c     *in33         ifeq      '0'
     c                   movea     '01'          *IN(72)                        Hvit=Bekreftet
     c                   movel     TTDEPO        WSTRAN
     c                   endif
     c                   endsr
     C***********************************************
     C     HentDta2      BEGSR
     C***********************************************
      *
      * avsender mail mm
     C     DOKUFECZ      CHAIN     DOKUFE                             33
     C     *in33         ifeq      '0'
     c                   eval      AvsTlf       =  FE_C3148A
     c                   eval      AvsSMS       =  FE_C3148D
     c                   eval      AvsMAIL      =  FE_C3148E
     C                   else
     c                   eval      AvsTlf       =  *blanks
     c                   eval      AvsSMS       =  *blanks
     c                   eval      AvsMAIL      =  *blanks
     c                   endif
      *
     C     TranKey       CHAIN     TRAN                               33
     C     *in33         ifeq      '1'
     c                   eval      VMNAVN       =  *blanks
xx   c                   else
xx   c                   if        WSTMAIL=*blanks
xx   c     Tmailkey      chain     cundc04                            33
xx   c  N33              eval      WSTMAIL=CEMAIL
xx   c  N33              eval      WSTSMS =CMOBIL
xx   c                   endif
     c                   endif
      *
     C                   ENDSR
      *
     C***********************************************
     C     DelPrev       BEGSR
     C***********************************************
      * Fjern ev tidligere tildelinger (sett N sist i TTACTI)
     C                   eval      XXACTI ='CAT'
     c     TrackKEY      setgt     TRACKL02
     c     TrackKEY      readpe    TRACKL02                               33
     c     *in33         doweq     '0'
     c                   Move      'N'           TTACTI
     c                   update    TRACKFR
     c     TrackKEY      readpe    TRACKL02                               33
     c                   enddo
      * Fjern ev tidligere bekreftelser(sett N sist i TTACTI)
     C                   eval      XXACTI ='CCT'
     c     TrackKEY      setgt     TRACKL02
     c     TrackKEY      readpe    TRACKL02                               33
     c     *in33         doweq     '0'
     c                   Move      'N'           TTACTI
     c                   update    TRACKFR
     c     TrackKEY      readpe    TRACKL02                               33
     c                   enddo
      *
     C                   ENDSR
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C*
     C     *ENTRY        PLIST
     C                   PARM                    UAVD              4 0
     C                   PARM                    UOPD              7 0
     C     DOKUFECZ      KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    UOPD
     C                   KFLD                    FBNR1             3 0
     C                   KFLD                    PART              3
     C                   EVAL      FBNR1=1
     C                   EVAL      PART ='CZ '
     C     TrackKEY      KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    UOPD
     C                   KFLD                    TTFBNR
     C                   KFLD                    XXACTI            3
     C     TRANKey       KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    WSTRAN
xx   C     Tmailkey      KLIST
xx   C                   KFLD                    FIFIRM
xx   C                   KFLD                    VMTRKU
xx   C                   KFLD                    CTYPE
xx   C                   movel     '*BOOKING'    CTYPE
      *
     C     *LOVAL        SETLL     FIRM
     C                   READ      FIRM                                   33
     C*
     C                   ENDSR
     C******************************************************
     c     SendMail      BEGSR
     C******************************************************
     c                   call      'ESOP11H'
     c                   parm                    HentAvd           4
     c                   parm                    HentOpd           7
     c                   parm                    HenMail          50
     c                   parm                    HenTLF           15
     c                   parm                    HenSMS           15
     c                   parm                    ANNEN             1
     c                   parm                    AnnSMS           15
     c                   parm                    TRAA              8
     c                   parm                    TRANAV           20
     c                   parm                    Merknad          50
     c                   parm                    HenUSR           10
      *
     C                   ENDSR
