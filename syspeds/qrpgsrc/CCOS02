      *********************************************************************
      * SLETT MERKET SLETT-LINJER
      * PROGRAM BEHANDLER INPUT FRA EISO13
      *
CRTPG+*  CRTPGM SYSPED/CCOS02 MODULE(*LIBL/CCOS02 *LIBL/CCOS01B
CRTPGM*        *LIBL/CHECKUSR *LIBL/INCGI) ACTGRP(CGI) AUT(*USE)
      *
      *
      *********************************************************************
      * MAIN PROGRAM FLOW
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
      * Boats for sale data base file (LF on BOATSALE by boat serial number)
     FCCBLV1    UF   E           K DISK    USROPN
     FCCBLV1LF  UF   E           K DISK    USROPN
     F                                     RENAME(CCBLV1R:CCBLV1RLF)
     FBRIDF     UF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Prototype definitions and standard system API error structure
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      * Variables common to CGI programs
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
      * VARIABLES SPECIFIC TO THIS PROGRAM
      *--------------------------------------------------------------------
      * Variables received from the input string
     D lng             s              2
     D UsrSpcName      s             10

     D WSUSER          S             10
     D WSAVD           S              4
     D BSAVD           S              4  0
     D WSOPD           S              7
     D BSOPD           S              7  0
     D BSLI            S              3  0
     D Lib             s             10
     D Fn              s             10
     D Mbr             s             10
     D ItemCount       s             10i 0
     D SernoC          s            105a
     D WSSORT          S              3
     D BSSORT          S              3  0
     D SL              S              3  0 DIM(900)
     D i               s             10i 0
     D PGM             C                   CONST('SYSPED/CHECKUSR')
     D PGM2            C                   CONST('SYSPED/CCOS01B')
     D                 DS
     D XXREF                   1     24
     D XXAVD                   1      4
     D XXOPD                   5     11
     D XXLI                   12     14
     D XXUSER                 15     24
      *
      /copy syspeds/qrpgsrcpy,prolog3
      *=====================================================================
      * MAIN PROCESSING LINE
      *=====================================================================
      * Parse input string
     C                   exsr      Parse
      * Initier
     C                   EXSR      INIT
      * Oppdater sortering
     C                   exsr      SORT
      * Slett plukklinje
     C                   exsr      SLETT
      *
     C                   EXSR      VISLINJE
      * Send output buffer and quit
     C     SLUTT         TAG
     C                   exsr      Exit
      *=====================================================================
      * Send output buffer and quit
      *=====================================================================
     C     Exit          begsr
     C                   CLOSE     CCBLV1LF
     C                   CLOSE     CCBLV1
     C                   CLOSE     BRIDF
     C                   eval      *inlr = *on
     C                   return
     C                   endsr
      *=====================================================================
      * Parse input string
      *=====================================================================
     C     Parse         begsr
     C                   eval      lng     = zhbgetvar('lng')

     C                   eval      WSUSER  = zhbgetvar('USER')
     C                   EVAL      UsrSpcName  = zhbgetvar('UsrSpcName')

     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$top
      *  (search criteria)
      *=====================================================================******
     C     SetTop        begsr
      * Repeat national language for the next invocation
     C                   callp     updhtmlvar('LNG':lng:
     C                             InitHTMLVars)
      * Color for text, background, links, visited links, active links
     C                   callp     updhtmlvar('TXTCOLOR':'black')
     C                   callp     updhtmlvar('BOLD':' ')
     C                   callp     updhtmlvar('BGCOLOR':BIFARG)
     C                   callp     updhtmlvar('LINK':'0000FF')
     C                   callp     updhtmlvar('VLINK':'FF0000')
     C                   callp     updhtmlvar('ALINK':'00FF00')
      *
     C                   endsr
      *=====================================================================
      *  Initier
      *=====================================================================
     C     INIT          begsr
      * Get program start time for calculating execution time
     C                   time                    timedata1
      *
     C                   OPEN      BRIDF
     C     WSUSER        CHAIN(N)  BRIDF                              50
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   CALLB     'INCGI'
     C                   PARM                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
      *
     C                   EXSR      TSTUSER
      *
     C                   open      CCBLV1LF                             50
     C                   open      CCBLV1                               50
     c     CCBLV1LFK     klist
     c                   kfld                    BSAVD
     c                   kfld                    BSOPD
     c     CCBLV1K       klist
     c                   kfld                    BSAVD
     c                   kfld                    BSOPD
     c                   kfld                    BSLI
      *
     C                   endsr
      *
      *////////////////////////////////////////////////////////////
     C     TSTUSER       BEGSR
      *////////////////////////////////////////////////////////////
      /copy syspeds/qrpgsrcpy,tstuser
      *
     C                   endsr
      *
      *=====================================================================
      *  Slett merket linje
      *=====================================================================
     C     SLETT         begsr
      *
     C                   EVAL      ItemCount = ZhbGetVarCnt('serno')
     C                   IF        ItemCount <> 0
     C                   DOW       i < ItemCount
     C                   eval      i = i + 1
     C                   eval      SernoC = ZhbGetVar('serno':i)
     C                   MOVEL     SERNOC        XXREF
     C                   MOVE      XXAVD         BSAVD
     C                   MOVE      XXOPD         BSOPD
     C                   MOVE      XXLI          BSLI
      *
     C     CCBLV1K       CHAIN     CCBLV1                             33
     C  N33              DELETE    CCBLV1R
      *
     C                   ENDDO
     C                   END
      *
     C                   MOVE      *ZEROS        BSLI
      *
     C                   endsr
      *=====================================================================
      * VIS LINJER
      *=====================================================================
     C     VISLINJE      begsr
      *
     C                   CALLB     PGM2
     C                   PARM                    BSAVD
     C                   PARM                    BSOPD
     C                   PARM                    BSLI
     C                   PARM                    WSUSER
     C                   PARM                    UsrSpcName
     C                   PARM                    BIFARG
     C                   PARM                    WSCN             17
     C                   PARM                    WSSL             17
     C                   PARM                    WSVS35           35
     C                   PARM                    WSCT              2
     C                   PARM                    BSNT              5 0
     C                   PARM                    BSVKT             9 0
     C                   PARM                    BSM3              9 3
     C                   PARM                    FMELDNR           2
     C                   PARM                    FEILMELDING      80
      *
     C                   endsr
      ****************************************
     C     SORT          BEGSR
      ****************************************
      *
     C                   eval      WSAVD   = zhbgetvar('WSAVD')
     C                   eval      BSAVD   = c2n2(WSAVD)
     C                   eval      WSOPD   = zhbgetvar('WSOPD')
     C                   eval      BSOPD   = c2n2(WSOPD)
     C                   MOVE      *ZEROS        SL
      *
     C     CCBLV1LFK     SETLL     CCBLV1LF
     C     CCBLV1LFK     READE     CCBLV1LF                               33
     C                   eval      i = 0
     C                   DOW       *IN33 = *OFF
     C     1             DO        99            XN                3 0
     C                   SELECT
     C                   WHEN      SL(XN) = BVLI
     C                   UPDATE    CCBLV1RLF
     C                   GOTO      XLESNESTE
     C                   WHEN      SL(XN) = 0
     C                   EVAL      SL(XN) = BVLI
     C                   LEAVE
     C                   END
     C                   ENDDO
     C                   eval      i = I + 1
     C                   eval      WSSORT = ZhbGetVar('WSSORT':i)
     C                   eval      BSSORT = c2n2(WSSORT)
     C                   EVAL      BVSORT = BSSORT
     C                   UPDATE    CCBLV1RLF
      *
     C     XLESNESTE     TAG
     C     CCBLV1LFK     READE     CCBLV1LF                               33
     C                   ENDDO
     C                   eval      i = 0
      *
     C                   ENDSR
      *
