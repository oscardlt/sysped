     A*%%TS  SD  20190308  085416  SVEIN       REL-V7R1M0  5770-WDS
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3                  -
     A                                             27 132 *DS4)
     A                                      PRINT
     A                                      HELP
     A                                      ALTHELP
     A*
     A          R GSS003DA                  SFL
     A*%%TS  SD  20190308  085416  SVEIN       REL-V7R1M0  5770-WDS
     A            WSNR           1A  I  5  2VALUES(' ' '2' '4')
     A            CGCNO          8Y 0O  5 11EDTCDE(4)
     A            CGCG           2A  O  5 22
     A            KNAVN         30A  O  5 36
     A*
     A          R GSS003DB                  SFLCTL(GSS003DA)
     A*%%TS  SD  20190308  085416  SVEIN       REL-V7R1M0  5770-WDS
     A  *DS3                                SFLSIZ(0016)
     A  *DS4                                SFLSIZ(0016)
     A  *DS3                                SFLPAG(0015)
     A  *DS4                                SFLPAG(0015)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                  1  2'SHIPSYS/GSS003D'
     A                                      COLOR(BLU)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                  1 70TIME
     A            ZRECNO         4S 0O  2 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  3  2'Choose option, press ENTER:'
     A                                      COLOR(BLU)
     A                                  3 35'2=Change  4=Delete   '
     A                                      COLOR(BLU)
     A                                  4  2'Option   Cust.no.   Cust.group    -
     A                                      Customer name'
     A                                      DSPATR(HI)
     A                                  1 21'Work with customers cust.group'
     A                                      COLOR(WHT)
     A          R GSS003DC
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF03(03 'AVSLUTT')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                      SLNO(23)
     A                                      CLRL(02)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'F3=End  F5=Refresh F6=Lag ny F8=Se-
     A                                        arch'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  2  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R GSS003DD
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF04(04 'Search')
     A                                      CF12(12 'Previous')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1 11'Cust.no    Cust.group'
     A                                      DSPATR(HI)
     A            WSCNO          8Y 0B  2 11DSPATR(UL)
     A  40                                  DSPATR(HI)
     A  40                                  DSPATR(PR)
     A  90                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A            WSCG           2A  B  2 22DSPATR(UL)
     A  89                                  DSPATR(RI)
     A                                  3  2'F4=Search F12=Previous'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  4  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A*
     A          R GSS003DE
     A*%%TS  SD  20131126  143211  JOVO        REL-V7R1M0  5770-WDS
     A                                      CF12(12 'Previous')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A                                  2  2'Søk fra'
     A            W2CNO          8Y 0I  2 11DSPATR(UL)
     A                                      DSPATR(PC)
     A                                  3  2'F12=Previous'
     A                                      COLOR(BLU)
     A            W2CG           2   I  2 22
      *
     A          R GSS003DF
     A                                      CF12(12 'Previous')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A                                  1 11'Cust.no.   Cust.group'
     A                                      DSPATR(HI)
     A            CGCNO          8Y 0O  2 11DSPATR(UL)
     A                                      DSPATR(PC)
     A                                      DSPATR(HI)
     A                                      EDTCDE(4)
     A            CGCG           2A  O  2 22DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  3  2'F12=Previous'
     A                                      COLOR(BLU)
     A*
     A          R GSS003DG
     A*%%TS  SD  20190308  085416  SVEIN       REL-V7R1M0  5770-WDS
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF03(03 'END')
     A                                      CF05(05 'Refresh')
     A                                      CF06(06 'New entry')
     A                                      CF08(08 'Search')
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SHIPSYS/GSS003D'
     A                                      COLOR(BLU)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  1 70TIME
     A                                      COLOR(BLU)
     A                                  3  2'Choose option, press ENTER:'
     A                                      COLOR(BLU)
     A                                  3 32'2=Change  4=Delete'
     A                                      COLOR(BLU)
     A                                  4  2'Option   Cust.no.   Cust.group    -
     A                                      Customers name'
     A                                      DSPATR(HI)
     A                                  6  6'No code found'
     A                                      COLOR(RED)
     A                                 23  2'F3=End   F5=Refresh  F6=New entry -
     A                                        F8=Search'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A                                  1 21'Work with customers cust.group'
     A                                      COLOR(WHT)
