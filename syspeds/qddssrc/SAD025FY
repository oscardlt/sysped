*****A**************************************************************
     A* RETTINGER I DETTE PROGRAM:
PTFnrA*Sek Sig Vers Beskrivelse...........................
"""""A*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10261 * 01 YBC PTF10 VEKT/KUBIKK/KOLLI I TURER ER ENDRET TIL 9.0, 7.3 OG 7.0
*****A**************************************************************
S01  A*                                     DSPSIZ(24 80 *DS3 27 132 *DS4)
N01  A                                      DSPSIZ(27 132 *DS4)
     A                                      PRINT
     A                                      HLPDOC('STD' 'HELPNU' 'HLP400')
     A                                      HELP
     A                                      ALTHELP
      *
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  4  2VALUES(' ' '1')
     A            TUPRO          8Y 0O  4  5EDTCDE(4)
     A            TUSDF2        12A  O  4 14
     A            TUSDT2        12A  O  4 27
EV5  A            TUDT           8Y 0H
NV5  A            WSDT           6Y 0O  4 40EDTCDE(Y)
     A            TUAO           5Y 0O  4 49EDTCDE(4)
S01  A*           TUTS           5Y 0O  4 55EDTCDE(4)
S01  A*           TUTVKT         5Y 0O  4 61EDTCDE(4)
S01  A*           TUTLM          4Y 2O  4 67EDTCDE(4)
S01  A*           TUTM3          5Y 2O  4 73EDTCDE(4)
N01  A            TUTS           7Y 0O  4 55EDTCDE(4)
N01  A            TUTVKT         9Y 0O  4 63EDTCDE(4)
N01  A            TUTLM          4Y 2O  4 73EDTCDE(4)
N01  A            TUTM3          7Y 3O  4 79EDTCDE(4)
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
S01  A*                                     SFLSIZ(0019)
S01  A*                                     SFLPAG(0018)
N01  A                                      SFLSIZ(0022)
N01  A                                      SFLPAG(0021)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A            ZRECNO         4S 0O  2 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'UTSKRIFT AV LASTELISTE'
     A                                      COLOR(WHT)
     A                                  1 26'Avdeling:'
     A            UAVD           4Y 0O  1 36COLOR(BLU)
     A                                      EDTCDE(4)
     A                                  1 42'Signatur:'
     A            USG            3A  O  1 52COLOR(BLU)
     A                                  1 57'Tdek.type:'
     A            UDTY           2A  O  1 68COLOR(BLU)
     A                                  1 72'Status:'
     A            UST            1A  O  1 80COLOR(BLU)
     A                                  2  2'Velg option:  1=Plukk'
     A                                      COLOR(BLU)
     A                                  3  2'Op Turnr... Sted fra.... Sted til.-
N01  A                                      ... Dato.... Ant.O Ant.Kll Tot. vek-
N01  A                                      t T.LM. Tot. M3'
S01  A*                                     ... Dato.... Ant.O Ant.K Tvekt T.LM-
S01  A*                                     . T.M3.'
     A                                      COLOR(WHT)
      *
     A          R BUNN
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF03(03 'Avslutt program')
     A                                      CF05(05 'Frisk')
     A                                      CF08(08 'Hopp til')
     A                                      CF12(12 'FORRIGE')
S01  A*                                     SLNO(22)
N01  A                                      SLNO(25)
     A                                      CLRL(03)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  2  2MSGCON(18 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A                                  2 22MSGCON(18 DIV0021 SYMSGF)
     A                                      COLOR(BLU)
     A                                  2 42MSGCON(18 DIV0023 SYMSGF)
     A                                      COLOR(BLU)
     A                                  2 62MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  3  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R NYVARE
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF03(03 'Avslutt program')
     A                                      CF05(05 'Frisk')
     A                                      CF08(08 'Hopp til')
     A                                      CF12(12 'FORRIGE')
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'UTSKRIFT AV LASTELISTE'
     A                                      COLOR(WHT)
     A                                  1 30'Avdeling:'
     A            WSAVD          4Y 0O  1 40COLOR(BLU)
     A                                      EDTCDE(4)
     A                                  1 47'Tdek.type:'
     A            WSDTY          2A  O  1 58COLOR(BLU)
     A                                  1 63'Status:'
     A            WSST           1A  O  1 71COLOR(BLU)
     A                                  2  2'Velg option:  1=Plukk'
     A                                      COLOR(BLU)
     A                                  3  2'Op Turnr... Sted fra.... Sted til.-
N01  A                                      ... Dato.... Ant.O Ant.Kll Tot. vek-
N01  A                                      t T.LM. Tot. M3'
S01  A*                                     ... Dato.... Ant.O Ant.K Tvekt T.LM-
S01  A*                                     . T.M3.'
     A                                      COLOR(WHT)
S01  A*                                23  2MSGCON(18 DIV0020 SYMSGF)
N01  A                                 26  2MSGCON(18 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
S01  A*                                23 22MSGCON(18 DIV0021 SYMSGF)
N01  A                                 26 22MSGCON(18 DIV0021 SYMSGF)
     A                                      COLOR(BLU)
S01  A*                                23 42MSGCON(18 DIV0023 SYMSGF)
N01  A                                 26 42MSGCON(18 DIV0023 SYMSGF)
     A                                      COLOR(BLU)
S01  A*                                23 62MSGCON(18 DIV0024 SYMSGF)
N01  A                                 26 62MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
S01  A* 30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
N01  A  30        MSGTXT        78A  O 27  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R VAREL2
     A                                      CF12(12 'Forrige')
S01  A*                                     SLNO(22)
N01  A                                      SLNO(25)
     A                                      CLRL(03)
     A                                  1  2'Hopp til tur   -->'
     A            UAVD           4Y 0B  1 24DSPATR(UL)
     A            W3PRO          8Y 0I  1 29DSPATR(UL)
     A                                      DSPATR(PC)
     A                                  2  2'F12=Forrige'
     A                                      COLOR(BLU)
