     FSADTRH11  UF   E           K DISK
     FSADTRCMP  O    E           K DISK
     FSADTRV    UF   E           K DISK
     D                 DS
     D  XDT2                   1      6  0
     D  XD                     1      2  0
     D  XM                     3      4  0
     D  XÅ                     5      6  0
     D                 DS
EV5  D  WDT                    1      8  0
EV5  D  WÅ                     1      4  0
EV5  D  WM                     5      6  0
EV5  D  WD                     7      8  0
      *___________
      * KEY FIELDS
      *"""""""""""
     C     SADHK         KLIST
     C                   KFLD                    SEAVD
     C                   KFLD                    SETDN
      *____________
      * WORK FIELDS
      *""""""""""""
     C     *LIKE         DEFINE    SEAVD         XAVD
     C     *LIKE         DEFINE    XDT2          XDT
      *_______________
      * PARAMETER LIST
      *"""""""""""""""
     C     *ENTRY        PLIST
     C                   PARM                    XAVD
     C     XDT2          PARM                    XDT
      *_____________________________________________
      * SETLL, READ AND DELETE RECORDS FROM SADTRH11
      *"""""""""""""""""""""""""""""""""""""""""""""
     C                   Z-ADD     XÅ            WÅ
     C                   Z-ADD     XM            WM
     C                   Z-ADD     XD            WD
NV5  C     XÅ            IFGT      50
NV5  C                   MOVEL     '19'          WÅ
NV5  C                   ELSE
NV5  C                   MOVEL     '20'          WÅ
NV5  C                   END
     C     SEDT          SETLL     SADTRH11
     C                   READ      SADTRH11                               51
     C  N51SEDT          COMP      WDT                                51
     C     *IN51         DOWEQ     '0'                                          <----------I
     C     XAVD          IFEQ      SEAVD                                         <--------II
     C     XAVD          OREQ      0
     C                   EXSR      SRA                                          SADTRV
     C                   WRITE     SADTCMPR
     C                   DELETE    SADTRHR
     C                   END                                                     <--------II
     C                   READ      SADTRH11                               51
     C  N51SEDT          COMP      WDT                                51
     C  N51              END                                                    <----------I
      *
     C                   MOVE      '1'           *INLR
     C                   RETURN
      *___________________________________________
      * SETLL, READ AND DELETE RECORDS FROM SADTRV
      *"""""""""""""""""""""""""""""""""""""""""""
     C     SRA           BEGSR
     C     SADHK         SETLL     SADTRV
     C     SADHK         READE     SADTRV                                 52
     C     *IN52         DOWEQ     '0'
     C                   DELETE    SADTRVR
     C     SADHK         READE     SADTRV                                 52
     C                   END
     C                   ENDSR
