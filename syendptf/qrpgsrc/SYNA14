********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
12XXX * 01 SH PTF12 startlinje definert
12309 * 02 CB  PTF12 FEIL KOLONNER FOR BRUTTO OG NETTOVEKTER (kun i DDS) Rekomp
      *
      ***********************************************************
      *** PROGRAM SKAL VEDLIKEHOLDE AUTOKUNDER KOLONNER EXP   ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01 02 07 09-11 13-24             ***
      *** LEDIGE INDIKATORER 27-29 31 32 34-39 41-87 95-97    ***
      ***********************************************************
      *
     FAUTOKUNDE UF A E           K DISK
     FCUNDL01   IF   E           K DISK
     FFIRM      IF   E           K DISK
     FSYNA14FM  CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D                 DS
     D  KNAVN                  1     30
     D  XDLT                   1      7
     C*
     C                   EXSR      INIT
     C*
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     keysok        SETLL     AUTOKUNDE
     C     START1        TAG
     C                   READ(N)   AUTOKUNDE                              94
      *
     C     START2        TAG
      *
     C  N94              DO
     C                   DO        15
     C                   ADD       1             ZRECNO
     C     CUND0K        CHAIN     CUNDL01                            33
     C   33              EVAL      KNAVN='*STANDARD*'
     C                   WRITE     SUBFIL
     C                   READ(N)   AUTOKUNDE                              94
     C  N94              END
     C                   END
     C                   Z-ADD     ZRECNO        YRECNO            4 0
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     3             POSNBR
     C*
     C     START3        TAG
     C                   SETOFF                                       9998
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYVARE
     C                   ELSE
     C                   WRITE     SUBCTL
     C                   EXFMT     BUNN
     C                   END
     C*
     C***
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C                   Z-ADD     WSSUBN        XRECNO            4 0
     C***
     C*
     C                   SETOFF                                       9830
     C*** AVSLUTT
     C   03              GOTO      SLUTT
     C*** FRISK OPP
     C   05              GOTO      START
     C*** LAGE NY
     C   06              DO
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     2             POSNBR
     C                   GOTO      START5
     C                   END
     C*** SØK FRA
     C   08              GOTO      START6
     C*
     C*** BLA VIDERE
     C   25              DO
     C                   Z-ADD     YRECNO        ZRECNO
     C                   SETOFF                                       25
     C                   GOTO      START2
     C                   END
     C*
     C     ZRECNO        IFNE      0
     C                   EXSR      HENT
     C                   END
     C   98XRECNO        IFNE      0
     C                   Z-ADD     XRECNO        ZRECNO
     C                   END
     C                   GOTO      START3
     C*
     C*** LAGE
     C     START5        TAG
     C                   EXFMT     VARELI
     C                   MOVEA     '000'         *IN(88)
     C                   SETOFF                                           30
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C*
     C   04              DO
     C                   EXSR      SØKDTA
     C                   GOTO      START5
     C                   END
      *
      *
     C   12              EXSR      MOVB2
     C  N12              EXSR      TESTB1
     C  N12
     CAN 30              GOTO      START5
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     3             POSNBR
     C                   GOTO      START3
     C*
     C*** SØK FRA
     C     START6        TAG
     C                   EXFMT     VAREL2
     C   04              DO
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C                   MOVE      W2KN          WSKUND
     C                   EXSR      SØKDTA
     C                   MOVE      WSKUND        W2KN
     C                   GOTO      START6
     C                   END
     C   12              Z-ADD     5             LINNBR
     C   12              Z-ADD     3             POSNBR
     C   12              GOTO      START3
     C                   GOTO      START
     C*
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
     C*
     C***********************************************
     C     TESTB1        BEGSR
     C***********************************************
     C  N40              DO
      * TEST KUNDENR.
     C     WSKUND        IFNE      0
     C     CUND1K        CHAIN     CUNDL01                            90
     C   90              MOVE      'SPK0001'     MSGNR
     C   90              SETON                                        30
     C                   END
     C                   END
     C*
     C   30              GOTO      SLTB1
     C*
     C*
     C*** OPPDATERE PÅ REGISTER OG SUBFIL
     c     wskey         chain     AUTOKUNDE                          33
     C                   EXSR      MOVWCE
     C  N33              UPDATE    AUTOKUNDER
     C   33              WRITE     AUTOKUNDER
     C   40              UPDATE    SUBFIL
     C  N40              ADD       1             YRECNO
     C  N40              Z-ADD     YRECNO        ZRECNO
     C  N40              WRITE     SUBFIL
     C                   WRITE     SUBCTL
     C                   EXSR      MOVB2
     C*
     C     SLTB1         ENDSR
     C*
     C***********************************************
     C     HENT          BEGSR
     C***********************************************
     C                   Z-ADD     0             LINNBR
     C                   SETOFF                                       919299
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C   98              DO
     C   99              SETOFF                                       98
     C                   Z-ADD     1             LINNBR
     C                   GOTO      SLHENT
     C                   END
     C     ZRECNO        DIV       15            LINNBR
     C                   MVR                     XLIN              2 0
     C     XLIN          IFEQ      0
     C                   Z-ADD     15            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C     WSNR          IFEQ      '2'
     C                   MOVE      ' '           WSNR
     C     XDLT          COMP      '*DELETE'                              98
     C   98              GOTO      SLHENT
     C                   EXSR      CHGCE
     C   12              GOTO      SLHENT
     C                   END
     C     WSNR          IFEQ      '4'
     C                   MOVE      ' '           WSNR
     C     XDLT          COMP      '*DELETE'                              98
     C   98              GOTO      SLHENT
     C                   EXSR      DLTCE
     C   12              GOTO      SLHENT
     C                   END
     C                   END
     C*
     C     SLHENT        TAG
     C                   ADD       4             LINNBR
     C                   Z-ADD     3             POSNBR
     C                   SETOFF                                       99
     C   98              DO
     C                   MOVE      'DIV0012'     MSGNR
     C                   SETON                                            30
     C                   END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     CHGCE         BEGSR
     C***********************************************
     C                   EXSR      MOVCEW
     C                   SETON                                        99
     C     STCHG         TAG
     C                   EXFMT     VARELI
     C                   MOVEA     '000'         *IN(88)
     C                   SETOFF                                         30
     C*
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C*
     C   04              DO
     C                   EXSR      SØKDTA
     C                   GOTO      STCHG
     C                   END
     C   12              DO
     C                   UPDATE    SUBFIL
     C                   EXSR      MOVB2
     C                   END
     C  N12              EXSR      TESTB1
     C  N12
     CAN 30              GOTO      STCHG
     C     XLIN          IFEQ      0
     C                   Z-ADD     15            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     DLTCE         BEGSR
     C***********************************************
     C                   SETON                                        99
     C                   EXFMT     VAREL3
     C  N12              DO
     C     keydel        CHAIN     AUTOKUNDE                          33
     C  N33              DELETE    AUTOKUNDER
     C                   EXSR      MOVB1
     C                   END
     C     XLIN          IFEQ      0
     C                   Z-ADD     15            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C                   UPDATE    SUBFIL
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVWCE        BEGSR
     C***********************************************
     C                   MOVE      WSkund        KUNDNR
     C                   MOVE      WSFYL         FYL
     C                   MOVE      WSTAN         TAN
     C                   MOVE      WSVER         VER
     C                   MOVE      WSBRV         BRV
     C                   MOVE      WSNEV         NEV
     C                   MOVE      WSANT         ANT
     C                   MOVE      WSART         ART
     C                   MOVE      WSBES         BES
n01  C                   MOVE      WSSTR         STR
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVCEW        BEGSR
     C***********************************************
     C                   MOVE      KUNDNR        WSkund
     C                   MOVE      FYL           WSFYL
     C                   MOVE      TAN           WSTAN
     C                   MOVE      VER           WSVER
     C                   MOVE      BRV           WSBRV
     C                   MOVE      NEV           WSNEV
     C                   MOVE      ANT           WSANT
     C                   MOVE      ART           WSART
     C                   MOVE      BES           WSBES
     C                   MOVE      STR           WSSTR
     C                   SETON                                        40
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     12            POSNBR
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVB1         BEGSR
     C***********************************************
     C                   MOVE      *ZEROS        wskund
     C                   MOVE      *BLANKS       KNAVN
     C                   MOVE      '*DELETE'     XDLT
     C                   MOVE      *ZEROS        WSFYL
     C                   MOVE      *ZEROS        WSTAN
     C                   MOVE      *ZEROS        WSVER
     C                   MOVE      *ZEROS        WSBRV
     C                   MOVE      *ZEROS        WSNEV
     C                   MOVE      *ZEROS        WSANT
     C                   MOVE      *ZEROS        WSART
     C                   MOVE      *ZEROS        WSBES
N01  C                   MOVE      *BLANKS       WSSTR
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVB2         BEGSR
     C***********************************************
     C                   SETOFF                                       40
     C                   MOVE      *ZEROS        WSkund
     C                   MOVE      *ZEROS        WSFYL
     C                   MOVE      *ZEROS        WSTAN
     C                   MOVE      *ZEROS        WSVER
     C                   MOVE      *ZEROS        WSBRV
     C                   MOVE      *ZEROS        WSNEV
     C                   MOVE      *ZEROS        WSANT
     C                   MOVE      *ZEROS        WSART
     C                   MOVE      *ZEROS        WSBES
N01  C                   MOVE      *BLANKS       WSSTR
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     SØKDTA        BEGSR
     C***********************************************
      *
     C     LINNBR        IFEQ      22
     C     POSNBR        ANDGE     2
     C     POSNBR        ANDLE     9
     C                   MOVE      *BLANKS       USØKKN           30
     C                   CALL      'SOEKKUN'
     C                   PARM                    WSKUND
     C                   PARM                    FIFIRM
     C                   PARM                    USØKKN           30
     C                   Z-ADD     16            POSNBR
     C                   GOTO      SLSØK
     C                   END
      *
     C*
     C                   MOVE      'DIV1037'     MSGNR
     C                   SETON                                        30
     C*
     C     SLSØK         ENDSR
     C*
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     KEYSOK        KLIST
     C                   KFLD                    W2KN
     C     WSKEY         KLIST
     C                   KFLD                    WSKUND
     C     KEYDEL        KLIST
     C                   KFLD                    KUNDNR
     C     CUND1K        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    WSKUND
     C     CUND0K        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    KUNDNR
     C*
     C     FIFIRM        SETLL     FIRM
     C                   READ      FIRM                                   33
     C*
     C                   ENDSR
     C*
