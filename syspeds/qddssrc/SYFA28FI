*****A**************************************************************
     A* RETTINGER I DETTE PROGRAM:
PTFnrA*Sek Sig Vers Beskrivelse...........................
"""""A*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
P8068 * 11 TH  PTF8 ny helpfunksjon KUN DDS
*****A**************************************************************
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
N11  A                                      HLPPNLGRP(HLPSCR SYFA28PNL)
N11  A                                      HLPTITLE('VEDLIKEHOLD AVDELINGERS-
N11  A                                       FASTE DATA')
     A                                      HELP
     A                                      ALTHELP
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  6  2VALUES(' ' '2' '4' '3')
     A  98                                  DSPATR(RI)
     A  98                                  DSPATR(PC)
     A            KOVAVD         4Y 0O  6  4EDTCDE(4)
     A                                      COLOR(WHT)
     A            KOANVN        33A  O  6  9
     A            KOWLAS         1A  O  6 45
     A            KOWAWB         1A  O  6 49
     A            KOWBBS         1A  O  6 53
     A            KOWKOM         1A  O  6 57
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0016)
     A                                      SFLPAG(0015)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
N11  A          H                           HLPPNLGRP(SYFA280020 SYFA28PNL)
N11  A                                      HLPARA(01 001 23 080)
     A            ZRECNO         4S 0O  2 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYSPED/SYFA28I'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(045 DIV1703 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  1 70TIME
     A                                      COLOR(BLU)
     A                                  3  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 34MSGCON(030 DIV0034 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  2MSGCON(077 SHU0998 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A          R BUNN
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF03(03 'AVSLUTT')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                      SLNO(23)
     A                                      CLRL(02)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  1 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  1 42MSGCON(018 DIV0022 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  1 62MSGCON(018 DIV0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  2  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                              2 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A          R VAREL2
     A                                      CF12(12 'Forrige')
     A                                      SLNO(22)
     A                                      CLRL(03)
     A                                      OVERLAY
     A                                  1  2MSGCON(012 DIV0026 *LIBL/SYMSGF)
     A            W2AAVD         4Y 0I  1 16DSPATR(UL)
     A                                      DSPATR(PC)
     A                                  3  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A          R VAREL3
     A                                      CF12(12 'Forrige')
     A                                      CF23(23 'Bekreft sletting')
     A                                      SLNO(22)
     A                                      CLRL(03)
     A                                      OVERLAY
     A            SIAVD          4Y 0O  1  4EDTCDE(4)
     A                                      COLOR(WHT)
     A            KOANVN        33A  O  1  9
     A                                  3  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A                                  2  2MSGCON(075 SHU0983 *LIBL/SYMSGF)
     A                                      COLOR(RED)
     A          R VAREL4
     A                                      CF12(12 'Forrige')
     A                                      SLNO(19)
     A                                      CLRL(06)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  2  2MSGCON(078 JOV0211 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            WSAAVD         4Y 0O  3  4COLOR(WHT)
     A                                      EDTCDE(4)
     A            WSDUS1        10A  B  3  9DSPATR(UL)
     A            WSDUS2        10A  B  3 20DSPATR(UL)
     A            WSDUS3        10A  B  3 31DSPATR(UL)
     A            WSDUS4        10A  B  3 42DSPATR(UL)
     A            WSDUS5        10A  B  3 53DSPATR(UL)
     A            WSDUS6        10A  B  3 64DSPATR(UL)
     A                                  5  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  6 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A          R NYVARE
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF03(03 'AVSLUTT')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/SYFA14'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(040 DIV0583 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                  1 69TIME
     A                                  3  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 34MSGCON(030 DIV0017 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(002 DIV0025 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                  4  5MSGCON(075 DIV0584 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 42MSGCON(018 DIV0022 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 62MSGCON(018 DIV0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                             24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A          R NYWRK
     A          R NOERASE
     A                                      ASSUME
     A                                  1  2' '
      * MAN KUN DEFINNERE VINDU PÅ REKORDNIVÅ
     A          R WINDOWSZ
     A                                      WINDOW(7 25 4 30 *NOMSGLIN)
     A                                      OVERLAY
     A          R BILDE01
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A                                      CF04(04 'søking')
     A                                      CF12(12 'Forrige')
     A            MSGNR          7A  P
     A                                  1  2MSGCON(018 SHU0984 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  2  2MSGCON(008 SHU0985 *LIBL/SYMSGF)
     A            KOVAVD         4  0O  2 14COLOR(WHT)
     A                                  3  2MSGCON(008 SHU0986 *LIBL/SYMSGF)
     A            WSAVDT         4Y 0B  3 14DSPATR(UL)
     A                                      EDTCDE(4)
     A  70                                  DSPATR(RI)
     A N30                              4  2MSGCON(24 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        28A  O  4  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
