********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 YBC PTF10 MELDINGSNR ER ENDRET FRA 7.0 TIL 9.0
********************************************************************
     A                                      REF(EFR911)
     A          R PACR
      * Key
S01  A*           PMN            7S 0       COLHDG('Msg.no')
N01  A            PMN       R               REFFLD(ØMN)
     A            PGN            2S 0       COLHDG('Group no')
     A            PREP0     R               REFFLD(ØREP0)
     A            PREP1     R               REFFLD(ØREP1)
     A            PREP2     R               REFFLD(ØREP2)
     A            PREP3     R               REFFLD(ØREP3)
     A            PREP4     R               REFFLD(ØREP4)
     A            PREP5     R               REFFLD(ØREP5)
     A            PREP6     R               REFFLD(ØREP6)
     A            PREP7     R               REFFLD(ØREP7)
     A            PREP8     R               REFFLD(ØREP8)
      *
     A            P7224          8          COLHDG('No of packages')
      * C531 Packaging details
     A            P7075          3          COLHDG('Pack lvl, coded')
     A            P7233          3          COLHDG('Pack rel inf, coded')
     A            P7073          3          COLHDG('Pack terms & cond')
      * C202 Package type
     A            P7065          7          COLHDG('Type of pack')
     A            P1131          3          COLHDG('Code list Q')
     A            P3055          3          COLHDG('Resp.agency code')
     A            P4774         35          COLHDG('Equ.Text')
      * C402 Package type identification
     A            P7077          3          COLHDG('Item desc type')
     A            P7064A        35          COLHDG('Type of pack')
     A            P7023A         3          COLHDG('Article no, coded')
     A            P7064B        35          COLHDG('Type of pack')
     A            P7023B         3          COLHDG('Article no, coded')
      * C532 Returnable package details
     A            P8771          3          COLHDG('Returnable pack pay')
     A            P8769          3          COLHDG('Returnable pack cont')
      * Key
     A          K PMN
     A          K PGN
     A          K PREP0
     A          K PREP1
     A          K PREP2
     A          K PREP3
     A          K PREP4
     A          K PREP5
     A          K PREP6
     A          K PREP7
     A          K PREP8
