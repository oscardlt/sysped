********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr *Sek Sig Vers Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
P8068 * 03 TH  PTF8 NY HELPFUNKSJON  KUN DDS
********************************************************************
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      REF(*LIBL/SAMLA)
     A                                      CF03(03 'Avslutt')
     A                                      PRINT
S03  A*                                     HLPDOC('SYEX0101' 'SYEX01 ' 'SY400-
S03  A*                                     LO/MAINT')
N03  A                                      HLPPNLGRP(HLPSCR SYEX01PNL)
N03  A                                      HLPTITLE('VEDLIKEHOLD AV SAMLAST TI-
N03  A                                      L DESTINASJONER')
     A                                      HELP
     A                                      ALTHELP
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  6  2VALUES(' ' '2' '4' '5' '8' '9')
     A  98                                  DSPATR(RI)
     A  98                                  DSPATR(PC)
     A            SMSDF          3A  O  6  4
     A            SMSD      R    3   O  6  8
     A            SMLNR          3S 0O  6 12
EV5  A            SMDT           8S 0H
NV5  A            WMDT           6S 0O  6 16
     A            SMKL      R    4 00O  6 23
     A            SMKNF     R    3 00O  6 28
     A            SMKNS     R    8 00O  6 32EDTCDE(4)
     A            SMROA1    R    3   O  6 41
     A            SMROB1    R    6   O  6 45
     A            SMROC1    R    2 00O  6 52
     A            SMROA2    R    3   O  6 55
     A            SMROB2    R    6   O  6 59
     A            SMROC2    R    2 00O  6 66
     A            SMROA3    R    3   O  6 69
     A            SMROB3    R    6   O  6 73
     A            SMMAWB    R   11 00H
     A            SMNT      R    3 00H
     A            SMPRO     R    8 00H
     A            SMVKT1    R    5 00H
     A            SMROC3         2S 0H
     A            SMETD1         4S 0H
     A            SMETA1         4S 0H
     A            SMEDA1         2S 0H
     A            SMETD2         4S 0H
     A            SMETA2         4S 0H
     A            SMEDA2         2S 0H
     A            SMETD3         4S 0H
     A            SMETA3         4S 0H
     A            SMEDA3         2S 0H
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0014)
     A                                      SFLPAG(0013)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A  91N93                               CF05(05 'Frisk')
     A  91N93N99                            CF06(06 'LAGE NY')
     A  91N93                               CF08(08 'SØK')
     A                                  1  2'SYSPED/SYEX01'
     A                                      COLOR(BLU)
     A            WSMSG         34A  O  1 22DSPATR(HI)
     A                                  1 59DATE
     A                                      EDTCDE(Y)
     A                                  1 69TIME
     A            ZRECNO         4S 0O  2 76SFLRCDNBR
     A                                      DSPATR(ND)
     A            MSG001        30A  O  3  2MSGID(DIV 0016 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG003        40A  O  3 38MSGID(BBA 0115 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG002        30A  O  4  5MSGID(DIV 0017 SYMSGF)
     A  99                                  DSPATR(ND)
     A                                      COLOR(BLU)
     A            MSG123        20A  O  4 60MSGID(DIV 2003 SYMSGF)
     A  99                                  DSPATR(ND)
     A                                      COLOR(BLU)
     A            MSG004        79A  O  5  2MSGID(DIV 1578 SYMSGF)
     A                                      DSPATR(HI)
     A          R BUNN
     A            MSG005        18   O 23  1MSGID(DIV 0020 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG006        18   O 23 20MSGID(DIV 0021 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG007        18A  O 23 40MSGID(DIV 0022 SYMSGF)
     A                                      COLOR(BLU)
     A  99                                  DSPATR(ND)
     A            MSG008        18   O 23 60MSGID(DIV 0023 SYMSGF)
     A                                      COLOR(BLU)
     A                                 24 40'(C) COPYRIGHT  SYSTEMA A/S  1998  '
     A                                      COLOR(WHT)
     A          R VARELI
     A                                      CF12(12 'Forrige')
     A                                      CF04(04 'Søk kunde')
     A                                      CF23(23 'Ruteforespørsel')
     A                                      SLNO(20)
     A                                      CLRL(05)
     A            MSGNR          7A  P
     A            MSG009        77A  O  1  3MSGID(DIV 2043 SYMSGF)
     A                                      COLOR(WHT)
     A            WSSDF          3A  B  2  3DSPATR(UL)
     A  40                                  DSPATR(PR)
     A  40                                  DSPATR(HI)
     A N40N30
     AO 71                                  DSPATR(PC)
     A  71                                  DSPATR(RI)
     A            WSSD           3A  B  2  7DSPATR(UL)
     A  40                                  DSPATR(HI)
     A  40                                  DSPATR(PR)
     A  70                                  DSPATR(PC)
     A  70                                  DSPATR(RI)
     A            WSDT           6Y 0B  2 11DSPATR(UL)
     A                                      EDTCDE(4)
     A  40                                  DSPATR(HI)
     A  40                                  DSPATR(PR)
     A  69                                  DSPATR(RI)
     A  69                                  DSPATR(PC)
     A            WSKL           4Y 0B  2 18DSPATR(UL)
     A  40                                  DSPATR(HI)
     A                                      EDTCDE(4)
     A  40                                  DSPATR(PR)
     A  68                                  DSPATR(RI)
     A  68                                  DSPATR(PC)
     A            WSKNF          3Y 0B  2 23DSPATR(UL)
     A                                      EDTCDE(4)
     A  40                                  DSPATR(HI)
     A  40                                  DSPATR(PR)
     A  67                                  DSPATR(RI)
     A  67                                  DSPATR(PC)
     A            WSKNS          8Y 0B  2 27DSPATR(UL)
     A                                      EDTCDE(4)
     A  40                                  DSPATR(HI)
     A  40                                  DSPATR(PR)
     A  66                                  DSPATR(RI)
     A  66                                  DSPATR(PC)
     A            WSROA1         3A  B  2 37DSPATR(UL)
     A  40N30
     AO 65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A            WSROB1         6A  B  2 41DSPATR(UL)
     A  64                                  DSPATR(RI)
     A  64                                  DSPATR(PC)
     A            WSROC1         2Y 0B  2 48DSPATR(UL)
     A                                      EDTCDE(4)
     A  63                                  DSPATR(RI)
     A  63                                  DSPATR(PC)
     A            WSROA2         3A  B  2 52DSPATR(UL)
     A  62                                  DSPATR(RI)
     A  62                                  DSPATR(PC)
     A            WSROB2         6A  B  2 56DSPATR(UL)
     A  61                                  DSPATR(RI)
     A  61                                  DSPATR(PC)
     A            WSROC2         2Y 0B  2 63DSPATR(UL)
     A                                      EDTCDE(4)
     A  60                                  DSPATR(RI)
     A  60                                  DSPATR(PC)
     A            WSROA3         3A  B  2 67DSPATR(UL)
     A  59                                  DSPATR(RI)
     A  59                                  DSPATR(PC)
     A            WSROB3         6A  B  2 71DSPATR(UL)
     A  58                                  DSPATR(RI)
     A  58                                  DSPATR(PC)
     A            WSROC3         2Y 0B  2 78DSPATR(UL)
     A                                      EDTCDE(4)
     A  57                                  DSPATR(RI)
     A  57                                  DSPATR(PC)
     A                                  3 27'Est.time:'
     A            WSETD1         4Y 0B  3 37EDTCDE(4)
     A            WSETA1         4Y 0B  3 42EDTCDE(4)
     A            WSEDA1         2Y 0B  3 48EDTCDE(4)
     A            WSETD2         4Y 0B  3 52EDTCDE(4)
     A            WSETA2         4Y 0B  3 57EDTCDE(4)
     A            WSEDA2         2Y 0B  3 63EDTCDE(4)
     A            WSETD3         4Y 0B  3 67EDTCDE(4)
     A            WSETA3         4Y 0B  3 72EDTCDE(4)
     A            WSEDA3         2Y 0B  3 78EDTCDE(4)
     A            MSG012        18A  O  4  2MSGID(DIV 0020 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG013        18A  O  4 21MSGID(DIV 0024 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG053         5A  O  4 48MSGID(DIV 1579 SYMSGF)
     A            WSVKT1         5Y 0B  4 54DSPATR(UL)
     A            MSG052         4A  O  4 60MSGID(DIV 0078 SYMSGF)
     A            WSMAWB        11Y 0B  4 66DSPATR(UL)
     A                                      EDTCDE(4)
     A  30        MSGTXT        78A  O  5  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSG014        18A  O  5  2MSGID(BBA 0042 SYMSGF)
     A                                      COLOR(BLU)
     A N30                              5 40'(C) COPYRIGHT  SYSTEMA A/S  1998  '
     A                                      COLOR(WHT)
     A          R VAREL2
     A                                      SLNO(20)
     A                                      CLRL(05)
     A                                  1 15MSGCON(034 BBA0123 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A            MSG014        12A  O  2  2MSGID(DIV 0026 SYMSGF)
     A            W2SD           3A  I  2 15DSPATR(UL)
     A                                      DSPATR(PC)
     A            W2DT           6D 0I  2 22DSPATR(UL)
     A            W2KL           4D 0I  2 30DSPATR(UL)
     A            W2KNF          3D 0I  2 35DSPATR(UL)
     A            W2KNS          8D 0I  2 39DSPATR(UL)
     A            MSG011        18A  O  4  2MSGID(DIV 0020 SYMSGF)
     A                                      COLOR(BLU)
     A                                  5 40'(C) COPYRIGHT  SYSTEMA A/S  1998  '
     A                                      COLOR(WHT)
     A          R VAREL3
     A                                      CF12(12 'Forrige')
     A                                      SLNO(20)
     A                                      CLRL(05)
     A            MSG010        79A  O  1  2MSGID(DIV 1578 SYMSGF)
     A                                      COLOR(WHT)
     A            SMSDF     R    3   O  2  2DSPATR(PC)
     A                                      DSPATR(HI)
     A            SMSD      R    3   O  2  6DSPATR(PC)
     A                                      DSPATR(HI)
EV5  A            SMDT           8S 0H
NV5  A            WMDT           6S 0O  2 11DSPATR(HI)
     A            SMKL      R    4 00O  2 18DSPATR(HI)
     A            SMKNF     R    3 00O  2 23DSPATR(HI)
     A            SMKNS     R    8Y00O  2 27EDTCDE(4)
     A                                      DSPATR(HI)
     A            SMROA1    R    3   O  2 37DSPATR(HI)
     A            SMROB1    R    6   O  2 41DSPATR(HI)
     A            SMROC1    R    2 00O  2 48DSPATR(HI)
     A            SMROA2    R    3   O  2 52DSPATR(HI)
     A            SMROB2    R    6   O  2 56DSPATR(HI)
     A            SMROC2    R    2 00O  2 63DSPATR(HI)
     A            SMROA3    R    3   O  2 67DSPATR(HI)
     A            SMROB3    R    6   O  2 71DSPATR(HI)
     A            SMROC3    R    2 00O  2 78DSPATR(HI)
     A                                  3 27'Est.time:'
     A            WSETD1         4Y 0O  3 37EDTCDE(4)
     A            WSETA1         4Y 0O  3 42EDTCDE(4)
     A            WSEDA1         2Y 0O  3 48EDTCDE(4)
     A            WSETD2         4Y 0O  3 52EDTCDE(4)
     A            WSETA2         4Y 0O  3 57EDTCDE(4)
     A            WSEDA2         2Y 0O  3 63EDTCDE(4)
     A            WSETD3         4Y 0O  3 67EDTCDE(4)
     A            WSETA3         4Y 0O  3 72EDTCDE(4)
     A            WSEDA3         2Y 0O  3 78EDTCDE(4)
     A            MSG041        18A  O  4  2MSGID(DIV 0024 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG043         5A  O  4 48MSGID(DIV 1579 SYMSGF)
     A            SMVKT1         5Y 0B  4 54COLOR(WHT)
     A            MSG042         4A  O  4 60MSGID(DIV 0078 SYMSGF)
     A            SMMAWB    R   11 00B  4 66DSPATR(HI)
     A                                  5 40'(C) COPYRIGHT  SYSTEMA A/S  1998  '
     A                                      COLOR(WHT)
     A          R NYWRK
     A                                      CF05(05 'Frisk')
     A N99                                  CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                  1  2'SYSPED/SYEX01'
     A                                      COLOR(BLU)
     A            WSMSG         34A  O  1 19DSPATR(HI)
     A                                  1 56DATE
     A                                      EDTCDE(Y)
     A                                  1 68TIME
     A            MSG031        30A  O  3  2MSGID(DIV 0016 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG033        20A  O  3 34MSGID(DIV 1577 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG032        30A  O  4  2MSGID(DIV 0017 SYMSGF)
     A  99                                  DSPATR(ND)
     A                                      COLOR(BLU)
     A            MSG034        79A  O  5  2MSGID(DIV 1578 SYMSGF)
     A                                      DSPATR(HI)
     A            MSG035        18A  O 23  2MSGID(DIV 0020 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG036        18A  O 23 21MSGID(DIV 0021 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG037        18A  O 23 59MSGID(DIV 0022 SYMSGF)
     A                                      COLOR(BLU)
     A  99                                  DSPATR(ND)
     A            MSG038        18A  O 23 40MSGID(DIV 0023 SYMSGF)
     A                                      COLOR(BLU)
     A                                 24 40'(C) COPYRIGHT  SYSTEMA A/S  1998  '
     A                                      COLOR(WHT)
