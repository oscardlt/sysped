     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPPNLGRP(HLPSCR SVX061PNL)
     A                                      HLPTITLE('REGISTRERING AV TET IMPOR-
     A                                      T')
     A                                      HELP
     A                                      ALTHELP
      *
     A          R SVX061D1
     A                                      CSRLOC(LINNBR     POSNBR)
     A N36                                  CF07(07 'Søke oppdrag')
     A                                      CF10(10 'Slette ferdigmeldt TVINN')
     A                                      CA12(12 'Forrige')
     A          H                           HLPPNLGRP(SVX061000 SVX061PNL)
     A                                      HLPARA(01 001 08 080)
     A          H                           HLPPNLGRP(SVX061000 SVX061PNL)
     A                                      HLPARA(09 001 09 080)
     A          H                           HLPPNLGRP(SVX06100A SVX061PNL)
     A                                      HLPARA(10 001 10 080)
     A          H                           HLPPNLGRP(SVX06100B SVX061PNL)
     A                                      HLPARA(11 001 11 080)
     A          H                           HLPPNLGRP(SVX06100C SVX061PNL)
     A                                      HLPARA(13 001 13 080)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1  3'SYSPED/SVX061R'
     A                                      COLOR(BLU)
     A                                  1 28'Underhåll NCTS Import'
     A                                      COLOR(WHT)
     A                                  1 62DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(GRN)
     A                                  1 71TIME
     A                                      COLOR(GRN)
     A                                  2  3'Firmakod:'
     A                                      COLOR(TRQ)
     A            FIFIRM         2A  O  2 16TEXT('FIRMAKODE')
     A                                      COLOR(TRQ)
     A                                  2 58MSGCON(022 DIV0047 *LIBL/SYMSGF)
     A                                      COLOR(TRQ)
     A                                  5 18'Avdelning'
     A            UAVD           4Y 0B  5 34EDTCDE(4)
     A  95                                  DSPATR(PC)
     A  31                                  DSPATR(PR)
     A                                      COLOR(WHT)
     A                                  9 18'Signatur:'
     A            WSSG           3A  B  9 34DSPATR(UL)
     A  99                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 10 18'Ærende:    '
     A            WSTDN          7Y 0O 10 34DSPATR(UL)
     A  98                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 11 18'Førenklad?'
     A            WSENKL         1A  B 11 34DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      VALUES('J' 'N')
     A                                 11 42'(J=Førenklad, N=Normal)'
     A                                      COLOR(BLU)
     A N36                             23  2'F7=Søk ærende  '
     A                                      COLOR(BLU)
     A                                 23 20'F12=Avbryt'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
      *
     A          R SVX061D2
     A N27                                  CF04(04 'Søking')
     A N36                                  CF07(07 'Søke oppdrag')
     A                                      CF10(10 'Se TVINN logg')
     A                                      CA12(12 'Forrige')
     A                                      CF14(14 'KUNDEINFO')
     A                                      CSRLOC(LINNBR     POSNBR)
     A          H                           HLPPNLGRP(SVX06100D SVX061PNL)
     A                                      HLPARA(01 001 02 080)
     A          H                           HLPPNLGRP(SVX061002 SVX061PNL)
     A                                      HLPARA(03 001 03 047)
     A          H                           HLPPNLGRP(SVX061001 SVX061PNL)
     A                                      HLPARA(04 001 04 030)
     A          H                           HLPPNLGRP(SVX061003 SVX061PNL)
     A                                      HLPARA(04 052 04 063)
     A          H                           HLPPNLGRP(SVX061003 SVX061PNL)
     A                                      HLPARA(05 002 05 014)
     A          H                           HLPPNLGRP(SVX061003 SVX061PNL)
     A                                      HLPARA(07 067 07 080)
     A          H                           HLPPNLGRP(SVX061003 SVX061PNL)
     A                                      HLPARA(11 022 11 033)
     A          H                           HLPPNLGRP(SVX061007 SVX061PNL)
     A                                      HLPARA(11 042 11 065)
     A          H                           HLPPNLGRP(SVX061003 SVX061PNL)
     A                                      HLPARA(18 002 18 033)
     A          H                           HLPPNLGRP(SVX061004 SVX061PNL)
     A                                      HLPARA(07 002 07 065)
     A          H                           HLPPNLGRP(SVX061004 SVX061PNL)
     A                                      HLPARA(08 002 08 029)
     A          H                           HLPPNLGRP(SVX061004 SVX061PNL)
     A                                      HLPARA(09 002 09 029)
     A          H                           HLPPNLGRP(SVX061005 SVX061PNL)
     A                                      HLPARA(11 002 11 020)
     A          H                           HLPPNLGRP(SVX061006 SVX061PNL)
     A                                      HLPARA(13 001 13 042)
     A          H                           HLPPNLGRP(SVX061006 SVX061PNL)
     A                                      HLPARA(14 001 14 042)
     A          H                           HLPPNLGRP(SVX061006 SVX061PNL)
     A                                      HLPARA(15 001 15 042)
     A          H                           HLPPNLGRP(SVX061006 SVX061PNL)
     A                                      HLPARA(16 001 16 042)
     A          H                           HLPPNLGRP(SVX061006 SVX061PNL)
     A                                      HLPARA(17 001 17 042)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1  2'SYSPED/SVX061R'
     A                                      COLOR(BLU)
     A N27                              1 17'Underhåll NCTS Import'
     A                                      COLOR(WHT)
     A  27                              1 18'Spørring på TET-import'
     A                                      COLOR(WHT)
     A                                  1 42'Ref.'
     A            UAVD           4Y 0O  1 47COLOR(WHT)
     A                                      EDTCDE(4)
     A            WSTDN          7Y 0O  1 52COLOR(WHT)
     A                                      EDTCDE(4)
     A            WSSG           3A  O  1 60COLOR(WHT)
     A            WSTARF        16A  O  1 64TEXT('Tariffer')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  3  2'Godsnr.'
     A            WSGN          35A  B  3 13DSPATR(UL)
     A  98                                  DSPATR(RI)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                  3 52'Språkkod '
     A            WSGNSK         2A  B  3 62DSPATR(UL)
     A  97                                  DSPATR(RI)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                  4  2'MRN-nr.'
     A            WSTRNR        18A  B  4 13TEXT('MRN-nr')
     A  99                                  DSPATR(RI)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                  5  2'Avs.land'
     A            WSALK          2A  B  5 13TEXT('Landk avs/utf-land')
     A                                      DSPATR(UL)
     A  96                                  DSPATR(RI)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                  7  2'Avt.lag.pl'
     A            WSALSK        17A  B  7 13DSPATR(UL)
     A  95                                  DSPATR(RI)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A            WSALS         35A  B  7 31DSPATR(UL)
     A  94                                  DSPATR(RI)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                  7 67'Språkkod '
     A            WSALSS         2A  B  7 77DSPATR(UL)
     A  93                                  DSPATR(RI)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                  8  2'Godkj.l.pl'
     A            WSGLSK        17A  B  8 13DSPATR(UL)
     A  92                                  DSPATR(RI)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                  9  2'Contr.pl  '
     A            WSACTS        17A  B  9 13DSPATR(UL)
     A  91                                  DSPATR(RI)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                 11  2'Freml.t.pl'
     A            WSTSB          8A  B 11 13DSPATR(UL)
     A  90                                  DSPATR(RI)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                 11 22'Språkkod '
     A            WSSKB          2A  B 11 32DSPATR(UL)
     A  89                                  DSPATR(RI)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                 11 42'Frigivelsedatum'
     A            WSDTF          8Y 0O 11 58DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTWRD('    .  .  ')
     A                                 13  2'Næringsliv'
     A            WSKN           8Y 0B 13 13TEXT('Kundenr')
     A                                      DSPATR(UL)
     A  88                                  DSPATR(RI)
     A  27                                  DSPATR(PR)
     A  37                                  DSPATR(BL)
     A  37                                  COLOR(RED)
     A N37                                  COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSTIN         17A  B 13 22TEXT('Tin kode')
     A                                      DSPATR(UL)
     A  87                                  DSPATR(RI)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A            WSNA          30A  B 14 13TEXT('Navn')
     A                                      DSPATR(UL)
     A  86                                  DSPATR(RI)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A            WSAD1         30A  B 15 13TEXT('Adresse-1')
     A                                      DSPATR(UL)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A            WSPN           9A  B 16 13TEXT('Postnr')
     A                                      DSPATR(UL)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A            WSPS          24A  B 17 13TEXT('Poststed')
     A                                      DSPATR(UL)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                 18  2'Landkod '
     A            WSLK           2A  B 18 13TEXT('Landkode')
     A                                      DSPATR(UL)
     A  85                                  DSPATR(RI)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                 18 22'Språkkod '
     A            WSSK           2A  B 18 32TEXT('Språkkode')
     A                                      DSPATR(UL)
     A  84                                  DSPATR(RI)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A N27                             23  2'F4=Søk'
     A                                      COLOR(BLU)
     A N36                             23 11'F7=Se uppdrag'
     A                                      COLOR(BLU)
     A                                 23 26'F10=NCTS-logg'
     A                                      COLOR(BLU)
     A                                 23 41'F12=Avbryt'
     A                                      COLOR(BLU)
     A                                 23 53'F14=Visa kundupplysningar'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
