     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HELP
     A                                      ALTHELP
      *
     A          R SVX011DA1
     A                                      CSRLOC(LINNBR     POSNBR)
     A N27                                  CF04(04 'Søking')
     A N27N28                               CF10(10 'SLETT')
     A                                      CA12(12 'Gå til forrige bilde')
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1  2'SYSPED/SVX011RA'
     A                                      COLOR(BLU)
     A                                  1 25'Arbeta med føranmælan'
     A                                      COLOR(WHT)
     A                                  1 52'Ref.'
     A            UAVD           4Y 0O  1 57COLOR(WHT)
     A                                      EDTCDE(4)
     A            UTDN           7Y 0O  1 62COLOR(WHT)
     A                                      EDTCDE(4)
     A            USG            3A  O  1 70COLOR(WHT)
     A                                  3  2'Sækerhet.:'
     A            WSSIK          1Y 0B  3 13TEXT('Sikkerhet')
     A                                      DSPATR(UL)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                      VALUES(0 1)
     A                                  3 15'Ank.dato/tid:'
     A            WSDTA          8Y 0B  3 29TEXT('DATO')
     A                                      DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  99                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSTMA          4Y 0B  3 38TEXT('TID')
     A                                      DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  98                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTWRD('  :  ')
     A                                  3 44'Særs.omstændigheter'
     A            WSSPOM         1A  B  3 64DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  97                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                  3 66'Bet.kod.Trp:'
     A            WSTKBM         1A  B  3 79DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  96                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                  4  2'K. ref:'
     A            WSKREF        70A  B  4 10DSPATR(UL)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                  5  2'Transportørs ref:'
     A            WSTREF        35A  B  5 20DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  72                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                  6  2'Lossplats:'
     A            WSLOSD        35A  B  6 13DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  95                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                  6 50'Språkkod.:'
     A            WSLOSDSK       2A  B  6 61DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  94                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                  7  2'Landkoder før resplan.:'
     A            WSLKR1         2A  B  7 26DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  93                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSLKR2         2A  B  7 29DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  92                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSLKR3         2A  B  7 32DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  91                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSLKR4         2A  B  7 35DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  90                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSLKR5         2A  B  7 38DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  89                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSLKR6         2A  B  7 41DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  88                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSLKR7         2A  B  7 44DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  87                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSLKR8         2A  B  7 47DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  86                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                  9  2'Avsændare sækerhet:'
     A            WSKNSS         8Y 0B  9 22TEXT('Kundenr selger')
     A                                      DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  85                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSTINSS       17A  B  9 31TEXT('Tin kode selger')
     A                                      DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  84                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSNASS        30A  B 10  2TEXT('Navn selger')
     A                                      DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  83                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSADSS1       30A  B 10 42TEXT('Adresse-1 selger')
     A                                      DSPATR(UL)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A            WSPNSS         9A  B 11  2TEXT('Postnr selger')
     A                                      DSPATR(UL)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A            WSPSSS        24A  B 11 12TEXT('Poststed selger')
     A                                      DSPATR(UL)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                 11 42'Landkod'
     A            WSLKSS         2A  B 11 52TEXT('Landkode selger')
     A                                      DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  82                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 11 62'Språkkod'
     A            WSSKSS         2A  B 11 72TEXT('Språkkode selger')
     A                                      DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  81                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 12  2'Mottagare sækerhet:'
     A            WSKNKS         8Y 0B 12 22TEXT('Kundenr kjøper')
     A                                      DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  80                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSTINKS       17A  B 12 31TEXT('Tin kode kjøper')
     A                                      DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  79                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSNAKS        30A  B 13  2TEXT('Navn kjøper')
     A                                      DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  78                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSADKS1       30A  B 13 42TEXT('Adresse-1 kjøper')
     A                                      DSPATR(UL)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A            WSPNKS         9A  B 14  2TEXT('Postnr kjøper')
     A                                      DSPATR(UL)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A            WSPSKS        24A  B 14 12TEXT('Poststed kjøper')
     A                                      DSPATR(UL)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                 14 42'Landkod'
     A            WSLKKS         2A  B 14 52TEXT('Landkode kjøper')
     A                                      DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  77                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 14 62'Språkkod'
     A            WSSKKS         2A  B 14 72TEXT('Språkkode kjøper')
     A                                      DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  76                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 15  2'Transportør:'
     A            WSKNT          8Y 0B 15 22TEXT('Kundenr transportør')
     A                                      DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  75                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSTINT        17A  B 15 31TEXT('Tin kode transportør')
     A                                      DSPATR(UL)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A            WSNAT         30A  B 16  2TEXT('Navn transportør')
     A                                      DSPATR(UL)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A            WSADT1        30A  B 16 42TEXT('Adresse-1 transportør')
     A                                      DSPATR(UL)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A            WSPNT          9A  B 17  2TEXT('Postnr transportør')
     A                                      DSPATR(UL)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A            WSPST         24A  B 17 12TEXT('Poststed transportør')
     A                                      DSPATR(UL)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                 17 42'Landkod'
     A            WSLKT          2A  B 17 52TEXT('Landkode H.ansvalig')
     A                                      DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  74                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 17 62'Språkkod'
     A            WSSKT          2A  B 17 72TEXT('Språkkode H.ansvalig')
     A                                      DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  73                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A N27                             23  2'F4=Søk'
     A                                      COLOR(BLU)
     A  27                             23 35'F12=Avbryt'
     A                                      COLOR(BLU)
     A N27N28                          23 11'F10=Radera føranmælan'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
