      *
      **************************************************************
      *** PROGRAM FOR BEREGNING AV MIN/MAX PRIS FOR SONETABELL   ***
      ***             EGNE FRAKTTABELLER                         ***
      ***********************************************************
     FSONET     UF   E           K DISK
     FSONES     IF   E           K DISK
     C*
     C                   EXSR      INIT
     C*
     C     SONESK        CHAIN     SONES                              33
     C  N33SSMIMA        IFEQ      '1'
     C                   EXSR      RMINP
     C                   ELSE
     C     SSMIMA        IFEQ      '2'
     C                   EXSR      RMAXP
     C                   ELSE
     C     SSMIMA        IFEQ      '0'
     C                   EXSR      RNULL
     C                   END
     C                   END
     C                   END
     C*
     C                   SETON                                        LR
     C                   RETURN
     C*
     C***********************************************
     C     RMINP         BEGSR
     C***********************************************
     C* Kode 1=Maxpris i et intervall skal legges inn i neste intervall
     C* som minimumspris (MAXPRIS legges i YPRIS)
     C                   Z-ADD     SSMINI        YPRIS            13 2
     C                   Z-ADD     1             XVEKT             9 0
      *
      * Beregn minpris for alle intervallene
     C     SONETK        SETLL     SONET
     C     STRMIN        TAG
     C     SONETK        READE     SONET                                  33
     C   33              GOTO      SLRMIN
      *
      * Finn minpris i denne intervallen (XVEKT=Minvekt i denne interv)
     C     SOPRPR        IFEQ      0
     C                   Z-ADD     SOPRIC        SOMMBE
      *
     C                   ELSE
     C                   Z-ADD     SOPRIC        XWORK            17 6
     C     SOPRPR        IFGT      1
     C     SOPRIC        DIV(H)    SOPRPR        XWORK
     C                   END
     C                   MULT      XVEKT         XWORK
      * Hvis minpris (denne intervall) < maxpris (forrige interv.),
      * bruker maxpris (forrige interv.) som minpris i denne interv.
     C     XWORK         IFLT      YPRIS
     C                   Z-ADD     YPRIS         SOMMBE
     C                   ELSE
     C                   Z-ADD(H)  XWORK         SOMMBE
     C                   END
      *
     C                   END
      *
     C     SOMMBE        IFLT      SSMINI
     C                   Z-ADD     SSMINI        SOMMBE
     C                   END
     C     SOMMBE        IFGT      SSMAXI
     C     SSMAXI        ANDNE     0
     C                   Z-ADD     SSMAXI        SOMMBE
     C                   END
      * Oppdater minpris på filen.
     C                   EXCEPT    NYPRIS
     C     SOVEKT        IFEQ      SSGREN                                       0<-B
      * Vekt er lik grensevekt. Dvs. at minpris i neste intervallen
      * ikke skal ta hensyn til maxpris i denne intervallen.
     C                   Z-ADD     SSMINI        YPRIS
     C                   ELSE                                                   0
      * Beregn maxpris for denne intervallen.
     C     SOPRPR        IFEQ      0                                            1<-B
     C                   Z-ADD     SOPRIC        YPRIS
     C                   ELSE                                                   1
     C                   Z-ADD     SOVEKT        XVEKT
     C     SOADJU        IFNE      0
      * VEKT SKAL FORHØYES TIL SOADJU       (EKS. SOADJU=50  XVEKT=120
     C                   SUB       1             XVEKT                            XVEKT=119
     C                   ADD       SOADJU        XVEKT                            XVEKT=169
     C                   DIV       SOADJU        XVEKT                            XVEKT=3
     C                   MULT      SOADJU        XVEKT                            XVEKT=150)
     C                   END
     C                   Z-ADD     SOPRIC        XWORK
     C     SOPRPR        IFGT      1                                             2<-B
     C     SOPRIC        DIV(H)    SOPRPR        XWORK
     C                   END                                                     2<-E
     C     XWORK         MULT(H)   XVEKT         YPRIS
     C                   END                                                    1<-E
     C                   END                                                    0<-E
      * Beregn minvekt for neste interv.
     C     SOVEKT        IFLT      999999999
     C     SOVEKT        ADD       1             XVEKT
     C                   ELSE
     C                   Z-ADD     999999999     XVEKT
     C                   END
      *
     C                   GOTO      STRMIN
     C*
     C     SLRMIN        ENDSR
     C*
     C***********************************************
     C     RMAXP         BEGSR
     C***********************************************
     C* Kode 2=Minpris i et intervall skal legges inn i forrige interv.
     C* som maximumspris (MINPRIS legges i YPRIS)
     C                   Z-ADD     1             XVEKT             9 0
      *
      * Beregn maxpris for alle intervallene
     C     SONETK        SETGT     SONET
     C     STRMAX        TAG
     C     SONETK        READPE    SONET                                  33
     C   33              GOTO      SLRMAX
      * Beregn minpris for neste intervall.
     C     XVEKT         IFEQ      1                                            1<B
     C                   Z-ADD     SSMAXI        YPRIS            13 2
     C                   ELSE                                                   1
     C     XOPRPR        IFEQ      0                                             2<-B
     C                   Z-ADD     XOPRIC        YPRIS
     C                   ELSE                                                    2
     C                   Z-ADD     SOVEKT        XVEKT
     C                   Z-ADD     XOPRIC        XWORK
     C     XOPRPR        IFGT      1                                              3<-B
     C     XOPRIC        DIV(H)    XOPRPR        XWORK
     C                   END                                                      3<-E
     C     XWORK         MULT(H)   XVEKT         YPRIS
     C                   END                                                     2<-E
     C                   END                                                    1<-E
      *
      * Finn maxpris i denne intervallen
     C     SOPRPR        IFEQ      0                                            1<-B
     C                   Z-ADD     SOPRIC        SOMMBE
      *
     C                   ELSE                                                   1
     C                   Z-ADD     SOVEKT        XVEKT
     C                   Z-ADD     SOPRIC        XWORK
     C     SOPRPR        IFGT      1                                             2<-B
     C     SOPRIC        DIV(H)    SOPRPR        XWORK
     C                   END                                                     2<-E
     C                   MULT      XVEKT         XWORK
      * Hvis maxpris (denne intervall) > minpris (neste interv.),
      * bruker minpris (neste interv.) som maxpris i denne interv.
     C     XWORK         IFGT      YPRIS                                         2<-B
     C     YPRIS         ANDNE     0
     C                   Z-ADD     YPRIS         SOMMBE
     C                   ELSE                                                    2
     C                   Z-ADD(H)  XWORK         SOMMBE
     C                   END                                                     2<-E
      *
     C                   END                                                    1<-E
      *
     C     SOMMBE        IFGT      SSMAXI
     C     SSMAXI        ANDNE     0
     C                   Z-ADD     SSMAXI        SOMMBE
     C                   END
     C     SOMMBE        IFLT      SSMINI
     C                   Z-ADD     SSMINI        SOMMBE
     C                   END
      * Oppdater maxpris på filen.
     C                   EXCEPT    NYPRIS
      * Bevar data av denne intervallen.
     C                   MOVE      SOPRIC        XOPRIC           13 2
     C                   MOVE      SOPRPR        XOPRPR            5 0
     C                   MOVE      SOADJU        XOADJU            5 0
     C                   MOVE      *ZEROS        XVEKT
      *
     C                   GOTO      STRMAX
     C*
     C     SLRMAX        ENDSR
     C*
     C***********************************************
     C     RNULL         BEGSR
     C***********************************************
     C* Nullstil min/max pris
     C     SONETK        SETLL     SONET
     C     STRNUL        TAG
     C     SONETK        READE     SONET                                  33
     C   33              GOTO      SLRNUL
     C                   MOVE      *ZEROS        SOMMBE
     C                   EXCEPT    NYPRIS
     C                   GOTO      STRNUL
     C*
     C     SLRNUL        ENDSR
     C*
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     SONETK        KLIST
     C                   KFLD                    UPROD
     C                   KFLD                    UAVDE
     C                   KFLD                    UZONO
     C     SONESK        KLIST
     C                   KFLD                    UPROD
     C                   KFLD                    UAVDE
     C                   KFLD                    UZONO
     C*
     C     *ENTRY        PLIST
     C                   PARM                    UPROD             1
     C                   PARM                    UAVDE             4 0
     C                   PARM                    UZONO             5
     C*
     C                   ENDSR
     C*
     OSONETR    E            NYPRIS
     O                       SOMMBE
