     FEDIC      UF   E             DISK
     FEUNH      O    E             DISK
     FEDIM      O    E             DISK
     FECNI94B   O    E             DISK
     FEDTM94B   O    E             DISK
     FEGID94B   O    E             DISK
     FELOC94B   O    E             DISK
     FENAD94B   O    E             DISK
     FEPCI94B   O    E             DISK
     FERFF94B   O    E             DISK
     FESTS94B   O    E             DISK
     FE160D     CF   E             WORKSTN USROPN
      *____________
      * DS for Dato
      *""""""""""""
     D                 DS
     D  WTIME                  1     14  0
     D  WTM4                   1      4
     D  WTM                    1      6
     D  WDD                    7      8  0
     D  WMM                    9     10  0
     D  W≈≈                   11     14  0
     D                 DS
     D  WDT                    1      8
     D  W≈                     1      4  0
     D  WM                     5      6  0
     D  WD                     7      8  0
WIL  D                 DS
WIL  D  UDATX                  1      6
WIL  D  UDATD                  1      2
WIL  D  UDATM                  3      4
WIL  D  UDAT≈                  5      6
WIL  D EDIDTA          DS           256
WIL  D  UEL1FI               194    195
     D                SDS
     D  USER                 254    263
      *______________________
      * Initialize subroutine
      *""""""""""""""""""""""
     C                   EXSR      INIT
     C                   SELECT
     C     UDSP          WHENEQ    'Y'
     C                   EXSR      DSPYES
     C                   OTHER
     C                   EXSR      DSPNO
     C                   ENDSL
     C                   RETURN
      *////////////////////////////////////////////////////////////////
     C     DSPNO         BEGSR
      *////////////////////////////////////////////////////////////////
      *________________________________________
      * Move external data to display variables
      *""""""""""""""""""""""""""""""""""""""""
     C                   MOVEL     UMAWB         ZMAWB
     C                   MOVEL     UMAWB         ZMAWB3            3
     C                   MOVEL     UHAWB         ZHAWB
     C                   MOVEL     UPLDLV        ZPLDLV
     C                   MOVEL     USIGN         ZSIGN
     C                   MOVEL     UNOPK         ZNOPK
      *_____
      * Send
      *"""""
     C                   EXSR      SNDRQS
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////////
     C     DSPYES        BEGSR
      *////////////////////////////////////////////////////////////////
      *___________
      * Main logic
      *"""""""""""
     C     START         TAG
      *_______________
      * Display Window
      *"""""""""""""""
     C                   EXFMT     E160DA
      *
     C                   MOVEA     '000000'      *IN(30)
      *________
      * F3=Exit
      *""""""""
     C     *IN03         IFEQ      '1'
     C                   GOTO      ENDDSP
     C                   ENDIF                                                  <*IN03
      *_________
      * F10=Send
      *"""""""""
     C     *IN10         IFEQ      '1'
     C                   EXSR      VALSR
     C     *IN30         CABEQ     '1'           START
     C                   EXSR      SNDRQS
     C                   GOTO      START
     C                   END                                                    <*IN10
      *____________________________________________________________
      * When Enter, perform validity check and display format again
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C                   EXSR      VALSR
     C                   GOTO      START
      *
     C     ENDDSP        TAG
     C                   ENDSR
      *////////////////////////////////////////////////////////////////
     C     INIT          BEGSR
      *////////////////////////////////////////////////////////////////
      *_________________
      * Input parameters
      *"""""""""""""""""
     C     *ENTRY        PLIST
     C                   PARM                    UDSP              1
     C                   PARM                    UMAWB            12
     C                   PARM                    UHAWB            24
     C                   PARM                    UPLDLV            4
     C                   PARM                    USIGN            24
     C                   PARM                    UNOPK             9
     C                   PARM                    UMS              17
     C                   PARM                    UNAME            30
     C                   PARM                    UEVS1            25
     C                   PARM                    UTIM              4
     C                   PARM                    UDAT              6
      *____________________
      * Open display file ?
      *""""""""""""""""""""
     C     UDSP          IFEQ      'Y'
     C                   OPEN      E160D
     C                   ENDIF
      *__________________
      * Define workfields
      *""""""""""""""""""
     C     *LIKE         DEFINE    CMN           WMN
      *__________________________
      * Retreive System Date/Time
      *""""""""""""""""""""""""""
     C                   TIME                    WTIME
     C                   Z-ADD     W≈≈           W≈
     C                   Z-ADD     WDD           WD
     C                   Z-ADD     WMM           WM
WIL   *__________________
WIL   * Retreive Dataarea
WIL   *""""""""""""""""""
WIL  C     *DTAARA       DEFINE                  EDIDTA
WIL  C                   IN        EDIDTA
     C                   ENDSR
      *////////////////////////////////////////////////////////////////
     C     SNDRQS        BEGSR
      *////////////////////////////////////////////////////////////////
      *_____________________________________________________
      * Retrieve last used recordnumber from EDIC and update
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     1             CHAIN     EDIC                               90
     C     *IN90         IFEQ      '0'
     C                   ADD       1             CMN
     C                   Z-ADD     CMN           WMN
     C                   UPDATE    EDICR
     C                   ENDIF
      *_____________________
      * Write record to EUNH
      *"""""""""""""""""""""
     C                   CLEAR                   UNHR
     C                   Z-ADD     WMN           UMN
     C                   MOVEL     WMN           U0062A
     C                   MOVEL     'IFTSTA'      U0065
     C                   MOVEL     'D'           U0052
     C                   MOVEL     '94B'         U0054
     C                   MOVEL     'UN'          U0051
     C                   MOVEL     'GEISTA'      U0057
     C                   MOVEL     'STA'         U1001
     C                   MOVEL     WMN           U1004
     C                   MOVEL     WMN           U0062B
     C                   WRITE     UNHR
      *____________________
      * Write record to DTM
      *""""""""""""""""""""
     C                   CLEAR                   DTMR
     C                   Z-ADD     WMN           DMN
     C                   Z-ADD     1             DREP0
     C                   MOVEL     '137'         D2005
     C                   MOVEL     WDT           D2380
     C     D2380         CAT       WTM4:0        D2380
     C                   MOVEL     '203'         D2379
     C                   WRITE     DTMR
      *____________________
      * Write record to NAD
      *""""""""""""""""""""
     C                   CLEAR                   NADR
     C                   Z-ADD     WMN           NMN
     C                   Z-ADD     1             NGN
     C                   Z-ADD     1             NREP0
     C                   MOVEL     'AG'          N3035
WIL  C     UEL1FI        IFEQ      '01'
     C                   SELECT
     C     ZMAWB3        WHENEQ    'B/L'
     C                   MOVEL     'WNOOSLS'     N3039
     C                   OTHER
     C                   MOVEL     'WNOOSLA'     N3039
     C                   ENDSL
WIL  C                   ELSE
     C                   MOVEL     'SNOOSL'      N3039
     C                   ENDIF
     C                   WRITE     NADR
      *____________________
      * Write record to LOC
      *""""""""""""""""""""
     C                   CLEAR                   LOCR
     C                   Z-ADD     WMN           LMN
     C                   Z-ADD     1             LREP0
     C                   MOVEL     '91'          L3227
     C                   MOVEL     'OSL'         L3225
     C                   WRITE     LOCR
      *____________________
      * Write record to CNI
      *""""""""""""""""""""
     C                   CLEAR                   CNIR
     C                   Z-ADD     WMN           CMN
     C                   Z-ADD     3             CGN
     C                   Z-ADD     1             CREP0
     C                   MOVEL     '1'           C1490
     C     ZMAWB3        IFNE      'B/L'
     C     ZHAWB         IFNE      *BLANKS
     C                   MOVEL     ZHAWB         C1004
     C                   MOVEL     'HWB'         C1366
     C                   ELSE
     C                   MOVEL     ZMAWB         C1004
     C                   MOVEL     'MWB'         C1366
     C                   ENDIF
     C                   ENDIF
     C                   WRITE     CNIR
      *____________________
      * Write record to STS
      *""""""""""""""""""""
     C                   CLEAR                   STSR
     C                   Z-ADD     WMN           SMN
     C                   Z-ADD     4             SGN
     C                   Z-ADD     1             SREP0
     C                   Z-ADD     1             SREP1
     C                   MOVEL     '13'          S9015
     C                   MOVEL     'ET'          S3055A
     C                   MOVEL     '72'          S9011
     C                   MOVEL     '200'         S3055B
     C                   WRITE     STSR
      *____________________
      * Write record to RFF
      *""""""""""""""""""""
     C     ZHAWB         IFNE      *BLANKS
     C                   CLEAR                   RFFR
     C                   Z-ADD     WMN           RMN
     C                   Z-ADD     4             RGN
     C                   Z-ADD     1             RREP0
     C                   Z-ADD     1             RREP1
     C                   Z-ADD     1             RREP2
     C                   SELECT
     C     ZMAWB3        WHENEQ    'B/L'
     C                   MOVEL     'BH'          R1153
     C                   MOVEL     ZHAWB         R1154
     C                   OTHER
     C                   MOVEL     'MWB'         R1153
     C                   MOVEL     ZMAWB         R1154
     C                   ENDSL
     C                   WRITE     RFFR
     C                   ENDIF
      *____________________
      * Write record to DTM
      *""""""""""""""""""""
     C                   CLEAR                   DTMR
     C                   Z-ADD     WMN           DMN
     C                   Z-ADD     4             DGN
     C                   Z-ADD     1             DREP0
     C                   Z-ADD     1             DREP1
     C                   Z-ADD     1             DREP2
     C                   MOVEL     '40'          D2005
     C     UDAT≈         IFGT      '50'
     C                   MOVEL     '19'          D2380
     C                   ELSE
     C                   MOVEL     '20'          D2380
     C                   END
     C                   MOVE      UDAT          UDATX
     C     D2380         CAT       UDAT≈:0       D2380
     C     D2380         CAT       UDATM:0       D2380
     C     D2380         CAT       UDATD:0       D2380
     C     D2380         CAT       UTIM:0        D2380
     C                   MOVEL     '203'         D2379
     C                   WRITE     DTMR
      *___________________________________
      * Write record to NAD DELIVERY PARTY
      *"""""""""""""""""""""""""""""""""""
     C                   CLEAR                   NADR
     C                   Z-ADD     WMN           NMN
     C                   Z-ADD     4             NGN
     C                   Z-ADD     1             NREP0
     C                   Z-ADD     1             NREP1
     C                   Z-ADD     1             NREP2
     C                   MOVEL     'DP'          N3035
     C                   MOVEL     UNAME         N3124A
     C                   MOVEL     ZSIGN         N3036A
     C                   WRITE     NADR
      *_______________________________________
      * Write record to NAD ISSUER OF DOCUMENT
      *"""""""""""""""""""""""""""""""""""""""
     C     C1366         IFNE      'MWB'
     C                   CLEAR                   NADR
     C                   Z-ADD     WMN           NMN
     C                   Z-ADD     4             NGN
     C                   Z-ADD     1             NREP0
     C                   Z-ADD     1             NREP1
     C                   Z-ADD     2             NREP2
     C                   MOVEL     'MS'          N3035
     C                   MOVEL     UMS           N3039
     C                   WRITE     NADR
     C                   ENDIF
      *____________________
      * Write record to LOC
      *""""""""""""""""""""
     C                   CLEAR                   LOCR
     C                   Z-ADD     WMN           LMN
     C                   Z-ADD     4             LGN
     C                   Z-ADD     1             LREP0
     C                   Z-ADD     1             LREP1
     C                   Z-ADD     1             LREP2
     C                   MOVEL     '7'           L3227
     C                   MOVEL     ZPLDLV        L3225
     C                   WRITE     LOCR
      *____________________
      * Write record to GID
      *""""""""""""""""""""
     C                   CLEAR                   GIDR
     C                   Z-ADD     WMN           GMN
     C                   Z-ADD     9             GGN
     C                   Z-ADD     1             GREP0
     C                   Z-ADD     1             GREP1
     C                   Z-ADD     1             GREP2
     C                   MOVEL     '1'           G1496
     C                   MOVEL     ZNOPK         G7224A
     C                   MOVEL     'PK'          G7065A
     C                   WRITE     GIDR
      *____________________
      * Write record to PCI
      *""""""""""""""""""""
     C                   CLEAR                   PCIR
     C                   Z-ADD     WMN           PMN
     C                   Z-ADD     9             PGN
     C                   Z-ADD     1             PREP0
     C                   Z-ADD     1             PREP1
     C                   Z-ADD     1             PREP2
     C                   Z-ADD     1             PREP3
     C                   MOVEL     '24'          P4233
     C                   MOVEL     UEVS1         P7102A
     C                   WRITE     PCIR
      *__________________________
      * Write record to EDIM file
      *""""""""""""""""""""""""""
     C                   CLEAR                   EDIMR
     C     UEL1FI        IFEQ      '01'
     C                   MOVEL     'WNO01'       M0004
     C                   ELSE
     C                   MOVEL     'WNO02'       M0004
     C                   ENDIF
     C                   MOVEL     'WTDBCOPY'    M0010
     C                   MOVEL     'WTDBCOPY'    M0010
     C                   MOVEL     U0065         M0065
     C                   MOVEL     USER          M0068
     C                   MOVEL     U1001         M1001
     C                   MOVEL     U1004         M1004
     C                   Z-ADD     WMN           MMN
     C                   MOVEL     'S'           MSR
     C                   MOVEL     WDT           MDT
     C                   MOVEL     WTM           MTM
     C                   WRITE     EDIMR
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////////
     C     VALSR         BEGSR
      *////////////////////////////////////////////////////////////////
     C                   MOVEA     '000000'      *IN(30)
      *_________________________________________
      * Perform validity check on control format
      *"""""""""""""""""""""""""""""""""""""""""
     C     ZMAWB         IFEQ      *BLANKS
     C     ZHAWB         ANDEQ     *BLANKS
     C                   MOVE      *ON           *IN30
     C                   MOVE      *ON           *IN31
     C                   MOVE      *ON           *IN32
     C                   MOVE      'CBA0190'     MSGNR
     C                   GOTO      VAL99
     C                   ENDIF
      *
     C     ZPLDLV        IFEQ      *BLANKS
     C                   MOVE      *ON           *IN30
     C                   MOVE      *ON           *IN33
     C                   MOVE      'CBA0200'     MSGNR
     C                   GOTO      VAL99
     C                   ENDIF
      *
     C     ZSIGN         IFEQ      *BLANKS
     C                   MOVE      *ON           *IN30
     C                   MOVE      *ON           *IN34
     C                   MOVE      'CBA0210'     MSGNR
     C                   GOTO      VAL99
     C                   ENDIF
      *
     C     ZNOPK         IFEQ      *BLANKS
     C                   MOVE      *ON           *IN30
     C                   MOVE      *ON           *IN35
     C                   MOVE      'CBA0220'     MSGNR
     C                   GOTO      VAL99
     C                   ENDIF
      *
     C     VAL99         ENDSR
      *
