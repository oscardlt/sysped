     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPDOC('SYFA5201' 'SYFA52' 'SY400LO-
     A                                      /MAINT')
     A                                      HELP
     A                                      ALTHELP
      *
     A          R SUBFIL                    SFL
     A            WSNR           1A  B  6  4VALUES(' ' '2' '4')
     A            ADUSER        10A  O  6  6
     A            SFIFTM        15A  O  6 17
     A            SFNETV        10A  O  6 33
     A            SFPCTM        13A  O  6 44
     A            SFPATH        17A  O  6 58
     A            ADPAGE         1A  O  6 76
     A            ADARTY         2A  O  6 78
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A                                  1  2'SYSPED/SAD105V'
     A                                      COLOR(BLU)
     A                                  1 19'Vedlikehold param. Arb m/ufortolle-
     A                                      de/PDF-åpning'
     A                                      COLOR(WHT)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  2 70TIME
     A                                      COLOR(BLU)
     A                                  3  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A            ZRECNO         4S 0O  3 75SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  4  2MSGCON(030 DIV0017 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  3'Op Brukerid   IFS TempMappe   Netv-
     A                                      Stasj. PC TempMappe  Adobe PgmPath -
     A                                         Pg TY'
     A                                      COLOR(WHT)
      *
     A          R BUNN
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF03(03 'EXIT')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A  98        MSG205        78A  O  4  2MSGID(DIV 0012 SYMSGF)
     A                                      COLOR(WHT)
     A N98                              4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A                                  3  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 42MSGCON(018 DIV0022 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 62MSGCON(018 DIV0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
      *
     A          R VARELI
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF12(12 'Forrige')
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1  2'SYSPED/SAD105V'
     A                                      COLOR(BLU)
     A                                  1 19'Vedlikehold param. Arb m/ufortolle-
     A                                      de/PDF-åpning'
     A                                      COLOR(WHT)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  2 70TIME
     A                                      COLOR(BLU)
     A                                  3  2'Brukerid:'
     A            WAUSER    R        B  3 12REFFLD(ADOBEFR/ADUSER *LIBL/ADOBEF)
     A  40                                  DSPATR(HI)
     A  40                                  DSPATR(PR)
     A  90                                  DSPATR(RI)
     A N40                                  COLOR(YLW)
     A                                  3 23'(blank=firmastandard)'
     A                                  4  2'Arkivkode,mappe (inkl / foran og b-
     A                                      ak) for skannet handelsfakt.:'
     A            WAARTY         2A  B  5  6COLOR(YLW)
     A            WAARKM        30A  B  5  9COLOR(YLW)
     A                                  7  2'Path til Acrobat reader (Blank/N=N-
     A                                      orsk(Programfiler..) E=Eng (Program-
     A                                       Files))'
     A                                  8  2'OBS!!!'
     A                                  8  9'IE'
     A                                      COLOR(WHT)
     A                                  8 14'betyr Åpne DIREKTE i Internet Expl-
     A                                      orer (eller annen browser) '
     A                                  9  9'LINK'
     A                                      COLOR(WHT)
     A                                  9 14'betyr vis URL for åpning i IE (ell-
     A                                      er annen browser).'
     A            WAPATH       150A  B 10  6COLOR(YLW)
     A                                 12  2'Åpne fra side:'
     A            WAPAGE         1A  B 12 17COLOR(YLW)
     A                                 12 19'(Når Acrobat reader benyttes)'
     A                                 14  2'Videre parametre trengs IKKE derso-
     A                                      m'
     A                                      COLOR(BLU)
     A                                 14 38'IE'
     A                                      COLOR(WHT)
     A                                 14 41'over. (Åpne i IE/annen browser).'
     A                                      COLOR(BLU)
     A                                 16  2'IFS TempMappe  (blank= /sysped/Tem-
     A                                      pPDF/)'
     A            WAIFTM        30A  B 17  6COLOR(YLW)
     A                                 18  2'PC TempMappe, nødvendig når Acroba-
     A                                      t reader benyttes (blank= %USERPROF-
     A                                      ILE%\)'
     A            WAPCTM        70A  B 19  6COLOR(YLW)
     A                                 20  2'Nettverksstasjon PC-path til IFS T-
     A                                      empMappe'
     A            WANETV        30A  B 21  6COLOR(YLW)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                             24 40'(C COPYRIGHT  SYSTEMA A/S  1990   '
     A                                      COLOR(WHT)
     A                                 15  2'Benyttes kun for å kjøre VB-script-
     A                                       for avansert open/close i acrobat -
     A                                      reader'
     A                                      COLOR(BLU)
     A                                 23 27'Enter=Lagre                  F12=F-
     A                                      orrige   '
     A                                      COLOR(BLU)
      *
     A          R VAREL2
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            W2USER        10A  I  2  6DSPATR(UL)
     A                                      DSPATR(PC)
     A                                  4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A                                  3  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  2 18'<-- Søk fra Brukerid'
      *
     A          R VAREL3
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A                                  1  6'Brukerid   IFS TempMappe   NetvSta-
     A                                      sj. PC TempMappe  Adobe PgmPath    -
     A                                      Pg TY'
     A                                      COLOR(WHT)
     A            ADUSER    R        O  2  6REFFLD(ADOBEFR/ADUSER *LIBL/ADOBEF)
     A            SFIFTM        15A  O  2 17
     A            SFNETV        10A  O  2 33
     A            SFPCTM        13A  O  2 44
     A            SFPATH        17A  O  2 58
     A            ADPAGE         1A  O  2 76
     A            ADARTY         2A  O  2 78
     A                                  3 27'Enter=SLETT valgt post'
     A                                      COLOR(BLU)
     A                                  3 56MSGCON(024 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
      *
     A          R NYWRK
     A                                      CF03(03 'EXIT')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                  1  2'SYSPED/SAD105V'
     A                                      COLOR(BLU)
     A                                  1 19'Vedlikehold param. Arb m/ufortolle-
     A                                      de/PDF-åpning'
     A                                      COLOR(WHT)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  2 70TIME
     A                                      COLOR(BLU)
     A                                  3  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(030 DIV0017 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  3'Op Brukerid   IFS TempMappe   Netv-
     A                                      Stasj. PC TempMappe  Adobe PgmPath -
     A                                         Pg TY'
     A                                      COLOR(WHT)
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 42MSGCON(018 DIV0022 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 62MSGCON(018 DIV0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
