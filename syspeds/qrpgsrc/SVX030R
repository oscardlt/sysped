      *
      ***********************************************************
      *** PROGRAM SKAL ARBEIDE MED garanti                    ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01 02 04 07-24                   ***
      *** LEDIGE INDIKATORER 27-29 31 32 34-90 95-97          ***
      ***********************************************************
      *
     FSVXGH     IF   E           K DISK
     FSVXSTD    IF   E           K DISK
     FSVX030D   CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     D                 DS
     D  WSDTD                  1      2  0
     D  WSDTM                  3      4  0
     D  WSDTÅ                  5      6  0
     D  WSDT                   1      6  0
     D                 DS
     D  TGDTÅ                  1      4  0
     D  TGDTM                  5      6  0
     D  TGDTD                  7      8  0
     D  TGDT                   1      8  0
     D                SDS
     D  UUSR                 254    263
     D                UDS
     D  UFIRM                  1      2
     D  UAVD                  15     18  0
      *
     C                   EXSR      INIT
     C     UAVD          CHAIN     SVXSTD                             33
     C  N33S0004         COMP      *BLANKS                                33
     C   33              GOTO      SLUTT
      *
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     *LOVAL        SETLL     SVXGH
     C                   READ      SVXGH                                  94
     C                   DOW       *IN94 = *OFF
     C                   ADD       1             ZRECNO
     C                   MOVE      TGDTD         WSDTD
     C                   MOVE      TGDTM         WSDTM
     C                   MOVE      TGDTÅ         WSDTÅ
     C                   WRITE     SUBFIL
     C                   READ      SVXGH                                  94
     C                   ENDDO
      *
     C                   MOVE      ZRECNO        XRECNO            5 0
     C                   IF        ZRECNO > 0
     C                   Z-ADD     1             ZRECNO
     C                   END
      *
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYVARE
     C                   ELSE
     C                   WRITE     BUNN
     C                   EXFMT     SUBCTL
     C                   END
      *
      *** AVSLUTT
     C                   MOVE      '0'           *IN30
     C                   MOVEA     '00'          *IN(98)
     C   03              GOTO      SLUTT
     C   05              GOTO      START
     C   06              DO
     C                   EXSR      CRTTRH
     C                   GOTO      START3
     C                   END
      *
     C     ZRECNO        IFNE      0
     C                   EXSR      HENT
     C                   END
     C                   GOTO      START3
      *
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
      *
     C***********************************************
     C     HENT          BEGSR
     C***********************************************
     C                   MOVE      *BLANKS       MSGNR
     C                   MOVEA     '00'          *IN(91)
     C                   MOVEA     '00'          *IN(98)
     C     *IN98         DOWEQ     '0'                                          1<-B
     C                   READC     SUBFIL                                 98
     C  N98              SELECT
     C                   WHEN      ((WSNR = '2') AND
     C                             ((TGST = ' ') OR
     C                              (TGST = 'P')))
     C                   MOVE      '1'           *IN99
     C                   EXSR      CHGTRH
     C                   WHEN      WSNR = '3'
     C                   MOVE      '1'           *IN99
     C                   EXSR      CPYTRH
     C                   WHEN      ((WSNR = '4') AND
     C                             ((TGST = ' ') OR
     C                              (TGST = 'P')))
     C                   MOVE      '1'           *IN99
     C                   EXSR      DLTTRH
     C                   WHEN      WSNR = '5'
     C                   MOVE      '1'           *IN99
     C                   EXSR      VISTRH
     C                   WHEN      WSNR = '9'
     C                   MOVE      '1'           *IN99
     C                   EXSR      VISTOL
     C                   ENDSL
     C                   END                                                    1<-E
      *
     C     SLHENT        TAG
     C   98
     CANN99              DO
     C                   MOVE      'DIV0012'     MSGNR
     C                   MOVE      '1'           *IN30
     C                   END
     C                   MOVE      '0'           *IN99
      *
     C                   ENDSR
      *
     C***********************************************
     C     CHGTRH        BEGSR
     C***********************************************
     C                   MOVE      'CHG'         XTYPE             3
     C                   CALL      'SVX031R'
     C                   PARM      TGGNR         XGNR             24
     C                   PARM                    XTYPE
     C                   PARM                    UFLAGG            1 0
      *
     C     XGNR          CHAIN     SVXGH                              33
     C                   MOVE      TGDTD         WSDTD
     C                   MOVE      TGDTM         WSDTM
     C                   MOVE      TGDTÅ         WSDTÅ
     C                   MOVE      *BLANKS       WSNR
     C                   UPDATE    SUBFIL
      *
     C                   ENDSR
      *
     C***********************************************
     C     CPYTRH        BEGSR
     C***********************************************
     C                   MOVE      'CPY'         XTYPE             3
     C                   CALL      'SVX031R'
     C                   PARM      TGGNR         XGNR
     C                   PARM                    XTYPE
     C                   PARM                    UFLAGG            1 0
      *
     C                   MOVE      *BLANKS       WSNR
     C                   UPDATE    SUBFIL
      *
     C                   IF        XGNR <> *BLANKS
     C     XGNR          CHAIN     SVXGH                              33
     C                   MOVE      TGDTD         WSDTD
     C                   MOVE      TGDTM         WSDTM
     C                   MOVE      TGDTÅ         WSDTÅ
     C                   ADD       1             XRECNO
     C                   MOVE      XRECNO        ZRECNO
     C                   WRITE     SUBFIL
     C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     CRTTRH        BEGSR
     C***********************************************
     C                   MOVE      'CRT'         XTYPE             3
     C                   CALL      'SVX031R'
     C                   PARM      *BLANKS       XGNR
     C                   PARM                    XTYPE
     C                   PARM                    UFLAGG            1 0
     C                   IF        XGNR <> *BLANKS
     C     XGNR          CHAIN     SVXGH                              33
     C                   MOVE      TGDTD         WSDTD
     C                   MOVE      TGDTM         WSDTM
     C                   MOVE      TGDTÅ         WSDTÅ
     C                   ADD       1             XRECNO
     C                   MOVE      XRECNO        ZRECNO
     C                   WRITE     SUBFIL
     C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     DLTTRH        BEGSR
     C***********************************************
     C                   MOVE      'DLT'         XTYPE             3
     C                   CALL      'SVX031R'
     C                   PARM      TGGNR         XGNR
     C                   PARM                    XTYPE
     C                   PARM                    UFLAGG            1 0
      *
     C     XGNR          CHAIN     SVXGH                              33
     C                   MOVE      *BLANKS       WSNR
     C                   UPDATE    SUBFIL
      *
     C                   ENDSR
      *
     C***********************************************
     C     VISTRH        BEGSR
     C***********************************************
     C                   MOVE      'DSP'         XTYPE             3
     C                   CALL      'SVX031R'
     C                   PARM      TGGNR         XGNR             24
     C                   PARM                    XTYPE
     C                   PARM                    UFLAGG            1 0
      *
     C                   MOVE      *BLANKS       WSNR
     C                   UPDATE    SUBFIL
      *
     C                   ENDSR
      *
     C***********************************************
     C     VISTOL        BEGSR
     C***********************************************
      *________________________
      * Call på eksternt CL-pgm
      *""""""""""""""""""""""""
     C                   MOVEL     TGRN          UAVD2             4 0
     C                   MOVE      TGRN          UTDN2             7 0
     C                   MOVE      '2'           WIN03             1
     C                   CALL      'SAD003'
     C                   PARM                    UAVD2
     C                   PARM                    UTDN2
     C                   PARM                    WIN03
      *
     C                   MOVE      *BLANKS       WSNR
     C                   UPDATE    SUBFIL
      *
     C                   ENDSR
      *
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
      *
     C                   ENDSR
      *
