     FLLHEAD01  IF   E           K DISK    RENAME(LLHEADR:LLHEADR01)
     FLLHEAD15  UF   E           K DISK
     FLLLINE    UF   E           K DISK
     FLLAGJD02  UF   E           K DISK
     FLLAGJL01  IF   E           K DISK
jov  FLLAGJFEU  IF   E           K DISK
     FLLLISF    IF   E           K DISK
     FLLMRF     IF   E           K DISK
     FKODTS2    IF   E           K DISK
     FDKTVK     IF   E           K DISK
     FDKEA      UF   E           K DISK
     FDKEH      UF A E           K DISK
     FDKEV      O  A E           K DISK
     FCUNDL01   IF   E           K DISK
     FFIRM      IF   E           K DISK
     FDKE503D   CF   E             WORKSTN
     D UAN             S              7    DIM(90)
     D UMN             S              7    DIM(90)
     D FA              S             17    DIM(900)
     D DT              S              8  0 DIM(900)
     D VK              S              3    DIM(900)
     D BL              S             11  2 DIM(900)
     D FN1             S             17    DIM(100)
     D FN2             S              8  0 DIM(100)
      *
     D SAVUDS         UDS
     D  UFIRM                  1      2
     D  UDSAVD                15     18  0
     D  UDSSG                 19     21
      *
     D                 DS
     D  DKEH_SYAV              1      4  0
     D  DKEH_SYOP              5     11  0
     D  DKEH_SYSG             12     14
     D  X14S                   1     14
     D                 DS
     D  VAFAR                  1     10
     D  VAVAL                  1      3
      *
     C                   EXSR      INIT
      *
     C     START         TAG
     C                   Z-ADD     0             XN                3 0
     C                   MOVE      *BLANKS       UAN
     C                   MOVE      *BLANKS       UMN
     C                   MOVE      *ZEROS        XANTL             3 0
     C     LLHEAD01K     SETLL     LLHEAD01
     C     LLHEAD01K     READE     LLHEAD01                               33
     C                   DOW       *IN33 = *OFF                                 1<-B
     C                   EXSR      FILTER
     C                   IF        OMIT <> 'J'                                   2<-B
     C                   ADD       1             XN
     C                   MOVE      LHALN         UAN(XN)
     C                   MOVE      LHMN          UMN(XN)
     C                   LEAVE
     C                   END                                                     2<-E
     C     LLHEAD01K     READE     LLHEAD01                               33
     C  N33              ENDDO                                                  1<-E
      *
     C     START2        TAG
     C                   IF        UAN(1) <> *BLANKS                            1<-B
     C                   MOVE      UMN(1)        LLMN
     C     LLAGJD02K2    SETLL     LLAGJD02
     C     START3        TAG
     C     LLAGJD02K2    READE(N)  LLAGJD02                               33
     C     LLAGJL01K     CHAIN     LLAGJL01                           33
jov  C     LLAGJL01K     CHAIN     LLAGJFEU                           33
     C                   IF        *IN33
     C                   EVAL      VAOPLK = *BLANKS
     C                   EVAL      VAFVUE = *BLANKS
     C                   EVAL      VAFRUE = 0
     C                   EVAL      VAFVIE = *BLANKS
     C                   EVAL      VAFRIE = 0
     C                   END
     C     LLMRFK        CHAIN     LLMRF                              33
      *
     C                   Z-ADD     1             DKEH_SYAV
     C     DKEH_SYAV     CHAIN     DKEA                               33
     C                   ADD       1             DKEA_SYOP
     C                   UPDATE    DKEAR
      *
      * FORTOLLING HEADING
jov  c                   move      *zeros        XLI                            Nullstill linjetell.
     C                   EVAL      DKEH_SYOP = DKEA_SYOP
     C                   EVAL      DKEH_SYSG = UDSSG
     C                   EVAL      DKEH_SYDT = WDT08
     C*                  EVAL      DKEH_MRN  = ???
     C                   EVAL      DKEH_A1   = 'DK004700'
     C                   EVAL      DKEH_DTM1 = LHTDT * 10000
     C                   EVAL      DKEH_AJOU = ' '
     C                   IF        LHBLK = 'NO' OR LHBLK = 'IS' OR
     C                             LHBLK = 'CH' OR LHBLK = 'LI'
     C                   EVAL      DKEH_R011 = 'EU'
     C                   ELSE
     C                   EVAL      DKEH_R011 = 'EX'
     C                   END
     C                   EVAL      DKEH_R012 = 'A'
     C                   EVAL      DKEH_AART = '31'
     C                   EVAL      DKEH_GIS1 = '0'
     C     CUNDL01K      CHAIN     CUNDL01                            33
     C                   EVAL      DKEH_02A  = EORI
     C                   EVAL      DKEH_02B  = KNAVN
     C                   EVAL      DKEH_02C  = ADR1
     C                   IF        POSTNR = 0                                    2<-B
     C                   EVAL      DKEH_02D  = SYPOGE
     C                   ELSE                                                    2
     C                   MOVEL     POSTNR        DKEH_02D
     C                   END                                                     2<-E
     C                   EVAL      DKEH_02E  = ADR3
     C                   IF        SYLAND = *BLANKS                              2<-B
     C                   EVAL      DKEH_02F  = FILAND
     C                   ELSE                                                    2
     C                   EVAL      DKEH_02F  = SYLAND
     C                   END                                                     2<-E
     C                   EVAL      DKEH_08B  = LHBNA
     C                   EVAL      DKEH_08C  = LHBA1
     C                   EVAL      DKEH_08D  = '-'
     C                   SELECT
     C                   WHEN      LHBPN <> 0
     C                   MOVEL     LHBPN         DKEH_08D
     C                   WHEN      LHBPU <> *BLANKS
     C                   EVAL      DKEH_08D  = LHBPU
     C                   ENDSL
     C                   EVAL      DKEH_08E  = LHBA3
     C                   EVAL      DKEH_08F  = LHBLK
     C*                  EVAL      DKEH_07   = MRGN ???
     C                   EVAL      DKEH_14A  = DKEA_14A
     C                   EVAL      DKEH_14B  = DKEA_14B
     C                   EVAL      DKEH_17A  = LHBLK
jovo C*                  EVAL      DKEH_181  = 'TRUCK'
jov  C*                  EVAL      DKEH_S29  = 'Y'
     C                   EVAL      DKEH_S29  = 'H'
jov  C*                  EVAL      DKEH_21   = 'DK'
     C                   EVAL      DKEH_25   = '3'
     C                   EVAL      DKEH_26   = '3'
     C                   EVAL      DKEH_29   = 'DK005600'
     C                   EVAL      DKEH_301  = 'DK'
     C                   EVAL      DKEH_302  = '10549744'
     C                   EVAL      DKEH_303  = '200'
     C                   EVAL      DKEH_304  = '080'
     C*                  EVAL      DKEH_YM21 = '100'
     C*                  EVAL      DKEH_YM23 = '1'
      *
     C                   WRITE     DKEHR
      * FINN VALUTAKODE
     C                   MOVE      UAN(1)        LHALN
     C     LLHEAD15K     CHAIN(N)  LLHEAD15                           33
      *
     C     LHMN          SETLL     LLLINE
     C     LHMN          READE(N)  LLLINE                                 33
     C                   DOW       *IN33 = *OFF                                  2<-B
     C                   IF        LLWNT0 <> 0                                    3<-B
     C     LLAGJD02K     SETLL     LLAGJD02
     C     LLAGJD02K     READE(N)  LLAGJD02                               33
     C                   DOW       *IN33 = *OFF                                    4<-B
     C     LLAGJL01K     CHAIN     LLAGJL01                           33
     C                   SELECT                                                     5<-B
     C                   WHEN      DKVK_KD = *BLANKS                                5
     C                   IF        VAVAL   = *BLANKS                                 6<-B
     C                   EVAL      DKVK_KD = 'DKK'
     C                   ELSE                                                        6
     C                   EVAL      DKVK_KD = VAVAL
     C                   END                                                         6<-E
     C                   WHEN      ((DKVK_KD <> VAVAL) AND (VAVAL <> *BLANKS))      5
     C                   EVAL      DKVK_KD = 'DKK'
     C                   ENDSL                                                      5<-E
      * HENT FAKTURANR
     C     VAMN          CHAIN     LLLISF                             33
     C                   DOW       *IN33 = *OFF                                     5<-B
     C                   IF        LIKD = 'FAK'                                      6<-B
     C                   EVAL      XX_28B  = LITXT
     C                   IF        %SUBST(LITXT:28:8) = *BLANKS                       7<-B
     C                   EVAL      XX_28C  = WDT08
     C                   ELSE                                                         7
     C                   MOVE      LITXT         XX_28C
     C                   END                                                          7<-E
     C                   LEAVE
     C                   END                                                         6<-E
     C     VAMN          READE     LLLISF                                 33
     C                   ENDDO                                                      5<-E
     C     1             DO        100           XN                3 0              5<-B
     C                   SELECT                                                      6<-B
     C                   WHEN      FN1(XN) = *BLANKS                                 6
     C                   EVAL      FN1(XN) = XX_28B
     C                   EVAL      FN2(XN) = XX_28C
     C                   LEAVE
     C                   WHEN      XX_28B = FN1(XN) AND XX_28C = FN2(XN)             6
     C                   LEAVE
     C                   ENDSL                                                       6<-E
     C                   ENDDO                                                      5<-E
      *
     C     LLAGJD02K     READE(N)  LLAGJD02                               33
     C                   ENDDO                                                     4<-E
     C                   END                                                      3<-E
     C     LHMN          READE(N)  LLLINE                                 33
     C                   ENDDO                                                   2<-E
      *
      * OPPDATER ALLE VARELINJER
     C                   MOVE      UAN(1)        LHALN
     C     LLHEAD15K     CHAIN     LLHEAD15                           33
     C*                  ADD       LHKLL         XNTK
     C*                  ADD       LHWVEK        XVKB
      *
     C     LHMN          SETLL     LLLINE
     C     LHMN          READE     LLLINE                                 33
     C                   DOW       *IN33 = *OFF                                  2<-B
     C                   IF        LLWNT0 <> 0                                    3<-B
     C                   EXSR      OPPDLIN
     C                   MOVE      'SAD'         LLHK
     C                   END                                                      3<-E
     C                   UPDATE    LLLINER
     C     LHMN          READE     LLLINE                                 33
     C                   ENDDO                                                   2<-E
      *
     C                   MOVE      'Z'           LHSTA
     C                   MOVE      DKEH_SYAV     LHAVD
     C                   MOVE      DKEH_SYOP     LHOPD
     C                   UPDATE    LLHEADR
      *
     C     DKEHK         CHAIN     DKEH                               33
     C*                  EVAL      DKEH_28B  = XX_28B
     C*                  EVAL      DKEH_28C  = XX_28C
     C                   EVAL      DKEH_28A  = 'N380'
     C                   EVAL      DKEH_28B  = FN1(1)
     C                   EVAL      DKEH_28C  = FN2(1)
     C                   EVAL      DKEH_222  = XX_222
     C                   MOVE      XX_06         DKEH_06
     C                   MOVE      XX_BRUT       DKEH_BRUT
     C                   EVAL      DKEH_221  = DKVK_KD
     C     DKTVKK        SETGT     DKTVK
     C     DKVK_KD       READPE    DKTVK                                  33
     C                   EVAL      DKEH_221B = DKVK_KRS
     C*                  EVAL      DKEH_221C = DKVK_OMR
     C                   UPDATE    DKEHR
     C                   END                                                    1<-E
      *
     C                   IF        UAN(1) <> *BLANKS                            1<-B
     C                   GOTO      START
     C                   END                                                    1<-E
      *
     C                   EVAL      *INLR = *ON
     C                   RETURN
      *
      *////////////////////////////////////////////////////////////
     C     FILTER        BEGSR
      *////////////////////////////////////////////////////////////
     C                   MOVE      'N'           OMIT              1
      *
jov  C                   IF        LHALN = 0 or LHST1 <> 'K'
     C                   MOVE      'J'           OMIT
     C                   GOTO      UTFILT
     C                   END
      *
     C     KODTS2K       CHAIN     KODTS2                             33
     C  N33              IF        KS2PRE = 'A' OR KS2PRE = 'B'
     C                   EVAL      *IN33 = *ON
     C                   END
     C                   IF        *IN33
     C                   MOVE      'J'           OMIT
     C                   GOTO      UTFILT
     C                   END
      *
      * Summer antall linjer. Max tillate 99.
     C                   MOVE      *ZEROS        XTOTL             3 0
     C     LHMN          SETLL     LLLINE
     C     LHMN          READE(N)  LLLINE                                 33
     C                   DOW       *IN33 = *OFF
     C                   ADD       1             XTOTL
     C                   IF        XTOTL > 99
     C                   EXFMT     BILDE01
     C                   MOVE      'J'           OMIT
     C                   GOTO      UTFILT
     C                   END
     C     LHMN          READE(N)  LLLINE                                 33
     C                   ENDDO
      *
     C     UTFILT        ENDSR
      *
      *////////////////////////////////////////////////////////////
     C     OPPDLIN       BEGSR
      *////////////////////////////////////////////////////////////
     C                   CLEAR                   DKEVR
     C                   EVAL      DKEV_SYAV = DKEH_SYAV
     C                   EVAL      DKEV_SYOP = DKEH_SYOP
      *
     C     LLAGJD02K     SETLL     LLAGJD02
     C     LLAGJD02K     READE     LLAGJD02                               33
     C                   DOW       *IN33 = *OFF
     C     LLAGJL01K     CHAIN     LLAGJL01                           33
     C                   EVAL      XLI       = XLI + 1
     C                   EVAL      DKEV_SYLI = XLI
     C                   MOVE      WDT           VDLDAT
     C                   MOVE      DKEH_SYAV     VDAVD
     C                   MOVE      DKEH_SYOP     VDOPD
     C                   EVAL      VDTEXT = LHBNA
     C                   EVAL      VDST   = 'R'
     C                   EVAL      VDBUSE = 'DK-FORTOL'
     C                   MOVE      WDT           VDBDAT
     C                   MOVE      WTM           VDBKL
     C                   EVAL      VDST   = 'R'
     C                   UPDATE    LLAGJDR
      *
     C                   EVAL      DKEV_311  = 'ADR.'
     C                   EVAL      DKEV_313  = LLWNT0
     C                   EVAL      DKEV_412  = LLWNT0
     C                   EVAL      DKEV_314  = 'PK'
     C                   EVAL      DKEV_315  = LLVB
     C                   EVAL      DKEV_32   = DKEV_SYLI
     C                   EVAL      DKEV_331  = *BLANKS
     C     VAMN          CHAIN     LLLISF                             33
     C                   DOW       *IN33 = *OFF
     C                   IF        LIKD = 'TAR'
     C                   EVAL      DKEV_331  = LITXT
     C                   LEAVE
     C                   END
     C     VAMN          READE     LLLISF                                 33
     C                   ENDDO
     C                   IF        %SUBST(MRTRNR:17:2) = *BLANKS
     C                   EVAL      DKEV_34A  = 'CN'
     C                   ELSE
     C                   EVAL      DKEV_34A  = %SUBST(MRTRNR:17:2)
     C                   END
jov  c                   IF        VAOPLK <> *blanks
jov  C                   EVAL      DKEV_34A  = VAOPLK
jov  c                   endif
jov  C*                  EVAL      DKEV_35   = LLWNT0 * VAVKT0
JOV  C*                  IF        MRTRNR = *BLANKS
JOV  C*                  EVAL      DKEV_37   = '7100099'
JOV  C*                  ELSE
JOV  C*                  EVAL      DKEV_37   = %SUBST(MRTRNR:1:7)
JOV  C*                  END
JOV  C*                  EVAL      DKEV_38   = DKEV_35 * 0,9
JOV  C**                 EVAL      DKEV_37   = '4071000'
JOV  C                   EVAL      DKEV_37   = '3171000'
YBC  C*                  EVAL(H)   DKEV_38   = VAVKT0 * VASALD                  Netto Vkt
YBC  C                   EVAL(H)   DKEV_38   = VAVKT0 * LLWNT0                  Netto Vkt
JOV  C                   IF        DKEV_38 < 1
JOV  C                   EVAL      DKEV_38   = 1
JOV  C                   EVAL      DKEV_35   = 1
JOV  C                   ELSE
YBC  C*                  EVAL(H)   DKEV_35   = VAVKF0 * VASALD                  Brutto Vkt
YBC  C                   EVAL(H)   DKEV_35   = VAVKF0 * LLWNT0                  Brutto Vkt
JOV  C                   IF        DKEV_35 = 0
JOV  C                   EVAL(H)   DKEV_35   = DKEV_38 * 1,1
JOV  C                   END
JOV  C                   END
      * HVOR KAN HENTE OPPLYSNING TIL RUBR. 40?
     C                   EVAL      DKEV_401A = 'Z'
     C                   EVAL      DKEV_402A = '730'
JOV  C                   EVAL      DKEV_403A = MRGN
     C                   EVAL      DKEV_411  = 'CEN'
     C                   IF        VAVAL = DKVK_KD
     C                   EVAL(H)   DKEV_42   = VAVERD * LLWNT0
     C                   ELSE
     C     DKTVKK2       SETGT     DKTVK
     C     VAVAL         READPE    DKTVK                                  33
     C                   EVAL(H)   DKEV_42   = VAVERD*LLWNT0*DKVK_KRS/DKVK_OMR
     C                   END
      *
     C                   EVAL      DKEV_28B  = LHPAK
     C                   WRITE     DKEVR
     C                   EVAL(H)   XX_222  = XX_222  + DKEV_42
     C                   EVAL      XX_06   = XX_06   + DKEV_313
     C                   EVAL      XX_BRUT = XX_BRUT + DKEV_35
      *
     C     LLAGJD02K     READE     LLAGJD02                               33
     C                   ENDDO
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
     C     INIT          BEGSR
      *////////////////////////////////////////////////////////////
     C     *ENTRY        PLIST
     C                   PARM                    UPNR              8
      *
     C     DKEHK         KLIST
     C                   KFLD                    DKEH_SYAV
     C                   KFLD                    DKEH_SYOP
     C     DKTVKK        KLIST
     C                   KFLD                    DKVK_KD
     C                   KFLD                    XX_DTS
     C     DKTVKK2       KLIST
     C                   KFLD                    VAVAL
     C                   KFLD                    XX_DTS
     C     CUNDL01K      KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    LHPNR
     C     LLHEAD01K     KLIST
     C                   KFLD                    XX_LHSTA
     C                   KFLD                    LHGRP
     C                   KFLD                    LHPNR
     C     LLHEAD15K     KLIST
     C                   KFLD                    LHGRP
     C                   KFLD                    LHPNR
     C                   KFLD                    LHALN
     C     LLAGJL01K     KLIST
     C                   KFLD                    LHGRP
     C                   KFLD                    LHPNR
     C                   KFLD                    VDMN
     C     LLAGJD02K     KLIST
     C                   KFLD                    LHGRP
     C                   KFLD                    LHPNR
     C                   KFLD                    LLMN
     C                   KFLD                    LLVN
     C     LLAGJD02K2    KLIST
     C                   KFLD                    LHGRP
     C                   KFLD                    LHPNR
     C                   KFLD                    LHMN
     C     LLMRFK        KLIST
     C                   KFLD                    LHGRP
     C                   KFLD                    LHPNR
     C                   KFLD                    VAMRNR
     C     KODTS2K       KLIST
     C                   KFLD                    KS2UNI
     C                   KFLD                    LHBLK
      *
     C     *DTAARA       DEFINE    *LDA          SAVUDS
     C     *LIKE         DEFINE    LHSTA         XX_LHSTA
     C     *LIKE         DEFINE    DKEH_28B      XX_28B
     C     *LIKE         DEFINE    DKEH_28C      XX_28C
     C     *LIKE         DEFINE    DKEH_222      XX_222
     C     *LIKE         DEFINE    DKEH_06       XX_06
     C     *LIKE         DEFINE    DKEH_BRUT     XX_BRUT
     C     *LIKE         DEFINE    DKEH_221      XVAL
     C     *LIKE         DEFINE    DKEV_SYLI     XLI
     C     *LIKE         DEFINE    DKVK_DTS      XX_DTS
      *
     C     *LOVAL        SETLL     FIRM
     C                   READ      FIRM                                   33
     C                   MOVE      FIFIRM        UFIRM
     C                   Z-ADD     1             UDSAVD
     C                   MOVE      'CB '         UDSSG
     C                   OUT       SAVUDS
     C                   MOVE      UPNR          LHPNR
     C                   MOVE      ' '           XX_LHSTA
     C                   EVAL      KS2UNI = 'S2'
     C                   EVAL      XX_DTS = 99999999
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               8
     C                   MOVE      WDT           WDT08             8 0
     C                   WRITE     WINDOWSZ
      *
     C                   ENDSR
      *
