     FEUR15D    CF   E             WORKSTN
     F                                     SFILE(EUR15DB:ZRECNO)
     FEURORLL1  IF   E           K DISK
     F                                     RENAME(EURORLR:EURORLR1)
     FEURORL    UF A E           K DISK
     FEUROOH    IF   E           K DISK
      *//////////////
      *  Hodelogikk /
      *//////////////
     C                   EXSR      SRA
     C                   EXSR      SRB
     C                   MOVE      '1'           *INLR
      *////////////////////////////////////////////////////////////
      *  Initiering                                           SRA /
      *////////////////////////////////////////////////////////////
     C     SRA           BEGSR
      *___________________
      * Definiere Workfelt
      *"""""""""""""""""""
     C     *LIKE         DEFINE    EURORL2       WEURORL2
     C     *LIKE         DEFINE    EURORL1       WEURORL1
      *__________________
      * Definiere Nøkkler
      *""""""""""""""""""
     C     KEYA          KLIST
     C                   KFLD                    WEURORL2
     C                   KFLD                    WEURORL1
     C     KEYB          KLIST
     C                   KFLD                    WEURORL2
     C                   KFLD                    WEURORL1
     C                   KFLD                    EURORL3
     C                   KFLD                    EURORL4
      *_________________
      * Input parametrer
      *"""""""""""""""""
     C     *ENTRY        PLIST
     C                   PARM                    WEURORL2
     C                   PARM                    WEURORL1
      *__________
      * Hent hode
      *""""""""""
     C     KEYA          CHAIN     EUROOH
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Bearbeide Formater                                   SRB /
      *////////////////////////////////////////////////////////////
     C     SRB           BEGSR
     C     SRB1          TAG
      *______________
      * Tømme Subfile
      *""""""""""""""
      *
     C                   MOVE      '1'           *IN93
     C                   WRITE     EUR15DC
     C                   MOVEA     '000'         *IN(93)
     C                   Z-ADD     *ZERO         ZRECNO
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     KEYA          SETLL     EURORLL1
     C     KEYA          READE     EURORLL1                               94
     C     *IN94         DOWEQ     '0'

     C                   ADD       1             ZRECNO
     C                   WRITE     EUR15DB

     C     KEYA          READE     EURORLL1                               94
     C                   ENDDO
      *_______________
      * Behandle bilde
      *"""""""""""""""
     C     SRB5          TAG
      *
     C                   MOVEA     '11'          *IN(91)
     C  N30              MOVE      'CCC0000'     ZMSGNO
     C                   WRITE     EUR15DØ
     C                   WRITE     EUR15DD
     C                   EXFMT     EUR15DC
      *
     C     *IN12         CABEQ     '1'           SRB1
     C     *IN03         IFEQ      '0'
      *
     C                   MOVEA     '00'          *IN(91)
      *_____________
      * F5=Frisk opp
      *"""""""""""""
     C     *IN05         CABEQ     '1'           SRB1
      *_______
      * Valg ?
      *"""""""
     C                   MOVE      '0'           *IN52
     C                   MOVE      '0'           *IN30
     C                   MOVE      '0'           *IN95
     C     *IN52         DOWEQ     '0'
      *
     C                   READC     EUR15DB                                52
     C     *IN52         IFEQ      '0'
     C     ZOP           CASEQ     '2'           SRBB
     C     ZOP           CASEQ     '3'           SRBC
     C     ZOP           CASEQ     '4'           SRBD
     C                   ENDCS
     C                   ENDIF
      *
     C  N30
     CANN52              END
      *
     C     *IN03         CABEQ     '1'           SRB9
     C                   END
     C  N03              GOTO      SRB1
      *
     C     SRB9          ENDSR
      *////////////////////////////////////////////////////////////
      *  Slett linje                                         SRBD /
      *////////////////////////////////////////////////////////////
     C     SRBD          BEGSR
     C     KEYB          CHAIN     EURORL                             51
     C                   IF        *IN51=*OFF
     C                   DELETE    EURORLR
     C                   ENDIF
      *_________________
      * Oppdatere SUBFIL
      *"""""""""""""""""
     C                   MOVE      *BLANKS       ZOP
     C                   UPDATE    EUR15DB
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Vise linjen                                         SRBB /
      *////////////////////////////////////////////////////////////
     C     SRBB          BEGSR
     C                   MOVE      'CCC0000'     ZMSGNO
     C     KEYB          CHAIN     EURORL                             51
     C                   EXFMT     EUR15DE
     C                   UPDATE    EURORLR
      *_________________
      * Oppdatere SUBFIL
      *"""""""""""""""""
     C                   MOVE      *BLANKS       ZOP
     C                   UPDATE    EUR15DB
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Kopier linjen                                       SRBC /
      *////////////////////////////////////////////////////////////
     C     SRBC          BEGSR
     C                   MOVE      'CCC0000'     ZMSGNO
     C                   EXFMT     EUR15DE
     C                   WRITE     EURORLR
      *_________________
      * Oppdatere SUBFIL
      *"""""""""""""""""
     C                   MOVE      *BLANKS       ZOP
     C                   UPDATE    EUR15DB
     C                   ENDSR
