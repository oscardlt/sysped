      *
      ***********************************************************
      *** PROGRAM SKAL VEDLIKEHOLDE EKSPEDISJONSENHET            ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01 02 07 09-14 17-24             ***
      *** LEDIGE INDIKATORER 29 31 32       52-75 78-89       ***
      ***********************************************************
      *
     H DFTACTGRP(*NO)
     FSADEFEEF  UF A E           K DISK
     FSAD143D   CF   E             WORKSTN SFILE(SUBFIL:ZRECNO)
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
      *  Prototype for SAD143R
     d sad143r         pr
      *  *ENTRY Interface for Main Procedure
     d sad143r         pi
      *
     d yrecno          s                   like(zrecno)
      *
      /free

         exsr init;

         *in93 = *on;
         write subctl;
         *in93 = *off;
         *in94 = *off;
         zrecno = 0;

         dow *in03 = *off;            // START                                   // 1<b

           setll *loval sadefeef;
           read(n) sadefeef;

           dow not %eof;                                                         // 2<b
             zrecno = zrecno + 1;
             write subfil;
             read(n) sadefeef;
           enddo;                                                                // 2<e
           yrecno = zrecno;
           if zrecno > 1;                                                        // 2<b
             zrecno = 1;
           endif;                                                                // 2<e
           linnbr = 5;
           posnbr = 2;

           dow *in03 = *off;               // START3                             // 2<b
           // vis subfil og få input
             *in91 = *on;
             *in92 = *on;
             write bunn;
             if zrecno = 0;                                                      // 3<b
               exfmt nyvare;
             else;                                                               // 3
               write subctl;
               exfmt bunn;
             endif;                                                              // 3<e

             *in30 = *off;
             *in98 = *off;
             *in99 = *off;
             *in12 = *off;

             select;                                                             // 3<b
             when *in03;                                                         // 3
               *inlr = *on;
               return;
             when *in05;                                                         // 3
               *in93 = *on;
               write subctl;
               *in93 = *off;
               *in94 = *off;
               zrecno = 0;
               leave;
             when *in06;                                                         // 3
               exsr lagenhet;
               *in93 = *on;
               write subctl;
               *in93 = *off;
               *in94 = *off;
               zrecno = 0;
               leave;
             when zrecno <> 0;                                                   // 3
               exsr hent;
             endsl;                                                              // 3<e

           enddo;                                                                // 2<e

         enddo;                                                                  // 1<e

         *inlr = *on;
         return;

      //*********************************************
       begsr testb1;

         if *in40 = *off;
           if ws3039e = 0;
             *in90 = *on;
             *in30 = *on;
             msgnr = 'YBC2117';
           else;
             chain(n) ws3039e sadefeef;
             if %found;
               *in90 = *on;
               *in30 = *on;
               msgnr = 'YBC2117';
             endif;
           endif;
         endif;

         if wsstdnv = *blanks;
           *in90 = *on;
           if *in30 = *off;
             *in30 = *on;
             msgnr = 'YBC2117';
           endif;
         endif;

         if *in30 = *off;
         // Oppdatere fil
           chain ws3039e sadefeef;
           ee3039e = ws3039e;
           eestdnv = wsstdnv;
           if %found (sadefeef);
             update sadefeefr;
           else;
             write sadefeefr;
           endif;

         // Oppdatere subfil
           if *in40;
             update subfil;
           else;
             yrecno = yrecno + 1;
             zrecno = yrecno;
             write subfil;
           endif;
           write subctl;
           ws3039e = 0;
           wsstdnv = *blanks;

         endif;


       endsr;

      //*********************************************
       begsr hent;

       msgnr = *blanks;
       *in90 = *off;
       *in91 = *off;
       *in92 = *off;
       *in98 = *off;
       *in99 = *off;

       readc subfil;
       dow not %eof;
         if eestdnv <> '* SLETTET *';
           select;
           when wsnr = 2;
             *in99 = *off;
             exsr chgenhet;
           when wsnr = 4;
             *in99 = *off;
             exsr sltenhet;
           endsl;
         endif;

         readc subfil;
       enddo;

       if *in99 = *off;
         msgnr = 'DIV0012';
         *in30 = *on;
       endif;
       *in99 = *off;

       endsr;

      //*********************************************
       begsr chgenhet;

         wsnr = 0;
         ws3039e = ee3039e;
         wsstdnv = eestdnv;
         *in40 = *on;
         linnbr = 22;
         posnbr = 12;
         *in99 = *on;

         dow *in12 = *off;
           exfmt vareli;
           *in90 = *off;
           *in89 = *off;
           *in30 = *off;
           if *in12 = *off;
             exsr testb1;
             if *in30 = *off;
               leave;
             endif;
           else;
             *in40 = *off;
             ws3039e = 0;
             wsstdnv = *blanks;
             update subfil;
           endif;
         enddo;

       endsr;

      //*********************************************
       begsr sltenhet;

         wsnr = 0;
         *in99 = *on;
         ws3039e = ee3039e;

         exfmt varel3;
         if *in12 = *off;
           chain ws3039e sadefeef;
           if %found;
             delete sadefeefr;
           endif;
           ee3039e = 0;
           eestdnv = '* SLETTET *';
         endif;

         update subfil;
         write subctl;

       endsr;


      //*********************************************
       begsr lagenhet;

         *in40 = *off;
         linnbr = 22;
         posnbr = 5;
         ws3039e = 0;
         wsstdnv = *blanks;

         dow *in12 = *off;
           exfmt vareli;
           *in90 = *off;
           *in89 = *off;
           *in30 = *off;
           if *in12 = *off;
             exsr testb1;
             if *in30 = *off;
               leave;
             endif;
           else;
             *in40 = *off;
             ws3039e = 0;
             wsstdnv = *blanks;
           endif;
         enddo;

       endsr;

      //*********************************************
       begsr init;

       endsr;

      /end-free
