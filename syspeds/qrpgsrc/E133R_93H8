      * OBS!!!
      * OBS - benyttes kun av RAMBERG - deres versjon mÂ regnes som "MASTER"
      * OBS!!!
      * BASERT P≈ E133R_93A8, MEN E133R_93H8 SKRIVER TIL HENTE-OPPDRAGSSYSTEM
      *
      * IFTMIN/FRAKTBREV MANGLER JO 'TURHEADER' SOM I IFCSUM DANNER GRUNNLAG FOR HENOP
      * LOGIKKEN VED IFTMIN ER DOM FÿLGER:
      *      DERSOM DET P≈ SAMME KUNDE/SAMME DAG FINNES EN ≈PEN HEADER S≈ FÿYES DET TIL DENNE
      *      DERSOM HEADER IKEK FINNES, ELLER ER OVERFÿRT, ELLER ER MERKET SOM 'STENGT' S≈ LAGES NY.
      *
HARD  * VIKTIGE HARDKODINGER ET MERKET MED HARD I 1-5
      *****************************************************************
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
11XXX * 01 JOV PTF11 fixai
11XXX * 02 JOV PTF11 feil i sendnr til HENOP-Hode
      * 03 JOV PTF11 clear av array A f¯r kall pÂ getnum
********************************************************************
     H DATEDIT(*YMD)
     FEDIS      IF   E           K DISK
     FEDIM      IF   E           K DISK
     FEUNB      IF   E           K DISK
     FFIRM      IF   E           K DISK
     FCUNDL01   IF   E           K DISK
     FVADR      IF   E           K DISK
     FECMSG2    IF   E           K DISK
     FEMOA93A   IF   E           K DISK
     FEMEA93A   IF   E           K DISK
     FECNT93A   IF   E           K DISK
     FEFTX93A   IF   E           K DISK
     FEGID93A   IF   E           K DISK
     FEPCI93A   IF   E           K DISK
     FENAD93A   IF   E           K DISK
     FERFF93A   IF   E           K DISK
     FETDT93A   IF   E           K DISK
     FETSR93A   IF   E           K DISK
     FFIRINL    IF   E           K DISK
     FKODTRI    IF   E           K DISK
     FNOPNR     IF   E           K DISK
     FHEAD39    IF   E           K DISK
     F                                     RENAME(HEADFR:HEAD39R)
     FPONRN     IF   E           K DISK
     FHENOP02   IF   E           K DISK
     F                                     RENAME(HENOPR:HENOP2R)
     FHENOP     UF A E           K DISK
     FHENOS01   IF   E           K DISK
     F                                     RENAME(HENOSR:HENOS1R)
     FHENOS     IF A E           K DISK
     FHENOF     O  A E             DISK
     FHENOK     O  A E             DISK
     FKUNKO2    IF   E           K DISK
     D A               S              1    DIM(79)
     D B               S              1    DIM(18)
      *________________________
      * DS for decimal handling
      *""""""""""""""""""""""""
     D                 DS
     D  ZDEC                   1      7  6
     D  ZDEC6                  2      7
     D  ZHEL                  11     22  0
     D                 DS
     D  WMN2                   1      9  0
     D                 DS
     D  M1004                  1     35
     D  W1004A                 1     17
     D  W1004b                11     17
     D                 DS
     D  XULTID                 1     14  0
     D  XULTM                  1      6  0
     D  XULDD                  7      8  0
     D  XULMM                  9     10  0
     D  XUL≈≈                 11     14  0
     D                 DS
     D  UL≈≈                   1      4  0
     D  ULMM                   5      6  0
     D  ULDD                   7      8  0
     D  ULDT                   1      8  0
N01   * takle bÂde med / uten AI
N01  D                 DS
N01  D  CLID00                 1      2    inz('00')
N01  D  CLID                   3     22
N01  D  CLID20                21     22
N01  D  CLIDB                  1     20
      *___________
      * Initiering
      *"""""""""""
     C                   EXSR      INIT
      *________
      * CONVERT
      *""""""""
     C                   EXSR      RTVD
      *
     C                   MOVE      '1'           *INLR
     C                   RETURN
      *
      **********************************************
     C     RTVD          BEGSR
      **********************************************
      *__________________
      * INIT. DATAFELTER
      *""""""""""""""""""
     C     W1004b        IFne      *blanks
     C                   MOVEL     '401'         SNDN20           20
     C                   MOVE      W1004A        SNDN20
     c                   else
     C                   MOVEL     W1004A        SNDN20
     c                   end
      * FINNES SENDINGEN ALLEREDE...
     C     Sndn20        chain     Head39                             33
     C   33Sndn20        chain     HENOS01                            33
     C     *IN33         CABEQ     '0'           NXTFBR
      *
      * HEADER - Lage ny / fortsette pÂ eksisterende
     C                   EXSR      HENOPSR
      *
      * lag sendingen i HENOS/kolli i HENOK mm
     C                   ADD       1             WSLNR2
     C                   CLEAR                   HENOSR
     C                   MOVE      HOLNR         HSLNR
     C                   MOVE      WSLNR2        HSLNR2
     C                   MOVEL     SNDN20        HSSNDN
     C                   MOVE      HODT          HSDT
     C                   MOVEL     'S'           HSFR
      *
     C                   MOVE      '0'           *IN51
      *__________________
      * READ ALL SEGMENTS
      *""""""""""""""""""
     C                   Z-ADD     CREP0         ZREPB0
     C                   EXSR      NAD10
     C                   EXSR      RFF03
     C                   EXSR      FTX00
     C                   EXSR      TSR00
     C                   EXSR      CNT00
     C                   EXSR      MEA19
     C                   EXSR      GID16
N01  C     HSPROD        IFEQ      'K'
N01  C     HSANT         ANDGT     1
N01  C                   MOVEL     ' '           HSPROD
N01  C                   end
      *
      *________________
      * WRITE TO SYSPED
      *""""""""""""""""
     C                   WRITE     HENOSR
      *
      * OPPDATER LINJETOTALER P≈ HENOP
     C     HOLNR         CHAIN     HENOP                              33
     C     *IN33         IFEQ      *ZEROS
     C                   ADD       HXVKT         HOVKT
     C                   ADD       HXFBV         HOFBV
     C                   ADD       HXM3          HOM3
     C                   ADD       HXLM          HOLM
     C                   UPDATE    HENOPR
     C                   END
      *
     C     NXTFBR        TAG
     C                   ENDSR
     C***********************************************
     C     HENOPSR       BEGSR
     C***********************************************
     C                   MOVE      *ZEROS        WSLNR2            3 0          NR INNE HENOP
     C                   MOVE      *ZEROS        HXVKT
     C                   MOVE      *ZEROS        HXFBV
     C                   MOVE      *ZEROS        HXM3
     C                   MOVE      *ZEROS        HXLM
      *
      *HENTE KUNDENR FRA ECMSG ELLER fra NAD+CZ (PRIO 1)
     C     ECMKEY        KLIST
     C                   KFLD                    M0004                          AVSENDER
     C                   KFLD                    M0010                          MOTTAKER
     C                   KFLD                    MSR                            R
     C                   KFLD                    M0065                          IFTMIN
     C     ECMKEY        CHAIN     ECMSG2                             33
     C     *IN33         IFEQ      '0'
     C     ECCNO         ANDNE     *ZEROS
     C                   Z-ADD     ECCNO         KUNDNR
     C                   ENDIF
      *
     C                   Z-ADD     10            ZGN0
     C     KEYMS0        SETLL     ENAD93A
     C     KEYMS0        READE     ENAD93A                                51
     C     *IN51         DOWEQ     '0'
     C     N3035         IFEQ      'CZ'                                         Avsender
      * ved oppgitt kunenummer i CZ benytt det (nÂr det f.eks sendeds fra AXIAFRAKT )
N03  C                   CLEAR                   A
     C                   MOVEA     N3039         A
     C                   EXSR      GETNUM
BHR  C**   N3055         IFEQ      '87'                                         LEMAN uten kvalik.
BHR  C**   ZNUM          andne     *zeros
BHR  C     ZNUM          ifne      *zeros
     C                   Z-ADD     ZNUM          KUNDNR
     C                   ENDIF
     C                   LEAVE
     C                   ENDIF
     C     KEYMS0        READE     ENAD93A                                51
     C  N51              ENDDO
      * finn evt BILNR
     C                   EXSR      TDT08
      *
      *  sjekk om det finnes en eksistyerende/Âpen HENOP som skal adderes til
     c     Henop2Key     setgt     HENOP02
     c     Henop2Key     readpe    HENOP02                                33
     c     *in33         doweq     '0'
     c     HOMOPD        ifeq      8888888                                      ikke avslutte
     c     HOSTIN        andeq     *zeros                                       samtaxpris ikke satt
     c     HOBILN        andeq     FFBIL                                        samme bil (hvis gitt
     C     HENOSMAX      setgt     HENOS
     c     HOLNR         readpe    HENOS                                  33
     c  N33              move      HSLNR2        WSLNR2
     c                   goto      UtHenop                                      FUNNET ≈PENT-HOPP UT
     c                   endif
     c     Henop2Key     readpe    HENOP02                                33
     c                   enddo
      *
      ** FINNES IKKE / NY skal lages...
      * INTIER DIV. FELT I HENOP
      *
      * HENT NESTE LÿPENR
     C                   MOVE      *ZEROS        HOLNR
     C     9999999       SETGT     HENOP
     C                   READP(N)  HENOP                                  33
     C     HOLNR         ADD       1             WSLNR             7 0
      * Klargj¯r HENOP
     C                   CLEAR                   HENOPR
     C                   MOVE      MMN           HOEDSN
     C                   MOVE      8888888       HOMOPD                         ≈PENT IFTMIN-H.OPPDR
     C                   MOVE      KUNDNR        HOKNS
     C                   MOVE      WSLNR         HOLNR
N02   * enkelte sender foranstilt 401 (dsv)
N02  c                   if        %subst(M1004:1:3) = '401'
     C                   MOVEL     M1004         HOSNDN
     c                   MOVEL     '402'         HOSNDN
N02  C                   else
N02  c                   Eval      HOSNDN='402'+ M1004
N02  C                   endif
     C                   MOVE      MDT           HODT
     C                   MOVE      FFBIL         HOBILN
BHRRIC     MOTEDI        IFEQ      'RIKSGODS'
BHRRIC                   MOVE      1000          HOPNRT
BHRRIC                   ELSE
BHRRIC                   MOVE      3000          HOPNRT
BHRRIC                   END
      *
     C     KUNKEY        CHAIN     CUNDL01                            33
     C     *IN33         IFEQ      '0'
     C     VADRKE        CHAIN     VADR                               33
     C     *IN33         IFEQ      '0'
     C                   MOVE      VADRNA        HONAS
     C                   MOVE      VADRN1        HOADS1
     C                   MOVE      *zeros        HOPNRS
     C                   MOVE      VADRN3        HOSTDS
     C                   MOVEL     VADRN3        PNRA              4
     C                   TESTN                   PNRA                 33
     c     *in33         ifeq      '1'
     C                   MOVE      PNRA          HOPNRS
     C                   eval      HOSTDS=%subst(VADRN3:5:26)
     c                   endif
     C                   ELSE
     C                   MOVEL     KNAVN         HONAS
     C                   MOVEL     ADR1          HOADS1
     C                   MOVEL     POSTNR        HOPNRS
     C                   MOVE      ADR3          HOSTDS
     C                   END
     C                   END
     C                   WRITE     HENOPR
      *
     c     UtHenop       endsr
      *
      *////////////////////////////////////////////////////////////
      * CNT00                                               CNT00 /
      *////////////////////////////////////////////////////////////
     C     CNT00         BEGSR
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     0             ZGN0
     C     KEYMS0        SETLL     ECNT93A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS0        READE     ECNT93A                                51
     C     *IN51         DOWEQ     '0'
      *
N03  C                   CLEAR                   A
     C                   MOVEA     C6066         A
     C                   EXSR      GETNUM
     C                   SELECT
     C     C6069         WHENEQ    '7  '
     C                   Z-ADD     ZNUM          HSVKT
     C                   ADD       HSVKT         HXVKT
     C                   ADD       HSVKT         HXFBV
     C     C6069         WHENEQ    '11 '
     C                   Z-ADD     ZNUM          HSANT
     C     C6069         WHENEQ    '15 '                                         DMQ
     C     C6411         IFEQ      'DMQ'
     C     ZNUM          DIV(H)    1000          HSM3
     C                   ELSE
     C                   Z-ADD     ZNUM          HSM3
     C                   END
     C                   ADD       HSM3          HXM3
     C     C6069         WHENEQ    'ZLM'
     C                   Z-ADD     ZNUM          HSLM
     C                   ENDSL
     C                   ADD       HSLM          HXLM
      *
     C  N51KEYMS0        READE     ECNT93A                                51
     C  N51              ENDDO
     C                   ENDIF
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      * MEA19                                               MEA19 /
      *////////////////////////////////////////////////////////////
     C     MEA19         BEGSR
     C     *LIKE         DEFINE    HSVKT         MEVKT
     C     *LIKE         DEFINE    HSM3          MEM3
     C     *LIKE         DEFINE    HSLM          MELM
     C                   MOVE      *ZEROS        MEVKT
     C                   MOVE      *ZEROS        MEM3
     C                   MOVE      *ZEROS        MELM
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     19            ZGN0
     C     KEYMS0        SETLL     EMEA93A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS0        READE     EMEA93A                                51
     C     *IN51         DOWEQ     '0'
      *
N03  C                   CLEAR                   A
     C                   MOVEA     M6314         A
     C                   EXSR      GETNUM
     C                   IF        ((M6311 = 'WT') AND (M6313 = 'G'))
     C                   EVAL      MEVKT = MEVKT + ZNUM
     C                   END
     C                   IF        ((M6311 = 'VOL') AND (M6411 = 'MTQ'))
     C                   EVAL      MEM3 = MEM3 + ZNUM
     C                   END
     C                   IF        ((M6311 = 'VOL') AND (M6411 = 'DMQ'))
     C     ZNUM          DIV       1000          TMPM3             9 5
     C                   EVAL      MEM3 = MEM3 + tmpm3
     C                   END
     C                   IF        M6311 = 'LMT'
     C                   EVAL      MELM = MELM + ZNUM
     C                   END
      *
     C  N51KEYMS0        READE     EMEA93A                                51
     C  N51              ENDDO
     C                   ENDIF
      *
      * DERSOM 0 FRA CNT OPPDATER MED DET SOM FANTES I MEA
     C     HSVKT         IFEQ      *ZEROS
     C                   eval      HSVKT = MEVKT
     C                   eval      HXVKT = MEVKT
     C                   end
     C     HSM3          IFEQ      *ZEROS
     C                   eval      HSM3  = MEM3
     C                   eval      HXM3  = MEM3
     C                   end
     C     HSLM          IFEQ      *ZEROS
     C                   eval      HSLM  = MELM
     C                   eval      HXLM  = MELM
     C                   end
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      * TSR00                                               TSR00 /
      *////////////////////////////////////////////////////////////
     C     TSR00         BEGSR
      *
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     0             ZGN0
     C     KEYMS0        SETLL     ETSR93A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS0        READE     ETSR93A                                51
     C     *IN51         DOWEQ     '0'
      *
      * RMD=FARLIG (-> ADR-MERKING)  ZHO=VARME (-> IMD-MERKING)
     C                   SELECT
     C     T7085         WHENEQ    'RMD'
     C     T4065         OREQ      'RMD'
     C                   MOVEL     'X'           HSFARL                         *FARLIG
     C     T7085         WHENEQ    'ZHO'
     C     T4065         OREQ      'ZHO'
     C                   MOVEL     'X'           HSVARM                         *VARME
     C     T7085         WHENEQ    'ZPL'
     C     T4065         OREQ      'ZPL'
     C                   MOVEL     'P'           HSPROD                         *PALL PRISET
     C     T7085         WHENEQ    'ZHP'
     C     T4065         OREQ      'ZHP'
     C                   MOVEL     'H'           HSPROD                         *Halv-PALL PRISET
     C     T7085         WHENEQ    'ZKL'
     C     T4065         OREQ      'ZKL'
     C                   MOVEL     'K'           HSPROD                         *KOLLIPRISET
     C     T7085         WHENEQ    'ZPG'
     C     T4065         OREQ      'ZPG'
     C                   MOVEL     'D'           HSPROD                         *Partigods/direkte
     C                   ENDSL
     C     KEYMS0        READE     ETSR93A                                51
     C  N51              ENDDO
     C                   ENDIF
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      * FTX00                                               FTX00 /
      *////////////////////////////////////////////////////////////
     C     FTX00         BEGSR
     C                   CLEAR                   HENOFR
     C                   MOVE      HOLNR         HFLNR
     C                   MOVE      WSLNR2        HFLNR2
     C                   MOVE      HSSNDN        HFSNDN
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     0             ZGN0
     C     KEYMS0        SETLL     EFTX93A                                20
     C     KEYMS0        READE     EFTX93A                                51
     C     *IN51         DOWEQ     '0'
      *
     C                   MOVE      ' '           HFKODE
     C     F4451         IFEQ      'SIC'
     C                   MOVE      'G'           HFKODE                         GODSLISTE/TRANSP
     C                   ENDIF
     C     F4451         IFEQ      'ICN'
     C                   MOVE      'M'           HFKODE                         MOTTAKER/KUNDE
     C                   ENDIF
      *
     C                   Add       1             HFLNR3
     C                   MOVEL     F4440A        HFTXT
     C                   WRITE     HENOFR
      *
     C     F4440B        IFNE      *BLANKS
     C                   Add       1             HFLNR3
     C                   MOVEL     F4440B        HFTXT
     C                   WRITE     HENOFR
     C                   ENDIF
     C     F4440C        IFNE      *BLANKS
     C                   Add       1             HFLNR3
     C                   MOVEL     F4440C        HFTXT
     C                   WRITE     HENOFR
     C                   ENDIF
     C     F4440D        IFNE      *BLANKS
     C                   Add       1             HFLNR3
     C                   MOVEL     F4440D        HFTXT
     C                   WRITE     HENOFR
     C                   ENDIF
     C     F4440E        IFNE      *BLANKS
     C                   Add       1             HFLNR3
     C                   MOVEL     F4440E        HFTXT
     C                   WRITE     HENOFR
     C                   ENDIF
     C     KEYMS0        READE     EFTX93A                                51
     C  N51              ENDDO
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * FTX16                                               FTX16 /
      *////////////////////////////////////////////////////////////
     C     FTX16         BEGSR
     C                   MOVE      '0'           *IN52
     C                   Z-ADD     16            ZGN1
     C     KEYMS1        SETLL     EFTX93A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS1        READE     EFTX93A                                52
     C     *IN52         DOWEQ     '0'
      *
     C     F4451         IFEQ      'AAA'
     C     HSVB          CAT       F4440A:1      HSVB
     C                   ENDIF
      *
     C  N52KEYMS1        READE     EFTX93A                                52
     C  N52              ENDDO
     C                   ENDIF
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      * PCI22                                               PCI22 /
      *////////////////////////////////////////////////////////////
     C     PCI22         BEGSR
     C                   MOVE      *ZEROS        HKLNR3
     C                   MOVE      '0'           *IN52
     C                   Z-ADD     22            ZGN1
     C     KEYMS1        SETLL     EPCI93A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS1        READE     EPCI93A                                52
     C     *IN52         DOWEQ     '0'
      *
      * OPPDATER KUN KOLLI-ID-ER (TYPE 18)
     C     P4233         IFEQ      '18'
     C                   MOVE      HSLNR         HKLNR
     C                   MOVE      HSLNR2        HKLNR2
     C     P7102A        IFNE      *BLANKS
     C                   ADD       1             HKLNR3
N01  C                   MOVEL     P7102A        CLID
N01  c                   exsr      FixAI
     C                   WRITE     HENOKR
     C                   END
     C     P7102B        IFNE      *BLANKS
     C                   ADD       1             HKLNR3
N01  C                   MOVEL     P7102B        CLID
N01  c                   exsr      FixAI
     C                   WRITE     HENOKR
     C                   END
     C     P7102C        IFNE      *BLANKS
     C                   ADD       1             HKLNR3
N01  C                   MOVEL     P7102C        CLID
N01  c                   exsr      FixAI
     C                   WRITE     HENOKR
     C                   END
     C     P7102D        IFNE      *BLANKS
     C                   ADD       1             HKLNR3
N01  C                   MOVEL     P7102D        CLID
N01  c                   exsr      FixAI
     C                   WRITE     HENOKR
     C                   END
     C     P7102E        IFNE      *BLANKS
     C                   ADD       1             HKLNR3
N01  C                   MOVEL     P7102E        CLID
N01  c                   exsr      FixAI
     C                   WRITE     HENOKR
     C                   END
     C     P7102F        IFNE      *BLANKS
     C                   ADD       1             HKLNR3
N01  C                   MOVEL     P7102F        CLID
N01  c                   exsr      FixAI
     C                   WRITE     HENOKR
     C                   END
     C     P7102G        IFNE      *BLANKS
     C                   ADD       1             HKLNR3
N01  C                   MOVEL     P7102G        CLID
N01  c                   exsr      FixAI
     C                   WRITE     HENOKR
     C                   END
     C     P7102H        IFNE      *BLANKS
     C                   ADD       1             HKLNR3
N01  C                   MOVEL     P7102H        CLID
N01  c                   exsr      FixAI
     C                   WRITE     HENOKR
     C                   END
     C     P7102I        IFNE      *BLANKS
     C                   ADD       1             HKLNR3
N01  C                   MOVEL     P7102I        CLID
N01  c                   exsr      FixAI
     C                   WRITE     HENOKR
     C                   END
     C     P7102J        IFNE      *BLANKS
     C                   ADD       1             HKLNR3
N01  C                   MOVEL     P7102J        CLID
N01  c                   exsr      FixAI
     C                   WRITE     HENOKR
     C                   END
     C                   END
      *
     C     KEYMS1        READE     EPCI93A                                52
     C  N52              ENDDO
     C                   ENDIF
      *
     C                   ENDSR
N01   ************************************************************
N01  C     FixAI         BEGSR
N01   ************************************************************
N01  c     CLID20        ifne      *blanks
N01  C                   MOVEL     CLID          HKID
N01  c                   else
N01  C                   MOVEL     CLIDB         HKID
N01  c                   end
N01  C                   ENDSR
N01   *
      *
      *////////////////////////////////////////////////////////////
      *  Get numerical value                               GETNUM /
      *////////////////////////////////////////////////////////////
     C     GETNUM        BEGSR
     C                   MOVE      *ZEROS        ZHEL
     C                   MOVE      *ZEROS        ZDEC
     C                   MOVE      *ZEROS        B
     C                   Z-ADD     1             AX                2 0
     C                   Z-ADD     1             BX                2 0
      * Get Integer
     C     A(AX)         DOWGE     '0'
     C     ZHEL          MULT      10            ZHEL
     C                   MOVE      A(AX)         ZHEL
     C                   ADD       1             AX
     C                   ENDDO
      * Decimal point ?
     C     A(AX)         IFEQ      '.'
     C     A(AX)         OREQ      ','
     C                   ADD       1             AX
     C     A(AX)         DOWGE     '0'
     C                   MOVE      A(AX)         B(BX)
     C                   ADD       1             AX
     C                   ADD       1             BX
     C                   ENDDO
     C                   MOVEA     B             ZDEC6
     C                   ENDIF
      * Concatenate
     C                   Z-ADD     ZHEL          ZNUM             18 6
     C                   ADD       ZDEC          ZNUM
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * GID16                                               GID16 /
      *////////////////////////////////////////////////////////////
     C     GID16         BEGSR
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     16            ZGN0
     C     KEYMS0        SETLL     EGID93A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS0        READE     EGID93A                                51
     C     *IN51         DOWEQ     '0'
      *
     C                   MOVEL     G7064A        HSVB
      *
     C                   Z-ADD     GREP0         ZREPB0
     C                   EXSR      FTX16
     C                   EXSR      PCI22
      *
     C     KEYMS0        READE     EGID93A                                51
     C  N51              ENDDO
     C                   ENDIF
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *                                                      INIT /
      *////////////////////////////////////////////////////////////
     C     INIT          BEGSR
     C     KEYMS0        KLIST
     C                   KFLD                    ZMN
     C                   KFLD                    ZGN0
     C     KEYMS1        KLIST
     C                   KFLD                    ZMN
     C                   KFLD                    ZGN1
     C                   KFLD                    ZREPB0
     C     KODRIK        KLIST
     C                   KFLD                    XXTERM            4 0
     C                   KFLD                    NUMPNR            4 0
HARD C                   MOVE      1000          XXTERM
     C     HENOSMAX      KLIST
     C                   KFLD                    HOLNR
     C                   KFLD                    LNR2max
     C                   z-add     999           LNR2max           3 0
     C     KUNKEY        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    KUNDNR
     C     VADRKE        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    KUNDNR
     C                   KFLD                    VADRNR
     C                   Z-ADD     1             VADRNR
     C     KUNKOK        KLIST
     C                   KFLD                    HOKNS
     C                   KFLD                    KUINTN
     c     Henop2Key     klist
     c                   kfld                    XXST              1
     c                   kfld                    Idag8
     c                   kfld                    KUNDNR
     C                   TIME                    XULTID
     C                   MOVE      XULDD         ULDD
     C                   MOVE      XULMM         ULMM
     C                   MOVE      XUL≈≈         UL≈≈
     C                   MOVE      ULDT          Idag8             8 0
      *
     C     *LIKE         DEFINE    NMN           ZMN
     C     *LIKE         DEFINE    NGN           ZGN0
     C     *LIKE         DEFINE    NGN           ZGN1
     C     *LIKE         DEFINE    HOVKT         HXVKT
     C     *LIKE         DEFINE    HOFBV         HXFBV
     C     *LIKE         DEFINE    HOM3          HXM3
     C     *LIKE         DEFINE    HOLM          HXLM
     C     *LIKE         DEFINE    NREP0         ZREPB0
     C     *LIKE         DEFINE    HSAVDH        AVDZER
     C                   MOVE      *ZEROS        AVDZER
     C     AVDZER        CHAIN     FIRINL                             33
      *
     C     *ENTRY        PLIST
     C                   PARM                    WMN1              9
     C                   MOVEL     WMN1          WMN2
     C                   Z-ADD     WMN2          ZMN
      * Get msg and interchange values
     C     ZMN           CHAIN     EDIM                               51
     C     MSN           CHAIN     EDIS                               51
      *
BHRRI * TERMINAL VIA MOTTAGER EDI
BHRRIC                   MOVEL     M0010         MOTEDI            8
BHRRIC     MOTEDI        IFEQ      'RIKSGODS'
BHRRIC                   MOVE      1000          XXTERM
BHRRIC                   ELSE
BHRRIC                   MOVE      3000          XXTERM
BHRRIC                   END
     C     *LOVAL        SETLL     FIRM
     C                   READ      FIRM                                   33
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * NAD10                                               NAD10 /
      *////////////////////////////////////////////////////////////
     C     NAD10         BEGSR
     C                   Z-ADD     10            ZGN0
     C     KEYMS0        SETLL     ENAD93A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS0        READE     ENAD93A                                51
     C     *IN51         DOWEQ     '0'
      *
     C                   SELECT
      * Shipper oppgitt - overstyrer oppdragsgivers adresse som avsender
     C     N3035         WHENEQ    'SH'                                         Avsender
      *
      * CZ er alt hÂndtert lenger opp
     C     N3035         WHENEQ    'CZ'                                         Avsender
      *
     C     N3035         WHENEQ    'CN'                                         MOTTAKER
     C     N3055         IFEQ      '87'
N03  C                   CLEAR                   A
     C                   MOVEA     N3039         A
     C                   EXSR      GETNUM
     C                   Z-ADD     ZNUM          HSKNK
     C                   ELSE
     C                   MOVEL     N3039         KUINTN
     C     KUNKOK        CHAIN     KUNKO2                             33
     C  N33              MOVE      KUKUN2        HSKNK
     C                   END
     C                   MOVEL     N3036A        HSNAK
     C                   MOVEL     N3042A        HSADK1
     C                   MOVEL     N3251         PNRA              4
     C                   TESTN                   PNRA                 33
     C   33              MOVE      PNRA          HSPNRK
     C                   MOVEL     N3164         STED25           25
     C     STED25        IFEQ      *BLANKS
     C     PNRA          CHAIN     PONRN                              33
     C  N33              MOVEL     PONNAV        STED25
     C                   ENDIF
     C                   MOVE      STED25        HSSTDK
      * avvikende leveringsadresse
     C     N3035         WHENEQ    'DP'                                         LEV.ADR
     C                   MOVEL     N3036A        HSNAK
     C                   MOVEL     N3042A        HSADK1
     C                   MOVEL     N3251         PNRA              4
     C                   TESTN                   PNRA                 33
     C   33              MOVE      PNRA          HSPNRK
     C                   MOVEL     N3164         STED25           25
     C     STED25        IFEQ      *BLANKS
     C     PNRA          CHAIN     PONRN                              33
     C  N33              MOVEL     PONNAV        STED25
     C                   ENDIF
     C                   MOVE      STED25        HSSTDK
      *
     C                   ENDSL
      *
     C     KEYMS0        READE     ENAD93A                                51
     C  N51              ENDDO
     C                   ENDIF
      *
      *
      * FINN AVD. UTFRA MOTTAKERS POSTNR
     C                   MOVE      HSPNRK        NUMPNR
     C     KODRIK        CHAIN     NOPNR                              33
     C     *IN33         IFEQ      *OFF
     C     NOAVDU        ANDNE     *ZEROS
     C                   MOVE      NOAVDU        RIAVDU
     C                   MOVE      NOAVDI        RIAVDI
     C                   MOVE      NOAVDH        RIAVDH
     C                   goto      AvdOK
     C                   END
     C     KODRIK        SETGT     KODTRI
     C     XXTERM        READPE    KODTRI                                 33
     C  N33NUMPNR        IFGE      RIPNRF
     C     NUMPNR        ANDLE     RIPNRT
     C     AvdOk         Tag
     C                   MOVE      RIAVDU        HSAVDH
     C                   END
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * TDT08                                               TDT08 /
      *////////////////////////////////////////////////////////////
     C     TDT08         BEGSR
     C                   MOVEL     *blanks       FFBIL            10
     C                   MOVEL     *blanks       DIRBIL           10
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     8             ZGN0
     C     KEYMS0        SETLL     ETDT93A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS0        READE     ETDT93A                                51
     C     *IN51         DOWEQ     '0'
      *
     C*                  SELECT
     C*    T8051         WHENEQ    '10'
     C*                  MOVEL     T8212         FFBIL
     C*    T8051         WHENEQ    '20'
     C*                  MOVEL     T8212         DIRBIL
     C*    T8051         WHENEQ    '22'
     C*                  MOVEL     T8212         DIRBIL
     C*                  ENDSL
     C                   MOVEL     T8212         FFBIL

     C  N51KEYMS0        READE     ETDT93A                                51
     C  N51              END
     C                   END
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * RFF03                                               RFF03 /
      *////////////////////////////////////////////////////////////
     C     RFF03         BEGSR
      *
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     3             ZGN0
     C     KEYMS0        SETLL     ERFF93A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS0        READE     ERFF93A                                51
     C     *IN51         DOWEQ     '0'
      *
     C     R1153         IFEQ      'CU '                                           TG
     C     R1153         OREQ      'CO '                                           LG
BHR  C     R1153         OREQ      'ZMO'                                           FREJA-MOROPPDRAG
     C                   MOVEL     R1154         HSRFA
     C                   ENDIF
      *
     C  N51KEYMS0        READE     ERFF93A                                51
     C  N51              ENDDO
     C                   ENDIF
      *
     C                   ENDSR
      *

