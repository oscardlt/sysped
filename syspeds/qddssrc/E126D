     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      CA03(03 'Avslutt')
     A                                      CA12(12 'Forrige')
     A                                      ALTHELP
     A                                      HELP
      *
     A          R E126D01                   SFL
     A            XLN            3  0O  5  2EDTCDE(4)
     A            XVN           15A  O  5  6
     A            XVTXT         15A  O  5 22
     A            XVGR           3A  O  5 38
     A            XNT            5Y 2O  5 42EDTCDE(L)
     A            XPRIS          7Y 2O  5 50EDTCDE(4)
     A            XTOTL          9Y 2O  5 59EDTCDE(L)
     A            XRABAT         7Y 2O  5 71EDTCDE(L)
      *
     A          R E126D02                   SFLCTL(E126D01)
     A                                      SFLSIZ(0017)
     A                                      SFLPAG(0016)
     A                                      CA05(05 'Frisk')
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            ZRECNO         4S 0O  1 56SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYEDI/E126R_93A'
     A                                      COLOR(BLU)
     A                                  1 20'Vise faktura/kreditnota'
     A                                      COLOR(WHT)
     A                                  1 62DATE
     A                                      EDTCDE(Y)
     A                                  1 72TIME
     A                                  2  2'Faktura/kreditnotanr.'
     A            UFN           10A  O  2 24DSPATR(UL)
     A                                      COLOR(BLU)
     A                                  2 42'Ordrenr.'
     A            UKON           7A  O  2 51DSPATR(UL)
     A                                      COLOR(BLU)
     A                                  3  2'Lin Varenummer      Varebeskrivels-
     A                                      e Vgr Antall. Pris.... Linjepris.. -
     A                                      T.lin.rab'
     A                                      COLOR(WHT)
     A            W2LN           3  0B  4  2DSPATR(UL)
      *
     A          R E126D03
     A                                      SLNO(22)
     A                                      CLRL(03)
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'Total for faktura/kreditnota -->'
     A            WSNT           5Y 2O  1 42EDTCDE(L)
     A                                      COLOR(BLU)
     A            WSTOT          9Y 2O  1 59EDTCDE(L)
     A                                      COLOR(BLU)
     A                                  2  2'F3=Avslutt   F5=Frisk   F12=Forrig-
     A                                      e'
     A                                      COLOR(BLU)
     A                                  3 40'(C) COPYRIGHT  SYSTEMA A/S  1997  '
     A                                      COLOR(WHT)
      *
     A          R E126D04
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CA05(05 'Frisk')
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYEDI/E126R_93A'
     A                                      COLOR(BLU)
     A                                  1 20'Vise faktura/kreditnota'
     A                                      COLOR(WHT)
     A                                  1 62DATE
     A                                      EDTCDE(Y)
     A                                  1 72TIME
     A                                  2  2'Faktura/kreditnotanr.'
     A            UFN           10A  O  2 24DSPATR(UL)
     A                                      COLOR(BLU)
     A                                  2 42'Ordrenr.'
     A            UKON           7A  O  2 51DSPATR(UL)
     A                                      COLOR(BLU)
     A                                  3  2'Lin Varenummer      Varebeskrivels-
     A                                      e Vgr Antall. Pris.... Linjepris.. -
     A                                      T.lin.rab'
     A                                      COLOR(WHT)
     A                                  7  2'Ingen faktura/kreditnota linje fin-
     A                                      nes.'
     A                                      COLOR(RED)
     A                                 22  2'Total for faktura/kreditnota -->'
     A            WSNT           5Y 2O 22 42EDTCDE(L)
     A                                      COLOR(BLU)
     A            WSTOT          9Y 2O 22 59EDTCDE(L)
     A                                      COLOR(BLU)
     A                                 23  2'F3=Avslutt   F5=Frisk   F12=Forrig-
     A                                      e'
     A                                      COLOR(BLU)
     A                                 24 40'(C) COPYRIGHT  SYSTEMA A/S  1997  '
     A                                      COLOR(WHT)
