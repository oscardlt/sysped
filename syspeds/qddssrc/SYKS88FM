*****A**************************************************************
     A* RETTINGER I DETTE PROGRAM:
PTFnrA*Sek Sig Vers Beskrivelse...........................
"""""A*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
P8068F* 01 TH  PTF8 NY HELPFUNKSJON  KUN DDS
*****A**************************************************************
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
S01  A*                                     HLPDOC('SYEX880010' 'SYEX88' 'SY400-
S01  A*                                     LO/LAND')
N01  A                                      HLPPNLGRP(HLPSCR SYEX88PNL)
N01  A                                      HLPTITLE('ARBEIDE MED TURER')
     A                                      HELP
     A                                      ALTHELP
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  6  3
     A  98                                  DSPATR(RI)
     A                                      VALUES(' ' 'A' '5' 'F' '6')
     A            TUAVD          4Y 0O  6  6EDTCDE(4)
     A            TUPERI         6Y 0O  6 11EDTWRD('  -    ')
     A            TUPRO          8Y 0O  6 19EDTCDE(4)
     A            WSAVSL         1   O  6 28DSPATR(HI)
     A            WSINNT        13Y 2O  6 30EDTWRD('           .  ')
     A            WSKOST        13Y 2O  6 46EDTWRD('           .  ')
     A            WSRESU        13Y 2O  6 62EDTWRD('           .  ')
     A  25                                  COLOR(RED)
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A                                  1  2'SYSPED/SYKS88'
     A                                      COLOR(BLU)
     A                                  1 67DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A  27                              2  2MSGCON(009 DIV0050 *LIBL/SYMSGF)
     A  27        WSAVD          4Y 0O  2 12COLOR(BLU)
     A                                      EDTCDE(4)
     A                                  2 67TIME
     A                                      COLOR(BLU)
     A            ZRECNO         4S 0O  2 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  3  2MSGCON(026 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(040 SHU0562 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  4  2MSGCON(075 SHU0564 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  2MSGCON(077 SHU0567 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A          R BUNN
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A N32                                  CF03(03 'Avslutt')
     A                                      CF05(05 'Frisk')
     A                                      CF08(08 'SØKE TUR')
     A                                      CF12(12 'AVSLUT')
     A                                      SLNO(20)
     A                                      CLRL(05)
     A                                      OVERLAY
     A N32                              4  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  32                              4  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 62MSGCON(018 SHU0148 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  5  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A          R SØKTUR
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF12(12 'FORRIGE')
     A                                      CF14(14 'legge inn tillatelser')
     A                                      SLNO(20)
     A                                      CLRL(05)
     A                                      OVERLAY
     A            MSGNR          7A  P
     A            WSAVD          4Y 0B  1  2DSPATR(UL)
     A                                      DSPATR(PC)
     A  99                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSPERI         6Y 0B  1  8EDTCDE(4)
     A                                      COLOR(YLW)
     A                                  1 15MSGCON(010 SHU0565 *LIBL/SYMSGF)
     A            WSPRO          8Y 0B  1 26DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  1 36MSGCON(008 SHU0566 *LIBL/SYMSGF)
     A            WSSTAT         1   B  2  2
     A                                  2  5MSGCON(060 SHU0568 *LIBL/SYMSGF)
     A                                  4  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  5  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A          R NYWRK
     A N32                                  CF03(03 'Avslutt')
     A                                      CF05(05 'Frisk')
     A  27                                  CF06(06 'LAGE NY')
     A                                      CF08(08 'SØKE TUR')
     A  32                                  CF12(12 'TIL SYEX10')
     A                                  1  2'SYSPED/SYKS88'
     A                                      COLOR(BLU)
     A                                  1 61DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A  27                              2  2MSGCON(009 DIV0050 *LIBL/SYMSGF)
     A  27        WSAVD          4Y 0O  2 12COLOR(BLU)
     A                                      EDTCDE(4)
     A                                  2 61TIME
     A                                      COLOR(BLU)
     A N32                             23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  32                             23  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 62MSGCON(018 SHU0148 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A                                  1 20MSGCON(040 SHU0562 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  5  2MSGCON(077 SHU0567 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
