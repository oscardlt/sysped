     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      REF(*LIBL/ZILLOF)
     A                                      PRINT
     A                                      ALTHELP(CA01)
     A                                      CF03(03 'Exit')
     A                                      CF04(04 'Prompt')
     A                                      CF06(06 'PROS')
     A                                      CF12(12 'Cancel')
     A                                      HELP
     A                                      CF05(05 'Refresh')
     A          R SYSJ73DA
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSJ73D'
     A                                      COLOR(BLU)
     A                                  1 24'Innkommende Bill of Lading'
     A                                      COLOR(WHT)
     A                                  1 63DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  1 73TIME
     A                                      COLOR(BLU)
     A                                  2 27'Fellesopplysninger'
     A                                      COLOR(WHT)
     A                                  3  2'Velg og trykk Enter.'
     A                                      COLOR(BLU)
     A                                  5  2'Containernummer  . . . . :'
     A            ZLAD3G        30A  O  5 29
     A                                  7  2'År . . . . . . . . . . . :'
     A            ZLAAR          2S 0B  7 29
     A                                  8  4'Uke  . . . . . . . . . :'
     A            ZLUKE          2S 0B  8 29
     A                                 10  2'Avdeling . . . . . . . . :'
     A            ZLAVD          4S 0B 10 29
     A                                 11  4'Signatur . . . . . . . :'
     A            ZLSG           3A  B 11 29
     A                                 13  2'Prosjektnummer . . . . . :'
     A            ZLPRO          8S 0B 13 29
     A                                 14  2'Båt  . . . . . . . . . . :'
     A            ZLOB           5A  B 14 29
     A                                 15  2'Rederi . . . . . . . . . :'
     A            ZLKNR          8S 0B 15 29
     A                                 16  2'TR . . . . . . . . . . . :'
     A            ZLTR           5A  B 16 29
     A                                 17  2'Lsst . . . . . . . . . . :'
     A            ZLLSS          5A  B 17 29
     A                                 18  2'Lasth  . . . . . . . . . :'
     A            ZLLSH          5A  B 18 29
     A                                 19  2'Viah . . . . . . . . . . :'
     A            ZLVH           5A  B 19 29
     A                                 20  2'Lossh  . . . . . . . . . :'
     A            ZLLOH          5A  B 20 29
     A                                 22  2MSGCON(010 EDI0803 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 22 15'F4=Søk'
     A                                      COLOR(BLU)
     A                                 22 24MSGCON(010 EDI0805 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 22 35'F6=Lag prosjektnummer'
     A                                      COLOR(BLU)
     A                                 22 59MSGCON(010 EDI0812 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A          R SYSJ73DØ
     A                                      OVERLAY
     A            ZMSGNO         7A  P
     A            ZMSGTX        79A  O 23  2MSGID(&ZMSGNO SYMSGF)
     A N30                                  COLOR(WHT)
     A  30                                  COLOR(RED)
