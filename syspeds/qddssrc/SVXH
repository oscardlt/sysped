********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
11133 * 01 YBC PTF11 VEKT ER ENDRET FRA 9.1 TIL 11.3
********************************************************************
      *__________________________________
      * SYSPED / Tolldekl. TRANSITT
      *""""""""""""""""""""""""""""""""""
     A                                      REF(SR)
     A          R SVXHR
     A            THST      R               REFFLD(ST)
      * THST = ' ' --> ÅPEN TRANSITTERING
      *        'A' --> HOLD PÅ Å SENDE UT
      *        'B' --> SENDT TIL IBM
      *        'C' --> SENDT TIL TOLLVESENET
      *        'D' --> KANSELLERT
      *        'E' --> HOLD PÅ Å BLI ENDRET
      *        'F' --> FEIL VED SIGILERING/TEKNISK
      *        'K' --> SIGILERING OK
      *        'M' --> FEILMELDING FRA TOLLVESENET
      *        'N' --> EDI-FACT OK
      *        'P' --> UTSKREVET IE29 MOTTATT
      *        'U' --> HAR FÅTT MRN-NUMMER
      *        'V' --> VAREKONTROLL
      *        'W' --> INGEN FRIGIVELSE
      *        'Z' --> TRANSITT AVSLUTTET
     A            THAVD          4S 0       COLHDG('AVDELING')
     A            THTDN          7S 0       COLHDG('OPPDRAGSNR')
     A            THSG      R               REFFLD(SG)
     A            THDT      R               REFFLD(DT)
     A            THPRO     R               REFFLD(PRO)
     A            THENKL         1A         COLHDG('FORENKLET PROSEDYRE')
     A            THDK           5A         COLHDG('DEKLARASJONSKODE')
      * HOVEDANSVARLIG
     A            THKNA     R               REFFLD(KN)
     A                                      COLHDG('HOVEDANSVARLIG')
     A            THNAA     R               REFFLD(NA)
     A                                      COLHDG('Navn ANSVARLIG')
     A            THADA1    R               REFFLD(AD)
     A                                      COLHDG('Adresse-1 ANSVARLIG')
     A            THPNA          9A         COLHDG('POSTNR ANSVARLIG')
     A            THPSA         24A         COLHDG('POSTSTED ANSVARLIG')
     A            THLKA          2A         COLHDG('LANDKODE ANSVARLIG')
     A            THSKA          2A         COLHDG('SPRÅKKODE ANSVARLIG')
     A            THTINA        17A         COLHDG('TIN-KODE ANSVARLIG')
      * SELGER
     A            THKNS     R               REFFLD(KN)
     A                                      COLHDG('Kundenr Selger')
     A            THNAS     R               REFFLD(NA)
     A                                      COLHDG('Navn Selger')
     A            THADS1    R               REFFLD(AD)
     A                                      COLHDG('Adresse-1 Selger')
     A            THPNS          9A         COLHDG('POSTNR SELGER')
     A            THPSS         24A         COLHDG('POSTSTED SELGER')
     A            THLKS          2A         COLHDG('LANDKODE SELGER')
     A            THSKS          2A         COLHDG('SPRÅKKODE SELGER')
     A            THTINS        17A         COLHDG('TIN-KODE SELGER')
      * KJØPER
     A            THKNK     R               REFFLD(KN)
     A                                      COLHDG('Kundenr Kjøper')
     A            THNAK     R               REFFLD(NA)
     A                                      COLHDG('Navn Kjøper')
     A            THADK1    R               REFFLD(AD)
     A                                      COLHDG('Adresse-1 Kjøper')
     A            THPNK          9A         COLHDG('POSTNR KJØPER')
     A            THPSK         24A         COLHDG('POSTSTED KJØPER')
     A            THLKK          2A         COLHDG('LANDKODE KJØPER')
     A            THSKK          2A         COLHDG('SPRÅKKODE KJØPER')
     A            THTINK        17A         COLHDG('TIN-KODE KJØPER')
      * BESTEMMELSES LAND  RUBR. 17A
     A            THBLK     R               REFFLD(LKA)
     A                                      COLHDG('LANDK TIL B.LAND')
      * AUTORISERT LAGRINGSSTED
     A            THLSD         17A         COLHDG('AUTO.LAGRINGSSTED')
      * LASTSTED
     A            THLASD        17A         COLHDG('LASTESTED')
     A            THLAS2        17A         COLHDG('SUB-LASTSTED')
      * AVSENDELAND  RUBR. 15A
     A            THALK     R               REFFLD(LKA)
     A                                      COLHDG('LANDK TIL A.LAND')
      * TRANSPORTMÅTE  RUBR. 25
     A            THTRM     R               REFFLD(TRM)
      * TRANSPORTMÅTE  RUBR. 25 INNLAND
     A            THTRMI    R               REFFLD(TRM)
      * TRANSPORTMIDDEL VED AVGANG  RUBR. 18
     A            THTAID        27A         COLHDG('IDENTITET')
     A            THTASK         2A         COLHDG('SPRÅK')
     A            THTALK    R               REFFLD(LKA)
     A                                      COLHDG('Landk Tr.middel')
      * TRANSPORTMIDDEL VED GRENSEPASSERING  RUBR. 21
     A            THTGID        27A         COLHDG('IDENTITET')
     A            THTGSK         2A         COLHDG('SPRÅK')
     A            THTGLK    R               REFFLD(LKA)
     A                                      COLHDG('Landk Tr.middel')
      * SPRÅKKODE FOR FØLGEDOKUMENTER
     A            THSKFD         2A         COLHDG('SPRÅK FØLGEDOK.')
      * RUBR. C
     A            THCATS         8A         COLHDG('AVGANGS TOLLSTED')
      * TRANSITTERINGS TOLLSTEDER  RUBR. 51
     A            THTSD1         8A         COLHDG('TR.TOLLSTED 1')
     A            THTSD2         8A         COLHDG('TR.TOLLSTED 2')
     A            THTSD3         8A         COLHDG('TR.TOLLSTED 3')
     A            THTSD4         8A         COLHDG('TR.TOLLSTED 4')
     A            THTSD5         8A         COLHDG('TR.TOLLSTED 5')
     A            THTSD6         8A         COLHDG('TR.TOLLSTED 6')
     A            THTSD7         8A         COLHDG('TR.TOLLSTED 7')
     A            THTSD8         8A         COLHDG('TR.TOLLSTED 8')
      * BESTEMMELSES TOLLSTED RUBR. 53
     A            THTSB          8A         COLHDG('BESTEMMELSES T.STED')
      * RUBR. D/J
     A            THDDT          8S 0       COLHDG('TIDFRIST')
     A            THDFKD        20A         COLHDG('FORSEGLING-ID KODE')
     A            THDFSK         2A         COLHDG('FORSEGLING-ID SPRÅK')
     A            THDKR          2A         COLHDG('KONTROLL RESULTAT')
     A            THDANT         4S 0       COLHDG('ANTALL FORSEGLING')
     A            THDDTK         8S 0       COLHDG('DATO FOR KONTROLL')
     A            THDATS         8A         COLHDG('KRL. AVG.TOLLSTED')
     A            THDKAV        35A         COLHDG('KONTROLLERT AV')
     A            THDKSK         2A         COLHDG('KONTROLLERT AV LNG')
      * GARANTI RUBR. 52
     A            THGKD          1S 0       COLHDG('GARATIKODE')
     A            THGFT1        24A         COLHDG('GARANTI')
     A            THGFT2        35A         COLHDG('ANNEN GARANTI')
     A            THGADK         4A         COLHDG('ADGANGSKODE')
     A            THGBL         13S 2       COLHDG('GARATIBELØP')
     A            THGVK          3A         COLHDG('VALUTA')
     A            THGBGI         1S 0       COLHDG('BEGRENSNING INNEN EF')
     A            THGBGU         2A         COLHDG('BEGRENSNING UTENFOR')
      * CONTAINER  RUBR. 19
     A            THKDC     R               REFFLD(KDC)
      * TILBAKE SENDES TIL
     A            THTSSD         8A         COLHDG('TILBAKE SENDES T.ST.')
     A            THTSN1        35A         COLHDG('TOLLSTED NAVN')
     A            THTSN2        35A         COLHDG('GATENAVN OG NR')
     A            THTSPN         9A         COLHDG('POSTNR')
     A            THTSPS        24A         COLHDG('POSTSTED')
     A            THTSLK         2A         COLHDG('LANDKODE')
     A            THTSSK         2A         COLHDG('SPRÅKKODE')
      * ANDRE
     A            THLSTL         4S 0       COLHDG('ANTALL LASTELISTE')
     A            THVPOS         5S 0       COLHDG('VAREPOST')
     A            THNTK     R               REFFLD(NTK)
S01  A*           THVKB          9S 1       COLHDG('BRUTTOVEKT')
N01  A            THVKB         11S 3       COLHDG('BRUTTOVEKT')
     A            THDST     R               REFFLD(DST)
     A            THDSK          2A         COLHDG('DEKL. SPRÅK')
     A            THTARF        16          COLHDG('TARIFFØRSNAVN')
     A            THWS      R               REFFLD(WS)
      * TRANSISTTERINGSNUMMER OG GODKJENNINGSDATO
     A            THTRNR        18A         COLHDG('TRANSITTERINGSNR')
     A            THTRDT         8S 0       COLHDG('GODKJENNINGSDATO')
     A            THOMD          1S 0       COLHDG('OMDIRIGERING FORBUDT')
     A            THBIND       140A         COLHDG('BINDENDE REISERUTE')
     A            THTET          1S 0       COLHDG('TET RERUTEKSEMPLAR')
      * TVINN - VARIABLER
     A            THMF      R               REFFLD(MF)
     A            THMP      R               REFFLD(MP)
     A            THPKL     R               REFFLD(PK)
     A                                      COLHDG('Print ved linje ?')
     A            THKDV     R               REFFLD(KDV)
     A            TH0035    R               REFFLD(N0035)
      * EKSTRA FELTER FOR SVENSK NCTS
     A            THTUID        14A         COLHDG('TULLID')
     A            THSGDK         6A         COLHDG('DOKUMENTSIGILL')
     A            THSGME         3A         COLHDG('SIGILLMETODE')
     A            THSGUT         6A         COLHDG('UTSTÆLLARSIGILL')
     A            THSGID        10A         COLHDG('ID PÅ UTSTÆLLARE')
     A            THSGDT        12A         COLHDG('DAUM FØR SIGILL')
     A            THBYTE         7S 0       COLHDG('ANTALL BYTE EDI')
      *
     A          K THAVD
     A          K THTDN
