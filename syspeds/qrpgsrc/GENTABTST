      * - benyttes for TEST generell tablesort HTML (GenTab.html)
      *********************************************************************
      *
      *
CRTPG+*  CRTPGM SYSPED/GENTABTST MODULE(*LIBL/GENTABTST
CRTPGM*        *LIBL/CHECKUSR *LIBL/INCGI) ACTGRP(CGI) AUT(*USE)
      *
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
********************************************************************
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     UF   E           K DISK    USROPN
     FESGENTAB  IF   E           K DISK    USROPN
     FEPOH01    IF   E           K DISK    USROPN
      *********************************************************************
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      /copy syspeds/qrpgsrcpy,variables3
      * Indicators for GetHtmlIfsMult subprocedure
     D IfsMultIndicators...
     d                 ds
     D  NoErrors                       n
     D  NameTooLong                    n
     D  NotAccessible                  n
     D  NoFilesUsable                  n
     D  DupSections                    n
     D  FileIsEmpty                    n

      *
     D OddEven         s             10
     0* Client input variables
     D user            s             10
     D UsrSpcName      s             10
     D WSKN            S              8
     D BSKN            S              8  0
     D ZZVKt           s              8  0
     D ZZM3            s              4  0
     D From            s              8
     D To              s              8
     D Title           S            100
     D CallBack        S           1000
     D ColButt         S            500
     D Col_Ao          S           1000
     D ColHea          S           1000
     D ColDta          S          10000
     D StdHea          S           2000
     D MaxCol          s              2  0                                      Ant kolonner i STD
     D KeyCol          s              2  0                                      STD-col. m/CallbKey
     D CbNr            s              2  0                                      Valgt co m/CallbKey
      * Definere  hex 0D / 25 (CRLF)
     D Hex0D           c                   x'0D'
     D Hex25           c                   x'25'

     D                 DS
     D  C_Ao                   1   5000
     D                                     DIM(50)
     D                 DS
     D  C_He                   1   5000
     D                                     DIM(50)
     D                 DS
     D  C_Dt                   1  50000
     D                                     DIM(50)
      * 50 elem=UT-kolonner,-Verdi mellom 01-max.kol->gi ønsket datainnhold i aktuell posisjon
      * Eks 03 i første element betyr at UTDATA kolonne 1 ønskes hentet fra STD array-elem 3
     D                 DS
     D  Seq                    1    100  0
     D                                     DIM(50)
     D  GTSEKV                 1    100
      * Definere  hex appostrof
     D Ap              c                   x'7D'

     D PGM             C                   CONST('SYSPED/CHECKUSR')
      *
     D ANTSND          s              5  0
     D MaxSN           s              5
     D MaxSND          s              5  0
     D ANTRCD          s              5  0
     D MaxRC           s              5
     D MaxRCD          s              5  0
     D Spaces          s             12a   inz('&nbsp;&nbsp;')
     D ItemCount       s             10i 0
     D i               s             10i 0
     D UPC             C                   CONST('ABCDEFGHIJKLMNOPQRST-
     D                                     UVWXYZÆØÅ')
     D LO              C                   CONST('abcdefghijklmnopqrst-
     D                                     uvwxyzæøå')
      *
      /copy syspeds/qrpgsrcpy,prolog3
      *
      * Get program start time for calculating execution time
     C                   time                    timedata1
      * Parse input / cvt to numeric
     C                   exsr      Parse
      * File open / keys
     C                   EXSR      INIT
      * deaktiver test go enkelhets skyld..
     C*                  CALLB     PGM
     C*                  PARM                    BIBRID
     C*                  PARM                    UsrSpcName
     C*                  PARM                    UINN              1
     C*                  IF        UINN = 'N'
     C*                  GOTO      SLUTT
     C*                  END
      * dato/tid siste vellykkede operasjon.
     C     USER          CHAIN     BRIDF                              50
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVE      WDT           BIDTF
     C                   MOVE      WTM           BITIDF
     C                   UPDATE    BRIDFR
      * Read output skeleton html member into memory
     C                   exsr      LoadHtml
      * Set section variables
     C                   exsr      Settop
     C                   callp     wrtsection('StdtopTab')
     C                   callp     wrtsection('top')                            Programmets html
      *
      * HOVEDRUTINE
      *
      * Vis liste over registrete saker
      *
     C     MaxSnd        IFEQ      *ZEROS
     C                   Z-add     500           MaxSnd
     C                   END
     C     MaxRcd        IFEQ      *ZEROS
     C                   Z-add     2000          MaxRcd
     C                   END
      *
     C                   Move      *zeros        AntSnd
     C                   Move      *zeros        AntRcd
      *
     c     Key           setll     EPOH01
     c     Key           Reade     EPOH01                                 35
      *
     c     *IN35         doweq     '0'
     C                   Add       1             AntRcd
     C     AntRcd        CabGe     MaxRcd        Utles
      * div filter
     c*    XINTERN       Ifne      'J'
     C*    BIKUNR        CabNe     MSGKNR        NextRec
     c*                  end
      * Ok - skriv
     C                   exsr      Setrow
     C                   callp     wrtsection('row')
     C                   Add       1             AntSnd
     C     AntSnd        CabGe     MaxSnd        Utles
      *
     c     NextRec       Tag
     c     Key           Reade     EPOH01                                 35
     c                   enddo
      *
      *
     C     UTLES         TAG
     C                   callp     updHTMLvar('ANTSND':
     C                             %trim(%editc(ANTSND:'3')))
     C                   callp     updHTMLvar('MaxSnd':
     C                             %trim(%editc(MaxSnd:'3')))
     C                   callp     updHTMLvar('ANTRCD':
     C                             %trim(%editc(ANTRCD:'3')))
     C                   callp     updHTMLvar('MaxRcd':
     C                             %trim(%editc(MaxRcd:'3')))
      * Quit
      * Compute run time
     C                   time                    timedata2
     C     timedata2     subdur    timedata1     ms:*ms
     C                   eval      sec = ms / 1000000
     C                   callp     updhtmlvar('runtime':
     C                             %trim(%editw(sec:'     0 .   ')))
      *
     C                   callp     wrtsection('bot')                            programmets Html
     C                   callp     wrtsection('Stdbot')
      *
     C     SLUTT         TAG
     C                   exsr      Exit
      *
     C                   eval      *inlr = *on
     C                   return
      *
      *
      *=====================================================================
      * Parse input / cvt to numeric
      *=====================================================================
     C     Parse         begsr
      * Parse variables from QUERY_STRING environment variable
     C                   eval      MAXSN    = zhbgetvar('MaxSn')
     C                   eval      MaxSnd   = c2n2(MAXSN)
     C                   eval      MAXRC    = zhbgetvar('MaxRc')
     C                   eval      MaxRcd   = c2n2(MAXRC)
      * Userid.....
     C                   eval      user = zhbgetvar('user')
     C                   EVAL      UsrSpcName  = zhbgetvar('UsrSpcName')
     C                   eval      WSKN    = zhbgetvar('WSKN')
     C                   eval      BSKN    = c2n2(WSKN)
     C                   endsr
      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   CLOSE     BRIDF
     C                   CLOSE     EPOH01
     C                   CLOSE     ESGENTAB
     C                   endsr
      *=====================================================================
      * Read output skeleton html member into memory
      *=====================================================================
     C     LoadHtml      begsr
cccccC                   eval      IfsMultIndicators = gethtmlifsmult(
cccccC                             '/syspeddev/html/eSpedTop.html -
cccccC                             /syspeddev/html/GenTable.html -
cccccC                             /syspeddev/html/eSpedBot.html':
cccccC                             '/CGITAG/')
     C                   endsr
      *=====================================================================
      *  Set variable data for section /$top
      *=====================================================================
     C     SetTop        begsr
      *
     c                   eval      Title='TEST generic htmlTable (via EPOH)'
     C                   callp     updhtmlvar('TITLE':Title)
     c
     C                   callp     updHTMLvar('LogoImg':LogoImg)
     C                   callp     updHTMLvar('USER':USER)
     C                   callp     updHTMLvar('BESK':BIBESK)
     C                   callp     updHtmlVar('UsrSpcName':UsrSpcName)
     C                   callp     updHtmlVar('WSKN':
     C                             %trim(%editc(BSKN:'Z')))
     C                   callp     updHtmlVar('KUNDE':
     C                             %trim(%editc(BIKUNR:'Z')))
     C                   callp     updHTMLvar('KNAVN':BIBESK)
      *
     c                   z-add     9             MaxCol                         Antall kolonner std
     c                   z-add     2             KeyCol                         Key for callback
     C                   callp     updHtmlVar('MAXCOL':
     C                             %trim(%editc(MAXCOL:'Z')))
     C                   callp     updHtmlVar('KEYCOL':
     C                             %trim(%editc(KEYCOL:'Z')))
      *
      * brukerstyrt sekvensnummerering ? - GTSEKV ulik blank
     c     GenTabKey     chain     ESGENTAB                           33
     c     *in33         ifeq      *on
     c     1             do        50            Cn                2 0
     c                   if        cn<=Maxcol
     c                   eval      Seq(cn) = Cn
     c                   else
     c                   eval      Seq(cn) = 99
     c                   endif
     c                   enddo
     c                   endif
      *
      * Hvis callback aktuelt i dette prog - fin om nødvendig nøkkel-kolonne er medtatt
      * Ved callback skapes hele funksjonen i CallBackSr
     c                   eval      CallBack= *blanks
     c                   if        KeyCol <> 0
     c     1             do        MaxCol        Cn                2 0
     c     Seq(cn)       ifeq      Keycol
     c                   eval      CbNr=Cn
     c                   endif
     c                   enddo
     c                   if        CbNr <> 0
     c                   exsr      CallBackSr
     c                   endif
     c                   endif
     C                   callp     updHTMLvar('CallBack':CallBack)

      /free

       // styring av kolonne-egenskaper i "aoColumns"  (i funk. jqTable.dataTable)
        C_Ao(01)=' { "sWidth": "10px"}                   ';   // valg
        C_Ao(02)=' { "sWidth": "40px"}                   ';   // Ponr
        C_Ao(03)=' { "sWidth": "50px"}                   ';   // datog
        C_Ao(04)=' { "sWidth": "200px"}                  ';   // Leverand
        C_Ao(05)=' { "sWidth": "70px"}                   ';   // From
        C_Ao(06)=' { "sWidth": "70px"}                   ';   // To
        C_Ao(07)=' { "sWidth": "70px"}                   ';   // Type
        C_Ao(08)=' { "sWidth": "70px"}                   ';   // weight
        C_Ao(09)=' { "sWidth": "70px"}                   ';   // m3

       // styring av kol.headere
        C_He(01)=' <th><font size=2>Op</th>              ';
        C_He(02)=' <th><font size=2>PoNo</th>            ';
        C_He(03)=' <th><font size=2>Date</th>            ';
        C_He(04)=' <th><font size=2>Supplier</th>        ';
        C_He(05)=' <th><font size=2>From</th>            ';
        C_He(06)=' <th><font size=2>To</th>              ';
        C_He(07)=' <th><font size=2>Type</th>            ';
        C_He(08)=' <th><font size=2>Weight</th>          ';
        C_He(09)=' <th><font size=2>CubeMtr</th>         ';
      /end-free

      * colstyring - skriv til html (men i rekkefølge fra evt brukrstyrt array)
      * komma mellom hver, men ikke etter den siste
     c     1             do        MaxCol        Cn                2 0
     c                   if        Seq(cn)<=MaxCol
     c                   if        C_Ao(Seq(cn)) <> *blanks
      * Valgt kolonne funnet (må vel behandles i users rekkefølge for å bli rett..)
     c                   if        Col_Ao = *blanks
     c                   eval      Col_Ao = C_Ao(Seq(cn))
     c                   else
     c                   eval      Col_Ao =%trim(Col_Ao)+', '+C_Ao(Seq(cn))
     c                   endif
      *
     c                   endif
     c                   endif
     c                   enddo
     C                   callp     updHTMLvar('Col_Ao':Col_Ao)

      * colheader - skriv til html
     c     1             do        MaxCol        Cn                2 0
     c                   if        Seq(cn) <= 50
     c                   eval      ColHea =%trim(ColHea)+' '+C_He(Seq(cn))
     c                   endif
     c                   enddo
     C                   callp     updHTMLvar('ColHea':ColHea)
     c
      * STANDARD koloneheader (input til evt kall på kolonnevedl.prog)
     c     1             do        MaxCol        Cn                2 0
     c                   eval      StdHea =%trim(STDHEA)+C_He(cn)
     c                   enddo
     C                   callp     updHTMLvar('STDHea':STDHea)
     C                   callp     updHTMLvar('PROG':GTPROG)
     C                   endsr
      ***********************************************
     C     CallBackSr    begsr
      ***********************************************
      /free
       // CallBack-funksjon (skal radene være Klikkbare / hva skal skje ved klikk)
       // CallBack-utføres kun dersom nøkkel-kolonne er blant de utvalgte
       CbNr=CbNr - 1;      // javascriptarrays teller fra 0 / første element
       CallBack=','+CrLf   // komma bak kolonneegenskaps-parametrene.../ før callback..
       + '"fnRowCallback": function (nRow, aData, iDisplayIndex, '
                          +'iDisplayIndexFull) {'+CrLf
       +'   $(nRow).click(function () {'+CrLf
       +'      var url = "/sycgip/EPOMS02.pgm?user='+%trim(USER)
                  +'&UsrSpcName='+UsrSpcName+'&WSKN='+ %trim(%editc(EHKUND:'Z'))
                  +'&EHBES="+ aData['+%trim(%editc(CbNr:'Z'))+'];'+CrLf
       +'      var width = 810;'+CrLf
       +'      var hight = 650;'+CrLf
       +'      var left = 100;'+CrLf
       +'      var top = 100;'+CrLf
       +'      OpenWindow(url,width,hight,left,top);'+CrLf
       +'   }).'+CrLf
       +'    hover('+CrLf
       +'           function () {$(nRow).addClass("hover");},'+CrLf
       +'           function () {$(nRow).removeClass("hover");}'+CrLf
       +'   ).'+CrLf
       +'   addClass("clickable");'+CrLf
       +'   return nRow;'+CrLf
       +'}'+CrLf;
      /end-free
     C                   endsr
      *=====================================================================
      *  Set variable data for section /$row
      *=====================================================================
     C     Setrow        begsr
      *
     c                   eval      From = EHFLK +'-'+EHFSTE
     c                   eval      To   = EHTLK +'-'+EHTSTE
     c                   z-add     EHVKT         ZZVKT
     c                   z-add     EHM3          ZZM3
      /free
       // styring av kol.data
        C_Dt(01)=' <td><a href="#" OnClick="'
          +'window.open('+Ap+'/sycgip/EPOMS03.pgm?user='+%trim(USER)
          +'&UsrSpcName='+UsrSpcName+'&WSKN='+ %trim(%editc(EHKUND:'Z'))
          +'&EHBES='+%trim(EHBES)
          +Ap+','+Ap+'TstSubWindow'+Ap+','+Ap+'width=500,height=500,'
          +'left=450,top=200,scrollbars,resizable'+Ap+')">'
          +'<img src="/syspeddev/ico_edit-on.png" width=13 height=13'
          +' border=0 alt="Edit"></A></td>';
        C_Dt(02)=' <td>'+%trim(EHBES)+' </td>                             ';
        C_Dt(03)=' <td>'+%trim(%editc(EHDATO:'Z'))+'</td>                 ';
        C_Dt(04)=' <td>'+%trim(EHLNAV)+' </td>                            ';
        C_Dt(05)=' <td>'+%trim(FROM)+' </td>                              ';
        C_Dt(06)=' <td>'+%trim(TO)+' </td>                                ';
        C_Dt(07)=' <td>'+%trim(EHTRKO)+' </td>                            ';
        C_Dt(08)=' <td align=right>'+%trim(%editc(ZZVKT:'Z'))+'</td>      ';
        C_Dt(09)=' <td align=right>'+%trim(%editc(ZZM3:'Z'))+'</td>       ';
      /end-free
      * Colonne data - skriv til HTML
     c                   eval      ColDta = *blanks
     c     1             do        MaxCol        Cn                2 0
     c                   if        Seq(cn) <= 50
     c                   eval      ColDta =%trim(ColDta)+' '+C_Dt(Seq(cn))
     c                   endif
     c                   enddo
     C                   callp     updHTMLvar('ColDta':ColDta)
      *
     C                   endsr
      *
      *
      *
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
     C                   OPEN      BRIDF
     C     USER          CHAIN(N)  BRIDF                              50
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
      *
      * hent Parametre som går igjen i mange prog:
      *      Odd/Even/Rowhead-farger,Logobilde,Språk,Intern/Ekstern bruker,  mm
     c                   call      'ESGETPAR'
     c                   parm                    BIBRID
     c                   parm                    BIFIRM
     c                   parm                    BIKUNR
     c                   parm                    BIKNG
     c                   parm                    RowHead          10
     c                   parm                    Odd              10
     c                   parm                    Even             10
     c                   parm                    LogoImg          50
     c                   parm                    XSPRÅK            2
     c                   parm                    XINTERN           1
     c                   parm                    ParmDS1        1024
     c                   parm                    ParmDS2        1024
     c                   parm                    ParmDS3        1024
      *
     c     Key           klist
     c                   kfld                    EHKUND
     c                   move      BIKUNR        EHKUND
     c     GenTabKey     klist
     c                   kfld                    BIBRID
     c                   kfld                    GTPROG
     c                   movel     'GENTABTST'   GTPROG
      *
     C                   OPEN      EPOH01
     C                   OPEN      ESGENTAB
      *
     c                   movel     Hex0D         CrLf              2
     c                   move      Hex25         CrLf
     C                   endsr
      *
