     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPDOC('STD' 'HELPNU' 'HLP400')
     A                                      HELP
     A          R BILDE01
     A                                      CF03(03 'Avslutt program')
     A            MSGNR          7A  P
     A                                  1  2'SYSPED/SYTR41A'
     A                                      COLOR(BLU)
     A                                  1 21MSGCON(030 DIV4095 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 62DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  1 73TIME
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(024 DIV5003 *LIBL/SYMSGF)
     A            WSDATO         6Y 0B  4 28EDTCDE(4)
     A            WSDAT2         6Y 0B  4 35EDTCDE(4)
     A                                  6  2MSGCON(024 DIV1497 *LIBL/SYMSGF)
     A            WSVAVD         4Y 0B  6 28DSPATR(UL)
     A                                      EDTCDE(4)
     A  99                                  DSPATR(RI)
     A N50                                  DSPATR(PR)
     A  50                                  DSPATR(PC)
     A                                  6 35MSGCON(010 DIV1518 *LIBL/SYMSGF)
     A                                  8  2MSGCON(024 DIV5004 *LIBL/SYMSGF)
     A            WSTY           1A  B  8 28
     A  96                                  DSPATR(RI)
     A  96                                  DSPATR(PC)
     A                                  8 35MSGCON(015 DIV5005 *LIBL/SYMSGF)
     A                                  9  2MSGCON(024 JOV0068 *LIBL/SYMSGF)
     A            WSANTB         1A  B  9 28VALUES('0' '1' '2' '3')
     A                                  9 35MSGCON(025 JOV0069 *LIBL/SYMSGF)
     A                                 10 61'V/3.-lands-traf:'
     A                                 11  2MSGCON(024 JOV0060 *LIBL/SYMSGF)
     A            WSSORT         1A  B 11 28VALUES('1' '2' '3' '4')
     A                                 11 35MSGCON(025 JOV0061 *LIBL/SYMSGF)
     A                                 11 61'(3=..TIL-land)'
     A                                 12 35MSGCON(025 JOV0062 *LIBL/SYMSGF)
     A                                 12 61'(4=..TIL-land)'
     A                                 13  2MSGCON(024 JOV0063 *LIBL/SYMSGF)
     A            WSTILL         1A  B 13 28VALUES('J' 'N' 'Y')
     A                                 13 35MSGCON(010 JOV0066 *LIBL/SYMSGF)
     A                                 14  2MSGCON(024 JOV0064 *LIBL/SYMSGF)
     A            WSFAKT         1A  B 14 28VALUES('J' 'N' 'Y')
     A                                 14 35MSGCON(010 JOV0066 *LIBL/SYMSGF)
     A                                 15  2MSGCON(024 JOV0065 *LIBL/SYMSGF)
     A            WSREK          1A  B 15 28VALUES('J' 'N' 'Y')
     A                                 15 35MSGCON(010 JOV0066 *LIBL/SYMSGF)
     A                                 16  2MSGCON(024 SHU0233 *LIBL/SYMSGF)
     A            WSCPI          2A  B 16 28VALUES('10' '15')
     A                                 16 35'(10/15)'
     A                                 17  2MSGCON(024 DIV4058 *LIBL/SYMSGF)
     A  50                                  DSPATR(ND)
     A            WSPRT         10A  B 17 28
     A  50                                  DSPATR(PR)
     A N50                                  DSPATR(PC)
     A  50                                  DSPATR(ND)
     A                                 19  2MSGCON(024 DIV4059 *LIBL/SYMSGF)
     A  50                                  DSPATR(ND)
     A            WSFORM        10A  B 19 28
     A  50                                  DSPATR(PR)
     A  50                                  DSPATR(ND)
     A                                 21  2MSGCON(024 DIV4060 *LIBL/SYMSGF)
     A  50                                  DSPATR(ND)
     A            WSJOBQ         1A  B 21 28
     A  50                                  DSPATR(PR)
     A  50                                  DSPATR(ND)
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
