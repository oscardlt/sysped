     FPALLDS01  UF   E           K DISK
     FSYPA09P   O    E             PRINTER OFLIND(*IN90)
      *___________________________
      * Finner username
      *"""""""""""""""""""""""""""
     D                SDS
     D  ZJOB                 244    253
     D  ZUSER                254    263
     D  ZJOBNR               264    269  0
      *
     C     *ENTRY        PLIST
     C                   PARM                    UBUNT             7
     C                   PARM                    UARB              1
      *
     C     BUNKEY        KLIST
     C                   KFLD                    XXSTAT            1
     C                   KFLD                    BUNTNR            7 0
     C                   MOVE      'B'           XXSTAT
     C                   MOVE      UBUNT         BUNTNR
      * UARB ULIK ' ' = 50 ON = KUN ARBEIDSLISTE, IKKE OPPDATER STATUS.
     C     UARB          COMP      ' '                                5050
      * SKRIV....
     C                   Z-ADD     1             SIDE              3 0
     C                   WRITE     HEAD
      *
     C     BUNKEY        SETLL     PALLDS01
     C     BUNKEY        READE     PALLDS01                               33
     C     *IN33         DOWEQ     '0'
      *
     C     UARB          IFEQ      ' '
     C                   MOVE      'U'           PASTAT
     C                   END
     C                   UPDATE    PALLDR
      *
     C                   MOVE      *ZEROS        XXMANT            5 0
     C                   MOVE      *ZEROS        XXLANT            5 0
     C     PAANT         IFGT      0
     C                   Z-ADD     PAANT         XXMANT                           MOTTATT
     C                   ELSE
     C                   Z-SUB     PAANT         XXLANT                           LEVERT
     C                   END
     C                   ADD       XXMANT        TOTMAN
     C                   ADD       XXLANT        TOTLAN
     C     PAKVNR        IFNE      GMKVNR
     C                   ADD       1             TOTKVN
     C     GMKVNR        IFNE      *ZEROS
     C                   WRITE     SPACE1
     C                   END
     C                   END
     C     PALØNR        IFNE      GMLØNR
     C                   ADD       1             TOTLØN
     C                   END
     C                   MOVE      PAKVNR        GMKVNR            7 0
     C                   MOVE      PALØNR        GMLØNR            7 0
      * OVERFLOW??
     C     *IN90         IFEQ      '1'
     C                   ADD       1             SIDE
     C                   WRITE     HEAD
     C                   MOVE      '0'           *IN90
     C                   END
      *
     C                   WRITE     DET
     C     BUNKEY        READE     PALLDS01                               33
     C                   END
      *
     C                   WRITE     TOT
      *
      *
     C                   SETON                                        LR
     C                   RETURN
      *
