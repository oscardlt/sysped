     A*****************************************************************
     A*   KJØREKVITTERING/OPPDRAGSFORMULAR                            *
     A* ANTALL LINJE=72,  ANTALL TEGN PR. LINJE=80, OVERFLOW=67
     A*****************************************************************
     A          R SKJØPHH1
     A            KFMT1         75A     1  1
     A          R SKJØPHH2
     A            KFMT2         75A     1  1
     A          R SKJØPNYS                  SPACEB(2)
     A                                    50MSGCON(10 YBC0492 SYMSGF)
     A          R SKJØP
     A            HOHT1         80A     1  1HIGHLIGHT
     A            HOHT2         80A     2  1HIGHLIGHT
     A            HOHT3         80A     3  1HIGHLIGHT
     A            HOHT4         80A     4  1HIGHLIGHT
     A            HOHT5         80A     5  1HIGHLIGHT
     A            HOHT6         80A     6  1HIGHLIGHT
     A            HOHT7         80A     7  1HIGHLIGHT
     A  25                              9 68'SIDE:'
     A  25        XSIDE          3  0   9 74
     A N77        KNAVN         30A    11 11
     A  77        HENAK         30A    11 11
     A                                 11 54'************************'
     A                                      HIGHLIGHT
     A N77        ADR1          30A    12 11
     A  77        HEADK1        30A    12 11
     A                                 12 54MSGCON(24 DIV0198 SYMSGF)
     A                                      HIGHLIGHT
     A N77        ADR2          30A    13 11
     A  77        HEADK2        30A    13 11
     A N99                             13 54'************************'
     A                                      HIGHLIGHT
     A  99                             13 54MSGCON(24 DIV2969 SYMSGF)
     A                                      HIGHLIGHT
     A N77        POSTNR         4  0  14 11
     A N77        ADR3          24A    14 17
     A  77        HEADK3        30A    14 11
     A  88                             15 11MSGCON(10 DIV3559 SYMSGF)
     A  88        TLF           15A    15 22
     A                                 15 54MSGCON(13 DIV2946 SYMSGF)
     A                                 15 70DATE EDTCDE(Y) HIGHLIGHT
     A                                 16  2MSGCON(13 DIV2945 SYMSGF)
     A*           HERFA         15     16 16HIGHLIGHT
     A            HERFA15       15     16 16HIGHLIGHT
     A                                 16 54MSGCON(13 DIV2947 SYMSGF)
SV5  A*           HEDT           6  0  16 70HIGHLIGHT
NV5  A            XEDTOP         6  0  16 70HIGHLIGHT
     A                                 17 52MSGCON(9 DIV0201 SYMSGF)
     A            HEAVD          4  0  17 62HIGHLIGHT
     A                                 17 66'/'
     A            HEOPD          7  0  17 67HIGHLIGHT
     A                                 17 74'/'
     A            HESG           3A    17 75HIGHLIGHT
     A                                 18  2MSGCON(13 DIV2949 SYMSGF)
     A            XLBN          30A    18 16HIGHLIGHT
     A                                 18 54MSGCON(13 DIV2950 SYMSGF)
     A            HEPRO          8  0  18 70EDTCDE(Z) HIGHLIGHT
     A                                 19  2MSGCON(13 DIV2951 SYMSGF)
     A            XVHN          30A    19 16HIGHLIGHT
     A                                 19 54MSGCON(13 DIV2952 SYMSGF)
     A            HEKNK          8  0  19 70EDTCDE(Z) HIGHLIGHT
     A                                 21  2MSGCON(13 DIV2953 SYMSGF)
     A            HETRM          2  0  21 16EDTCDE(4) HIGHLIGHT
     A                                 21 24MSGCON(7 DIV2954 SYMSGF)
     A            HETRI          2A    21 32HIGHLIGHT
     A                                 21 48MSGCON(12 DIV2955 SYMSGF)
     A            HETRC          1  0  21 61HIGHLIGHT
     A                                 22  2MSGCON(13 DIV2956 SYMSGF)
     A            KFRNTX        28A    22 16HIGHLIGHT
     A                                 22 48MSGCON(12 DIV2957 SYMSGF)
     A            HESDL         20     22 61HIGHLIGHT
     A                                 23  2MSGCON(13 DIV2958 SYMSGF)
     A            HEGN          15A    23 16HIGHLIGHT
     A                                 23 31'-'
     A            HEPOS1         7A    23 32HIGHLIGHT
     A            HEPOS2         4  0  23 40EDTCDE(4) HIGHLIGHT
     A                                 23 52MSGCON(12 DIV2959 SYMSGF)
     A            HETRCN        17A    23 65HIGHLIGHT
     A                                 25  2MSGCON(13 DIV2960 SYMSGF)
     A            HENAS         30A    25 16HIGHLIGHT
     A                                 25 44MSGCON(6 DIV2961 SYMSGF)
     A            XLSSN         30A    25 51HIGHLIGHT
     A            HEADS1        30A    26 16HIGHLIGHT
     A                                 26 44MSGCON(6 DIV2962 SYMSGF)
     A            XLOSN         30A    26 51HIGHLIGHT
     A            HEADS2        30A    27 16HIGHLIGHT
     A            HEADS3        30A    28 16HIGHLIGHT
     A                                 29  2MSGCON(10 DIV2963 SYMSGF)
     A            XOBN          30A    29 13HIGHLIGHT
     A                                 29 44MSGCON(6 DIV2964 SYMSGF)
     A            BLREIS        14A    29 51HIGHLIGHT
     A                                 30  1' _____________________________'
     A                                      HIGHLIGHT
     A                                 30 31'______________________________'
     A                                      HIGHLIGHT
     A                                 30 61'____________________'
     A                                      HIGHLIGHT
     A                                 31  2MSGCON(79 DIV2965 SYMSGF)
     A                                 32  1' ¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯'
     A                                      HIGHLIGHT
     A                                 32 31'¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯'
     A                                      HIGHLIGHT
     A                                 32 61'¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯'
     A          R DETBL1                    SPACEB(1)
     A            BVMN          17A        2
     A            BVNT           5  0     21EDTCDE(Z)
     A            BVVS          25A       29
     A  89        BVVKT          9  0     55EDTCDE(1)
     A  90        BVM3           9  3     68EDTCDE(1)
     A          R TOMLIN                    SPACEB(1)
     A                                     2' '
     A          R DETBL2                    SPACEB(1)
     A            XMN1          17A        2
     A            XMN2          51A       22
     A          R DETFS
     A  28        FRISTX        30         2
     A  28                                32':'
     A            FS1           21        35
     A            FS2           21        57SPACEA(1)
     A          R DETUFB
     A                                     2MSGCON(24 YBC0491 SYMSGF)
     A                                      HIGHLIGHT
     A                                    32':'
     A                                      HIGHLIGHT
     A            HEFT1A        30        35HIGHLIGHT
     A            HEFT1B        15        66HIGHLIGHT
     A                                      SPACEA(1)
     A          R DETFRT
     A            FRTTXT        70         2SPACEA(1)
     A                                     2' '
     A          R SKJØPB
     A                                 58  2MSGCON(24 DIV2966 SYMSGF)
     A            HENAK         30A    59 11
     A            HEADK1        30A    60 11
     A            HEADK2        30A    61 11
     A            HEADK3        30A    62 11
     A                                 63  1' _____________________________'
     A                                 63 31'______________________________'
     A                                 63 61'____________________'
     A                                 64 15MSGCON(16 DIV2967 SYMSGF)
     A            XSUM          11  2  64 35EDTCDE(1)
     A                                 65  1' ¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯'
     A                                 65 31'¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯'
     A                                 65 61'¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯'
     A                                 67 17MSGCON(64 DIV2968 SYMSGF)
