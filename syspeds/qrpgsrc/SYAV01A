********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 sh  PTF10 ny parameter syge03    .
********************************************************************
      *
      **************************************************************
      *** PROGRAM SKAL ARBEIDE MED TRANSPORTØRAVREGNING          ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER       ***
      *** LEDIGE INDIKATORER                                     ***
      **************************************************************
      *
     FTURER14   UF   E           K DISK
     FFIRM      IF   E           K DISK
     FHEADF     UF   E           K DISK
     FHEAD21    IF   E           K DISK
     F                                     RENAME(HEADFR:HEADFR20)
     FFRISOK    IF   E           K DISK
     FFAKT      UF   E           K DISK
     FFAK2      IF   E           K DISK
     F                                     RENAME(FAKTR:FAKTR2)
     FSYAV06W   O  A E           K DISK    USROPN
     FSYAV01FA  CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D                 DS
EV5  D  TUDT                   1      8  0
EV5  D  TUDTÅ                  1      4  0
EV5  D  TUDTM                  5      6  0
EV5  D  TUDTD                  7      8  0
NV5  D                 DS
NV5  D  WSDT                   1      6  0
NV5  D  WSDTD                  1      2  0
NV5  D  WSDTM                  3      4  0
NV5  D  WSDTÅ                  5      6  0
     D                 DS
     D  HENAK                  1     30
     D  HEADK1                31     60
     D  HEADK2                61     90
     D  HEADK3                91    120
     D  HENAS                121    150
     D  HEADS1               151    180
     D  HEADS2               181    210
     D  HEADS3               211    240
     D  XAM1                   1    240
     D UCMD            DS          1024
     D                 DS
     D XCMD10          C                   CONST('CLRPFM FILE(SYAV06W)-
     D                                      MBR(')
     D                SDS
     D  UJOBNM               244    253
      *
     C                   EXSR      INIT
      *  SUBFILE CLEAR
     C     START         TAG
     C                   CLEAR                   SMBELI
     C                   CLEAR                   SMBELA
     C                   MOVEA     '00'          *IN(91)
     C                   MOVE      *ON           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
      * INITIERE FOR Å HINDRE AT 1. LESTE POST ER "LIK FOREGÅENDE"
     C                   MOVE      *BLANKS       XAM2            240
     C                   MOVE      'Z'           PROD2             1             (ULIK!!!)
NV5  C                   MOVE      *ZEROS        XKM               5 0
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C                   CLEAR                   ZRECNO
     C                   CLEAR                   MRECNO
      * FINN ALLE OPPDRAG PÅ TUREN
      *
     C     TUPRO         SETLL     HEAD21
     C     HELES         TAG
     C     TUPRO         READE     HEAD21                                 35
     C     *IN35         IFEQ      *ON
NV5  C     XOPDBL        IFEQ      'J'
NV5  C     TUPRO         CHAIN     TURER14                            33
NV5  C                   Z-ADD     SMBELA        TUTBEL
NV5  C                   UPDATE    TURERR
NV5  C                   ENDIF                                                  <*IN34
     C                   GOTO      START2
     C                   ENDIF                                                  <*IN34
      * HVIS SAMTAKSERING-BASIS (HAR PEKER TIL SAA = SAMTAKS.-AVR.OPPD)
      * 46=PROTECT BELØP/FARGE, 42*OFF=IKKE FORESLÅ SAMMENSLÅING
     C                   MOVEA     '00'          *IN(46)
     C                   MOVE      'SAA'         FSKODE
     C     FRIKEY        CHAIN     FRISOK                             33
     C     *IN33         IFEQ      '0'
     C                   MOVEA     '10'          *IN(46)
     C                   MOVE      '0'           *IN42
     C                   MOVE      'Z'           PROD1                           (ULIK!!!)
     C                   END
      * HVIS SAMTAKSERING-OPPDR (HAR PEKER TIL SAB = SAMTAKS.-BASIS)
      * 47=FARGE, 42*OFF=IKKE FORESLÅ SAMMENSLÅING
     C                   MOVE      'SAB'         FSKODE
     C     FRIKEY        CHAIN     FRISOK                             33
     C     *IN33         IFEQ      '0'
     C                   MOVEA     '01'          *IN(46)
     C                   MOVE      '0'           *IN42
     C                   MOVE      'Z'           PROD1                           (ULIK!!!)
     C                   END
      * HIGHLIGHT FOR SAMMENSLÅING? (MEN IKKE VIS SAA/SAB)
     C     *IN46         IFEQ      '0'
     C     *IN47         ANDEQ     '0'
     C                   MOVE      HEKDPL        PROD1             1
     C     PROD1         IFNE      'H'
     C                   MOVE      ' '           PROD1
     C                   END
     C     XAM1          COMP      XAM2                                   42
SV5  C*  42      PROD1     COMP PROD2                    42
     C                   END
      * BEVAR SAMMENLIGNINGSVERDIER ...
     C                   MOVE      PROD1         PROD2
     C                   MOVE      XAM1          XAM2
     C                   Z-ADD     HEAVD         SFAVD
     C                   Z-ADD     HEOPD         SFOPD
     C                   Z-ADD     HENT          SFNT
     C                   Z-ADD     HEVKT         SFVKT
     C                   Z-ADD     HEM3          SFM3
     C                   MOVEL     HESDVT        SFPOST
     C                   MOVE      HEKDPL        SFKDPL
     C                   MOVE      HEKDPL        SHKDPL
     C  N43              MOVEL     HENAS         SFNAS
     C   43              MOVEL     HENAK         SFNAS
      *
     C     START1        TAG
      * LES ALLE FAKTURALINJENE PÅ OPPDRAGET
     C                   EXSR      LESFAK
     C*
      *  SKRIV SUBFILE
NV5  C     TRKM          IFGT      XKM
NV5  C                   MOVE      TRKM          XKM
NV5  C                   END
     C                   ADD       1             ZRECNO
     C                   ADD       1             MRECNO
     C                   WRITE     SUBFIL
     C                   MOVEA     '100'         *IN(80)
     C                   GOTO      HELES
      *
     C     START2        TAG
     C                   Z-ADD     8             LINNBR
     C  N40              Z-ADD     68            POSNBR
     C   40              Z-ADD     2             POSNBR
NV5  C     XXRECN        IFNE      0
NV5  C     XXRECN        ANDLT     ZRECNO
NV5  C                   MOVE      XXRECN        ZRECNO
NV5  C                   ELSE
     C                   Z-ADD     1             ZRECNO
NV5  C                   END
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     MRECNO        IFEQ      0
     C  N40              Z-ADD     68            POSNBR
     C   40              Z-ADD     2             POSNBR
     C                   EXFMT     NYWRK
     C                   ELSE
     C                   MOVE      *ON           *IN(94)
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   EXFMT     BUNN
     C                   END
      *
     C                   SETOFF                                       9899
      * AVSLUTT PROGRAM
     C     *IN03         IFEQ      *ON
     C     *IN12         OREQ      *ON
     C                   GOTO      SLUTT
     C                   END                                                    <*IN12
      * VEKSLE Å VISE AVSENDER OG MOTTAGER
     C     *IN07         IFEQ      *ON
     C     *IN43         IFEQ      *ON
     C                   MOVE      '0'           *IN43
     C                   ELSE
     C                   MOVE      '1'           *IN43
     C                   END
     C                   GOTO      START
     C                   END
      *
     C                   SETOFF                                       94
      * FERDIGMELD
     C     *IN10         IFEQ      *ON
     C     UPRO          CHAIN     TURER14                            32
     C     TUAVRE        IFEQ      ' '
     C                   MOVE      'K'           TUAVRE
     C                   ELSE
     C     TUAVRE        IFEQ      'K'
     C                   MOVE      ' '           TUAVRE
     C                   END
     C                   END
     C                   Z-ADD     SMBELA        TUTBEL
     C                   UPDATE    TURERR
     C                   GOTO      SLUTT
     C                   END                                                    <*IN10
      *
      * ROLLUP
     C     *IN25         IFEQ      *ON
     C                   MOVE      *OFF          *IN25
     C                   MOVE      *ON           *IN94
     C                   GOTO      START3
     C                   END                                                    <*IN25
      * ROLLDOWN
     C     *IN26         IFEQ      *ON
     C                   MOVE      *OFF          *IN25
     C                   MOVE      *ON           *IN94
     C                   GOTO      START3
     C                   END                                                    <*IN26
      *
     C     ZRECNO        IFNE      0
     C                   EXSR      HENTSR
     C                   END
      *
     C                   GOTO      START
      *
     C     SLUTT         TAG
     C                   MOVE      *ON           *INLR
     C                   RETURN
     C*
     C***********************************************
     C     HENTSR        BEGSR
     C***********************************************
NV5  C                   MOVE      'N'           XOPDBL            1
NV5  C                   MOVE      *ZEROS        XXRECN            5 0
     C     *IN40         IFEQ      '0'
     C                   Z-ADD     1024          UCMDL            15 5
     C                   MOVE      *BLANKS       UCMD
     C                   MOVEL     XCMD10        UCMD
     C     XCMD10        CAT       UJOBNM:0      UCMD
     C                   CAT       ')':0         UCMD
     C                   CALL      'QCMDEXC'                            91
     C                   PARM                    UCMD
     C                   PARM                    UCMDL
     C                   OPEN      SYAV06W
     C                   MOVE      '0'           *IN30
     C                   MOVE      *BLANKS       XKDPL             1
     C                   MOVE      *ZEROS        XANTOP            5 0
     C                   END
      *
     C                   Z-ADD     0             LINNBR
     C                   SETOFF                                       919299
     C     *IN98         DOWEQ     *OFF
     C                   READC     SUBFIL                                 98
     C   98              DO
     C   99              SETOFF                                       98
     C                   Z-ADD     1             LINNBR
     C                   GOTO      SLHENT
     C                   ENDDO                                                  <98 DO
     C     ZRECNO        DIV       13            LINNBR
     C                   MVR                     XLIN              2 0
     C     XLIN          IFEQ      0
     C                   Z-ADD     13            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END                                                    <XLIN
     C*    OPTION ' ' = ENDRE
     C     WSNRO         IFEQ      ' '                                          SLETTE
      * BELØP ENDRET. PRODUKTKODE IKKE ENDRET
     C     SFKDPL        IFEQ      SHKDPL
     C     FAKTK         CHAIN     FAKT                               31
     C     *IN31         IFEQ      *OFF
     C                   Z-ADD     SFBELA        FABELN
     C                   Z-ADD     SFBELA        FABELV
     C                   MOVE      'E'           FAKDA
     C                   UPDATE    FAKTR
     C                   ENDIF                                                  <*IN31
     C                   ELSE
      * PRODUKTKODE ENDRET
     C                   MOVE      SFKDPL        SHKDPL
     C     HEADSF        CHAIN     HEADF                              41
     C     *IN41         IFEQ      *OFF
     C                   MOVE      SFKDPL        HEKDPL
     C                   UPDATE    HEADFR
     C                   ENDIF                                                  <*IN41
     C     FAKTK         CHAIN     FAKT                               31
     C     *IN31         IFEQ      *OFF
     C                   EXSR      BERAVR
     C                   Z-SUB     AVRBEL        SFBELA
     C                   Z-SUB     AVRBEL        FABELN
     C                   Z-SUB     AVRBEL        FABELV
     C                   MOVE      'M'           FAKDA
     C                   UPDATE    FAKTR
     C                   ENDIF                                                  <*IN31
     C                   ENDIF                                                  <SFKDPL
     C                   MOVE      FAKDA         SFKDA
     C                   MOVE      *ON           *IN99
     C                   UPDATE    SUBFIL
     C                   MOVEA     '001'         *IN(80)
     C                   END                                                    <WSNRO
     C*
     C*    OPTION 6=VISE OPPDRAG
     C     WSNRO         IFEQ      '6'                                          ENDRE
     C                   MOVE      ' '           WSNRO
     C                   MOVE      *ON           *IN99
     C*
     C                   MOVE      *ZEROS        DUMFLA            1 0
     C                   CALL      'SYFM21A'
     C                   PARM                    FIFIRM
     C                   PARM                    SFAVD
     C                   PARM                    SFOPD
     C                   PARM                    DUMFLA
     C*
     C                   MOVE      *OFF          *IN44
     C                   UPDATE    SUBFIL
     C                   MOVEA     '010'         *IN(80)
     C                   MOVE      *ON           *IN31
     C                   ENDIF                                                  <WSNRO
     C*    OPTION 7=VISE FAKTURA
     C     WSNRO         IFEQ      '7'                                          ENDRE
     C                   MOVE      ' '           WSNRO
     C                   MOVE      *ON           *IN99
     C*
     C                   MOVE      *ZEROS        DUMFLA            1 0
     C                   CALL      'SYFM21B'
     C                   PARM                    FIFIRM
     C                   PARM                    SFAVD
     C                   PARM                    SFOPD
     C                   PARM                    DUMFLA
     C*
     C                   MOVE      *OFF          *IN44
     C                   UPDATE    SUBFIL
     C                   MOVEA     '010'         *IN(80)
     C                   MOVE      *ON           *IN31
     C                   ENDIF                                                  <WSNRO
     C*    OPTION 8=VISE FRIE SØKEVEIER
     C     WSNRO         IFEQ      '8'                                          ENDRE
     C                   MOVE      ' '           WSNRO
     C                   MOVE      *ON           *IN99
     C*
     C                   CALL      'SYFA64'
     C                   PARM                    SFAVD
     C                   PARM                    SFOPD
     C*
     C                   MOVE      *OFF          *IN44
     C                   UPDATE    SUBFIL
     C                   MOVEA     '010'         *IN(80)
     C                   MOVE      *ON           *IN31
     C                   ENDIF                                                  <WSNRO
      *    OPTION 9=SAMMENSLÅING
     C  N40WSNRO         IFEQ      '9'                                          SAMMENSLÅING
     C                   ADD       1             XANTOP
     C     XKDPL         IFEQ      *BLANKS
     C                   MOVE      SFKDPL        XKDPL
     C                   ELSE
     C                   SELECT
     C     XKDPL         WHENEQ    'H'
     C     SFKDPL        ANDNE     'H'
     C                   MOVE      *ON           *IN30
     C                   MOVE      'JOV0794'     MSGNR
     C                   MOVE      ' '           WSNRO
     C                   UPDATE    SUBFIL
     C                   GOTO      SLHENT
     C     XKDPL         WHENNE    'H'
     C     SFKDPL        ANDEQ     'H'
     C                   MOVE      *ON           *IN30
     C                   MOVE      'JOV0794'     MSGNR
     C                   MOVE      ' '           WSNRO
     C                   UPDATE    SUBFIL
     C                   GOTO      SLHENT
     C                   ENDSL
     C                   END
     C                   WRITE     SYAV06WR
NV5  C     XXRECN        IFEQ      0
NV5  C                   MOVE      ZRECNO        XXRECN
NV5  C                   ENDIF                                                  <XXRECN
     C                   ENDIF                                                  <WSNRO
NV5   *    OPTION O=OMREGN FRAKTBEREGNINGSVEKT
NV5  C     WSNRO         IFEQ      'O'
NV5  C                   CALL      'SYAV07'
NV5  C                   PARM      SFAVD         UAVDOM            4 0
NV5  C                   PARM      SFOPD         UOPDOM            7 0
NV5  C                   PARM      SFLI          ULIOM             5 0
NV5  C                   MOVE      *ON           *IN99
NV5  C                   MOVE      ' '           WSNRO
NV5  C                   MOVE      *OFF          *IN44
NV5  C                   UPDATE    SUBFIL
NV5  C                   MOVEA     '010'         *IN(80)
NV5  C                   MOVE      *ON           *IN31
NV5  C                   MOVE      'J'           XOPDBL
NV5  C                   ENDIF                                                  <WSNRO
     C                   END                                                    <*IN98
     C*
     C     SLHENT        TAG
     C  N40              CLOSE     SYAV06W
     C  N30XANTOP        IFGT      0
     C     XANTOP        IFGT      1
     C                   CALL      'SYAV06'
NV5  C                   PARM                    XKM
     C                   END
     C                   MOVE      ' '           WSNRO
     C                   END
     C     XANTOP        IFEQ      0
     C                   ADD       7             LINNBR
     C  N40              Z-ADD     68            POSNBR
     C   40              Z-ADD     2             POSNBR
     C                   END
     C*
     C                   ENDSR
      *
      *****************************************************************
     C     BERAVR        BEGSR
      *****************************************************************
     C                   MOVE      'FRA'         UGEBTY            3
     C                   MOVE      *BLANKS       UUVAL             3
     C                   MOVE      *ZEROS        AVRBEL           13 2
     C                   MOVE      *ZEROS        URABAT
     C                   MOVE      FAKUNR        UKUNDE
     C                   CALL      'SYGE03'
     C                   PARM                    UGEBTY
     C                   PARM                    SFAVD
     C                   PARM                    SFOPD
     C                   PARM                    UUVAL
     C                   PARM                    AVRBEL
     C                   PARM                    USONE             5
     C                   PARM                    URABAT            5 2
     C                   PARM                    N5                5 0
     C                   PARM                    N5                5 0
     C                   PARM                    N5                5 0
     C                   PARM                    N5                5 0
     C                   PARM                    UKUNDE            8 0
     C                   PARM                    UAVNR             9 0
n01  C                   PARM                    atdum             1
     C                   ENDSR
     C*
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
      *
     C     *ENTRY        PLIST
     C                   PARM                    UPRO              8 0
     C                   PARM                    UOPT              1
     C     UOPT          COMP      '5'                                    40
      *
     C     *LIKE         DEFINE    ZRECNO        MRECNO
SV5  C*                    Z-ADD0         TUDT
     C*--------------------------------------------------------------
     C* DEFINERER NØKKEL TIL FAKT
     C*--------------------------------------------------------------
     C     FAKTK         KLIST
     C                   KFLD                    SFAVD
     C                   KFLD                    SFOPD
     C                   KFLD                    SFLI
     C*--------------------------------------------------------------
     C* DEFINERER NØKKEL TIL FAK2
     C*--------------------------------------------------------------
     C     FAK2K         KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C*--------------------------------------------------------------
     C* DEFINERER NØKKEL TIL HEADF
     C*--------------------------------------------------------------
     C     HEADSF        KLIST
     C                   KFLD                    SFAVD
     C                   KFLD                    SFOPD
     C*--------------------------------------------------------------
     C* DEFINERER NØKKEL TIL FRISOK
     C*--------------------------------------------------------------
     C     FRIKEY        KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C                   KFLD                    FSKODE
     C*
     C                   READ      FIRM                                   32
     C     UPRO          CHAIN(N)  TURER14                            32
     C     *IN32         IFEQ      *ON
     C                   SETON                                        LR
     C                   RETURN
     C                   ENDIF                                                  <*IN32
NV5   *
NV5  C                   MOVE      TUDTD         WSDTD
NV5  C                   MOVE      TUDTM         WSDTM
NV5  C                   MOVE      TUDTÅ         WSDTÅ
     C* STATUS 44 OFF= AVREGNET,  44 ON = ÅPEN ( ' ' EL. 'K')
     C*                           45 OFF UFERDIG, 45 ON = KONTROLLERT
     C                   MOVEA     '00'          *IN(44)
     C     TUAVRE        IFEQ      ' '
     C                   MOVEA     '10'          *IN(44)
     C                   ELSE
     C     TUAVRE        IFEQ      'K'
     C                   MOVEA     '11'          *IN(44)
     C                   END
     C                   END
      * HVIS AVREGNET -> SPØRREMODUS
     C     *IN44         IFEQ      '0'
     C                   MOVE      '1'           *IN40
     C                   END
     C     UTI           ENDSR
     C*
     C*
     C***********************************************
     C     LESFAK        BEGSR
     C***********************************************
     C* LESER OG SUMMERER ALLE FAKTURALINJENE
     C*
     C                   MOVE      *ZEROS        SFBELI
     C                   MOVE      *ZEROS        SFBELA
     C     FAK2K         SETLL     FAK2
     C                   SETOFF                                           38
      * HENTER BELØP
     C     FALES         TAG
     C     FAK2K         READE     FAK2                                   39
     C     *IN39         IFEQ      *ON
     C                   GOTO      UTLESF
     C                   ENDIF                                                  <*IN39
     C                   MOVE      FAKDA         SFKDA
      *
     C     FAVK          IFEQ      'INI'
     C                   ADD       FABELN        SFBELI
     C                   ADD       FABELN        SMBELI
     C                   GOTO      FALES
     C                   ENDIF                                                  <FAVK
      *
     C     FAVK          IFEQ      'AVR'
     C                   ADD       FABELN        SFBELA
     C                   ADD       FABELN        SMBELA
     C                   Z-ADD     FALI          SFLI
     C                   ENDIF                                                  <FAVK
      *
     C                   GOTO      FALES
     C     UTLESF        ENDSR
