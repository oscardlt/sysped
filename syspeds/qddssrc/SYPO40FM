     A*%%TS  SD  20090202  133654  JOVO        REL-V5R4M0  5722-WDS
*****A**************************************************************
     A* RETTINGER I DETTE PROGRAM:
PTFnrA*Sek Sig Vers Beskrivelse...........................
"""""A*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
P8068A* 02 TH  PTF8 NY HELPFUNKSJON  KUN DDS
*****A**************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      HLPPNLGRP(HLPSCR SYPO40PNL)
     A                                      HLPTITLE('POSTAL FRAKTBEREGNING - E-
     A                                      NDRING AV PRISER I PROSENT')
     A                                      HELP
     A                                      ALTHELP
     A*
     A          R BILDE01
     A*%%TS  SD  20090202  133654  JOVO        REL-V5R4M0  5722-WDS
     A                                      CF03(03 'Avslutt program')
     A                                      CF04(04 'SØKING')
     A                                      CF05(05 'PRISJUSTER')
     A                                      CF12(12 'FORIGE BILDE')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1  2'SYSPED/SYPO40'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(040 SHU0471 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  2 70TIME
     A                                      COLOR(BLU)
     A                                  5 29MSGCON(020 SHU0469 *LIBL/SYMSGF)
     A                                  7  2MSGCON(025 SHU0030 *LIBL/SYMSGF)
     A            KSPROD         1A  B  7 29
     A            WSPROD    R        B  7 33REFFLD(SONESR/SSPROD *LIBL/SONES)
     A                                  9  2MSGCON(025 SHU0031 *LIBL/SYMSGF)
     A            KSAVDE         1A  B  9 29
     A            WSAVDE    R        B  9 33REFFLD(SONESR/SSAVDE *LIBL/SONES)
     A                                 11  2MSGCON(025 SHU0467 *LIBL/SYMSGF)
     A            KSZONO         1A  B 11 29
     A            WSZON1    R        B 11 33REFFLD(SONESR/SSZONO *LIBL/SONES)
     A            WSZON2    R        B 11 40REFFLD(SONESR/SSZONO *LIBL/SONES)
     A                                 13  2MSGCON(025 SHU0470 *LIBL/SYMSGF)
     A            PLUMIN         1A  B 13 29
     A            WSPROS         5Y 2B 13 33EDTWRD('   .  ')
     A                                      COMP(GE ,00)
     A                                 13 42'(+,-,M,B)'
     A                                 15  2MSGCON(025 SHU0472 *LIBL/SYMSGF)
     A            ZKODE          1Y 0B 15 29EDTCDE(4)
     A                                 17  2MSGCON(025 JOV1480 *LIBL/SYMSGF)
     A            WSMM           1A  B 17 29
     A                                 17 32MSGCON(045 JOV1481 *LIBL/SYMSGF)
     A                                 19  2MSGCON(020 JOV1478 *LIBL/SYMSGF)
     A            WSFRVG         9Y 0B 19 25EDTCDE(4)
     A                                 19 35'-'
     A            WSTIVG         9Y 0B 19 37EDTCDE(4)
     A                                 19 47MSGCON(034 JOV1479 *LIBL/SYMSGF)
     A                                 21  2MSGCON(025 SHU0473 *LIBL/SYMSGF)
     A            ANTJUS         5Y 0O 21 29COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(018 DIV0032 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 42MSGCON(018 SHU0468 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 62MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                             24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
