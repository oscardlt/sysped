     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPPNLGRP(HLPSCR SYFA09PNL)
     A                                      HLPTITLE('VEDLIKEHOLD AV GEBYRAVTAL-
     A                                      ER (KUNDER)')
     A                                      HELP
     A                                      ALTHELP
     A          R BILDE01
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF03(03 'Avslutt program')
     A                                      CF04(04 'PROMT')
     A          H                           HLPPNLGRP(SYFA09010 SYFA09PNL)
     A                                      HLPARA(01 001 24 080)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/SYFA09'
     A                                      COLOR(BLU)
     A                                  1 66DATE
     A                                      EDTCDE(Y)
     A  28                              1 19MSGCON(45 DIV1183 SYMSGF)
     A                                      COLOR(WHT)
     A  27                              1 19MSGCON(45 SHU1463 SYMSGF)
     A                                      COLOR(WHT)
     A                                  2 66TIME
     A                                  9  2MSGCON(15 DIV1184 SYMSGF)
     A            WSKNR          8Y 0B  9 20DSPATR(UL)
     A  99                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                 11  2MSGCON(15 DIV1185 SYMSGF)
     A            WSAVTN         1A  B 11 20DSPATR(UL)
     A  98                                  DSPATR(RI)
     A                                 14  2MSGCON(30 DIV2205 SYMSGF)
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 16  2MSGCON(15 DIV1184 SYMSGF)
     A            WSKNRK         8Y 0B 16 20DSPATR(UL)
     A  97                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                 18  2MSGCON(15 DIV1185 SYMSGF)
     A            WSAVTK         1A  B 18 20DSPATR(UL)
     A  96                                  DSPATR(RI)
     A                                 23  2MSGCON(18 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(18 DIV0032 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                             24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A          R SUBFIL                    SFL
     A            PKLNR          4S 0H
     A            WSNR           1A  I  6  2VALUES(' ' '2' '3' '4')
     A            PKGEBY         3A  O  6  4
     A            KGENOT        20A  O  6  8
     A            PKAVTT         1A  O  6 29
     A            PKTNR          1A  O  6 31
     A            PKMVA          1A  O  6 33
     A            PKINT          5Y 0O  6 35EDTCDE(4)
     A            PKBELØ        10Y 3O  6 41EDTCDE(M)
     A            PKPPR          5Y 0O  6 54EDTCDE(4)
     A            PKSAMM         1A  O  6 60
     A            PKKOLL         1A  O  6 62
     A            PKTEGN         1A  O  6 64
     A            PKJUST        10Y 3O  6 66EDTCDE(4)
     A            PKFVAL         3A  O  6 78
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0016)
     A                                      SFLPAG(0015)
     A  41                                  SFLDSP
     A  42                                  SFLDSPCTL
     A  43                                  SFLCLR
     A  44                                  SFLEND
     A  41N43N44                            ROLLUP(25)
     A  41N43N44                            ROLLDOWN(26)
     A          H                           HLPPNLGRP(SYFA09015 SYFA09PNL)
     A                                      HLPARA(01 001 20 080)
     A            ZRECNO         4S 0O  3 75SFLRCDNBR
     A                                      COLOR(WHT)
     A                                  1  2'SYSPED/SYFA09'
     A                                      DSPATR(ND)
     A  28                              1 19MSGCON(45 DIV1183 SYMSGF)
     A                                      COLOR(WHT)
     A  27                              1 19MSGCON(45 SHU1463 SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 71DATE
     A                                      EDTCDE(Y)
     A                                  2 71TIME
     A                                  2  2MSGCON(015 DIV1184 *LIBL/SYMSGF)
     A            WSKNR          8Y 0O  2 19EDTCDE(4)
     A                                      COLOR(WHT)
     A            KNAVN         30A  O  2 29COLOR(WHT)
     A                                  3 35MSGCON(015 DIV1185 *LIBL/SYMSGF)
     A            WSAVTN         1A  O  3 51COLOR(WHT)
     A                                  3  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(030 DIV0034 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  1MSGCON(079 DIV1186 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A          R BUNN
     A                                      CF03(03 'EXIT')
     A                                      CF05(05 'REFRESH')
     A                                      CF06(06 'CREATE')
     A                                      CF07(07 'FR,TILLEGG')
     A                                      CF08(08 'SEARCH')
     A                                      CF10(10 'DELETE')
     A                                      CF12(12 'CANCEL')
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A          H                           HLPPNLGRP(SYFA09015 SYFA09PNL)
     A                                      HLPARA(01 001 04 080)
     A                                  2  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  2 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  2 42MSGCON(018 DIV0022 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  2 62MSGCON(018 DIV0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 22MSGCON(018 DIV2206 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  4  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                              4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A                                  3 42MSGCON(018 JOV1028 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A          R VAREL2
     A                                      CF12(12 'CANCEL')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A          H                           HLPPNLGRP(SYFA09015 SYFA09PNL)
     A                                      HLPARA(01 001 04 080)
     A                                  1 18MSGCON(20 DIV1187 SYMSGF)
     A                                  2  2MSGCON(15 DIV1168 SYMSGF)
     A            W2GEBY         3A  B  2 19DSPATR(UL)
     A                                      DSPATR(PC)
     A            W2AVTT         1A  B  2 25DSPATR(UL)
     A            W2INT          5Y 0B  2 30DSPATR(UL)
     A                                      EDTCDE(4)
     A                                  3  2MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A          R NYWRK
     A                                      CF03(03 'EXIT')
     A                                      CF05(05 'REFRESH')
     A                                      CF06(06 'CREATE')
     A                                      CF07(07 'FR,TILLEGG')
     A                                      CF08(08 'SEARCH')
     A                                      CF12(12 'CANCEL')
     A          H                           HLPPNLGRP(SYFA09015 SYFA09PNL)
     A                                      HLPARA(01 001 24 080)
     A                                  1  2'SYSPED/SYFA09'
     A                                      COLOR(BLU)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                  2 70TIME
     A  28                              1 19MSGCON(45 DIV1183 SYMSGF)
     A                                      COLOR(WHT)
     A  27                              1 19MSGCON(45 SHU1463 SYMSGF)
     A                                      COLOR(WHT)
     A                                  2  2MSGCON(015 DIV1184 *LIBL/SYMSGF)
     A            WSKNR          8Y 0O  2 19EDTCDE(4)
     A                                      COLOR(WHT)
     A            KNAVN         30A  O  2 29COLOR(WHT)
     A                                  3 35MSGCON(015 DIV1185 *LIBL/SYMSGF)
     A            WSAVTN         1A  O  3 51COLOR(WHT)
     A                                  3  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(030 DIV0034 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  2MSGCON(078 DIV1186 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                 22  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 22 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 22 42MSGCON(018 DIV0022 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 22 62MSGCON(018 DIV0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                             24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A                                 23 42MSGCON(018 JOV1028 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A          R DLTAVT
     A                                      CF05(05 'CONFORM DELETE')
     A                                      CF12(12 'CANCEL')
     A                                      SLNO(15)
     A                                      CLRL(04)
     A                                      ALARM
     A                                      OVERLAY
     A                                  1  8'|¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯-
     A                                      ¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯|'
     A                                      COLOR(BLU)
     A                                  2  8'|'
     A                                      COLOR(BLU)
     A                                  2 11MSGCON(60 DIV2207 SYMSGF)
     A                                      DSPATR(PC)
     A                                      COLOR(RED)
     A                                  2 74'|'
     A                                      COLOR(BLU)
     A                                  3  8'|'
     A                                      COLOR(BLU)
     A                                  3 11MSGCON(60 DIV2208 SYMSGF)
     A                                      COLOR(RED)
     A                                  3 74'|'
     A                                      COLOR(BLU)
     A                                  4  8'|_________________________________-
     A                                      ________________________________|'
     A                                      COLOR(BLU)
