*****A**************************************************************
     A* RETTINGER I DETTE PROGRAM:
PTFNRA*SEK SIG VERS  BESKRIVELSE...........................
"""""A*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXXA* 01 TH HJELPETEKST HTML
*****A**************************************************************
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      REF(*LIBL/HEADF)
     A                                      PRINT
     A                                      CF03(03 'Avslutt program')
     A                                      CF12(12 'Annuller')
     A                                      HLPPNLGRP(HLPSCR SYFA94PNL)
     A                                      HLPTITLE('MERKE OPPDRAG LEVERT')
     A                                      HELP
     A                                      ALTHELP
     A          R BILDE1
     A                                      OVERLAY
     A                                      ROLLUP(25)
     A          H                           HLPPNLGRP(SYFA94010 SYFA94PNL)
     A                                      HLPARA(02 001 24 080)
     A                                  1  2'SYSPED/SYFA94R'
     A                                      COLOR(BLU)
     A                                  1 20MSGCON(040 DIV1892 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 64DATE
     A                                      EDTCDE(Y)
     A                                  2 64TIME
     A                                 10  2MSGCON(024 DIV1497 *LIBL/SYMSGF)
     A            ZEAVD     R     D  B 10 33REFFLD(HEAVD)
     A  31                                  DSPATR(PC)
     A  31                                  DSPATR(RI)
     A                                 12  2MSGCON(024 DIV1846 *LIBL/SYMSGF)
     A            ZEOPD     R     D  B 12 33REFFLD(HEOPD)
     A  32
     AON55N30                               DSPATR(PC)
     A                                      CHECK(ER)
     A  32                                  DSPATR(RI)
     A                                 14  2MSGCON(024 SHU1237 *LIBL/SYMSGF)
     A            ZEEXT         35   B 14 33
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 21MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A          R BILDE2
     A                                      ROLLUP(25)
     A                                      CF20(20 'Send på fax')
     A                                      CF21(21 'Send på fax')
     A                                      OVERLAY
S01  A*         H                           HLPDOC('MRKMOTT' 'HELPNU' 'HLP400')
N01  A          H                           HLPPNLGRP(SYFA94030 SYFA94PNL)
     A                                      HLPARA(02 001 24 080)
     A                                  1  2'SYSPED/SYFA94R   '
     A                                      COLOR(BLU)
     A                                  1 20MSGCON(040 DIV1892 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 65DATE
     A                                      EDTCDE(Y)
     A                                  2 65TIME
     A                                  5  2MSGCON(024 DIV1497 *LIBL/SYMSGF)
     A            ZEAVD     R        O  5 29REFFLD(HEAVD)
     A                                      EDTCDE(4)
     A                                  6  2MSGCON(024 DIV1846 *LIBL/SYMSGF)
     A            ZEOPD     R        O  6 29REFFLD(HEOPD)
     A                                      EDTCDE(4)
     A                                  9  2MSGCON(024 DIV1893 *LIBL/SYMSGF)
     A            ZESGM     R        B  9 29REFFLD(HESGM)
     A  31
     AON55N30                               DSPATR(PC)
     A  31                                  DSPATR(RI)
     A                                 10  2MSGCON(024 DIV1894 *LIBL/SYMSGF)
     A            ZEDTMO         6A  B 10 29
     A  32                                  DSPATR(PC)
     A  32                                  DSPATR(RI)
     A                                 11  2MSGCON(024 TRO0492 *LIBL/SYMSGF)
     A            ZEKLMO         4A  B 11 29
     A  33                                  DSPATR(RI)
     A  33                                  DSPATR(PC)
     A                                 13  2MSGCON(024 DIV1895 *LIBL/SYMSGF)
     A            ZETLE     R        B 13 29REFFLD(HETLE)
     A  33                                  DSPATR(PC)
     A  33                                  DSPATR(RI)
     A  97                                  DSPATR(PR)
     A  97                                  COLOR(RED)
     A                                 14  2MSGCON(024 DIV1896 *LIBL/SYMSGF)
     A            ZETLL     R        B 14 29REFFLD(HETLL)
     A  34                                  DSPATR(PC)
     A  34                                  DSPATR(RI)
     A  97                                  DSPATR(PR)
     A  97                                  COLOR(RED)
     A                                 15  2MSGCON(024 DIV1897 *LIBL/SYMSGF)
     A            ZETLKU    R        B 15 29REFFLD(HETLKU)
     A  35                                  DSPATR(PC)
     A  35                                  DSPATR(RI)
     A  97                                  DSPATR(PR)
     A  97                                  COLOR(RED)
     A                                 15 45'( ,X)'
     A                                 16  2MSGCON(024 DIV1898 *LIBL/SYMSGF)
S13  A*           ZIDTG     R        B 16 29REFFLD(SIDTG SADH)
N13  A            ZIDTG6         6Y 0B 16 29
     A  36                                  DSPATR(PC)
     A  36                                  DSPATR(RI)
     A  97                                  DSPATR(PR)
     A  97                                  COLOR(RED)
     A                                 17  2MSGCON(024 DIV1899 *LIBL/SYMSGF)
     A            ZEGNN     R        B 17 29REFFLD(HEGNN)
     A  37                                  DSPATR(PC)
     A  37                                  DSPATR(RI)
     A                                 23  2MSGCON(024 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 26MSGCON(024 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 30MSGCON(018 SHU0209 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A          R BILDE2T
     A                                      ROLLUP(25)
     A                                      CF20(20 'Send på fax')
     A                                      CF21(21 'Send på fax')
     A                                      OVERLAY
     A                                  1  2'SYSPED/SYFA94R   '
     A                                      COLOR(BLU)
     A                                  1 20MSGCON(040 DIV1892 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 65DATE
     A                                      EDTCDE(Y)
     A                                  2 65TIME
     A                                  6  2'Turnummer...............'
     A            ZEPRO          8Y 0O  6 29EDTCDE(4)
     A                                  9  2MSGCON(024 DIV1893 *LIBL/SYMSGF)
     A            ZESGM     R        B  9 29REFFLD(HESGM)
     A  31
     AON55N30                               DSPATR(PC)
     A  31                                  DSPATR(RI)
     A                                 10  2MSGCON(024 DIV1894 *LIBL/SYMSGF)
     A            ZEDTMO         6A  B 10 29
     A  32                                  DSPATR(PC)
     A  32                                  DSPATR(RI)
     A                                 11  2MSGCON(024 TRO0492 *LIBL/SYMSGF)
     A            ZEKLMO         4A  B 11 29
     A  33                                  DSPATR(RI)
     A  33                                  DSPATR(PC)
     A                                 14  2'OBS! Denne funksjonen legger inn P-
     A                                      OD (IOD)                          '
     A                                      COLOR(WHT)
     A                                 15  2'     på ALLE OPPDRAG/FRAKTBREV på -
     A                                      oppgitt turnr!'
     A                                      COLOR(WHT)
     A                                 17  2'BØR KUN BRUKES NÅR EN HEL TUR OVER-
     A                                      LATES TIL EN UNDERTRANSPORTØR,     -
     A                                        '
     A                                 18  2'OG DET IKKE SENERE SKAL REGISTRERE-
     A                                      S (MANUELT ELLER AUTOMATISK)       -
     A                                        '
     A                                 19  2'ENDELIG POD/IOD MOT SLUTTKUNDEN !!-
     A                                      !!                                 -
     A                                        '
     A                                 23  2MSGCON(024 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 26MSGCON(024 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A          R BILDE3
     A                                      OVERLAY
S01  A*         H                           HLPDOC('MRKMOTT' 'HELPNU' 'HLP400')
N01  A          H                           HLPPNLGRP(SYFA94010 SYFA94PNL)
     A                                      HLPARA(02 001 24 080)
     A  30        ZMSGTX        79A  O 24  2MSGID(&ZMSGNO SYMSGF)
     A                                      COLOR(WHT)
     A N30                             24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A            ZMSGNO         7A  P
     A          R BILDE5
     A                                      OVERLAY
     A                                      ROLLUP(25)
S01  A*         H                           HLPDOC('MRKMOTT' 'HELPNU' 'HLP400')
N01  A          H                           HLPPNLGRP(SYFA94010 SYFA94PNL)
     A                                      HLPARA(02 001 24 080)
     A                                  1  2'SYSPED/SYFA94R'
     A                                      COLOR(BLU)
     A                                  1 20MSGCON(040 DIV1892 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 65DATE
     A                                      EDTCDE(Y)
     A                                  2 65TIME
     A                                  5  2MSGCON(024 DIV1497 *LIBL/SYMSGF)
     A            ZEAVD     R        O  5 30REFFLD(HEAVD)
     A                                      EDTCDE(4)
     A                                  6  2MSGCON(024 DIV1846 *LIBL/SYMSGF)
     A            ZEOPD     R        O  6 29REFFLD(HEOPD)
     A                                      EDTCDE(4)
     A                                  7  2MSGCON(024 DIV2062 *LIBL/SYMSGF)
     A            HEST           1A  O  7 33
     A            FAVK           3A  O  9  2
     A                                  9  6MSGCON(015 DIV2060 *LIBL/SYMSGF)
     A            ÅDGR           3Y 0O  9 22EDTCDE(4)
     A                                  9 26MSGCON(009 DIV2061 *LIBL/SYMSGF)
     A  98                              9 36MSGCON(010 DIV2069 *LIBL/SYMSGF)
     A N98                              9 36MSGCON(010 DIV2068 *LIBL/SYMSGF)
     A            FABELN        13Y 2B  9 48EDTCDE(4)
     A                                      CHECK(RB)
     A            FICURR         3A  O  9 66
     A  99                             13  2MSGCON(024 DIV2064 *LIBL/SYMSGF)
     A  99        WSCALC         1A  B 13 33
     A  99                             13 51MSGCON(010 DIV0259 *LIBL/SYMSGF)
