SY   A*  FUNKSJONSTASTER ER FJERNET
SY   A*  ENDRINGER ER MERKET MED SY
     A*%%TS  SD  19921203  123534  IBMJO       REL-V2R1M0  5738-PW1
     A*  90/11/29  12:40:33    IBMSS       REL-R02M00  5728-PW1
     A*****************************************************************
     A*   5797-AWD    IBM NORWAY 1988                                 *
     A*   5797-AWD    COPYRIGHT IBM CORP 1988                         *
     A*   REFER TO INSTRUCTIONS ON COPYRIGHT NOTICE NO. G120-2083     *
     A*****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/IBMFRF)
     A                                      MSGLOC(24)
     A                                      ALTHELP
     A                                      ALTPAGEDWN
     A                                      PRINT
     A                                      HLPDOC('VR00101' 'VR001' 'IBMFFT')
     A                                      HELP
     A*  90/11/29  12:11:48    IBMSS       REL-R02M00  5728-PW1
     A          R VR001B01
     A                                      OVERLAY
     A          H                           HLPDOC('VR00101' 'VR001' 'IBMFFT')
     A                                      HLPARA(01 001 24 080)
     A            WFNAVN    R        O  1  3DSPATR(HI)
     A                                  1 55'Dato:'
     A                                      DSPATR(HI)
     A                                  1 61DATE
     A                                      EDTCDE(Y)
     A                                      DSPATR(HI)
     A            WWSID     R        O  1 70DSPATR(HI)
     A            WUSER     R        O  2  3DSPATR(HI)
     A            WJOBNR    R        O  2 14DSPATR(HI)
     A                                  2 27'STYRING AV UTSKRIFT'
     A                                      DSPATR(HI)
     A                                  2 55'Tid :'
     A                                      DSPATR(HI)
     A                                  2 61TIME
     A                                      DSPATR(HI)
     A                                  2 70'VR001'
     A                                      DSPATR(HI)
     A                                  3  2'-----------------------------------
     A                                      ------------------------------------
     A                                      ---------'
     A                                      DSPATR(HI)
     A*  90/11/29  12:40:33    IBMSS       REL-R02M00  5728-PW1
     A          R VR001B02
     A*%%TS  SD  19921203  123534  IBMJO       REL-V2R1M0  5738-PW1
SY   A**                                    CA12(12 'AVBRYT')
SY   A**                                    CA03(03 'GÅ UT')
SY   A**                                    CF04(04 'FORESPØRSEL')
     A                                      OVERLAY
     A                                      PUTOVR
     A          H                           HLPDOC('VR00101' 'VR001' 'IBMFFT')
     A                                      HLPARA(07 001 07 080)
     A          H                           HLPDOC('VR00102' 'VR001' 'IBMFFT')
     A                                      HLPARA(08 001 08 080)
     A          H                           HLPDOC('VR00103' 'VR001' 'IBMFFT')
     A                                      HLPARA(09 001 09 080)
     A          H                           HLPDOC('VR00104' 'VR001' 'IBMFFT')
     A                                      HLPARA(10 001 10 080)
     A          H                           HLPDOC('VR00105' 'VR001' 'IBMFFT')
     A                                      HLPARA(11 001 11 080)
     A          H                           HLPDOC('VR00106' 'VR001' 'IBMFFT')
     A                                      HLPARA(12 001 12 080)
     A          H                           HLPDOC('VR00107' 'VR001' 'IBMFFT')
     A                                      HLPARA(13 001 14 080)
     A          H                           HLPDOC('VR00108' 'VR001' 'IBMFFT')
     A                                      HLPARA(15 001 15 080)
     A          H                           HLPDOC('VR00109' 'VR001' 'IBMFFT')
     A                                      HLPARA(16 001 18 080)
     A          H                           HLPDOC('VR00199' 'VR001' 'IBMFFT')
     A                                      HLPARA(19 001 24 080)
     A  49                              6 10'Skriverfil..............:'
     A  49        SKRILD    R        O  6 38REFFLD(FORMID)
     A                                      OVRDTA
     A  49        SKKOMM    R        O  6 49
     A                                  7 10'Formularnavn.............'
     A                                      DSPATR(HI)
     A            HFORM     R        B  7 38REFFLD(FORMID)
     A                                      OVRDTA
     A  35                                  DSPATR(PR)
     A  72                                  ERRMSG('Formularnavn kan ikke være -
     A                                      blank')
     A                                  8 10'Skriver..................'
     A                                      DSPATR(HI)
     A            HSKID     R        B  8 38REFFLD(SKID)
     A                                      OVRDTA
     A  36                                  DSPATR(PR)
     A  71                                  ERRMSG('Ukjent skriver')
     A  75                                  ERRMSG('Skriver eller utskriftskø m-
     A                                      å fylles ut')
     A  73                                  ERRMSG('Kan ikke fylle ut begge fel-
     A                                      ter')
     A                                  8 51'F4=Liste'
     A                                  9 10'Utskriftskø..............'
     A                                      DSPATR(HI)
     A            HSKKO     R        B  9 38REFFLD(SKKO)
     A                                      OVRDTA
     A  37                                  DSPATR(PR)
     A  73                                  ERRMSG('Kan ikke fylle ut begge fel-
     A                                      ter')
     A  74                                  ERRMSG('Ukjent utskriftskø')
     A  75                                  ERRMSG('Skriver eller utskriftskø m-
     A                                      å fylles ut')
     A                                 10 10'Antall eksemplarer.......'
     A                                      DSPATR(HI)
     A            HANTKO    R        B 10 38REFFLD(ANTKO)
     A                                      OVRDTA
     A  79                                  ERRMSG('Ugyldig verdi for antall ek-
     A                                      semplarer')
     A  38                                  DSPATR(PR)
     A                                 10 51'1-99'
     A                                 11 10'Formularlengde...........'
     A                                      DSPATR(HI)
     A            HLENGD    R        B 11 38REFFLD(LENGDE)
     A                                      OVRDTA
     A  80                                  ERRMSG('Ugyldig verdi for formularl-
     A                                      engde')
     A  39                                  DSPATR(PR)
     A                                      COMP(NL 0)
     A                                      EDTCDE(4)
     A                                 11 51'1-255'
     A                                 12 10'Skrivetetthet............'
     A                                      DSPATR(HI)
     A            HTEGN     R     M  B 12 38REFFLD(TEGN)
     A                                      OVRDTA
     A  70                                  ERRMSG('Ugyldig verdi for skrivetet-
     A                                      thet')
     A  78                                  ERRMSG('For å vise tegn pr. tomme m-
     A                                      å skriver el. skrivekø være angitt')
     A  40                                  DSPATR(PR)
     A                                      TEXT('Tegn pr. tomme/font')
     A                                 12 51'F4=Liste'
     A                                 14 10'Ta vare på liste.........'
     A            HSAVE     R        B 14 38REFFLD(SAVE)
     A                                      OVRDTA
     A  41                                  DSPATR(PR)
     A                                 14 51'J=Ja,blank=Nei'
     A                                 15 10'Holde tilbake liste......'
     A            HHOLD     R        B 15 38REFFLD(HOLD)
     A                                      OVRDTA
     A  42                                  DSPATR(PR)
     A                                 15 51'J=Ja,blank=Nei'
     A                                 17 10'Jobbkø...................'
     A            HJOBQ         10   B 17 38
     A  77                                  ERRMSG('Valgt jobbkø eksisterer ikk-
     A                                      e')
     A  43                                  DSPATR(PR)
     A                                 18 10'Holde i jobbkø...........'
     A            HJHOLD         1   B 18 38
     A                                      VALUES(' ' 'J')
     A  44                                  DSPATR(PR)
     A                                 18 51'J=Ja,blank=Nei'
SY   A**                               23  2'F3=Gå ut'
SY   A**                                    COLOR(BLU)
SY   A**                               23 17'F4=Forespørsel'
SY   A**                                    COLOR(BLU)
SY   A**                               23 66'F12=Annuller'
SY   A**                                    COLOR(BLU)
     A                                 24  2'Skriverfilen er ikke opprettet i F-
     A                                      ormularfilen. Bruker STANDARD-formu-
     A                                      lar'
     A N76                                  DSPATR(ND)
     A                                      OVRATR
     A                                      COLOR(WHT)
     A*  88/08/29  14:34:25    IBM         REL-R01M00  5728-PW1
     A          R VR001B03
     A                                      WINDOW(VR001B99)
     A                                      OVERLAY
     A                                 13  1'F12=Annuller'
     A                                      COLOR(BLU)
     A*  88/08/29  14:34:25    IBM         REL-R01M00  5728-PW1
     A          R VR001B04
SY   A**                                    CA12(12 'AVBRYT')
     A                                      WINDOW(9 44 11 31)
     A                                      OVERLAY
     A            WSKRIV    R        O  1 12REFFLD(SKID)
     A                                      DSPATR(HI)
     A                                  2  2'Velg alternativ, trykk Enter'
     A                                      COLOR(BLU)
     A                                  3  2'1=Valg'
     A                                      COLOR(BLU)
     A                                  4  2'Valg  Tegn  Font'
     A                                      DSPATR(HI)
     A            VELG1          1A  I  5  3CHECK(AB)
     A N50                                  DSPATR(PC)
     A  50                                  DSPATR(PR ND)
     A                                      VALUES(' ' '1')
     A            TEG1SK    R        O  5  8REFFLD(TEGN)
     A            FON1SK    R        O  5 14REFFLD(TEGN)
     A            VELG2          1A  I  6  3CHECK(AB)
     A  50                                  DSPATR(PR ND)
     A                                      VALUES(' ' '1')
     A            TEG2SK    R        O  6  8REFFLD(TEGN)
     A            FON2SK    R        O  6 14REFFLD(TEGN)
     A            VELG3          1A  I  7  3CHECK(AB)
     A  50                                  DSPATR(PR ND)
     A                                      VALUES(' ' '1')
     A            TEG3SK    R        O  7  8REFFLD(TEGN)
     A            FON3SK    R        O  7 14REFFLD(TEGN)
     A            VELG4          1A  I  8  3CHECK(AB)
     A  50                                  DSPATR(PR ND)
     A                                      VALUES(' ' '1')
     A            TEG4SK    R        O  8  8REFFLD(TEGN)
     A            FON4SK    R        O  8 14REFFLD(TEGN)
     A            MELD04        20A  O  9  8DSPATR(HI)
     A                                 10  2'F12=Annuller'
     A                                      COLOR(BLU)
     A*  88/08/29  14:34:25    IBM         REL-R01M00  5728-PW1
     A          R VR001H01                  SFL
     A            VELG           1A  I  5  2DSPATR(PC)
     A                                      CHECK(AB)
     A                                      DFT(' ')
     A                                      VALUES('1')
     A            SKID      R        O  5  6
     A            SKTYPE    R        O  5 20
     A            TEG1SK    R        O  5 23REFFLD(TEGN)
     A            TEG2SK    R        O  5 28REFFLD(TEGN)
     A            TEG3SK    R        O  5 33REFFLD(TEGN)
     A            TEG4SK    R        O  5 38REFFLD(TEGN)
     A            SKKOMM    R        O  5 43
     A*  88/08/29  14:34:25    IBM         REL-R01M00  5728-PW1
     A          R VR001K01                  SFLCTL(VR001H01)
     A                                      WINDOW(VR001B99)
     A                                      SFLSIZ(0009)
     A                                      SFLPAG(0008)
     A                                      CA12(12 'AVBRYT')
     A N34N33 31                            ROLLUP(07)
     A N34N33 31                            ROLLDOWN(08)
     A N34N33 31                            CA08(07)
     A N34N33 31                            CA07(08)
     A                                      FRCDTA
     A                                      OVERLAY
     A  31                                  SFLDSP
     A  32                                  SFLDSPCTL
     A  33                                  SFLCLR
     A  34                                  SFLEND
     A          H                           HLPDOC('VR00110' 'VR001' 'IBMFFT')
     A                                      HLPARA(01 001 12 052)
     A          H                           HLPDOC('VR00199' 'VR001' 'IBMFFT')
     A                                      HLPARA(13 001 14 052)
     A                                  1  1'Velg alternativ, trykk Enter'
     A                                      COLOR(BLU)
     A                                  2  3'1=Valg'
     A                                      COLOR(BLU)
     A            RRN            4S 0O  2 20SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  3 23' Tegn pr. tomme'
     A                                      DSPATR(HI)
     A                                  4  1'Valg'
     A                                      DSPATR(HI)
     A                                  4  6'Skrivernavn'
     A                                      DSPATR(HI)
     A                                  4 18'Type'
     A                                      DSPATR(HI)
     A                                  4 23' 1    2    3    4'
     A                                      DSPATR(HI)
     A                                  4 43'Kommentar'
     A                                      DSPATR(HI)
     A          R VR001B99
     A                                      WINDOW(9 5 14 72)
