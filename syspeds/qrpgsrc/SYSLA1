      *********************************************************************
      * SPØRRING PÅ LAGERPOSTER
      * PROGRAM BEHANDLER INPUT FRA SYSLA (UTVALG/FILTER) OG VISER LISTE
      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/SYSLA1 MODULE(*LIBL/SYSLA1
CRTPGM*        *LIBL/CHECKUSR *LIBL/INCGI) ACTGRP(CGI) AUT(*USE)
      *
      *********************************************************************
      * MAIN PROGRAM FLOW
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
     FLLGRPK05  if   e           k disk    USROPN
     FLLHEAD    if   e           k disk    USROPN
     FLLHEAD04  if   e           k disk    USROPN
     F                                     RENAME(LLHEADR:LLHEADR04)
     FLLHEAD05  if   e           k disk    USROPN
     F                                     RENAME(LLHEADR:LLHEADR05)
     FLLHEAD06  if   e           k disk    USROPN
     F                                     RENAME(LLHEADR:LLHEADR06)
     FLLHEAD07  if   e           k disk    USROPN
     F                                     RENAME(LLHEADR:LLHEADR07)
     FLLHEAD10  if   e           k disk    USROPN
     F                                     RENAME(LLHEADR:LLHEADR10)
     FLLHEAD14  if   e           k disk    USROPN
     F                                     RENAME(LLHEADR:LLHEADR14)
     FLLHEAD15  if   e           k disk    USROPN
     F                                     RENAME(LLHEADR:LLHEADR15)
     FLLHEAD20  if   e           k disk    USROPN
     F                                     RENAME(LLHEADR:LLHEADR20)
     FLLLINE02  if   e           k disk    USROPN
     FTRACKF    if   e           k disk    USROPN
     FBRIDF     UF   E           K DISK    USROPN
     FBRPARF    IF   E           K DISK    USROPN
     FCUNDL01   IF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Prototype defintions and standard system API error structure
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      *--------------------------------------------------------------------
      * Variables common to CGI programs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
      * Variables received from the input string
     D OddEven         s             10
     D lng             s              2
     D BckGnd          s             10
     D UsrSpcName      s             10
     D WSUSER          S             10
     D WSNA            S             30
     D WSTDT0          s             10
     D WSTDT0T         s             10
     D WSBNR           s             17
     D WSTDT           s             10
     D WSPAK           s             17
     D WSORDR          s             17
     D WSFLN           s             17
     D WSALN           s              7
     D WSAVD           s              4
     D WSOPD           s              7
     D WSANR           s             15
     D W2BNR           s             17
     D W2TDT           s             10
     D W2PAK           s             17
     D W2ORDR          s             17
     D W2FLN           s             17
     D W2ALN           s              5
     D W2ANT0          s              7
     D W2WVEK          s              7
     D W2WKUB          s              7
     D W2AVD           s              4
     D W2OPD           s              7
     D KUNDEREF1       S             50
     D KUNDEREF2       S             50
     D KUNDEREF3       S             50
     D GrpNr           s              8
     D ANTSND          s              5  0
     D MaxSN           s              5
     D MaxSND          s              5  0
     D ANTRCD          s              5  0
     D MaxRC           s              5
     D MaxRCD          s              5  0
     D PGM             C                   CONST('SYSPED/CHECKUSR')
     D Spaces          s             12a   inz('&nbsp;&nbsp;')
     D TITLE           C                   CONST('Resultat av søk i historikk -
     D                                     (plukklister)')
     D TITLEEN         C                   CONST('Result of query archives -
     D                                     (packinglists)')
      *
     D BWSALN          s              7S 0
     D BWSTDT          s              8S 0
     D BWSTDT0T        s              8S 0
     D BWSAVD          s              4S 0
     D BWSOPD          s              7S 0
     D IfsMultIndicators...
     d                 ds
     D  NoErrors                       n
     D  NameTooLong                    n
     D  NotAccessible                  n
     D  NoFilesUsable                  n
     D  DupSections                    n
     D  FileIsEmpty                    n
      *
      *--------------------------------------------------------------------
      * Prolog common to CGI programs
      * -Receive input from the remote browser
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prolog3
      *=====================================================================******
      * MAIN PROCESS LINE
      *=====================================================================******
      * Get program start time for calculating execution time
     C                   time                    timedata1
      * Parse input
     C                   exsr      Parse
      *
     C                   EXSR      INIT
     C                   CALLB     PGM
     C                   PARM                    BIBRID
     C                   PARM                    UsrSpcName
     C                   PARM                    UINN              1
     C                   IF        UINN = 'N'
     C                   GOTO      SLUTT
     C                   END
      * dato/tid siste vellykkede operasjon.
     C     WSUSER        CHAIN     BRIDF                              50
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVE      WDT           BIDTF
     C                   MOVE      WTM           BITIDF
     C                   UPDATE    BRIDFR
     C                   MOVE      BIBESK        WINT              3
      *
      * Set output skeleton html member name
     C                   exsr      SetSkl
      *------------------                                                   ******
      * Write HTML sections
      *
      * 1-Section /$top
     C                   exsr      SetTop
     C                   callp     wrtsection('stdtop')
     C                   callp     wrtsection('top')
      * 4-Sections /$tablerow
      *
     C                   EXSR      HENTPL
      *
     C                   callp     updHTMLvar('ANTSND':
     C                             %trim(%editc(ANTSND:'3')))
     C                   callp     updHTMLvar('MaxSnd':
     C                             %trim(%editc(MaxSnd:'3')))
     C                   callp     updHTMLvar('ANTRCD':
     C                             %trim(%editc(ANTRCD:'3')))
     C                   callp     updHTMLvar('MaxRcd':
     C                             %trim(%editc(MaxRcd:'3')))
     C                   callp     updhtmlvar('runtime':
     C                             %trim(%editw(sec:'     0 .   ')))
      *
     C                   callp     wrtsection('bot')
     C                   callp     wrtsection('stdbot')
      *
      * 6-Section /$endhtml
     C     endhtml       tag
     C                   exsr      SetEnd
     C     SLUTT         tag
     C                   callp     wrtsection('endhtml')
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
     C                   EXSR      EXIT
     C                   return
      *=====================================================================******
      * Parse input
      *=====================================================================******
     C     Parse         begsr
     C                   eval      lng     = zhbgetvar('lng')
     C                   eval      BckGnd  = zhbgetvar('bckgnd')
     C                   EVAL      WSUSER  = zhbgetvar('USER')
     C                   EVAL      WSNA    = zhbgetvar('WSNA')
     C                   EVAL      UsrSpcName  = zhbgetvar('UsrSpcName')
      *
     C                   MOVE      *BLANKS       WSTDT
     C                   eval      WSTDT0  = zhbgetvar('WSTDT0')
     C                   eval      WSTDT0T = zhbgetvar('WSTDT0T')
     C                   eval      WSBNR   = zhbgetvar('WSBNR')
     C                   eval      WSBNR   = uppify(WSBNR)
      *
     C                   EXSR      TESTBNR
      *
     C                   eval      WSPAK   = zhbgetvar('WSPAK')
     C                   eval      WSPAK   = uppify(WSPAK)
     C                   eval      WSORDR  = zhbgetvar('WSORDR')
     C                   eval      WSORDR  = uppify(WSORDR)
     C                   eval      WSFLN   = zhbgetvar('WSFLN')
     C                   eval      WSFLN   = uppify(WSFLN)
     C                   eval      WSALN   = zhbgetvar('WSALN')
     C                   eval      BWSALN  = c2n2(WSALN)
     C                   eval      WSAVD   = zhbgetvar('WSAVD')
     C                   eval      BWSAVD  = c2n2(WSAVD)
     C                   eval      WSOPD   = zhbgetvar('WSOPD')
     C                   eval      BWSOPD  = c2n2(WSOPD)
     C                   eval      WSANR   = zhbgetvar('WSANR')
     C                   eval      WSANR   = uppify(WSANR)
     C                   SELECT
     C     WSTDT0        WHENNE    *BLANKS
     C                   eval      WSTDT   = WSTDT0
     C                   MOVEA     '10000000'    *IN(40)
     C     WSBNR         WHENNE    *BLANKS
     C                   eval      WSTDT   = zhbgetvar('WSTDT1')
     C                   MOVEA     '01000000'    *IN(40)
     C     WSPAK         WHENNE    *BLANKS
     C                   eval      WSTDT   = zhbgetvar('WSTDT2')
     C                   MOVEA     '00100000'    *IN(40)
     C     WSORDR        WHENNE    *BLANKS
     C                   eval      WSTDT   = zhbgetvar('WSTDT3')
     C                   MOVEA     '00010000'    *IN(40)
     C     WSFLN         WHENNE    *BLANKS
     C                   eval      WSTDT   = zhbgetvar('WSTDT4')
     C                   MOVEA     '00001000'    *IN(40)
     C     BWSALN        WHENNE    0
     C                   MOVEA     '00000100'    *IN(40)
     C     BWSAVD        WHENNE    0
     C                   MOVEA     '00000010'    *IN(40)
     C     WSANR         WHENNE    *BLANKS
     C                   eval      WSTDT   = zhbgetvar('WSTDT5')
     C                   MOVEA     '00000001'    *IN(40)
     C                   OTHER
     C                   eval      WSTDT   = '99999999'
     C                   eval      WSTDT0  = '99999999'
     C                   eval      WSTDT0T = *BLANKS
     C                   MOVEA     '10000000'    *IN(40)
     C                   ENDSL
     C                   EVAL      BWSTDT  = c2n2(WSTDT)
     C   40              EVAL      BWSTDT0T = c2n2(WSTDT0T)
     C  N40              EVAL      BWSTDT0T = *ZEROS
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$top
      *  (search criteria)
      *=====================================================================******
     C     SetTop        begsr
      * Repeat national language for the next invocation
     C                   callp     updhtmlvar('LNG':lng:
     C                             InitHTMLVars)
     C                   callp     updHTMLvar('ROWHEAD':RowHead)
     C                   callp     updHTMLvar('LogoImg':LogoImg)
      * Color for text, background, links, visited links, active links
     C                   callp     updhtmlvar('TXTCOLOR':'black')
     C                   callp     updhtmlvar('BOLD':' ')
     C                   callp     updhtmlvar('BGCOLOR':BIFARG)
     C                   callp     updhtmlvar('LINK':'0000FF')
     C                   callp     updhtmlvar('VLINK':'FF0000')
     C                   callp     updhtmlvar('ALINK':'00FF00')
     C                   IF        XSPRÅK = 'EN'
     C                   callp     updhtmlvar('TITLE':TITLEEN)
     C                   ELSE
     C                   callp     updhtmlvar('TITLE':TITLE)
     C                   END
      * KUNDE
     C                   callp     updhtmlvar('WSKN':
     C                             %trim(%editc(BIKUNR:'Z')))
     C     CUNDL01K      CHAIN     CUNDL01                            33
     C                   callp     updhtmlvar('WSNA':KNAVN)
     C                   callp     updhtmlvar('USER':WSUSER)
BODY C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updHtmlVar('UsrSpcName':UsrSpcName)
     C                   callp     updHTMLvar('sdato':
     C                             %trim(%editw(BIDTV:'    .  .  ')))
     C                   callp     updHTMLvar('stid':
     C                             %trim(%editw(BITIDV:'  .  .  ')))
      *
     C                   callp     updhtmlvar('KUNDEREF1':KUNDEREF1)
     C                   callp     updhtmlvar('KUNDEREF2':KUNDEREF2)
     C                   callp     updhtmlvar('KUNDEREF3':KUNDEREF3)
      * TRANS.DATO
     C     WSTDT0        ifeq      *BLANKS
     C                   callp     updhtmlvar('WSTDT0':spaces)
     C                   else
     C                   callp     updhtmlvar('WSTDT0':
     C                             %trim(%editw(BWSTDT:'    .  .  ')))
     C                   endif
      * TIL OG MED DATO
     C     WSTDT0T       ifeq      *BLANKS
     C                   callp     updhtmlvar('WSTDT0T':spaces)
     C                   else
     C                   callp     updhtmlvar('WSTDT0T':
     C                             %trim(%editw(BWSTDT0T:'    .  .  ')))
     C                   endif
      * MOTTAKER OG TRANS.DATO
     C     WSBNR         ifeq      *blanks
     C                   callp     updhtmlvar('WSBNR':spaces)
     C                   callp     updhtmlvar('WSTDT1':spaces)
     C                   else
     C                   callp     updhtmlvar('WSBNR':WSBNR)
     C                   callp     updhtmlvar('WSTDT1':
     C                             %trim(%editw(BWSTDT:'    .  .  ')))
     C                   endif
      * KUNDEREF.1 OG DATO
     C     WSPAK         ifeq      *blanks
     C                   callp     updhtmlvar('WSPAK':spaces)
     C                   callp     updhtmlvar('WSTDT2':spaces)
     C                   else
     C                   callp     updhtmlvar('WSPAK':WSPAK)
     C                   callp     updhtmlvar('WSTDT2':
     C                             %trim(%editw(BWSTDT:'    .  .  ')))
     C                   endif
      * KUNDEREF.2 OG DATO
     C     WSORDR        ifeq      *blanks
     C                   callp     updhtmlvar('WSORDR':spaces)
     C                   callp     updhtmlvar('WSTDT3':spaces)
     C                   else
     C                   callp     updhtmlvar('WSORDR':WSORDR)
     C                   callp     updhtmlvar('WSTDT3':
     C                             %trim(%editw(BWSTDT:'    .  .  ')))
     C                   endif
      * KUNDEREF.3 OG DATO
     C     WSFLN         ifeq      *blanks
     C                   callp     updhtmlvar('WSFLN':spaces)
     C                   callp     updhtmlvar('WSTDT4':spaces)
     C                   else
     C                   callp     updhtmlvar('WSFLN':WSFLN)
     C                   callp     updhtmlvar('WSTDT4':
     C                             %trim(%editw(BWSTDT:'    .  .  ')))
     C                   endif
      * PLUKKLISTENR
     C                   callp     updhtmlvar('WSALN':
     C                             %trim(%editC(BWSALN:'Z')))
      * OPPDRAGSNR
     C                   callp     updhtmlvar('WSAVD':
     C                             %trim(%editC(BWSAVD:'Z')))
     C                   callp     updhtmlvar('WSOPD':
     C                             %trim(%editC(BWSOPD:'Z')))
      * ARTIKKELNR OG DATO
     C     WSANR         ifeq      *blanks
     C                   callp     updhtmlvar('WSANR':spaces)
     C                   callp     updhtmlvar('WSTDT5':spaces)
     C                   else
     C                   callp     updhtmlvar('WSANR':WSANR)
     C                   callp     updhtmlvar('WSTDT5':
     C                             %trim(%editw(BWSTDT:'    .  .  ')))
     C                   endif
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$tablerow (table row)
      *=====================================================================******
     C     SetTabRow     begsr
      *
      * MOTTAKER
     C                   callp     updhtmlvar('LHBNA':LHBNA)
     C                   callp     updhtmlvar('LHBNR':LHBNR)
      * TRANS.DATO
     C                   callp     updhtmlvar('LHTDT':
     C                             %trim(%editW(LHTDT:'    .  .  ')))
      * KUNDEREF 1
     C                   callp     updhtmlvar('LHPAK':LHPAK)
      * KUNDEREF 2
     C                   callp     updhtmlvar('LHORDR':LHORDR)
      * KUNDEREF 3
     C                   callp     updhtmlvar('LHFLN':LHFLN)
     c* PLUKKLISTENR
     C                   callp     updhtmlvar('LHALN':
     C                             %trim(%editc(LHALN:'3')))
      * REKORDNR
     C                   MOVE      LHMN          XXMN              7
     C                   callp     updhtmlvar('LHMN':XXMN)
      * TOTAL STYKK
     C                   callp     updhtmlvar('LHANT0':
     C                             %trim(%editc(LHANT0:'Z')))
      * TOTAL VEKT
     C                   callp     updhtmlvar('LHWVEK':
     C                             %trim(%editc(LHWVEK:'3')))
      * TOTAL KUBIKK
     C                   callp     updhtmlvar('LHWKUB':
     C                             %trim(%editc(LHWKUB:'3')))
      * AVDELING
     C*                  callp     updhtmlvar('LHAVD':
     C*                            %trim(%editc(LHAVD:'Z')))
     C                   MOVE      LHAVD         XAN04             4
     C                   callp     updhtmlvar('LHAVD':XAN04)
      * OPPDRAG
     C*                  callp     updhtmlvar('LHOPD':
     C*                            %trim(%editc(LHOPD:'Z')))
     C                   MOVE      LHOPD         XAN07             7
     C                   callp     updhtmlvar('LHOPD':XAN07)
      * REGISTRERINGSDATO
     C                   callp     updhtmlvar('LHRDT':
     C                             %trim(%editw(LHRDT:'    .  .  ')))
      * REGISTRERINGSTID
     C                   callp     updhtmlvar('LHRTD':
     C                             %trim(%editw(LHRTD:'  :  :  ')))
      * HENT POD (ATA)
     C                   IF        LHAVD = 0
     C                   MOVE      *ZEROS        TTDATE
     C                   MOVE      *ZEROS        TTTIME
     C                   ELSE
     C     TRACKFK       SETLL     TRACKF
     C     TRACKFK       READE     TRACKF                                 33
     C                   MOVE      *ZEROS        XTTDATE           8 0
     C                   MOVE      *ZEROS        XTTTIME           6 0
     C                   DOW       *IN33 = *OFF
     C                   IF        TTACTI = 'POD'
     C                   IF        ((TTDATE = 0) OR (TTTIME = 0))
     C                   MOVE      TTDATE        XTTDATE
     C                   MOVE      TTTIME        XTTTIME
     C                   ELSE
     C                   LEAVE
     C                   END
     C                   END
     C     TRACKFK       READE     TRACKF                                 33
     C                   ENDDO
     C                   IF        *IN33
     C                   IF        ((XTTDATE = 0) AND (XTTTIME = 0))
     C                   MOVE      *ZEROS        TTDATE
     C                   MOVE      *ZEROS        TTTIME
     C                   ELSE
     C                   MOVE      XTTDATE       TTDATE
     C                   MOVE      XTTTIME       TTTIME
     C                   END
     C                   END
     C                   END
     C                   callp     updhtmlvar('TTDATE':
     C                             %trim(%editw(TTDATE:'    .  .  ')))
     C                   callp     updhtmlvar('TTTIME':
     C                             %trim(%editw(TTTIME:'  :  :  ')))
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$endhtml
      *=====================================================================******
     C     SetEnd        begsr
      * Compute run time
     C                   time                    timedata2
     C     timedata2     subdur    timedata1     ms:*ms
     C                   eval      sec = ms / 1000000
     C                   callp     updhtmlvar('runtime':
     C                             %trim(%editw(sec:'     0 .   ')))
      *
     C                   endsr
      *=====================================================================******
     C*  Select a Recd
      *=====================================================================******
     C     RecordSlt     begsr
     C                   move      'N'           selected          1
     C                   move      'Y'           selected
     C                   IF        ((XBNR <> *BLANKS) AND (XBNR <> LHBNR))
     C                   MOVE      'N'           SELECTED
     C                   END
     C*
     C     RecordSltX    tag
     C                   endsr
      *=====================================================================******
      * Set html output skeleton member before its read into memory
      *=====================================================================******
     C     SetSkl        begsr
      *------------------
      * Set html output skeleton member
     C                   IF        XSPRÅK = 'EN'
     C                   eval      IfsMultIndicators = gethtmlifsmult(
     C                             '/syspeddev/html/Header/eSpedTop.html -
     C                             /syspeddev/html/SYSLA1EN.html -
     C                             /syspeddev/html/Footer/eSpedBot.html':
     C                             '/CGITAG/')
     C                   ELSE
     C                   eval      IfsMultIndicators = gethtmlifsmult(
     C                             '/syspeddev/html/Header/eSpedTop.html -
     C                             /syspeddev/html/SYSLA1.html -
     C                             /syspeddev/html/Footer/eSpedBot.html':
     C                             '/CGITAG/')
     C                   END
      *
     C                   endsr
      *=====================================================================******
      * HENT ORDRE
      *=====================================================================******
     C     HENTPL        BEGSR
      *
     C                   Move      *zeros        AntSnd
     C                   Move      *zeros        AntRcd
     C     MaxSnd        IFEQ      *ZEROS
     C                   Z-add     1000          MaxSnd
     C                   END
     C     MaxRcd        IFEQ      *ZEROS
     C                   Z-add     5000          MaxRcd
     C                   END
      *
     C                   IF        XSORTRD = 'J'
     C   40LH10K1        setll     LLHEAD20
     C                   ELSE
     C   40LH10K1        setll     LLHEAD10
     C                   END
     C   41LH14K1        setll     LLHEAD14
     C   42LH05K1        setll     LLHEAD05
     C   43LH04K1        setll     LLHEAD04
     C   44LH06K1        setll     LLHEAD06
     C   45LH15K         setll     LLHEAD15
     C   46LH07K         setll     LLHEAD07
     C   47LL02K         setll     LLLINE02
     C   47              MOVE      *ZEROS        XLLMN             7 0
      *
     C     STREAD1       TAG
     C   40              IF        WSTDT = '99999999'
     C                   IF        XSORTRD = 'J'
     C     LH10K         READPE    LLHEAD20                               50
     C                   ELSE
     C     LH10K         READPE    LLHEAD10                               50
     C                   END
     C                   ELSE
     C                   IF        XSORTRD = 'J'
     C     LH10K         READE     LLHEAD20                               50
     C                   ELSE
     C     LH10K         READE     LLHEAD10                               50
     C                   END
     C  N50              IF        BWSTDT0T <> 0
     C     LHTDT         COMP      BWSTDT0T                           50
     C                   END
     C                   END
     C   41LH14K         READE     LLHEAD14                               50
     C   42LH05K         READE     LLHEAD05                               50
     C   43LH04K         READE     LLHEAD04                               50
     C   44LH06K         READE     LLHEAD06                               50
     C   45LH15K         READE     LLHEAD15                               50
     C   46LH07K         READE     LLHEAD07                               50
     C   47LL02K         READE     LLLINE02                               50
     C   47
     CANN50LLMN          CABEQ     XLLMN         STREAD1
     C   47
     CANN50              DO
     C     LLMN          CHAIN     LLHEAD                             50
     C  N50LHTDT         CABLT     BWSTDT        STREAD1
     C                   MOVE      LLMN          XLLMN
     C                   ENDDO
     C  N50LHSTA         CABEQ     'D'           STREAD1
     C  N50              IF        XLEVADR <> *BLANKS
     C     XLEVADR       CABNE     LHBNR         STREAD1
     C                   END
      *
     C     *in50         doweq     '0'
     C                   Add       1             AntRcd
     C     AntRcd        CabGe     MaxRcd        SLHENTPL
     C                   exsr      RecordSlt
     C     selected      ifeq      'Y'
     C                   exsr      SetTabRow
     C                   callp     wrtsection('row')
     C                   Add       1             AntSnd
     C     AntSnd        CabGe     MaxSnd        SLHENTPL
     C                   endif
      *
     C     STREAD2       TAG
     C   40              IF        WSTDT = '99999999'
     C                   IF        XSORTRD = 'J'
     C     LH10K         READPE    LLHEAD20                               50
     C                   ELSE
     C     LH10K         READPE    LLHEAD10                               50
     C                   END
     C                   ELSE
     C                   IF        XSORTRD = 'J'
     C     LH10K         READE     LLHEAD20                               50
     C                   ELSE
     C     LH10K         READE     LLHEAD10                               50
     C                   END
     C  N50              IF        BWSTDT0T <> 0
     C     LHTDT         COMP      BWSTDT0T                           50
     C                   END
     C                   END
     C   41LH14K         READE     LLHEAD14                               50
     C   42LH05K         READE     LLHEAD05                               50
     C   43LH04K         READE     LLHEAD04                               50
     C   44LH06K         READE     LLHEAD06                               50
     C   45LH15K         READE     LLHEAD15                               50
     C   46LH07K         READE     LLHEAD07                               50
     C   47LL02K         READE     LLLINE02                               50
     C   47
     CANN50LLMN          CABEQ     XLLMN         STREAD2
     C   47
     CANN50              DO
     C     LLMN          CHAIN     LLHEAD                             50
     C  N50LHTDT         CABLT     BWSTDT        STREAD2
     C                   MOVE      LLMN          XLLMN
     C                   ENDDO
     C  N50LHSTA         CABEQ     'D'           STREAD2
     C  N50              IF        XLEVADR <> *BLANKS
     C     XLEVADR       CABNE     LHBNR         STREAD2
     C                   END
     C                   ENDDO                                                  *hival
      *
     C     SLHENTPL      ENDSR
      *
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
     C                   OPEN      BRIDF
     C     WSUSER        CHAIN(N)  BRIDF                              50
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
OddEv * hent Parametre som går igjen i mange prog:
OddEv *      Odd/Even/Rowhead-farger,Logobilde,Språk,Intern/Ekstern bruker,  mm
OddEvc                   call      'ESGETPAR'
OddEvc                   parm                    BIBRID
OddEvc                   parm                    BIFIRM
OddEvc                   parm                    BIKUNR
OddEvc                   parm                    BIKNG
OddEvc                   parm                    RowHead          10
OddEvc                   parm                    Odd              10
OddEvc                   parm                    Even             10
OddEvc                   parm                    LogoImg          50
OddEvc                   parm                    XSPRÅK            2
OddEvc                   parm                    XINTERN           1
OddEvc                   parm                    ParmDS1        1024
OddEvc                   parm                    ParmDS2        1024
OddEvc                   parm                    ParmDS3        1024
OddEv *
     C                   open      BRPARF                               50
     C                   OPEN      CUNDL01
     C                   open      LLHEAD                               50
     C                   open      LLHEAD04                             50
     C                   open      LLHEAD05                             50
     C                   open      LLHEAD06                             50
     C                   open      LLHEAD07                             50
     C                   open      LLHEAD10                             50
     C                   open      LLHEAD14                             50
     C                   open      LLHEAD15                             50
     C                   open      LLHEAD20                             50
     C                   open      LLLINE02                             50
     C                   open      LLGRPK05                             50
     C                   open      TRACKF                               50
      *
     C     TRACKFK       klist
     C                   kfld                    LHAVD
     C                   kfld                    LHOPD
     c     LH10K         klist
     c                   kfld                    LHGRP
     c                   kfld                    LHPNR
     c     LH10K1        klist
     c                   kfld                    LHGRP
     c                   kfld                    LHPNR
     c                   kfld                    BWSTDT
     c     LH14K         klist
     c                   kfld                    LHGRP
     c                   kfld                    LHPNR
     c                   kfld                    WSBNR
     c     LH14K1        klist
     c                   kfld                    LHGRP
     c                   kfld                    LHPNR
     c                   kfld                    WSBNR
     c                   kfld                    BWSTDT
     c     LH04K         klist
     c                   kfld                    LHGRP
     c                   kfld                    LHPNR
     c                   kfld                    WSORDR
     c     LH04K1        klist
     c                   kfld                    LHGRP
     c                   kfld                    LHPNR
     c                   kfld                    WSORDR
     c                   kfld                    BWSTDT
     c     LH05K         klist
     c                   kfld                    LHGRP
     c                   kfld                    LHPNR
     c                   kfld                    WSPAK
     c     LH05K1        klist
     c                   kfld                    LHGRP
     c                   kfld                    LHPNR
     c                   kfld                    WSPAK
     c                   kfld                    BWSTDT
     c     LH06K         klist
     c                   kfld                    LHGRP
     c                   kfld                    LHPNR
     c                   kfld                    WSFLN
     c     LH06K1        klist
     c                   kfld                    LHGRP
     c                   kfld                    LHPNR
     c                   kfld                    WSFLN
     c                   kfld                    BWSTDT
     c     LH07K         klist
     c                   kfld                    BWSAVD
     c                   kfld                    BWSOPD
     c     LH15K         klist
     c                   kfld                    LHGRP
     c                   kfld                    LHPNR
     c                   kfld                    BWSALN
     c     LL02K         klist
     c                   kfld                    WSANR
     C     LH01K         klist
     C                   kfld                    LHSTA
     C                   kfld                    GKGRP
     C                   kfld                    BIKUNR
     C     CUNDL01K      klist
     C                   kfld                    BIFIRM
     C                   kfld                    BIKUNR
     C     BRPARFK       klist
     C                   kfld                    PABRID
     C                   kfld                    PAKUNR
     C                   kfld                    PAID
      *
     C     CUNDL01K      CHAIN     CUNDL01                            50
     C     BIKUNR        chain     LLGrpK05                           33
     C                   MOVE      GKGRP         LHGRP
     C                   MOVE      GKPNR         LHPNR
      *
     C                   MOVE      BIBRID        PABRID
     C                   MOVE      *ZEROS        PAKUNR
     C                   MOVEL     'SPRÅK'       PAID
     C     BRPARFK       chain     BRPARF                             33
     C                   IF        (*IN33 OR (PAVRDA <> 'EN'))
     C                   MOVE      *BLANKS       XSPRÅK            2
     C                   MOVE      'Ingen '      XINGEN            6
     C                   ELSE
     C                   MOVE      'EN'          XSPRÅK            2
     C                   MOVE      'No'          XINGEN            6
     C                   END
      *
     C                   MOVEL     'LLSOR'       PAID
     C     BRPARFK       chain     BRPARF                             33
     C                   IF        (*IN33 OR ((PAVRDA<>'J') AND (PAVRDA<>'j')))
     C                   MOVE      'N'           XSORTRD           1
     C                   ELSE
     C                   MOVE      'J'           XSORTRD
     C                   END
      *
     C                   MOVEL     'LLVMO'       PAID
     C     BRPARFK       chain     BRPARF                             33
     C                   IF        *IN33
     C                   MOVE      *BLANKS       XLEVADR          17
     C                   ELSE
     C                   MOVEL     PAVRDA        XLEVADR
     C                   MOVE      *BLANKS       WSBNR
     C                   END
      *
     C                   endsr
      *=====================================================================******
      *  TEST OM KUN EN VAREMOTTAKER
      *=====================================================================******
     C     TESTBNR       BEGSR
      *
     C                   open      BRIDF
     C     WSUSER        CHAIN(N)  BRIDF                              50
     C     BILIBL        ifeq      *blanks
     C                   CALLB     'INCGI'
     C                   PARM                    BISTDL
     C                   else
     C                   call      BILIBL
     C                   end
     C                   CLOSE     BRIDF
      *
     C                   open      BRPARF
      * HENT LEDERTEKST
     C                   MOVE      BIBRID        PABRID
     C                   MOVE      *ZEROS        PAKUNR
     C                   MOVEL     'LLRF1'       PAID
     C     BRPARFK       CHAIN     BRPARF                             50
     C                   IF        *IN50
     C                   MOVE      '*KUNDFT*  '  PABRID
     C                   MOVE      BIFIRM        PABRID
     C                   MOVE      BIKUNR        PAKUNR
     C     BRPARFK       SETLL     BRPARF
     C     BRPARFK       READE     BRPARF                                 50
     C                   IF        *IN50
     C                   MOVE      *ZEROS        PAKUNR
     C     BRPARFK       SETLL     BRPARF
     C     BRPARFK       READE     BRPARF                                 50
     C                   END
     C                   END
     C                   IF        *IN50
     C                   EVAL      KUNDEREF1 = 'Kunderef.1'
     C                   ELSE
     C                   EVAL      KUNDEREF1 = PAVRDA
     C                   END
      *
     C                   MOVE      BIBRID        PABRID
     C                   MOVE      *ZEROS        PAKUNR
     C                   MOVEL     'LLRF2'       PAID
     C     BRPARFK       CHAIN     BRPARF                             50
     C                   IF        *IN50
     C                   MOVE      '*KUNDFT*  '  PABRID
     C                   MOVE      BIFIRM        PABRID
     C                   MOVE      BIKUNR        PAKUNR
     C     BRPARFK       SETLL     BRPARF
     C     BRPARFK       READE     BRPARF                                 50
     C                   IF        *IN50
     C                   MOVE      *ZEROS        PAKUNR
     C     BRPARFK       SETLL     BRPARF
     C     BRPARFK       READE     BRPARF                                 50
     C                   END
     C                   END
     C                   IF        *IN50
     C                   EVAL      KUNDEREF2 = 'Kunderef.2'
     C                   ELSE
     C                   EVAL      KUNDEREF2 = PAVRDA
     C                   END
      *
     C                   MOVE      BIBRID        PABRID
     C                   MOVE      *ZEROS        PAKUNR
     C                   MOVEL     'LLRF3'       PAID
     C     BRPARFK       CHAIN     BRPARF                             50
     C                   IF        *IN50
     C                   MOVE      '*KUNDFT*  '  PABRID
     C                   MOVE      BIFIRM        PABRID
     C                   MOVE      BIKUNR        PAKUNR
     C     BRPARFK       SETLL     BRPARF
     C     BRPARFK       READE     BRPARF                                 50
     C                   IF        *IN50
     C                   MOVE      *ZEROS        PAKUNR
     C     BRPARFK       SETLL     BRPARF
     C     BRPARFK       READE     BRPARF                                 50
     C                   END
     C                   END
     C                   IF        *IN50
     C                   EVAL      KUNDEREF3 = 'Kunderef.3'
     C                   ELSE
     C                   EVAL      KUNDEREF3 = PAVRDA
     C                   END
      *
     C                   MOVEL     'LLVMO'       PAID
     C     BRPARFK       CHAIN     BRPARF                             50
     C                   IF        (*IN50 OR (PAVRDA = *BLANKS))
     C                   MOVE      *BLANKS       XBNR             17
     C                   ELSE
     C                   MOVEL     PAVRDA        XBNR
     C                   MOVE      *BLANKS       WSBNR
     C                   END
     C                   CLOSE     BRPARF
      *
     C                   endsr
      *=====================================================================******
      *  AVSLUTT
      *=====================================================================******
     C     EXIT          begsr
     C                   close     LLHEAD
     C                   close     LLHEAD04
     C                   close     LLHEAD05
     C                   close     LLHEAD06
     C                   close     LLHEAD07
     C                   close     LLHEAD10
     C                   close     LLHEAD14
     C                   close     LLHEAD15
     C                   close     LLHEAD20
     C                   close     LLLINE02
     C                   close     LLGRPK05
     C                   close     TRACKF
     C                   close     BRPARF
     C                   close     CUNDL01
     C                   eval      *inlr = *on
      *
     C                   endsr
      *
