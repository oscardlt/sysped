      *...............................................................
      * OBS!  VED INNLEGGING/FJERNING AV FELT MÅ OGSÅ GJØRES I PROGRAM
      *       SYST35 HVOR FELTNAVN ER OMDEFINERT.
      *"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
     A*___________________________
     A* SYSPED / Fakturavarelinjer
     A*"""""""""""""""""""""""""""
     A                                      REF(SR)
     A          R FAKTR
     A*
     A            FA01           1          COLHDG('*blanks')
     A*
     A            FALI           5S 0       COLHDG('LINE' 'NBR')
     A*
     A            FAAVD     R               REFFLD(AVD)
     A                                      COLHDG('Dep')
     A                                      EDTCDE(Z)
     A                                      TEXT('Department')
     A*
     A            FAOPD     R               REFFLD(OPD)
     A                                      EDTCDE(Z)
     A                                      COLHDG('Order' 'nbr')
     A*
     A            FASK      R               REFFLD(SK)
     A                                      COLHDG('Code' 'part')
     A                                      TEXT('S=Shipp,+
     A                                      K=Cons,+
     A                                      A=Agent,+
     A                                      I=Intern,+
     A                                      X=Free part,+
     A                                      E=Est.')
     A*
     A            FAVK      R               REFFLD(VK)
     A                                      COLHDG('Fee' 'cde')
     A*
     A            FAVT      R               REFFLD(VT)
     A                                      COLHDG('Freetext')
     A*
     A            FAVAL     R               REFFLD(VAL)
     A                                      COLHDG('Curr' 'cde')
     A*
     A            FABELV    R               REFFLD(BEL13)
     A                                      COLHDG('Amount')
     A                                      EDTCDE(J)
     A*
     A            FAKDM     R               REFFLD(KDM)
     A                                      COLHDG('VAT' 'cde')
     A*
     A            FABELN    R               REFFLD(BEL13)
     A                                      COLHDG('Amount Local' 'Currency')
     A                                      EDTCDE(J)
     A*
     A            FAKDA     R               REFFLD(KDA)
     A                                      COLHDG('Line' 'status')
     A                                      TEXT('C=Credited,+
     A                                      A=Bruttoavtale,+
     A                                      B=Kundeavtale,+
     A                                      D=Deleted,+
     A                                      S=Cons.inv,+
     A                                      L=Lager-sys,+
     A                                      T=Ship-sys,+
     A                                      K=AP post')
     A*
     A            FAKDUL    R               REFFLD(KDUL)
     A                                      COLHDG('Calc' 'dispmnt' 'fee')
     A                                      TEXT('J=yes,+
     A                                      N=no')
     A            FA01B          1
     A                                      TEXT('Unittype:D/1-9 days+
     A                                      L= lines SAD')
     A*
     A            FAKDUK    R               REFFLD(KDUK)
     A                                      COLHDG('Budget' 'cde')
     A                                      TEXT('B= budget,+
     A                                      S= splitt,+
     A                                      L= AP/GLcost,+
     A                                      H= GL cost')
     A*
     A            FABELU    R               REFFLD(BEL13)
     A                                      COLHDG('Budget amount')
     A                                      EDTCDE(J)
     A*
     A            FAKUNR    R               REFFLD(KN)
     A                                      COLHDG('Debitor' 'nbr')
     A                                      EDTCDE(Z)
     A*
     A            FAFAKT    R               REFFLD(FN)
     A                                      COLHDG('Invoice' 'nbr')
     A                                      EDTCDE(Z)
     A*
     A            FAOPKO    R               REFFLD(ST)
     A                                      COLHDG('status' 'cde')
     A                                      TEXT('F=invoiced,+
     A                                      G=ready GL,+
     A                                      O=transf.GL')
     A*
     A            FAJOUR    R               REFFLD(JNR)
     A                                      COLHDG('journal' 'nbr')
     A                                      TEXT('X-ref budgetfile')
     A                                      EDTCDE(Z)
     A*
     A            FACC      R               REFFLD(DTC)
     A                                      COLHDG('periode' 'century')
     A*
     A            FAÅR      R               REFFLD(DTÅ)
     A                                      COLHDG('periode' 'year')
     A*
     A            FAMND     R               REFFLD(DTM)
     A                                      COLHDG('periode' 'month')
     A*
     A            FAFRBN    R               REFFLD(FRBN)
     A                                      COLHDG('Waybill' 'nbr')
     A*
     A            FADATO    R               REFFLD(DT)
     A                                      COLHDG('Invoice' 'date')
     A*
     A            FADAFF    R               REFFLD(DT)
     A                                      COLHDG('Due' 'date')
     A*
     A            FAAVDR    R               REFFLD(AVD)
     A                                      COLHDG('Cost' 'place')
     A                                      EDTCDE(Z)
     A*
     A            FABÆR     R               REFFLD(AVD)
     A                                      COLHDG('Cost' 'level2')
     A                                      EDTCDE(Z)
     A*
     A            FALEVN    R               REFFLD(LEVN)
     A                                      COLHDG('Creditor' 'nbr')
     A                                      EDTCDE(Z)
     A            FAMADE         1          COLHDG('Line' 'made')
     A                                      TEXT('E=changed,+
     A                                      M=manual,+
     A                                      A=Agree.,+
     A                                      B=Brutto')
     A            FAAGRN         9P 0       COLHDG('Agreem. nbr')
     A                                      EDTCDE(Z)
     A            FAACCN         5P 0       COLHDG('Accou' 'nbr GL')
     A                                      EDTCDE(Z)
     A            FAEXRA         7P 4       COLHDG('Ex. rate')
     A                                      EDTCDE(J)
     A            FATAXA        13P 2       COLHDG('Tax amount')
     A                                      EDTCDE(J)
     A            FACURI         3          COLHDG('Cur' 'invoice')
     A            FAAMOU        13P 2       COLHDG('Amount unused')
     A                                      EDTCDE(J)
     A            FADOCN        11          COLHDG('Ref document')
     A            FACD11         1          COLHDG('Sammenslåes')
     A            FACD12         1          COLHDG('KONTR.STATUS')
     A            FACD13         1          COLHDG('L=LAGERLEIER')
     A            FACD21         2          COLHDG('X=I TRAVR,Y=JUS LAGD')
     A            FACD22         2          COLHDG('BETBET OVERSTYRT')
     A            FACD23         2          COLHDG('CODE 23')
     A            FACD31         3          COLHDG('CODE 31')
     A            FACD32         3          COLHDG('CODE 32')
     A            FACU33         3          COLHDG('VAL-KODE BUDSJETT')
     A            FAGGRP         2          COLHDG('GEBYR GRUPPE')
     A            FANT           7P 2       COLHDG('ANTALL')
     A            FAKURS         7P 4       COLHDG('KURS')
     A            FATAX          4S 4       COLHDG('MOMSSATS')
     A          K FAAVD
     A          K FAOPD
     A          K FALI
