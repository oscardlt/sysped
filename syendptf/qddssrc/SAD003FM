     A*%%TS  SD  20240417  141222  BING        REL-V7R4M0  5770-WDS
*****A**************************************************************
     A* RETTINGER I DETTE PROGRAM:
PTFnrA*Sek Sig Vers  Beskrivelse...........................
"""""A*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXXA* 01 YBC PTF10 NY OPSJON FOR KONVERTERING AV EDI
10XXXA* 02 YBC PTF10 NY OPSJON I FOR Å VISE FILER I IFS-OMRÅDE
xxxxxA*xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
11110A* 03 YBC PTF11 VIS FIL FRA IFS-OMRÅDE
11134A* 04 CB  PTF11 VIS LOGG OG FTP-LOGG
xxxxxA*xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
12227A* 05 YBC PTF12 Vis kurermanifest. pdf og json ved opsjon W
12XXX * 06 YBC PTF12 Opsjon 5 -> Vis regler som beskriver feil
*****A**************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3                  -
     A                                             27 132 *DS4)
     A                                      REF(*LIBL/EDIM)
     A                                      CF03(03 'Avslutt program')
     A                                      CF05(05 'Forny')
     A                                      CF12(12 'Annuller')
     A                                      PRINT
     A                                      HLPPNLGRP(HLPSCR SAD003PNL)
     A                                      HLPTITLE('SAD IMPORT')
     A                                      HELP
     A                                      ALTHELP
     A*
     A          R SAD003A                   SFL
     A*%%TS  SD  20211026  141832  JOVO        REL-V7R4M0  5770-WDS
     A            MSN       R        H
     A            MMN       R        H
     A            MSR       R        H
     A            SSR            1A  H
     A            M0004     R        H
     A            M0010     R        H
     A            MVEN      R        H
S01  A*           ZOP            1A  I  9  3VALUES(' ' '5' '6' '7' '9' 'P')
S02  A*           ZOP            1A  I  9  3VALUES(' ' '5' '6' '7' '9' 'P' 'V')
     A            ZOP            1A  I  9  3VALUES(' ' '5' '6' '7' '9' 'P' 'V' -
     A                                      'L' 'I' 'A' 'W' '1')
     A            W0004          8   O  9  6
     A  27                                  COLOR(PNK)
     A            W0010          8A  O  9 15
     A  27                                  COLOR(PNK)
     A            M0035     R        O  9 24
     A  27                                  COLOR(PNK)
     A            M0065     R        O  9 26
     A  27                                  COLOR(PNK)
     A            M1N07     R        O  9 33
     A  27                                  COLOR(PNK)
     A            M0068A    R        O  9 38
     A  27                                  COLOR(PNK)
     A            M0068B    R        O  9 47
     A  27                                  COLOR(PNK)
     A            M0068C    R        O  9 54
     A  27                                  COLOR(PNK)
     A            M3039E    R        O  9 57
     A  27                                  COLOR(PNK)
     A            MST       R        O  9 64
     A  27                                  COLOR(PNK)
     A            MDT       R        O  9 66
     A  27                                  COLOR(PNK)
     A            MTM       R        O  9 75
     A  27                                  COLOR(PNK)
     A*
     A          R SAD003B                   SFLCTL(SAD003A)
     A*%%TS  SD  20170314  130527  SVEIN       REL-V7R1M0  5770-WDS
     A  *DS3                                SFLSIZ(0013)
     A  *DS4                                SFLSIZ(0013)
     A  *DS3                                SFLPAG(0012)
     A  *DS4                                SFLPAG(0012)
     A                                      TEXT('EDI MESSAGE-LOG, Heading')
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A                                  1  2'TVINN'
     A                                      COLOR(BLU)
     A                                  1 34'Meldingslogg'
     A                                      COLOR(WHT)
     A                                  1 69'SAD003FM A'
     A                                      COLOR(BLU)
     A                                  3  2'Avdeling:'
     A            WAVD      R        O  3 12REFFLD(SIAVD *LIBL/SADH)
     A                                      EDTCDE(4)
     A                                  3 19'Oppdragsnummer:'
     A            WTDN      R        O  3 35REFFLD(SITDN *LIBL/SADH)
     A                                      EDTCDE(4)
S03  A*                                 5  2'Velg og trykk Enter:'
     A                                  5  2'Velg:'
     A                                      COLOR(BLU)
     A                                  5 10'L=FTP-Logg'
     A                                      COLOR(BLU)
     A                                  5 22'I=*IFS  W=*IFS via Browser'
     A                                      COLOR(BLU)
     A                                  5 50'A=Alt i Edi-mottak'
     A                                      COLOR(BLU)
     A                                  5 70'V=Vis SAD'
     A                                      COLOR(BLU)
     A                                  6  2'5=Vis melding  6=Vis feilmelding  -
     A                                      7=Vis komponent  9=Vis EDI-fact P=P-
     A                                      rint f.me'
     A                                      COLOR(BLU)
     A            ZRECNO         4  0O  4 75SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  8  2'Alt'
     A                                      COLOR(WHT)
     A                                  8  6'Avsender'
     A                                      COLOR(WHT)
     A                                  8 15'Mottaker'
     A                                      COLOR(WHT)
     A                                  8 24'T'
     A                                      COLOR(WHT)
     A                                  8 26'MSG ID'
     A                                      COLOR(WHT)
     A                                  8 33'Funk'
     A                                      COLOR(WHT)
     A                                  8 38'Dato'
     A                                      COLOR(WHT)
     A                                  8 47'Sekv'
     A                                      COLOR(WHT)
     A                                  8 53'Ver'
     A                                      COLOR(WHT)
     A                                  8 59'Eksp'
     A                                      COLOR(WHT)
     A                                  8 64'S'
     A                                      COLOR(WHT)
     A                                  8 66'Sent'
     A                                      COLOR(WHT)
     A                                  7  2MSGCON(078 SHU1713 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
      *
     A          R SAD003C
     A                                      OVERLAY
     A                                 23  2'F3=Gå ut   F5=Forny   F12=Annuller'
     A                                      COLOR(BLU)
     A*
     A          R SAD003D
     A*%%TS  SD  20211026  141832  JOVO        REL-V7R4M0  5770-WDS
     A                                      OVERLAY
     A                                      ROLLUP(25)
     A                                  1  2'TVINN'
     A                                      COLOR(BLU)
     A                                  1 34'Meldingslogg'
     A                                      COLOR(WHT)
     A                                  1 69'SAD003FM D'
     A                                      COLOR(BLU)
     A                                  3  2'Trykk Enter for å fortsette:'
     A                                      COLOR(BLU)
     A                                  5  2'Avdeling . . . . . . . . . . . . .-
     A                                       . :'
     A            MAVD      R        O  5 43REFFLD(SIAVD *LIBL/SADH)
     A                                      EDTCDE(4)
     A                                  6  4'Oppdragsnummer . . . . . . . . . .-
     A                                       :'
     A            MTDN      R        O  6 43REFFLD(SITDN *LIBL/SADH)
     A                                      EDTCDE(4)
     A                                  7  6'NODI Id Avsender . . . . . . . . :'
     A            M0004     R        O  7 43
     A                                  9  6'NODI Id Mottaker . . . . . . . . :'
     A            M0010     R        O  9 43
     A                                 22  6'Testindikator  . . . . . . . . . :'
     A            M0035     R        O 22 43
     A                                 11  6'Meldings Referanse . . . . . . . :'
     A            M0062     R        O 11 43
     A                                 12  6'Meldings Identitet . . . . . . . :'
     A            M0065     R        O 12 43
     A                                 13  6'Deklarasjonens dato  . . . . . . :'
     A            M0068A    R        O 13 43EDTWRD('    .  .  ')
     A                                 14  8'Sekvensnummer  . . . . . . . . :'
     A            M0068B    R        O 14 43EDTCDE(4)
     A                                 15 10'Versjonsnummer . . . . . . . :'
     A            M0068C    R        O 15 43EDTCDE(4)
     A                                 16  6'Omberegning, Deklarasjonens dato :'
     A            M0068D    R        O 16 43EDTWRD('    .  .  ')
     A                                 17  8'Sekvensnummer  . . . . . . . . :'
     A            M0068E    R        O 17 43EDTCDE(4)
     A                                 18 10'Versjonsnummer . . . . . . . :'
     A            M0068F    R        O 18 43EDTCDE(4)
     A                                 19  6'Ønsket Behandlingsdato . . . . . :'
     A            M2005B    R        O 19 43
     A                                 20  6'Deklarantens Referanse . . . . . :'
     A            M3039D    R        O 20 43EDTCDE(4)
     A                                 21  6'Ekspidisjons Enhet . . . . . . . :'
     A            M3039E    R        O 21 43EDTCDE(4)
     A                                 21 72'Mer...'
     A                                      COLOR(WHT)
     A                                 23  2'F3=Gå ut   F12=Annuller'
     A                                      COLOR(BLU)
     A            SNDNAME       30   O  8 43COLOR(WHT)
     A            RECNAME       30   O 10 43COLOR(WHT)
      *
     A          R SAD003I                   OVERLAY
     A                                      ROLLDOWN(26)
     A                                  1  2'TVINN'
     A                                      COLOR(BLU)
     A                                  1 34'Meldingslogg'
     A                                      COLOR(WHT)
     A                                  1 69'SAD003FM I'
     A                                      COLOR(BLU)
     A                                  3  2'Trykk Enter for å fortsette:'
     A                                      COLOR(BLU)
     A                                  5  2'Avdeling . . . . . . . . . . . . .-
     A                                       . :'
     A            MAVD      R        O  5 43REFFLD(SIAVD *LIBL/SADH)
     A                                      EDTCDE(4)
     A                                  6  4'Oppdragsnummer . . . . . . . . . .-
     A                                       :'
     A            MTDN      R        O  6 43REFFLD(SITDN *LIBL/SADH)
     A                                      EDTCDE(4)
     A                                  8  7'Depositum Beløp . . . . . . . . :'
     A            M5004D    R        O  8 43EDTCDE(K)
     A                                  9  7'Meldingsfunksjon  . . . . . . . :'
     A            M1N07     R        O  9 43
     A                                 10  7'Meldingsfunksjon Korrigert  . . :'
     A            M1N08     R        O 10 43
     A                                 11  7'Ekspidisjons Prioritet  . . . . :'
     A            M9N01     R        O 11 43
     A                                 12  7'Sendningsnummer . . . . . . . . :'
     A            MSN       R        O 12 43EDTCDE(4)
     A                                 13  9'Meldingsnummer  . . . . . . . :'
     A            MMN       R        O 13 43EDTCDE(4)
     A                                 14  7'Send/Recive . . . . . . . . . . :'
     A            MSR       R        O 14 43
     A                                 15  7'Status  . . . . . . . . . . . . :'
     A            MST       R        O 15 43
     A                                 16  9'Status Dato . . . . . . . . . :'
     A            MDT       R        O 16 43EDTWRD('    .  .  ')
     A                                 17 11'Status Tid  . . . . . . . . :'
     A            MTM       R        O 17 43EDTWRD('  :  :  ')
     A                                 18  7'Ventekode . . . . . . . . . . . :'
     A            MVEN      R        O 18 43
     A                                 21 72'Slutt '
     A                                      COLOR(WHT)
     A                                 23  2'F3=Gå ut   F12=Annuller'
     A                                      COLOR(BLU)
     A*
     A          R SAD003E                   SFL
     A*%%TS  SD  20240417  141222  BING        REL-V7R4M0  5770-WDS
     A            F0077     R        O  8  2REFFLD(TVINFF/F0077 *LIBL/TVINF)
     A            F4815     R        O  8  6REFFLD(TVINFF/F4815 *LIBL/TVINF)
     A            F0078A    R        O  8 11REFFLD(TVINFF/F0078A *LIBL/TVINF)
     A            F0078B    R        O  9 11REFFLD(TVINFF/F0078B *LIBL/TVINF)
     A            ZOP2           1A  I  9  9VALUES(' ' '5')
N06  A N89                                  DSPATR(PR)
     A            F0078C    R        O 10 11REFFLD(TVINFF/F0078C *LIBL/TVINF)
     A            F0078D    R        O 11 11REFFLD(TVINFF/F0078D *LIBL/TVINF)
     A            F0078E    R        O 12 11REFFLD(TVINFF/F0078E *LIBL/TVINF)
     A          R SAD003F                   SFLCTL(SAD003E)
     A*%%TS  SD  20240417  141222  BING        REL-V7R4M0  5770-WDS
     A  *DS3                                SFLSIZ(0004)
     A  *DS4                                SFLSIZ(0004)
     A  *DS3                                SFLPAG(0003)
     A  *DS4                                SFLPAG(0003)
     A  81N83N84                            ROLLUP(25)
     A  81N83N84                            ROLLDOWN(26)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  84                                  SFLEND
     A                                  1  2'TVINN'
     A                                      COLOR(BLU)
     A                                  1 21'Meldingslogg - Feilmeldinger - Tek-
     A                                      st'
     A                                      COLOR(WHT)
     A                                  1 69'SAD003FM E'
     A                                      COLOR(BLU)
     A                                  3  2'Avdeling:'
     A            MAVD      R        O  3 12EDTCDE(4)
     A                                  3 23'Oppdragsnummer:'
     A            MTDN      R        O  3 39EDTCDE(4)
     A                                  4  2'Dato....:'
     A            M0068A    R        O  4 12EDTWRD('    .  .  ')
     A                                  4 25'Sekvensnummer :'
     A            M0068B    R        O  4 41EDTCDE(4)
     A                                  4 50'Versjonsnummer:'
     A            M0068C    R        O  4 66EDTCDE(4)
     A                                  5  2'Trykk Enter for å fortsette:'
     A                                      COLOR(BLU)
     A            ÆRECNO         4S 0O  5 68SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  7  2'Ref'
     A                                      COLOR(WHT)
     A                                  7  6'Funk'
     A                                      COLOR(WHT)
     A                                  7 11'Tekst'
     A                                      COLOR(WHT)
      *
     A          R SAD003Ø
     A                                      OVERLAY
     A            ZMSGTX        79A  O 24  2MSGID(&ZMSGNO SYMSGF)
     A N30                                  COLOR(WHT)
     A  30                                  COLOR(RED)
     A            ZMSGNO         7A  P
