             PGM        PARM(&OPG &TRF &OPT &UNT &FDAT &TDAT &UFLG &SAKN +
                          &FORM &PRT &SPLQ &ANT &SAV &HLD &EPOST)
  /* */
  /* */
             DCL        VAR(&PRANT) TYPE(*CHAR) LEN(2) /* ANTALL +
                          UTSKRIFTER. */
             DCL        VAR(&OPG) TYPE(*CHAR) LEN(8)
             DCL        VAR(&TRF) TYPE(*CHAR) LEN(8)
             DCL        VAR(&OPT) TYPE(*CHAR) LEN(2)
             DCL        VAR(&UNT) TYPE(*CHAR) LEN(4)
             DCL        VAR(&FDAT) TYPE(*CHAR) LEN(6)
             DCL        VAR(&TDAT) TYPE(*CHAR) LEN(6)
             DCL        VAR(&UFLG) TYPE(*CHAR) LEN(1)
             DCL        VAR(&SAKN) TYPE(*CHAR) LEN(7)
             DCL        VAR(&FORM) TYPE(*CHAR) LEN(10)
             DCL        VAR(&PRT) TYPE(*CHAR) LEN(10)
             DCL        VAR(&SPLQ) TYPE(*CHAR) LEN(10)
             DCL        VAR(&ANT) TYPE(*CHAR) LEN(2)
             DCL        VAR(&SAV) TYPE(*CHAR) LEN(1)
             DCL        VAR(&HLD) TYPE(*CHAR) LEN(1)
             DCL        VAR(&WSAV) TYPE(*CHAR) LEN(4)
             DCL        VAR(&WHLD) TYPE(*CHAR) LEN(4)
             DCL        VAR(&EPOST) TYPE(*CHAR) LEN(70)
             DCL        VAR(&QUSER) TYPE(*CHAR) LEN(10)
             DCL        VAR(&QJOB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&QNBR) TYPE(*CHAR) LEN(6)
             DCL        VAR(&CVP1) TYPE(*CHAR) LEN(40) VALUE('SENDER +
                          AVVIKSRAPPORT VIA EPOST')
             DCL        VAR(&CVP2) TYPE(*CHAR) LEN(40)
             DCL        VAR(&CVP3) TYPE(*CHAR) LEN(40)
             DCL        VAR(&CVP4) TYPE(*CHAR) LEN(40)
             DCL        VAR(&CVP5) TYPE(*CHAR) LEN(40)
             DCL        VAR(&CVP6) TYPE(*CHAR) LEN(40)
             DCL        VAR(&CVP7) TYPE(*CHAR) LEN(40)
             DCL        VAR(&CVP8) TYPE(*CHAR) LEN(40)
             DCL        VAR(&CVP9) TYPE(*CHAR) LEN(40)
             DCL        VAR(&FILEN) TYPE(*CHAR) LEN(10) +
                          VALUE('SYIS20PF')
             DCL        VAR(&PDF) TYPE(*CHAR) LEN(4) VALUE('*YES')
 /*  */
             RTVJOBA    JOB(&QJOB) USER(&QUSER) NBR(&QNBR)
 /*  */
             IF         COND(&SAV *EQ 'J') THEN(CHGVAR VAR(&WSAV) +
                          VALUE('*YES'))
             ELSE       CMD(CHGVAR VAR(&WSAV) VALUE('*NO'))
 /*  */
             IF         COND(&HLD *EQ 'J') THEN(CHGVAR VAR(&WHLD) +
                          VALUE('*YES'))
             ELSE       CMD(CHGVAR VAR(&WHLD) VALUE('*NO'))
 /*  */
             IF         COND(&EPOST = ' ') THEN(DO)
             OVRPRTF    FILE(SYIS20PF) PAGESIZE(72 80) OVRFLW(66) +
                          OUTQ(&PRT) FORMTYPE(&FORM) COPIES(&ANT) +
                          HOLD(&WHLD) SAVE(&WSAV)
 /*  */
             CALL       PGM(SYIS21) PARM(&OPG &TRF &OPT &UNT &FDAT +
                          &TDAT &UFLG &SAKN &PRANT)
 /*  */
             IF         COND(&PRANT *NE '00') THEN( +
             SNDPGMMSG  MSG(' ' *BCAT &PRANT *BCAT 'SIDER ER SKR +
                          EVET PÅ VALGTE KRITERIER') +
                          TOUSR(&QUSER) MSGTYPE(*INFO))
 /*  */
             IF         COND(&PRANT *EQ '00') THEN( +
             SNDPGMMSG  MSG('OBS  INGEN ISOAVIK SKREVET PÅ VALGTE +
                          KRITERIER') +
                          TOUSR(&QUSER) MSGTYPE(*INFO))
             RETURN
             ENDDO
 /*  */
             OVRPRTF    FILE(SYIS20PF) DEVTYPE(*AFPDS) PAGESIZE(72 +
                          80) OVRFLW(66) OUTQ(OUTMAIL)
 /*  */
             CALL       PGM(SYIS21) PARM(&OPG &TRF &OPT &UNT &FDAT +
                          &TDAT &UFLG &SAKN &PRANT)
/*                                                       */
/* KALLER PROGRAMMET SOM SENDER EPOSTEN KUN HVIS POSTER  */
/*                                                       */
             IF         COND(&PRANT *NE '00') THEN( +
             CALL       PGM(FAXEPMMCL) PARM(&QUSER &QNBR &QJOB &FILEN +
                          &EPOST +
                          &CVP1 &CVP2 &CVP3 &CVP4 &CVP5 +
                          &CVP6 &CVP7 &CVP8 &CVP9 &PDF))
 /*  */
             IF         COND(&PRANT *NE '00') THEN( +
             SNDPGMMSG  MSG(' ' *BCAT &PRANT *BCAT 'SIDER ER SKR +
                          EVET PÅ VALGTE KRITERIER') +
                          TOUSR(&QUSER) MSGTYPE(*INFO))
 /*  */
             IF         COND(&PRANT *EQ '00') THEN( +
             SNDPGMMSG  MSG('OBS  INGEN ISOAVIK SKREVET PÅ VALGTE +
                          KRITERIER') +
                          TOUSR(&QUSER) MSGTYPE(*INFO))

 SLUTT:      ENDPGM
