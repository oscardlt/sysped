********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 YBC PTF10 VAREBESKRIVELSE ER ENDRET TIL 70, REST SKAL INN
      *              END NY FIL.
10312 * 02 YBC PTF10 LENGDE PÅ EN DEL FELTER
10311 * 03 YBC PTF10 DOKUMENTKODE ER ENDRET FRA 3A TIL 4A
********************************************************************
      *_____________________________________
      * SYSPED / Tolldekl. TRANSIT Varelinjer
      *"""""""""""""""""""""""""""""""""""""
     A                                      REF(SR)
     A          R DKNLR
     A            TV01           1          COLHDG('IKKE I BRUK')
     A            TVAVD     R               REFFLD(AVD)
     A            TVTDN          7S 0       COLHDG('OPPDRAGSNR')
     A            TVLI      R               REFFLD(LI5)
     A            TVVNT          6S 0       COLHDG('TARIFFNR')
N02  A            TVVNTL         1S 0       COLHDG('LENDE PÅ FELT')
     A            TVDK           5A         COLHDG('DEKLARASJONSTYPE')
S01  A*           TVVT         140A         COLHDG('VAREBESKRIVELSE')
N01  A            TVVT          70A         COLHDG('VAREBESKRIVELSE')
N02  A            TVVTL          3S 0       COLHDG('LENGDE PÅ FELT')
     A            TVVTSK         2A         COLHDG('SPRÅK FOR V.BESK.')
     A            TVVKTB         9S 3       COLHDG('VEKT BRUTTO')
     A            TVVKTN         9S 3       COLHDG('NETTO VEKT')
     A            TVALK          2A         COLHDG('AVSENDERSLAND')
     A            TVBLK          2A         COLHDG('BESTEMMELSESLAND')
      *
     A            TVNAS     R               REFFLD(NA)
     A                                      COLHDG('Navn Selger')
     A            TVADS1    R               REFFLD(AD)
     A                                      COLHDG('Adresse-1 Selger')
     A            TVPNS          9A         COLHDG('POSTNR SELGER')
     A            TVPSS         24A         COLHDG('POSTSTED SELGER')
     A            TVLKS          2A         COLHDG('LANDKODE SELGER')
     A            TVSKS          2A         COLHDG('SPRÅKKODE SELGER')
     A            TVTINS        17A         COLHDG('TIN-KODE SELGER')
     A            TVNAK     R               REFFLD(NA)
     A                                      COLHDG('Navn Kjøper')
     A            TVADK1    R               REFFLD(AD)
     A                                      COLHDG('Adresse-1 Kjøper')
     A            TVPNK          9A         COLHDG('POSTNR KJØPER')
     A            TVPSK         24A         COLHDG('POSTSTED KJØPER')
     A            TVLKK          2A         COLHDG('LANDKODE KJØPER')
     A            TVSKK          2A         COLHDG('SPRÅKKODE KJØPER')
     A            TVTINK        17A         COLHDG('TIN-KODE KJØPER')
      *
S03  A*           TVDTY          3A         COLHDG('44 DOKUMENTTYPE')
N03  A            TVDTY          4A         COLHDG('44 DOKUMENTTYPE')
     A            TVDREF        35A         COLHDG('44 DOKUMENTREF.')
N02  A            TVDREFL        3S 0       COLHDG('LENGDE PÅ FELT')
     A            TVDSK          2A         COLHDG('44 DOKUMENT SPRÅK')
     A            TVDO          26A         COLHDG('44 UTFYLT OPPLYSN.')
N02  A            TVDOL          3S 0       COLHDG('LENGDE PÅ FELT')
     A            TVDOSK         2A         COLHDG('44 UTF. OPPL.SPRÅK')
     A            TVTLO          3A         COLHDG('44 TILLEGGSOPPL.')
     A            TVEXKD         1S 0       COLHDG('44 EKSPORT FRA EF')
     A            TVEXLK         2A         COLHDG('44 EKSPORT FRA LAND')
     A            TVAVD2    R               REFFLD(AVD)
     A                                      COLHDG('AVD. FOR HENTET OPD')
     A            TVTDN2         7S 0       COLHDG('OPP. FOR HENTET OPD')
     A            TVCNR         17A         COLHDG('31 CONTAINERNR')
     A            TVMN          42A         COLHDG('31 MERKE & NR')
N02  A            TVMNL          3S 0       COLHDG('LENGDE PÅ FELT')
     A            TVMNSK         2A         COLHDG('31 MERKE & NR SPRÅK')
     A            TVEH           2A         COLHDG('31 PAKKTYPE')
     A            TVNT           5S 0       COLHDG('31 ANTALL KOLLI')
     A            TVNTEH         5S 0       COLHDG('31 ANTALL ENHET')
     A            TVFV           2A         COLHDG('FØLSOMME VARE KODE')
     A            TVFVNT        11S 3       COLHDG('MENGDE PÅ F.VARE')
      *================================================================
      * FOR IE44 LOSSEMERKNADER
      *================================================================
     A            NVVNT          6S 0       COLHDG('TARIFFNR')
S01  A*           NVVT          48A         COLHDG('VAREBESKRIVELSE')
N01  A            NVVT          70A         COLHDG('VAREBESKRIVELSE')
     A            NVVTSK         2A         COLHDG('SPRÅK FOR V.BESK.')
     A            NVVKTB         9S 3       COLHDG('Vekt Brutto')
     A            NVVKTN         9S 3       COLHDG('Vekt Netto')
S03  A*           NVDTY          3A         COLHDG('44 DOKUMENTTYPE')
N03  A            NVDTY          4A         COLHDG('44 DOKUMENTTYPE')
     A            NVDREF        35A         COLHDG('44 DOKUMENTREF.')
     A            NVDSK          2A         COLHDG('44 DOKUMENT SPRÅK')
     A            NVDO          26A         COLHDG('44 UTFYLT OPPLYSN.')
     A            NVDOSK         2A         COLHDG('44 UTF. OPPL.SPRÅK')
     A            NVCNR         17A         COLHDG('31 CONTAINERNR')
     A            NVMN          42A         COLHDG('31 MERKE & NR')
     A            NVMNSK         2A         COLHDG('31 MERKE & NR SPRÅK')
     A            NVEH           2A         COLHDG('31 PAKKTYPE')
     A            NVNT           5S 0       COLHDG('31 ANTALL KOLLI')
     A            NVNTEH         5S 0       COLHDG('31 ANTALL ENHET')
     A            NVFV           2A         COLHDG('FØLSOMME VARE KODE')
     A            NVFVNT        11S 3       COLHDG('MENGDE PÅ F.VARE')
      * KONTROLL RESULTAT PÅ LINJENIVÅ
     A            NVCT           2A         COLHDG('INDIKATOR DI/OT')
     A            NVCTB         70A         COLHDG('BESKRIVELSE')
     A            NVCTSK         2A         COLHDG('SPRÅKKODE')
     A            NVCTP         35A         COLHDG('PEKER TIL ATTR.')
      *================================================================
      *
     A          K TVAVD
     A          K TVTDN
     A          K TVLI
