     H DECEDIT(',') debug(*yes)
      **************************************************************
      *** PROGRAM for utskrift av tollagerregnskap               ***
      **************************************************************
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 xxx
12XXX * 02 YBC PTF12 Hent kolli og vekt fra omberegning
12XXX * 03 YBC PTF12 Henter data fra NCTS
12XXX * 04 YBC PTF12 Glemt test på nytt godsnr
********************************************************************
N03  FTRIH10    IF   E           K DISK
     FCUNDL01   IF   E           K DISK
     FFIRM      IF   E           K DISK
     FTURER14   IF   E           K DISK
     FFRISOK    IF   E           K DISK
     F*EADF     IP   E           K DISK
     Fhead18    IF   E           K DISK
     FSADH      IF   E           K DISK
     FSAEH6B    IF   E           K DISK
     FSADH6B    IF   E           K DISK
     F                                     RENAME(SADHR:SADHR6)
     FHEADFDUM  IF   E           K DISK    PREFIX(H_)
     F                                     RENAME(HEADFR:HEADFRD)
     FTRACKL02  IF   E           K DISK
     FSYIM03P   O    E             PRINTER OFLIND(*IN02)
     FSYIM03F   O  A E             DISK
     D                 DS
     D  LESGNP                 1     22
     D  HEGN                   1     15
     D  HEGN13                13     13
     D  HEPOS1                16     22
     D                 DS
     D  TTTEXT                 1     70
     D  TTDTG                 21     30
     D                 DS
     D  UDATO                  1      6
     D  UDAG                   1      2
     D  UMND                   3      4
     D  UAAR                   5      6
     D                 DS
     D  FSSOK                  1     35
     D  EXTREF                 1     15
     D                 DS
     D  ZST1                   1      5
     D  ZST                    1      5    DIM(5)
     D                 DS
     D  ZOT01                  1      2
     D  ZOT02                  3      4
     D  ZOT03                  5      6
     D  ZOT04                  7      8
     D  ZOT05                  9     10
     D  ZOT06                 11     12
     D  ZOT07                 13     14
     D  ZOT08                 15     16
     D  ZOT09                 17     18
     D  ZOT10                 19     20
     D  DSOT                   1     20
     D  ot                     1     20
     D                                     DIM(10)
      *_____
      * Init
     c                   exsr      srinit
      * les headf
     c                   exsr      lesh18
     C                   SETON                                        LR
     C                   RETURN
      ***************************************************************
     C     SRINIT        BEGSR
      ***************************************************************
      * Initiering                                  SRINIT
      * Input paramerer
     C     *ENTRY        PLIST
     C                   PARM                    ZAVDF
     C                   PARM                    ZAVDT
     C                   PARM                    ZGNF
     C                   PARM                    ZGNT
     C                   PARM                    ZDTF
     C                   PARM                    ZDTT
     C                   PARM                    ZOT              20
     C     ZST1          PARM                    ZST2              5
     C                   PARM                    ZUNG
     C                   PARM                    ZBRUDD            1
     C                   PARM                    ZLNRF             1
     C                   PARM                    ZLNRT             1
     C                   PARM                    EPOST            70
     C                   PARM                    ZPS               1
     c                   move      zot           dsot
      *
     c                   move      *zeros        zdtf8             8 0
     c                   move      *blanks       zdtt8             8 0
     c                   move      *blanks       datony            8
     c                   move      zdtf          udato
     c                   eval      datony='20' + uaar + umnd +udag
     c                   move      datony        zdtf8
     c                   move      zdtt          udato
     c                   eval      datony='20' + uaar + umnd +udag
     c                   move      datony        zdtt8
      *
     C     kunkey        KLIST
     C                   KFLD                    fifirm
     C                   KFLD                    heknk
     c                   read      firm                                   33
     C     SADHK         KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C     TTKEY         KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C                   KFLD                    XXFBNR
     C                   KFLD                    XXACTI
     C     FRIKEY        KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C                   KFLD                    WSKODE
     C                   MOVEL     '*CR'         WSKODE            3
     C     *LIKE         DEFINE    TTFBNR        XXFBNR
     C     *LIKE         DEFINE    TTACTI        XXACTI
     C                   MOVE      *ZEROS        XXFBNR
     C                   MOVE      'RUC'         XXACTI
      * Skriv ut Første siden.
     C     ZPS           IFNE      'S'
     C                   WRITE     SYIM03PA
      * Skriv ut Heading.
     C                   WRITE     SYIM03PB
     C                   END
      * 1-page indikator.
     C                   MOVE      '1'           WIN1P             1
      *
     C                   ENDSR
     C**********************************************************
     C     ikkesad       BEGSR
     C**********************************************************
      * start finnes ikke i SADH
     C                   MOVE      ' '           SIST
     C                   MOVE      *blanks       SILKT
     C                   MOVE      *zeros        SITRM
     C                   MOVE      *zeros        SIDTG
     C                   MOVE      *blanks       SIKDLS
     C                   MOVE      *blanks       SILS
     C                   MOVE      *BLANKS       SITRID
     C                   MOVEL     HEHAWB        SITRID
     C                   MOVE      'N'           XTUR              1
     C     HEUR          IFEQ      'H'
     C     HEPRO         andne     *zeros
     C     HEPRO         CHAIN     TURER14                            97
     C                   move      TUTRMA        HETRM
     c                   endif
     C     HETRM         IFNE      0
     C                   Z-ADD     HETRM         SITRM
     C* VED TRANSPORTMÅTE 16 HENTES TOLLSTED FRA TURER
     C                   MOVE      'N'           XTUR              1
     C     HETRM         IFEQ      16
     C     HEPRO         CHAIN     TURER14                            97
     C  N97              MOVE      'J'           XTUR
     C                   ELSE
     C     HEPRO         IFNE      0
     C     HEPRO         CHAIN     TURER14                            97
     C  N97              MOVE      'J'           XTUR
     C                   END
     C                   END
     C                   END
     C                   MOVE      HETRC         SIKDC
     C                   IF        XTUR = 'N'
     C                   MOVE      HETRI         SILKT
     C                   ELSE
     C     TULK          IFNE      *BLANKS
     C                   EVAL      SILKT  = TULK
     C                   ELSE
     C                   MOVE      HETRI         SILKT
     C                   ENDIF
     C     TUBILN        IFNE      *BLANKS
     C                   EVAL      SITRID = TUBILN
     C                   ENDIF
     C                   END
     C                   ENDsr
     C**************************************************************************
     C     flere         begsr
     C**************************************************************************
     C     KEYDUM        KLIST
     C                   KFLD                    SIAVD
     C                   KFLD                    SITDN
     c                   move      'N'           headfler          1
      *
     C     FORIGE        SETLL     SADH6B
     C     FORIGE        READE     SADH6B                                 34
     C     *IN34         DOWEQ     '0'
     C     keyDUM        chain     HEADFDUM                           35
     C     *IN35         IFEQ      '1'
     C     sitdn         andge     0
      *
      * kun uten løpenummer
      *
     c     zung          ifeq      'J'
     c     SItll         andne     *blanks
     c                   GOTO      FLEREN
     c                   end
      *
      * kunn tolldek statuser
      *
     C                   IF        ZST1 <> *BLANKS
     C     1             DO        5             XN                3 0
     C                   IF        ((ZST(XN) <> *BLANKS) AND (ZST(XN) = SIST))
     c                   GOTO      FLEREN
     C                   ENDIF
     C                   ENDDO
     C                   ENDIF
      *
      *
      *  Sjekk omberegning
      *  overflow ?
     C     *IN02         IFEQ      '1'
     C     zps           andne     'S'
     C                   WRITE     SYIM03PB
     C                   MOVE      '0'           *IN02
     c                   move      'N'           headfler          1
     C                   END
      *
      *  Sjekk om heading skal skrives
     C     headfler      IFEQ      'N'
     C     zps           andne     'S'
     C                   WRITE     SYIM03TI0
     c                   move      'J'           headfler          1
     C                   END
      *
     C                   ADD       1             POSANT
     C                   ADD       SINTK         POSKLL
     C                   ADD       SIVKB         POSVKT
     C                   ADD       1             GODANT
     C                   ADD       SINTK         GODKLL
     C                   ADD       SIVKB         GODVKT
     C     ZPS           IFNE      'S'
     C                   WRITE     SYIM03TI
     C                   END
     C                   END
     C     FLEREN        TAG
     C     FORIGE        READE     SADH6B                                 34
     C                   END
     C                   endsr
     C**************************************************************************
     C     lesh18        begsr
     C**************************************************************************
     c     key18         klist
     c                   kfld                    zgnf
     c     key18         setll     head18
     c     *in44         doweq     '0'
     c                   read      head18                                 44
     c  N44hegn          comp      zgnt                               44
     C     *IN44         IFEQ      '0'
      * BRUDD GODSNR
     C     FORIGE        IFNE      *BLANKS
     C     HEGN          ANDNE     FORIGE
     C                   EXSR      BRUDDGN
     C                   END
     C                   EXSR      DETSR
     C                   MOVE      HEGN          FORIGE           15
     C                   ENDIF
     C                   ENDDO
      *
      * SLUTTBEHANDLING
      *
     c                   exsr      lastr
     C                   ENDSR
      *
     C**************************************************************************
     C     detsr         begsr
     C**************************************************************************
      *
      * TEST OM OPPDRAG SKAL MED PÅ LISTE
      *
     C                   EXSR      TESTOPD
     C     OK            IFEQ      'J'
      *Flere med lik Godsn / posisj. -> skriv bruddsum
     C     FORGNP        IFNE      LESGNP
     C     POSANT        IFGT      1
     C     ZPS           IFNE      'S'
     C                   WRITE     SYIM03PD
     C                   END
     C                   END
     C                   MOVE      *ZEROS        POSANT            7 0
     C                   MOVE      *ZEROS        POSKLL            7 0
     C                   MOVE      *ZEROS        POSVKT            9 0
     C                   END
      *NYTT Godsn  -> skriv bruddsum (MEN IKKE HVIS KUN EN POST)
     C     FORIGE        IFNE      *BLANKS
     C     HEGN          ANDNE     FORIGE
     C     GODANT        ANDNE     *ZEROS
     C     ZPS           IFNE      'S'
     C                   EXSR      flere
     C                   WRITE     SYIM03PE
     C                   END
     C                   MOVE      *ZEROS        GODANT            7 0
     C                   MOVE      *ZEROS        GODKLL            7 0
     C                   MOVE      *ZEROS        GODVKT            9 0
     C                   MOVE      *ZEROS        POSANT
     C                   MOVE      *ZEROS        POSKLL
     C                   MOVE      *ZEROS        POSVKT
     C                   END
      * SIDE BRUD  PR GODSNUMMER ??
     C     ZBRUDD        IFEQ      'J'
     C     FORIGE        IFNE      *BLANKS
     C     HEGN          IFNE      FORIGE
     C     ZPS           IFNE      'S'
     C                   WRITE     SYIM03PB
     C                   END
     C                   MOVE      '0'           *IN02
     C                   END
     C                   END
     C                   END
      * Overflow ?
     C     *IN02         IFEQ      '1'
     C     ZPS           IFNE      'S'
     C                   WRITE     SYIM03PB
     C                   END
     C                   MOVE      '0'           *IN02
     C                   END
      * Hent data fra SADH om det finnes
     C                   MOVE      *blanks       SIKDLS
     c                   move      *blanks       vaveko            3
     C     SADHK         CHAIN     SADH                               33
     C     *IN33         IFEQ      '1'
     C                   exsr      ikkesad
     C                   END
     C     *IN33         IFEQ      *OFF
      *
     c     trverb        ifeq      0
     c                   move      '(S)'         vaveko
     c                   z-add     sibel3        trverb
     c                   move      sival3        trverv
     c                   end
      *
     C                   MOVEL     SINAS         ZZNAS            20
     C                   MOVEL     SIADS1        ZZADS1           20
     C                   MOVEL     SINAK         ZZNAK            20
     C                   move      *blanks       syrg
     c     heknk         ifne      *zeros
     C     kunkey        chain     cundl01                            33
     C                   end
     C                   MOVEL     SINAS         ZFNAS            30
     C                   MOVEL     SIADS1        ZFADS1           30
     C                   MOVEL     SINAK         ZFNAK            30
     C     sikdls        ifeq      'A8'
     c                   move      '(A8)'        ZZNAK
     c                   end
     C                   MOVEL     SIKDLS        ZZKDLS            2
     C                   MOVEL     SILS          ZZLS              8
     C                   MOVEL     SIADK1        ZZADK1           20
     C                   MOVEL     SIADK3        ZZADK3           20
     C                   MOVEL     SIADK1        ZFADK1           30
     C                   MOVEL     SIADK3        ZFADK3           30
     c     HETLL         ifeq      *blanks
     C                   MOVEL     SITLE         HETLE
     C                   MOVEL     SITLL         HETLL
     c                   endif
     C                   ELSE
     C                   MOVEL     HENAS         ZZNAS
     C                   MOVEL     HEADS1        ZZADS1
     C                   MOVEL     HENAK         ZZNAK
     C                   MOVEL     HEADK1        ZZADK1
     C                   MOVEL     HEADK3        ZZADK3
     C                   MOVEL     HENAS         ZFNAS
     C                   MOVEL     HEADS1        ZFADS1
     C                   MOVEL     HENAK         ZFNAK
     C                   MOVEL     HEADK1        ZFADK1
     C                   MOVEL     HEADK3        ZFADK3
     C                   MOVEL     *BLANKS       SIDTG
     C                   END
      * fri søkevei
     C                   MOVE      *OFF          *IN68
     C     FRIKEY        CHAIN     FRISOK                             33
     c   33              move      *blanks       fssok
     c  N33              seton                                        68
      * Hent data fra TRACKF om det finnes (RUCKATTEST)
     C     TTKEY         CHAIN     TRACKL02                           33
     C     *IN33         IFEQ      '1'
     C                   MOVE      *BLANKS       TTDTG
     C                   END
     C     HETLL         IFNE      *BLANKS
     C     SIDTG         ANDEQ     *BLANKS
     C                   MOVE      TTDTG         SIDTG
     C                   END
      * Skriv ut detaljer
     C                   MOVE      *OFF          *IN66
     C                   MOVE      *OFF          *IN67
     C     HEGNN         IFNE      *BLANKS
     C                   MOVE      *ON           *IN66
     C                   ENDIF                                                  <HEGNN
      *
      * vareverdi
      *
     C     TRVERB        IFNE      *zeros
     C                   MOVE      *ON           *IN67
     C                   ENDIF                                                  vareverdi
N02  C                   EVAL      UFLAG = 0
     C     ZPS           IFNE      'S'
      *
     C                   CALL      'SYIM03RA'
     C                   PARM      HEAVD         UAVD              4 0
     C                   PARM      HEOPD         UOPD              7 0
     C                   PARM                    UNT               7 0
     C                   PARM                    UVKT              9 0
     C                   PARM                    UTLL             10
     C                   PARM                    UTLE              6
     C                   PARM                    UDTG             10
     C                   PARM                    UFLAG             1 0
     C                   IF        UFLAG = 1
S02  C*                  EVAL      POSKLL = POSKLL - HENT + SINTK
S02  C*                  EVAL      POSVKT = POSVKT - HEVKT + SIVKB
N02  C                   EVAL      HENT  = 0
N02  C                   EVAL      HEVKT = 0
     C                   EVAL      SINTK = UNT
     C                   EVAL      SIVKB = UVKT
     C                   EVAL      *IN41 = *ON
     C                   ELSE
     C                   EVAL      *IN41 = *OFF
     C                   END
      *
     C                   WRITE     SYIM03PC
     C                   ELSE
     C                   MOVEL     HEGN          HEGN16
     C                   MOVE      '.'           HEGN16
N03  C                   CLEAR                   trihr
     c                   movel     hegn          tign
N03  c     tign          chain     trih10                             36
     C                   WRITE     SYIM03FR
     C                   END
     C                   MOVEL     LESGNP        FORGNP           22
     C                   ADD       1             POSANT
N02  C                   IF        UFLAG = 0
     C                   ADD       HENT          POSKLL
     C                   ADD       HEVKT         POSVKT
N02  C                   ELSE
N02  C                   ADD       UNT           POSKLL
N02  C                   ADD       UVKT          POSVKT
N02  C                   END
     C                   ADD       1             GODANT
N02  C                   IF        UFLAG = 0
     C                   ADD       HENT          GODKLL
     C                   ADD       HEVKT         GODVKT
N02  C                   ELSE
N02  C                   ADD       UNT           GODKLL
N02  C                   ADD       UVKT          GODVKT
N02  C                   END
      *
     C                   MOVEL     HEGN          FORIGE           15
     C                   END
     C                   ENDSR
     C**************************************************************************
     C     BRUDDGN       begsr
     C**************************************************************************
      *
     C     KEYDUME       KLIST
     C                   KFLD                    SEAVD
     C                   KFLD                    SETDN
      *
      *
      * REEKSP. PÅ DETTE NR?                                           TFYLT
      *
     C     POSANT        IFGT      0
     C     FORIGE        SETLL     SAEH6B
     C     FORIGE        READE     SAEH6B                                 34
     C     *IN34         DOWEQ     '0'
     C     ZDTF8         IFLE      SEDT
     C     ZDTT8         ANDGE     SEDT
      *
      * kun uten løpenummer
      *
     c     zung          ifeq      'J'
     c     SEtll         andne     *blanks
     c                   GOTO      FLERENE
     c                   end
      *
      * oppdragstype
      *
     C     keyDUME       chain     HEADFDUM                           35
     c     *in35         ifeq      '0'
     C     DSOT          andne     *blanks
     c     heot          lookup    OT                                     33
     c   33              GOTO      FLERENE
     C                   END
      *
      * kunn tolldek statuser
      *
     C                   IF        ZST1 <> *BLANKS
     C     1             DO        5             XN                3 0
     C                   IF        ((ZST(XN) <> *BLANKS) AND (ZST(XN) = SEST))
     c                   GOTO      FLERENE
     C                   ENDIF
     C                   ENDDO
     C                   ENDIF
      *
      *
      * Overflow ?
     C     *IN02         IFEQ      '1'
     C     ZPS           IFNE      'S'
     C                   WRITE     SYIM03PB
     C                   MOVE      '0'           *IN02
     C                   END
     C                   END
     C     ZPS           IFNE      'S'
     C                   WRITE     SYIM03RE
     C                   END
     C                   END
     C     FLERENE       TAG
     C     FORIGE        READE     SAEH6B                                 34
     C                   END
     C                   END
     C                   ENDSR
     C**************************************************************************
     C     TESTOPD       begsr
     C**************************************************************************
     C                   MOVE      'J'           ok                1
      *
      * avdeling fra til
      *
     c     heavd         iflt      zavdf
     c     heavd         orgt      zavdt
     c                   move      'N'           ok
     C                   END
      *
      * dato fra til
      *
     c     hedtop        iflt      zdtf8
     c     hedtop        orgt      zdtt8
     c                   move      'N'           ok
     c                   end
      *
      * oppdragstype
      *
     C     DSOT          ifne      *blanks
     c     heot          lookup    OT                                     33
     c   33              move      'N'           OK
     C                   END
      *
      * kun uten løpenummer
      *
     c     zung          ifeq      'J'
     c     hetll         andne     *blanks
N04  c     zung          oreq      'J'
N04  c     hegnn         andne     *blanks
     c                   move      'N'           ok
     c                   end
      *
      * kunn tolldek statuser
      *
     C                   IF        ZST1 <> *BLANKS
     c     keydum        chain     sadh                               33
     c     *in33         ifeq      '0'
     C     1             DO        5             XN                3 0
     C                   IF        ((ZST(XN) <> *BLANKS) AND (ZST(XN) = SIST))
     C                   move      'N'           ok
     C                   END
     C                   ENDDO
     C                   END
     C                   END
      *
      * KUN LØPENR... FYLT UT (3 SISTE SIFFER AV GN..)
      *
     C     ZLNRF         IFNE      ' '
     C     HEGN13        IFLT      ZLNRF
     C     HEGN13        ORGT      ZLNRT
     C                   MOVE      'N'           OK
     C                   END
     C                   END
     C                   ENDSR
     C**************************************************************************
     C     lastr         begsr
     C**************************************************************************
      *Hadde siste Godsn/posisj. flere poster?? -> skriv bruddsum
     C     POSANT        IFGT      1
     C     ZPS           IFNE      'S'
     C                   WRITE     SYIM03PD
     C                   END
     C                   END
     C     ZPS           IFNE      'S'
     c     forige        andne     *blanks
     C                   EXSR      flere
     C                   WRITE     SYIM03PE
     C                   END
     C     ZPS           IFNE      'S'
     C                   WRITE     SYIM03PØ
     C                   END
     c                   endsr
