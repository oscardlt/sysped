      *
      *****************************************************************
      *** PROGRAM SKAL VEDLIKEHOLDE INFORMASJON (SJØ-TRANSPORT)     ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER          ***
      *** LEDIGE INDIKATORER 01-03 06-11 13-26                      ***
      *** LEDIGE INDIKATORER 27-29 31 32 34-93                      ***
      *****************************************************************
      *
     FINFO      UF A E           K DISK
     FCDINFO    IF   E           K DISK
     FCDTRAF    IF   E           K DISK
     FKODTA     IF   E           K DISK
     FKODTLK    IF   E           K DISK
     FFIRM      IF   E           K DISK
     FCUNDL01   IF   E           K DISK
     FSYSJ04FM  CF   E             WORKSTN
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D                 DS
     D  WSTX1                  1     62
     D  WSTX2                 63    124
     D  WSTX3                125    186
     D  WSTX4                187    248
     D  WSTX5                249    310
     D  WSTX6                311    372
     D  WSTX7                373    434
     D  WSTX8                435    496
     D  WSTX9                497    558
     D  WSTX10               559    620
     D  WSTX11               621    682
     D  WSTX12               683    744
     D  WSTX13               745    806
     D  WSTX14               807    868
     D  WSTX15               869    930
     D  WSTX16               931    992
     D  WSTX17               993   1054
     D  WSTX18              1055   1116
     D  TX                     1   1116
     D                                     DIM(18)
     D                 DS
     D  INDTÅ                  1      4  0
     D  INDTM                  5      6  0
     D  INDTD                  7      8  0
     D  INDT                   1      8  0
     D                 DS
     D  XDTÅ                   1      4  0
     D  XDTM                   5      6  0
     D  XDTD                   7      8  0
     D  XDT                    1      8  0
     D                 DS
     D  WSDTD                  1      2  0
     D  WSDTM                  3      4  0
     D  WSDTÅ                  5      6  0
     D  WSDT                   1      6  0
     C*
     C                   EXSR      INIT
     C     UFLAGG        IFEQ      2
     C                   GOTO      START2
     C                   END
     C*
     C     START         TAG
     C  N30
     CANN04              Z-ADD     8             LINNBR
     C  N30
     CANN04              Z-ADD     40            POSNBR
     C                   EXSR      KDTFMT
     C                   EXFMT     BILDE01
     C   03              Z-ADD     3             UFLAGG
     C   12              Z-ADD     1             UFLAGG
     C   03
     COR 12              GOTO      SLUTT
     C                   EXSR      KDTFMT
     C                   SETOFF                                       30
     C                   MOVEA     '000000'      *IN(94)
     C*
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C                   Z-ADD     WSSUBN        XRECNO            4 0
     C*
     C* SEARCH CUSTOMER
     C   04              DO
     C                   EXSR      SØKDTA
     C                   GOTO      START
     C                   END
     C   05UKODE         IFEQ      2
     C                   GOTO      SLUTT
     C                   ELSE
     C                   EXSR      SØKINF
     C     UFLAGG        IFEQ      1
     C                   GOTO      START
     C                   END
     C                   END
     C*
     C     START2        TAG
     C                   EXSR      TESTB1
     C   30              GOTO      START
     C*
     C                   EXSR      GETINF
     C*
     C                   EXFMT     BILDE02
     C                   Z-ADD     0             UFLAGG
     C  N12              EXSR      OPDINF
     C     UKODE         IFEQ      1
     C                   GOTO      START
     C                   END
     C*
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
     C*
     C***********************************************
     C     TESTB1        BEGSR
     C***********************************************
     C     KODTAK        CHAIN     KODTA                              99
     C   99              DO
     C                   MOVE      'SPA0001'     MSGNR
     C                   SETON                                        30
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     40            POSNBR
     C                   END
     C*
     C     KUND1K        CHAIN     CUNDL01                            98
     C  N98              MOVEL     KNAVN         WSNAR
     C   98
     CANN30              DO
     C                   MOVE      'SPK0121'     MSGNR
     C                   SETON                                        30
     C                   Z-ADD     8             LINNBR
     C                   Z-ADD     40            POSNBR
     C                   END
     C*
     C     TRAFK         CHAIN     CDTRAF                             97
     C  N97CTAKT         COMP      'I'                                    97
     C  N97              MOVEL     CTFT          WSNATR
     C   97
     CANN30              DO
     C                   MOVE      'SPT0102'     MSGNR
     C                   SETON                                        30
     C                   Z-ADD     10            LINNBR
     C                   Z-ADD     40            POSNBR
     C                   END
     C*
     C     KODLKK        CHAIN     KODTLK                             96
     C  N96              MOVEL     KLKNVN        WSNALK
     C   96
     CANN30              DO
     C                   MOVE      'SPL0001'     MSGNR
     C                   SETON                                        30
     C                   Z-ADD     12            LINNBR
     C                   Z-ADD     40            POSNBR
     C                   END
     C*
     C                   MOVE      WSDTÅ         UÅÅ               2 0
     C                   MOVE      WSDTM         UMM               2 0
     C                   MOVE      WSDTD         UDD               2 0
     C                   CALL      'TESTDATO'
     C                   PARM                    UDD
     C                   PARM                    UMM
     C                   PARM                    UÅÅ
     C                   PARM                    UFLAGG            1 0
     C     UFLAGG        COMP      1                                      95
     C   95
     CANN30              DO
     C                   MOVE      'SPD0001'     MSGNR
     C                   SETON                                        30
     C                   Z-ADD     14            LINNBR
     C                   Z-ADD     40            POSNBR
     C                   END
     C*
     C     WSKD          CHAIN     CDINFO                             94
     C   94
     CANN30              DO
     C                   MOVE      'SPD0001'     MSGNR
     C                   SETON                                        30
     C                   Z-ADD     16            LINNBR
     C                   Z-ADD     40            POSNBR
     C                   END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     GETINF        BEGSR
     C***********************************************
     C                   SETOFF                                       33
     C                   MOVE      *BLANKS       TX
     C     WSDTÅ         IFGT      50
     C     WSDTÅ         ADD       1900          XDTÅ
     C                   ELSE
     C     WSDTÅ         ADD       2000          XDTÅ
     C                   END
     C                   MOVE      WSDTM         XDTM
     C                   MOVE      WSDTD         XDTD
     C                   MOVE      *ZEROS        YLI
     C     INFOK         SETLL     INFO
     C     STGET         TAG
     C     WSAVD         READE     INFO                                   33
     C  N33              UPDATE    INFOR
     C  N33WSKNR         COMP      INKNR                              3333
     C  N33WSTR          COMP      INTR                               3333
     C  N33WSLK          COMP      INLK                               3333
     C  N33XDT           COMP      INDT                               3333
     C  N33WSKD          COMP      INKD                               3333
     C  N33              DO
     C                   MOVE      INLI          XN                3 0
     C                   MOVE      INTXT         TX(XN)
     C                   GOTO      STGET
     C                   END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     OPDINF        BEGSR
     C***********************************************
     C                   MOVE      WSAVD         INAVD
     C                   MOVE      WSKNR         INKNR
     C                   MOVE      WSTR          INTR
     C                   MOVE      WSLK          INLK
     C                   MOVE      XDT           INDT
     C                   MOVE      WSKD          INKD
      *
     C     1             DO        18            YLI
     C     INFOK         CHAIN     INFO                               33
     C     TX(YLI)       IFEQ      *BLANKS
     C  N33              DELETE    INFOR
     C                   ELSE
     C                   MOVE      YLI           INLI
     C                   MOVE      TX(YLI)       INTXT
     C   33              WRITE     INFOR
     C  N33              UPDATE    INFOR
     C                   END
     C                   END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     SØKINF        BEGSR
     C***********************************************
     C                   Z-ADD     1             UFLAGG
     C                   CALL      'SYSJ05'
     C                   PARM                    UKODE
     C                   PARM                    WSKNR
     C                   PARM                    WSTR
     C                   PARM                    WSLK
     C     WSDT          PARM      WSDT          UDT               6 0
     C                   PARM                    WSKD
     C                   PARM                    UFLAGG
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     SØKKUN        BEGSR
     C***********************************************
     C                   MOVE      *BLANKS       USØKKN           30
     C                   CALL      'SOEKKUN'
     C                   PARM                    WSKNR
     C                   PARM                    FIFIRM
     C                   PARM                    USØKKN           30
     C                   Z-ADD     0             UFLAGG
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     SØKDTA        BEGSR
     C***********************************************
     C                   Z-ADD     1             UFLAGG
     C     LINNBR        IFEQ      8
     C     POSNBR        ANDGE     40
     C     POSNBR        ANDLE     47
     C                   EXSR      SØKKUN
     C                   Z-ADD     10            LINNBR
     C                   Z-ADD     40            POSNBR
     C                   GOTO      SLSØK
     C                   END
     C*
     C     LINNBR        IFEQ      10
     C     POSNBR        ANDGE     40
     C     POSNBR        ANDLE     41
     C                   CALL      'SYSJ17A'
     C                   PARM                    WSKNR
     C                   PARM                    WSTR
     C                   Z-ADD     12            LINNBR
     C                   Z-ADD     40            POSNBR
     C                   GOTO      SLSØK
     C                   END
     C*
     C     LINNBR        IFEQ      12
     C     POSNBR        ANDGE     40
     C     POSNBR        ANDLE     41
     C                   CALL      'SYFI10L'
     C                   PARM                    WSLK
     C                   PARM                    USPED
     C                   Z-ADD     14            LINNBR
     C                   Z-ADD     40            POSNBR
     C                   GOTO      SLSØK
     C                   END
     C*
     C     LINNBR        IFEQ      16
     C     POSNBR        ANDGE     40
     C     POSNBR        ANDLE     42
     C                   Z-ADD     1             UFLAGG
     C                   CALL      'SYSJ03'
     C                   PARM                    UFLAGG
     C                   PARM                    WSKD
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     40            POSNBR
     C                   GOTO      SLSØK
     C                   END
     C*
     C                   MOVE      'DIV1037'     MSGNR
     C                   SETON                                        30
     C*
     C     SLSØK         ENDSR
     C*
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     KODTAK        KLIST
     C                   KFLD                    YAUNI
     C                   KFLD                    WSAVD
     C     KUND1K        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    WSKNR
     C     TRAFK         KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    WSKNR
     C                   KFLD                    WSTR
     C     KODLKK        KLIST
     C                   KFLD                    YLKUNI
     C                   KFLD                    WSLK
     C     INFOK         KLIST
     C                   KFLD                    WSAVD
     C                   KFLD                    WSKNR
     C                   KFLD                    WSTR
     C                   KFLD                    WSLK
     C                   KFLD                    XDT
     C                   KFLD                    WSKD
     C                   KFLD                    YLI
     C*
     C     *ENTRY        PLIST
     C* UKODE = 1  --->  FRA SYSJ04CL
     C* UKODE = 2  --->  FRA SYSJ05
     C                   PARM                    UKODE             1 0
     C                   PARM                    WSAVD
     C                   PARM                    WSKNR
     C                   PARM                    WSTR
     C                   PARM                    WSLK
     C     WSDT          PARM                    UDT
     C                   PARM                    WSKD
     C* UFLAGG = 1  -->  LAGE INFORMASJONSTEKST
     C* UFLAGG = 2  -->  ENDREE INFORMASJONSTEKST
     C                   PARM                    UFLAGG            1 0
     C*
     C                   MOVE      'J'           USPED             1
     C                   MOVE      'A'           YAUNI             1
     C                   MOVE      'LK '         YLKUNI            3
     C                   MOVE      *ZEROS        YLI               3 0
     C                   MOVE      *ZEROS        INDT
     C                   MOVE      *ZEROS        XDT
     C*
     C*
     C                   MOVE      *BLANKS       FIFIRM
     C     FIFIRM        SETLL     FIRM
     C                   READ      FIRM                                   33
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     KDTFMT        BEGSR
     C***********************************************
     C                   MOVE      WSDTD         UDT1              2 0
     C                   MOVE      WSDTM         UDT2              2 0
     C                   MOVE      WSDTÅ         UDT3              2 0
     C                   CALL      'DATOFMT'
     C                   PARM                    UDT1
     C                   PARM                    UDT2
     C                   PARM                    UDT3
     C                   MOVE      UDT1          WSDTD
     C                   MOVE      UDT2          WSDTM
     C                   MOVE      UDT3          WSDTÅ
     C*
     C                   ENDSR
     C*
