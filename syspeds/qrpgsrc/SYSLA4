      *********************************************************************
      * SPØRRING PÅ LAGERPOSTER
      * PROGRAM BEHANDLER INPUT FRA SYSLA1 OG VISER UTVALGT HEADLING
      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/SYSLA4 MODULE(*LIBL/SYSLA4
CRTPGM*        *LIBL/CHECKUSR *LIBL/INCGI) ACTGRP(CGI) AUT(*USE)
      *
      *********************************************************************
      * MAIN PROGRAM FLOW
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
      * Boats for sale data base file (LF on BOATSALE by boat serial number)
     FLLLINE    if   e           k disk    USROPN
     FLLHEAD    if   e           k disk    USROPN
     FBRIDF     UF   E           K DISK    USROPN
     FBRPARF    if   e           k disk    usropn
      *--------------------------------------------------------------------
      * Prototype definitions and standard system API error structure
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      * Variables common to CGI programs
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
      * VARIABLES SPECIFIC TO THIS PROGRAM
      *--------------------------------------------------------------------
      * Variables received from the input string
     D lng             s              2
     D USER            s             10
     D UsrSpcName      s             10
     D KUNDEREF1       S             50
     D KUNDEREF2       S             50
     D KUNDEREF3       S             50
      * Variables used to load the external html source member
     D Lib             s             10
     D Fn              s             10
     D Mbr             s             10
      * Boats serial numbers received in the input string
     D ItemCount       s             10i 0
     D SernoC          s             10a
     D i               s             10i 0
     D Spaces          s             12a   inz('&nbsp;&nbsp;')
     D PGM             C                   CONST('SYSPED/CHECKUSR')
      *
     D                 DS
     D  ALLLOK                 1     26
     D  ALLLOK1                1      5
     D  ALLLOK2                7     26
      *--------------------------------------------------------------------
      * Prolog common to CGI programs
      * -Receive input from the remote browser
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prolog3
      *=====================================================================
      * MAIN PROCESSING LINE
      *=====================================================================
      * Get program start time for calculating execution time
     C                   time                    timedata1
      * Parse input string
     C                   exsr      Parse
      *
     C                   EXSR      INIT
     C                   CALLB     PGM
     C                   PARM                    BIBRID
     C                   PARM                    UsrSpcName
     C                   PARM                    UINN              1
     C                   IF        UINN = 'N'
     C                   GOTO      SLUTT
     C                   END
     C     USER          CHAIN     BRIDF                              50
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVE      WDT           BIDTF
     C                   MOVE      WTM           BITIDF
     C                   UPDATE    BRIDFR
      * Set output skeleton html member name
     C                   exsr      SetSkl
      * Read skeleton output html into core
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
      * Retrieve environment variables
     C                   exsr      RtvEnvVar
      *
      * Issue html section /$top
     C                   exsr      SetTop
     C                   callp     wrtsection('top')
      *------------------
      * Retrieve number of boats to be shown
     C                   eval      ItemCount = ZhbGetVarCnt('serno')
     C                   MOVE      'N'           XHEADJA           1
      * Show the requested boats
     C                   eval      i =1
     C     i             DOWlE     ItemCount
     C                   eval      SernoC = ZhbGetVar('serno':i)
     C                   MOVEL     SernoC        LHMN
     C                   MOVE      SERNOC        LLVN
     C     XHEADJA       IFEQ      'N'
     C     LH00K         CHAIN     LLHEAD                             60
     C                   ENDIF
     C     LL00K         CHAIN     LLLINE                             60
     C     *in60         ifeq      '0'
     C                   exsr      SetFound
     C                   callp     wrtsection('found')
     C                   else
     C                   callp     wrtsection('notfound')
     C                   endif
     C                   eval      i = i +1
     C                   ENDDO
      * If no boats selected
     C     ItemCount     ifeq      0
     C                   callp     wrtsection('noselect')
     C                   endif
      * Send output buffer and quit
     C     SLUTT         TAG
     C                   exsr      Exit
      *=====================================================================
      * Send output buffer and quit
      *=====================================================================
     C     Exit          begsr
      * Compute and display run time
     C                   time                    timedata2
     C     timedata2     subdur    timedata1     ms:*ms
     C                   eval      sec = ms / 1000000
     C                   callp     updhtmlvar('runtime':
     C                             %trim(%editw(sec:'     0 .   ')))
     C                   callp     wrtsection('endhtml')
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      *
     C                   close     LLLINE
     C                   close     LLHEAD
     C                   close     BRIDF
     C                   close     BRPARF
     C                   eval      *inlr = *on
      *
     C                   return
     C                   endsr
      *=====================================================================
      * Parse input string
      *=====================================================================
     C     Parse         begsr
     C                   eval      lng     = zhbgetvar('lng')
     C                   eval      USER    = zhbgetvar('USER')
     C                   EVAL      UsrSpcName  = zhbgetvar('UsrSpcName')
     C                   endsr
      *=====================================================================
      * Set html output variables for section /$top
      *=====================================================================
     C     SetTop        begsr
      * Repeat national language for the next invocation
     C                   callp     updhtmlvar('LNG':lng:
     C                             InitHTMLVars)
      * Repeat background color for the next invocation
     C                   callp     updhtmlvar('TXTCOLOR':'black')
     C                   callp     updhtmlvar('BOLD':' ')
     C                   callp     updhtmlvar('BGCOLOR':BIFARG)
     C                   callp     updhtmlvar('LINK':'0000FF')
     C                   callp     updhtmlvar('VLINK':'FF0000')
     C                   callp     updhtmlvar('ALINK':'00FF00')
     C                   callp     updhtmlvar('KUNDEREF1':KUNDEREF1)
     C                   callp     updhtmlvar('KUNDEREF2':KUNDEREF2)
     C                   callp     updhtmlvar('KUNDEREF3':KUNDEREF3)
      * Protocol
     C                   callp     updHtmlVar('PROTOCOL':S_Protocol)
BODY C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updhtmlvar('USER':USER)
     C                   callp     updHtmlVar('UsrSpcName':UsrSpcName)
     C                   endsr
      *=====================================================================
      * Set html output variables for section /$found (display a boat)
      *=====================================================================
     C     SetFound      begsr
      *
     C                   callp     updhtmlvar('USER':USER)
     C                   callp     updHtmlVar('UsrSpcName':UsrSpcName)
      * Sett inn i HTMLvariabler
     C                   callp     updHtmlVar('LHBNA':LHBNA)
     C                   callp     updhtmlvar('LHTDT':
     C                             %trim(%editw(LHTDT:'    .  .  ')))
     C                   callp     updhtmlvar('LHPAK':LHPAK)
     C                   callp     updhtmlvar('LLANR':LLANR)
     C                   callp     updhtmlvar('LLVB':LLVB)
     C                   MOVEL     LLLOK         ALLLOK1
     C                   MOVE      LLLOK         ALLLOK2
     C                   callp     updhtmlvar('LLLOK':ALLLOK)
     C                   callp     updhtmlvar('LLNTLK':
     C                             %trim(%editc(LLNTLK:'Z')))
     C                   callp     updhtmlvar('LLPRI':
     C                             %trim(%editc(LLPRI:'4')))
     C                   callp     updhtmlvar('LLSER':LLSER)
     C                   callp     updhtmlvar('LLANT0':
     C                             %trim(%editc(LLANT0:'Z')))
     C                   callp     updhtmlvar('LLWNT0':
     C                             %trim(%editc(LLWNT0:'Z')))
     C                   callp     updhtmlvar('LLANTL':
     C                             %trim(%editc(LLANTL:'Z')))
     C                   callp     updhtmlvar('LLWNTL':
     C                             %trim(%editc(LLWNTL:'Z')))
     C                   callp     updhtmlvar('LLANT1':
     C                             %trim(%editc(LLANT1:'Z')))
     C                   callp     updhtmlvar('LLWNT1':
     C                             %trim(%editc(LLWNT1:'Z')))
     C                   callp     updhtmlvar('LLANT2':
     C                             %trim(%editc(LLANT2:'Z')))
     C                   callp     updhtmlvar('LLWNT2':
     C                             %trim(%editc(LLWNT2:'Z')))
     C                   callp     updhtmlvar('LLANT3':
     C                             %trim(%editc(LLANT3:'Z')))
     C                   callp     updhtmlvar('LLWNT3':
     C                             %trim(%editc(LLWNT3:'Z')))
     C                   callp     updhtmlvar('LLWVEK':
     C                             %trim(%editc(LLWVEK:'3')))
     C                   callp     updhtmlvar('LLWKUB':
     C                             %trim(%editc(LLWKUB:'3')))
      *
      *
     C                   endsr
      *=====================================================================
      * Set html output skeleton member before its read into memory
      *=====================================================================
     C     SetSkl        begsr
     C                   eval      lng = uppify(lng)
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC' + lng
     C                   eval      Mbr = 'SYSLA4'
     C                   CAT       XSPRÅK:0      MBR
     C                   endsr
      *=====================================================================
      *  Retrieve environment variables
      *=====================================================================
     C     RtvEnvVar     begsr
      * Use getenvp to get this server's protocol
     C                   eval      S_Protocol =getenv('SERVER_PROTOCOL':
     C                             qusec)
      *
     C                   endsr
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
     C                   OPEN      BRIDF
     C     USER          CHAIN(N)  BRIDF                              50
     C     BILIBL        ifeq      *blanks
     C                   CALLB     'INCGI'
     C                   PARM                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
      *
     C                   open      LLLINE                               50
     C                   open      LLHEAD                               50
     C                   open      BRPARF                               50
     c     LL00K         klist
     c                   kfld                    LHMN
     c                   kfld                    LLVN
     c     LH00K         klist
     c                   kfld                    LHMN
     C     BRPARFK       klist
     C                   kfld                    PABRID
     C                   kfld                    PAKUNR
     C                   kfld                    PAID
      * HENT LEDERTEKST
     C                   MOVE      BIBRID        PABRID
     C                   MOVE      *ZEROS        PAKUNR
     C                   MOVEL     'LLRF1'       PAID
     C     BRPARFK       CHAIN     BRPARF                             50
     C                   IF        *IN50
     C                   MOVE      '*KUNDFT*  '  PABRID
     C                   MOVE      BIFIRM        PABRID
     C                   MOVE      BIKUNR        PAKUNR
     C     BRPARFK       SETLL     BRPARF
     C     BRPARFK       READE     BRPARF                                 50
     C                   IF        *IN50
     C                   MOVE      *ZEROS        PAKUNR
     C     BRPARFK       SETLL     BRPARF
     C     BRPARFK       READE     BRPARF                                 50
     C                   END
     C                   END
     C                   IF        *IN50
     C                   EVAL      KUNDEREF1 = 'Kunderef.1'
     C                   ELSE
     C                   EVAL      KUNDEREF1 = PAVRDA
     C                   END
      *
     C                   MOVE      BIBRID        PABRID
     C                   MOVE      *ZEROS        PAKUNR
     C                   MOVEL     'LLRF2'       PAID
     C     BRPARFK       CHAIN     BRPARF                             50
     C                   IF        *IN50
     C                   MOVE      '*KUNDFT*  '  PABRID
     C                   MOVE      BIFIRM        PABRID
     C                   MOVE      BIKUNR        PAKUNR
     C     BRPARFK       SETLL     BRPARF
     C     BRPARFK       READE     BRPARF                                 50
     C                   IF        *IN50
     C                   MOVE      *ZEROS        PAKUNR
     C     BRPARFK       SETLL     BRPARF
     C     BRPARFK       READE     BRPARF                                 50
     C                   END
     C                   END
     C                   IF        *IN50
     C                   EVAL      KUNDEREF2 = 'Kunderef.2'
     C                   ELSE
     C                   EVAL      KUNDEREF2 = PAVRDA
     C                   END
      *
     C                   MOVE      BIBRID        PABRID
     C                   MOVE      *ZEROS        PAKUNR
     C                   MOVEL     'LLRF3'       PAID
     C     BRPARFK       CHAIN     BRPARF                             50
     C                   IF        *IN50
     C                   MOVE      '*KUNDFT*  '  PABRID
     C                   MOVE      BIFIRM        PABRID
     C                   MOVE      BIKUNR        PAKUNR
     C     BRPARFK       SETLL     BRPARF
     C     BRPARFK       READE     BRPARF                                 50
     C                   IF        *IN50
     C                   MOVE      *ZEROS        PAKUNR
     C     BRPARFK       SETLL     BRPARF
     C     BRPARFK       READE     BRPARF                                 50
     C                   END
     C                   END
     C                   IF        *IN50
     C                   EVAL      KUNDEREF3 = 'Kunderef.3'
     C                   ELSE
     C                   EVAL      KUNDEREF3 = PAVRDA
     C                   END
      *
     C                   MOVE      BIBRID        PABRID
     C                   MOVE      *ZEROS        PAKUNR
     C                   MOVEL     'SPRÅK'       PAID
     C     BRPARFK       chain     BRPARF                             33
     C                   IF        (*IN33 OR (PAVRDA <> 'EN'))
     C                   MOVE      *BLANKS       XSPRÅK            2
     C                   ELSE
     C                   MOVE      'EN'          XSPRÅK            2
     C                   END
      *
     C                   endsr
