     H debug(*yes)
      *_______________________________________________________________
      * Utskrift av samlefaktura - "KOSTNADSOVERSLAG"(A4-SPESIFIKASJ.)
      *"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 JOV PTF10 avvikende fakturaadr(vadr -1) - Skannesentral el.l. - LÅNER XDINV / ind 48
10XXX * 02 JOV PTF10 merker linjer for at de er med på faktura
10XXX * 03 SH  PTF10 kostnadsoverslag til arkiv
10XXX * 04 SH  PTF10 arunde settes til fakturanummer
10XXX * 05 SH  PTF11 ny layout m.h.t. dokuv BRINGTMS-PROSJEKT
10XXX * 06 SH  PTF11 unngå kostnadslinjer på spesifikasjon
10XXX * 07 JOV PTF11 IKKE skipp ny arkivering dersom den finnes (endret beløp/ferd.meldt på ny?)
10XXX * 08 sh  PTF11 test om fra arkivering
10XXX * 09 sh  PTF11 Rettelse til ptf 07 Ikke skriv ny hvis på samme faktura
********************************************************************
      *
N05  FDOKUFV    IF   E           K DISK
N05  FDOKUFM    IF   E           K DISK
N05  FKODTW     IF   E           K DISK
n03  FARKTXT    IF   E           K DISK
     FFIRMARC   IF   E           K DISK
s09  F*RKIVL03  IF A E           K DISK
N09  FARKIVL03  IF A E           K DISK
s09  F*RKIVL03  O  A E           K DISK
     FKODTFM    IF   E           K DISK
     FHEADF     IF   E           K DISK
     FFAK8      IF   E           K DISK
     FKODSAM    IF   E           K DISK
     FKODTGE    IF   E           K DISK
     FKODTLK    IF   E           K DISK
     FKODTG2    IF   E           K DISK
     FKODTOTY   IF   E           K DISK
     FKODTFR    IF   E           K DISK
     FKODTH     IF   E           K DISK
     FFRISOK    IF   E           K DISK
     FKODFRI    IF   E           K DISK
     FKODWL01   IF   E           K DISK
     FHODE      IF   E           K DISK
     FFIRM      IF   E           K DISK
s02  F*REETXT2  IF   E           K DISK
N02  FFREETXT2  UF   E           K DISK
     FCUNDL01   IF   E           K DISK
N01  FVADR      IF   E           K DISK
     FSORT      UF A E           K DISK
     FSYFA27PP  O    E             PRINTER OFLIND(*IN66)
     F                                     USROPN
     D HO              S             80    DIM(10)
     D HE              S             80    DIM(10)
     D HT              S             80    DIM(10)
N03  D                SDS
N03  D  ZUSER                254    263
n05  D                 DS
n05  D  KOWXXX                 1     41
N05  D  KOWVLF                 2      2
     D                 DS
     D  DFS                    1     10
     D                                     DIM(10)
     D  FSDOKK                 1     10
     D                 DS
     D  HEDTÅ                  1      4  0
     D  HEDTM                  5      6  0
     D  HEDTD                  7      8  0
     D  HEDTOP                 1      8  0
     D                 DS
     D  XDTD                   1      2  0
     D  XDTM                   3      4  0
     D  XDTÅ                   5      6  0
     D  XDT                    1      6  0
     D                 DS
     D  IDAGÅ                  3      4  0
     D  IDAGM                  5      6  0
     D  IDAGD                  7      8  0
     D  IDAG8                  1      8  0
     D  IDAG                   3      8  0
     D UCMD            DS          1024
     D                 DS
     D XCMD11          C                   CONST('OVRPRTF -
     D                                     FILE(SYFA27PP) -
     D                                     COPIES(')
     D                 DS
     D  SOLNR                  1      3  0
     D  KG2SRT                 1      3  0
     D                 DS
     D  SOAVD                  4      7  0
     D  FAAVD                  4      7  0
     D  SOOPD                  8     14  0
     D  FAOPD                  8     14  0
     D  SOSK                  15     15
     D  FASK                  15     15
     D  SOVK                  16     18
     D  FAVK                  16     18
     D  SOVT                  19     38
     D  FAVT                  19     38
     D  XFAVT1                19     19
     D  XFAVT2                20     38
     D  SOVAL                 39     41
     D  FAVAL                 39     41
     D  SOBELV                42     54  2
     D  FABELV                42     54  2
     D  SOKDM                 55     55
     D  FAKDM                 55     55
     D  SOBELN                56     68  2
     D  FABELN                56     68  2
     D  SOSAMM                69     69
     D  FACD11                69     69
     D                 DS
     D  HOHT1                  1     80
     D  HOHT2                 81    160
     D  HOHT3                161    240
     D  HOHT4                241    320
     D  HOHT5                321    400
     D  HOHT6                401    480
     D  HOHT7                481    560
     D  HOBT1                561    640
     D  HOBT2                641    720
     D  HOBT3                721    800
     D  HP                     1    800
     D                                     DIM(10)
     D                 DS
     D  ADR3                   1     24
     D  ADR31                  1     24
     D  ADR3B                  1     24
     D  ADR3C                  1     24
      * Arrays - oppdragsnivå - for variabel moms
     D  WSMVAK         S              1    DIM(11)                              Momskoder
     D  WSMVAP         S              4  2 DIM(11)                              %-sats
     D  WSGRL          S             13  2 DIM(11)                              Grunnlag
     D  WSMVA          S              9  2 DIM(11)                              Mva-bel.
      * Benyttet momssats dobbeltdefinert i DS for å forenkle logikk i pgm
     D                 DS
     D  MVAPRO                 1      4  2                                      eks: 25,00
     D  FITAXB                 1      4  4                                      eks: 0,2500
n03  D                 DS
n03  D  XULTID                 1     14  0
n03  D  XULTM                  1      6  0
n03  D  XULDD                  7      8  0
n03  D  XULMM                  9     10  0
n03  D  XULÅÅ                 11     14  0
n03  D                 DS
n03  D  ULÅÅ                   1      4  0
n03  D  ULMM                   5      6  0
n03  D  ULDD                   7      8  0
n03  D  ULDT                   1      8  0
     D                UDS
     D  UAVD                  15     18  0
n03  D  UARKIV                48     48
     IKODSAMR
     I              SANORS                      KGENOT
     I              SAENGE                      KGEENT
     I              SATYSK                      KG2TYT
      *
     C                   EXSR      INIT
     C                   EXSR      MAIN
n38  c     uarkiv        ifne      'A'
n03  c                   exsr      arkivsr
n08  c                   end
      *
     C                   SETON                                        LR
     C                   RETURN
      *
      *////////////////////////////////////////////////////////////
     C     INIT          BEGSR
      *////////////////////////////////////////////////////////////
     C     *ENTRY        PLIST
     C                   PARM                    XKUNDE
     C                   PARM                    XAVD
     C                   PARM                    XOPD
     C                   PARM                    XFAKT
     C                   PARM                    XVAL
     C                   PARM                    XANKOK            1
     C                   PARM                    XKOHE            28
     C                   PARM                    XFADAT            6 0
     C                   PARM                    XFFDAT            6 0
     C                   PARM                    XTYPE             1
     c*                  dump
N03   *
N03   * arkiv  ind 80
N03   *
N03  c     XANKOK        ifeq      'A'
N03  c                   move      '1'           XANKOK
N03  c                   seton                                        80
N03  c                   end
      *(XTYPE:O=PÅ OPPDRAGSTIDSP./S=SAMLEF.TIDSP./P=PRØVE/K=KOPI)
      *         29-ON                             27+28   27+N28
      *(27 BRUKES IKKE PÅ SPESIFIKASJON - INGEN SPES. MERKING AV KOPI)
     C                   SELECT
     C     XTYPE         WHENEQ    'O'
     C                   MOVEA     '001'         *IN(27)
     C     XTYPE         WHENEQ    'P'
     C                   MOVEA     '110'         *IN(27)
     C     XTYPE         WHENEQ    'K'
     C                   MOVEA     '100'         *IN(27)
     C                   OTHER
     C                   MOVEA     '000'         *IN(27)
     C                   ENDSL
     C                   MOVE      *ZEROS        SOLNR
      * 26 ON=Standard  headingtekst
     C     XKOHE         COMP      *BLANKS                            2626
      *
     C                   MOVE      'X'           INIIND            1
      *
      *
     C     *LIKE         DEFINE    KUNDNR        XKUNDE
     C     *LIKE         DEFINE    HEAVD         XAVD
     C     *LIKE         DEFINE    HEOPD         XOPD
      *
N05  C     DOKUVK        KLIST
N05  C                   KFLD                    YRI
N05  C                   KFLD                    HEAVD
N05  C                   KFLD                    HEOPD
N05  C                   KFLD                    FVFBNR
N05  C                   z-add     1             FVFBNR
N05  C                   MOVE      'F'           YRI               1
N05   *
N05  C     DOKUFMKEY     KLIST
N05  C                   KFLD                    YRI
N05  C                   KFLD                    HEAVD
N05  C                   KFLD                    HEOPD
N05  C                   KFLD                    FVFBNR
N05  C                   KFLD                    FVLINR
N05  C     KODTWK        KLIST
N05  C                   KFLD                    KOWUNI
N05  C                   KFLD                    UAVD
N05  C                   MOVE      'W'           KOWUNI
N05  C     KODTWK        CHAIN     KODTW                              33
     C     HEADFK        KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
      *
     C     FAK8K         KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C                   KFLD                    XFAKT
     C                   KFLD                    KUNDNR
     C                   KFLD                    XVAL
      *
     C     FREETK        KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C                   KFLD                    XXSKKO            1
S02  C*                  KFLD                    XFAKT
N02  C                   KFLD                    FAKNUL            7 0
     C                   MOVEL     'LK'          YLKUNI            3
     C     LKKEY         KLIST
     C                   KFLD                    YLKUNI
     C                   KFLD                    SYLAND
     C     SORTK         KLIST
     C                   KFLD                    YSOLNR
     C                   KFLD                    YSAMM
     C                   KFLD                    YSOAVD
     C                   KFLD                    YSOOPD
     C                   KFLD                    YSOSK
     C     KODWKY        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    WMVAKD
     C                   KFLD                    FAKDM
     C                   MOVE      *BLANKS       WMVAKD            2
     C     KODGEK        KLIST
     C                   KFLD                    YGEUNI
     C                   KFLD                    YVK
     C     KODG2K        KLIST
     C                   KFLD                    YG2UNI
     C                   KFLD                    YVK
     C     KODOTK        KLIST
     C                   KFLD                    YOTUNI
     C                   KFLD                    HEOT
     C     KODFRK        KLIST
     C                   KFLD                    YFRUNI
     C                   KFLD                    HEFR
     C     KODTHK        KLIST
     C                   KFLD                    YHUNI
     C                   KFLD                    UAVD
     C     HODEK         KLIST
     C                   KFLD                    YNET
     C                   KFLD                    UAVD
     C     KUND1K        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    KUNDNR
N01  C     VadrKey       KLIST
N01  C                   KFLD                    FIFIRM
N01  C                   KFLD                    KUNDNR
N01  C                   KFLD                    YADRNR
N01  C                   z-sub     1             YADRNR            5 0
      *
     C                   Z-SUB     1             YSOLNR            3 0
     C                   MOVE      ' '           YSAMM             1
     C                   MOVE      *ZEROS        YSOAVD            4 0
     C                   MOVE      *ZEROS        YSOOPD            7 0
     C                   MOVE      ' '           YSOSK             1
     C                   MOVE      *ZEROS        YSKKN             8 0
     C                   MOVE      *ZEROS        YSKPOS            4 0
     C                   MOVE      'GE'          YGEUNI            2
     C                   MOVE      *BLANKS       YVK               3
     C                   MOVE      'G2'          YG2UNI            2
     C                   MOVE      'OTY'         YOTUNI            3
     C                   MOVE      'FR'          YFRUNI            2
     C                   MOVE      'H'           YHUNI             1
      *
     C     *LOVAL        SETLL     FIRM
     C                   READ      FIRM                                   33
     C     FITAX         MULT      100           MVAPR1            4 2
      * FOREGÅENDE MOMSSATS..
     C     FITAXD        IFNE      *ZEROS
     C     FITAX2        ANDNE     *ZEROS
     C     FITAX2        MULT      100           MVAPR2            4 2
     C                   END
      *
     C     KODTHK        CHAIN     KODTH                              33
     C     KOHFAK        COMP      'J'                                    47
      *
      * Henter hode i alle språkvarianter
     C     *IN47         IFEQ      '1'
     C                   MOVEL     ' '           YNET              1            *NORSK/EN/TY
     C     HODEK         CHAIN     HODE                               33
     C  N33              MOVE      HP            HO
     C                   MOVEL     'E'           YNET
     C     HODEK         CHAIN     HODE                               33
     C  N33              MOVE      HP            HE
     C                   MOVEL     'T'           YNET
     C     HODEK         CHAIN     HODE                               33
     C  N33              MOVE      HP            HT
     C                   END
      *
     C                   Z-ADD     1024          UCMDL            15 5
      *
      * HENTER STRINGKODE
      *
     C                   Z-ADD     19            WTSKRN            3 0
     C                   MOVE      XAVD          CMRAV2            4 0
     C                   CALL      'SYGE01'
     C                   PARM                    CMRAV2
     C                   PARM                    WTSKRN
     C                   PARM                    WOPNVN           10
     C                   PARM                    WOPTY            10
     C                   PARM                    WOPHEA            1
     C                   PARM                    WOPFM             5
     C     WOPFM         IFNE      *BLANKS
      * Utskrift med stringformularer.
     C     WOPFM         CHAIN     KODTFM                             60
      * Utskrift uten stringformularer.
     C                   ELSE
     C                   MOVE      *BLANKS       KFMT1
     C                   MOVE      *BLANKS       KFMT2
     C                   END
     C                   ENDSR
      *
      ***********************************************
     C     MAIN          BEGSR
      ***********************************************
     C                   MOVE      *ZEROS        XMVAGR
     C                   MOVE      *ZEROS        XPMOMS
     C                   MOVE      *ZEROS        XBRUTT
     C                   MOVE      *ZEROS        XTOTAL
     C                   Z-ADD     XAVD          HEAVD
     C                   Z-ADD     XOPD          HEOPD
     C                   MOVE      XKUNDE        KUNDNR
     C                   MOVE      XVAL          XVAL1
     C     XVAL          COMP      *BLANKS                            7171
     C   71xval          comp      ficurr                             7171
     C                   IF        ((XANKOK = '0') OR (XANKOK = ' '))
     C                   MOVE      '1'           ANTKOK            1
     C                   ELSE
     C                   MOVE      XANKOK        ANTKOK            1
     C                   END
     C     XFADAT        IFGT      501231
     C     XFADAT        ADD       19000000      IDAG8
     C                   ELSE
     C     XFADAT        ADD       20000000      IDAG8
     C                   END
     C                   MOVE      XFADAT        IDAGX             6 0
     C                   MOVEL     IDAGD         IDAGX
     C                   MOVE      IDAGÅ         IDAGX
     C                   MOVE      XFFDAT        XFFDT             6 0
      * HOVED-MOMSSATS..
     C                   MOVE      FITAX         FITAXB            4 4
     C                   MOVE      FITAX         MVAPRO            4 2
      * FOREGÅENDE-MOMSSATS??..
     C     FITAXD        IFNE      *ZEROS
     C     FITAX2        ANDNE     *ZEROS
     C     IDAG8         ANDLT     FITAXD
     C                   MOVE      FITAX2        FITAXB
     C                   MOVE      FITAX2        MVAPRO
     C                   END
      *
     C     HEADFK        CHAIN     HEADF                              33
     C     *IN33         IFEQ      '1'
     C                   RETURN
     C                   END
      *
     C     HEPRO         COMP      *ZEROS                                 52
     C     TRRUND        COMP      *ZEROS                                 54
     C     HETRCN        COMP      *BLANKS                                54
      *
      * HENTER FRANKATURTEKST
     C     KODFRK        CHAIN     KODTFR                             33
     C                   MOVEL     KFRNTX        XFRNTX           20
     C                   MOVEL     KFRTTX        XFRTTX           20
      *
      * HENTER OPPDRAGSTYPE TEKST
     C     KODOTK        CHAIN     KODTOTY                            33
      *
      *
     C     KUND1K        CHAIN     CUNDL01                            33
     C     POSTNR        COMP      0                                      37
     C     SYPOGE        COMP      *BLANKS                                38
N03  C  N38              eval      sypogn=sypoge
n03  C  N38sypogn        cat       adr3b:1       sypogn
N01  c     *in29         IFEQ      *OFF                                         Ikke v/KOST-OVERSLAG
N01  c     VadrKey       Chain     VADR                               33
N01  c     *in33         IFEQ      *OFF
N01  C                   MOVE      VADRNA        KNAVN
N01  C                   MOVE      VADRN1        ADR1
N01  C                   MOVE      VADRN2        ADR2
N01  C                   MOVEL     VADRN3        ADR3                           Vadrn3=inkl postnr
N01  C                   MOVEA     '11'          *in(37)
N01  c                   end
N01  c                   end
     C  N33SPRAAK        COMP      'E'                                    70
     C  N33SPRAAK        COMP      'T'                                    79
      *
     C  N70
     CANN79              MOVE      HO            HP
     C   70              MOVE      HE            HP
     C   79              MOVE      HT            HP
     C                   SETOFF                                       88
     C                   MOVE      *BLANKS       KLKNVN
     C     LKKEY         CHAIN     KODTLK                             33
      * Antall kopier kan variere fra kunde til kunde
     C     ANTKOK        IFNE      GMKOK
     C     GMKOK         IFNE      ' '
     C                   CLOSE     SYFA27PP
     C                   END
     C                   MOVE      ANTKOK        GMKOK             1            ANT. KOPIER
      * Ny override av printerfil
     C                   MOVE      *BLANKS       UCMD
     C     XCMD11        CAT       ANTKOK:0      UCMD
     C                   CAT       ')':0         UCMD
     C                   CALL      'QCMDEXC'                            33
     C                   PARM                    UCMD
     C                   PARM                    UCMDL
     C                   OPEN      SYFA27PP
     C                   END
      * Summer linjer/legg ut i SORT/sett indikator for kredit
      * Sett S/K/X for key FREETK
     C                   EXSR      KREDIT
      *
      * Skriv heading / fritekst
     C                   EXSR      KDTFM1
     C                   WRITE     TKJØP
     C                   EXSR      FRILIN
     C                   WRITE     THEAT
N05  C     DOKUVK        CHAIN     DOKUFV                             33
N05  C     KOWVLF        IFEQ      'J'
N05  C     *in33         andeq     '0'
N05  C                   EXSR      PRVALI
N05  C                   ELSE
     C                   WRITE     TEAD22
N05  C                   END
N51  C                   WRITE     TEAD22B
      **
      ** GÅR HER I LØKKE FOR VARELINJER LEST FRA SORT..
      **
     C     SORTK         SETLL     SORT
      *
     C     STVALI        TAG
     C                   SETOFF                                       33
     C                   READ      SORT                                   33
     C   33              GOTO      SLVALI
      *
      * TEST IF NEXT LINE HAVE SOME CODE AND NO TEXT
      *
     C     SOVT          IFEQ      *BLANKS
     C                   MOVE      SOKDM         XOKDM             1
     C                   MOVE      SOSAMM        XOSAMM            1
     C                   MOVE      SOVK          XOVK              3
     C                   MOVE      SOVT          XOVT             20
     C                   MOVE      SOBELN        XOBELN           13 2
     C                   MOVE      SOBELV        XOBELV           13 2
     C                   EXSR      TESTEQ
     C                   END
     C     SOSAMM        IFNE      *BLANK
     C     SOSAMM        CHAIN     KODSAM                             33
     C                   END
     C  N71              Z-ADD     SOBELN        XPFNOK
     C   71              Z-ADD     SOBELV        XPFNOK
     C                   ADD       XPFNOK        XTOTAL           13 2
     C                   EXSR      MVASR
      *
      * Hvis 0 skrives blank (ikke 0,00) Indikator for prtf.
     C     XPFNOK        COMP      *ZEROS                             3636
      *
     C                   MOVE      SOVK          YVK
     C     KODGEK        CHAIN     KODTGE                             33
     C   79KODG2K        CHAIN     KODTG2                             33
     C     SOSAMM        IFNE      *BLANK
     C     SOSAMM        CHAIN     KODSAM                             33
     C                   END
      * "Tillegsttekst" fakt.linje skal ERSTATTE standardtekst
     C     XFAVT1        IFEQ      'X'
     C                   MOVEL     XFAVT2        KGENOT
     C                   MOVEL     XFAVT2        KGEENT
     C                   MOVEL     XFAVT2        KG2TYT
     C                   MOVE      ' '           KGENOT
     C                   MOVE      ' '           KGEENT
     C                   MOVE      ' '           KG2TYT
     C                   MOVE      *BLANKS       SOVT
     C                   END
      *
     C   66              DO
     C                   WRITE     TKJØP
S05  C*                  WRITE     TEAD22
     C                   SETOFF                                       66
     C                   END
     C                   WRITE     TDET
      *
     C                   DELETE    SORTR
     C                   GOTO      STVALI
      *
     C     SLVALI        TAG
      *
     C                   z-add     XTOTAL        XBRUTT           13 2
      * skriv variabelt antall momslinjer før TOT
     C                   Z-ADD     1             XX
     C     SKMVA         TAG
     C     WSMVAK(XX)    IFNE      ' '
     C                   MOVE      WSMVAK(XX)    XMVAK
     C                   Z-ADD     WSMVAP(XX)    MVAPRO
     C                   Z-ADD     WSGRL(XX)     XMVAGR
     C                   Z-ADD     WSMVA(XX)     XPMOMS
     C                   ADD       XPMOMS        XBRUTT
     C                   WRITE     TTOTCM
     C                   ADD       1             XX
     C                   GOTO      SKMVA
     C                   ENDIF
     C                   WRITE     TTOTC
      *
     C                   ENDSR
     C***********************************************
     C     MVASR         BEGSR
     C***********************************************
     C*  Ved utgang av denne SR gir følgende variabler verdier FOR DENNE LINJA!!
      *    XPFNOK = beløp til print (i NOK /Valuta avh. av *IN71)
      *    MVAPRO = momssats 4,2 (eks 25,00)
      *    FITAXB = Momssats 4,4 (eks 0,250)
      *    XMVAK  = momskode på printet linje (* = Ja , ' '=N)
      *    NETTOF = Netto beløp Fritt
      *    NETTO  = Netto beløp Pliktig
      *    MVA    = Momsbeløp
      *    BRUTTO = Brutto
      * (variabelnavn lånt fra totalliste pr gebyr)
     C                   MOVE      *ZEROS        NETTOF           11 2          Netto beløp Fritt
     C                   MOVE      *ZEROS        NETTO            11 2          Netto beløp Pliktig
     C                   MOVE      *ZEROS        MVA              11 2          Momsbeløp
     C                   MOVE      *ZEROS        BRUTTO           11 2          Brutto
     C                   MOVE      *ZEROS        FITAXB
     c                   select
     c     SOKDM         Wheneq    'J'
     C                   MOVE      '*'           XMVAK
     c     SOKDM         Wheneq    'N'
     C                   MOVE      ' '           XMVAK
     c                   other
     C                   MOVE      SOKDM         XMVAK
     c                   endsl
      *
     C     FAKDM         IFEQ      ' '
     C     FAKDM         OREQ      'N'
      *  Momsfritt
     C                   ADD       XPFNOK        XMVAFR            9 2
     C                   z-add     XPFNOK        NETTOF
     C                   ELSE
      * DET ER moms  - sats kan varier - gr.lag mm må lagres i array
     C                   z-add     XPFNOK        NETTO
      * finn aktuell momssats
     C                   MOVE      FITAX         FITAXB
     C     FITAXD        IFNE      *ZEROS
     C     FITAX2        ANDNE     *ZEROS
     C     IDAG8         ANDLT     FITAXD
     C                   MOVE      FITAX2        FITAXB
     C                   END
     C     KODWKY        CHAIN     KODWL01                            84
     C     *IN84         IFEQ      *OFF
     C     MVAAVI        DIV       100           FITAXB
     C                   ENDIF
     C     XPFNOK        MULT(H)   FITAXB        MVA
     C*
     C* legg ut i array på oppdragsnivå (finn samme eller første ledige..)
     C                   Z-ADD     1             XX                2 0
     C     TMVA          TAG
     C     WSMVAK(XX)    IFEQ      ' '
     C     WSMVAK(XX)    OREQ      XMVAK
     C                   MOVEL     XMVAK         WSMVAK(XX)
     C                   Z-add     MVAPRO        WSMVAP(XX)
     C                   ADD       XPFNOK        WSGRL(XX)
     C                   ADD       MVA           WSMVA(XX)
     C                   ELSE
     C                   ADD       1             XX
     C                   GOTO      TMVA
     C                   ENDIF
     C                   ENDIF
     C*
     C     XPFNOK        ADD       MVA           BRUTTO           11 2
     C*
     C                   ENDSR
      *
      ***********************************************
     C     KREDIT        BEGSR
      ***********************************************
      *
     C     FAK8K         SETLL     FAK8
      *
     C     STKRE         TAG
     C                   SETOFF                                       33
     C     FAK8K         READE     FAK8                                   33
      *
     C   33              GOTO      SLKRE
      *
      * INVOICE LINES WITH AMT. ZERO:
     C     FABELN        IFEQ      0                                             2B
      *
      * SKIPP BUT DO NOT DELETE IF OTHER UNIT=D(PR DAY)
     C     FA01B         IFEQ      'D'                                            3B
     C                   GOTO      STKRE
     C                   END                                                      3E
      * SKIPP 0-LINE TYPE "PR LINE ON SAD" - DELETE IF SAD FINISHED
     C     FA01B         IFEQ      'L'                                            3B
     C     HEPK1         ANDEQ     'J'
     C                   GOTO      STKRE                                         SAD UFERDIG
     C                   END                                                      3E
      *
      * SKIPP IF AUTOGENERATED LINE
     C     FAKDA         IFEQ      'A'                                            3B
     C     FAKDA         OREQ      'B'
     C                   MOVE      FAVK          YVK
     C     KODGEK        CHAIN     KOGER                              33
     C     KGETYP        IFNE      'NUL'                                           4B
     C                   GOTO      STKRE
     C                   END                                                       4E
     C                   END                                                      3E
     C                   END                                                     2E (BEL=0)
      *
      * Linjen skal skrives ..
      *
     C     FAKDM         COMP      'J'                                    34
     C                   MOVE      FAVK          YVK
     C     KODGEK        CHAIN     KODTGE                             33
     C     KODG2K        CHAIN     KODTG2                             33
     C     SOSAMM        IFNE      *BLANK
     C     SOSAMM        CHAIN     KODSAM                             33
     C                   END
     C     SOSAMM        IFNE      *BLANKS
     C                   Z-SUB     1             SOLNR
     C                   END
     C                   WRITE     SORTR
      * Selger/kjøper/X??? for key FREETK
     C                   MOVE      FASK          XXSKKO
      *
     C                   GOTO      STKRE
      *
     C     SLKRE         ENDSR
      *
      ***********************************************
     C     TESTEQ        BEGSR
      ***********************************************
     C     STEQ          TAG
     C                   READ      SORT                                   33
     C   33              DO
     C     *LOVAL        SETLL     SORT
     C                   READ      SORT                                   33
     C                   GOTO      UTEQSR
     C                   END
      *
     C     XOKDM         IFEQ      SOKDM
     C     XOSAMM        IFNE      ' '
     C     XOSAMM        IFEQ      SOSAMM
     C                   ADD       SOBELN        XOBELN
     C                   ADD       SOBELV        XOBELV
     C                   DELETE    SORTR
     C                   GOTO      STEQ
     C                   END
     C                   END
     C     XOVK          IFEQ      SOVK
     C     XOVT          IFEQ      SOVT
     C                   ADD       SOBELN        XOBELN
     C                   ADD       SOBELV        XOBELV
     C                   DELETE    SORTR
     C                   GOTO      STEQ
     C                   ELSE
     C                   GOTO      UTEQ
     C                   END
     C                   ELSE
     C                   GOTO      UTEQ
     C                   END
     C                   ELSE
     C                   GOTO      UTEQ
     C                   END
     C     UTEQ          TAG
     C                   READP     SORT                                   33
     C     UTEQSR        TAG
     C                   Z-ADD     XOBELN        SOBELN
     C                   Z-ADD     XOBELV        SOBELV
     C                   ENDSR
      ***********************************************
     C     FRILIN        BEGSR
      ***********************************************
      * FRIE SØKEVEIER (KUN DE SOM HAR DOK.KODE LIK K/S/X  EL B)
     C     HEADFK        SETLL     FRISOK
     C     HEADFK        READE     FRISOK                                 91
     C     *IN91         DOWEQ     '0'
     C                   SORTA     DFS
     C     DFS(10)       IFNE      ' '
     C     XXSKKO        LOOKUP    DFS                                    91
     C  N91'B'           LOOKUP    DFS                                    91
     C     *IN91         IFEQ      '1'
     C     FSKODE        CHAIN     KODFRI                             91
     C     *IN91         IFEQ      '0'
     C                   WRITE     TKJØFS
     C                   END
     C                   END
     C                   END
     C*
     C     HEADFK        READE     FRISOK                                 91
     C                   END
     C*
     C* FRIE SØKEVEIER....
     C     FREETK        SETLL     FREETXT2
     C*
     C     FREETK        READE     FREETXT2                               91
     C     *IN91         DOWEQ     '0'
     C                   WRITE     TFRIEL
N02  c     xfakt         ifne      *zeros
N02  c                   z-add     xfakt         frtfak
N02  c                   end
N02  c                   update    freetxtr
     C     FREETK        READE     FREETXT2                               91
     C                   END
     C*
     C                   ENDSR
      *
      ***********************************************
     C     KDTFM1        BEGSR
      ***********************************************
     C                   MOVE      HEDTD         UDT1              2 0
     C                   MOVE      HEDTM         UDT2              2 0
     C                   MOVE      HEDTÅ         UDT3              2 0
     C                   EXSR      KDTFMT
     C                   MOVE      UDT1          XDTD
     C                   MOVE      UDT2          XDTM
     C                   MOVE      UDT3          XDTÅ
     C                   ENDSR
      *
      ***********************************************
     C     KDTFMT        BEGSR
      ***********************************************
     C                   CALL      'DATOFMT'
     C                   PARM                    UDT1
     C                   PARM                    UDT2
     C                   PARM                    UDT3
      *
     C                   ENDSR
n03  C***********************************************
n03  C     ARKIVSR       BEGSR
n03  C***********************************************
n03  C***********************************************
s07  c*    keya          klist
s07  c*                  kfld                    artype
s07  c*                  kfld                    aravd
s07  c*                  kfld                    aropd
n09  c     keya          klist
n09  c                   kfld                    artype
n09  c                   kfld                    aravd
n09  c                   kfld                    aropd
n09  c                   kfld                    arunde
n03  C                   TIME                    XULTID
n03  C                   MOVE      XULDD         ULDD
n03  C                   MOVE      XULMM         ULMM
n03  C                   MOVE      XULÅÅ         ULÅÅ
n03  C     FIFIRM        CHAIN     FIRMARC                            33
n03  C     *IN33         IFEQ      '0'
n03  C     'ko'          chain     arktxt                             33
n03  C     *IN33         IFEQ      '0'
n03  C     arkjn         andeq     'J'
n03   *
n03  C                   MOVE      FIFIRM        ARFIRM
n03  C                   MOVE      HEAVD         ARAVD
n03  C                   MOVE      HEOPD         AROPD
n03  C                   MOVE      'ko'          ARTYPE
s07  c**   keya          chain     arkivl03                           33
s07  c**   *in33         ifeq      '1'
      *
      * ikke skriv på samme faktura flere eksemplarer
      *
N09  C     xfakt         ifne      *zeros
N09  C                   MOVEL     XFAKT         ARUNDE
n09  c     keya          chain     arkivl03                           33
n09  c     *in33         ifeq      '0'
n09  c                   goto      uta
n09  c                   end
n09  c                   end
     C
N03  C                   MOVE      *BLANKS       ARUNDE
N04  C                   MOVEL     XFAKT         ARUNDE
n03  C                   MOVE      ZUSER         ARUSER
n03  C                   MOVE      ULDT          ARDATE
n03  C                   MOVE      XULTM         ARTIME
n03  C                   MOVE      KUNDNR        ARKUND
n03  C                   WRITE     ARKIVR
n03  C                   MOVE      'N'           ARXXXX            1
n03  C                   CALL      'ARC002FACL'
n03  c                   PARM                    ARTYPE
n03  c                   PARM                    ARUSER
n03  c                   PARM                    ARXXXX
s07  C**                 END
n03  C                   END
n03  C                   END
s09  C*                  ENDSR
n09  C     uta           ENDSR
N05  C*******************************************************
N05  C     PRVALI        BEGSR
N05  C*******************************************************
N05   * Printer varelinjer fra fraktbrev opdrag
N05  C                   write     vareli0
N05  C                   write     vareli1
N05  C     DOKUVK        SETLL     DOKUFV                             33
N05  C     STGVL         TAG
N05  C     DOKUVK        READE     DOKUFV                                 33
N05  C     *IN33         DOWEQ     '0'
N05  C                   Z-ADD     1             FMMKNR
N05  C                   Z-ADD     1             FMLONR
N05  C     DOKUFMKEY     CHAIN     DOKUFM                             33
N05  C     *IN33         IFEQ      '0'
N05  C                   MOVEL     FMMRK1        R33MRK
N05  C                   ELSE
N05  C                   MOVE      *BLANKS       R33MRK
N05  C                   END
N05  C                   Z-ADD     FVANT         R34ANT
N05  C                   eval      R35VSL = %trim(FVPAKN)+' '+FVVT
N05  C                   Z-ADD     FVVKT         R36VKT
N05  c
N05  C                   Z-ADD     FVLM          R37LM
N05  C                   write     vareli
     C   66              DO
     C                   WRITE     TKJØP
     C                   SETOFF                                       66
     C                   END
N05  C     DOKUVK        READE     DOKUFV                                 33
N05  C                   ENDDO
N05  C                   write     vareli1
     C   66              DO
     C                   WRITE     TKJØP
     C                   SETOFF                                       66
     C                   END
N05  C                   write     varelit
N05  C                   write     vareli1
N05  C                   ENDSR
