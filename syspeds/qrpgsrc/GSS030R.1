********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
P5037 * 01 YBC PTF5 MULIGHET FOR Å VELGE NY LEDERTEKST
P6041 * 02 YBC PTF6 OPPGI NY LAGERLOKASJON VED BEKREFTE PÅ TRANSFER TUR
P7XXX * 03 YBC PTF7 OPT P=PLUKKLISTE (MM.)
P7XXX * 04 YBC PTF7 SEND E-MAIL AV FINAL PACKINGLIST
P7XXX * 05 YBC PTF7 NY PARAMETER VED KALL PÅ GSS025R, GSS026R
P7XXX * 06 sh  PTf8 arkiv
P8XXX * 07 YBC PTF8 OPSJON L FOR MERKELAPP AV ORDRE
P8XXX * 08 YBC PTF8 IKKE BEKREFTE VED SKANNING AV TRANSITT
P8XXX * 09 sh  PTF8 ny parameter arc003
P8XXX * 10 YBC PTF8 FLERE ENN 6 PO.NR
P8XXX * 11 YBC PTF8 CONTAINER
P8XXX * 12 sh  PTF8 protect ordre from 2 workstations
********************************************************************
      *
      **************************************************************
      *** PROGRAM SKAL ARBEIDE MED PROFORMAFAKTURA
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER
      *** LEDIGE INDIKATORER 01 02 04 06-22 24
      *** LEDIGE INDIKATORER 28 29 32 34-40 48-89 95-97
      **************************************************************
      *
     FHEADF     IF   E           K DISK
NV5  FGSS035W   UF A E           K DISK    USROPN
N11  FGSSOLL01  IF   E           K DISK
     FGSSTRF    UF   E           K DISK
     FGSSTRL11  IF   E           K DISK
     F                                     RENAME(GSSTRFR:GSSTRF11)
     FGSSTRL12  IF   E           K DISK
     F                                     RENAME(GSSTRFR:GSSTRF12)
     FGSSTRL13  IF   E           K DISK
     F                                     RENAME(GSSTRFR:GSSTRF13)
     FGSSTRL14  IF   E           K DISK
     F                                     RENAME(GSSTRFR:GSSTRF14)
     FGSSTRL15  IF   E           K DISK
     F                                     RENAME(GSSTRFR:GSSTRF15)
     FGSSTRL16  IF   E           K DISK
     F                                     RENAME(GSSTRFR:GSSTRF16)
     FGSSBIF    IF   E           K DISK
     FGSSCGF    IF   E           K DISK
N07  FGSSORL22  IF   E           K DISK
N07  FGSSPOF    IF   E           K DISK
N07  FCDBOAT    IF   E           K DISK
     FFIRM      IF   E           K DISK
     FVALUL01   IF   E           K DISK
N01  FCUNDL01   IF   E           K DISK
     FGSS030D   CF   E             WORKSTN
     F                                     SFILE(GSS030DA:ZRECNO)
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D                 DS
     D XFT01           C                   CONST('DEL. BY:')
     D XFT02           C                   CONST('TRUCK NO:')
     D XFT03           C                   CONST('AWBNO:')
     D                 DS
     D  XFT11                  1     10
     D  XFT12                 13     27
     D  WSFT1                  1     70
     D                 DS
     D  XFT21                  1     10
     D  XFT22                 13     19
     D  XFT23                 20     20
     D  XFT24                 21     28
     D  XFT25                 13     27
     D  WSFT2                  1     70
     D                 DS
EV5  D  TRDTLÅ                 1      4  0
EV5  D  TRDTLM                 5      6  0
EV5  D  TRDTLD                 7      8  0
EV5  D  TRDTL                  1      8  0
     D                 DS
     D  WXDTLD                 1      2  0
     D  WXDTLM                 3      4  0
     D  WXDTLÅ                 5      6  0
     D  WXDTL                  1      6  0
     D                 DS
EV5  D  TRDTSÅ                 1      4  0
EV5  D  TRDTSM                 5      6  0
EV5  D  TRDTSD                 7      8  0
EV5  D  TRDTS                  1      8  0
     D                 DS
     D  WXDTSD                 1      2  0
     D  WXDTSM                 3      4  0
     D  WXDTSÅ                 5      6  0
     D  WXDTS                  1      6  0
N07  D                 DS
N07  D  ORDTLÅ                 1      4  0
N07  D  ORDTLM                 5      6  0
N07  D  ORDTLD                 7      8  0
N07  D  ORDTL                  1      8  0
N07  D                 DS
N07  D  UPDTLD                 1      2
N07  D  UPDTLM                 3      4
N07  D  UPDTLÅ                 5      6
N07  D  UPDTL                  1      6
N07  D                 DS
S10  D* UPO                    1    120    DIM(6)
N10  D  UPO                    1   1000    DIM(50)
N07  D  UPCON1                 1     20
N07  D  UPCON2                21     40
N07  D  UPCON3                41     60
N07  D  UPCON4                61     80
N07  D  UPCON5                81    100
N07  D  UPCON6               101    120
N11  D                 DS
N11  D  OLMN                   1     35
N11  D  OLMN01                 1      1
N11  D  OLMN02                 1      2
N11  D  OLMN03                 3      4
N05  D                SDS
N05  D  UUSER                254    263
     D SAVUDS         UDS
     D  UFIRM                  1      2
     D  UCURR                  3      5
     D  UBIUSI               101    110
     D  UBICGR               111    112
     D  UBICNO               113    120  0
     D  UBIFC                121    123
     D  UBIPRT               124    133
     D  UBIPRM               134    143
      *
     C                   EXSR      INIT
     C   33              GOTO      SLUTT
      *
     C     START         TAG
     C                   MOVE      '1'           *IN(93)
     C                   WRITE     GSS030DB
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
     C                   MOVE      *BLANKS       WSNR
     C                   MOVE      *ZEROS        XTNO              9 0
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C                   EXSR      SETKEY
     C     START1        TAG
     C                   EXSR      LESREC
      *
     C     START2        TAG
      *
     C  N94              DO
     C                   DO        15
     C                   ADD       1             ZRECNO
     C                   MOVE      TRDTLD        WXDTLD
     C                   MOVE      TRDTLM        WXDTLM
     C                   MOVE      TRDTLÅ        WXDTLÅ
     C                   MOVE      TRDTSD        WXDTSD
     C                   MOVE      TRDTSM        WXDTSM
     C                   MOVE      TRDTSÅ        WXDTSÅ
     C                   WRITE     GSS030DA
     C                   MOVE      ZRECNO        XRECNO            5 0
     C                   EXSR      LESREC
     C  N94              END
     C                   END
      *
     C     START3        TAG
     C  N30              Z-ADD     5             LINNBR
     C  N30              Z-ADD     2             POSNBR
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     GSS030DG
     C                   ELSE
     C                   WRITE     GSS030DC
     C                   EXFMT     GSS030DB
     C                   END
      *
      *** AVSLUTT
     C                   MOVE      '0'           *IN30
     C                   MOVEA     '00'          *IN(98)
      *
     C   03              GOTO      SLUTT
     C   04              DO
     C                   EXSR      SØKDTA
     C     W2BCD         IFEQ      *BLANKS
     C     W2CNO         ANDEQ     0
     C                   GOTO      START3
     C                   END
     C                   END
     C   05              GOTO      START
      *
      *** BLA VIDERE
     C   25              DO
     C                   Z-ADD     XRECNO        ZRECNO
     C  N94XTNO          CHAIN(N)  GSSTRF                             25
     C                   SETOFF                                       25
     C                   GOTO      START2
     C                   END
      *
     C     W2NR          IFNE      *BLANKS                                      1<-B
     C     W2TNO         ORNE      0
     C     W2BCD         ORNE      *BLANKS
     C     W2CNO         ORNE      0
     C     W2NR          IFEQ      *BLANKS                                       2<-B
     C                   EXSR      NYSØK
     C                   GOTO      START
     C                   ELSE                                                    2
     C     W2TNO         IFEQ      0                                              3<-B
     C                   SETON                                        9830
     C                   MOVE      'GSS0047'     MSGNR
     C                   ELSE                                                     3
     C                   EXSR      NYOPT
     C                   END                                                      3<-E
     C                   MOVE      *ZEROS        W2TNO
     C                   MOVE      *BLANKS       W2BCD
     C                   MOVE      *ZEROS        W2CNO
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     2             POSNBR
     C                   GOTO      START3
     C                   END                                                     2<-E
     C                   END                                                    1<-E
      *
     C     ZRECNO        IFNE      0
     C                   EXSR      HHENT
     C                   END
     C                   GOTO      START3
      *
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
      *
     C***********************************************
     C     TESTB         BEGSR
     C***********************************************
     C     YTNO          COMP      0                                  4141
     C   41              GOTO      SLTB2
     C     YBCD          COMP      *BLANKS                            4242
     C   42              GOTO      SLTB2
     C     YCNO          COMP      0                                  4343
     C   43              GOTO      SLTB2
      *
     C     SLTB2         TAG
     C   41UBICNO        IFNE      0
     C                   MOVE      '0'           *IN41
     C                   MOVE      '1'           *IN44
     C                   ELSE
     C     UBICGR        IFNE      *BLANKS
     C                   MOVE      '0'           *IN41
     C                   MOVE      '1'           *IN46
     C                   END
     C                   END
     C   42UBICNO        IFNE      0
     C                   MOVE      '0'           *IN42
     C                   MOVE      '1'           *IN45
     C                   ELSE
     C     UBICGR        IFNE      *BLANKS
     C                   MOVE      '0'           *IN42
     C                   MOVE      '1'           *IN47
     C                   END
     C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     NYSØK         BEGSR
     C***********************************************
     C                   MOVE      *ZEROS        YTNO
     C                   MOVE      *BLANKS       YBCD
     C                   MOVE      *ZEROS        YCNO
     C     W2TNO         IFNE      0
     C                   MOVE      W2TNO         YTNO
     C                   END
     C     W2BCD         IFNE      *BLANKS
     C                   MOVE      W2BCD         YBCD
     C                   END
     C     W2CNO         IFNE      YCNO
     C     UBICNO        IFNE      0
     C                   MOVE      UBICNO        W2CNO
     C                   END
     C                   MOVE      W2CNO         YCNO
     C                   END
      *
     C                   MOVE      *ZEROS        W2TNO
     C                   MOVE      *BLANKS       W2BCD
     C                   MOVE      *ZEROS        W2CNO
      *
     C                   MOVEA     '0000000'     *IN(41)
     C                   EXSR      TESTB
      *
     C                   ENDSR
      *
     C***********************************************
     C     NYOPT         BEGSR
     C***********************************************
     C     W2TNO         CHAIN(N)  GSSTRF                             98
     C  N98TRST          IFNE      '2'
     C     TRST          COMP      '3'                                9898
     C                   END
     C   98              MOVE      'GSS0048'     MSGNR
     C   98              GOTO      SLOPT
     C     UBICGR        IFNE      *BLANKS                                      1<-B
     C     TRCNO         CHAIN     GSSCGF                             98
     C  N98CGCG          COMP      UBICGR                             9898
     C   98              MOVE      'GSS0029'     MSGNR
     C   98              GOTO      SLOPT
     C                   END                                                    1<-E
     C     UBICNO        IFNE      0                                            1<-B
     C     UBICNO        ANDNE     TRCNO
     C                   MOVE      '1'           *IN98
     C                   MOVE      'GSS0030'     MSGNR
     C                   GOTO      SLOPT
     C                   END                                                    1<-E
     C                   MOVE      '1'           *IN90
      *
     C                   SELECT
     C     W2NR          WHENEQ    '1'
     C                   EXSR      CHGTR
     C     W2NR          WHENEQ    '2'
     C                   EXSR      EDTTR
     C     W2NR          WHENEQ    '5'
     C                   EXSR      DSPTR
     C     W2NR          WHENEQ    '6'
     C                   EXSR      PRTTR
     C     W2NR          WHENEQ    '7'
     C                   EXSR      CPYPL
     C     W2NR          WHENEQ    '9'
     C     TRST          ANDEQ     '3'
     C                   EXSR      CFMTR
N03  C     W2NR          WHENEQ    'P'
N03  C                   EXSR      PLKTR
N06  C     W2NR          WHENEQ    'A'
N06  C                   EXSR      arkivsr
N07  C     W2NR          WHENEQ    'L'
N07  C                   EXSR      PRTML
     C                   OTHER
     C                   MOVE      'GSS0009'     MSGNR
     C                   MOVE      '1'           *IN98
     C                   GOTO      SLOPT
     C                   ENDSL
      *
     C     SLOPT         TAG
     C   98              MOVE      '1'           *IN30
     C                   MOVE      '0'           *IN90
     C                   MOVE      *BLANKS       W2NR
      *
     C                   ENDSR
      *
     C***********************************************
     C     HHENT         BEGSR
     C***********************************************
     C                   MOVE      *BLANKS       MSGNR
     C                   MOVEA     '000'         *IN(90)
     C                   MOVE      '0'           *IN99
     C     *IN98         DOWEQ     '0'
     C                   READC     GSS030DA                               98
     C   98              DO
     C   99              MOVE      '0'           *IN98
     C                   GOTO      SLHENT
     C                   END
      *
     C     ZRECNO        DIV       15            LINNBR
     C                   MVR                     XLIN              1 0
     C     XLIN          IFEQ      0
     C                   Z-ADD     15            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
      *
n12  C     TRTNO         CHAIN(N)  GSSTRF                             33
n12  c     trst          ifeq      'X'
n12  c     wsnr          ifeq      '2'
n12  c     wsnr          oreq      '1'
n12  C                   MOVE      'GSS0070'     MSGNR
n12  c                   SETON                                        9830
n12  C                   MOVE      *BLANKS       WSNR
n12  C                   UPDATE    GSS030DA
n12  C                   GOTO      SLHENT
n12  c                   end
n12  c                   end
     C                   SELECT
     C     WSNR          WHENEQ    '1'
     C                   EXSR      CHGTR
     C     WSNR          WHENEQ    '2'
     C                   EXSR      EDTTR
     C     WSNR          WHENEQ    '5'
     C                   EXSR      DSPTR
     C     WSNR          WHENEQ    '6'
     C                   EXSR      PRTTR
     C     WSNR          WHENEQ    '7'
     C                   EXSR      CPYPL
     C     WSNR          WHENEQ    '9'
     C     TRST          ANDEQ     '3'
     C                   EXSR      CFMTR
N03  C     WSNR          WHENEQ    'P'
N03  C                   EXSR      PLKTR
N06  C     WSNR          WHENEQ    'A'
N06  C                   EXSR      arkivsr
N07  C     WSNR          WHENEQ    'L'
N07  C                   EXSR      PRTML
     C                   OTHER
     C                   MOVE      '1'           *IN98
     C                   MOVE      *BLANKS       WSNR
     C                   UPDATE    GSS030DA
     C                   GOTO      SLHENT
     C                   ENDSL
     C                   MOVE      '1'           *IN99
      *
     C                   END
      *
     C     SLHENT        TAG
     C                   MOVE      '0'           *IN99
     C   98MSGNR         IFEQ      *BLANKS
     C                   MOVE      'GSS0009'     MSGNR
     C                   END
     C   98              MOVE      '1'           *IN30
     C  N30              Z-ADD     5             LINNBR
     C   30              ADD       5             LINNBR
     C                   Z-ADD     2             POSNBR
      *
     C                   ENDSR
      *
     C***********************************************
     C     SETKEY        BEGSR
     C***********************************************
     C   41YTNO          IFEQ      999999999
     C     GTRK01        SETGT     GSSTRL11
     C                   ELSE
     C     GTRK01        SETLL     GSSTRL11
     C                   END
     C   42GTRK02        SETGT     GSSTRL12
     C   43GTRK03        SETGT     GSSTRL13
     C   44YTNO          IFEQ      999999999
     C     GTRK04        SETGT     GSSTRL13
     C                   ELSE
     C     GTRK04        SETLL     GSSTRL13
     C                   END
     C   45GTRK05        SETGT     GSSTRL14
     C   46YTNO          IFEQ      999999999
     C     GTRK06        SETGT     GSSTRL15
     C                   ELSE
     C     GTRK06        SETLL     GSSTRL15
     C                   END
     C   47GTRK07        SETGT     GSSTRL16
      *
     C                   ENDSR
      *
     C***********************************************
     C     LESREC        BEGSR
     C***********************************************
     C     STLF          TAG
     C   41YTNO          IFEQ      999999999
     C                   READP     GSSTRL11                               94
     C                   ELSE
     C                   READ      GSSTRL11                               94
     C                   END
     C   42YBCD          READPE    GSSTRL12                               94
     C   43YCNO          READPE    GSSTRL13                               94
     C   44YTNO          IFEQ      999999999
     C     UBICNO        READPE    GSSTRL13                               94
     C                   ELSE
     C     UBICNO        READE     GSSTRL13                               94
     C                   END
     C   45GTRX05        READPE    GSSTRL14                               94
     C   46YTNO          IFEQ      999999999
     C     UBICGR        READPE    GSSTRL15                               94
     C                   ELSE
     C     UBICGR        READE     GSSTRL15                               94
     C                   END
     C   47GTRX07        READPE    GSSTRL16                               94
     C  N94              MOVE      TRTNO         XTNO
      *
     C                   ENDSR
      *
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     GTRK01        KLIST
     C                   KFLD                    YTNO
     C     *LIKE         DEFINE    TRTNO         YTNO
     C     GTRK02        KLIST
     C                   KFLD                    YBCD
     C                   KFLD                    YTNO2
     C     *LIKE         DEFINE    TRBCD         YBCD
     C     *LIKE         DEFINE    TRTNO         YTNO2
     C                   Z-ADD     999999999     YTNO2
     C     GTRK03        KLIST
     C                   KFLD                    YCNO
     C                   KFLD                    YTNO2
     C     *LIKE         DEFINE    TRCNO         YCNO
     C     GTRK04        KLIST
     C                   KFLD                    UBICNO
     C                   KFLD                    YTNO
     C     GTRK05        KLIST
     C                   KFLD                    UBICNO
     C                   KFLD                    YBCD
     C                   KFLD                    YTNO2
     C     GTRX05        KLIST
     C                   KFLD                    UBICNO
     C                   KFLD                    YBCD
     C     GTRK06        KLIST
     C                   KFLD                    UBICGR
     C                   KFLD                    YTNO
     C     GTRK07        KLIST
     C                   KFLD                    UBICGR
     C                   KFLD                    YBCD
     C                   KFLD                    YTNO2
     C     GTRX07        KLIST
     C                   KFLD                    UBICGR
     C                   KFLD                    YBCD
     C     KODVAK        KLIST
     C                   KFLD                    UFIRM
     C                   KFLD                    WSCUR
N01  C     CUND1K        KLIST
N01  C                   KFLD                    UFIRM
N01  C                   KFLD                    WSKNC
N07  C     CUNDL01K      KLIST
N07  C                   KFLD                    FIFIRM
N07  C                   KFLD                    ORCNO
      *
     C                   MOVE      '2'           UMODE             1
     C                   MOVE      'P'           WSNYL
     C                   MOVE      'N'           WSFAX
N01  C                   MOVE      'N'           WSCOMM
     C     UBIUSI        CHAIN     GSSBIF                             33
     C     *IN33         IFEQ      '0'
     C     FIFIRM        SETLL     FIRM
     C                   READ      FIRM                                   33
     C                   MOVE      *ZEROS        WXDTL
     C                   MOVE      *ZEROS        WXDTS
     C                   MOVE      *ZEROS        TRDTL
     C                   MOVE      *ZEROS        TRDTS
     C                   Z-ADD     1             WSCPY
     C     *DTAARA       DEFINE    *LDA          SAVUDS
     C                   IN        SAVUDS
     C                   MOVE      FIFIRM        UFIRM
     C                   MOVE      FICURR        UCURR
     C                   MOVE      BICGR         UBICGR
     C                   MOVE      BICNO         UBICNO
     C                   MOVE      BIFC          UBIFC
     C                   MOVE      BIPRT         UBIPRT
     C                   MOVE      BIPRTM        UBIPRM
     C                   OUT       SAVUDS
     C                   MOVE      BIPRT         WSPRT
NV5  C                   MOVE      BIPRTM        WSMLP
     C                   Z-ADD     999999999     YTNO
     C                   EXSR      TESTB
     C                   END
N07  C                   MOVE      UBIPRM        WSPRTM
      *
     C                   ENDSR
      *
     C***********************************************
     C     CHGTR         BEGSR
     C***********************************************
     C  N90              MOVE      '1'           *IN99
     C  N90              MOVE      *BLANKS       WSNR
     C  N90              UPDATE    GSS030DA
      *
     C                   MOVE      'EDT'         UTYPE             3
     C                   MOVE      TRTNO         UTNO
     C                   CALL      'GSS021R'
     C                   PARM                    UTNO
     C                   PARM                    UTYPE
     C                   PARM                    UMODE
      *
     C                   ENDSR
      *
     C***********************************************
     C     DSPTR         BEGSR
     C***********************************************
     C  N90              MOVE      '1'           *IN99
     C  N90              MOVE      *BLANKS       WSNR
     C  N90              UPDATE    GSS030DA
      *
     C                   MOVE      'DSP'         UTYPE             3
     C                   MOVE      TRTNO         UTNO
     C     *LIKE         DEFINE    TRTNO         UTNO
     C                   CALL      'GSS021R'
     C                   PARM                    UTNO
     C                   PARM                    UTYPE
     C                   PARM                    UMODE
      *
     C                   ENDSR
      *
     C***********************************************
     C     EDTTR         BEGSR
     C***********************************************
     C  N90              MOVE      '1'           *IN99
     C  N90              MOVE      *BLANKS       WSNR
     C  N90              UPDATE    GSS030DA
      *
     C                   Z-ADD     2             UFLAGG
     C                   MOVE      'EDT'         UTYPE             3
     C                   MOVE      TRTNO         UTNO
n12  c                   move      trst          uuusta            1
n12  c                   move      trst          UFLAGG
n12  C     TRTNO         CHAIN     GSSTRF                             33
n12  c  n33              move      'X'           trst
n12  c  n33              update    gsstrfr
     C                   CALL      'GSS023R'
     C                   PARM                    UTNO
     C                   PARM                    UTYPE
     C                   PARM                    UMODE
     C                   PARM                    UFLAGG            1 0
      *
n12  C     TRTNO         CHAIN     GSSTRF                             33
n12  c                   IF        TRST = 'X'
n12  c  N33              move      uuusta        trst
n12  c                   END
n12  c  N33              update    gsstrfr
     C                   ENDSR
      *
     C***********************************************
     C     PRTTR         BEGSR
     C***********************************************
     C                   MOVE      '0'           *IN99
     C                   MOVE      '0'           *IN31
     C                   MOVE      'GSS0056'     MSGHN
     C                   MOVE      FICURR        WSCUR
     C     TRTNO         CHAIN(N)  GSSTRF                             33
     C     TRMFT1        IFEQ      *BLANKS
     C     HEADFK        KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C                   MOVEL     TRTNO         HEAVD
     C                   MOVE      TRTNO         HEOPD
     C     HEADFK        CHAIN     HEADF                              99
     C                   MOVE      *BLANKS       WSFT1
     C                   MOVE      *BLANKS       WSFT2
     C                   MOVEL     XFT01         XFT11
     C                   MOVE      TRDW          XFT12
     C     HEUR          IFEQ      'A'
     C     HEUR          OREQ      'B'
     C                   MOVEL     XFT02         XFT21
     C                   MOVEL     HEGN          XFT25
     C                   ELSE
     C                   MOVEL     XFT03         XFT21
     C                   MOVEL     HEGN          XFT22
     C                   MOVE      '-'           XFT23
     C                   MOVE      HEGN          XFT24
     C                   END
     C                   ELSE
     C                   MOVE      TRMFT1        WSFT1
     C                   MOVE      TRMFT2        WSFT2
     C                   END
NV5  C                   MOVE      'N'           WSML
NV5  C     TRBCD         COMP      *BLANKS                                27
NV5  C  N27TRLOCF        COMP      *BLANKS                            2727
NV5  C  N27TRLOCT        COMP      *BLANKS                            2727
      *
     C     STPRT         TAG
     C                   EXFMT     GSS030DY
     C                   MOVE      '0'           *IN99
N01  C   04              DO
N01  C                   MOVE      *BLANKS       USØKKN           30
N01  C                   CALL      'SOEKKUN'
N01  C     WSKNC         PARM      WSKNC         UKNC80            8 0
N01  C                   PARM                    UFIRM
N01  C                   PARM                    USØKKN
N01  C                   GOTO      STPRT
N01  C                   END
     C   12              GOTO      SLPRT
     C     WSPRT         COMP      *BLANKS                                99
     C   99              GOTO      STPRT
     C     KODVAK        CHAIN     VALUL01                            98
     C   98              GOTO      STPRT
N01  C     WSCOMM        IFEQ      'Y'
N01  C     WSKNC         ANDNE     0
N01  C     CUND1K        CHAIN     CUNDL01                            97
N01  C   97              GOTO      STPRT
N01  C                   END
N11   *
N11  C                   MOVE      'AV'          XCONT             2
N11  C     TRTNO         SETLL     GSSOLL01
N11  C     TRTNO         READE     GSSOLL01                               33
N11  C  N33              IF        ((OLMN01 = 'C') AND (OLMN03 = ': '))
N11  C                   MOVE      'CT'          XCONT
N11  C                   MOVE      'N'           WSFAX
N11  C                   END
N11  C                   OUT       SAVUDS
      *
     C     TRTNO         CHAIN     GSSTRF                             33
     C                   MOVE      WSFT1         TRMFT1
     C                   MOVE      WSFT2         TRMFT2
     C                   UPDATE    GSSTRFR
     C                   SELECT
     C     WSNYL         WHENEQ    'P'
     C     WSNYL         OREQ      'O'
     C                   MOVE      'PRT'         UTYPE
     C     WSNYL         WHENEQ    'L'
     C                   MOVE      'PRL'         UTYPE
     C                   ENDSL
N01  C                   MOVE      WSKNC         UKNC              8
     C                   MOVE      TRCNO         UCNO2             8
     C                   MOVE      TRTNO         UTNO2             9
      *
     C     WSFAX         IFEQ      'Y'
     C     WSNYL         IFEQ      'P'
     C     WSNYL         OREQ      'L'
     C                   MOVE      '2'           UMOD              1
     C                   ELSE
     C                   MOVE      '3'           UMOD
     C                   END
     C                   CALL      'GSS034C'
     C                   PARM                    UCNO2
     C                   PARM                    UUTFAX            1
     C                   PARM                    UTNO2
     C                   PARM                    UTYPE
     C                   PARM                    WSCUR
     C                   PARM                    UMOD
N01  C                   PARM                    WSCOMM
N01  C                   PARM                    UKNC
     C     UUTFAX        CABEQ     'J'           STPRT2
     C                   GOTO      STPRT
     C                   END
      *
     C     WSCPY         IFEQ      0
     C                   Z-ADD     1             WSCPY
     C                   END
     C                   MOVE      WSCPY         UCPY
NV5   *
NV5  C     WSML          IFEQ      'Y'
NV5  C                   OPEN      GSS035W
NV5  C     TRTNO         CHAIN     GSS035W                            33
NV5  C     *IN33         IFEQ      '0'
NV5  C                   UPDATE    GSS035WR
NV5  C                   ELSE
NV5  C                   MOVE      TRTNO         W3TNO
NV5  C                   WRITE     GSS035WR
NV5  C                   END
NV5  C                   CLOSE     GSS035W
NV5  C                   END
      *
     C     WSNYL         IFEQ      'P'
     C     WSNYL         OREQ      'L'
N11  C                   IF        XCONT = 'CT'
N11  C                   MOVE      'PRC'         UTYPE
N11  C                   MOVEL     'CT'          WSPOS1
N11  C                   END
     C                   CALL      'GSS031R'
     C                   PARM                    UTNO2
     C                   PARM                    UTYPE
     C                   PARM                    WSPRT
     C                   PARM                    WSCUR
     C                   PARM                    UCPY
     C                   PARM                    WSFAX
N01  C                   PARM                    WSCOMM
N01  C                   PARM                    UKNC
N02  C                   PARM                    UQJOB            10
N02  C                   PARM                    UQUSER           10
N02  C                   PARM                    UQNBR             6
N02  C                   PARM                    WSPOS1
N02  C                   PARM                    WSPOS2
N02  C                   PARM                    WSPOS3
     C                   ELSE
     C                   CALL      'GSS033R'
     C                   PARM                    UTNO2
     C                   PARM                    UTYPE
     C                   PARM                    WSPRT
     C                   PARM                    UCPY
     C                   PARM                    WSFAX
     C                   END
      *
     C     STPRT2        TAG
     C  N90              MOVE      '1'           *IN99
     C  N90              MOVE      '3'           TRST
     C  N90              MOVE      *BLANKS       WSNR
     C  N90              UPDATE    GSS030DA
      *
     C     SLPRT         ENDSR
      *
     C***********************************************
     C     CPYPL         BEGSR
     C***********************************************
     C                   MOVE      '0'           *IN99
     C                   MOVE      '1'           *IN31
     C                   MOVE      'GSS0058'     MSGHN
     C     STCPY         TAG
     C                   EXFMT     GSS030DX
     C                   MOVE      '0'           *IN99
     C   12              GOTO      SLCPY
     C     WSPRT         COMP      *BLANKS                                99
     C   99              GOTO      STCPY
      *
     C                   MOVE      'CPY'         UTYPE
     C                   MOVE      TRCNO         UCNO2             8
     C                   MOVE      TRTNO         UTNO2             9
      *
     C     WSFAX         IFEQ      'Y'
     C                   MOVE      '1'           UMOD
     C                   CALL      'GSS034C'
     C                   PARM                    UCNO2
     C                   PARM                    UUTFAX            1
     C                   PARM                    UTNO2
     C                   PARM                    UTYPE
     C                   PARM                    WSCUR
     C                   PARM                    UMOD
N01  C                   PARM                    WSCOMM
N01  C                   PARM                    UKNC
     C     UUTFAX        CABEQ     'J'           STCPY2
     C                   GOTO      STCPY
     C                   END
      *
     C     WSCPY         IFEQ      0
     C                   Z-ADD     1             WSCPY
     C                   END
     C                   MOVE      WSCPY         UCPY              1
      *
     C                   CALL      'GSS022R'
     C                   PARM                    UTNO2
     C                   PARM                    UTYPE
     C                   PARM                    UCPY
     C                   PARM                    WSPRT
     C                   PARM                    WSFAX
      *
     C     STCPY2        TAG
     C  N90              MOVE      '1'           *IN99
     C  N90              MOVE      *BLANKS       WSNR
     C  N90              UPDATE    GSS030DA
      *
     C     SLCPY         ENDSR
      *
     C***********************************************
     C     CFMTR         BEGSR
     C***********************************************
     C  N90              MOVE      '1'           *IN99
     C   23              DO
     C                   MOVE      *BLANKS       WSFAX
     C                   MOVE      'CFM'         UTYPE
     C                   MOVE      TRTNO         UTNO2
N02  C     TRTNO         CHAIN(N)  GSSTRF                             33
N02  C     TRBCD         IFEQ      *BLANKS
N02  C     TRLOCF        ORNE      *BLANKS
N02  C     TRLOCT        ANDNE     *BLANKS
N02  C                   MOVE      '*NEW '       WSPOS1
N02  C                   MOVE      *BLANKS       WSPOS2
N02  C                   MOVE      *BLANKS       WSPOS3
N02  C                   MOVE      '0'           *IN99
N02  C     STCFM         TAG
N02  C                   EXFMT     GSS030DD
N02  C                   MOVE      '0'           *IN99
N02  C   12              GOTO      SLCFM
N02  C     WSPOS1        COMP      *BLANKS                                99
N02  C   99              GOTO      STCFM
N02  C                   END
     C                   CALL      'GSS031R'
     C                   PARM                    UTNO2
     C                   PARM                    UTYPE
     C                   PARM                    WSPRT
     C                   PARM                    WSCUR
     C                   PARM                    UCPY
     C                   PARM                    WSFAX
N01  C                   PARM                    WSCOMM
N01  C                   PARM                    UKNC
N02  C                   PARM                    UQJOB            10
N02  C                   PARM                    UQUSER           10
N02  C                   PARM                    UQNBR             6
N02  C                   PARM                    WSPOS1
N02  C                   PARM                    WSPOS2
N02  C                   PARM                    WSPOS3
N08  C                   SELECT
N08  C                   WHEN      UTYPE = 'CF4'
     C                   MOVE      '4'           TRST
N08  C                   WHEN      UTYPE = 'CF6'
N08  C                   MOVE      '6'           TRST
N08  C                   OTHER
N08  C                   ENDSL
N08  C                   MOVE      'CFM'         UTYPE
     C                   END
      *
     C  N90              MOVE      *BLANKS       WSNR
     C  N90              UPDATE    GSS030DA
      *
S02  C*                    ENDSR
N02  C     SLCFM         ENDSR
      *
N03  C***********************************************
N03  C     PLKTR         BEGSR
N03  C***********************************************
N03  C                   MOVE      '0'           *IN99
N03  C                   MOVE      'F'           WSSORT
N04  C                   MOVE      'N'           WSFAX
N03  C     STPLK         TAG
N03  C                   EXFMT     GSS030DZ
N03  C                   MOVE      '0'           *IN99
N03  C   12              GOTO      SLPLK
N03  C     WSPRT         COMP      *BLANKS                                99
N03  C   99              GOTO      STPLK
N03   *
N03  C                   MOVE      'PRT'         UTYPE
N03  C                   MOVE      TRTNO         UTNO2             9
N03   *
N03  C                   MOVE      'J'           UKOPI             1
N03   *
N03  C                   SELECT
N03  C     WSSORT        WHENEQ    'L'
N03  C     WSSORT        OREQ      'O'
N03  C     WSSORT        OREQ      'B'
N03  C                   CALL      'GSS025R'
N03  C                   PARM                    UTNO2
N03  C                   PARM                    WSPRT
N03  C                   PARM                    WSSORT
N03  C                   PARM                    UKOPI
N05  C                   PARM                    UUSER
N03  C     WSSORT        WHENEQ    'P'
N03  C     WSSORT        OREQ      'F'
N04  C     WSSORT        IFEQ      'P'
N04  C                   MOVE      'N'           WSFAX
N04  C                   END
N04  C     WSFAX         IFEQ      'Y'
N04  C                   MOVE      TRCNO         UCNO2             8
N04  C                   MOVE      '5'           UMOD              1
N04  C                   CALL      'GSS034C'
N04  C                   PARM                    UCNO2
N04  C                   PARM                    UUTFAX            1
N04  C                   PARM                    UTNO2
N04  C                   PARM                    UTYPE
N04  C                   PARM                    UVALK             3
N04  C                   PARM                    UMOD              1
N04  C                   PARM                    UCOMM             1
N04  C                   PARM                    UKNC              8
N04  C                   ELSE
N04  C                   MOVE      '1'           UMOD              1
N03  C                   MOVE      '*JOB'        WSPRT2           10
N03  C                   CALL      'GSS026R'
N03  C                   PARM                    UTNO2
N03  C                   PARM                    WSPRT
N03  C                   PARM                    WSPRT2
N03  C                   PARM                    WSSORT
N04  C                   PARM                    WSFAX
N04  C                   PARM                    UMOD
N05  C                   PARM                    UQJOB            10
N05  C                   PARM                    UQUSER           10
N05  C                   PARM                    UQNBR             6
N05  C                   PARM                    UUSER
N04  C                   END
N03  C                   ENDSL
N03   *
N03  C  N90              MOVE      '1'           *IN99
N03  C  N90              MOVE      *BLANKS       WSNR
N03  C  N90              UPDATE    GSS030DA
N03   *
N03  C     SLPLK         ENDSR
N03   *
N06  C***********************************************
N06  C     arkivsr       BEGSR
N06  C***********************************************
N06  C                   MOVEl     TRTNO         uavd              4 0
N06  C                   MOVE      TRTNO         uopd              5 0
N06  C                   CALL      'ARC003'
N06  C                   PARM                    uavd
N06  C                   PARM                    uopd
n09  C                   PARM                    URUNDE           10
n06  C                   MOVE      *BLANKS       WSNR
N06  C                   UPDATE    GSS030DA
N06  C                   ENDSR
N06   *
N07  C***********************************************
N07  C     PRTML         BEGSR
N07  C***********************************************
N07  C                   MOVE      '0'           *IN99
N07  C     STPML         TAG
N07  C                   EXFMT     GSS030DH
N07  C                   MOVE      '0'           *IN89
N07  C   12              GOTO      SLPML
N07  C     WSPRTM        COMP      *BLANKS                                89
N07  C   89              GOTO      STPML
N07   *
N07  C     TRTNO         SETLL     GSSORL22
N07  C     TRTNO         READE     GSSORL22                               33
N07  C                   DOW       *IN33 = *OFF
N07  C                   MOVE      ORONO         UPONO             7
N07  C                   MOVE      ORDTLD        UPDTLD
N07  C                   MOVE      ORDTLM        UPDTLM
N07  C                   MOVE      ORDTLÅ        UPDTLÅ
N07  C     ORBCD         CHAIN     CDBOAT                             33
N07  C     CUNDL01K      CHAIN     CUNDL01                            33
N07  C                   MOVE      *BLANKS       UPO
N07  C                   MOVE      *ZEROS        XN                3 0
N07  C     ORONO         SETLL     GSSPOF
N07  C     ORONO         READE     GSSPOF                                 33
N07  C                   DOW       *IN33 = *OFF
N07  C                   ADD       1             XN                3 0
N07  C                   MOVE      POCONO        UPO(XN)
N07  C     ORONO         READE     GSSPOF                                 33
N07  C  N33              ENDDO
N07  C                   CALL      'GSS011RP'
N07  C                   PARM                    WSPRTM                         PRINTNAVN
N07  C                   PARM                    UPONO                          INTER ORDRENR
N07  C                   PARM                    CBNAB                          BÅTNAVN
N07  C                   PARM                    KNAVN                          KUNDENAVN
N07  C                   PARM                    ORSNM                          LEVERANDØR
N07  C                   PARM                    UPDTL                          LEV.DATO
N07  C                   PARM                    ORCLI                          ANTALL KOLLI
N07  C                   PARM                    UPCON1                         P.O.NR 1
N07  C                   PARM                    UPCON2                         P.O.NR 2
N07  C                   PARM                    UPCON3                         P.O.NR 3
N07  C                   PARM                    UPCON4                         P.O.NR 4
N07  C                   PARM                    UPCON5                         P.O.NR 5
N07  C                   PARM                    UPCON6                         P.O.NR 6
N07  C     TRTNO         READE     GSSORL22                               33
N07  C  N33              ENDDO
N07   *
N07  C  N90              MOVE      '1'           *IN99
N07  C  N90              MOVE      *BLANKS       WSNR
N07  C  N90              UPDATE    GSS030DA
N07   *
N07  C     SLPML         ENDSR
N07   *
     C***********************************************
     C     SØKDTA        BEGSR
     C***********************************************
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C                   Z-ADD     WSSUBN        GRECNO            4 0
     C                   Z-ADD     1             UFLAGG            1 0
      *
     C     LINNBR        IFEQ      5
     C     POSNBR        ANDGE     48
     C     POSNBR        ANDLE     52
     C                   CALL      'SYSJ11A'
     C                   PARM                    W2BCD
     C                   GOTO      SLSØK
     C                   END
      *
     C     LINNBR        IFEQ      5
     C     POSNBR        ANDGE     54
     C     POSNBR        ANDLE     61
     C                   MOVE      *BLANKS       USØKKN           30
     C                   CALL      'SOEKKUN'
     C                   PARM                    W2CNO
     C                   PARM                    UFIRM
     C                   PARM                    USØKKN           30
     C                   GOTO      SLSØK
     C                   END
      *
     C                   MOVE      'GSS0013'     MSGNR
     C                   SETON                                        30
      *
     C     SLSØK         ENDSR
      *
