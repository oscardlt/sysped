      *                                                          **********
      *
CRTPG+*  CRTPGM SYSPED/TJGE21Rf MODULE(*LIBL/TJGE21Rf *LIBL/INCGI)
CRTPGM*        ACTGRP(*NEW) AUT(*USE)
      *
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
11XXX * 01 JOV PTF11 nytt felt - FFINDX .....
      *%%%%%%%%%%%%%%%%%%%%%%%%%%%
12038 * 02 JOV PTF12 endring DOKUFVF / FFANTE
      * 03 JOV PTF12 kall Med UNIK/REFF = kall bra booking/Z-filer
********************************************************************
      *********************************************************************
      /copy SYSPEDS/qrpgsrcpy,hspecs
      /copy SYSPEDS/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FDOKUFV    IF   E           K DISK    USROPN
     FDOKUFVF   IF   E           K DISK    USROPN
     FDOKUFM    IF   E           K DISK    USROPN
N03  FZOKUFV2   IF   E           K DISK    USROPN
N03  F                                     RENAME(DOKUFVR:ZOKUFVR)
N03  FZOKUFVF2  IF   E           K DISK    USROPN
N03  F                                     RENAME(DOKUFVFR:ZOKUFVFR)
N03  FZOKUFM2   IF   E           K DISK    USROPN
N03  F                                     RENAME(DOKUFMR:ZOKUFMR)
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy SYSPEDS/qrpgsrcpy,prototypeb
      /copy SYSPEDS/qrpgsrcpy,usec
      /copy SYSPEDS/qrpgsrcpy,variables3
      *
      * Varible for
     D WSUSER          S             10
     D AVD             S              4
     D AVDN            S              4S 0
     D OPD             S              7
     D OPDN            S              7S 0
     D FBN             S              3
     D FBNN            S              3S 0
     D LIN             S              2
     D LINN            S              2S 0
     D LIN2            S              2
     D LIN2N           S              2S 0
N03  D WA              S             15
N03  D UNIK            S              9S 0
N03  D REFF            S             17
     D JSONstring      S          32000
     D Error           S            150
     D AntLest         S              9  0
      *get input-string
      /copy syspeds/qrpgsrcpy,prolog3

      * Parse input
     C                   exsr      Parse
      * Open files etc
     C                   EXSR      INIT
      *
     c                   callp     gethtmlifs(
     C                             '/syspeddev/html/JSON.html':
     C                             '/CGITAG/')
     C                   callp     wrtsection('top')
      * header-info
N03  c     OPDN          ifne      *zeros
     C     DOKUFVK       chain     DOKUFV
     C     DOKUFMK       CHAIN     DOKUFM
N03  c                   else
N03  C     ZOKUFVK       chain     ZOKUFV2
N03  C     ZOKUFMK       CHAIN     ZOKUFM2
N03  c                   endif
      * ............................................................................................
      * skriv JSON-Object start..(alltid '{' + x ant param. m/komma mellom (IKKE komma etter siste))
      * ............................................................................................
      *
      /free
        JSONstring='{'
        +'¬user¬ : ¬'+%trim(WSUSER)+'¬, '
        +'¬avd¬ : ¬'+%trim(%editc(AVDN:'Z'))+'¬, '
        +'¬opd¬ : ¬'+%trim(%editc(OPDN:'Z'))+'¬, '
        +'¬fbn¬ : ¬'+%trim(%editc(FBNN:'Z'))+'¬, '
        +'¬lin¬ : ¬'+%trim(%editc(linN:'Z'))+'¬, '
        +'¬unik¬ : ¬'+%trim(%editc(UNIK:'Z'))+'¬, '
        +'¬reff¬ : ¬'+%trim(REFF)+'¬, '
        +'¬fvavd¬ : ¬'+%trim(%editc(fvavd:'Z'))+'¬, '
        +'¬fvopd¬ : ¬'+%trim(%editc(fvopd:'Z'))+'¬, '
        +'¬fvfbnr¬ : ¬'+%trim(%editc(fvfbnr:'Z'))+'¬, '
        +'¬fvlinr¬ : ¬'+%trim(%editc(fvlinr:'Z'))+'¬, '
        +'¬fmmrk1¬ : ¬'+%trim(fmmrk1)+'¬, '
        +'¬fvant¬ : ¬'+%trim(%editc(fvant:'Z'))+'¬, '
        +'¬fvpakn¬ : ¬'+%trim(fvpakn)+'¬, '
        +'¬fvvt¬ : ¬'+%trim(fvvt)+'¬, '
        +'¬fvvkt¬ : ¬'+%trim(%editc(fvvkt:'Z'))+'¬, '
        +'¬fvvol¬ : ¬'+%trim(%editc(fvvol:'4'))+'¬, '
        +'¬fvlm¬ : ¬'+%trim(%editc(fvlm:'4'))+'¬, '
        +'¬fvlm2¬ : ¬'+%trim(%editc(fvlm2:'4'))+'¬, '
        +'¬fvlen¬ : ¬'+%trim(%editc(fvlen:'Z'))+'¬, '
        +'¬fvbrd¬ : ¬'+%trim(%editc(fvbrd:'Z'))+'¬, '
        +'¬fvhoy¬ : ¬'+%trim(%editc(fvhoy:'Z'))+'¬, '
        +'¬errMsg¬ : ¬'+%trim(Error)+'¬, ';
      /end-free
      * JSONfix escaper " i tekst,  for deretter å bytte ¬->"
     c                   call      'JSONFIX'
     c                   parm                    JSONstring
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')
      *
      * ............................................................................................
      * Skriv JSON-LISTE Start (en liste er EN parameter(fra [ til   )
      * ............................................................................................
     c                   eval      JSONstring ='"adrlinelist" : ['
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')
      *
      * Liste
      *
     c     Error         ifeq      *blanks
N03  c     OPDN          ifne      *zeros
     C     DOKUFVFK      SETLL     DOKUFVF
     C     DOKUFVFK      READE     DOKUFVF                                55
N03  c                   else
N03  C     ZOKUFVFK      SETLL     ZOKUFVF2
N03  C     ZOKUFVFK      READE     ZOKUFVF2                               55
N03  c                   endif
     C     *IN55         DOWEQ     '0'
      * "primiteivt" å lese alle/skippe mend dette blir uansett aldri nor ressursproblem
     c                   if        lin2n <> 0 and
     c                             fflin2<> lin2n
     c                   goto      ReadNext
     c                   endif
      *
     C                   Add       1             AntLest
      * ............................................................................................
      * Skriv en JSON-Array-linje pr menypunkt - komma før hvert nytt element
      * ............................................................................................
      /free
       if AntLest=1;
          JSONstring=*blanks;
          else;
          JSONstring=', ';
          endif;
       JSONstring=%trim(JSONstring)+'{'
          +'¬fflin2¬ : ¬'+%trim(%editc(fflin2:'Z'))+'¬, '
          +'¬ffunnr¬ : ¬'+%trim(ffunnr)+'¬, '
          +'¬ffembg¬ : ¬'+%trim(ffembg)+'¬, '
N01       +'¬ffindx¬ : ¬'+%trim(ffindx)+'¬, '
          +'¬ffklas¬ : ¬'+ %trim(ffklas)+'¬, '
          +'¬ffsedd¬ : ¬'+%trim(ffsedd)+'¬, '
          +'¬fftres¬ : ¬'+%trim(fftres)+'¬, '
          +'¬fffakt¬ : ¬'+%trim(%editc(fffakt:'Z'))+'¬, '
          +'¬ffantk¬ : ¬'+%trim(%editc(ffantk:'Z'))+'¬, '
N02       +'¬ffante¬ : ¬'+%trim(%editc(ffante:'4'))+'¬, '
          +'¬ffenh¬  : ¬'+%trim(ffenh)+'¬, '
          +'¬ffpoen¬ : ¬'+%trim(%editc(ffpoen:'Z'))+'¬}';
      /end-free
     C                   call      'JSONFIX'
     C                   parm                    JSONstring
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')


     C     READNEXT      TAG
N03  c     OPDN          ifne      *zeros
     C     DOKUFVFK      READE     DOKUFVF                                55
N03  c                   else
N03  C     ZOKUFVFK      READE     ZOKUFVF2                               55
N03  c                   endif

     C                   ENDDO
     c                   ENDIF
      * ............................................................................................
      * Skriv JSON-Liste/Array  Avslutning
      * ............................................................................................
     c                   eval      JSONstring =']'
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')
      * ............................................................................................
      * Skriv JSON-string Avsluttning
      * ............................................................................................
     c                   eval      JSONstring ='}'
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')

      * Quit
     C                   exsr      Exit


      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Lukk fil
     C                   CLOSE     BRIDF
     C  N50              CLOSE     DOKUFV
     C  N50              CLOSE     DOKUFVF
     C  N50              CLOSE     DOKUFM
N03  C  N50              CLOSE     ZOKUFV2
N03  C  N50              CLOSE     ZOKUFVF2
N03  C  N50              CLOSE     ZOKUFM2

      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   MOVE      *ON           *INLR
     C                   return
     C                   endsr
      *
      *********************************************************
     C     Parse         Begsr
      *********************************************************
      * Get input
     C                   EVAL      WSUSER = zhbgetvar('USER')
     C                   EVAL      AVD    = zhbgetvar('AVD')
     C                   EVAL      AVDN   = c2n2(AVD)
     C                   EVAL      OPD    = zhbgetvar('OPD')
     C                   EVAL      OPDN = c2n2(OPD)
     C                   EVAL      FBN    = zhbgetvar('FBN')
     C                   EVAL      FBNN = c2n2(FBN)
N03  C                   if        FBNN = *zeros
N03  C                   EVAL      FBNN = 1
N03  C                   endif
     C                   EVAL      LIN    = zhbgetvar('LIN')
     C                   EVAL      LINN = c2n2(LIN)
     C                   EVAL      LIN2   = zhbgetvar('LIN2')
     C                   EVAL      LIN2N= c2n2(LIN2)
N03  C                   eval      REFF    = zhbgetvar('REFF')
N03  C                   eval      WA      = zhbgetvar('UNIK')
N03  C                   eval      UNIK    = c2n2(WA)
     c                   endsr
      *
      *********************************************************
     C     INIT          Begsr
      *********************************************************
     C                   OPEN      BRIDF
      * Test innlogging
     C     WSUSER        CHAIN     BRIDF                              50
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C                   call      BILIBL
     C                   end
      *
     C     DOKUFVK       KLIST
     C                   KFLD                    WFVRI
     C                   KFLD                    AVDN
     C                   KFLD                    OPDN
     C                   KFLD                    FBNN
     C                   KFLD                    LINN
N03  C     ZOKUFVK       klist
N03  C                   KFLD                    UNIK
N03  C                   KFLD                    FBNN
     C                   KFLD                    LINN
     C     DOKUFMK       KLIST
     C                   KFLD                    FVRI
     C                   KFLD                    FVAVD
     C                   KFLD                    FVOPD
     C                   KFLD                    FVFBNR
     C                   KFLD                    FVLINR
N03  C     ZOKUFMK       KLIST
N03  C                   KFLD                    UNIK
N03  C                   KFLD                    FVFBNR
N03  C                   KFLD                    FVLINR
     C     DOKUFVFK      KLIST
     C                   KFLD                    FVAVD
     C                   KFLD                    FVOPD
     C                   KFLD                    FVFBNR
     C                   KFLD                    FVLINR
     C     *LIKE         DEFINE    FVRI          WFVRI
     C                   EVAL      WFVRI='F'
     C                   Z-ADD     1             X                 3 0
N03  C     ZOKUFVFK      KLIST
N03  C                   KFLD                    UNIK
N03  C                   KFLD                    FVFBNR
N03  C                   KFLD                    FVLINR
      *
     C   50              eval      Error = 'Invalid userID'
     C  N50              OPEN      DOKUFV
     C  N50              OPEN      DOKUFVF
     C  N50              OPEN      DOKUFM
N03  C  N50              OPEN      ZOKUFV2
N03  C  N50              OPEN      ZOKUFVF2
N03  C  N50              OPEN      ZOKUFM2

     c                   endsr
