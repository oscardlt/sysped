     H DATEDIT(*YMD)
     FTELL      UF   E           K DISK
     FEDIS      IF   E           K DISK
     FEDIM      IF   E           K DISK
     FECNI99A   IF   E           K DISK
     FECNT99A   IF   E           K DISK
     FEDTM99A   IF   E           K DISK
     FEEQD99A   IF   E           K DISK
     FELOC99A   IF   E           K DISK
     FETDT99A   IF   E           K DISK
     FEFTX99A   IF   E           K DISK
     FENAD99A   IF   E           K DISK
     FERFF99A   IF   E           K DISK
     FEGID99A   IF   E           K DISK
     FEMEA99A   IF   E           K DISK
     FEMOA99A   IF   E           K DISK
     FHEADF     O  A E             DISK
     FFRISOK    O  A E             DISK
     D A               S              1    DIM(79)
     D B               S              1    DIM(18)
     D TABA            S             35    DIM(12) CTDATA PERRCD(1)
     D TABB            S              8    DIM(12) ALT(TABA)
      *________________________
      * DS for decimal handling
      *""""""""""""""""""""""""
     D                 DS
     D  ZDEC                   1      7  6
     D  ZDEC6                  2      7
     D  ZHEL                  11     22  0
     D                 DS
     D  WMN2                   1      9  0
      */////////////
      * MAIN LOGIC /
      */////////////
      *___________
      * Initiering
      *"""""""""""
     C                   EXSR      INIT
      *________
      * CONVERT
      *""""""""
     C                   EXSR      RTVD
      *
     C                   MOVE      '1'           *INLR
      *
      *////////////////////////////////////////////////////////////
      *                                                      INIT /
      *////////////////////////////////////////////////////////////
     C     INIT          BEGSR
     C     KEYMS0        KLIST
     C                   KFLD                    ZMN
     C                   KFLD                    ZGN0
     C     KEYMS1        KLIST
     C                   KFLD                    ZMN
     C                   KFLD                    ZGN1
     C                   KFLD                    ZREPB0
     C     KEYMS2        KLIST
     C                   KFLD                    ZMN
     C                   KFLD                    ZGN2
     C                   KFLD                    ZREPC0
     C                   KFLD                    ZREPC1
      *
     C     *LIKE         DEFINE    NMN           ZMN
     C     *LIKE         DEFINE    NGN           ZGN0
     C     *LIKE         DEFINE    NGN           ZGN1
     C     *LIKE         DEFINE    NGN           ZGN2
     C     *LIKE         DEFINE    NREP0         ZREPB0
     C     *LIKE         DEFINE    NREP0         ZREPC0
     C     *LIKE         DEFINE    NREP1         ZREPC1
     C     *LIKE         DEFINE    HEHAWB        WEHAWB
     C     *LIKE         DEFINE    HETRI         WETRI
     C     *LIKE         DEFINE    HETRC         WETRC
     C     *LIKE         DEFINE    HETRCN        WETRCN
     C     *LIKE         DEFINE    HESDF         WESDF
     C     *LIKE         DEFINE    HEKNA         WEKNA
     C     *LIKE         DEFINE    HESDL         WESDL
      * INIT WORKFIELDS
     C                   MOVE      *BLANKS       WEHAWB
     C                   MOVE      *BLANKS       WETRI
     C                   MOVE      *BLANKS       WETRC
     C                   MOVE      *BLANKS       WETRCN
     C                   MOVE      *BLANKS       WESDF
     C                   MOVE      *ZEROS        WEKNA
     C                   MOVE      *BLANKS       WESDL
      *
     C     *ENTRY        PLIST
     C                   PARM                    WMN1              9
     C                   MOVEL     WMN1          WMN2
     C                   Z-ADD     WMN2          ZMN
      * Get msg and interchange values
     C     ZMN           CHAIN     EDIM                               51
     C     MSN           CHAIN     EDIS                               51
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      * RETREIVE INFO FROM EDIFACT DATABASE TO SYSPED400    RTVDB /
      *////////////////////////////////////////////////////////////
     C     RTVD          BEGSR
      *__________________
      * INIT. DATAFELTER
      *""""""""""""""""""
     C                   CLEAR                   HEADFR
     C                   Z-ADD     1             HEAVD
     C     HEAVD         CHAIN     TELL                               52
     C                   MOVE      TEOPDN        HEOPD
     C                   ADD       1             TEOPDN
     C                   UPDATE    TELLR
     C                   MOVE      'A'           HEUR
     C                   Z-ADD     1             HEKNA
     C                   MOVEL     'Expeditors'  HENAA
     C**                 MOVEL     's'           HESDLK
     C                   MOVEL     's'           HESDL
     C                   MOVE      C1004         HERFA
      *__________________
      * READ ALL SEGMENTS
      *""""""""""""""""""
     C                   Z-ADD     CREP0         ZREPB0
     C                   EXSR      FTX00
     C                   EXSR      CNT00
     C                   EXSR      RFF01
     C                   EXSR      DTM02
     C                   EXSR      NAD04
     C                   EXSR      TDT08
     C                   EXSR      LOC08
     C                   EXSR      MEA09
     C                   EXSR      RFF26
      *___________________
      * WRITE TO SYSPED400
      *"""""""""""""""""""
     C                   WRITE     HEADFR
      *________________
      * WRITE TO FRISOK
      *""""""""""""""""
     C                   CLEAR                   FRISOKR
     C                   Z-ADD     HEAVD         FSAVD
     C                   Z-ADD     HEOPD         FSOPD
     C                   MOVEL     '*CR'         FSKODE
     C                   MOVEL     HEGM1         FSSOK
     C                   WRITE     FRISOKR
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      * CNT00                                               CNT00 /
      *////////////////////////////////////////////////////////////
     C     CNT00         BEGSR
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     0             ZGN0
     C     KEYMS0        SETLL     ECNT99A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS0        READE     ECNT99A                                51
     C     *IN51         DOWEQ     '0'

     C                   SELECT
     C     C6069         WHENEQ    '11'
     C                   MOVEA     C6066         A
     C                   EXSR      GETNUM
     C                   Z-ADD     ZNUM          HENT
     C                   ENDSL

     C  N51KEYMS0        READE     ECNT99A                                51
     C  N51              ENDDO
     C                   ENDIF

     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      * DTM02                                               DTM02 /
      *////////////////////////////////////////////////////////////
     C     DTM02         BEGSR
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     2             ZGN0
     C     KEYMS0        SETLL     EDTM99A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS0        READE     EDTM99A                                51
     C     *IN51         DOWEQ     '0'

     C                   SELECT
     C     D2005         WHENEQ    '178'
     C                   MOVEL     D2380         HEDTOP
     C                   ENDSL

     C  N51KEYMS0        READE     EDTM99A                                51
     C  N51              ENDDO
     C                   ENDIF

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * LOC08                                               LOC08 /
      *////////////////////////////////////////////////////////////
     C     LOC08         BEGSR
     C                   MOVE      '0'           *IN52
     C                   Z-ADD     8             ZGN1
     C     KEYMS1        SETLL     ELOC99A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS1        READE     ELOC99A                                52
     C     *IN52         DOWEQ     '0'
      *
     C                   SELECT
     C     L3227         WHENEQ    '76'
     C                   MOVEL     L3225         HESDF
     C     L3227         WHENEQ    '8'
     C                   MOVEL     L3225         HESDT
     C                   ENDSL
      *
     C     KEYMS1        READE     ELOC99A                                52
     C  N52              ENDDO
     C                   ENDIF
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      * NAD04                                               NAD04 /
      *////////////////////////////////////////////////////////////
     C     NAD04         BEGSR
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     4             ZGN0
     C     KEYMS0        SETLL     ENAD99A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS0        READE     ENAD99A                                51
     C     *IN51         DOWEQ     '0'
      *
     C                   SELECT
     C     N3035         WHENEQ    'IM'                                         Importør
     C     N3124A        IFNE      *BLANKS
     C                   MOVEL     N3124A        HENAK
     C                   ENDIF
     C     N3124B        IFNE      *BLANKS
     C                   MOVEL     N3124B        HEADK1
     C                   ENDIF
     C     N3124C        IFNE      *BLANKS
     C                   MOVEL     N3124C        HEADK2
     C                   ENDIF
     C     N3124D        IFNE      *BLANKS
     C                   MOVEL     N3124D        HEADK3
     C                   ENDIF
     C     N3036A        IFNE      *BLANKS
     C                   MOVEL     N3036A        HENAK
     C                   ENDIF
     C     N3042A        IFNE      *BLANKS
     C                   MOVEL     N3042A        HEADK1
     C                   ENDIF
     C     N3251         IFNE      *BLANKS
     C                   MOVEL     N3251         WEADS1            9
     C                   MOVEL     N3164         WEADS2           21
     C                   MOVEL     WEADS1        HEADK3
     C                   MOVE      WEADS2        HEADK3
     C                   MOVEL     N3251         HESDVT
     C                   MOVE      ' '           HESDVT
     C                   MOVEL     'NO'          HELKK
     C                   ENDIF
      *
     C     N3035         WHENEQ    'OS'                                         Avsender
     C     N3124A        IFNE      *BLANKS
     C                   MOVEL     N3124A        HENAS
     C                   ENDIF
     C     N3124B        IFNE      *BLANKS
     C                   MOVEL     N3124B        HEADS1
     C                   ENDIF
     C     N3124C        IFNE      *BLANKS
     C                   MOVEL     N3124C        HEADS2
     C                   ENDIF
     C     N3124D        IFNE      *BLANKS
     C                   MOVEL     N3124D        HEADS3
     C                   ENDIF
     C     N3036A        IFNE      *BLANKS
     C                   MOVEL     N3036A        HENAS
     C                   ENDIF
     C     N3042A        IFNE      *BLANKS
     C                   MOVEL     N3042A        HEADS1
     C                   ENDIF
     C     N3251         IFNE      *BLANKS
     C                   MOVEL     N3251         WEADS1            9
     C                   MOVEL     N3164         WEADS2           21
     C                   MOVEL     WEADS1        HEADS3
     C                   MOVE      WEADS2        HEADS3
     C                   MOVEL     N3251         HESDFF
     C                   MOVE      ' '           HESDFF
     C                   ENDIF
     C     N3207         IFNE      *BLANKS
     C                   MOVEL     N3207         HELKS
     C                   ENDIF
      *
     C                   ENDSL
      * HENT RFF
     C                   Z-ADD     6             ZGN1
     C                   Z-ADD     NREP0         ZREPC0
     C     KEYMS1        CHAIN     ERFF99A                            52
     C     *IN52         IFEQ      *OFF
     C                   SELECT
     C     R1153         WHENEQ    'IT'
     C                   ENDSL
     C                   ENDIF
      *
     C     KEYMS0        READE     ENAD99A                                51
     C  N51              ENDDO
     C                   ENDIF
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      * RFF01                                               RFF01 /
      *////////////////////////////////////////////////////////////
     C     RFF01         BEGSR
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     1             ZGN0
     C     KEYMS0        SETLL     ERFF99A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS0        READE     ERFF99A                                51
     C     *IN52         DOWEQ     '0'
      *
     C     R1153         IFEQ      'ZZZ'
     C                   MOVEL     R1154         HEGM1
     C                   ENDIF
      *
     C  N51KEYMS0        READE     ERFF99A                                51
     C  N51              ENDDO
     C                   ENDIF
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      * RFF26                                               RFF26 /
      *////////////////////////////////////////////////////////////
     C     RFF26         BEGSR
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     26            ZGN0
     C     KEYMS0        SETLL     ERFF99A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS0        READE     ERFF99A                                51
     C     *IN52         DOWEQ     '0'
      *
     C                   SELECT
     C     R1153         WHENEQ    'MB'
     C                   MOVEL     R1154         HEGM2
     C     R1153         WHENEQ    'BH'
     C                   MOVEL     R1154         HERFA
     C                   ENDSL
      *
     C  N51KEYMS0        READE     ERFF99A                                51
     C  N51              ENDDO
     C                   ENDIF
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      * FTX00                                               FTX00 /
      *////////////////////////////////////////////////////////////
     C     FTX00         BEGSR
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     0             ZGN0
     C     KEYMS0        SETLL     EFTX99A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS0        READE     EFTX99A                                51
     C     *IN51         DOWEQ     '0'

     C                   SELECT
     C     F4451         WHENEQ    'AAA'
     C                   MOVEL(P)  'KLL'         HEVS1
     C                   CAT       F4440A:1      HEVS1
     C     F4451         WHENEQ    'ADU'
     C                   MOVEL     F4440A        HEVS2
     C                   ENDSL

     C  N51KEYMS0        READE     EFTX99A                                51
     C  N51              ENDDO
     C                   ENDIF

     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      * MEA09                                               MEA09 /
      *////////////////////////////////////////////////////////////
     C     MEA09         BEGSR
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     9             ZGN0
     C     KEYMS0        SETLL     EMEA99A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS0        READE     EMEA99A                                51
     C     *IN51         DOWEQ     '0'
      *
     C                   MOVEA     M6314         A
     C                   EXSR      GETNUM
     C                   SELECT
     C     M6311         WHENEQ    'WT '
     C                   ADD       ZNUM          HEVKT
     C                   ENDSL
      *
     C     KEYMS0        READE     EMEA99A                                51
     C  N51              ENDDO
     C                   ENDIF
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      * TDT08                                               TDT08 /
      *////////////////////////////////////////////////////////////
     C     TDT08         BEGSR
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     8             ZGN0
     C     KEYMS0        SETLL     ETDT99A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS0        READE     ETDT99A                                51
     C     *IN51         DOWEQ     '0'

     C                   MOVEL     T8213         HEHAWB

     C  N51KEYMS0        READE     ETDT99A                                51
     C  N51              ENDDO
     C                   ENDIF

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Get numerical value                               GETNUM /
      *////////////////////////////////////////////////////////////
     C     GETNUM        BEGSR
     C                   MOVE      *ZEROS        ZHEL
     C                   MOVE      *ZEROS        ZDEC
     C                   MOVE      *ZEROS        B
     C                   Z-ADD     1             AX                2 0
     C                   Z-ADD     1             BX                2 0
      * Get Integer
     C     A(AX)         DOWGE     '0'
     C     ZHEL          MULT      10            ZHEL
     C                   MOVE      A(AX)         ZHEL
     C                   ADD       1             AX
     C                   ENDDO
      * Decimal point ?
     C     A(AX)         IFEQ      '.'
     C     A(AX)         OREQ      ','
     C                   ADD       1             AX
     C     A(AX)         DOWGE     '0'
     C                   MOVE      A(AX)         B(BX)
     C                   ADD       1             AX
     C                   ADD       1             BX
     C                   ENDDO
     C                   MOVEA     B             ZDEC6
     C                   ENDIF
      * Concatenate
     C                   Z-ADD     ZHEL          ZNUM             18 6
     C                   ADD       ZDEC          ZNUM
     C                   ENDSR
**
FDY                                00091970
FODE047373                         00000000
FODE070450                         00000000
FODE072110                         00091910
FODE072819                         00091920
FODE073371                         00000000
FODE074909                         00091900
FODE133088                         00000000
FONL017040                         00091400
03332351450500037                  00090360
03339319386700024                  00090575
03339319566402615                  00090570
