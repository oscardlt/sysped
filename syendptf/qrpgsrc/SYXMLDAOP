********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
12XXX * 01 SH  PTF12 'F' i moskode
12XXX * 02 SH  unngå doble fakturaer
12XXX * 03 SH  rettelse på 01
12XXX * 04 SH  skriv til loggfil
12XXX * 05 SH  endret skriv track & trace
12XXX * 06 SH  få med foretaksnummer i katxt
12165 * 07 CB  Send status med belöp til CW1
      * 08 SH  lagt til mnd år til logg
********************************************************************
      ***********************************************************
      *                                                        **
      *** PROGRAM SKAL KONVERTER XML-FIL TIL mottak actual cost *
      ***********************************************************
      *
N04  FKOSTL     O  A E             DISK
N04  FLEVEL01   IF   E           K DISK
     FHEADF     IF   E           K DISK
     FSADH      IF   E           K DISK
     F                                     RENAME(SADHR:SADHRX)
     FFPXREF1   IF   E           K DISK
     FFPXREF    IF   E           K DISK
     F                                     RENAME(FPXREFR:FPXREF0)
     FSADH12B   IF A E           K DISK
     FSAEH12B   IF A E           K DISK
     FFIRMDAG   IF   E           K DISK
     FXMLRCVF   IF   E           K DISK
N02  FXMLRCVL01 IF   E           K DISK
N02  F                                     RENAME(xmlrcvfr:xmlrcvr01)
     FFIRM      IF   E           K DISK
     Fkostt     uf   e           k DISK
     FKOSTA     O  A E           K DISK
N02  FKOSTA2    IF   E           K DISK
N02  F                                     RENAME(kostar:kostar2)
     FKOSTB     O  A E           K DISK
     FTRACKF    O  A E           K DISK
N07  FFRISOK    IF   E           K DISK
     D                 DS
     D  XTXT                   1     15
     D  XT                     1     15    DIM(15)
     D                 DS
     D  XTAL                   1     15  3
     D  XTALH                  1     12  0
     D  XTALD1                13     13
     D  XTALD2                14     14
     D  XTALD3                15     15
      * gedato
      * 2016-09-26T15:37:58
     D                 DS
     D  GEDATO                 1     19
     D  AAR                    1      4
     D  MND                    6      7
     D  DAG                    9     10
     D  TT                    12     13
     D  MM                    15     16
     D  SS                    18     19
     D                 DS
     D  GEDATOD                1     19
     D  AARD                   1      4
     D  MNDD                   6      7
     D  DAGD                   9     10
      *
     D                 DS
     D  DATOU                  1      8  0
     D  AARU                   1      4  0
     D  MNDU                   5      6  0
     D  DAGU                   7      8  0
     D                 DS
     D  DATOUD                 1      8  0
     D  AARUD                  1      4  0
     D  MNDUD                  5      6  0
     D  DAGUD                  7      8  0
      *
     D                 DS
     D  Xtime                  1     14  0
     D  Xtm                    1      6  0
     D  Xdg                    7      8  0
     D  Xmn                    9     10  0
     D  Xar                   11     14  0
     D                 DS
     D  kadte                  1      8  0
     D  KAARE                  1      4  0
     D  KAMNE                  5      6  0
     D  KADGE                  7      8  0
n04  D                SDS
n04  D  uusr                 254    263
      *
     C                   EXSR      INIT
      *
     C                   EXSR      SRHEAD
      *
     C                   EVAL      *INLR = *ON
     C                   RETURN
      *
      ***********************************************
     C     SRHEAD        BEGSR
      ***********************************************
     C                   clear                   kostar
     C                   Z-ADD     DGLEVN        KALNR
     c                   eval      kamva='F'
     c                   eval      kavk=DGGEB1
     C                   Z-ADD     100           KAVKU
     C                   Z-ADD     100           KAOMRF
      * hente bilagsnr
     c     DGSERI        chain     kostt                              44
     c     *in44         ifeq      '0'
     c                   z-add     ktnr          kabnr2
     c                   add       1             ktnr
     c                   update    kosttr
     c                   end
      * HENTE INNREGNR
     C     'Ø'           CHAIN     KOSTT                              20
     C     *IN20         IFEQ      '0'
     C                   Z-ADD     KTNR          KABNR
     C                   ADD       1             KTNR
     C                   UPDATE    KOSTTR
     C                   ENDIF
      * hente dato tid
     c                   time                    xtime
     C                   EVAL      KAUSER=DGATT
     C                   MOVE      DGATT         KASG
     C                   MOVE      XTM           KATME
     C                   MOVE      XDG           KADGE
     C                   MOVE      XMN           KAMNE
     C                   MOVE      XAR           KAARE
     C                   MOVE      KADTE         KADTR
     C                   MOVE      KATME         KATDR
     C                   MOVE      *ZEROS        KAJOUR
     C                   MOVE      ' '           KAST
     C     XMLKEY01      SETLL     XMLRCVF
     C                   DOW       *IN33 = *OFF                                 1<-B
     C     XMLKEY01      READE     XMLRCVF                                33
     C     *in33         ifeq      '0'
     C                   SELECT                                                  2<-B
n06   * foretaksnummer
n06  C                   WHEN      XRNV = 'PartyId'
n06  c                   movel     xrverd        katxt
      * invoicetype 380 faktura 381 kreditnota
     C                   WHEN      XRNV = 'InvoiceType'
     c                   move      *blanks       invtyp            3
     c                   eval      invtyp=XRVERD
      * InvoiceNumber
     C                   WHEN      XRNV = 'InvoiceNumber'
     C                   EVAL      KAFNR = XRVERD
      * InvoiceDate
     C                   WHEN      XRNV = 'InvoiceDate'
     C                   EVAL      GEDATO  = XRVERD
     C                   move      aar           aaru
     C                   move      mnd           mndu
     C                   move      dag           dagu
     c                   move      datou         kabdt
     c                   move      datou         kafdt
     c                   z-add     20            kapcc
     c                   z-add     mndu          kapmn
     c                   z-add     aaru          kapÅR
      * Duedate
     C                   WHEN      XRNV = 'DueDate'
     C                   EVAL      GEDATOD  = XRVERD
     C                   move      aard          aarud
     C                   move      mndd          mndud
     C                   move      dagd          dagud
     c                   move      datoud        kaffdt
      * Currency
     C                   WHEN      XRNV = 'Currency'
     C                   EVAL      KAVAL   = XRVERD
      * KidNumber
     C                   WHEN      XRNV = 'KidNumber'
     C                   EVAL      KALKID  = XRVERD
      * BaseItemDetails detaler
     C                   WHEN      XRNV = 'BaseItemDetails'                      2<-B
     C                   EXSR      LINJER
      * LineItemTotalsAmount
     C                   WHEN      XRNV = 'LineItemTotalsAmount'                 2<-B
     C                   MOVEL     XRVERD        XTXT
     C                   EXSR      KNVTAL
     C                   Z-SUB     XTAL          KABL
     C                   ENDSL                                                   2<-E
      *
     c                   endif
     c                   enddo
      *
      *
     C                   write     kostar
N04  c                   EXSR      LOGGSR
      *
     C                   ENDSR
      *
      *
      ***********************************************
     C     LINJER        BEGSR
      ***********************************************
      *
     C     XMLKEY01D     KLIST
     C                   KFLD                    XRSN
     C                   KFLD                    XRLEV1
     C                   KFLD                    XRLEV2
     C                   KFLD                    XRLEV3
     c                   clear                   kostbr
     C                   z-add     kabnr         kbbnr
     c                   move      kasg          kbsg
s03  C*                  move      'F'           KBKDMV
N03  C                   move      'F'           KBKDPF
      *
     C     XMLKEY01D     setll     XMLRCVF
     C                   setoff                                       44
     c     *IN44         doweq     '0'
     c     XMLKEY01D     reade     XMLRCVF                                44
      *
     c     *in44         ifeq      '0'
      *
      *
     C                   SELECT                                                  2<-B
      * LineItemNum         ekspedisjonssted
     C                   WHEN      XRNV = 'LineItemNum'                          2<-B
     C                   MOVEL     XRVERD        XTXT
     C                   EXSR      KNVTAL
     C                   Z-ADD     XTAL          SITLEN            6 0
     C                   MOVE      SITLEN        SITLE
      * SuppliersProductId  Tollnummer
     C                   WHEN      XRNV = 'SuppliersProductId'                   2<-B
     C                   MOVEL     XRVERD        XTXT
     C                   EXSR      KNVTAL
     C                   Z-ADD     XTAL          SITLLN           10 0
     C                   MOVE      SITLLN        SITLL
      * EDIREF1  kbrefa
     C
     C                   EVAL      KBREFA=%trimr(SITLE)
     C                                  + %trimr(SITLL)
      * LineItemGrossAmount    beløp
     C                   WHEN      XRNV = 'LineItemGrossAmount'                  2<-B
     C                   MOVEL     XRVERD        XTXT
     C                   EXSR      KNVTAL
     C                   Z-ADD     XTAL          TOL              13 2
      * VatAmount    beløp
     C                   WHEN      XRNV = 'VatAmount'                            2<-B
     C                   MOVEL     XRVERD        XTXT
     C                   EXSR      KNVTAL
     C                   Z-ADD     XTAL          VAT              13 2
     c     tol           ifne      *zeros
     C     TOL           SUB       VAT           TOL
     c                   end
     C                   ENDSL
     c                   endif
     c                   enddo
      *
     C     XMLKEY01D     setgt     XMLRCVF
      *
      *  hent sad
      *
     C     keysad        klist
     c                   kfld                    sitle
     c                   kfld                    sitll
     c     keysad        chain     sadh12b                            44
     c     *in44         ifeq      '0'
      * omberegning
     c     SITDN         iflt      0
     c                   z-sub     sitdn         sitdn
     c                   end
     c                   move      siavd         kbavd
     c                   move      sitdn         kbopd
     c                   z-add     siknk         kbkn
N07  C                   EXSR      N07SRI
     c                   else
     c     keysad        chain     saeh12b                            44
     c     *in44         ifeq      '0'
      * omberegning
     c     SETDN         iflt      0
     c                   z-sub     setdn         setdn
     c                   end
     c                   move      seavd         kbavd
     c                   move      setdn         kbopd
     c                   z-add     sekns         kbkn
N07  C                   EXSR      N07SRE
     c                   end
     c                   end
      *
     c     kbavd         ifeq      0
     c                   z-add     9             kbnØkk
     c                   else
s05  c*                  exsr      ttsr
     c                   end
     c                   eval      kavk=DGGEB1
     C                   Z-ADD     100           KAVKU
     C                   Z-ADD     100           KAOMRF
     C     TOL           IFNE      *ZEROS
     C                   Z-ADD     TOL           KBBLHB
     c                   eval      kBvk=DGGEB2
      * fordeles ved forenklet prosedyre
     C     FPKEY1        CHAIN     FPXREF1                            35
     C   35FPKEY0        CHAIN     FPXREF                             35
     c     *in35         ifeq      '0'
     c                   z-add     kbblhb        forbel           11 2
     c                   move      'T'           fpgeby            1
     c                   exsr      fordel
     c                   else
     C                   write     kostbr
n05  c                   exsr      ttsr
     c                   end
     C                   END
     C     VAT           IFNE      *ZEROS
     C                   Z-ADD     VAT           KBBLHB
     c                   eval      kBvk=DGGEB3
      * fordeles ved forenklet prosedyre
     C     FPKEY1        CHAIN     FPXREF1                            35
     C   35FPKEY0        CHAIN     FPXREF                             35
     c     *in35         ifeq      '0'
     c                   z-add     kbblhb        forbel           11 2
     c                   move      'M'           fpgeby            1
     c                   exsr      fordel
     c                   else
     C                   write     kostbr
n05  c                   exsr      ttsr
     c                   end
     C                   END
      *
     C                   ENDSR
      *
      *
      ******************************************************************
     c     init          begsr
      ******************************************************************
N02  C     kosta2k       klist
N02  c                   kfld                    dglevn
N02  c                   kfld                    kafnr
n02  c                   kfld                    uaar
n02  c                   move      uyear         uaar              2 0
N02  C     dupk          klist
N02  c                   kfld                    xrsn
N02  c                   kfld                    xrnv
      *
     C     FPKEYN        KLIST
     C                   KFLD                    FPAVD
     C                   KFLD                    FPOPD
     C     FPKEY1        KLIST
     C                   KFLD                    sta
     C                   KFLD                    KBAVD
     C                   KFLD                    KBOPD
     C     FPKEY0        KLIST
     C                   KFLD                    KBAVD
     C                   KFLD                    KBOPD
     C     FPKEY2        KLIST
     C                   KFLD                    sta
     C                   KFLD                    FPAVDH
     C                   KFLD                    FPOPDH
     c                   move      'O'           sta               1
     C     XMLKEY01      KLIST
     C                   KFLD                    XRSN
     C     *ENTRY        PLIST
     C                   PARM                    USN               9
     C                   MOVE      USN           XRSN
      *
     c                   read      firm                                   33
     c     fifirm        chain     firmdag                            33
     c                   time                    xtime
     C                   MOVE      XTM           KATME
     C                   MOVE      XDG           KADGE
     C                   MOVE      XMN           KAMNE
     C                   MOVE      XAR           KAARE
N02  C                   exsr      dupsr
      *
      *
      * VARIABLE VAT
     C                   MOVE      FITAX         FITAXB            5 4
     C     FITAXD        IFNE      *ZEROS
     C     FITAX2        ANDNE     *ZEROS
     C     KADTE         ANDLT     FITAXD
     C                   MOVE      FITAX2        FITAXB            5 4
     C                   END
     c                   add       1             FITAXB
     c                   endsr
      ***********************************************
     C     KNVTAL        BEGSR
      ***********************************************
      * test for negativ verdi
      *
     c                   move      'N'           negativ           1
     c     xt(1)         ifeq      '-'
     c                   move      'J'           negativ
     c                   move      xtxt          xtxt14           14
     c                   move      *blanks       xtxt
     c                   movel     xtxt14        xtxt
     c                   end
      *
     C                   MOVE      *ZEROS        XTAL
     C     1             DO        15            XN                3 0          1<-B
     C                   IF        ((XT(XN) = *BLANKS) OR (XT(XN) = '.'))        2<-B
     C                   LEAVE
     C                   ENDIF                                                   2<-E
     C                   MULT      10            XTALH
     C                   MOVE      XT(XN)        XTALH
     C                   ENDDO                                                  1<-E
      *
     C                   IF        XT(XN) = '.'                                 1<-B
     C                   ADD       1             XN
     C                   MOVE      XT(XN)        XTALD1
     C                   ADD       1             XN
     C                   IF        XT(XN) <> ' '                                 2<-B
     C                   MOVE      XT(XN)        XTALD2
     C                   ADD       1             XN
     C                   IF        XT(XN) <> ' '                                  3<-B
     C                   MOVE      XT(XN)        XTALD3
     C                   END                                                      3<-E
     C                   END                                                     2<-E
     C                   END                                                    1<-E
      * negativ
     c     negativ       ifeq      'J'
     c                   z-sub     xtal          xtal
     c                   end
     C                   ENDSR
     C***********************************************
     C     TTSR          BEGSR
     C***********************************************
      * mottak kosnad EYESHARE
n05  c     kbblhb        ifne      0
     C                   MOVE      KBAVD         TTAVD
     C                   MOVE      KBOPD         TTOPD
     C                   MOVE      'EDI'         TTACTI
     C                   MOVE      KADTE         TTDATE
     C                   MOVE      XTM           TTTIME
     C                   MOVE      KADTE         TTDATL
     C                   MOVE      XTM           TTTIML
     C                   EVAL      TTUSER='EDI'
      *
s05  C*                  MOVEL     'Received'    XXTEXT           16
s05  C*                  MOVE      ' costs  '    XXTEXT
s05  C*                  MOVEL     'from TOL'    XXTEXT2          16
s05  C*                  MOVE      'L       '    XXTEXT2
s05  C*                  movel     XXTEXT        XXTEXT3          32
s05  C*                  move      XXTEXT2       XXTEXT3          32
s05  C*                  MOVEL     XXTEXT3       TTTEXT
     C                   eval      TTTEXT = 'Received costs from' +
     C                             ' TOLL:  ' +
     C                             %trim(%editc(KBBLHB:'J'))
     C                   eval      TTTEXL = 'Mottatt kostnader' +
     C                             ' fra TOLL:  ' +
     C                             %trim(%editc(KBBLHB:'J'))
      *
      *
      *
     C                   WRITE     TRACKFR
n05  c                   end
     c                   endsr
      *
      ***************************************************************
     c     fordel        begsr
      ***************************************************************
      *
     c                   setoff                                       35
     c     FPKEY2        setll     fpxref1
     c     *in(35)       doweq     '0'
     c     FPKEY2        READE     fpxref1                                35
     c     *in(35)       IFEQ      '0'
      *
     C     FPKEYN        CHAIN     SADH                               34
      *
     C  N34              eval      kbfree=sinak
      * legger avd opd på kostb posten
     C     FPKEYN        CHAIN     HEADF                              34
     c                   z-add     siavd         kbavd
     c                   z-add     sitdn         kbopd
     C                   Z-ADD     0             KBNØKK
     C   34              Z-ADD     9             KBNØKK
      *
      *
      * toll
     C     FPGEBY        IFEQ      'T'
     c     sibel7        sub       sibel6        toll             11 0
     C                   Z-ADD     TOLL          kbblhb
     C                   ELSE
      * mva
     C                   Z-ADD     SIBEL6        MOMS             11 0
     C                   Z-ADD     MOMS          KBBLHB
     c                   END
      *
     c     kbblhb        ifne      0
     C                   WRITE     KOSTBR
n05  c                   exsr      ttsr
     c                   end
      * reduser fordelingssum
     c                   sub       kbblhb        forbel
     c                   end
     c                   end
     C     FORBEL        IFNE      0
     C                   z-add     forbel        kbblhb
     c                   z-add     FPAVDH        kbavd
     c                   z-add     FPOPDH        kbopd
     C
     C                   eval      kbfree='**RESTBELØP**'
     C                   Z-ADD     9             KBNØKK
     C                   WRITE     KOSTBR
n05  c                   exsr      ttsr
     c                   end
     c                   endsr
N02   ***********************************************
N02  C     DUPSR         BEGSR
N02   ***********************************************
N02  C                   EVAL      xrnv='InvoiceNumber'
N02  c     dupk          chain     xmlrcvl01                          34
N02  c     *in34         ifeq      '0'
N02  C                   EVAL      KAFNR = XRVERD
N02  c     kosta2k       chain     kosta2                             34
N02  c     *in34         ifeq      '0'
N02  c                   seton                                        LR
N02  c                   return
N02  c                   end
N02  c                   end
N02  c                   endsr
N04  C***********************************************
N04  C     LOGGSR        BEGSR
N04  C***********************************************
N04  c     levek         klist
N04  c                   kfld                    fifirm
N04  c                   kfld                    kalnr
N04  c     levek         chain     level01                            44
N04  c     *in44         ifeq      '0'
N04  C                   CALL      'SYGE15C'
N04  C                   PARM                    WDT               8
N04  C                   PARM                    WTM               6
N04  c                   move      wdt           LODATO
N04  c                   move      wtm           loklok
N04  c                   eval      louser=uusr
N04  c                   move      kabnr         lounik
N04  c                   move      kabnr2        lobila
n04  c                   move      'E'           lofoan
n04  c                   move      *zeros        lojour
n04  c                   move      kasg          losg
n04  c
N04  C                   eval      lotxt='Mottat TOLL'
N04  c                             +'Faktura fra:'
N04  c                             +' ALTINN'
n08  c                   move      kapÅr         loaar
n08  c                   move      kapmn         lomnd
n04  c                   write     kostlr
n04  c                   end
n04  c                   endsr
N07  C************************************************
N07  C     N07SRI        BEGSR
N07  C************************************************
N07  C     FRIKEN07      KLIST
N07  C                   KFLD                    SIAVD
N07  C                   KFLD                    SITDN
N07  C                   KFLD                    FSKODN07          3
N07  C                   MOVE      'N07'         FSKODN07
N07  C     FRIKEN07      CHAIN     FRISOK
N07  C                   IF        %FOUND(FRISOK)
     C                   DUMP
N07  C                   CALL      'SYXML48NOI'
N07  C                   PARM                    FSSOK
N07  C                   PARM                    KAFNR
N07  C                   PARM                    TOL
N07  C                   PARM                    VAT
N07  C                   ENDIF
N07  C                   ENDSR
N07  C************************************************
N07  C     N07SRE        BEGSR
N07  C************************************************
N07  C     FRIKECW2      KLIST
N07  C                   KFLD                    SEAVD
N07  C                   KFLD                    SETDN
N07  C                   KFLD                    FSKODN07          3
N07  C                   MOVE      'N07'         FSKODN07
N07  C     FRIKECW2      CHAIN     FRISOK
N07  C                   IF        %FOUND(FRISOK)
N07  C                   CALL      'SYXML48NOE'
N07  C                   PARM                    FSSOK
N07  C                   PARM                    KAFNR
N07  C                   PARM                    TOL
N07  C                   PARM                    VAT
N07  C                   ENDIF
N07  C                   ENDSR
