********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
11XXX * 01 SH  PTF12 Ny parameter
********************************************************************
      * BASERT PÅ SAD003
     FSYEDILFM  CF   E             WORKSTN
     F                                     SFILE(SAD003A:ZRECNO)
     F                                     SFILE(SAD003E:ÆRECNO)
     FEDIM2     IF   E           K DISK
     FTVINF     IF   E           K DISK
     FEDIS      IF   E           K DISK
      *//////////////
      *  Hodelogikk /
      *//////////////
     C                   EXSR      SRA
     C                   EXSR      SRB
     C                   MOVE      '1'           *INLR
      *////////////////////////////////////////////////////////////
      *  Initiering                                           SRA /
      *////////////////////////////////////////////////////////////
     C     SRA           BEGSR
      *___________________
      * Definiere Workfelt
      *"""""""""""""""""""
     C     *LIKE         DEFINE    M0068A        W0068A
     C     *LIKE         DEFINE    M0068B        W0068B
     C     *LIKE         DEFINE    M0068C        W0068C
     C     *LIKE         DEFINE    FLN           WLN
     C     *LIKE         DEFINE    *IN03         WIN03
     C     *LIKE         DEFINE    *IN30         WIN30
     C                   MOVE      *ZEROS        W0068A
     C                   MOVE      *ZEROS        W0068B
     C                   MOVE      *ZEROS        W0068C
     C                   MOVE      *ZEROS        WLN
      *__________________
      * Definiere Nøkkler
      *""""""""""""""""""
     C     KEYA          KLIST
     C                   KFLD                    WAVD
     C                   KFLD                    WTDN
     C                   KFLD                    W0068A
     C                   KFLD                    W0068B
     C                   KFLD                    W0068C
     C     KEYA1         KLIST
     C                   KFLD                    WAVD
     C                   KFLD                    WTDN
     C     KEYB          KLIST
     C                   KFLD                    MMN
     C                   KFLD                    WLN
     C     KEYC          KLIST
     C                   KFLD                    MSN
      *_________________
      * Input parametrer
      *"""""""""""""""""
     C     *ENTRY        PLIST
     C     WAVD          PARM                    WAVD
     C     WTDN          PARM                    WTDN
     C                   PARM                    WFBNR
     C                   PARM                    WMLDTY            6
     C                   PARM                    WTSTSP            1
     C     *IN29         PARM      *IN03         WIN03
      * 29 AV  -->  TVINN-IMPORT
      * 29 PÅ  -->  TVINN-EXPORT
      * TEST (RETUR='1' HVIS EN FINNES OG IKKE KVITTERT OMKJØRING)
     C     WTSTSP        COMP      'T'                                    50
      * FRAKTBREVSNR GITT SOM INNPARAMETER.
     C     WFBNR         COMP      *ZEROS                             5151
      *
     C*
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Bearbeide Formater                                   SRB /
      *////////////////////////////////////////////////////////////
     C     SRB           BEGSR
      *
     C     SRB1          TAG
      *______________
      * Tømme Subfile
      *""""""""""""""
     C                   MOVE      '1'           *IN93
     C                   WRITE     SAD003B
     C                   MOVEA     '000'         *IN(93)
     C                   Z-ADD     *ZERO         ZRECNO
     C     *IN50         IFEQ      *ON
     C                   MOVE      '0'           *IN53
     C                   MOVE      'T'           WTSTSP                          IKKE FEIL
     C                   END
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     KEYA          SETLL     EDIM2
     C     SRB2          TAG
     C     KEYA1         READE     EDIM2                                  94
     C  N94              DO
     C     WMLDTY        IFNE      '*ALL  '
     C     WMLDTY        ANDNE     M0065
     C                   GOTO      SRB2
     C                   END
     C     WFBNR         IFNE      MFFBNR
     C                   GOTO      SRB2
     C                   END
      * HVIS CUSDEC - SKILL IMP/EKSP.
     C     WMLDTY        IFEQ      'CUSDEC'
     C  N29M1001         IFNE      '929'
     C                   GOTO      SRB2
     C                   END
     C   29M1001         IFNE      '830'
     C                   GOTO      SRB2
     C                   END
     C                   END
     C     MSR           COMP      'R'                                    27
     C     MSR           IFEQ      'S'
     C     MST           ANDEQ     ' '
     C                   MOVE      '1'           *IN53                            ÅPEN!!
     C                   END
     C                   ADD       1             ZRECNO
     C                   MOVEL     M0004         W0004
     C                   MOVEL     M0010         W0010
     C                   WRITE     SAD003A
     C                   GOTO      SRB2
     C                   END
      *
      *
     C     ZRECNO        CABEQ     0             SRB9
      *
      *
      * HVIS TEST AV TYPE OG DE FINNES NOE....  FEILM (HVIS IKKE F23)
     C     WTSTSP        IFEQ      'T'
     C                   MOVE      '1'           WTSTSP
     C                   END
     C                   Z-ADD     1             ZRECNO
      *_______________
      * Behandle bilde
      *"""""""""""""""
     C     SRB3          TAG
      *
     C                   WRITE     WINDOWSZ
      *
     C                   MOVEA     '11'          *IN(91)
     C  N30              MOVE      'SY00000'     ZMSGNO
     C*                    WRITESAD003Ø
     C                   WRITE     SAD003C
     C                   EXFMT     SAD003B
      *
     C     *IN23         IFEQ      '1'                                           OVERSTYRT =
     C     *IN53         ANDEQ     '0'                                           ÅPEN....
     C                   MOVE      'T'           WTSTSP                          IKKE FEIL
     C                   GOTO      SRB9
     C                   END
      *
     C     *IN03         IFEQ      '0'
     C     *IN12         ANDEQ     '0'
      *
     C                   MOVEA     '00'          *IN(91)
      *_____________
      * F5=Frisk opp
      *"""""""""""""
     C     *IN05         CABEQ     '1'           SRB1
      *_______
      * Valg ?
      *"""""""
     C                   MOVE      '0'           *IN52
     C                   MOVE      '0'           *IN30
     C                   MOVE      '0'           *IN95
     C     *IN52         DOWEQ     '0'
      *
     C                   READC     SAD003A                                52
     C     *IN52         IFEQ      '0'
     C     ZOP           IFEQ      '4'
     C                   EXSR      SRBDEL
     C                   GOTO      SRB1
     C                   END
     C     ZOP           CASEQ     '5'           SRBE
     C     ZOP           CASEQ     '6'           SRBF
     C     ZOP           CASEQ     '7'           SRBG
     C     ZOP           CASEQ     '9'           SRBH
     C                   END
     C                   END
      *
     C  N30
     CANN52              END
      *
     C     *IN03         CABEQ     '1'           SRB9
     C     *IN95         CABEQ     '1'           SRB1
     C     *IN95         CABEQ     '0'           SRB3
     C                   END
      *
     C     SRB9          ENDSR
      *////////////////////////////////////////////////////////////
      *  Vise hele meldingsloggen.                           SRBE /
      *////////////////////////////////////////////////////////////
     C     SRBE          BEGSR
     C                   MOVE      '0'           *IN28
     C                   MOVE      'SY00000'     ZMSGNO
     C                   WRITE     SAD003Ø
     C     SRBE1         TAG
     C                   MOVEA     '00'          *IN(25)
     C                   MOVE      WAVD          MAVD
     C                   MOVE      WTDN          MTDN
     C  N28              EXFMT     SAD003D
     C   28              EXFMT     SAD003I
     C     *IN25         IFEQ      '1'
     C                   MOVE      '1'           *IN28
     C                   GOTO      SRBE1
     C                   END
     C     *IN26         IFEQ      '1'
     C                   MOVE      '0'           *IN28
     C                   GOTO      SRBE1
     C                   END
      *_________________
      * Oppdatere SUBFIL
      *"""""""""""""""""
     C     SRBE9         TAG
     C                   MOVE      *BLANKS       ZOP
     C     MSR           COMP      'R'                                    27
     C                   UPDATE    SAD003A
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Vise feilmeldinger                                  SRBF /
      *////////////////////////////////////////////////////////////
     C     SRBF          BEGSR
     C     SRBF1         TAG
     C                   MOVE      '1'           *IN30
      *______________
      * Tømme Subfile
      *""""""""""""""
     C                   MOVE      '1'           *IN83
     C                   MOVE      WAVD          MAVD
     C                   MOVE      WTDN          MTDN
     C                   WRITE     SAD003F
     C                   MOVEA     '000'         *IN(83)
     C                   Z-ADD     *ZERO         ÆRECNO
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     KEYB          SETLL     TVINF
     C     MMN           READE     TVINF                                  84
      *
     C     *IN84         DOWEQ     '0'
     C                   MOVE      '0'           *IN30
     C                   ADD       1             ÆRECNO
     C                   WRITE     SAD003E
     C     MMN           READE     TVINF                                  84
     C  N84              END
     C                   Z-ADD     1             ÆRECNO
      *_______________
      * Ingen poster ?
      *"""""""""""""""
     C     *IN30         IFEQ      '1'
     C                   MOVE      'SY00110'     ZMSGNO
     C                   GOTO      SRBF9
     C                   END
      *_______________
      * Behandle bilde
      *"""""""""""""""
     C                   MOVEA     '11'          *IN(81)
     C  N30              MOVE      'SY00000'     ZMSGNO
     C                   WRITE     SAD003Ø
     C                   WRITE     SAD003C
     C                   EXFMT     SAD003F
     C     *IN05         CABEQ     '1'           SRBF1
      *_________________
      * Oppdatere SUBFIL
      *"""""""""""""""""
     C     SRBF9         TAG
     C  N30              MOVE      *BLANKS       ZOP
     C  N30MSR           COMP      'R'                                    27
     C  N30              UPDATE    SAD003A
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Vise fysisk filkomponent                            SRBG /
      *////////////////////////////////////////////////////////////
     C     SRBG          BEGSR
      *___________________
      * Hente sendningsref
      *"""""""""""""""""""
     C     KEYC          CHAIN     EDIS                               20
      *________________________
      * Call på eksternt CL-pgm
      *""""""""""""""""""""""""
     C                   CALL      'SAD003C'
     C                   PARM      SLIB          SLIB
     C                   PARM      SFILE         SFILE
     C                   PARM      SMBR          SMBR
     C     WIN30         PARM                    WIN30
     C     WIN30         IFEQ      '0'
     C                   MOVE      '0'           *IN30
     C                   ELSE
     C                   MOVE      '1'           *IN30
     C                   MOVE      'SY00120'     ZMSGNO
     C                   END
      *_________________
      * Oppdatere SUBFIL
      *"""""""""""""""""
     C  N30              MOVE      *BLANKS       ZOP
     C  N30MSR           COMP      'R'                                    27
     C  N30              UPDATE    SAD003A
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      *  Vise fysisk filkomponent STRUKTURERT                SRBH /
      *////////////////////////////////////////////////////////////
     C     SRBH          BEGSR
      *___________________
      * Hente sendningsref
      *"""""""""""""""""""
     C     KEYC          CHAIN     EDIS                               20
     C     *IN20         IFEQ      '0'
      * Finnes komponenten kvar ?
     C     SMBR          IFEQ      'REMOVED'
     C                   MOVE      'SPS0007'     ZMSGNO
     C                   MOVE      '1'           *IN30
     C                   GOTO      SRBH8
     C                   END
      * TEST OM XML-FILER
     C                   MOVEL     S0026         XAN04             4
     C                   IF        XAN04 = 'XML-'
     C                   CALL      'EDI41RXM'
     C                   PARM                    SLIB
     C                   PARM                    SFILE
     C                   PARM                    SMBR
N01  C                   PARM                    ZOP
     C                   ELSE
      * Vise komponent.
     C                   MOVEL     SLIB          XSLIB             6
     C     XSLIB         IFEQ      'SYSPEDO'
     C                   CALL      'EDI41RX'
     C                   PARM                    SLIB
     C                   PARM                    SFILE
     C                   PARM                    SMBR
N01  C                   PARM                    ZOP
     C                   ELSE
     C                   SELECT
     C     SFILE         WHENEQ    'EDIRE'
     C                   CALL      'EDI41R'
     C                   PARM                    SLIB
     C                   PARM                    SFILE
     C                   PARM                    SMBR
N01  C                   PARM                    ZOP
     C     SFILE         WHENEQ    'EDIRE1K'
     C                   CALL      'EDI41R1K'
     C                   PARM                    SLIB
     C                   PARM                    SFILE
     C                   PARM                    SMBR
N01  C                   PARM                    ZOP
     C     SFILE         WHENEQ    'EDISE'
     C                   CALL      'EDI41S'
     C                   PARM                    SLIB
     C                   PARM                    SFILE
     C                   PARM                    SMBR
N01  C                   PARM                    ZOP
     C     SFILE         WHENEQ    'EDISE1K'
     C                   CALL      'EDI41S1K'
     C                   PARM                    SLIB
     C                   PARM                    SFILE
     C                   PARM                    SMBR
N01  C                   PARM                    ZOP
     C                   ENDSL
     C                   END
     C                   END
     C                   END
      *_________________
      * Oppdatere SUBFIL
      *"""""""""""""""""
     C     SRBH8         TAG
     C                   MOVE      *BLANKS       ZOP
     C     MSR           COMP      'R'                                    27
     C                   UPDATE    SAD003A
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      *  SLETTE ÅPEN LOGGPOST                                SRBDEL*
      *////////////////////////////////////////////////////////////
     C     SRBDEL        BEGSR
      *________________________
      * Call på eksternt CL-pgm
      *""""""""""""""""""""""""
     C     MST           IFEQ      ' '
     C                   CALL      'EDI60R'
     C                   PARM                    MMN
     C                   PARM                    XIN03             1
     C                   END
      *_________________
      * Oppdatere SUBFIL
      *"""""""""""""""""
     C                   MOVE      *BLANKS       ZOP
     C     MSR           COMP      'R'                                    27
     C                   UPDATE    SAD003A
      *
     C                   ENDSR
