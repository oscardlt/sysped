      *
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      HELP
     A                                      ALTHELP
      *
     A          R SUBFIL                    SFL
     A            WSNR           1Y 0B  5  2EDTCDE(4)
     A            EE3039E        6Y 0O  5  5TEXT('Ekspedisjonsenhet')
     A                                      EDTCDE(4)
     A            EESTDNV       30A  O  5 12TEXT('Enhetsnavn')
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A                                      CF03(03 'Avslutt program')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'Lag ny')
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A            ZRECNO         4S 0O  2 60SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYSPED/SAD143R'
     A                                      COLOR(BLU)
     A                                  1 20'Vedlikehold eksp.enhet for ekspres-
     A                                      sfortolling'
     A                                      COLOR(WHT)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                  2 70TIME
     A                                  3  2'Velg:  2=Endre   4=Slett'
     A                                      COLOR(BLU)
     A                                  4  2'Op Enhet  Beskrivelse'
     A                                      COLOR(WHT)
      *
     A          R BUNN
     A                                      CF03(03 'EXIT')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A                                  3  2'F3=Avslutt  F5=Frisk  F6=Lage ny'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  4  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R NYVARE
     A                                      CF03(03 'Avslutt program')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'Lag ny')
     A                                  1  2'SYSPED/SAD138R'
     A                                      COLOR(BLU)
     A                                  1 20'Vedlikehold eksp.enhet for ekspres-
     A                                      sfortolling'
     A                                      COLOR(WHT)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                  2 70TIME
     A                                  3  2'Velg:  2=Endre   4=Slett'
     A                                      COLOR(BLU)
     A                                  4  2'Op Enhet  Beskrivelse'
     A                                      COLOR(WHT)
     A                                 23  2'F3=Avslutt  F5=Frisk  F6=Lage ny'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R VARELI
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1  2'   Enhet  Beskrivelse'
     A            WS3039E        6Y 0B  2  5DSPATR(UL)
     A  40                                  DSPATR(HI)
     A  40                                  DSPATR(PR)
     A  90                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A            WSSTDNV       30A  B  2 12DSPATR(UL)
     A  89                                  DSPATR(RI)
     A                                  3  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  4  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
      *
     A          R VAREL3
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A                                  1  2'   Enhet  Beskrivelse'
     A                                      COLOR(WHT)
     A            EE3039E        6Y 0O  2  5DSPATR(UL)
     A                                      DSPATR(PC)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            EESTDNV       30A  O  2 12DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  3  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
      *
