      *********************************************************************
      * OPPDATER ORDRE FRA IPR TIL RFP
      * PROGRAM BEHANDLER INPUT FRA SS101
      *
CRTPG+*  CRTPGM SYSPED/SS102 MODULE(*LIBL/SS102 *LIBL/SS101B
CRTPGM*    *LIBL/SS101C *LIBL/CHECKUSR *LIBL/INCGI) ACTGRP(CGI) AUT(*USE)
      *
      *
      *********************************************************************
      * MAIN PROGRAM FLOW
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
      * Boats for sale data base file (LF on BOATSALE by boat serial number)
     FGSSORF    UF   E           K DISK    USROPN
     FGSSAKF    IF   E           K DISK    USROPN
     FGSSPOF    IF   E           K DISK    USROPN
     FGSSRFF    IF   E           K DISK    USROPN
     FGSSPAF    UF   E           K DISK    USROPN
     FGSSLGF    O    E             DISK    USROPN
     FKUFAST    IF   E           K DISK    USROPN
     FLLVARE01  IF   E           K DISK    USROPN
     FLLGRPK05  IF   E           K DISK    USROPN
     FBRIDF     UF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Prototype definitions and standard system API error structure
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      * Variables common to CGI programs
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
      * VARIABLES SPECIFIC TO THIS PROGRAM
      *--------------------------------------------------------------------
      * Variables received from the input string
     D lng             s              2
     D BckGnd          s             10
     D UsrSpcName      s             10
     D lstbrd          s              1
     D Lib             s             10
     D Fn              s             10
     D Mbr             s             10
     D ItemCount       s             10i 0
     D i               s             10i 0
     D TB              S              1    DIM(15)

     D WSONO           S              7
     D WSONO2          S              7
     D WSUSER          S             10
     D WSNA            S             30
     D WSKN            S              8
     D WSPOF           S             20
     D WSREADY         S              1
     D WSCOMPL         S              1
     D WSST2           S              5
     D WSNULL          S              1
     D BWONO           S              7  0
     D*WSRNR           S              5  0
     D PGM             C                   CONST('SYSPED/CHECKUSR')
     D PGM2            C                   CONST('*LIBL/SS101B')
     D*XXTXT01         C                   CONST('Stauts is changed at')
     D                 DS
     D WTONO                   1    700
     D TONO                    1    700    DIM(100)
      *
      /copy syspeds/qrpgsrcpy,prolog3
      *=====================================================================
      * MAIN PROCESSING LINE
      *=====================================================================
      * Parse input string
     C                   exsr      Parse
      * Initier
     C                   EXSR      INIT
     C                   CALLB     PGM
     C                   PARM                    BIBRID
     C                   PARM                    UsrSpcName
     C                   PARM                    UINN              1
     C                   IF        UINN = 'N'
     C                   GOTO      SLUTT
     C                   END
     C     WSUSER        CHAIN     BRIDF                              50
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVE      WDT           BIDTF
     C                   MOVE      WTM           BITIDF
     C                   UPDATE    BRIDFR
      *
     C                   MOVE      'N'           XDELLEV           1
     C                   IF        WSNULL  = 'J'
     C     1             DO        100           XN
     C                   IF        TONO(XN) = *BLANKS
     C                   LEAVE
     C                   END
     C                   EVAL      WSONO   = TONO(XN)
     C                   EVAL      BWONO   = c2n2(WSONO)
     C                   EXSR      DELLEV
     C                   ENDDO
     C                   MOVE      'J'           XDELLEV           1
     C                   GOTO      STVIS
     C                   END
      *
     C                   EVAL      WSREADY = 'J'
     C*                  Z-ADD     -1            WSRNR
     C                   eval      ItemCount = ZhbGetVarCnt('READY')
     C                   eval      i = 1
     C     i             DOWLE     ItemCount
     C                   EVAL      WSONO   = ZhbGetVar('READY':i)
     C                   EVAL      BWONO   = c2n2(WSONO)
     C                   EVAL      WSONO2  = zhbgetvar('COMPL':i)
      * Oppdater ordre
     C                   IF        WSST2   = *BLANKS
     C                   IF        WSONO2  = *BLANKS
     C                   EXSR      OPPDAT
     C                   ELSE
     C                   EXSR      DELLEV
     C                   END
     C                   ELSE
     C                   EXSR      OPPDAT2
     C                   END
     C                   eval      i = i + 1
     C                   ENDDO
      *
     C                   IF        WSST2   = *BLANKS
     C                   EVAL      WSREADY = 'N'
     C                   eval      ItemCount = ZhbGetVarCnt('COMPL')
     C                   eval      i = 1
     C     i             DOWLE     ItemCount
     C                   EVAL      WSONO   = ZhbGetVar('COMPL':i)
     C                   EVAL      BWONO   = c2n2(WSONO)
     C                   EVAL      WSONO2  = zhbgetvar('READY':i)
      * Oppdater ordre
     C                   IF        WSONO2  = *BLANKS
     C                   EXSR      DELLEV
     C                   END
     C                   eval      i = i + 1
     C                   ENDDO
     C                   END
      *
     C     STVIS         TAG
     C                   IF        XDELLEV = 'N'
     C                   EXSR      VISORDRE
     C                   ELSE
     C                   callp     updhtmlvar('WTONO':WTONO)
     C                   callp     wrtsection('tableend')
     C                   exsr      SetFndSome2
     C                   callp     wrtsection('foundsome')
     C                   exsr      SetEnd2
     C                   callp     wrtsection('endhtml')
     C                   callp     wrtsection('*fini')
     C                   END
      * Send output buffer and quit
     C     SLUTT         TAG
     C                   exsr      Exit
      *=====================================================================
      * Send output buffer and quit
      *=====================================================================
     C     Exit          begsr
     C                   CLOSE     GSSORF
     C                   CLOSE     GSSAKF
     C                   CLOSE     GSSPOF
     C                   CLOSE     GSSRFF
     C                   CLOSE     GSSPAF
     C                   CLOSE     GSSLGF
     C                   CLOSE     KUFAST
     C                   CLOSE     LLVARE01
     C                   CLOSE     LLGRPK05
     C                   CLOSE     BRIDF
     C                   eval      *inlr = *on
     C                   return
     C                   endsr
      *=====================================================================
      * Parse input string
      *=====================================================================
     C     Parse         begsr
     C                   eval      lng     = zhbgetvar('lng')
     C                   eval      BckGnd  = zhbgetvar('bckgnd')

     C                   eval      WSUSER  = zhbgetvar('USER')
     C                   EVAL      UsrSpcName  = zhbgetvar('UsrSpcName')
     C                   EVAL      WSKN    = zhbgetvar('WSKN')
     C                   EVAL      WSPOF   = zhbgetvar('WSPOF')
     C                   eval      WSST2   = zhbgetvar('WSST2')
     C                   eval      WSNULL  = zhbgetvar('NULLSTIL')
     C                   eval      WTONO   = zhbgetvar('WTONO')

     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$top
      *  (search criteria)
      *=====================================================================******
     C     SetTop        begsr
      * Repeat national language for the next invocation
     C                   callp     updhtmlvar('LNG':lng:
     C                             InitHTMLVars)
      * Color for text, background, links, visited links, active links
     C                   callp     updhtmlvar('TXTCOLOR':'black')
     C                   callp     updhtmlvar('BOLD':' ')
     C                   callp     updhtmlvar('BGCOLOR':BIFARG)
     C                   callp     updhtmlvar('LINK':'0000FF')
     C                   callp     updhtmlvar('VLINK':'FF0000')
     C                   callp     updhtmlvar('ALINK':'00FF00')
      * KUNDE
     C                   callp     updhtmlvar('WSNA':WSNA)
BODY C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updhtmlvar('USER':WSUSER)
     C                   callp     updHtmlVar('UsrSpcName':UsrSpcName)
     C                   callp     updHTMLvar('sdato':
     C                             %trim(%editw(BIDTV:'    .  .  ')))
     C                   callp     updHTMLvar('stid':
     C                             %trim(%editw(BITIDV:'  .  .  ')))
      *
     C                   endsr
      *=====================================================================
      *  Initier
      *=====================================================================
     C     INIT          begsr
      * Get program start time for calculating execution time
     C                   time                    timedata1
      *
     C                   OPEN      BRIDF
     C     WSUSER        CHAIN(N)  BRIDF                              50
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   CALLB     'INCGI'
     C                   PARM                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
      *
     C                   open      GSSORF                               50
     C                   open      GSSAKF                               50
     C                   open      GSSPOF                               50
     C                   open      GSSRFF                               50
     C                   open      GSSPAF                               50
     C                   open      GSSLGF                               50
     C                   open      KUFAST                               50
     C                   open      LLVARE01                             50
     C                   open      LLGRPK05                             50
      *
     C     LLVARE1K      klist
     C                   kfld                    GKGRP
     C                   kfld                    ORCNO
     C                   kfld                    AKANR
     C     KUFASTK       KLIST
     C                   KFLD                    KFTYP
     C                   KFLD                    ORST2
      *
     C                   MOVEL     'SSSTATUS'    KFTYP
      *
     C                   endsr
      *
      *=====================================================================
      *  Ferdigmerke ordre
      *=====================================================================
     C     OPPDAT        begsr
     C     BWONO         CHAIN     GSSORF                             33
     C                   IF        *IN33 = *OFF
     C                   EVAL      LGAGM = ORST2
     C     KUFASTK       CHAIN     KUFAST                             33
     C                   CAT       KFTXT:2       LGAGM
     C                   MOVEL     'RFP'         ORST2
     C                   UPDATE    GSSORFR
      *
     C                   EXSR      OPDLOGG
      *
     C                   END
      *
     C                   endsr
      *=====================================================================
      *  Ikke ferdigmerke ordre
      *=====================================================================
     C     OPPDAT2       begsr
     C     BWONO         CHAIN     GSSORF                             33
     C                   IF        *IN33 = *OFF
     C                   IF        ((ORST2 = 'RFP') AND (ORTNO = 0))
      *
     C                   EVAL      LGAGM = ORST2
     C     KUFASTK       CHAIN     KUFAST                             33
     C                   CAT       KFTXT:2       LGAGM
      *
     C                   MOVEL     'IPR'         ORST2
     C     BWONO         CHAIN     GSSPAF                             33
     C                   IF        *IN33 = *OFF
     C                   MOVE      *BLANKS       PAPKD
     C                   MOVE      *BLANKS       PATXT1
     C                   UPDATE    GSSPAFR
     C                   ENDIF
     C                   ENDIF
     C                   UPDATE    GSSORFR
      *
     C                   EXSR      OPDLOGG
      *
     C                   ENDIF
      *
     C                   endsr
      *=====================================================================
      *  Oppdater loggen
      *=====================================================================
     C     OPDLOGG       begsr
      *
     C                   MOVE      WSUSER        LGUSER
     C                   MOVE      WDT           LGDT
     C                   MOVE      WTM           LGTID
     C                   Z-ADD     1             LGLN
     C                   MOVEL     'SS102'       LGPGM
     C                   Z-ADD     3             LGNIVÅ
     C                   MOVE      WDT           LGHDT
     C                   MOVEL     WTM           LGHTID
     C                   MOVEL     'MAIN'        LGFTYP
     C                   MOVE      BWONO         LGONO
     C                   MOVE      *ZEROS        LGLNO
     C*                  MOVEL     XXTXT01       LGTXT
     C*                  CAT       ORLC:1        LGTXT
     C                   EVAL      LGANY = ORST2
     C     KUFASTK       CHAIN     KUFAST                             33
     C                   EVAL      LGTXT = KFTXT
     C                   CAT       ORLC:1        LGTXT
     C                   CAT       KFTXT:2       LGANY
     C                   WRITE     GSSLGFR
      *
     C                   endsr
      *=====================================================================
      * VIS ORDRE
      *=====================================================================
     C     VISORDRE      begsr
      *
     C                   CALLB     PGM2
     C                   PARM                    WSNA
     C                   PARM                    WSUSER
     C                   PARM                    UsrSpcName
     C                   PARM                    WSST2
     C                   PARM                    bckgnd
     C                   PARM                    WSKN
     C                   PARM                    WSPOF
      *
     C                   endsr
      *=====================================================================
      * DELLEVERING
      *=====================================================================
     C     DELLEV        begsr
      *
      * TEST OM FINNES RESTORDRE. I SÅ FALL TEST OM DET ER UNDER ARBEID.
      * HVIS DET FINNES OG ER UNDER ARBEID, KAN BRUKER IKKE BEHANDLE DETTE.
     C                   MOVE      *ZEROS        XXONOR            7 0
     C     BWONO         SETLL     GSSAKF
     C     BWONO         READE     GSSAKF                                 50
     C     *IN50         DOWEQ     '0'
     C     AKONOR        IFNE      0
     C                   EVAL      XXONOR = AKONOR
     C                   LEAVE
     C                   END
     C     BWONO         READE     GSSAKF                                 50
     C  N50              ENDDO
     C     XXONOR        IFNE      0
     C     XXONOR        SETLL     GSSAKF
     C     XXONOR        READE     GSSAKF                                 50
     C     *IN50         DOWEQ     '0'
     C     AKONOR        IFNE      0
      * RESTORDRE ER UNDER ARBEID. IKKE BEHANDLE DETTE ORDRET.
     C                   GOTO      SLDELLEV
     C                   END
     C     BWONO         READE     GSSAKF                                 50
     C  N50              ENDDO
     C                   END
      *
     C     BWONO         SETLL     GSSAKF
     C     BWONO         READE     GSSAKF                                 50
     C     *IN50         IFEQ      '0'
     C                   IF        XDELLEV = 'N'
     C                   MOVE      'J'           XDELLEV           1
     C                   exsr      SetSkl2
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
     C                   exsr      RtvEnvVar2
     C                   exsr      SetTop2
     C                   callp     wrtsection('top')
     C                   exsr      SetTabStr2
     C                   callp     wrtsection('tablestr')
     C                   z-add     0             bseq              3 0
     C                   END
      *
     C     *IN50         DOWEQ     '0'
     C                   eval      bseq=bseq + 1
     C                   exsr      SetTabRow2
     C                   callp     wrtsection('tablerow')
     C     BWONO         READE     GSSAKF                                 50
     C  N50              ENDDO
     C                   ADD       1             XN                3 0
     C                   MOVE      BWONO         TONO(XN)
      *
     C                   ELSE
      *
     C                   EXSR      OPPDAT
     C                   END
      *
     C     SLDELLEV      endsr
      *=====================================================================******
      *  Set output variables for section /$top
      *  (search criteria)
      *=====================================================================******
     C     SetTop2       begsr
      * Repeat national language for the next invocation
     C                   callp     updhtmlvar('LNG':lng:
     C                             InitHTMLVars)
      * Color for text, background, links, visited links, active links
     C                   callp     updhtmlvar('TXTCOLOR':'black')
     C                   callp     updhtmlvar('BOLD':' ')
     C                   callp     updhtmlvar('BGCOLOR':BIFARG)
     C                   callp     updhtmlvar('LINK':'0000FF')
     C                   callp     updhtmlvar('VLINK':'FF0000')
     C                   callp     updhtmlvar('ALINK':'00FF00')
      * KUNDE
     C                   callp     updhtmlvar('WSNA':WSNA)
     C                   callp     updhtmlvar('USER':WSUSER)
     C                   callp     updHtmlVar('UsrSpcName':UsrSpcName)
     C                   callp     updHTMLvar('sdato':
     C                             %trim(%editw(BIDTV:'    .  .  ')))
     C                   callp     updHTMLvar('stid':
     C                             %trim(%editw(BITIDV:'  .  .  ')))
     C                   callp     updhtmlvar('WSST2':WSST2)
     C                   callp     updhtmlvar('WSREADY':WSREADY)
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$tablestr (table start)
      *=====================================================================******
     C     SetTabStr2    begsr
     C     lstbrd        ifeq      'Y'
     C                   callp     updhtmlvar('LB':'0')
     C                   else
     C                   callp     updhtmlvar('LB':'1')
     C                   endif
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$tablerow (table row)
      *=====================================================================******
     C     SetTabRow2    begsr
     c* Sequence of this Recd (numeric, converted to char)
     C                   move      bseq          bseqchar          3
     C                   callp     updhtmlvar('SEQ':bseqchar)
     C                   callp     updhtmlvar('AKONO':
     C                             %trim(%editc(AKONO:'Z')))
     C                   callp     updhtmlvar('AKLNO':
     C                             %trim(%editc(AKLNO:'Z')))
     C     BWONO         SETLL     GSSPOF
     C     BWONO         READE     GSSPOF                                 33
     C   33              MOVE      *BLANKS       POCONO
     C                   callp     updhtmlvar('POCONO':POCONO)
     C     BWONO         SETLL     GSSRFF
     C     BWONO         READE     GSSRFF                                 33
     C     *IN33         DOWEQ     '0'
     C     RFKD          IFEQ      'CPO'
     C                   LEAVE
     C                   ENDIF
     C     BWONO         READE     GSSRFF                                 33
     C  N33              ENDDO
     C   33              MOVE      *BLANKS       RFREF
     C                   callp     updhtmlvar('RFREF':RFREF)
     C                   callp     updhtmlvar('AKANR':AKANR)
     C                   callp     updhtmlvar('AKSTK':
     C                             %trim(%editc(AKSTK:'Z')))
     C                   IF        WSNULL = 'J'
     C                   Z-ADD     0             XNULL             1 0
     C                   callp     updhtmlvar('WSSTKM':
     C                             %trim(%editc(XNULL:'Z')))
     C                   ELSE
     C                   callp     updhtmlvar('WSSTKM':
     C                             %trim(%editc(AKSTKM:'Z')))
     C                   END
     C                   callp     updhtmlvar('WSTXT':AKTXT)
     C                   IF        AKONOR = 0
     C     BWONO         CHAIN(N)  GSSORF                             33
     C                   ELSE
     C     AKONOR        CHAIN(N)  GSSORF                             33
     C                   END
     C                   callp     updhtmlvar('WSDTL':
     C                             %trim(%editw(ORDTL:'    .  .  ')))
     C     ORCNO         CHAIN     LLGRPK05                           33
     C     LLVARE1K      CHAIN     LLVARE01                           33
     C                   callp     updhtmlvar('LVVB':LVVB)
      *
     C*                  ADD       1             WSRNR
     C*                  callp     updhtmlvar('RNR':
     C*                            %trim(%editc(WSRNR:'Z')))
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$foundsome (some Recds were found)
      *=====================================================================******
     C     SetFndSome2   begsr
      * Repeat background color
     C                   callp     updhtmlvar('BCKGND':bckgnd)
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$endhtml
      *=====================================================================******
     C     SetEnd2       begsr
      * Server protocol
     C                   callp     updhtmlvar('PROTOCOL':S_Protocol)
      * Compute run time
     C                   time                    timedata2
     C     timedata2     subdur    timedata1     ms:*ms
     C                   eval      sec = ms / 1000000
     C                   callp     updhtmlvar('runtime':
     C                             %trim(%editw(sec:'     0 .   ')))
      *
     C                   endsr
      *=====================================================================******
      * Set html output skeleton member before its read into memory
      *=====================================================================******
     C     SetSkl2       begsr
      *------------------
      * Set html output skeleton member
     C                   eval      lng = uppify(lng)
      * "HTMLSRC  " er dft = NORSK, ved å trykke på FLAGGET i inng.
      * kan dette endre stil "HTMLSRCXX" avh. av støttede språk
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC' + lng
     C                   eval      Mbr = 'SS102'
      *------------------
     C                   endsr
      *=====================================================================******
      *  Retrieve environment variables
      *=====================================================================******
     C     RtvEnvVar2    begsr
      * Use getenvp to get this server's protocol and name
     C                   eval      S_Protocol =getenv('SERVER_PROTOCOL':
     C                             qusec)
     C                   eval      S_Name     =getenv('SERVER_NAME':
     C                             qusec)
      *
     C                   endsr
