     A                                      DSPSIZ(27 132 *DS4)
     A                                      PRINT
     A                                      HELP
     A                                      ALTHELP
      *
     A          R WINDOWSZ
     A                                      WINDOW(*DFT 20 122 *NOMSGLIN)
     A                                      OVERLAY
      *
     A          R BILDE01
     A                                      WINDOW(WINDOWSZ)
     A                                      CF04(04 'Søking')
     A                                      CF09(09 'Hent UUID')
     A                                      CA12(12 'Avbryt')
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1  2'SYSPED/SAD114R'
     A                                      COLOR(BLU)
     A                                  1 20'Arbeid med oppdrag'
     A                                      COLOR(WHT)
     A                                  2  2'Avd:'
     A            KMAVD          4Y 0O  2  7COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  2 13'Tur:'
     A            KMPRO          8Y 0O  2 18COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  2 28'R.Dato:'
     A            WSDTR          6Y 0O  2 36COLOR(WHT)
     A                                      EDTWRD('  .  .  ')
     A                                  2 46'UUID:'
     A            KMUUID        36A  O  2 52COLOR(WHT)
      *
     A                                  4  2'Avdeling:'
     A            WSAVD          4Y 0O  4 15TEXT('Deklarantnr')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  4 21'Oppdrag:'
     A            WSTDN          7Y 0O  4 30TEXT('Oppdragsnr')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  4 40'Cons.nr:'
     A            WSCN           2Y 0O  4 49TEXT('Consignment nr')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  4 53'Status:'
     A            MCST           1A  O  4 61TEXT('Status')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
      *
     A                                  5  2'Selger:'
     A            WSKNS          8Y 0B  5 15TEXT('Deklarantnr')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N99                                  COLOR(YLW)
     A  99                                  COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  5 24'Org.nr.:'
     A            WSRGS         11A  B  5 33TEXT('Org.nr')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N98                                  COLOR(YLW)
     A  98                                  COLOR(WHT)
     A            WSNAS         30A  B  6 15TEXT('Navn Deklarant')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N97                                  COLOR(YLW)
     A  97                                  COLOR(WHT)
     A            WSADS1        30A  B  6 46TEXT('Adr-1 Deklarant')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N96                                  COLOR(YLW)
     A  96                                  COLOR(WHT)
     A            WSADS2        30A  B  7 15TEXT('Adr-2 Deklarant')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A            WSPNS          9A  B  7 46TEXT('Postnr Deklarant')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N95                                  COLOR(YLW)
     A  95                                  COLOR(WHT)
     A            WSADS3        30A  B  7 56TEXT('Adr-3 Deklarant')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N94                                  COLOR(YLW)
     A  94                                  COLOR(WHT)
     A                                  7 87'LK:'
     A            WSLKS          2A  B  7 91TEXT('Landkode Deklarant')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N93                                  COLOR(YLW)
     A  93                                  COLOR(WHT)
     A                                  8  2'Tlf/epost:'
     A            WSCMS         50A  B  8 15TEXT('Tlf/epostadresse')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N92                                  COLOR(YLW)
     A  92                                  COLOR(WHT)
     A                                      CHECK(LC)
     A                                  8 66'Type:'
     A            WSCMST         3A  B  8 72TEXT('Type')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A                                      VALUES(' ' 'TE' 'EM')
     A                                  8 77'(TE eller EM)'
     A                                  9  2'Kont.person:'
     A            WSCPS         70A  B  9 15TEXT('Tlf/epostadresse')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A                                      COLOR(YLW)
      *
     A                                 10  2'Mottaker:'
     A            WSKNK          8Y 0B 10 15TEXT('Mottaker')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N91                                  COLOR(YLW)
     A  91                                  COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 10 24'Org.nr.:'
     A            WSRGK         11A  B 10 33TEXT('Org.nr')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N90                                  COLOR(YLW)
     A  90                                  COLOR(WHT)
     A            WSNAK         30A  B 11 15TEXT('Navn Mottaker')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N89                                  COLOR(YLW)
     A  89                                  COLOR(WHT)
     A            WSADK1        30A  B 11 46TEXT('Adr-1 Mottaker')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N88                                  COLOR(YLW)
     A  88                                  COLOR(WHT)
     A            WSADK2        30A  B 12 15TEXT('Adr-2 Mottaker')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A            WSPNK          9A  B 12 46TEXT('Postnr Mottaker')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N87                                  COLOR(YLW)
     A  87                                  COLOR(WHT)
     A            WSADK3        30A  B 12 56TEXT('Adr-3 Mottaker')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N86                                  COLOR(YLW)
     A  86                                  COLOR(WHT)
     A                                 12 87'LK:'
     A            WSLKK          2A  B 12 91TEXT('Landkode Mottaker')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N85                                  COLOR(YLW)
     A  85                                  COLOR(WHT)
     A                                 13  2'Tlf/epost:'
     A            WSCMK         50A  B 13 15TEXT('Tlf/epostadresse')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N84                                  COLOR(YLW)
     A  84                                  COLOR(WHT)
     A                                      CHECK(LC)
     A                                 13 66'Type:'
     A            WSCMKT         3A  B 13 72TEXT('Type')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A                                      VALUES(' ' 'TE' 'EM')
     A                                 13 77'(TE eller EM)'
     A                                 14  2'Kont.person:'
     A            WSCPK         70A  B 14 15TEXT('Kontaktperson')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A                                      COLOR(YLW)
      *
     A                                 15  2'Transp.ref.:'
     A            WSTRF         70A  B 15 15TEXT('Transportref.')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N83                                  COLOR(YLW)
     A  83                                  COLOR(WHT)
     A                                 15 87'Type:'
     A            WSTRFT         4A  B 15 93TEXT('Type av transportref')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N82                                  COLOR(YLW)
     A  82                                  COLOR(WHT)
     A                                 16  2'Godstype:'
     A            WSTYPE         3A  B 16 15TEXT('Godstype')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                      VALUES('11' '21' '31' '32' '91' ' ')
     A                                 16 20'(11=Salg, 21=Retur, 31=Gift, 32=Ko-
     A                                      mmersiell, 91=Dokumenter)'
     A                                 18  2'Valuta:'
     A            WSVAL          3A  B 18 15TEXT('Valutakode')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N81                                  COLOR(YLW)
     A  81                                  COLOR(WHT)
     A                                 18 20'Avs.LK:'
     A            WSLKA          2A  B 18 28TEXT('Avsendersland')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N80                                  COLOR(YLW)
     A  80                                  COLOR(WHT)
     A                                 18 31'VOECnr:'
     A            WSVOEC        17A  B 18 39TEXT('VOECnr')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                 19  2'F4=Søk   F9=Hent UUID   F12=Avbryt'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 20  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
