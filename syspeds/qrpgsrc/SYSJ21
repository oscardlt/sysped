      *
      ***********************************************************
      *** PROGRAM SKAL SKRIVE UT BILL OF LADIND (TRIP)        ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01-26                            ***
      *** LEDIGE INDIKATORER 27-31 35-59 63-95 98 99          ***
      ***********************************************************
      *
     FTURER14   IF   E           K DISK
     FHEAD14    IF   E           K DISK
     FBILLOFTU  IF   E           K DISK
     FBILLOF    IF   E           K DISK    PREFIX(BL_)
     FBLV1      IF   E           K DISK
     FSTED2     IF   E           K DISK
     FKODTFM    IF   E           K DISK
     FKODTP     IF   E           K DISK
     FFIRM      IF   E           K DISK
     FBRPARF    IF   E           K DISK
     FSYSJ21P   O    E             PRINTER
     FSYSJ21PL  O    E             PRINTER OFLIND(*IN27)
     D                 DS
     D  BLTXT1                 1    100
     D  BLTXT2               101    200
     D  BLTXT3               201    300
     D  BLTXT4               301    400
     D  OSFT2                  1    400    DIM(4)
     D                SDS
     D  UUSER                254    263
     D                UDS
     D  USTMF1               501    550
     D  USTMF2               551    600
      *
     C                   EXSR      INIT
      *
      * SKRIV TIL PRINTER
      *
     C     TUPRO         CHAIN     TURER14                            33
     C  N33TUPRO         CHAIN     BILLOFTU                           33
     C     *IN33         CABEQ     *ON           SLUTT
      *
     C                   IF        TUATD = 0
     C                   MOVE      TUETD         XAN08             8
     C                   ELSE
     C                   MOVE      TUATD         XAN08
     C                   END
     C                   EVAL      XATD = %SUBST(XAN08:7:2)
     C                   SELECT
     C                   WHEN      %SUBST(XAN08:5:2) = '01'
     C                   EVAL      XATD = %TRIM(XATD) + '. JANUARY'
     C                   WHEN      %SUBST(XAN08:5:2) = '02'
     C                   EVAL      XATD = %TRIM(XATD) + '. FEBRUARY'
     C                   WHEN      %SUBST(XAN08:5:2) = '03'
     C                   EVAL      XATD = %TRIM(XATD) + '. MARCH'
     C                   WHEN      %SUBST(XAN08:5:2) = '04'
     C                   EVAL      XATD = %TRIM(XATD) + '. APRIL'
     C                   WHEN      %SUBST(XAN08:5:2) = '05'
     C                   EVAL      XATD = %TRIM(XATD) + '. MAY'
     C                   WHEN      %SUBST(XAN08:5:2) = '06'
     C                   EVAL      XATD = %TRIM(XATD) + '. JUNE'
     C                   WHEN      %SUBST(XAN08:5:2) = '07'
     C                   EVAL      XATD = %TRIM(XATD) + '. JULY'
     C                   WHEN      %SUBST(XAN08:5:2) = '08'
     C                   EVAL      XATD = %TRIM(XATD) + '. AUGUST'
     C                   WHEN      %SUBST(XAN08:5:2) = '09'
     C                   EVAL      XATD = %TRIM(XATD) + '. SEPTEMBER'
     C                   WHEN      %SUBST(XAN08:5:2) = '10'
     C                   EVAL      XATD = %TRIM(XATD) + '. OCTOBER'
     C                   WHEN      %SUBST(XAN08:5:2) = '11'
     C                   EVAL      XATD = %TRIM(XATD) + '. NOVEMBER'
     C                   WHEN      %SUBST(XAN08:5:2) = '12'
     C                   EVAL      XATD = %TRIM(XATD) + '. DECEMBER'
     C                   ENDSL
     C                   EVAL      XATD = %TRIM(XATD) + ' ' + %SUBST(XAN08:1:4)
     C                   EVAL      BLNAG  = 'COLLICARE LOGISTICS AS'
     C                   EVAL      BLAD1G = 'SOLGAARD SKOG 144'
     C                   EVAL      BLAD2G = '1599 MOSS'
     C                   EVAL      BLAD3G = 'NO   TEL: +47 69209500'
      *
     C                   MOVE      *ZEROS        XSUMNT
     C                   MOVE      *ZEROS        XSUMVK
     C                   MOVE      *ZEROS        XSUMM3
     C                   MOVE      'N'           XFRTXT            1
     C                   WRITE     SYSJ21P1
      *
     C                   Z-ADD     0             XANTLIN           3 0
     C     TUPRO         SETLL     HEAD14                             33
     C     TUPRO         READE     HEAD14                                 33
     C                   DOW       *IN33 = *OFF
     C     BILLOFK       CHAIN     BILLOF                             33
     C                   IF        *IN33 = *OFF
     C     BILLOFK       SETLL     BLV1
     C     BILLOFK       READE     BLV1                                   33
     C                   DOW       *IN33 = *OFF
     C     BVNT          COMP      0                                  3737
     C     BVVKT         COMP      0                                  3838
     C     BVM3          COMP      0                                  3939
     C                   WRITE     SYSJ21P2
     C                   ADD       BVNT          XSUMNT
     C                   ADD       BVVKT         XSUMVK
     C                   ADD       BVM3          XSUMM3
     C                   ADD       1             XANTLIN
     C                   IF        XANTLIN >= 15
     C                   GOTO      STARTSD2
     C                   END
     C     BILLOFK       READE     BLV1                                   33
     C                   ENDDO
     C                   END
     C     TUPRO         READE     HEAD14                                 33
     C                   ENDDO
      *
     C                   IF        XFRTXT <> 'J'
     C                   ADD       1             XANTLIN
     C                   WRITE     SYSJ21P3
     C                   END
      *
     C     1             DO        4             XN                3 0
     C                   IF        OSFT2(XN) <> *BLANKS
     C                   EVAL      XTXT = OSFT2(XN)
     C                   WRITE     SYSJ21P4
     C                   ADD       1             XANTLIN
     C                   IF        XANTLIN >= 15
     C                   MOVE      'J'           XFRTXT
     C                   LEAVE
     C                   END
     C                   END
     C                   ENDDO
      *
     C     STARTSD2      TAG
     C                   WRITE     SYSJ21P9
      *
     C                   IF        *IN33 = *OFF OR XFRTXT = 'J'
     C                   EXSR      PRTSD2
     C                   ELSE
     C                   EVAL      USTMF2 = *BLANKS
     C                   END
      *
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
      *
      ******************************************
     C     PRTSD2        BEGSR
      ******************************************
     C                   EVAL      *IN27 = *OFF
     C                   WRITE     SIDE2
     C                   IF        XFRTXT = 'J'
     C                   GOTO      STPRTSD2
     C                   END
      *
     C     BILLOFK       READE     BLV1                                   33
     C                   DOW       *IN33 = *OFF
     C   27              WRITE     SIDE2
     C   27              EVAL      *IN27 = *OFF
     C     BVNT          COMP      0                                  3737
     C     BVVKT         COMP      0                                  3838
     C     BVM3          COMP      0                                  3939
     C                   WRITE     SIDE2D
     C                   ADD       BVNT          XSUMNT
     C                   ADD       BVVKT         XSUMVK
     C                   ADD       BVM3          XSUMM3
     C     BILLOFK       READE     BLV1                                   33
     C                   ENDDO
      *
     C     TUPRO         READE     HEAD14                                 33
     C                   DOW       *IN33 = *OFF
     C     BILLOFK       CHAIN     BILLOF                             33
     C                   IF        *IN33 = *OFF
     C     BILLOFK       SETLL     BLV1
     C     BILLOFK       READE     BLV1                                   33
     C                   DOW       *IN33 = *OFF
     C   27              WRITE     SIDE2
     C   27              EVAL      *IN27 = *OFF
     C     BVNT          COMP      0                                  3737
     C     BVVKT         COMP      0                                  3838
     C     BVM3          COMP      0                                  3939
     C                   WRITE     SIDE2D
     C                   ADD       BVNT          XSUMNT
     C                   ADD       BVVKT         XSUMVK
     C                   ADD       BVM3          XSUMM3
     C     BILLOFK       READE     BLV1                                   33
     C                   ENDDO
     C                   END
     C     TUPRO         READE     HEAD14                                 33
     C                   ENDDO
      *
     C     STPRTSD2      TAG
     C                   SELECT
     C                   WHEN      XFRTXT = 'N'
     C                   Z-ADD     1             XN2               3 0
     C                   WHEN      XN < 4
     C                   EVAL      XN2 = XN + 1
     C                   OTHER
     C                   EVAL      XN2 = 5
     C                   ENDSL
     C     XN2           DO        4             XN                3 0
     C                   IF        OSFT2(XN) <> *BLANKS
     C                   EVAL      XTXT = OSFT2(XN)
     C   27              WRITE     SIDE2
     C   27              EVAL      *IN27 = *OFF
     C                   WRITE     SIDE2DOS
     C                   END
     C                   ENDDO
      *
     C     XSUMNT        COMP      0                                  3737
     C     XSUMVK        COMP      0                                  3838
     C     XSUMM3        COMP      0                                  3939
     C                   WRITE     SIDE2S
      *
     C                   ENDSR
      *
      ******************************************
     C     INIT          BEGSR
      ******************************************
     C     BILLOFK       KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C     KODTPK        KLIST
     C                   KFLD                    KOPUNI
     C                   KFLD                    TUAVD
     C                   KFLD                    KOPLNR
     C                   MOVE      'P'           KOPUNI
     C                   Z-ADD     26            KOPLNR
     C     BrParKey      KLIST
     C                   KFLD                    PABRID
     C                   KFLD                    PAKUNR
     C                   KFLD                    PAID
     C                   MOVE      *ZEROS        PAKUNR
     C                   MOVE      'OLBAN'       PAID
      *
     C     *ENTRY        PLIST
     C                   PARM                    UPRO              8
      *
     C                   MOVE      UPRO          TUPRO
     C     TUPRO         CHAIN     TURER14                            33
     C     KODTPK        CHAIN     KODTP                              33
     C     KOPFM         CHAIN     KODTFM                             33
     C     *LOVAL        SETLL     FIRM
     C                   READ      FIRM                                   33
      *
     C                   eval      PABRID = '*KUNDFT*' + %TRIM(FIFIRM)
     C     BrParKey      CHAIN     BRPARF                             33
     C                   CALL      'ARCRAN'
     C                   PARM                    NEWRAN           10
     C                   EVAL      USTMF1 = %TRIM(PAVRDA) + 'BLTR' + %TRIM(UPRO)
     C                             + %TRIM(NEWRAN) + '.pdf'
     C                   EVAL      USTMF2 = %TRIM(PAVRDA) + 'B2TR' + %TRIM(UPRO)
     C                             + %TRIM(NEWRAN) + '.pdf'
      *
     C                   ENDSR
      *
