     H DATEDIT(*YMD)
     FEUNH      IF   E           K DISK
     FEDIM      UF   E           K DISK
     FEDTM902   IF   E           K DISK
     FEMOA902   IF   E           K DISK
     FEMEA902   IF   E           K DISK
     FENAD902   IF   E           K DISK
     FETCC902   IF   E           K DISK
     FEGID902   IF   E           K DISK
     FEFTX902   IF   E           K DISK
     FERFF902   IF   E           K DISK
     FFRISOL01  IF   E           K DISK
     FE143P     O    E             PRINTER
     D A               S              1    DIM(79)
     D B               S              1    DIM(18)
      *________________________
      * DS for decimal handling
      *""""""""""""""""""""""""
     D                 DS
     D  ZDEC                   1      7  6
     D  ZDEC6                  2      7
     D  ZHEL                  11     22  0
     D                 DS
     D  R1154                  1     35
     D  R11540                 1     10
     D                 DS
     D  DSMNN                  1      9  0
     D                 DS
     D  WFSSOK                 1     35
     D  WFSSO0                 1      9
     D  WFSSO1                 1      4
     D  WFSSO2                 5      9
     D                 DS            10
     D DIGITS          C                   CONST('0123456789')
     IEDIMR
     I              MMN                         TVIMMN
     IMOAR
     I              M5004D                      Æ5004D
      */////////////
      * MAIN LOGIC /
      */////////////
      *___________
      * Initiering
      *"""""""""""
     C                   EXSR      INIT
      *________
      * CONVERT
      *""""""""
     C  N20              EXSR      RTVD
      *___________
      * UPDATE LOG
      *"""""""""""
     C                   MOVE      'C'           MST
     C                   UPDATE    EDIMR
     C                   RETURN
      *////////////////////////////////////////////////////////////
      *                                                      INIT /
      *////////////////////////////////////////////////////////////
     C     INIT          BEGSR
     C     KEYMS0        KLIST
     C                   KFLD                    ZMN
     C                   KFLD                    ZGN0
     C     KEYMS1        KLIST
     C                   KFLD                    ZMN
     C                   KFLD                    ZGN1
     C                   KFLD                    ZREPB0
     C     KEYMS2        KLIST
     C                   KFLD                    ZMN
     C                   KFLD                    ZGN2
     C                   KFLD                    ZREPC0
     C                   KFLD                    ZREPC1
     C     KEYMS3        KLIST
     C                   KFLD                    ZMN
     C                   KFLD                    ZGN3
     C                   KFLD                    ZREPD0
     C                   KFLD                    ZREPD1
     C                   KFLD                    ZREPD2
     C     KEYMS4        KLIST
     C                   KFLD                    ZMN
     C                   KFLD                    ZGN4
     C                   KFLD                    ZREPE0
     C                   KFLD                    ZREPE1
     C                   KFLD                    ZREPE2
     C                   KFLD                    ZREPE3
     C     KEYMS5        KLIST
     C                   KFLD                    ZMN
     C                   KFLD                    ZGN5
     C                   KFLD                    ZREPF0
     C                   KFLD                    ZREPF1
     C                   KFLD                    ZREPF2
     C                   KFLD                    ZREPF3
     C                   KFLD                    ZREPF4
     C     FRIKEY        KLIST
     C                   KFLD                    FSKODE
     C                   KFLD                    FSSOK
     C     FRIKE2        KLIST
     C                   KFLD                    FSAVD
     C                   KFLD                    FSOPD
     C                   KFLD                    FSKODE
     C     HEAKEY        KLIST
     C                   KFLD                    FSAVD
     C                   KFLD                    FSOPD
     C     *LIKE         DEFINE    TVIMMN        ZMMN
     C     *LIKE         DEFINE    NMN           ZMN
     C     *LIKE         DEFINE    NGN           ZGN0
     C     *LIKE         DEFINE    NGN           ZGN1
     C     *LIKE         DEFINE    NGN           ZGN2
     C     *LIKE         DEFINE    NGN           ZGN3
     C     *LIKE         DEFINE    NGN           ZGN4
     C     *LIKE         DEFINE    NGN           ZGN5
     C     *LIKE         DEFINE    NREP0         ZREPB0
     C     *LIKE         DEFINE    NREP0         ZREPC0
     C     *LIKE         DEFINE    NREP1         ZREPC1
     C     *LIKE         DEFINE    NREP0         ZREPD0
     C     *LIKE         DEFINE    NREP1         ZREPD1
     C     *LIKE         DEFINE    NREP2         ZREPD2
     C     *LIKE         DEFINE    NREP0         ZREPE0
     C     *LIKE         DEFINE    NREP1         ZREPE1
     C     *LIKE         DEFINE    NREP2         ZREPE2
     C     *LIKE         DEFINE    NREP3         ZREPE3
     C     *LIKE         DEFINE    NREP0         ZREPF0
     C     *LIKE         DEFINE    NREP1         ZREPF1
     C     *LIKE         DEFINE    NREP2         ZREPF2
     C     *LIKE         DEFINE    NREP3         ZREPF3
     C     *LIKE         DEFINE    NREP4         ZREPF4
     C     *LIKE         DEFINE    NREP0         ZREPG0
     C     *LIKE         DEFINE    NREP1         ZREPG1
     C     *LIKE         DEFINE    NREP2         ZREPG2
     C     *LIKE         DEFINE    NREP3         ZREPG3
     C     *LIKE         DEFINE    NREP4         ZREPG4
     C     *LIKE         DEFINE    NREP5         ZREPG5
     C     *LIKE         DEFINE    NREP0         ZREPH0
     C     *LIKE         DEFINE    NREP1         ZREPH1
     C     *LIKE         DEFINE    NREP2         ZREPH2
     C     *LIKE         DEFINE    NREP3         ZREPH3
     C     *LIKE         DEFINE    NREP4         ZREPH4
     C     *LIKE         DEFINE    NREP5         ZREPH5
     C     *LIKE         DEFINE    NREP6         ZREPH6
     C     *LIKE         DEFINE    NREP0         ZREPI0
     C     *LIKE         DEFINE    NREP1         ZREPI1
     C     *LIKE         DEFINE    NREP2         ZREPI2
     C     *LIKE         DEFINE    NREP3         ZREPI3
     C     *LIKE         DEFINE    NREP4         ZREPI4
     C     *LIKE         DEFINE    NREP5         ZREPI5
     C     *LIKE         DEFINE    NREP6         ZREPI6
     C     *LIKE         DEFINE    NREP7         ZREPI7
     C     *LIKE         DEFINE    NREP0         ZREPJ0
     C     *LIKE         DEFINE    NREP1         ZREPJ1
     C     *LIKE         DEFINE    NREP2         ZREPJ2
     C     *LIKE         DEFINE    NREP3         ZREPJ3
     C     *LIKE         DEFINE    NREP4         ZREPJ4
     C     *LIKE         DEFINE    NREP5         ZREPJ5
     C     *LIKE         DEFINE    NREP6         ZREPJ6
     C     *LIKE         DEFINE    NREP7         ZREPJ7
     C     *LIKE         DEFINE    NREP8         ZREPJ8
     C     *ENTRY        PLIST
     C                   PARM                    DSMNA             9
     C                   MOVE      DSMNA         DSMNN
     C                   Z-ADD     DSMNN         ZMN
     C     ZMN           CHAIN     EUNH                               20
     C  N20              Z-ADD     ZMN           ZMMN
     C  N20ZMMN          CHAIN     EDIM                               20
      *DELAY THIS JOB
     C                   MOVEL(P)  'DLYJOB D'    QCMDR            15
     C                   CAT       'LY(180':0    QCMDR
     C                   CAT       ')':0         QCMDR
     C                   Z-ADD     15            QCMDLE           15 5
     C                   CALL      'QCMDEXC'                            3131
     C                   PARM                    QCMDR
     C                   PARM                    QCMDLE
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * RETREIVE INFO FROM EDIFACT DATABASE TO SYSPED400     RTVD /
      *////////////////////////////////////////////////////////////
     C     RTVD          BEGSR
      *___________________________
      * INIT AV FELT I PRINTERFILE
      *"""""""""""""""""""""""""""
     C                   CLEAR                   RCD000
      *___________________
      * READ MAIN SEGMENTS
      *"""""""""""""""""""
      * BGM00
     C                   MOVEL     U1004         X1004
     C                   MOVEL     X1004         FSSOK
     C                   MOVE      'IFB'         FSKODE
     C     FRIKEY        CHAIN     FRISOL01                           20
     C     *IN20         IFEQ      '0'
     C                   MOVE      FSAVD         HEAVD
     C                   MOVE      FSOPD         HEOPD
     C                   ENDIF
      *
      * DTM00
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     0             ZGN0
     C     KEYMS0        SETLL     EDTM902                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS0        READE     EDTM902                                51
     C     *IN51         DOWEQ     '0'
      *
     C                   MOVEA     D2782         A
     C                   EXSR      GETNUM
     C                   SELECT
     C     D2005         WHENEQ    '22 '
     C                   MOVEL     D2782         X2782
     C                   ENDSL
      *
     C     KEYMS0        READE     EDTM902                                51
     C  N51              ENDDO
     C                   ENDIF
      * TCC05
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     5             ZGN0
     C     KEYMS0        SETLL     ETCC902                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS0        READE     ETCC902                                51
     C     *IN51         DOWEQ     '0'
      *
     C                   SELECT
     C     T6063A        WHENEQ    '101'
     C     T6411A        ANDEQ     'KGM'
     C                   MOVEL     T6060A        X6060A
     C                   ENDSL
      *
     C     KEYMS0        READE     ETCC902                                51
     C  N51              ENDDO
     C                   ENDIF
      * NAD07
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     7             ZGN0
     C     KEYMS0        SETLL     ENAD902                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS0        READE     ENAD902                                51
     C     *IN51         DOWEQ     '0'
      *
     C                   MOVEA     N3039         A
     C                   EXSR      GETNUM
     C                   SELECT
      *
     C     N3035         WHENEQ    'CZ '
     C                   MOVEL     N3039         Z3039
     C                   MOVEL     N3036A        Z3036
     C                   MOVEL     N3251         Z3251
     C                   MOVEL     N3164         Z3164
     C     N3035         WHENEQ    'DP '
     C                   MOVEL     N3039         Å3039
     C                   MOVEL     N3036A        Å3036
     C                   MOVEL     N3251         Å3251
     C                   MOVEL     N3164         Å3164
     C     N3035         WHENEQ    'CA '
     C                   MOVEL     N3039         X3039
     C                   MOVEL     N3036A        X3036
     C                   MOVEL     'TOLLPOST'    X3036
     C                   MOVEL     N3042A        X3042
     C                   MOVEL     N3251         X3251
     C                   MOVEL     N3164         X3164
     C                   ENDSL
      *
     C     KEYMS0        READE     ENAD902                                51
     C  N51              ENDDO
     C                   ENDIF
      * MOA07
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     7             ZGN0
     C     KEYMS0        SETLL     EMOA902                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS0        READE     EMOA902                                51
     C     *IN51         DOWEQ     '0'
      *
     C     M5797A        IFEQ      '138'
     C                   MOVEL     M5004A        X5004B
     C                   ENDIF
     C     M5797B        IFEQ      '139'
     C                   MOVEL     M5004B        X5004C
     C                   ENDIF
     C     M5797B        IFEQ      '132'
     C                   MOVEL     M5004B        X5004E
     C                   ENDIF
      *
     C     KEYMS0        READE     EMOA902                                51
     C  N51              ENDDO
     C                   ENDIF
      * TCC07
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     7             ZGN0
     C     KEYMS0        SETLL     ETCC902                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS0        READE     ETCC902                                51
     C     *IN51         DOWEQ     '0'
      *
     C                   SELECT
     C     T5797A        WHENEQ    '144'
     C                   MOVEL     T5004A        X5004A
     C     T5797A        WHENEQ    '23 '
     C                   MOVEL     T5004A        X5004D
     C                   ENDSL
      *
     C     KEYMS0        READE     ETCC902                                51
     C  N51              ENDDO
     C                   ENDIF
      * RFF09
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     9             ZGN0
     C     KEYMS0        SETLL     ERFF902                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS0        READE     ERFF902                                51
     C     *IN51         DOWEQ     '0'
      *
     C                   MOVEL     R1154         X1154
      *
     C     KEYMS0        READE     ERFF902                                51
     C  N51              ENDDO
     C                   ENDIF
      * GID11
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     11            ZGN0
     C     KEYMS0        SETLL     EGID902                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS0        READE     EGID902                                51
     C     *IN51         DOWEQ     '0'
      *
     C                   MOVEL     G7224A        X7224
      *
     C     KEYMS0        READE     EGID902                                51
     C  N51              ENDDO
     C                   ENDIF
      * FTX11
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     11            ZGN0
     C     KEYMS0        SETLL     EFTX902                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS0        READE     EFTX902                                51
     C     *IN51         DOWEQ     '0'
      *
     C                   MOVEL     F4440A        X4440A
     C                   MOVEL     F4440B        X4440B
     C                   MOVEL     F4440C        X4440C
     C                   MOVEL     F4440D        X4440D
     C                   MOVEL     F4440E        X4440E
      *
     C     KEYMS0        READE     EFTX902                                51
     C  N51              ENDDO
     C                   ENDIF
      * MEA14
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     14            ZGN0
     C     KEYMS0        SETLL     EMEA902                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS0        READE     EMEA902                                51
     C     *IN51         DOWEQ     '0'
      *
     C                   SELECT
     C     M6311         WHENEQ    'GRO'
     C     M6411         ANDEQ     'KGM'
     C                   MOVEL     M6314         X6314A
     C                   ENDSL
      *
     C     KEYMS0        READE     EMEA902                                51
     C  N51              ENDDO
     C                   ENDIF
      *_______________________
      * SKRIV KONTRAKT HEADING
      *"""""""""""""""""""""""
     C                   WRITE     RCD000
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Get numerical value                               GETNUM /
      *////////////////////////////////////////////////////////////
     C     GETNUM        BEGSR
     C                   MOVE      *ZEROS        ZHEL
     C                   MOVE      *ZEROS        ZDEC
     C                   MOVE      *ZEROS        B
     C                   Z-ADD     1             AX                2 0
     C                   Z-ADD     1             BX                2 0
      * Get Integer
     C     A(AX)         DOWGE     '0'
     C     ZHEL          MULT      10            ZHEL
     C                   MOVE      A(AX)         ZHEL
     C                   ADD       1             AX
     C                   ENDDO
      * Decimal point ?
     C     A(AX)         IFEQ      '.'
     C     A(AX)         OREQ      ','
     C                   ADD       1             AX
     C     A(AX)         DOWGE     '0'
     C                   MOVE      A(AX)         B(BX)
     C                   ADD       1             AX
     C                   ADD       1             BX
     C                   ENDDO
     C                   MOVEA     B             ZDEC6
     C                   ENDIF
      * Concatenate
     C                   Z-ADD     ZHEL          ZNUM             18 6
     C                   ADD       ZDEC          ZNUM
     C                   ENDSR
