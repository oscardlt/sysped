********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 JOV PTF10 STREKKODER            ..
10XXX * 02 JOV PTF10 SNUDD REKKEFØLGE PÅ PARTER - NÅ AVSENDER -> MOTTAKER
10XXX * 12 JOV PTF10 TEKST PÅ HEADER-NIVÅ
********************************************************************
     A          R HEAD1
     A            KFMT12       120      1  1
     A  06        HP1           80      1  1HIGHLIGHT
     A  06        HP2           80      2  1HIGHLIGHT
     A  06        HP3           80      3  1HIGHLIGHT
     A  06        HP4           80      4  1HIGHLIGHT
     A  06        HP5           80      5  1HIGHLIGHT
     A  06        HP6           80      6  1HIGHLIGHT
     A  06        HP7           80      7  1HIGHLIGHT
N01  A*N73 42     AUPRO102      10      6 88HIGHLIGHT
N01  A*                                     FNTCHRSET(C0S0OB10 +
N01  A*                                     T1D0BASE)
N01  A*N73 43     AUPRO82        8      6 88HIGHLIGHT
N01  A*                                     FNTCHRSET(C0S0OB10 +
N01  A*                                     T1D0BASE)
      ** N73 = SIDE 1
S01  A*N73 42     AUPRO10       10      7 78BARCODE(CODE128 3 *HRITOP *HRZ +
S01  A*                                     (*WIDTH 0.022) (*RATIO 3) X'02')
S01  A*N73 43     AUPRO8         8      7 78BARCODE(CODE128 3 *HRITOP *HRZ +
S01  A*                                     (*WIDTH 0.022) (*RATIO 3) X'02')
S01  A N73 42     AUPRO10       10     10 60BARCODE(CODE128 2 *HRITOP *HRZ +
S01  A                                      (*WIDTH 0.013) (*RATIO 3) X'02')
S01  A N73 43     AUPRO8         8     10 60BARCODE(CODE128 2 *HRITOP *HRZ +
S01  A                                      (*WIDTH 0.013) (*RATIO 3) X'02')
      **
     A                                 10  1MSGCON(7 JOV0690 SYMSGF)
     A                                      HIGHLIGHT
     A            ZUSER         10     10 11HIGHLIGHT
     A            PDAUSR        10     10 22HIGHLIGHT
     A  44                             10 34'*NATTPORT* Foreløpig !! '
     A                                      HIGHLIGHT
     A  73N70N79                       10 78'Side.......:'
     A  73 70                          10 78'Page.......:'
     A  73 79                          10 78'Seite......:'
     A  73        SIDENO         3  0  10 91EDTCDE(Z)
      **
     A N70N79                          11  1MSGCON(7 JOV1425 SYMSGF)
     A  70                             11  1'Printed:'
     A  79                             11  1'Datum..:'
     A            UDATO1         6  0  11 11EDTCDE(Y)
     A            TID            6  0  11 20EDTWRD('  :  :  ')
N01  A  46                             11 36'L A S T E L I S T E     '
N01  A                                      HIGHLIGHT
S01  A*N70N79                          11 37MSGCON(24 PRI0016 SYMSGF)
N01  A N70N79N46                       11 36MSGCON(24 PRI0016 SYMSGF)
     A                                      HIGHLIGHT
S01  A* 70                             11 37'M A N I F E S T         '
N01  A  70N46                          11 36'M A N I F E S T         '
     A                                      HIGHLIGHT
S01  A* 79                             11 37'L A D E L I S T E       '
N01  A  79N46                          11 36'L A D E L I S T E       '
     A                                      HIGHLIGHT
     A  44                             11 56'/ Lasteliste'
     A N70N79                          11 78MSGCON(12 PRI0002 SYMSGF)
     A                                      HIGHLIGHT
     A  70                             11 78'Tripno.....:'
     A                                      HIGHLIGHT
     A  79                             11 78'Reise nr...:'
     A                                      HIGHLIGHT
     A            HEPRO          8  0  11 91
     A                                      HIGHLIGHT
     A N70N79                          11102MSGCON(8 PRI0004 SYMSGF)
     A  70                             11102'Date...:'
     A  79                             11102'Datum..:'
     A            DATOTU         6  0  11111EDTCDE(Y)
      **
     A N70N79                          12  1MSGCON(8 JOV1426 SYMSGF)
     A  70                             12  1'Agent..:'
     A  79                             12  1'Agent..:'
     A            KNAVN25       25     12 11
     A  92                             12 36'Utskr.nr/versj:'
     A  92        TUTST1         1     12 52
     A  46                             12 36'Kun nye:'
     A  46        KUNNYE         1     12 45
S03  A* 46                             12 51'Kun lagerkode:'
S03  A* 46        KUNLAG         5     12 65
L03  A  46                             12 48'Lager:'
L03  A  46        KUNLAG         5     12 55
     A N45                             12 78MSGCON(12 PRI0005 SYMSGF)
     A N45        TUTARF        15     12 91
     A  45
     A N70N79N41                       12 78MSGCON(12 JOV0578 SYMSGF)
     A  70 45N41                       12 78'Driver 1...:'
     A  79 45N41                       12 78'Schaufeur 1:'
     A  45   N41  TUSJN1        30     12 91
     A        41                       12 78'ETD/ETD....:'
     A        41  XETD           6  0  12 91EDTCDE(Y)
     A        41                       12100'/'
     A        41  XETA           6  0  12102EDTCDE(Y)
      **
     A N70N79N41                       13  1MSGCON(8 PRI0006 SYMSGF)
     A  70   N41                       13  1'Unit no:'
     A  79   N41                       13  1'LKW....:'
     A       N41  TUBILN        10     13 11
     A       N41  TUHENG        10     13 21
     A        41                       13  1'B/L No.:'
     A        41  TUBLNR        17     13 11
     A N70N79N41                       13 36MSGCON(11 PRI0007 SYMSGF)
     A  70   N41                       13 36'Carrier...:'
     A  79   N41                       13 36'Transport.:'
     A       N41  TUNAT         30     13 48
     A                                      HIGHLIGHT
     A        41                       13 33'Vessel name.:'
     A        41  TUBNV         30     13 47
     A  45
     AAN70N79N41                       13 78MSGCON(12 JOV0579 SYMSGF)
     A  70 45N41                       13 78'Driver 2...:'
     A  79 45N41                       13 78'Schaufeur 2:'
     A  45   N41  TUSJN2        30     13 91
     A        41                       13 78'Containerno:'
     A        41  TUCON1        17     13 91
     A        41  TUCKD1         2     13110
      **
     A                                 14  1'------------------------'
     A                                 14 25'------------------------'
     A                                 14 49'------------------------'
     A                                 14 73'------------------------'
     A                                 14 97'------------------------'
      **
S02  A*N70N79                          15  1MSGCON(120 PRI0030 SYMSGF)
N02  A N70N79                          15  1MSGCON(120 JOV1581 SYMSGF)
     A  70                             15 12'Mark/no.                '
     A  79                             15 12'Zeichen/nr.             '
S02  A* 70                             15 30'Consignee               '
S02  A* 79                             15 30'Empfanger               '
S02  A* 70                             15 60'Shipper                 '
S02  A* 79                             15 60'Absender                '
N02  A  70                             15 30'Shipper                 '
N02  A  79                             15 30'Absender                '
N02  A  70                             15 60'Consignee               '
N02  A  79                             15 60'Empfanger               '
     A  70                             15 89'No.pcs'
     A  79                             15 89'Anzahl'
     A  70                             15100'Weight'
     A  79                             15 99'Gewicht'
     A  70                             15108'M3='
     A  70                             15115'LM='
     A  79                             15108'M3='
     A  79                             15115'LM='
      **
     A                                 16  1'------------------------'
     A                                 16 25'------------------------'
     A                                 16 49'------------------------'
     A                                 16 73'------------------------'
     A                                 16 97'------------------------'
     A*
     A                                 17  1' '
     A**
N12  A          R HEAD2SS                   HIGHLIGHT
N12  A                                     1'************************'
N12  A                                    25'************************'
N12  A                                    49'************************'
N12  A                                    73'**'
N12  A                                      SPACEA(1)
N12  A          R HEAD2                     HIGHLIGHT
N12  A                                     1'*'
N12  A            FRTTXT        70         3
N12  A                                    74'*'
N12  A                                      SPACEA(1)
      **
     A          R DET1
     A N48 94                              2'Ref:'
     A N48 94     XXRFA         35         7SPACEA(1)
     A N48        POSNR          3  0      2EDTCDE(Z)
     A N48        HESG           3         8
     A  46        HXLKOD         5         2
     A  46        HXLLST         1         8
     A  47        HEPOSA         3         2
     A            HEGM1         15        14
S02  A*           HENAK29       29        30
S02  A*           HENAS29       29        60
N02  A            HENAS29       29        30
N02  A            HENAK29       29        60
     A            HENT           7  0     88EDTCDE(Z)
     A            HEVKT          9  0     96EDTCDE(Z)
     A            HEM3           7  3    105EDTCDE(3)
     A            HELM           4  2    114EDTCDE(3)
     A                                      SPACEA(1)
      *
     A            HEAVD          4  0      1EDTCDE(Z)
     A                                     5'/'
     A            HEOPD          7  0      6EDTCDE(Z)
     A N48        HEGM2         15        14
     A  48                                13'/'
     A  48        HESGX          3        14
S02  A*           HEADK129      29        30
S02  A*           HEADS129      29        60
N02  A            HEADS129      29        30
N02  A            HEADK129      29        60
     A N70N79                             90MSGCON(11 PRI0010 SYMSGF)
     A  70                                90'Fr.weight: '
     A  79                                90'Fr.Gewicht:'
     A            BVEKT          9  0    102EDTCDE(Z)
     A  48                               112'OT:'
     A  48        HEOT           2       116
     A                                   120' '
     A                                      SPACEA(1)
      * 3.LINJE IKKE VED INNLAND (48 *on=innland)
     A N48N70N79                           1MSGCON(6 PRI0017 SYMSGF)
     A N48 70                              1'Custn:'
     A N48 79                              1'Zolln:'
     A N48        HETLE          6         8
     A N48                                14'.'
     A N48        HETLL         10        15
     A N48 97                             13'Unntaksord.'
     A N48 98                             13'Kan utfores'
S02  A*N48        HEADK229      29        30
S02  A*N48        HEADS229      29        60
N02  A N48        HEADS229      29        30
N02  A N48        HEADK229      29        60
     A N48        HEVS1         25        90
     A                                      SPACEA(1)
      * 4.LINJE, AVVIK VED INNLAND (48 *on=innland)
     A N48        HESDF          5         1
     A N48                                 6'-->'
     A N48        HESDT          5         9
     A N48        HEFR           3        19
S02  A*N48        HEADK329      29        30
S02  A*N48        HEADS329      29        60
N02  A N48        HEADS329      29        30
N02  A N48        HEADK329      29        60
     A N48        HEVS2         25        90
     A                                      SPACEA(1)
      *      V. innland
     A  48        HXSNDN        20         1
S02  A* 48        HEADK3X29     29        30
S02  A* 48        HEADS3X29     29        60
N02  A  48        HEADS3X29     29        30
N02  A  48        HEADK3X29     29        60
     A  48N80     HEVS1X        25        89
     A  48N80     BARCO2         4       114
     A  48 80     BARCOD         8       110
     A  48                               120' '
     A                                      SPACEA(1)
      *      V. innland OG merke2/VS2 - ekstra linje..
     A  48 74                              3'Merke 2:'
     A  48 74 76  HEGM2X        15        12
     A  48 74                             79'V.slag 2:'
     A  48 74 78  HEVS2X        25        89
     A  48 74                            120' '
     A                                      SPACEA(1)
      **
     A          R DETFS                     SPACEA(1)
     A            KFSOTX        30         1
     A                                    31':'
     A            FSSOK         35        33
      **
     A          R DETUFB                    SPACEA(1)
     A                                      HIGHLIGHT
     A                                     1MSGCON(24 YBC0491 SYMSGF)
     A                                    31':'
     A            HEFT1A        30        33
     A            HEFT1B        15        64
      **
     A          R DET2                      SPACEA(1)
     A            FRTTXT        70         1
     A          R BARLEFT
     A            HXSNDNB       20        13
     A            HXSNDN        20         8BARCODE(CODE128 2 *NOHRI *HRZ +
     A                                      (*WIDTH 0.015) X'02')
     A                                      SPACEA(2)
     A          R BARRIGHT
E13  A            HXSNDNB       20        60
E13  A            HXSNDN        20        60BARCODE(CODE128 2 *NOHRI *HRZ +
     A                                      (*WIDTH 0.015) X'02')
     A                                      SPACEA(2)
     A          R BARLEFTO
     A            HEAVDOPDB     12        13
     A            HEAVDOPD      12         8BARCODE(CODE128 2 *NOHRI *HRZ +
     A                                      (*WIDTH 0.015) X'02')
     A                                      SPACEA(2)
     A          R BARRIGHTO
E13  A            HEAVDOPDB     12        70
E13  A            HEAVDOPD      12        70BARCODE(CODE128 2 *NOHRI *HRZ +
     A                                      (*WIDTH 0.015) X'02')
     A                                      SPACEA(2)
     A          R BARSPACE                  SPACEB(1)
     A                                     3' '
      **
     A          R DET3                      SPACEA(1)
     A                                     1'________________________'
     A                                    25'________________________'
     A                                    49'________________________'
     A                                    73'________________________'
     A                                    97'________________________'
      **
     A          R DET4                      SPACEA(1)
     A            FAVK           3         3
     A            WAVT          20         8
     A            WFABEL        13  2     28EDTCDE(J)
     A            WVALK          3        48
      **
     A          R DET5                      SPACEA(1)
     A                                     1'________________________'
     A                                    25'________________________'
     A                                    49'__'
      **
     A          R DET6                      SPACEA(1)
     A                                     1'Godskriving av agent.:'
     A            AGEDET         9  2     25EDTCDE(J)
      **
     A          R DETADR                    SPACEA(1)
     A  72                                 1'Distribusjonsadresser:'
     A                                      SPACEA(1)
     A                                     1'Shippers Rf'
     A            DFSREF        17        13
     A            DFNAVM29      29        30
     A            DFAD1M28      28        60
     A            DFNT           7  0     88EDTCDE(Z)
     A            DFVKT          9  0     96EDTCDE(Z)
     A            DFM3           7  3    105EDTCDE(3)
     A            HELM           4  2    113EDTCDE(3)
     A                                      SPACEA(1)
      *
     A  71                                 1'Instr.'
     A  71        DFLEVI        30         8
     A            DFAD2M29      29        30
     A            DFLKM          2        60
     A            DFPNUM         9        63
     A            DFAD3M        30        72
      **
     A          R SUMADR                    SPACEA(1)
     A                                     1'========================'
     A                                    25'========================'
     A                                    49'========================'
     A                                    73'========================'
     A                                    97'========================'
      **
     A          R PTOTIL                    SPACEB(1)
     A                                      HIGHLIGHT
     A                                    28'Ant byttepall:'
     A            TPALL          5  0     42EDTCDE(Z)
     A                                    48'_______'
      **
     A                                      HIGHLIGHT
     A          R PTOTT                     SPACEB(1)
     A                                     1'BELØP TIL TRANSPORTØR..:'
     A            TRATOT         9  2     28EDTCDE(J)
     A                                    42'(TOTAL DETTE TUR-NR !! )'
      **
     A          R PTOTI                     SPACEB(1)
     A                                      HIGHLIGHT
     A                                     1'* KVITTERING *'
     A                                    26'Ant pll UT(til Transp):'
     A            TPALL          5  0     51
     A                                    78'Sjåførs sign:'
     A                                    92'________________________'
      **
     A          R MANKHEA                   SPACEB(1)
     A                                      HIGHLIGHT
     A                                     1'* MANKO *'
     A                                    22'Sendingsnummer'
     A                                    45'Avd./Oppd'
     A                                    61'Kll listet'
     A                                    73'Kll skutt'
     A                                    85'Kll manko'
      **
     A          R MANKDET                   SPACEB(1)
     A                                    11'Pos:'
     A            HEPOSA         3        17
     A            HXSNDN        20        21
     A                                    43'('
     A            HEAVD          4  0     44EDTCDE(Z)
     A                                    48'/'
     A            HEOPD          7  0     49EDTCDE(Z)
     A                                    56')'
     A            SFANKI         4  0     64EDTCDE(Z)
     A N51        SFANKK         4  0     75EDTCDE(Z)
     A N51        XXANKM         4  0     88EDTCDE(Z)
     A  51                                75'*Ikke kollimerket!*'
     A  52                               100'*HEL-Manko!'
      **
     A          R PTOT1                     SPACEB(2)
     A                                      HIGHLIGHT
     A N70N79                              1MSGCON(24 PRI0012 SYMSGF)
     A  70                                 1'******  T O T A L  *****'
     A  79                                 1'****  GESAMTBETRAG  ****'
     A N70N79                             28MSGCON(14 PRI0013 SYMSGF)
     A  70                                28'Positions....:'
     A  79                                28'Positionen...:'
     A            POSNR          3  0     42EDTCDE(Z)
     A N70N79                             47MSGCON(17 PRI0014 SYMSGF)
     A  70                                47'No. of pcs......:'
     A  79                                47'Anzahl..........:'
     A            TANTAL         7  0     64EDTCDE(Z)
     A N70N79                             73MSGCON(15 PRI0015 SYMSGF)
     A  70                                73'Weight........:'
     A  79                                73'Gewicht.......:'
     A            TVEKT          9  0     87EDTCDE(Z)
     A N70N79                             98MSGCON(11 PRI0010 SYMSGF)
     A  70                                98'Fr.weight.:'
     A  79                                98'Fr.Gewicht:'
     A            TBVEKT         9  0    109EDTCDE(Z)
      *
