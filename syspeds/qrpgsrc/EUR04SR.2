********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
11XXX * 01 YBC PTF11 FLERE KUNDER
11XXX * 02 YBC PTF11 EUROSKO AB
********************************************************************
     H Option( *SrcStmt ) DftActGrp( *No ) BndDir( 'QC2LE' )
     FEPOPOS    IF   E           K DISK
     FEPOH      IF   E           K DISK
     FEPOL      IF   E           K DISK
     FHEADF     IF   E           K DISK
     FBILLOF    IF   E           K DISK
     FBLV1      IF   E           K DISK
     FTURER14   IF   E           K DISK
     FTRACKT    IF   E           K DISK
     FFIRM      IF   E           K DISK
     FCDTRAFL2  IF   E           K DISK
     FEDIC      UF   E             DISK
     FEDIS      O  A E             DISK
     FEDIM      O  A E             DISK
     FEDISX     O  A E             DISK
      *__________
      * Variabler
      *""""""""""
     D FILE_o          s               *
     D String          s            512a
     D rc              s             10i 0
     D Data            s             80a   Varying
     D Codepage        s              5a   Varying
     D CmdLine         S            512
     D CmdLen          S             15  5
      *___________
      * Konstanter
      *"""""""""""
     D LF              c                   x'25'
      *___________________________
      * IFS Stream file funksjoner
      *"""""""""""""""""""""""""""
     Dopen             Pr              *   ExtProc( '_C_IFS_fopen' )
     D                                 *   Value Options( *String )
     D                                 *   Value Options( *String )

     Dfputs            Pr            10i 0 ExtProc( '_C_IFS_fputs' )
     D                                 *   Value Options( *String )
     D                                 *   Value

     Dclose            Pr            10i 0 ExtProc( '_C_IFS_fclose' )
     D                                 *   Value
     D                 DS
     D  WMBR                   1     14
     D  WMBRA                  1      1
     D  WMBRB                  2     10  0
      *//////////////
      *  Hodelogikk /
      *//////////////
     C                   EXSR      SRA
     C                   EXSR      SRB
     C                   MOVE      '1'           *INLR
      *////////////////////////////////////////////////////////////
      *  Initiering                                           SRA /
      *////////////////////////////////////////////////////////////
     C     SRA           BEGSR
      *___________________
      * Definiere Workfelt
      *"""""""""""""""""""
      *_________________
      * Input parametrer
      *"""""""""""""""""
     C     *ENTRY        PLIST
     C                   PARM                    UAVD              4
     C                   PARM                    UOPD              7
     C                   PARM                    FUNKSJON          2
      *__________________
      * Definiere Nøkkler
      *""""""""""""""""""
     C     KEY           KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C     EPOHK         KLIST
     C                   KFLD                    EPKUND
     C                   KFLD                    EPBES
     C     EPOLK         KLIST
     C                   KFLD                    EPKUND
     C                   KFLD                    EPBES
     C                   KFLD                    EPLIN
     C                   KFLD                    EPULIN
     C     CDTRAFL2K     KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    BLTR
      *
     C     Qcmdexc       PLIST
     C                   PARM                    CmdLine
     C                   PARM                    Cmdlen
      *_____________
      * Init av felt
      *"""""""""""""
     C                   MOVE      'G'           WMBRA                          GENERELL
     C                   MOVE      *BLANKS       SDATA          1024
     c                   MOVE      *BLANKS       XXBLNR           16
     c                   MOVE      UAVD          HEAVD
     c                   MOVE      UOPD          HEOPD
     C* Hente dagens dato og klokkeslett.
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
      *
     C     *LOVAL        SETLL     FIRM
     C                   READ      FIRM                                   33
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *                                                       SRB /
      *////////////////////////////////////////////////////////////
     C     SRB           BEGSR

     C     KEY           CHAIN     EPOPOS                             51

     C     *IN51         CABEQ     '1'           SRB9

      *________________________________________
      * Hente og oppdater neste sendningsnummer
      *""""""""""""""""""""""""""""""""""""""""
     C     1             CHAIN     EDIC                               33
     C     *IN33         CABEQ     '1'           SRB9
     C                   ADD       1             CSN
     C                   ADD       1             CMN
     C                   UPDATE    EDICR
     C                   EVAL      WMBRB=CSN
      * Lag fil og set codepage (pc)
     C                   MOVE      *BLANKS       WFilNam         256
N02  C                   SELECT
N02  C                   WHEN      FILAND = 'NO'
     C                   SELECT
     C                   WHEN      FUNKSJON = '40'
N01  C                   SELECT
N01  C                   WHEN      TRKNFA = 10136                               EUROSKO
N01  C                   EVAL      WFilNam='/SYSPEDFX/EUROSKOASN/EDISE1K/' +
N01  C                                     WMBR
N01  C                   WHEN      TRKNFA = 13705                               SHOEDAY
N01  C                   EVAL      WFilNam='/SYSPEDFX/SHOEDAYASN/EDISE1K/' +
N01  C                                     WMBR
N01  C                   OTHER                                                  EUROPRIS
     C                   EVAL      WFilNam='/SYSPEDFX/EUROASN/EDISE1K' +
     C                                     '/BACKUP/' + WMBR
N01  C                   ENDSL
     C                   WHEN      FUNKSJON = '50'
N01  C                   SELECT
N01  C                   WHEN      TRKNFA = 10136                               EUROSKO
N01  C                   EVAL      WFilNam='/SYSPEDFX/EUROSKOASNSTA/EDISE1K/' +
N01  C                                     WMBR
N01  C                   WHEN      TRKNFA = 13705                               SHOEDAY
N01  C                   EVAL      WFilNam='/SYSPEDFX/SHOEDAYASNSTA/EDISE1K/' +
N01  C                                     WMBR
N01  C                   OTHER                                                  EUROPRIS
     C                   EVAL      WFilNam='/SYSPEDFX/EUROASNSTA/EDISE1K' +
     C                                     '/BACKUP/' + WMBR
N01  C                   ENDSL
     C                   ENDSL
N02  C                   WHEN      FILAND = 'SE'
N02  C                   SELECT
N02  C                   WHEN      FUNKSJON = '40'
N02  C                   SELECT
N02  C                   WHEN      TRKNFA = 2035                                EUROSKO AB
N02  C                   EVAL      WFilNam='/SYSPEDFSEX/EUROSKOASN/EDISE1K/' +
N02  C                                     WMBR
N02  C                   OTHER                                                  EUROSKO AB
N02  C                   EVAL      WFilNam='/SYSPEDFSEX/EUROASN/EDISE1K' +
N02  C                                     '/BACKUP/' + WMBR
N02  C                   ENDSL
N02  C                   WHEN      FUNKSJON = '50'
N02  C                   SELECT
N02  C                   WHEN      TRKNFA = 2035                                EUROSKO AB
N02  C                   EVAL      WFilNam='/SYSPEDFSEX/EUROSKOASNSTA/EDISE1K/'
N02  C                                   + WMBR
N02  C                   OTHER                                                  EUROSKO AB
N02  C                   EVAL      WFilNam='/SYSPEDFSEX/EUROSKOASNSTA/EDISE1K/'
N02  C                                   + WMBR
N02  C                   ENDSL
N02  C                   ENDSL
N02  C                   ENDSL
     C                   EVAL      FILE_o = open( %TrimR( WFilNam )
     C                             : 'w, codepage=850' )
     C                   EVAL      rc = close( FILE_o )
     C                   EVAL      FILE_o = open( %TrimR( WFilNam )
     C                             : 'w' )

      *Skriv hode til fil
     C                   EXSR      SR00
      *
     C                   EXSR      SR11
     C*    FUNKSJON      CABEQ     '50'          SRB9
      *
      *Skriv linjer til fil
     C                   DOW       *IN51 = *OFF
     C                   EXSR      SR21
     C     KEY           READE     EPOPOS                                 51
     C                   ENDDO
      *Skriv avslutt til fil
     C                   EXSR      SR09
      *_________________
      * Logge sendningen
      *"""""""""""""""""
     C                   CLEAR                   EDISR
N02  C                   SELECT
N02  C                   WHEN      FILAND = 'NO'
     C                   EVAL      S0004='CC'
     C                   SELECT
     C                   WHEN      FUNKSJON = '40'
N01  C                   SELECT
N01  C                   WHEN      TRKNFA = 10136                               EUROSKO
N01  C                   EVAL      S0010='EUROSKOASN'
N01  C                   WHEN      TRKNFA = 13705                               SHOEDAY
N01  C                   EVAL      S0010='SHOEDAYASN'
N01  C                   OTHER                                                  EUROPRIS
     C                   EVAL      S0010='EUROASN'
N01  C                   ENDSL
     C                   WHEN      FUNKSJON = '50'
N01  C                   SELECT
N01  C                   WHEN      TRKNFA = 10136                               EUROSKO
N01  C                   EVAL      S0010='EUROSKOASNSTA'
N01  C                   WHEN      TRKNFA = 13705                               SHOEDAY
N01  C                   EVAL      S0010='SHOEDAYASNSTA'
N01  C                   OTHER                                                  EUROPRIS
     C                   EVAL      S0010='EUROASNSTA'
N01  C                   ENDSL
     C                   ENDSL
N02  C                   WHEN      FILAND = 'SE'
N02  C                   EVAL      S0004='CCSE'
N02  C                   SELECT
N02  C                   WHEN      FUNKSJON = '40'
N02  C                   SELECT
N02  C                   WHEN      TRKNFA = 2035                                EUROSKO AB
N02  C                   EVAL      S0010='EUROSKOASN'
N02  C                   OTHER                                                  EUROSKO AB
N02  C                   EVAL      S0010='EUROSKOASN'
N02  C                   ENDSL
N02  C                   WHEN      FUNKSJON = '50'
N02  C                   SELECT
N02  C                   WHEN      TRKNFA = 2035                                EUROSKO AB
N02  C                   EVAL      S0010='EUROSKOASNSTA'
N02  C                   OTHER                                                  EUROSKO AB
N02  C                   EVAL      S0010='EUROSKOASNSTA'
N02  C                   ENDSL
N02  C                   ENDSL
N02  C                   ENDSL
     C                   Z-ADD     CSN           WMBRB
     C                   MOVE      WMBR          S0020
     C                   EVAL      S0026  = UAVD + '-' + UOPD
     C                   Z-ADD     CSN           SSN
     C                   MOVE      'S'           SSR
N02  C                   SELECT
N02  C                   WHEN      FILAND = 'NO'
N01  C                   SELECT
N01  C                   WHEN      TRKNFA = 10136                               EUROSKO
N01  C*                  MOVE      'C'           SST
N01  C                   MOVE      'A'           SST
N01  C                   WHEN      TRKNFA = 13705                               SHOEDAY
N01  C                   MOVE      'C'           SST
N01  C                   OTHER                                                  EUROPRIS
     C                   MOVE      'A'           SST
N01  C                   ENDSL
N02  C                   WHEN      FILAND = 'SE'
N02  C                   SELECT
N02  C                   WHEN      TRKNFA = 2035                                EUROSKO AB
N02  C                   MOVE      'A'           SST
N02  C                   OTHER                                                  EUROSKO AB
N02  C                   MOVE      'A'           SST
N02  C                   ENDSL
N02  C                   ENDSL
     C                   MOVEL     WDT           SDT
     C                   MOVEL     WTM           STM
     C                   MOVEL     '*IFS'        SFILE
     C                   MOVEL     WMBR          SMBR
     C                   MOVEL     WFILNAM       SIFS
     C                   MOVEL     'SYSPEDF'     SLIB
     C                   WRITE     EDISR
      *_________________
      * Logge melding
      *"""""""""""""""""
     C                   CLEAR                   EDIMR
     C                   EVAL      M0004  = S0004
     C                   EVAL      M0010  = S0010
     C                   EVAL      M0062  = UAVD + UOPD
     C                   EVAL      M0065  = 'ASN' + FUNKSJON
     C                   EVAL      M0068  = UAVD + UOPD + M0065
     C                   EVAL      M1001  = 'ASN'
     C                   EVAL      M1225  = 'ASN'
     C                   EVAL      M1N07  = 'S' + FUNKSJON
     C                   EVAL      MSN    = SSN
     C                   EVAL      MMN    = CMN
     C                   EVAL      MSR    = SSR
     C                   EVAL      MST    = 'C'
     C                   EVAL      MST    = 'C'
     C                   EVAL      MDT    = SDT
     C                   EVAL      MTM    = STM
     C                   MOVE      UAVD          MAVD
     C                   MOVE      UOPD          MTDN
     C                   WRITE     EDIMR

     C                   CLEAR                   EDISXR
     C                   EVAL      SXPRODM=WMBR
     C                   EVAL      SXIFSOBJ=Wfilnam
     C                   WRITE     EDISXR

     C     SRB9          ENDSR
      *////////////////////////////////////////////////////////////
      * Skriv 00 Start av fil                                SR00 /
      *////////////////////////////////////////////////////////////
     C     SR00          BEGSR

     C                   EVAL      SDATA=*BLANKS

N02  C                   SELECT
N02  C                   WHEN      FILAND = 'NO'
     C                   SELECT
     C                   WHEN      FUNKSJON = '40'
N01  C                   SELECT
N01  C                   WHEN      TRKNFA = 10136                               EUROSKO
N01  C                   EVAL      SDATA = '00;CCNO;EUROSKO;'
N01  C                   WHEN      TRKNFA = 13705                               SHOEDAY
N01  C                   EVAL      SDATA = '00;CCNO;SHOEDAY;'
N01  C                   OTHER                                                  EUROPRIS
     C                   EVAL      SDATA = '00;CCNO;EUROPRIS;'
N01  C                   ENDSL
N01  C                   EVAL      SDATA = %TRIM(SDATA)
     C                             + %TRIM(WDT) + ';' + %TRIM(WTM) + ';'
     C                             + %TRIM(WMBR) + ';' + 'ASN;'
     C                             + %TRIM(FUNKSJON) + ';'
     C                   WHEN      FUNKSJON = '50'
N01  C                   SELECT
N01  C                   WHEN      TRKNFA = 10136                               EUROSKO
N01  C                   EVAL      SDATA = '00;CCNO;EUROSKO;'
N01  C                   WHEN      TRKNFA = 13705                               SHOEDAY
N01  C                   EVAL      SDATA = '00;CCNO;SHOEDAY;'
N01  C                   OTHER                                                  EUROSKO
     C                   EVAL      SDATA = '00;CCNO;EUROPRIS;'
N01  C                   ENDSL
N01  C                   EVAL      SDATA = %TRIM(SDATA)
     C                             + %TRIM(WDT) + ';' + %TRIM(WTM) + ';'
     C                             + %TRIM(WMBR) + ';' + 'ASNStatus;'
     C                             + %TRIM(FUNKSJON) + ';'
     C                   ENDSL
N02  C                   WHEN      FILAND = 'SE'
N02  C                   SELECT
N02  C                   WHEN      FUNKSJON = '40'
N02  C                   SELECT
N02  C                   WHEN      TRKNFA = 2035                                EUROSKO AB
N02  C                   EVAL      SDATA = '00;CCSE;EUROSKO;'
N02  C                   OTHER                                                  EUROSKO AB
N02  C                   EVAL      SDATA = '00;CCSE;EUROSKO;'
N02  C                   ENDSL
N02  C                   EVAL      SDATA = %TRIM(SDATA)
N02  C                             + %TRIM(WDT) + ';' + %TRIM(WTM) + ';'
N02  C                             + %TRIM(WMBR) + ';' + 'ASN;'
N02  C                             + %TRIM(FUNKSJON) + ';'
N02  C                   WHEN      FUNKSJON = '50'
N02  C                   SELECT
N02  C                   WHEN      TRKNFA = 2035                                EUROSKO AB
N02  C                   EVAL      SDATA = '00;CCSE;EUROSKO;'
N02  C                   OTHER                                                  EUROSKO AB
N02  C                   EVAL      SDATA = '00;CCSE;EUROSKO;'
N02  C                   ENDSL
N02  C                   EVAL      SDATA = %TRIM(SDATA)
N02  C                             + %TRIM(WDT) + ';' + %TRIM(WTM) + ';'
N02  C                             + %TRIM(WMBR) + ';' + 'ASNStatus;'
N02  C                             + %TRIM(FUNKSJON) + ';'
N02  C                   ENDSL
N02  C                   ENDSL

     C                   EVAL      rc = fputs(%trimr(SDATA) + LF: File_o)

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * Skriv 09 Slutt på fil                                SR09 /
      *////////////////////////////////////////////////////////////
     C     SR09          BEGSR

     C                   EVAL      SDATA=*BLANKS

     C                   EVAL      SDATA = '09;' + %TRIM(WMBR) + ';'

     C                   EVAL      rc = fputs(%trimr(SDATA) + LF: File_o)
     C                   EVAL      rc = close( FILE_o )

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * Skriv 11 Hode                                        SR11 /
      *////////////////////////////////////////////////////////////
     C     SR11          BEGSR

     C     KEY           CHAIN     HEADF                              33
     C     KEY           CHAIN     BILLOF                             33
     C     HEPRO         CHAIN     TURER14                            33
     C     KEY           CHAIN     TRACKT                             33
     C     KEY           CHAIN     BLV1                               33
     C     CDTRAFL2K     CHAIN     CDTRAFL2                           33
     C   33              EVAL      CTFT = BLTR

     C                   SELECT
     C                   WHEN      FUNKSJON = '40'
     C                   EVAL      SDATA='11;'                                  01=Posttype
     C                             +%trim(BVMN)+';'                             02=Containernr
     C*                            +%trim(TUBNV)+'/'+%trim(BVKDC)+';'           03=Carrier name
     C                             +%trim(CTFT)+'/'+%trim(BVKDC)+';'            03=Carrier name
     C                             +%trim(%editc(TXATDD:'Z'))+';'               04=Shi.dato bekr-ATD
     C                             +%trim(%editc(TXETAD:'Z'))+';'               05=Ank.dato ETA
     C                   WHEN      FUNKSJON = '50'
     C                   EVAL      SDATA='11;'                                  01=Posttype
     C*                            +%trim(BVMN)+';'                             02=Containernr
     C*                            +%trim(%editc(TXATAD:'Z'))+';'               03=Shi.dato bekr-ATD
     C                             +%trim(BVMN)+';'                             02=Containernr
     C*                            +%trim(TUBNV)+'/'+%trim(BVKDC)+';'           03=Carrier name
     C                             +%trim(CTFT)+'/'+%trim(BVKDC)+';'            03=Carrier name
     C                             +%trim(%editc(TXATDD:'Z'))+';'               04=Shi.dato bekr-ATD
     C                             +%trim(%editc(TXATAD:'Z'))+';'               05=Ank.dato bekr-ATA
     C                   ENDSL

     C                   EVAL      rc = fputs(%trimr(SDATA) + LF: File_o)

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * Skriv 21 Linje                                       SR21 /
      *////////////////////////////////////////////////////////////
     C     SR21          BEGSR
     C*    KEY           CHAIN     BILLOF                             33
     C     EPOHK         CHAIN     EPOH                               33
     C     EPOLK         CHAIN     EPOL                               33
     c     *in33         cabeq     '1'           UtSr21                         finnes ikke
     c     EHOPD         cabeq     -9999999      UtSr21                         Slettet
     c     ELOPD         cabeq     -9999999      UtSr21                         Slettet
      *
     C                   eval      XXBLNR = BLBLNR
      *
     C                   IF        EHBLNR <> *BLANKS
     C                   EVAL      XXBLNR = EHBLNR
     C                   ENDIF

     C                   IF        ELBLNR <> *BLANKS
     C                   EVAL      XXBLNR = ELBLNR
     C                   END
      *
      * ant kolli lev ligger med 3 desim - over i heltall
     c                   z-add(H)  ELNTKL        ELNTKLH           7 0
      *
     c                   EVAL      SDATA='21;'                                  01=Posttype
     c                             +%trim(ELBES)+';'                            02=Ordrenr
     c                             +%trim(%editc(ELLIN:'Z'))+';'                03=Linjenr
     c                             +%trim(%editc(ELULIN:'Z'))+';'               04=UnderLinjenr
     c                             +%trim(ELART)+';'                            05=Art.Nr Europris
     c                             +%trim(ELARTE)+';'                           06=Art.Nr EAN
     c                             +%trim(ELARTL)+';'                           07=Art.Nr Leverandør
     c                             +%trim(ELABES)+';'                           08=Art.Beskrivelse
     c*                            +%trim(%editc(ELANTS:'Z'))+';'               09=Ant.F-Pak best.
     c*                            +%trim(%editc(ELANKL:'Z'))+';'               10=Ant.D-Pak best.
     c                             +%trim(%editc(ELNTSL:'Z'))+';'               09=Ant.F-Pak bekr.
     c                             +%trim(%editc(ELNTKLH:'Z'))+';'              10=Ant.D-Pak bekr.
     c                             +%trim(XXBLNR)+';'                           11=BL-nr

     C                   EVAL      rc = fputs(%trimr(SDATA) + LF: File_o)

     C     UtSr21        ENDSR
