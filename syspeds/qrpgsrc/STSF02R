      *********************************************************************
      *  After compiling this RPG MODULE,
      *  create the related program with the following command:
      *
CRTPG+*  CRTPGM SYSPED/STSF02R MODULE(*LIBL/STSF02R *LIBL/INCGI)
CRTPGM*         ACTGRP(*NEW) AUT(*USE)
      *
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 JOV PTF10 felt/Avd 0000 = Alle felt
********************************************************************
      *
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
      *--------------------------------------------------------------------
      * Data base files
      *--------------------------------------------------------------------
     FBRIDF     IF   E           K DISK    USROPN
     FKUFAST    IF   E           K DISK    USROPN
     FDISP      IF   E           K DISK    USROPN
N01  FDISPL01   IF   E           K DISK    USROPN
N01  F                                     RENAME(DISPR:DISPR01)
     FHEADF     IF   E           K DISK    USROPN
     FHEAD8     IF   E           K DISK    USROPN
     F                                     RENAME(HEADFR:HEADFR08)
     FHEAD39    IF   E           K DISK    USROPN
     F                                     RENAME(HEADFR:HEADFR39)
     FTELLGE    UF A E           K DISK    usropn
     FDOKUF     IF   E           K DISK    usropn
     FDOKUFM    IF   E           K DISK    usropn
     FDOKUFM1   IF   E           K DISK    usropn
     F                                     RENAME(DOKUFMR:DOKUFMR1)
     FTRACKK    O  A E             DISK    usropn
     FSTSTUR    UF A E           K DISK    usropn
     FSTSOPD    UF A E           K DISK    usropn
     FSTSFBR    UF A E           K DISK    usropn
     FSTSCLI1   UF A E           K DISK    usropn
      *--------------------------------------------------------------------
      * Prototype defintions and standard system API error structure
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      *
      *Array
     D AVDFELT1        S              4  0 DIM(100)
     D AVDFELT2        S             50    DIM(100)
      *--------------------------------------------------------------------
      * Variables common to CGI programs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
      * Variables received from the input string
     DTurNr            s              8
     DwsPro            s              8  0
     DAvd              s              4
     DWsAvd            s              4  0
     DOpdNr            s              7
     DWsOpd            s              7  0
     DManAnt           s              4
     DManAntN          s              4  0
     DManTxt           s             30
     DManu             s              1
     DWsClId           s             20
     Dwsclid18         s             18
     DViaFrbr          s              1
     DUser             s             10
     D SEMI            s              1
      * andre variabler
     D WsAnOM          s              4  0
     D BridSw          s              1
     D WsAnFb          s              4  0
     D WsAnFk          s              4  0
     D WsAnFM          s              4  0
     D WsAnKi          s              4  0
     D WsAnKk          s              4  0
     D WsAnKM          s              4  0
     D Fn              s             10
     D Lib             s             10
     D Mbr             s             10
     D Feil            s            150
     D FeilKode        s              1
     D FeilIni         s              1
      *
     D                 DS
     D  KFTXTE                 1     50
     D  XFKAVD                 1     50    DIM(10)
     D                 DS
     D  FMAI00                 1      2
     D  FMMRK1                 3     37
     D  FMMRK18                3     20
     D  FMCLID                 1     20
     D                 DS
     D  xxrfk                  1     35
     D  xxavd                  1      4
     D  xxstrek                5      5
     D  xxopd                  6     12
      *--------------------------------------------------------------------
      * Prolog common to CGI programs
      * -Receive input from the remote browser
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prolog3
      *=====================================================================******
      * MAIN PROCESS LINE
      *=====================================================================******
      * Parse input string
     C                   EXSR      PARSE
      ***
      * User  - Set libl ..
     C                   exsr      Init
      *
     C                   IF        WSPRO = 0
     C                   EXSR      HENTFELT
     C                   END
      * Set output skeleton html member name
     C                   exsr      SetSkl
      * Read skeleton output html, etc.
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
      *------------------
      * Write sections of HTML.
      *
      * Her er HOVED logikk-rutine
     C                   exsr      SetTop
      *
     C     SEMI          IFNE      'Y'
     C                   callp     wrtsection('top')
     C     FeilIni       IFNE      'X'
     C                   callp     wrtsection('Bot')
     C                   else
     C                   callp     wrtsection('FeilI')
     C                   end
     C                   callp     wrtsection('Bott')
     C                   else
     C                   callp     wrtsection('all')
     C                   end
      *
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      *
      *
     C                   close     BRIDF
     C                   close     TRACKK
     C                   close     KUFAST
     C                   close     DISP
N01  C                   close     DISPL01
     C                   close     HEADF
     C                   close     HEAD8
     C                   close     HEAD39
     C                   close     tellge
     C                   close     DOKUF
     C                   close     DOKUFM
     C                   close     DOKUFM1
     C                   close     STSTUR
     C                   close     STSOPD
     C                   close     STSFBR
     C                   close     STSCLI1
     C                   eval      *inlr = *on
     C                   RETURN
      *
      *=====================================================================******
      * Parse input
      *=====================================================================******
     C     Parse         begsr
     C                   eval      USER     = zhbgetvar('USER')
     C                   eval      YTYPE    = zhbgetvar('YTYPE')
     C                   eval      TURNR    = zhbgetvar('TURNR')
     C                   eval      WSPRO    = c2n2(TURNR)
     C                   eval      WSCLID   = zhbgetvar('CLID')
     C                   eval      ManAnt   = zhbgetvar('Ant')
     C                   eval      ManAntN  = c2n2(ManAnt)
     C                   eval      ManTxt   = zhbgetvar('Txt')
     C                   eval      Manu     = zhbgetvar('Manu')
     C     Manu          ifeq      'Y'
     C     ManAntN       andeq     *zeros
     C     ManTxt        andeq     *blanks
     C                   eval      ManTxt = 'SHORT!'
     C                   end
     C                   eval      SEMI     = zhbgetvar('SEMI')
      *
     C                   endsr
      *=====================================================================
      * EXECUTE LOGIC / Set html output variables for section /$top
      *=====================================================================
     C     SetTop        begsr
      *
     C     WSCLID        IFNE      *BLANKS
     C                   EXSR      TESTB2
     C                   END
      *
      *
      * HENT ANTALL FOR Å VISE PÅ SKJERMEN
     C                   EXSR      HANTS
      *
     C                   callp     updhtmlvar('FEILKOD':FeilKode)
     C                   callp     updhtmlvar('FEILTXT':Feil)
      *
      * Repeat UserId for the next invocation
     C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updhtmlvar('User':user)
     C                   MOVE      WSPRO         TURNR             8
     C                   callp     updhtmlvar('TURNR':turnr)
     C                   callp     updhtmlvar('YTYPE':YTYPE)
      *
     C* Ant. oppdrag totalt / kontrollert/Manko
     C                   callp     updhtmlvar('OPD':
     C                             %trim(%editc(stanop:'3')))
     C                   callp     updhtmlvar('OPDK':
     C                             %trim(%editc(stanok:'3')))
     C     Stanop        sub       stanok        WsAnOm
     c     WsAnOm        IFLT      *ZEROS
     c                   MOVE      *ZEROS        WsAnOm
     C                   end
     C                   callp     updhtmlvar('OPDM':
     C                             %trim(%editc(Wsanom:'3')))
     C* Ant. oppdrag totalt / kontrollert/Manko
     C                   callp     updhtmlvar('FBR':
     C                             %trim(%editc(Wsanfb:'3')))
     C                   callp     updhtmlvar('FBRK':
     C                             %trim(%editc(wsanfk:'3')))
     C     wsanfb        sub       wsanfk        wsanfm
     c     wsanfm        IFLT      *ZEROS
     c                   MOVE      *ZEROS        wsanfm
     C                   end
     C                   callp     updhtmlvar('FBRM':
     C                             %trim(%editc(wsanfm:'3')))
     C* Ant. oppdrag totalt / kontrollert/Manko
     C                   callp     updhtmlvar('KLI':
     C                             %trim(%editc(wsanki:'3')))
     C                   callp     updhtmlvar('KLIK':
     C                             %trim(%editc(wsankk:'3')))
     C     wsanki        sub       wsankk        wsankm
     c     wsankm        IFLT      *ZEROS
     c                   MOVE      *ZEROS        wsankm
     C                   end
     C                   callp     updhtmlvar('KLIM':
     C                             %trim(%editc(wsankm:'3')))
      *
      * Hvis manuelt fra oppdr. og det finnes flere, vis oppdragsliste på ny.
     C     Manu          ifeq      'Y'
     C     Feilkode      andeq     ' '
     C     *in30         andeq     '0'
     C                   callp     updhtmlvar('FEILKOD':'O')
     C                   End
      *
     C                   endsr
      *
     C***********************************************
     C     TESTB2        BEGSR
     C***********************************************
      * SR KJØRES KUN NÅR WSCLID ULIK BLANK
     C                   MOVE      *BLANKS       WSCLID3           3
     C                   MOVE      *BLANKS       WSCLID10         10
     C                   MOVEL     WSCLID        WSCLID3
     C                   MOVE      WSCLID        WSCLID10
     C                   MOVEA     '00000'       *IN(41)
     C                   move      'N'           ViaFrbr
     C                   Move      *blanks       FeilINI
     C                   Move      *blanks       Feil
     C                   Move      *blanks       FeilKode
      *
      * ER DET FRAKTBREV SOM ER SKUTT? (begynner på 401 eller gml 10 lang)
     C     WSCLID3       IFEQ      '401'                                        1<-B
     C     WSCLID10      OREQ      *BLANKS                                      1<-B
     C                   EXSR      SR401
     C                   ELSE
     C                   EXSR      SR003
     C                   END
      *
     C   30              eval      feilKode = 'X'
     c   30              goto      UtTestb
      *
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVE      WDT           SCDT
     C                   MOVE      WTM           SCTID
     C                   MOVE      User          SCUSER
     C                   MOVE      'X'           SCSTAT
      * manuellt merking forutsettes å være på oppdaget - minder ant - kun txt
     C     ManAntN       ifne      *zeros
     C     ManAntN       andlt     HENT
     C     OPDK1         CHAIN     STSOPD                             33
     C                   movel     ManTxt        SOMERK
     C                   z-add     ManAntN       SOAMKK
     C                   UPDATE    STSOPDR
     c                   goto      Uttestb
     C                   end
     C     FBRK1         CHAIN     STSFBR                             33
     C     FlereFb       Tag
     C     ViaFrbr       IFNE      'J'
     C                   ADD       1             SFANKK                          *FIL
     C                   ADD       1             WSANKK                          *PÅ SKJERM
     c                   ELSE
     C                   z-add     SFANKI        SFANKK
     c                   END
     C     SFANKK        IFGE      SFANKI                                         3<-B
     C                   MOVE      'X'           SFSTAT
     c
     C     OPDK1         CHAIN     STSOPD                             33
     C                   movel     ManTxt        SOMERK
     C                   z-add     ManAntN       SOAMKK
     C                   ADD       1             SOANFK                          *FIL
     C                   ADD       1             WSANFK                          *PÅ SKJERM
     C     SOANFK        IFGE      SOANFB                                          4<-B
     C                   MOVE      'X'           SOSTAT
     C     TURKEY        CHAIN     STSTUR                             33         *FIL
     C                   ADD       1             STANOK                          *SKJERM/FIL
     C                   UPDATE    STSTURR
     C                   END                                                       4<-E
     C                   UPDATE    STSOPDR
     C                   END                                                      3<-E
     C                   UPDATE    STSFBRR
      * Når lest på fr.brevsnivå skal ikke STSCLI oppdateres..
     C     ViaFrbr       IFNE      'J'
     C                   UPDATE    STSCLIR
     C                   Else                                                   1<-E
     C                   add       1             scfbnr
     C     FBRK1         CHAIN     STSFBR                             33
     C  N33              goto      FlereFb
     C                   END                                                    1<-E
      *
     C     UTTESTB       ENDSR
      *
     C***********************************************
     C     SR003         BEGSR
     C***********************************************
     C     CLIK1         CHAIN     STSCLI1                            30
      *
      * finner ikke kolli-id i skyte-fila
     C                   IF        *IN30                                        1<-B
     C                   MOVE      WSCLID        XCLID18          18
     C                   MOVEL     XCLID18       XCLID35          35
     C                   MOVE      *BLANKS       XTXT             50
     C     XCLID35       SETGT     DOKUFM1
     C     XCLID35       READPE    DOKUFM1                                33
     C                   IF        *IN33
     C                   EVAL      TKAVD  = 0
     C                   EVAL      TKOPD  = 0
     C                   EVAL      TKFBNR = 0
     C                   ELSE
     C                   EVAL      TKAVD  = FMAVD
     C                   EVAL      TKOPD  = FMOPD
     C                   EVAL      TKFBNR = FMFBNR
     C     1             DO        100           XN
     C                   IF        AVDFELT1(XN) = TKAVD
     C                   EVAL      XTXT = ', Skulle på felt '
     C                             + %TRIM(AVDFELT2(XN))
     C                   LEAVE
     C                   END
     C                   ENDDO
     C                   END
      *
      * Skriv feilmelding til TRACKK
     C                   EXSR      UPDTRACK
      * SEND FEILMELDING
     C                   EVAL      feil = 'UKjent kolli-ID lest'
     C                             + %TRIM(XTXT)
     C                   GOTO      Ut003
     C                   END
      *
      * KOLLI-ID finnes i skytelista - vellykket les.
     C     SCSTAT        IFEQ      'X'                                           2<-B
      * KOLLI-ID ER KONTROLLERT FRA FØR.
     C                   MOVE      *ON           *IN30
     C                   eval      feil = wsclid+ ' er alt skutt'
     c                   update    stsclir
     C     FBRK1         CHAIN(N)  STSFBR                             33
     C     OPDK1         CHAIN(N)  STSOPD                             33
     C     TURKEY        CHAIN(N)  STSTUR                             33         *FIL
     C                   End                                                     2
      *
     C     UT003         ENDSR
      *
     C***********************************************
     C     SR401         BEGSR
     C***********************************************
     C                   EVAL      TKAVD  = 0
     C                   EVAL      TKOPD  = 0
     C                   EVAL      TKFBNR = 0
     C                   MOVE      *BLANKS       XTXT             50
      * Skyting basert på Sendingsnr...
      * sendingen må finnes..
     C                   movel     wsclid        XXRFK            35
     C     xxstrek       ifeq      '/'
     C                   eval      FMAVD    = c2n2(xxavd)
     C                   eval      FMOPD    = c2n2(xxopd)
     C     HEAKEY        CHAIN     HEADF                              30
     c                   IF        *IN33
     C                   EXSR      UPDTRACK
     C                   eval      feil = 'UKjent sending lest'
     c                   goto      Ut401
     c                   else
     c                   goto      OkSndnr
     c                   end
     c                   end
     C     wsclid        SETLL     HEAD39
     C     wsclid        READE     HEAD39                                 30
     C     *in30         IFEQ      '1'
     C                   EXSR      UPDTRACK
     C                   eval      feil = 'UKjent sending lest'
     c   30              goto      Ut401
     C                   end
      *
      * OK sending lest
     C     OkSndnr       tag
      *
      * Sending må finnes i "skyteliste" for aktuell tur
     C     Str401        tag
     C     OPDKEY        CHAIN(N)  STSFBR                             33
     C                   IF        *IN33
     C                   EXSR      UPDTRACK
     C                   SETON                                        30
     C                   eval      feil = 'UKjent sending lest'
     c                   goto      Ut401
     C                   end
      *
      * Fraktbrev må ikke være skutt før..
     c     SFSTAT        IFNE      ' '
     C                   SETON                                        30
     C                   eval      feil = 'Sendingen ER alt skutt'
     c   30              goto      Ut401
     C                   end
      *
      * ALT OK.
      * Klargjør nøkkel for oppdat av STSFBR/STSOPD... i sr "settop"
     C                   MOVE      'J'           ViaFrbr
     c                   move      ' '           scstat
     c                   move      heavd         scavd
     c                   move      heopd         scopd
     c                   z-add     1             scfbnr
      *
     C     Ut401         ENDSR
      *
      ***********************************************
     C     UPDTRACK      BEGSR
      ***********************************************
     C                   EVAL      TKCLID = WSCLID
     C                   EVAL      TKTEXT = 'Funnet på felt'
     C                             + ' ' + YTYPE + %TRIM(XTXT)
     C                   EVAL      TKUSER = USER
     C                   EVAL      TKKODE = 'FLT'
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVE      WDT           TKDATE
     C                   MOVE      WTM           TKTIME
     C                   MOVE      YTYPE         TKSTYP
     C                   MOVE      WSPRO         TKSPRO
     C                   WRITE     TRACKKR
      *
     C                   ENDSR
      *
      ***********************************************
     C     HENTFELT      BEGSR
      ***********************************************
      *  SJEKK OM DET FINNES PARKERT FELTKONTROLL
     C     YTYPE         SETLL     STSTUR
     C     YTYPE         READE(N)  STSTUR                                 33
     C                   DOW       *IN33 = *OFF
     C                   IF        STSTAT = ' '
     C                   MOVE      STPRO         WSPRO
     C                   LEAVE
     C                   END
     C     YTYPE         READE(N)  STSTUR                                 33
     C                   ENDDO
      *
     C                   IF        *IN33
     C                   Movel     'ESTURSTS'    GECO
     C     GECO          Chain     TellGE                             33
     C   33              Z-add     1             GENO
     C                   Z-add     GENO          WSPRO
     C                   add       1             GENO
     C   33              Write     TELLGER
     C  N33              Update    TELLGER
      *
     C                   CLEAR                   STSTURR
     C                   MOVE      YTYPE         STTYPE
     C                   MOVE      WSPRO         STPRO
      * Kun oppdrag i Transport-modulen med turnr 0 / disp-stat ' ' bør finnes på term...
N01  c                   IF        XFKAVD(1)  =  '0000'
n01  c     Disp1Key      setll     displ01
N01  c     Disp1Key      reade     displ01                                33
n01  c     *in33         doweq     '0'
n01  C     DISPK         CHAIN     DISP                               33
N01  C  N33              EXSR      HETTOPD
n01  c     Disp1Key      reade     displ01                                33
N01  c                   enddo
N01  c                   else
     C                   MOVE      *ZEROS        HEPRO
     C     1             DO        10            XN                3 0
     C                   IF        XFKAVD(XN) <> *BLANKS
     C                   MOVEL     XFKAVD(XN)    HEAVD
     C     HEAD8K        SETLL     HEAD8
     C     HEAD8K        READE     HEAD8                                  33
     C                   DOW       *IN33 = *OFF
     C     DISPK         CHAIN     DISP                               33
     C                   IF        DISTA1 = ' '
     C                   EXSR      HETTOPD
     C                   END
     C     HEAD8K        READE     HEAD8                                  33
     C                   ENDDO
     C                   END
     C                   ENDDO
N01  c                   endif
      *
     C                   WRITE     STSTURR
     C                   EXSR      TTSR
     C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     HETTOPD       BEGSR
     C***********************************************
     C                   ADD       1             STANOP
     C                   CLEAR                   STSOPDR
     C                   MOVE      STTYPE        SOTYPE
     C                   MOVE      WSPRO         SOPRO
     C                   MOVE      HEAVD         SOAVD
     C                   MOVE      HEOPD         SOOPD
      *
     C                   EXSR      HFBR
      *
     C                   WRITE     STSOPDR
      *
     C                   ENDSR
      *
     C***********************************************
     C     HFBR          BEGSR
     C***********************************************
     C     DOKUFK        SETLL     DOKUF
     C     DOKUFK        READE     DOKUF                                  33
      * ingen fraktbrev, simuler nr 001 ("frittstående" edimottatte CLID!!!)
     C     *in33         ifeq      '1'
     C                   Z-add     1             DFFBNR
     C                   goto      dummyf
     C                   end
     C     *IN33         DOWEQ     '0'
     C     dummyf        tag
     C                   ADD       1             SOANFB
     C                   CLEAR                   STSFBRR
     C                   MOVE      SOTYPE        SFTYPE
     C                   MOVE      WSPRO         SFPRO
     C                   MOVE      HEAVD         SFAVD
     C                   MOVE      HEOPD         SFOPD
     C                   MOVE      DFFBNR        SFFBNR
      *
     C                   EXSR      HCLI
      *
     C                   WRITE     STSFBRR
     C     DOKUFK        READE     DOKUF                                  33
     C  N33              ENDDO
      *
     C                   ENDSR
      *
     C***********************************************
     C     HCLI          BEGSR
     C***********************************************
     C     DOKFMK        SETLL     DOKUFM
     C     DOKFMK        READE     DOKUFM                                 33
     C     *IN33         DOWEQ     '0'
     C                   ADD       1             SFANKI
     C                   CLEAR                   STSCLIR
     C                   MOVE      SFTYPE        SCTYPE
     C                   MOVE      WSPRO         SCPRO
     C                   MOVE      HEAVD         SCAVD
     C                   MOVE      HEOPD         SCOPD
     C                   MOVE      FMFBNR        SCFBNR
     C                   MOVE      FMCLID        SCCLID
     C                   WRITE     STSCLIR
     C     DOKFMK        READE     DOKUFM                                 33
     C  N33              ENDDO
      *
     C                   ENDSR
      *
     C***********************************************
     C     HANTS         BEGSR
     C***********************************************
      * HENT TURENS TOTALER FOR VISING PÅ SKJERM
     C     TURKEY        CHAIN(N)  STSTUR                             33
     C   33              MOVE      *BLANKS       STSTAT
      * FRAKTBREV / FBR. KONTROLLERT.
     C                   MOVE      *ZEROS        WSANFB
     C                   MOVE      *ZEROS        WSANFK
     C     TURKEY        SETLL     STSOPD
     C     TURKEY        READE(N)  STSOPD                                 33
     C     *IN33         DOWEQ     '0'                                          1<-B
     C                   ADD       SOANFB        WSANFB
     C                   ADD       SOANFK        WSANFK
     C     TURKEY        READE(N)  STSOPD                                 33
     C  N33              ENDDO                                                  1<-E
      * KOLLIID / KLID KONTROLLERT.
     C                   MOVE      *ZEROS        WSANKI
     C                   MOVE      *ZEROS        WSANKK
     C     TURKEY        SETLL     STSFBR
     C     TURKEY        READE(N)  STSFBR                                 33
     C     *IN33         DOWEQ     '0'                                          1<-B
     C                   ADD       SFANKI        WSANKI
     C                   ADD       SFANKK        WSANKK
     C     TURKEY        READE(N)  STSFBR                                 33
     C  N33              ENDDO                                                  1<-E
      *
     C     Feilkode      IFeq      ' '                                              5<-B
     C     STANOK        andNE     *ZEROS                                           5<-B
     C     STANOK        andGE     STANOP                                           5<-B
     C                   eval      feil = 'FERDIG SKUTT, Fullfør, Parker ' +
     C                                    'eller skyt flere.'
     C                   eval      feilKode = 'X'
     C                   END                                                    1<-E
      *
     C                   ENDSR
      *
      *=====================================================================******
      *  Different User than last invocation
      *=====================================================================******
     C     Init          begsr
     C                   open      BRIDF
     C     User          CHAIN     BRIDF                              30
      *
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C* (bedre ressursmessig). MODUL må da være med comp. av dette pgm!!
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
      *
     C                   open      KUFAST
     C                   open      TRACKK
     C                   open      DISP
N01  C                   open      DISPL01
     C                   open      HEADF
     C                   open      HEAD8
     C                   open      HEAD39
     C                   open      DOKUF
     C                   open      DOKUFM
     C                   open      DOKUFM1
     C                   open      tellge
     C                   open      STSTUR
     C                   open      STSOPD
     C                   open      STSFBR
     C                   open      STSCLI1
      *
      * definer KEYS m.m.
      *
     C     *LIKE         DEFINE    STTYPE        YTYPE
      *
     C     KUFASTK       KLIST
     C                   KFLD                    KFTYP
     C                   KFLD                    YTYPE
     C     TURKEY        KLIST
     C                   KFLD                    YTYPE
     C                   KFLD                    WSPRO
     C     OPDKEY        KLIST
     C                   KFLD                    YTYPE
     C                   KFLD                    WSPRO
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C     FBRKEY        KLIST
     C                   KFLD                    YTYPE
     C                   KFLD                    WSPRO
     C                   KFLD                    DFAVD
     C                   KFLD                    DFOPD
     C                   KFLD                    DFFBNR
     C                   move      '00'          FMAI00
     C     OPDK1         KLIST
     C                   KFLD                    YTYPE
     C                   KFLD                    WSPRO
     C                   KFLD                    SFAVD
     C                   KFLD                    SFOPD
     C     FBRK1         KLIST
     C                   KFLD                    YTYPE
     C                   KFLD                    WSPRO
     C                   KFLD                    SCAVD
     C                   KFLD                    SCOPD
     C                   KFLD                    SCFBNR
     C     CLIK1         KLIST
     C                   KFLD                    YTYPE
     C                   KFLD                    WSPRO
     C                   KFLD                    WSCLID
     C     DOKUFK        KLIST
     C                   KFLD                    DFRI
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C     DOKFMK        KLIST
     C                   KFLD                    DFRI
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C                   KFLD                    DFFBNR
     C     HEAKEY        KLIST
     C                   KFLD                    FMAVD
     C                   KFLD                    FMOPD
     C     HEAD8K        KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEPRO
     C     DISPK         KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
n01  c     Disp1Key      Klist
n01  c                   KFLD                    xxSTA1            1
n01  c                   KFLD                    xxTUR             8 0
      *
     C                   MOVE      'F'           DFRI
     C                   EVAL      KFTYP = 'FELTKONTR'
      * HENT ALLE AVDELINGER OG FELTER
     C     KFTYP         SETLL     KUFAST
     C     KFTYP         READE     KUFAST                                 33
     C                   DOW       *IN33 = *OFF
     C                   IF        KFKOD <> ' DEFN'
     C     1             DO        10            XN                3 0
     C                   IF        XFKAVD(XN) <> *BLANKS
     C                   MOVEL     XFKAVD(XN)    XAVD              4 0
     C     1             DO        100           XN2               3 0
     C                   SELECT
     C                   WHEN      AVDFELT1(XN2) = 0
     C                   MOVE      XAVD          AVDFELT1(XN2)
     C                   MOVEL     KFTXT         AVDFELT2(XN2)
     C                   LEAVE
     C                   WHEN      AVDFELT1(XN2) = XAVD
     C                   LEAVE
     C                   ENDSL
     C                   ENDDO
     C                   END
     C                   ENDDO
     C                   END
     C     KFTYP         READE     KUFAST                                 33
     C                   ENDDO
     C     KUFASTK       CHAIN     KUFAST                             33
      *
     C                   endsr
      *
      *=====================================================================******
      * Set html output skeleton member before its read into memory
      *=====================================================================******
     C     SetSkl        begsr
      *------------------
      * Set html output skeleton member
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC'
     C                   eval      Mbr = 'STSF02'
     C     SEMI          IFeq      'Y'
     C                   eval      Mbr = 'STSF02T'
     C                   END
      *------------------
     C                   endsr
      ***********************************************
     C     TTSR          BEGSR
      ***********************************************
      *
     C                   CLEAR                   TRACKKR
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   EVAL      TKAVD  = 0
     C                   EVAL      TKOPD  = 0
     C                   EVAL      TKFBNR = 0
     C                   MOVE      WDT           TKDATE
     C                   MOVE      WTM           TKTIME
     C                   MOVE      *BLANKS       TKCLID
     C                   MOVEL     STPRO         TKCLID
     C                   CAT       STTYPE:1      TKCLID
     C                   MOVE      'FTS'         TKKODE
     C                   MOVE      USER          TKUSER
     C                   EVAL      TKTEXT = 'FELT KONTROLL STARTET.'
     C                   WRITE     TRACKKR
      *
     C                   ENDSR
