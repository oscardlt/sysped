      *
      *****************************************************************
      *
      * Danner fakturalinjer i FAKT basert på fordelingsfraktbrev
      * Kalles fra SYFI10X når gebyrtype = "TGF"
      *          + SYAM10X
      *
      * Parameterfilen (CSTYF) er en TGI spesialitet
      *
      *****************************************************************
     H DATEDIT(*YMD)
     FFIRM      IF   E           K DISK
     FHEADF     IF   E           K DISK
     FDOKUF     IF   E           K DISK
     FCSTYL01   IF   E           K DISK
     FFRSTTG    IF   E           K DISK
     FFAKT      O    E           K DISK
     D NUM             C                   CONST('0123456789')
      *//////////////
      *  Main logic /
      *//////////////
     C                   EXSR      SRA
     C                   EXSR      SRB
     C                   EXSR      SRC
      *////////////////////////////////////////////////////////////
      *  Hovedrutine                                              /
      *////////////////////////////////////////////////////////////
     C     SRB           BEGSR
      *
      * Henter opplysninger fra HEADF. Bl.a. selger og mottager
      *
     C     HEADKI        CHAIN     HEADF                              90
     C     *IN90         IFEQ      *OFF
      *
     C     HEKNKF        IFNE      *ZEROS
     C                   MOVE      HEKNKF        UKUNDE
     C                   ELSE
     C                   MOVE      HEKNK         UKUNDE
     C                   END
      *
      * Sjekker om oppdragets selger finnes i parameterfilen og om
      * innlandsberegning skal utføres.
      *
     C     HEKNS         CHAIN     CSTYL01                            90
     C     *IN90         IFEQ      *OFF
     C     CSINNL        ANDEQ     'J'
      *
      * Leser alle fraktbrev registrert på oppdraget.
      *
     C     DOKEKI        SETLL     DOKUF
     C     DOKEKI        READE     DOKUF                                  90
     C     *IN90         DOWEQ     *OFF
     C                   EXSR      GENFAK
     C     DOKEKI        READE     DOKUF                                  90
     C                   ENDDO                                                  <*IN90
      *
     C                   ENDIF                                                  <*IN90
     C                   ENDIF                                                  <*IN90
      *
     C     SRB99         TAG
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Danner opplysninger i FAKT og skriver posten             /
      *////////////////////////////////////////////////////////////
     C     GENFAK        BEGSR
      *
      * Kaller program for beregning av fraktbrevsbeløpet.
      *
     C     *LOVAL        SETLL     FRSTTG
     C                   READ      FRSTTG                                 33
     C  N33TGEK          COMP      *BLANKS                                33
     C                   IF        *IN33
     C                   MOVE      '0101'        FRAII             4 0
     C                   MOVEL     DFPOUL        TILII             4 0
     C                   Z-ADD     0             IILM              7 3
     C                   Z-ADD     0             IIM3              7 3
     C                   Z-ADD     DFNT          IIANT
     C                   Z-ADD     0             NULLTR
     C                   MOVE      'J'           UOVERS                          *IKKE STED!
     C                   CALL      'SYFB03'
     C                   PARM                    FRAII
     C                   PARM                    TILII
     C                   PARM                    DFVKT
     C                   PARM                    IILM
     C                   PARM                    IIM3
     C                   PARM                    NULLTR            8 0
     C                   PARM                    INLFRA            9 0          <Returbeløp
     C                   PARM                    UVKT2             9 0
     C                   PARM                    IFLAGG            1 0
     C                   PARM                    UOVERS            1
     C                   PARM                    UKUNDE            8 0
     C                   PARM                    IIANT             7 0
     C                   PARM                    UTGSON            3
     C                   PARM                    UBL2              9 0
     C                   PARM                    URAB              3 1
     C                   PARM                    DFAVD
     C                   PARM                    DFOPD
     C                   PARM                    DFFBNR
      *
     C                   ELSE
     C                   EXSR      TGIHTTP
     C                   END
      *
      * Legger verdier i FAKT og skriver posten.
      *
     C                   MOVEL     *BLANKS       FA01
     C                   CALL      'SYGE06'
     C                   PARM                    ZAAVD
     C                   PARM                    ZAOPD
     C                   PARM                    ZAULI             5 0
     C                   Z-ADD     ZAULI         FALI
     C                   Z-ADD     ZAAVD         FAAVD
     C                   Z-ADD     ZAOPD         FAOPD
     C     CSBET         IFEQ      'A'
     C                   MOVE      'K'           FASK
     C                   Z-ADD     0             FAKUNR
     C                   ELSE
     C                   MOVE      'X'           FASK
     C                   MOVEL     DFKNM         WWKUND            8
     C     NUM           CHECK     WWKUND        TST               1 0
     C     TST           IFEQ      0
     C                   MOVEL     WWKUND        FAKUNR
     C                   ELSE
     C                   Z-ADD     0             FAKUNR
     C                   ENDIF
     C                   ENDIF
     C                   MOVE      ZAVK          FAVK
     C                   MOVEL     DFNAVM        FAVT                           <FRITEKST
     C                   MOVEL     'NOK'         FAVAL
     C                   Z-ADD     INLFRA        FABELV
     C                   Z-ADD     INLFRA        FABELN
     C* 0 DECIMAL
     C     FIDECI        IFEQ      0
     C                   Z-ADD(H)  FABELN        FABEL0           11 0
     C                   Z-ADD     FABEL0        FABELN
     C                   ENDIF
     C* 1 DECIMAL
     C     FIDECI        IFEQ      1
     C                   Z-ADD(H)  FABELN        FABEL1           12 1
     C                   Z-ADD     FABEL1        FABELN
     C                   END
     C                   MOVEL     ZAKDM         FAKDM
     C                   MOVEL     'A'           FAKDA
     C                   MOVE      ZAKDUL        FAKDUL
     C                   MOVE      *BLANKS       FA01B
     C                   Z-ADD     0             FABELU
     C                   Z-ADD     0             FALEVN
     C                   Z-ADD     0             FAAVDR
     C                   Z-ADD     0             FAJOUR
     C                   MOVE      *BLANK        FAKDUK
     C                   Z-ADD     0             FAAGRN
     C                   Z-ADD     DFFBNR        FAFRBN
      *
     C                   WRITE     FAKTR
      *
     C                   ENDSR
      ***********************************************
     C     TGIHTTP       BEGSR
      ***********************************************
     C                   MOVE      '0101'        U2PNF             4
     C                   MOVE      DFPOUL        U2PNT             4
     C                   MOVE      DFNT          U2NT              7
     C                   MOVE      UVKT2         U2VKT             9
     C                   MOVE      *ZEROS        U2DM3             7
     C                   MOVE      *ZEROS        U2KNT             8
     C                   MOVE      *ZEROS        U2RAB             3
     C                   MOVE      '0'           U2FLAGG           1
     C                   MOVE      DFAVD         U2AVD             4
     C                   MOVE      DFOPD         U2OPD             7
     C     HEKNKF        IFNE      *ZEROS
     C                   MOVE      HEKNKF        U2KN              8
     C                   ELSE
     C                   MOVE      HEKNK         U2KN
     C                   END
     C                   CALL      'SYFB05C'
     C                   PARM                    U2PNF
     C                   PARM                    U2PNT
     C                   PARM                    U2NT
     C                   PARM                    U2VKT
     C                   PARM                    U2DM3
     C                   PARM                    U2KNT
     C                   PARM                    U2BRTBL           9
     C                   PARM                    U2NETBL           9
     C                   PARM                    U2RAB
     C                   PARM                    U2FLAGG
     C                   PARM                    U2AVD
     C                   PARM                    U2OPD
     C                   PARM                    U2KN
     C                   IF        U2FLAGG = '0'
     C                   MOVE      U2BRTBL       U2BRTBL2          9 0
     C                   Z-ADD     U2BRTBL2      FABELN
     C                   Z-ADD     U2BRTBL2      FABELV
     C                   END
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      *  Init                                                     /
      *////////////////////////////////////////////////////////////
     C     SRA           BEGSR
      * Parameterliste
     C     *ENTRY        PLIST
     C                   PARM                    ZPAVD
     C                   PARM                    ZPOPD
     C                   PARM                    ZPKDM
     C                   PARM                    ZPKDUL
     C                   PARM                    ZPVK
      *__________________
      * Define Workfields
      *""""""""""""""""""
     C     *LIKE         DEFINE    FAAVD         ZPAVD
     C     *LIKE         DEFINE    FAOPD         ZPOPD
     C     *LIKE         DEFINE    FAKDM         ZPKDM
     C     *LIKE         DEFINE    FAKDUL        ZPKDUL
     C     *LIKE         DEFINE    FAVK          ZPVK
     C     *LIKE         DEFINE    FAAVD         ZAAVD
     C     *LIKE         DEFINE    FAOPD         ZAOPD
     C     *LIKE         DEFINE    FAKDM         ZAKDM
     C     *LIKE         DEFINE    FAKDUL        ZAKDUL
     C     *LIKE         DEFINE    FAVK          ZAVK
     C     *LIKE         DEFINE    DFRI          ZARI
     C                   Z-ADD     ZPAVD         ZAAVD
     C                   Z-ADD     ZPOPD         ZAOPD
     C                   MOVE      ZPKDM         ZAKDM
     C                   MOVE      ZPKDUL        ZAKDUL
     C                   MOVE      ZPVK          ZAVK
     C                   MOVEL     'F'           ZARI
      * Keylist
     C     HEADKI        KLIST
     C                   KFLD                    ZAAVD
     C                   KFLD                    ZAOPD
     C     DOKEKI        KLIST
     C                   KFLD                    ZARI
     C                   KFLD                    ZAAVD
     C                   KFLD                    ZAOPD
      *
     C                   READ      FIRM                                   90
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Avslutt                                                  /
      *////////////////////////////////////////////////////////////
     C     SRC           BEGSR
     C                   MOVE      *ON           *INLR
      *
     C                   ENDSR
