     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      REF(*LIBL/HEADF)
     A                                      PRINT
     A                                      HLPDOC('STD' 'HELPNU' 'HLP400')
     A                                      HELP
     A                                      ALTHELP
     A          R BILD1                     SFL
     A            HEKNKF    R        H
     A            ZOP            1A  I  9  3VALUES(' ' '4')
     A            HEAVD     R        O  9  6EDTCDE(4)
     A            HEOPD     R        O  9 11EDTCDE(4)
     A            HEPOS1    R        O  9 20
     A          R BILD2                     SFLCTL(BILD1)
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0012)
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                      CF03(03 'Gå ut')
     A                                      CF05(05 'Frisk opp')
     A                                      CF12(12 'Forrige')
     A                                      CF16(16 'Fullfør')
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A                                  1 32MSGCON(040 DIV1620 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            ZRECNO         4S 0O  2 74SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  3  2MSGCON(010 DIV3432 *LIBL/SYMSGF)
     A            WAVDUU         4Y 0O  3 13EDTCDE(4)
     A                                  3 20MSGCON(010 DIV0659 *LIBL/SYMSGF)
     A            WTUR           8Y 0O  3 32EDTCDE(4)
     A                                  5  2MSGCON(040 DIV2055 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  6  4MSGCON(021 DIV3764 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  8  2MSGCON(003 SSI0280 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  8  7MSGCON(003 DIV3230 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  8 12MSGCON(006 DIV1920 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  8 20'Pos.'
     A                                      COLOR(WHT)
     A          R BILDØ
     A                                      OVERLAY
     A                                 23  2MSGCON(060 DIV3765 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A            ZMSGTX        79A  O 24  2MSGID(&ZMSGNO SYMSGF)
     A N30                                  COLOR(WHT)
     A  30                                  COLOR(RED)
     A            ZMSGNO         7A  P
