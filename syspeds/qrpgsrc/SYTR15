      *
      ***********************************************************
      *** PROGRAM FOR BOOKING AV FERJER IKKE KNYTTET TIL TUR  ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01 02 05-26                      ***
      *** LEDIGE INDIKATORER 27-29 35-92                      ***
      ***********************************************************
     FTURER     UF A E           K DISK
     FHEADF     UF A E           K DISK
     FDISP      UF A E           K DISK
     FTRAN      IF   E           K DISK
     FUNITL01   IF   E           K DISK
     FBILK      IF   E           K DISK
     FFIRM      IF   E           K DISK
     FKODTA     IF   E           K DISK
     FKODTÅ     IF   E           K DISK
     FSYTR15FM  CF   E             WORKSTN
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSID                 197    206
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D                 DS
     D  WSPER                  1      6  0
     D  WSPERC                 1      2  0
     D  WSPERÅ                 3      4  0
     D  WSPERM                 5      6  0
     D                 DS
     D  MINPER                 1      6  0
     D  KOÅCC                  1      2  0
     D  KOÅÅR                  3      4  0
     D  KOÅMND                 5      6  0
     C*
     C                   EXSR      INIT
     C*
     C     START         TAG
     C                   EXFMT     BILDE1
     C   04              EXSR      SØK
     C   04              GOTO      START
     C*
     C   03              GOTO      SLUTT
     C*
     C                   EXSR      TESTB1
     C   30              GOTO      START
     C*
     C* LEGG UT DUMMY TURRECORD (AVD 0 / TRANSPORTØRNR)
     C     TURERK        CHAIN     TURER                              33
     C                   EXSR      MOVWST
     C  N33              UPDATE    TURERR
     C   33              WRITE     TURERR
     C* LEGG UT DUMMY HEADRECORD (AVD 0 / TRANSPORTØRNR)
     C*(MÅ FINNES FOR Å KUNNE "FØRE BORT" FERJE MOT "TUR" I KOST.MODUL)
     C                   MOVE      WSTNR         YYOPD
     C     HEADFK        CHAIN     HEADF                              33
     C  N33              UPDATE    HEADFR
     C   33              EXSR      MOVWSH
     C   33              WRITE     HEADFR
     C     HEADFK        CHAIN     DISP                               33
     C  N33              UPDATE    DISPR
     C   33              EXSR      MOVWSD
     C   33              WRITE     DISPR
     C*
     C* GÅ I MODUL FOR FERGEOVERSIKT / PLUKK
     C                   CALL      'SYTR17A'
     C                   PARM                    TUAVD
     C                   PARM                    WSTNR
     C                   PARM                    TURUND
     C                   PARM                    UKOST
     C*
     C                   GOTO      START
     C*
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
     C*
     C***********************************************
     C     SØK           BEGSR
     C***********************************************
     C*** FINN CURSOR POSISJON
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C                   Z-ADD     WSSUBN        XRECNO            4 0
     C* TRANSPORTØRNR
     C     LINNBR        IFEQ      4
     C                   CALL      'SYTR04A'
     C                   PARM                    WSTNR
     C                   Z-ADD     7             LINNBR
     C                   Z-ADD     29            POSNBR
     C                   GOTO      SLSØK
     C                   END
     C* BILNR
     C     LINNBR        IFEQ      7
     C                   CALL      'SYTR19'
     C                   PARM                    WSBILN
     C                   Z-ADD     10            LINNBR
     C                   Z-ADD     29            POSNBR
     C                   GOTO      SLSØK
     C                   END
     C*
     C*
     C     SLSØK         ENDSR
     C***********************************************
     C     TESTB1        BEGSR
     C***********************************************
     C                   MOVEA     '0'           *IN(30)
     C                   MOVEA     '0000'        *IN(70)
     C     WSBILN        IFNE      *BLANKS
     C     WSBILN        CHAIN     UNITL01                            33
     C* HENTE BILKODE  FRA UNIT ?
     C  N33WSBILK        IFEQ      *BLANKS
     C                   MOVE      UNTRME        WSBILK
     C                   END
     C* HENTE TRANSPORTØR FRA UNIT ?
     C  N33WSTNR         IFEQ      *ZEROS
     C                   MOVE      UNTRAN        WSTNR
     C                   END
     C                   END
     C*
     C* TEST TRANSPORTØR
     C     TRANK         CHAIN     TRAN                               70
     C   70              DO
     C                   MOVE      'SPT0105'     MSGNR
     C                   SETON                                        30
     C                   Z-ADD     4             LINNBR
     C                   Z-ADD     29            POSNBR
     C                   GOTO      ENDB1
     C                   END
     C* BILNUMMER
     C     WSBILN        COMP      *ZEROS                                 72
     C   72              DO
     C                   MOVE      'SPB0009'     MSGNR
     C                   SETON                                        30
     C                   Z-ADD     7             LINNBR
     C                   Z-ADD     29            POSNBR
     C                   GOTO      ENDB1
     C                   END
     C* BILKODE
     C     WSBILK        CHAIN     BILK                               73
     C   73              DO
     C                   MOVE      'DIV4014'     MSGNR
     C                   SETON                                        30
     C                   Z-ADD     8             LINNBR
     C                   Z-ADD     29            POSNBR
     C                   GOTO      ENDB1
     C                   END
     C* PERIODISERING  (MÅ VÆRE STØRRE EN MINSTE ÅPNE)
     C     WSPERM        IFEQ      0
     C                   MOVE      *ZEROS        WSPER
     C                   ELSE
     C     WSPERÅ        IFGT      50
     C                   MOVE      19            WSPERC
     C                   ELSE
     C                   MOVE      20            WSPERC
     C                   END
     C                   END
     C     WSPER         COMP      MINPE2                               71
     C   71              DO
     C                   MOVE      'DIV2295'     MSGNR
     C                   SETON                                        30
     C                   Z-ADD     10            LINNBR
     C                   Z-ADD     29            POSNBR
     C                   GOTO      ENDB1
     C                   END
     C*
     C*
     C     ENDB1         ENDSR
     C*
     C***********************************************
     C     MOVWST        BEGSR
     C***********************************************
     C   33              MOVE      WSTNR         TUPRO
     C                   MOVE      WSPERM        TURMND
     C                   MOVE      WSPERÅ        TURAAR
     C                   MOVE      WSPERC        TURACC
     C                   MOVE      WSBILN        TUBILN
     C                   MOVE      WSBILK        TUBILK
     C                   MOVE      *ZEROS        TURUND
     C* BLANK SKJERMFELT FOR NESTE  VISNING - IKKE TRANSPORTØR/PERIODE
     C                   MOVE      *BLANKS       WSBILN
     C                   MOVE      *BLANKS       WSBILK
     C                   Z-ADD     7             LINNBR
     C                   Z-ADD     29            POSNBR
     C                   ENDSR
     C***********************************************
     C     MOVWSH        BEGSR
     C***********************************************
     C* LAGER DUMMY OPPDRAG KNYTTET TIL DUMMY TUR SLIK AT EN VED
     C* KOSTNADSFØRING AV DISSE DIVERSEFERJENE (SOM IKKE INNGÅR I
     C* ORDINÆR TRAFIKK) KAN "FØRE BORT" KOSTNADEN.
     C* (INNTEKTEN MÅ/BØR "FØRES BORT MOT SAMME PROSJEKT/OPPDRAG)
     C* SOM KUNDE PÅ OPPDRAGET VELGES FIRMAETS EGET KUNDENR
     C* I.H.T AVDELINGSREGISTER
     C                   MOVE      'O'           HEST
     C                   MOVE      *ZEROS        HEAVD
     C                   MOVE      WSTNR         HEOPD
     C                   MOVE      WSTNR         HEPRO
     C                   Z-ADD     1             HEVKT
     C                   Z-ADD     1             HEFBV
     C                   Z-ADD     KOAKNR        HEKNKF
     C                   ENDSR
     C*
     C*
     C***********************************************
     C     MOVWSD        BEGSR
     C***********************************************
     C* KNYTTER DUMMYOPPDRAG TIL TURREGISTERET VIA DISP FILEN
     C* SLIK AT VED HENTING AV BILKODE M.M. VED OVERFØRING TIL
     C* REGNSKAP FÅR MED AVD 0000 I TURKEY
     C* (TURNR=TRANSPORTØRNR KAN ELLERS FORVEKSLES MED EN AKTIV TUR
     C*  ..KAN FÅ FEIL BILKODE I OVERFØRING AV KOSTNADER TIL REGNSK.
     C* - FULLSTENDIG TURKEY (AVD 0000) VIA HEADF/DISP SIKRER MOT DETTE)
     C* SETTER DISPONERINGSSTATUS TIL 'D' FOR Å UNNGÅ AT TRAFIKKSYSTEMET
     C* SKAL FÅ PROBLEM MED VISNING ALLE UDISPONERTE OPPDRAG
     C                   MOVE      'D'           DISTA1
     C                   MOVE      *ZEROS        DIAVD
     C                   MOVE      HEOPD         DIOPD
     C                   MOVE      WSTNR         DITUR
     C                   MOVE      *ZEROS        DIRUND
     C                   ENDSR
     C*
     C*
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     *ENTRY        PLIST
     C                   PARM                    UKOST             1
     C*
     C     TURERK        KLIST
     C                   KFLD                    TUAVD
     C                   KFLD                    WSTNR
     C     HEADFK        KLIST
     C                   KFLD                    TUAVD
     C                   KFLD                    YYOPD
     C     TRANK         KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    WSTNR
     C     KODÅKE        KLIST
     C                   KFLD                    KOÅUNI
     C                   KFLD                    KOÅCC
     C                   KFLD                    KOÅÅR
     C                   KFLD                    KOÅMND
     C     KODTAK        KLIST
     C                   KFLD                    YAUNI
     C                   KFLD                    YAVD
     C                   MOVE      'A'           YAUNI             1
     C                   MOVE      *ZEROS        YAVD              4 0
     C*
     C                   MOVE      *ZEROS        YYOPD             7 0
     C                   MOVE      *ZEROS        TUAVD
     C                   MOVE      *ZEROS        TURUND
     C                   MOVE      *ZEROS        WSPER
     C                   MOVE      *ZEROS        MINPER
     C*
     C*
     C* HENTER MINSTE ÅPNE PERIODE
     C                   MOVE      'Å'           KOÅUNI
     C                   MOVE      *ZEROS        KOÅMND
     C                   MOVE      *ZEROS        KOÅÅR
     C                   MOVE      *ZEROS        KOÅCC
     C     KODÅKE        SETLL     KODTÅ
     C     KOÅUNI        READE     KODTÅ                                  33
     C     *IN33         IFEQ      '1'
     C     UYEAR         IFGT      50
     C     UYEAR         ADD       1900          MINPE2            6 0
     C                   ELSE
     C     UYEAR         ADD       2000          MINPE2
     C                   END
     C                   MULT      100           MINPE2
     C                   MOVE      UMONTH        MINPE2
     C                   ELSE
     C                   MOVE      MINPER        MINPE2
     C                   END
     C*
     C                   READ      FIRM                                   33
     C     KODTAK        SETLL     KODTA
     C                   READ      KODTA                                  33
     C*
     C*
     C                   Z-ADD     4             LINNBR
     C                   Z-ADD     29            POSNBR
     C*
     C                   ENDSR
