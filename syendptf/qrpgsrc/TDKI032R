      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/TDKI032R MODULE(*LIBL/TDKI032R *LIBL/INCGI)
CRTPGM*        ACTGRP(*NEW) AUT(*USE)
      *
      *********************************************************************
      /copy SYSPEDS/qrpgsrcpy,hspecs
      /copy SYSPEDS/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FDKIF      UF A E           K DISK    USROPN
     F                                     PREFIX(X)
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy SYSPEDS/qrpgsrcpy,prototypeb
      /copy SYSPEDS/qrpgsrcpy,usec
      /copy SYSPEDS/qrpgsrcpy,variables3
      *
      * Varible for
     D WSUSER          S             10
     D WMODE           S              2
     D JSONstring      S          32000
     D Error           S            150
     D WA              S             15
     D DKIF_SYAV       S              4S 0
     D DKIF_SYOP       S              7S 0
     D DKIF_FATY       S              4
     D DKIF_FATX       S             17
     D DKIF_VAKD       S              3
     D DKIF_VAKU       S              7S 4
     D DKIF_FABL       S             11S 3

      *get input-string
      /copy syspeds/qrpgsrcpy,prolog3

      * Parse input
     C                   EXSR      Parse
      * Open files etc
     C                   EXSR      INIT
      *
     c                   callp     gethtmlifs(
     C                             '/syspeddev/html/JSON.html':
     C                             '/CGITAG/')
     C                   callp     wrtsection('top')
      * Teste input og utfør
     C  N50              EXSR      TEST

      * ............................................................................................
      * skriv JSON-Object start..(alltid '{' + x ant param. m/komma mellom (IKKE komma etter siste))
      * ............................................................................................
      *
      /free
        JSONstring='{'
        +'¬user¬ : ¬'+%trim(WSUSER)+'¬, '
        +'¬avd¬ : ¬'+%trim(%editc(DKIF_SYAV:'Z'))+'¬, '
        +'¬opd¬ : ¬'+%trim(%editc(DKIF_SYOP:'Z'))+'¬, '
        +'¬fak¬ : ¬'+%trim(DKIF_FATX)+'¬, '
        +'¬mode¬ : ¬'+%trim(WMODE)+'¬, '
        +'¬errMsg¬ : ¬'+%trim(Error)+'¬, ';
      /end-free
      * JSONfix escaper " i tekst,  for deretter å bytte ¬->"
     c                   call      'JSONFIX'
     c                   parm                    JSONstring
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      *
      * ............................................................................................
      * Skriv JSON-LISTE Start (en liste er EN parameter(fra [ til   )
      * ............................................................................................
     c                   eval      JSONstring ='"invoiceUpdate" : ['
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      * ............................................................................................
      * Skriv JSON-Liste/Array  Avslutning
      * ............................................................................................
     c                   eval      JSONstring =']'
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      * ............................................................................................
      * Skriv JSON-string Avsluttning
      * ............................................................................................
     c                   eval      JSONstring ='}'
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      *
      * Quit
     C                   exsr      Exit


      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Lukk fil
     C                   CLOSE     BRIDF
     C  N50              CLOSE     DKIF

      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   MOVE      *ON           *INLR
     C                   return
     C                   endsr
      *
      *********************************************************
     C     Parse         Begsr
      *********************************************************
      * Get input
     C                   EVAL      WSUSER  = zhbgetvar('USER')
     C                   EVAL      WMODE  = zhbgetvar('MODE')
     C                   EVAL      WA         = zhbgetvar('AVD')
     C                   EVAL      DKIF_SYAV  = c2n2(WA)
     C                   EVAL      WA         = zhbgetvar('OPD')
     C                   EVAL      DKIF_SYOP  = c2n2(WA)
     C                   EVAL      DKIF_FATX  = zhbgetvar('FAK')
     C                   EVAL      DKIF_FATY  = zhbgetvar('DKIF_FATY')
     C                   EVAL      DKIF_VAKD  = zhbgetvar('DKIF_VAKD')
     C                   EVAL      WA      = zhbgetvar('DKIF_VAKU')
N01  C                   EVAL      DKIF_VAKU  = c2n2(WA)
     C                   EVAL      WA      = zhbgetvar('DKIF_FABL')
N01  C                   EVAL      DKIF_FABL  = c2n2(WA)
     C                   ENDSR
      *********************************************************
     C     INIT          Begsr
      *********************************************************
     C                   OPEN      BRIDF
      * Test innlogging
     C     WSUSER        CHAIN     BRIDF                              50
     C  N50BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C                   call      BILIBL
     C                   end
      *
     C     DKIFKEY       KLIST
     C                   KFLD                    DKIF_SYAV
     C                   KFLD                    DKIF_SYOP
     C                   KFLD                    DKIF_FATX
      *
     C   50              eval      Error = 'Invalid userID'
     C  N50              OPEN      DKIF

     c                   endsr
      *
      *______________________________________________________
      * TEST                                             TEST
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     TEST          BEGSR
     C     WMODE         IFNE      'A'
     C     DKIFKEY       CHAIN     DKIF                               55
     C                   ENDIF
      * MODE A=Add  D=Delete  U=Update
     C                   SELECT
     C     WMODE         WHENEQ    'A'
     C                   eval      Error = *blanks
     C                   EXSR      SRA
     C     WMODE         WHENEQ    'D'
     C                   IF        *IN55=*ON
     C                   eval      Error = 'Invoice not found'
     C                   ELSE
     C                   eval      Error = *blanks
     C                   EXSR      SRD
     C                   ENDIF
     C     WMODE         WHENEQ    'U'
     C                   IF        *IN55=*ON
     C                   eval      Error = 'Invoice not found'
     C                   ELSE
     C                   eval      Error = *blanks
     C                   EXSR      SRU
     C                   ENDIF
     C                   OTHER
     C                   eval      Error = 'Wrong mode'
     C                   ENDSL
     C                   ENDSR
      *______________________________________________________
      * Add                                               SRA
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     SRA           BEGSR
     C                   CLEAR                   DKIFR
     C                   EVAL      XDKIF_SYAV=DKIF_SYAV
     C                   EVAL      XDKIF_SYOP=DKIF_SYOP
     C                   EVAL      XDKIF_FATX=DKIF_FATX
     C                   WRITE     DKIFR
     C                   ENDSR
      *______________________________________________________
      * Delete                                            SRD
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     SRD           BEGSR
     C                   DELETE    DKIFR
     C                   ENDSR
      *______________________________________________________
      * Update                                            SRU
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     SRU           BEGSR
     C                   EVAL      XDKIF_FATY=DKIF_FATY
     C                   EVAL      XDKIF_VAKD=DKIF_VAKD
     C                   EVAL      XDKIF_VAKU=DKIF_VAKU
     C                   EVAL      XDKIF_FABL=DKIF_FABL
     C                   UPDATE    DKIFR
     C                   ENDSR
