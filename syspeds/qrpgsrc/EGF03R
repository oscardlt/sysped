     FINFIL     IF   F   80        DISK
     FEDIC      UF   E             DISK
     FEDIS      O  A E             DISK
     D                 DS
     D  WMBR                   1     14
     D  WMBRA                  1      1
     D  WMBRB                  2     10  0
     IINFIL     AA  09
     I                                  1   80  INDATA
      *//////////////
      *  Hodelogikk /
      *//////////////
     C                   EXSR      SRA
     C                   EXSR      SRB
     C                   MOVE      '1'           *INLR
      *////////////////////////////////////////////////////////////
      *  Initiering                                           SRA /
      *////////////////////////////////////////////////////////////
     C     SRA           BEGSR
      *___________________
      * Definiere Workfelt
      *"""""""""""""""""""
     C     *LIKE         DEFINE    *IN51         WIN51
     C     *LIKE         DEFINE    SSR           WSR
     C     *LIKE         DEFINE    SST           WST
      *__________________
      * Definiere Nøkkler
      *""""""""""""""""""
      *_________________
      * Input parametrer
      *"""""""""""""""""
     C     *ENTRY        PLIST
     C                   PARM      *IN51         WIN51
     C                   PARM      SMBR          SMBR
     C                   PARM      SSN           SSN
     C                   PARM      FROM          FROM             35
     C                   PARM      TO            TO               35
      *_____________
      * Init av felt
      *"""""""""""""
     C                   MOVE      '0'           *IN51
     C                   MOVE      'S'           WSR
     C                   MOVE      *BLANKS       WST
     C                   MOVE      'G'           WMBRA                          GENERELL
      *                                                    FLATFIL
     C* Hente dagens dato og klokkeslett.
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Bearbeide Formater                                   SRB /
      *////////////////////////////////////////////////////////////
     C     SRB           BEGSR
      *________________________________________
      * Noe å sende.....
      *""""""""""""""""""""""""""""""""""""""""
     C                   READ      INFIL                                  51
     C     *IN51         CABEQ     '1'           SRB9
      *________________________________________
      * Hente og oppdater neste sendningsnummer
      *""""""""""""""""""""""""""""""""""""""""
     C     1             CHAIN     EDIC                               20
     C                   ADD       1             CSN
     C                   UPDATE    EDICR
      *_________________
      * Logge sendningen
      *"""""""""""""""""
     C                   MOVEL     FROM          S0004
     C                   MOVEL     TO            S0010
     C                   Z-ADD     CSN           WMBRB
     C                   MOVE      WMBR          S0020
     C                   MOVE      ' '           S0035
     C                   Z-ADD     0             S0036
     C                   Z-ADD     CSN           SSN
     C                   MOVE      'S'           SSR
     C                   MOVE      ' '           SST
     C                   MOVEL     WDT           SDT
     C                   MOVEL     WTM           STM
     C                   MOVEL     '*LIBL   '    SLIB
     C                   MOVE      '  '          SLIB
     C                   MOVEL     'EDISE'       SFILE
     C                   MOVEL     WMBR          SMBR
     C                   WRITE     EDISR
     C     SRB9          ENDSR
