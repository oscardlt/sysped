      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/inqavd MODULE(*LIBL/inqavd *LIBL/INCGI)
CRTPGM*         ACTGRP(CGI) AUT(*USE)
      *
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FKODTA     IF   E           K DISK    USROPN
     FKODTALF   IF   E           K DISK    USROPN
     F                                     RENAME(KOAR:KOARx)
      *********************************************************************
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      /copy syspeds/qrpgsrcpy,variables3
      *
     D Lib             s             10
     D Fn              s             10
     D Mbr             s             10
OddEvD OddEven         s             10
      * Client input variables
     D WSVARNV         s             20
     D WSFORMS         s             20
     D WSREFR          s              1
     D REFRESH         s             50
     D SOKANR          s              4
     D SOKANRN         s              4  0
     D SOKNVN          s             33    varying
     D SOKNVN2         s             33
     D user            s             10
     D MaxV            s              5
     D MaxVis          s              5  0
      * Other variables
      *
     D Spaces          s             12a   inz('&nbsp;&nbsp;')
      *
      *
      *
      /copy syspeds/qrpgsrcpy,prolog3
      *
      * Get program start time for calculating execution time
     C                   time                    timedata1
      * Parse input / cvt to numeric
     C                   exsr      Parse
      * File open / keys
     C                   EXSR      INIT
      * Read output skeleton html member into memory
     C                   exsr      LoadHtml
      * Set section variables
     C                   exsr      SetTop
      * Send section top
     C                   callp     wrtsection('top')
      * Find/send lines
     C                   exsr      LINES
      * Send section bot
      *
     C                   callp     wrtsection('bot')
      * Quit
     C                   exsr      Exit
      *
     C                   eval      *inlr = *on
     C                   return
      *
      *
      *=====================================================================
      * Parse input / cvt to numeric
      *=====================================================================
     C     Parse         begsr
      * Parse variables from QUERY_STRING environment variable
     C                   eval      SOKANR  = zhbgetvar('SOKANR')
     C                   eval      SOKANRN = c2n2(SOKANR)
     C                   eval      SOKNVN  = zhbgetvar('SOKNVN')
     C                   eval      SOKNVN  = uppify(SOKNVN)
     C                   eval      WSVARNV = zhbgetvar('WSVARNV')
     C                   IF        WSVARNV = *BLANKS
     C                   EVAL      WSVARNV = 'WSAVD'
     C                   END
     C                   eval      WSFORMS = zhbgetvar('WSFORMS')
     C                   IF        WSFORMS = *BLANKS
     C                   EVAL      WSFORMS = 'forms[0]'
     C                   END
     C                   eval      REFRESH = *blanks
     C                   eval      WSREFR  = zhbgetvar('WSREFR')
     C                   IF        WSREFR = 'J' OR  WSREFR = 'j' OR
     C                             WSREFR = 'Y' OR WSREFR = 'y'
     C                   eval      REFRESH = 'self.opener.document.'
     C                             + %trim(WSFORMS)
     C                             + '.submit();'
     C                   END
     C                   eval      MaxV    = zhbgetvar('MaxV')
     C                   eval      MaxVis  = c2n2(MaxV)
     c     MaxVis        ifeq      *zeros
     c                   z-add     500           MaxVis
     c                   end
      * Userid.....
     C                   eval      user = zhbgetvar('user')
     C                   endsr
      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   CLOSE     BRIDF
     C                   CLOSE     KODTA
     C                   CLOSE     KODTALF
     C                   return
     C                   endsr
      *=====================================================================
      * Read output skeleton html member into memory
      *=====================================================================
     C     LoadHtml      begsr
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC'
     C                   eval      Mbr = 'INQAVD'
     C                   CAT       XSPRÅK:0      MBR
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
     C                   endsr
      *=====================================================================
      *  Set variable data for section /$top
      *=====================================================================
     C     SetTop        begsr
      *
     C* klargjøre HTMLvariable
OddEvC                   callp     updHTMLvar('ROWHEAD':RowHead:InitHTMLVars)
OddEvC                   callp     updHTMLvar('LogoImg':LogoImg)
      *
BODY C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updHTMLvar('USER':USER)
     C                   callp     updHTMLvar('wsvarnv':WSVARNV)
     C                   callp     updHTMLvar('wsforms':WSFORMS)
     C                   callp     updHTMLvar('wsrefr':WSREFR)
     C                   callp     updHTMLvar('refresh':REFRESH)
      *
     C                   endsr
      *
      *=====================================================================
      * Fine lins - send to browser
      *=====================================================================
     C     Lines         begsr
     c                   eval      SOKNVN2   = SOKNVN
     c                   Move      *zeros        AntVist           5 0
     C     SOKANRN       IFEQ      *ZEROS
     C     SOKNVN        ANDEQ     *BLANKS
     C                   GOTO      UTLES
     C                   END
      *
     C     SOKANRN       IFNE      *ZEROS
     C     ANRKEY        SETLL     KODTA
     C                   READ      KODTA                                  33
     C                   ELSE
     C     NVNKEY        SETLL     KODTALF
     C                   READ      KODTALF                                33
     C     SOKNVN        SCAN      KOANVN                                 34
     c     *IN34         CaBEQ     *OFF          UtLes
     C                   END
     C     *in33         DOWEQ     *OFF
     C                   exsr      exclude
     c     EXCL          IFEQ      'N'
     C* klargjøre HTMLvariable - SEND ROW
     C                   callp     updHTMLvar('KOAAVD':
     C                             %trim(%editc(KOAAVD:'3')))
     C                   callp     updHTMLvar('KOANVN':KOANVN)
OddEvc     OddEven       IFEQ      ODD
OddEvc                   eval      OddEven = EVEN
OddEvc                   else
OddEvc                   eval      OddEven = ODD
OddEvc                   end
OddEvC                   callp     updHTMLvar('ODDEVEN':OddEven)
OddEv *
     C                   callp     wrtsection('row')
     c                   Add       1             AntVist           5 0
     c     AntVist       cabge     MaxVis        UtLes
     C                   END
     C     SOKANRN       IFNE      *ZEROS
     C                   READ      KODTA                                  33
     C                   ELSE
     C                   READ      KODTALF                                33
     C     SOKNVN        SCAN      KOANVN                                 34
     c     *IN34         CaBEQ     *OFF          UtLes
     C                   END
     C                   end
      *
     C     UTLES         TAG
      *
      * Compute run time
     C                   time                    timedata2
     C     timedata2     subdur    timedata1     ms:*ms
     C                   eval      sec = ms / 1000000
     C                   callp     updhtmlvar('runtime':
     C                             %trim(%editw(sec:'     0 .   ')))
      *
     C                   endsr
      *=====================================================================******
      *  Eksluder post basert på...
      *=====================================================================******
     C     EXCLUDE       begsr
     C                   Move      'J'           EXCL              1
      * diverse tester - om ikke bestått hopp ut (med J i EXCL)
      * Posten skal med på liste
     C                   Move      'N'           EXCL              1
      *
     C     UtExcl        endsr
      *
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
     C                   OPEN      BRIDF
     C     USER          CHAIN     BRIDF                              50
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
      *
OddEv * hent Parametre som går igjen i mange prog:
OddEv *      Odd/Even/Rowhead-farger,Logobilde,Språk,Intern/Ekstern bruker,  mm
OddEvc                   call      'ESGETPAR'
OddEvc                   parm                    BIBRID
OddEvc                   parm                    BIFIRM
OddEvc                   parm                    BIKUNR
OddEvc                   parm                    BIKNG
OddEvc                   parm                    RowHead          10
OddEvc                   parm                    Odd              10
OddEvc                   parm                    Even             10
OddEvc                   parm                    LogoImg          50
OddEvc                   parm                    XSPRÅK            2
OddEvc                   parm                    XINTERN           1
OddEvc                   parm                    ParmDS1        1024
OddEvc                   parm                    ParmDS2        1024
OddEvc                   parm                    ParmDS3        1024
      *
     C                   OPEN      KODTA
     C                   OPEN      KODTALF
      * Key for CUNDL01
     C     ANRKEY        KLIST
     C                   KFLD                    KOAUNI
     C                   KFLD                    SOKANRN
     C                   move      'A'           KOAUNI
      * Key for CUNDL01
     C     NVNKEY        KLIST
     C                   KFLD                    SOKNVN2
      *
      *
     C                   endsr
