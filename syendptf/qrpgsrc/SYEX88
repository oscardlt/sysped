********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 SH  PTF10 søk på turnummer alene
10XXX * 02 SH  PTF10 hengernummer på turer
10XXX * 03 SH  PTF10 søk hengernummer
10XXX * 04 SH  PTF10 track and trace
10XXX * 05 SH  PTF10 print skilleark på alle dokumenter
11XXX * 06 SH  PTF11 *T i call skilleark
11XXX * 07 SH  PTF11 'C' SAMLECMR
11XXX * 08 SH  PTF11 Vise agentnavn på skjermbildet
      *%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
      * 09 JOV PTF12 E = EventGrabeber,send SMS-lenke  G = Godstiste/mail, send SMS-lenke
      * 10 SH  PTF12 søk transportør
12XXX * 11 YBC PTF12 SETGT ved les bakover
********************************************************************
      *
      *
      ****************************************************************
      *** PROGRAM SKAL OPPRETTE/PLANLEGGE TURER                    ***
      *** INDIKATORER 01 - 25 KUN CMD-TASTER / ROLL TASTER         ***
      *** LEDIGE INDIKATORER 01 02 04 07 09-11 13-24               ***
      *** LEDIGE INDIKATORER 34-90 95-97                           ***
      *** INDIKATOR 32 PÅ = KALLE FRA SYEX10                       ***
      ***           32 AV = KALLE FRA SYEX88CL                     ***
      ****************************************************************
      *
N08  FCUNDL01   IF   E           K DISK
N08  FFIRM      IF   E           K DISK
N05  FARKTXT    IF   E           K DISK
     FAVSLUTG2  IF   E           K DISK
     FAVSLUTL2  IF   E           K DISK
     FTELL      IF   E           K DISK
     FTURER     UF   E           K DISK
     FTURER1    IF   E           K DISK
     F                                     RENAME(TURERR:TURE1R)
     FTURER2    IF   E           K DISK
     F                                     RENAME(TURERR:TURE2R)
     FTURER3    IF   E           K DISK
     F                                     RENAME(TURERR:TURE3R)
N01  FTURER14   IF   E           K DISK
N01  F                                     RENAME(TURERR:TURE14R)
N03  FTURER15   IF   E           K DISK
N03  F                                     RENAME(TURERR:TURE15R)
N10  FTURER24   IF   E           K DISK
N10  F                                     RENAME(TURERR:TURE24R)
     FSYEX88FM  CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
N08  D                 DS
N08  D  KNAVN                  1     30
N08  D  KNAVN19                1     19
     D                 DS
     D  YBILN2                 1     10
     D  YSDT2                  1     20
     D  AL                     1     20
     D                                     DIM(20)
     D                 DS
     D  TUDTÅ                  1      4  0
     D  TUDTM                  5      6  0
     D  TUDTD                  7      8  0
     D  TUDT                   1      8  0
     D                 DS
     D  WSDTD                  1      2  0
     D  WSDTM                  3      4  0
     D  WSDTÅ                  5      6  0
     D  WSDT                   1      6  0
     D                 DS
     D  WSDTD1                 1      2  0
     D  WSDTM1                 3      4  0
     D  WSDTÅ1                 5      6  0
     D  WSDT1                  1      6  0
     D                 DS
     D  WSDTD2                 1      2  0
     D  WSDTM2                 3      4  0
     D  WSDTÅ2                 5      6  0
     D  WSDT2                  1      6  0
     D                 DS
     D  XDT2Å                  1      4  0
     D  XDT2M                  5      6  0
     D  XDT2D                  7      8  0
     D  XDT2                   1      8  0
     D                 DS
     D  XDT3Å                  1      4  0
     D  XDT3M                  5      6  0
     D  XDT3D                  7      8  0
     D  XDT3                   1      8  0
n10  D                 DS
n10  D  XDT4Å                  1      4  0
n10  D  XDT4M                  5      6  0
n10  D  XDT4D                  7      8  0
n10  D  XDT4                   1      8  0
n10  D                 DS
n10  D  WSDTD4                 1      2  0
n10  D  WSDTM4                 3      4  0
n10  D  WSDTÅ4                 5      6  0
n10  D  WSDT4                  1      6  0
N02  D                 DS
N02  D  TUSDT                  1     20
N02  D  TUSDTX                 1     13
     D                 DS
     D  TUSDF                  1     20
     D  WSSDF                  1     18
N02  D  WSSDFX                 1     13
      * PARAMETERSTRENG FOR KALL PÅ SYKS12
     D                 DS
     D  P1DS                   1    256
     D  P1FLAG                 1      1
     D  P1BNR                  2      8  0
     D  P1AVD                  9     12  0
     D  P1TUNR                13     20  0
     D  P1OAVD                21     24  0
     D  P1OPD                 25     31  0
     D  P1BL                  32     44  2
     D                UDS
     D  UFIRM                  1      2
     D  UAVD                  15     18  0
     D  USG                   19     21
     C*
     C                   EXSR      INIT
     C*
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
      *_____________
      * Fyll Subfile
      *"""""""""""""
n10  c   26turE24K       setll     turer24
s01  C*  27TURE1K        SETGT     TURER1
n01  c   27wsavd         ifeq      0
n01  c     ture14k       setll     turer14
n01  c                   else
S12  c*    ture1k        setll     turer1
N12  c     ture1k        setgt     turer1
n01  c                   end
n03  c   28wsheng        ifeq      'H'
n03  c     ture2k        setll     turer15
n03  c                   else
n03  C     TURE2K        SETLL     TURER2
n03  c                   end
s03  C*  28TURE2K        SETLL     TURER2
     C   29TURE3K        SETLL     TURER3
     C     START1        TAG
     C                   EXSR      LESFIL
      *
     C     START2        TAG
      *
     C  N94              DO
     C                   DO        14
     C                   ADD       1             ZRECNO
     C                   EXSR      KDTFM1
     C     TUPRO         CHAIN     AVSLUTG2                           78
     C     TUPRO         CHAIN     AVSLUTL2                           79
N08  C     KUNDK         CHAIN     CUNDL01                            33
N08  C   33              move      *blanks       knavn
     C                   WRITE     SUBFIL
     C                   EXSR      LESFIL
     C  N94              END
     C                   END
     C*
     C                   MOVE      ZRECNO        XRECNO            5 0
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYWRK
     C                   ELSE
     C                   WRITE     SUBCTL
     C                   EXFMT     BUNN
     C                   END
     C*
     C                   SETOFF                                       9830
     C   06              DO
     C                   MOVE      WSAVD         TUAVD
     C                   MOVE      *ZEROS        UPRO
     C                   MOVE      'N'           USPØR
     C                   EXSR      TILTUR
     C*** UPRO <> 0  --> HAR OPPRETTET EN NY TUR OG LEGGE DEN PÅ SUBFIL
     C     UPRO          IFNE      0
     C     TURERK        CHAIN(N)  TURER                              33
     C  N33              ADD       1             XRECNO
     C  N33              MOVE      XRECNO        ZRECNO
     C  N33              EXSR      KDTFM1
     C  N33TUPRO         CHAIN     AVSLUTG2                           78
     C  N33TUPRO         CHAIN     AVSLUTL2                           79
     C  N33              WRITE     SUBFIL
     C                   END
     C                   GOTO      START3
     C                   END
     C   03              GOTO      SLUTT
     C   12              GOTO      SLUTT
     C   05              DO
     C   28              MOVE      WSBILN        YBILN1
     C   29              MOVE      WSSDT         YSDT1
     C                   GOTO      START
     C                   END
     C   08              DO
     C                   EXSR      MOVB
     C                   GOTO      START6
     C                   END
     C*
     C*** BLA VIDERE
     C   25              DO
     C                   Z-ADD     XRECNO        ZRECNO
     C                   SETOFF                                       25
     C                   GOTO      START2
     C                   END
     C*
     C     ZRECNO        IFEQ      0
     C                   GOTO      START3
     C                   ELSE
     C                   EXSR      HENT
     C   98              GOTO      START3
     C   32
     CAN 12              GOTO      SLUTT
     C     WSNR          IFEQ      '9'
     C                   MOVE      ' '           WSNR
     C     TUPRO         CHAIN     AVSLUTG2                           78
     C     TUPRO         CHAIN     AVSLUTL2                           79
     C                   UPDATE    SUBFIL
     C                   GOTO      START3
     C                   END
     C                   END
     C     WSNR          IFEQ      '7'
     C     WSNR          OREQ      '4'
     C   27              MOVE      ' '           YST
     C   28              MOVE      WSBILN        YBILN1
     C   29              MOVE      WSSDT         YSDT1
     C                   GOTO      START
     C                   END
     C*
      * ELLER TILTUR FOR ENDRING SPØRRING
      *
     C     WSNR          IFEQ      '2'
     C     WSNR          OREQ      '5'
     C                   EXSR      TILTUR
     C                   END
     C     WSNR          IFEQ      '8'
     C                   CALL      'SYTR31'
     C                   PARM                    TUAVD
     C                   PARM                    TUPRO
     C                   PARM      0             UFLAGG2           1 0
     C                   ENDIF
     C                   GOTO      START3
     C*
     C*** SØK FRA UTSKRIFTTYPE
     C     START6        TAG
     C                   EXSR      KDTFM2
     C                   EXFMT     SØKTUR
     C                   EXSR      KDTFM2
     C                   SETOFF                                       9930
     C   03
     COR 12              MOVE      XAVD          WSAVD
     C   03              GOTO      SLUTT
     C   12              GOTO      START3
     C*
n10  c     *in04         ifeq      '1'
N10  C                   CALL      'SOEKKUN'
N10  C                   PARM                    WSKNT
N10  C                   PARM                    FIFIRM
N10  C                   PARM                    UKNAVN           30
n10  c                   goto      start6
n10  c                   end
     C                   EXSR      TSTSØK
     C   30              GOTO      START6
     C  N27              MOVE      XAVD          WSAVD
     C     WSDT1         IFEQ      0
     C                   MOVE      *ZEROS        XDT2
     C                   ELSE
     C                   MOVE      WSDTD1        XDT2D
     C                   MOVE      WSDTM1        XDT2M
     C                   MOVE      WSDTÅ1        XDT2Å
     C     WSDTÅ1        IFGT      50
     C                   MOVEL     '19'          XDT2Å
     C                   ELSE
     C                   MOVEL     '20'          XDT2Å
     C                   END
     C                   END
     C     WSDT2         IFEQ      0
     C                   MOVE      *ZEROS        XDT3
     C                   ELSE
     C                   MOVE      WSDTD2        XDT3D
     C                   MOVE      WSDTM2        XDT3M
     C                   MOVE      WSDTÅ2        XDT3Å
     C     WSDTÅ2        IFGT      50
     C                   MOVEL     '19'          XDT3Å
     C                   ELSE
     C                   MOVEL     '20'          XDT3Å
     C                   END
     C                   END
     C                   GOTO      START
     C*
     C     SLUTT         TAG
     C  N32              MOVE      WSAVD         UAVD
     C                   SETON                                        LR
     C                   RETURN
     C*
     C***********************************************
     C     TILTUR        BEGSR
     C***********************************************
     C     UTRAN         IFEQ      'N'
     C                   CALL      'SYEX88A'
     C                   PARM                    UFIRM
     C                   PARM                    TUAVD
     C                   PARM                    UPRO
     C                   PARM                    USPØR
     C                   PARM                    UIMP
     C                   PARM                    UFLAGG            1 0
     C     UFLAGG        COMP      1                                      12
     C     UFLAGG        COMP      3                                      03
     C                   ELSE
     C                   CALL      'SYTR88A'
     C                   PARM                    UFIRM
     C                   PARM                    TUAVD
     C                   PARM                    UPRO
     C                   PARM                    USPØR
     C                   PARM                    UIMP
     C                   PARM                    UFLAGG            1 0
     C     UFLAGG        COMP      1                                      12
     C     UFLAGG        COMP      3                                      03
     C                   END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     HENT          BEGSR
     C***********************************************
     C                   MOVEA     '00'          *IN(91)
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
      *
N07   * C = SAMLECMR
N07   *
N07  C  N98WSNR          IFEQ      'C'
N07  C                   MOVE      TUPRO         UPROA             8
N07  C                   CALL      'SYFA55CMCL'
N07  C                   PARM                    UPROA
     C                   MOVE      ' '           WSNR
     C                   UPDATE    SUBFIL
N07  C                   GOTO      SLHENT
N07  C                   END
      *
      * 1 = PLUKKING AV TUR
      *
     C  N98
     CAN 32WSNR          IFEQ      '1'
     C                   MOVE      TUPRO         UPRO
     C                   SETON                                        12
     C                   GOTO      SLHENT
     C                   END
      *
      * 7 = OVER TIL BUDSJETT REKVISISJON
      *
     C  N98WSNR          IFEQ      '7'
     C                   MOVE      'T'           P1FLAG
     C                   MOVE      *ZEROS        P1BNR
     C                   MOVE      TUAVD         P1AVD
     C                   MOVE      TUPRO         P1TUNR
     C                   MOVE      *ZEROS        P1OAVD
     C                   MOVE      *ZEROS        P1OPD
     C                   MOVE      *ZEROS        P1BL
     C                   MOVE      P1DS          PARM1           256
     C                   CALL      'SYKS12'
     C                   PARM                    PARM1
     C                   GOTO      SLHENT
     C                   END
      *
n04   * T = track and trace
n04   *
n04  C  N98WSNR          IFEQ      'T'
n04  C                   MOVE      TUAVD         ttavd             4 0
n04  C                   Z-ADD     0             TTopd             7 0
n04  C                   Z-ADD     1             TTFLAG            1 0
N04  C                   Z-ADD     TUPRO         TTTUR             8 0
n04  C                   CALL      'SYTT01CL'
n04  C                   PARM                    TTAVD
n04  C                   PARM                    ttopd
N04  C                   PARM                    TTTUR
n04  C                   PARM                    TTFLAG
n04  c                   move      ' '           wsnr
n04  C                   UPDATE    SUBFIL
n04  C                   GOTO      SLHENT
n04  c                   end
      *
      *
      * 4 SETTE G-STATUS
      *
     C  N98WSNR          IFEQ      '4'
     C                   MOVE      TUPRO         UPRO
     C     TURERK        CHAIN     TURER                              98
     C  N98              MOVE      'G'           TUST
     C  N98              UPDATE    TURERR
     C     TUPRO         CHAIN     AVSLUTG2                           78
     C     TUPRO         CHAIN     AVSLUTL2                           79
     C                   UPDATE    SUBFIL
     C                   GOTO      SLHENT
     C                   END
      *
      * 2 = ENDRE TURINFO
      *
     C  N98WSNR          IFEQ      '2'
     C                   MOVE      'N'           USPØR             1
     C                   MOVE      TUPRO         UPRO
     C     TURERK        CHAIN(N)  TURER                              98
     C  N98TUST          IFNE      ' '
     C     TUST          ANDNE     'G'
     C                   MOVE      'SPT0006'     MSGNR
     C                   SETON                                        3098
     C                   END
     C  N98
     CANN98TUPRO         CHAIN     AVSLUTL2                           33
     C  N33              DO
     C                   MOVE      'SHU0580'     MSGNR
     C                   SETON                                        9830
     C                   END
     C     TUPRO         CHAIN     AVSLUTG2                           78
     C     TUPRO         CHAIN     AVSLUTL2                           79
     C                   UPDATE    SUBFIL
     C                   GOTO      SLHENT
     C                   END
      *
      * 5 = SPØRRING TURINFO
      *
     C  N98WSNR          IFEQ      '5'
     C                   MOVE      'J'           USPØR             1
     C                   MOVE      TUPRO         UPRO
     C     TURERK        CHAIN(N)  TURER                              98
     C     TUPRO         CHAIN     AVSLUTG2                           78
     C     TUPRO         CHAIN     AVSLUTL2                           79
     C                   UPDATE    SUBFIL
     C                   GOTO      SLHENT
     C                   END
     C  N98WSNR          IFEQ      '9'
     C                   MOVE      *ZEROS        UTDN              4 0
     C                   MOVE      USG           USG2              3
     C                   MOVE      'CRT'         UTYPE             3
     C                   CALL      'TR011C'
     C                   PARM                    TUAVD
     C                   PARM                    UTDN
     C                   PARM                    TUPRO
     C                   PARM                    USG2
     C                   PARM                    UTYPE
     C                   PARM                    UFLAGG            1 0
     C                   GOTO      SLHENT
     C                   END
     C  N98WSNR          IFEQ      '8'
     C                   CALL      'SYTR31'
     C                   PARM                    TUAVD
     C                   PARM                    TUPRO
     C                   PARM      0             UFLAGG2           1 0
     C                   GOTO      SLHENT
     C                   ENDIF
      *
      * I = ISO
      *
     C  N98WSNR          IFEQ      'I'
     C                   MOVE      *ZEROS        UAVD2             4 0
     C                   MOVE      *ZEROS        UOPD2             7 0
     C                   MOVE      TUPRO         UTNO2             8 0
     C                   CALL      'SYIS10A'
     C                   PARM                    UFIRM
     C                   PARM                    UAVD2
     C                   PARM                    UOPD2
     C                   PARM                    UTNO2
     C                   MOVE      ' '           WSNR
     C                   UPDATE    SUBFIL
     C                   GOTO      SLHENT
     C                   END
      *
      * S = Skilleark
      *
     C  N98WSNR          IFEQ      'S'
     C                   MOVE      '  '          SCATYP            2
     C                   MOVEL     TUPRO         SCASTR           20
n06  C                   MOVE      '*T'          SCATYP            2
     C                   CALL      'ARCA000CL'
     C                   PARM                    SCATYP
     C                   PARM                    SCASTR
     C                   MOVE      ' '           WSNR
     C                   UPDATE    SUBFIL
     C                   GOTO      SLHENT
     C                   END
      *
      *
N05  C  N98WSNR          IFEQ      'O'
N05  C     XWIN          IFEQ      ' '
N05  C                   MOVE      'J'           XWIN              1
N05  C                   WRITE     WINDOWSZ
N05  C                   END
N05  C                   MOVE      '  '          SCATYP            2
N05  c                   setoff                                       33
N05  C     feilsk        tag
N05  C                   EXFMT     SCANN
N05  C     *IN12         IFEQ      '0'
N05  C     scatyp        chain     arktxt                             33
N05  C   33              goto      feilsk
N05  C                   MOVEL     TUPRO         SCASTR           20
N05  C                   CALL      'ARCA000T'
N05  C                   PARM                    SCATYP
N05  C                   PARM                    SCASTR
N05  C                   END
N05  C                   MOVE      ' '           WSNR
N05  C                   UPDATE    SUBFIL
N05  C                   GOTO      SLHENT
N05  C                   END
      *
      * P = PDF-dok/se Arkiv
      *
     C  N98WSNR          IFEQ      'P'
     C                   MOVEL     'T:'          uuparu
     C                   MOVE      TUPRO         uuparu           10
     C                   CALL      'ARC003'
     C                   PARM                    uuuavd            4 0
     C                   PARM                    uuuopd            7 0
     C                   PARM                    uuparu
     C                   MOVE      ' '           WSNR
     C                   UPDATE    SUBFIL
     C                   GOTO      SLHENT
     C                   END
N09   *
N09   * E = EventGrabber - send SMS/Turlenke TKeventgrabber
N09   *
N09  C  N98WSNR          IFEQ      'E'
N09  C                   MOVE      TUPRO         UPROA             8
N09  C                   CALL      'SYGE72S'
N09  C                   PARM                    uproa
N09  C                   MOVE      ' '           WSNR
N09  C                   UPDATE    SUBFIL
N09  C                   GOTO      SLHENT
N09  C                   END
N09   *
N09   * G = Godsliste mail- send mail med godsliste
N09   *
N09  C  N98WSNR          IFEQ      'G'
N09  C                   MOVE      TUPRO         UPROA             8
N09  C                   CALL      'SYGE72T'
N09  C                   PARM                    uproa
N09  C                   MOVE      ' '           WSNR
N09  C                   UPDATE    SUBFIL
N09  C                   GOTO      SLHENT
N09  C                   END
      *
     C                   END
     C*
     C   98              DO
     C                   MOVE      'DIV0012'     MSGNR
     C                   SETON                                            30
     C                   END
     C*
     C     SLHENT        ENDSR
     C*
     C***********************************************
     C     TSTSØK        BEGSR
     C***********************************************
     C     WSAVD         IFNE      0
s01  C*    WSPRO         ORNE      0
     C     WSAVD         CHAIN     TELL                               99
     C   99AVDZER        CHAIN     TELL                               99
     C   99              MOVE      'SPA0009'     MSGNR
     C   99              SETON                                        30
     C  N99WSPRO         IFEQ      0
     C                   MOVE      99999999      WSPRO
     C                   END
     C  N99              MOVEA     '100'         *IN(27)
     C                   GOTO      SLTST
     C                   END
n01  C     WSPRO         IFNE      0
s10  C*                  MOVEA     '100'         *IN(27)
n10  C                   MOVEA     '0100'        *IN(26)
n01  C                   GOTO      SLTST
n01  C                   end
     C*
     C     WSBILN        IFNE      *BLANKS
     C                   EXSR      MAXBIL
s10  C*                  MOVEA     '010'         *IN(27)
n10  C                   MOVEA     '0010'        *IN(26)
     C                   GOTO      SLTST
     C                   END
N10  C*
N10  C     WSKNT         IFNE      0
n10  C                   MOVEA     '1000'        *IN(26)
n10  C     WSDT4         IFEQ      0
N10  C                   MOVE      *ZEROS        XDT4
N10  C                   ELSE
n10  C                   MOVE      WSDTD4        XDT4D
n10  C                   MOVE      WSDTM4        XDT4M
N10  C                   MOVE      WSDTÅ4        XDT4Å
N10  C     WSDTÅ4        IFGT      50
n10  C                   MOVEL     '19'          XDT4Å
n10  C                   ELSE
n10  C                   MOVEL     '20'          XDT4Å
n10  C                   END
n10  C                   END
n10  C                   GOTO      SLTST
n10  C                   END
     C*
     C     WSSDT         IFNE      *BLANKS
n10  c     wsknt         andeq     0
     C                   EXSR      MAXSDT
s10  C*                  MOVEA     '001'         *IN(27)
n10  C                   MOVEA     '0001'        *IN(26)
     C                   GOTO      SLTST
     C                   END
     C*
     C                   MOVE      'DIV0983'     MSGNR
     C                   SETON                                        9930
     C*
     C     SLTST         ENDSR
     C*
     C***********************************************
     C     MAXBIL        BEGSR
     C***********************************************
     C                   MOVE      WSBILN        YBILN1
     C                   MOVE      WSBILN        YBILN2
     C                   Z-ADD     10            XN                2 0
     C     STMBIL        TAG
     C     AL(XN)        IFEQ      ' '
     C                   MOVE      '9'           AL(XN)
     C                   SUB       1             XN
     C                   GOTO      STMBIL
     C                   END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MAXSDT        BEGSR
     C***********************************************
     C                   MOVE      WSSDT         YSDT1
     C                   MOVE      WSSDT         YSDT2
     C                   Z-ADD     20            XN
     C     STMSDT        TAG
     C     AL(XN)        IFEQ      ' '
     C                   MOVE      '9'           AL(XN)
     C                   SUB       1             XN
     C                   GOTO      STMSDT
     C                   END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVB          BEGSR
     C***********************************************
     C                   MOVE      WSAVD         XAVD              4 0
     C                   MOVE      *ZEROS        WSAVD
     C                   MOVE      *ZEROS        WSPRO
     C                   MOVE      *BLANKS       WSBILN
     C                   MOVE      *ZEROS        WSDT1
     C                   MOVE      *BLANKS       WSSDT
     C                   MOVE      *ZEROS        WSDT2
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     LESFIL        BEGSR
     C***********************************************
     C     STLES         TAG
n10  c     *in26         ifeq      '1'
n10  c     ture24k2      reade     turer24                                94
n10  c  N94wsavd4        ifne      0
n10  c     tuavd         andlt     wsavd4
n10  c                   goto      stles
n10  c                   end
n10  c  N94Yst4          ifne      '*'
n10  c     tust          andne     Yst4
n10  c                   goto      stles
n10  c                   end
n10  c                   end
s01  C*  27YST           READPE    TURER1                                 94
n01  c   27wsavd         ifeq      0
n01  c                   read      turer14                                94
n01  c                   else
n01  C     YST           READPE    TURER1                                 94
n01  c                   end
n03  c   28wsheng        ifeq      'H'
n03  c                   read      turer15                                94
n03  c                   else
n03  C                   read      TURER2                                 94
n03  c                   end
s03  C*  28              READ      TURER2                                 94
     C   29              READ      TURER3                                 94
     C*
     C   27
     CANN94WSAVD         IFNE      TUAVD                                        1<-B
n01  C     WSAVD         ANDNE     0
     C                   SETON                                            94
     C                   END                                                     2<-E
     C*
     C   28
S03  C*NN94TUBILN        COMP      YBILN2                             94
n03  cANN28wsheng        ifeq      'H'
N03  C     TUHENG        COMP      YBILN2                             94
N03  C                   ELSE
N03  C     TUBILN        COMP      YBILN2                             94
N03  C                   END
     C   28
     CANN94              DO
     C     TUDT          IFLT      XDT2
n03  c     wsheng        ifeq      'H'
N03  C                   MOVE      TUHENG        YBILN1
N03  C                   ELSE
     C                   MOVE      TUBILN        YBILN1
N03  C                   END
     C     TURE2K        SETLL     TURER2
     C                   GOTO      STLES
     C                   END
     C     TUST          IFNE      ' '
     C     TUST          ANDNE     'G'
     C                   GOTO      STLES
     C                   END
     C                   END
     C*
     C   29
     CANN94TUSDT         COMP      YSDT2                              94
     C   29
     CANN94              DO
     C     TUDT          IFLT      XDT3
     C                   MOVE      TUSDT         YSDT1
     C     TURE3K        SETLL     TURER3
     C                   GOTO      STLES
     C                   END
     C     TUST          IFNE      ' '
     C     TUST          ANDNE     'G'
     C                   GOTO      STLES
     C                   END
     C                   END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     KUNDK         KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    TUKNA
     C     TURERK        KLIST
     C                   KFLD                    TUAVD
     C                   KFLD                    UPRO
n10  C     TURE24K       KLIST
n10  C                   KFLD                    WSknt
n10  C                   KFLD                    xdt4
n10  C     TURE24K2      KLIST
n10  C                   KFLD                    WSknt
     C     TURE1K        KLIST
     C                   KFLD                    YST
     C                   KFLD                    WSAVD
     C                   KFLD                    WSPRO
n01  C     TURE14K       KLIST
n01  C                   KFLD                    WSPRO
     C     TURE2K        KLIST
     C                   KFLD                    YBILN1
     C                   KFLD                    XDT2
     C     TURE3K        KLIST
     C                   KFLD                    YSDT1
     C                   KFLD                    XDT3
     C*
     C     *ENTRY        PLIST
     C                   PARM                    UIMP              1
     C                   PARM                    U10               1
     C                   PARM                    UPRO              8 0
     C                   PARM                    UTRAN             1
     C     UIMP          COMP      'J'                                    31
     C     U10           COMP      'J'                                    32
     C*
     C                   MOVE      99999999      WSPRO
     C     *LIKE         DEFINE    WSAVD         AVDZER
     C                   MOVE      'N'           USPØR             1
     C                   MOVE      UAVD          WSAVD
     C                   MOVE      ' '           YST               1
     C                   MOVE      *BLANKS       YBILN1           10
     C                   MOVE      *BLANKS       YSDT1            20
     C                   MOVE      *ZEROS        TUDT
     C                   MOVE      *ZEROS        WSDT
     C                   MOVE      *ZEROS        WSDT1
     C                   MOVE      *ZEROS        WSDT2
N10  C                   MOVE      *ZEROS        WSDT4
N10  C                   MOVE      *ZEROS        xdt4
     C                   MOVE      *ZEROS        XDT2
     C                   MOVE      *ZEROS        XDT3
     C                   SETON                                        27
     C*
N08  C                   READ      FIRM                                   33
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     KDTFM1        BEGSR
     C***********************************************
     C                   MOVE      TUDTD         UDT1
     C                   MOVE      TUDTM         UDT2
     C                   MOVE      TUDTÅ         UDT3
     C                   EXSR      KDTFMT
     C                   MOVE      UDT1          WSDTD
     C                   MOVE      UDT2          WSDTM
     C                   MOVE      UDT3          WSDTÅ
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     KDTFM2        BEGSR
     C***********************************************
     C                   MOVE      WSDTD1        UDT1              2 0
     C                   MOVE      WSDTM1        UDT2              2 0
     C                   MOVE      WSDTÅ1        UDT3              2 0
     C                   EXSR      KDTFMT
     C                   MOVE      UDT1          WSDTD1
     C                   MOVE      UDT2          WSDTM1
     C                   MOVE      UDT3          WSDTÅ1
     C*
     C                   MOVE      WSDTD2        UDT1
     C                   MOVE      WSDTM2        UDT2
     C                   MOVE      WSDTÅ2        UDT3
     C                   EXSR      KDTFMT
     C                   MOVE      UDT1          WSDTD2
     C                   MOVE      UDT2          WSDTM2
     C                   MOVE      UDT3          WSDTÅ2
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     KDTFMT        BEGSR
     C***********************************************
     C                   CALL      'DATOFMT'
     C                   PARM                    UDT1
     C                   PARM                    UDT2
     C                   PARM                    UDT3
     C*
     C                   ENDSR
     C*
