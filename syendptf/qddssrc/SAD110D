      *
     A                                      DSPSIZ(27 132 *DS4)
     A                                      PRINT
     A                                      HELP
     A                                      ALTHELP
      *
     A          R BILDE01
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF03(03 'Avslutt program')
     A                                      CF04(04 'Søk kunde/leverandør')
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1  2'SYSPED/SAD110R'
     A                                      COLOR(BLU)
     A                                  1 20'Vedlikehold kurermanifest'
     A                                      COLOR(WHT)
     A                                  1120DATE
     A                                      EDTCDE(Y)
     A                                  2120TIME
     A                                  4 13MSGCON(012 DIV1705 *LIBL/SYMSGF)
     A            WSAVD          4Y 0B  4 26TEXT('AVDELINGSNR')
     A                                      DSPATR(UL)
     A  99                                  DSPATR(RI)
     A  99                                  DSPATR(PC)
     A                                      COLOR(BLU)
     A                                      EDTCDE(4)
     A            KOANVN        33A  O  4 32TEXT('AVDELINGSNAVN')
     A                                      COLOR(BLU)
     A                                  7 20MSGCON(035 DIV1901 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  8 20'Turnummer:'
     A            WSPRO          8Y 0B  8 35TEXT('TURNUMMER')
     A                                      DSPATR(UL)
     A                                      DSPATR(PC)
     A  98                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  9 20'UUID:'
     A            WSUUID        36A  B  9 35TEXT('Registreringsdato')
     A                                      CHECK(LC)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 10 20'Avg.dato:'
     A            WSATDD         6Y 0B 10 35TEXT('TURNUMMER')
     A                                      DSPATR(UL)
     A                                      DSPATR(PC)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 11 20'Reg.dato:'
     A            WSDTR          6Y 0B 11 35TEXT('TURNUMMER')
     A                                      DSPATR(UL)
     A                                      DSPATR(PC)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 12 20'Status:'
     A            WSST           1A  B 12 35TEXT('Status')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A*                                26 20'F3=Avslutt   F4=Søk kundenr'
     A                                 26 20'F3=Avslutt'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 27  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
      *
     A          R SUBFIL                    SFL
     A            WSNR           2Y 0B  6  2EDTCDE(4)
     A            KMAVD          4Y 0O  6  5TEXT('Avdeling')
     A                                      EDTCDE(4)
     A            KMPRO          8Y 0O  6 10TEXT('Turnr')
     A                                      EDTCDE(4)
     A  41                                  COLOR(TRQ)
     A            KMSG           3A  O  6 19TEXT('Signatur')
     A            X_KMATDD       6S 0O  6 23TEXT('Akt. avgangsdato')
     A  43                                  COLOR(TRQ)
     A*           KMUUID        36A  O  6 30TEXT('UUID')
     A            MCNAS         30A  O  6 30TEXT('Avsender')
     A  42                                  COLOR(TRQ)
     A            KMTM           2Y 0O  6 67TEXT('Transport måte')
     A                                      EDTCDE(4)
     A            KMST           1A  O  6 70TEXT('Status')
     A  45                                  COLOR(TRQ)
     A            KMTD1          8A  O  6 73TEXT('1. tollsted ankomst')
     A            KMTDA          8A  O  6 82TEXT('Akt. tollsted ankomst')
     A            KMNAC         30A  O  6 91TEXT('Transportør')
     A            X_KMDTR        6S 0O  6122TEXT('Reg.dato')
     A  44                                  COLOR(TRQ)
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0019)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A                                      CF03(03 'Avslutt program')
     A                                      CF12(12 'AVBRYT')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'Lag ny')
     A                                      CF08(08 'Søk')
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A            ZRECNO         4S 0O  1 60SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYSPED/SAD110R'
     A                                      COLOR(BLU)
     A                                  1 20'Vedlikehold kurermanifest'
     A                                      COLOR(WHT)
     A                                  1120DATE
     A                                      EDTCDE(Y)
     A                                  2120TIME
     A                                  2  2MSGCON(012 DIV1705 *LIBL/SYMSGF)
     A            WSAVD          4Y 0O  2 15TEXT('AVDELINGSNR')
     A                                      COLOR(BLU)
     A                                      EDTCDE(4)
     A            KOANVN        33A  O  2 20TEXT('AVDELINGSNAVN')
     A                                      COLOR(BLU)
     A                                  3  2'Velg   1=Lage 2=Endre 3=Kopiere 5=-
     A                                      Se manifest 9=EDI-logg 90=Send'
     A                                      COLOR(BLU)
     A*                                 4  2'Op Avd. Turnr.   Sig Avg.DT UUID  -
     A*                                                                   TM St-
     A                                  4  2'Op Avd. Turnr.   Sig Avg.DT Avsend-
     A                                      er                             TM S-
     A                                      t Ank.T.S. Akt.T.S. Transportør    -
     A                                                      Reg.DT'
     A                                      COLOR(WHT)
     A            W2NR           2Y 0B  5  2DSPATR(UL)
     A                                      EDTCDE(4)
     A            W2AVD          4Y 0B  5  5DSPATR(UL)
     A                                      EDTCDE(4)
     A            W2PRO          8Y 0B  5 10DSPATR(UL)
     A                                      EDTCDE(4)
     A            W2ATDD         6Y 0B  5 23DSPATR(UL)
     A                                      EDTCDE(4)
     A*           W2UUID        36A  B  5 30DSPATR(UL)
     A            W2ST           1A  B  5 70DSPATR(UL)
     A            W2DTR          6Y 0B  5122DSPATR(UL)
     A                                      EDTCDE(4)
      *
     A          R BUNN
     A                                 26  2'F3=Avslutt  F5=Frisk  F6=Lage ny  -
     A                                      F8=Søk  F12=Avbryt'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 27  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R NYVARE
     A                                      CF03(03 'Avslutt program')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'Lag ny')
     A                                      CF08(08 'Søk')
     A                                      CF12(12 'AVBRYT')
     A                                  1  2'SYSPED/SAD110R'
     A                                      COLOR(BLU)
     A                                  1 20'Vedlikehold kurermanifest'
     A                                      COLOR(WHT)
     A                                  1120DATE
     A                                      EDTCDE(Y)
     A                                  2120TIME
     A                                  2  2MSGCON(012 DIV1705 *LIBL/SYMSGF)
     A            WSAVD          4Y 0O  2 15TEXT('AVDELINGSNR')
     A                                      COLOR(BLU)
     A                                      EDTCDE(4)
     A            KOANVN        33A  O  2 20TEXT('AVDELINGSNAVN')
     A                                      COLOR(BLU)
     A                                  3  2'Velg   1=Lage 2=Endre 3=Kopiere 5=-
     A                                      Se manifest 9=EDI-logg 90=Send'
     A                                      COLOR(BLU)
     A*                                 4  2'Op Avd. Turnr.   Sig Avg.DT UUID  -
     A*                                                                   TM St-
     A                                  4  2'Op Avd. Turnr.   Sig Avg.DT Avsend-
     A                                      er                             TM S-
     A                                      t Ank.T.S. Akt.T.S. Transportør    -
     A                                                      Reg.DT'
     A                                      COLOR(WHT)
     A            W2NR           2Y 0B  5  2DSPATR(UL)
     A                                      EDTCDE(4)
     A            W2AVD          4Y 0B  5  5DSPATR(UL)
     A                                      EDTCDE(4)
     A            W2PRO          8Y 0B  5 10DSPATR(UL)
     A                                      EDTCDE(4)
     A            W2ATDD         6Y 0B  5 23DSPATR(UL)
     A                                      EDTCDE(4)
     A*           W2UUID        36A  B  5 30DSPATR(UL)
     A*                                     CHECK(LC)
     A            W2ST           1A  B  5 70DSPATR(UL)
     A            W2DTR          6Y 0B  5122DSPATR(UL)
     A                                      EDTCDE(4)
     A                                 26  2'F3=Avslutt  F5=Frisk  F6=Lage ny  -
     A                                      F8=Søk  F12=Avbryt'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 27  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R ERRMSG
     A                                      WINDOW(*DFT 4 40 *NOMSGLIN)
     A                                      OVERLAY
     A                                  1  2'Standardmelding er ikke definert.'
     A                                      COLOR(RED)
     A                                  2  2'Start pkt. 5 på meny EDIMNU3.'
     A                                      COLOR(RED)
     A                                  3  2'Meldingstype = KURERM'
     A                                      COLOR(YLW)
     A                                  4  2'Meldingsutgavenr = 001'
     A                                      COLOR(YLW)
