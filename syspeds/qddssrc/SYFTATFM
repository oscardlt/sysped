********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
P7013 * 01 YBC PTF7 ORGANISASJONSNR ER ENDRET FRA 9.0 TIL 11A          EST
********************************************************************
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      CF03(03 'Avslutt program')
     A                                      CF12(12 'Gå til forrige bilde')
     A                                      HLPDOC('STD' 'HELPNU' 'HLP400')
     A                                      HELP
     A          R BILDE01
     A                                  1  3'SYSPED/SYFTAT'
     A                                      COLOR(BLU)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(GRN)
     A                                  1 70TIME
     A                                      COLOR(GRN)
     A                                  1 20'Vedlikehold av standardopplysninge-
     A                                      r'
     A                                      COLOR(WHT)
     A                                  2 29'S A D  -  transitt'
     A                                      COLOR(WHT)
     A                                  2  3MSGCON(12 DIV1451 SYMSGF)
     A                                      COLOR(TRQ)
     A            FIFIRM         2A  O  2 16TEXT('FIRMAKODE')
     A                                      COLOR(TRQ)
     A                                  2 58MSGCON(022 DIV0047 *LIBL/SYMSGF)
     A                                      COLOR(TRQ)
     A                                  4  2MSGCON(040 DIV2055 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  6  2MSGCON(034 SSI0030 *LIBL/SYMSGF)
     A            WSAAVD         4Y 0B  6 39DSPATR(UL)
     A  99                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                 23 23'F3 = Avslutt'
     A                                      COLOR(BLU)
     A  99        MSG105        78A  O 24  2MSGID(SPA0001 SYMSGF)
     A                                      COLOR(WHT)
     A          R BILDE02
     A                                  1  3'SYSPED/SYFTAT'
     A                                      COLOR(BLU)
     A                                  1 76'Del-1'
     A                                      COLOR(YLW)
     A                                  2 20'Vedlikehold av standardopplysninge-
     A                                      r'
     A                                      COLOR(WHT)
     A                                  2 29'S A D  -  transitt'
     A                                      COLOR(WHT)
     A                                  4  2'Avdeling:'
     A            WSAAVD         4Y 0O  4 15TEXT('AVDELINGSNR')
     A                                      COLOR(BLU)
     A                                      EDTCDE(4)
     A            KOANVN        33A  O  4 28TEXT('AVDELINGSNAVN')
     A                                      COLOR(BLU)
     A                                  6  2MSGCON(040 DIV2055 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  7  4MSGCON(030 DIV1707 *LIBL/SYMSGF)
     A            WSDL           1A  B  7 37TEXT('BRUKE DATALISTE ?')
     A                                      DSPATR(UL)
     A  99                                  DSPATR(RI)
     A  99                                  DSPATR(PC)
     A                                      VALUES(' ' 'J' 'N' '1')
     A                                  7 45MSGCON(025 DIV1708 *LIBL/SYMSGF)
     A                                  8  6MSGCON(015 DIV1709 *LIBL/SYMSGF)
     A            WSA4           1A  B  8 37DSPATR(UL)
     A                                      VALUES(' ' 'J' 'N')
     A                                  8 45MSGCON(009 DIV1710 *LIBL/SYMSGF)
     A                                  9  4MSGCON(030 DIV1714 *LIBL/SYMSGF)
     A            WSTDN          5Y 0B  9 37TEXT('TELLER TIL SAD-IMPORT')
     A                                      DSPATR(UL)
     A  94                                  DSPATR(RI)
     A  94                                  DSPATR(PC)
     A                                      EDTCDE(4)
     A                                 23 17'F3=Avslutt uten endring'
     A                                      COLOR(BLU)
     A                                 23 42'F12=Gå til forrige uten endring'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A          R BILDE04
     A                                  1 10'SAD-TRANSITT'
     A                                      COLOR(WHT)
     A                                  1 41'1.Dekl.'
     A                                  1 76'Del-3'
     A                                      COLOR(YLW)
     A            WSDTY          2A  B  1 49TEXT('Deklarasjonstype')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSDK           2A  B  1 55TEXT('DEKLARASJONSKODE')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  1 59'4.L.liste'
     A            WSLSTL         2Y 0B  1 69TEXT('LASTELISTE')
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(YLW)
     A                                  2  2'2.Avsender'
     A            WSKNK          8Y 0B  2 13TEXT('Kundenr Kjøper')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  2 23'Regnr'
S01  A*           WSRG           9Y 0B  2 29TEXT('Regn Fylk.skatt.sjef')
N01  A            WSRG          11A  B  2 29TEXT('Regn Fylk.skatt.sjef')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
S01  A*                                     EDTCDE(4)
     A                                  2 41'5.V.post'
     A            WSVPOS         3Y 0B  2 50TEXT('VAREPOST')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  2 54'6.Kolli'
     A            WSNTK          7Y 0B  2 62TEXT('Antall kolli')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSNAK         30A  B  3  2TEXT('Navn Kjøper')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  3 41'Brutto vekt'
     A            WSVKB          9Y 0B  3 53TEXT('Bruttovekt')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  3 63'Ref'
     A            WSAAVD         4Y 0O  3 67COLOR(WHT)
     A                                      EDTCDE(4)
     A            WSTDN          5Y 0O  3 72COLOR(WHT)
     A                                      EDTCDE(4)
     A            STSG           3A  O  3 78COLOR(WHT)
     A            WSADK1        30A  B  4  2TEXT('Adresse-1 Kjøper')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  4 40'15.Avs/utf.land'
     A            WSLKA          2A  B  4 56TEXT('Landk avs/utf-land')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSLKAT        17A  B  4 59TEXT('F.T. AVS/UTF.LAND')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSADK2        30A  B  5  2TEXT('Adresse-2 Kjøper')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  5 40'17.Be.land'
     A            WSLKB          2A  B  5 56TEXT('LANDK TIL B.LAND')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSFTB         17A  B  5 59TEXT('FRITEKST TIL B.LAND')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSADK3        30A  B  6  2TEXT('Adresse-3 Kjøper')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  6 40'18.Mid.land'
     A            WSLKM          2A  B  6 56TEXT('LK MIDLETS ID LAND')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSFTM         17A  B  6 59TEXT('F.T. MIDLETS ID LAND')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  7  2'8.Mottaker'
     A            WSKNS          8Y 0B  7 13TEXT('Kundenr Selger')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  7 40'19.Cont.'
     A            WSKDC          1A  B  7 49TEXT('Container 0/1')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSNAS         30A  B  8  2TEXT('Navn Selger')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  8 40'21.Ident.'
     A            WSTRID        17A  B  8 50TEXT('Transport ID')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSLKT          2A  B  8 68TEXT('Landk Tr.middel')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSADS1        30A  B  9  2TEXT('Adresse-1 Selger')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  9 40'25.Tr.måte'
     A            WSTRM          2Y 0B  9 51TEXT('Transportmåte')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(9)
     A            WSADS2        30A  B 10  2TEXT('Adresse-2 Selger')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 10 44'Tilbakesendes til:'
     A            WSTS01        14   B 10 63DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSADS3        30A  B 11  2TEXT('Adresse-3 Selger')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSTS02        14   B 11 63DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSTS03        14   B 12 63DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSTS04        14   B 13 63DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSTS05        14   B 14 63DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 15 53'Transitteringsnr.serie'
     A            WSCTNT         1A  B 15 76DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 16  1'50.Hovedansvarlig'
     A            WSKNA          8Y 0B 16 19TEXT('HOVEDANSVARLIG')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 16 36'51.'
     A            WSSTG1         8A  B 16 40TEXT('GARANTITOLLSTED-1')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSLKG1         2A  B 16 49TEXT('GARANTI-land-1')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSSTG2         8A  B 16 53TEXT('GARANTITOLLSTED-2')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSLKG2         2A  B 16 62TEXT('GARANTI-land-2')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSSTG3         8A  B 16 66TEXT('GARANTITOLLSTED-3')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSLKG3         2A  B 16 75TEXT('GARANTI-land-3')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSNAA         30A  B 17  2TEXT('Navn ANSVARLIG')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSSTG4         8A  B 17 40TEXT('GARANTITOLLSTED-4')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSLKG4         2A  B 17 49TEXT('GARANTI-land-4')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSSTG5         8A  B 17 53TEXT('GARANTITOLLSTED-5')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSLKG5         2A  B 17 62TEXT('GARANTI-land-5')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSSTG6         8A  B 17 66TEXT('GARANTITOLLSTED-6')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSLKG6         2A  B 17 75TEXT('GARANTI-land-6')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSADA1        30A  B 18  2TEXT('Adresse-1 ANSVARLIG')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 18 36'52.Garanti'
     A            WSGFT1        30A  B 18 47TEXT('GARATI FRITEKST-1')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSADA3        30A  B 19  2TEXT('Adresse-3 ANSVARLIG')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSGKD          1Y 0B 19 37TEXT('GARATIKODE')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSGFT2        30A  B 19 47TEXT('GARATI FRITEKST-2')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 20  1'Rubr. D/J'
     A            WSDFT1        15A  B 20 11DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 20 36'53.Tollsted'
     A            WSTSTB        20A  B 20 51TEXT('BESTEMMELSESTOLLSTED')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSTLKB         2A  B 20 48TEXT('LANDK TIL B.T.STED')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSDFT2        22A  B 21 11DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 21 36'54.Sted'
     A            WSDST         15A  B 21 44TEXT('Deklarantens sted')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 21 61'Reg.dato'
EV5  A            SEDT           8Y 0H
NV5  A            WSDT           6Y 0O 21 70TEXT('Registreringsdato')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(Y)
     A                                 22 36'Dato'
     A            WSDTG         10A  B 22 41TEXT('Dekl. godkj.dato')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 22 52'Løpenr'
     A            WSTLL          5A  B 22 59TEXT('Tollnr Løpenr')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 22 65'Eksp.'
     A            WSTLE          3A  B 22 71TEXT('Tollnr Ekspsted')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 23  1'F3 = Avslutt uten oppdatering'
     A                                      COLOR(BLU)
     A                                 23 32'F12 = Gå til forrige uten oppdater-
     A                                      ing'
     A                                      COLOR(BLU)
