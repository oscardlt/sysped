********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
11128 * 01 YBC PTF11 VEKT ER ENDRET FRA 9.0 TIL 11.3
********************************************************************
     A          R TR108P1
     A            KFMT1         75      1  2
     A            KFMT2         75      2  2
     A                                  4  2'FORHÅNDSVARSEL NORGE'
     A            UTRNR         18      6 42BARCODE(CODE128 3 *HRITOP *HRZ +
     A                                      (*WIDTH 0.010) (*RATIO 3) X'02')
     A                                 12  2'Utskriftdato  :'
     A                                 12 20DATE
     A                                      EDTCDE(Y)
     A                                 12 34'Tid:'
     A                                 12 39TIME
     A                                      EDTWRD('  :  :  ')
     A                                 14  2'Vår referanse :'
     A            THAVD          4  0  14 20
     A                                 14 24'/'
     A            THTDN          7  0  14 25
     A                                 14 32'/'
     A            THSG           3     14 33
     A                                 16  2'Avg.tollsted  :'
     A            THCATS         8A    16 20HIGHLIGHT
     A            XHCATS        35A    16 30HIGHLIGHT
     A                                 18  2'Avsender      :'
     A            THNAS         30A    18 20
     A            XHTINS        17A    18 51
     A            THADS1        30A    19 20
     A            THPNS          9A    20 20
     A            THPSS         24A    20 30
     A                                 22  2'Mottaker      :'
     A            THNAK         30A    22 20
     A            XHTINK        17A    22 51
     A            THADK1        30A    23 20
     A            THPNK          9A    24 20
     A            THPSK         24A    24 30
     A            THLKK          2A    24 58
     A                                 26  2'Hovedansvarlig:'
     A            THNAA         30A    26 20
     A            XHTINA        17A    26 51
     A            THADA1        30A    27 20
     A            THPNA          9A    28 20
     A            THPSA         24A    28 30
     A            THLKA          2A    28 58
     A                                 30  2'Brutto vekt   :'
S01  A*           THVKB          9  0  30 20EDTCDE(Z)
N01  A            THVKB         11  3  30 20EDTCDE(Z)
     A                                 32  2'Antall kolli  :'
     A            THNTK          7  0  32 20EDTCDE(Z)
     A                                 34  2'Transportmåte :'
     A            THTRM          2  0  34 20
     A                                 36  2'Tr.middels ID :'
     A            THTAID        27A    36 20
     A            KFMT3         75     64  2
