      *__________________________________
      * SYSPED / Tolldekl. TRANSITT
      *""""""""""""""""""""""""""""""""""
     A                                      REF(SR)
     A          R NCTSESTDR
     A            THST      R               REFFLD(ST)
     A            THST004   R               REFFLD(ST)
     A                                      COLHDG('IE004 MOTTATT')
     A            THST009   R               REFFLD(ST)
     A                                      COLHDG('IE009 MOTTATT')
     A            THST019   R               REFFLD(ST)
     A                                      COLHDG('IE019 MOTTATT')
     A            THST028   R               REFFLD(ST)
     A                                      COLHDG('IE028 MOTTATT')
     A            THST029   R               REFFLD(ST)
     A                                      COLHDG('IE029 MOTTATT')
     A            THST035   R               REFFLD(ST)
     A                                      COLHDG('IE035 MOTTATT')
     A            THST045   R               REFFLD(ST)
     A                                      COLHDG('IE045 MOTTATT')
     A            THST051   R               REFFLD(ST)
     A                                      COLHDG('IE051 MOTTATT')
     A            THST055   R               REFFLD(ST)
     A                                      COLHDG('IE055 MOTTATT')
     A            THST056   R               REFFLD(ST)
     A                                      COLHDG('IE056 MOTTATT')
     A            THST060   R               REFFLD(ST)
     A                                      COLHDG('IE060 MOTTATT')
     A            THST140   R               REFFLD(ST)
     A                                      COLHDG('IE140 MOTTATT')
     A            THST182   R               REFFLD(ST)
     A                                      COLHDG('IE182 MOTTATT')
     A            THST928   R               REFFLD(ST)
     A                                      COLHDG('IE928 MOTTATT')
     A            THST2     R               REFFLD(ST)
     A                                      COLHDG('LEDIG')
     A            THST3     R               REFFLD(ST)
     A                                      COLHDG('LEDIG')
     A            THAVD          4S 0       COLHDG('AVDELING')
     A            THTDN     R               REFFLD(TDN)
     A            THSG      R               REFFLD(SG)
     A            THDT      R               REFFLD(DT)
     A            THENKL         1A         COLHDG('FORENKLET PROSEDYRE')
      *
      * TRANSIT OPERATION
     A            THDK           5A         COLHDG('DEKLARASJONSTYPE')
     A            THDKT          1A         COLHDG('ADDI. DECL.TYPE')
     A            THTIRN        12A         COLHDG('TIR CARNET NUMBER')
     A            THSECU         1S 0       COLHDG('SECURITY')
     A            THRDIN         1S 0       COLHDG('REDUCED DATASET IND.')
     A            THSCIN         3A         COLHDG('SP. CIRCUMSTANCE IND')
     A            THCLKD         2A         COLHDG('LANGUAGE DEPARTURE')
     A            THBINI         1S 0       COLHDG('BINGING ITINEERARY')
     A            THFLAG         1S 0       COLHDG('ENDRINGSFLAG')
     A            THLIDT        10A         COLHDG('LIMIT DATE')
      * AUTORISASJON
     A            THATYP         4A         COLHDG('1. AUTORISATIONSTYPE')
     A            THAREF        35A         COLHDG('1. AUTORISATIONSREF.')
      * CUSTOMS OFFICE OF DEPARTURE, RUBR. C
     A            THCATS         8A         COLHDG('AVGANGS TOLLSTED')
     A            THCDT          8S 0       COLHDG('AKSEPTDATO')
     A            THCNA         22A         COLHDG('NÆRINGLIV AUTORISER')
     A            THCANR        17A         COLHDG('AUTORISASJONSNR')
      * CUSTOMS OFFICE OF DESTINATION, RUBR. 53
     A            THTSB          8A         COLHDG('BESTEMMELSES T.STED')
      * CUSTOMS OFFICE OF TRANSIT, RUBR. 51
     A            THTSD1         8A         COLHDG('TR.TOLLSTED 1')
     A            THETA1         8S 0       COLHDG('EST. ARRIVAL DATE 1')
     A            THETAT1        4S 0       COLHDG('EST. ARRIVAL TIME 1')
     A            THTSD2         8A         COLHDG('TR.TOLLSTED 2')
     A            THETA2         8S 0       COLHDG('EST. ARRIVAL DATE 2')
     A            THETAT2        4S 0       COLHDG('EST. ARRIVAL TIME 2')
     A            THTSD3         8A         COLHDG('TR.TOLLSTED 3')
     A            THETA3         8S 0       COLHDG('EST. ARRIVAL DATE 3')
     A            THETAT3        4S 0       COLHDG('EST. ARRIVAL TIME 3')
     A            THTSD4         8A         COLHDG('TR.TOLLSTED 4')
     A            THETA4         8S 0       COLHDG('EST. ARRIVAL DATE 4')
     A            THETAT4        4S 0       COLHDG('EST. ARRIVAL TIME 4')
     A            THTSD5         8A         COLHDG('TR.TOLLSTED 5')
     A            THETA5         8S 0       COLHDG('EST. ARRIVAL DATE 5')
     A            THETAT5        4S 0       COLHDG('EST. ARRIVAL TIME 5')
     A            THTSD6         8A         COLHDG('TR.TOLLSTED 6')
     A            THETA6         8S 0       COLHDG('EST. ARRIVAL DATE 6')
     A            THETAT6        4S 0       COLHDG('EST. ARRIVAL TIME 6')
     A            THTSD7         8A         COLHDG('TR.TOLLSTED 7')
     A            THETA7         8S 0       COLHDG('EST. ARRIVAL DATE 7')
     A            THETAT7        4S 0       COLHDG('EST. ARRIVAL TIME 7')
     A            THTSD8         8A         COLHDG('TR.TOLLSTED 8')
     A            THETA8         8S 0       COLHDG('EST. ARRIVAL DATE 8')
     A            THETAT8        4S 0       COLHDG('EST. ARRIVAL TIME 8')
      * CUSTOMS OFFICE OF EXIT FOR TRANSIT
     A            THTSDE         8A         COLHDG('1. OFFICE FOR EXIT')
     A            THTSDE2        8A         COLHDG('2. OFFICE FOR EXIT')
      * HOLDER OF THE TRANSIT PROCEDURE
     A            THKNH     R               REFFLD(KN)
     A                                      COLHDG('HOLDER OG TRANSIT')
     A            THTINH        17A         COLHDG('ID NUMMER')
     A            THTIRH        17A         COLHDG('TIR HOLDER ID NUMBER')
     A            THNAH         30A         COLHDG('HOLDER NAVN')
     A            THAD1H        30A         COLHDG('HOLDER ADDESS 1')
     A            THPNH         17A         COLHDG('HOLDER POST CODE')
     A            THPSH         24A         COLHDG('HOLDER CITYE')
     A            THLKH          2A         COLHDG('HOLDER COUNTRY')
     A            THCPH         30A         COLHDG('HOLDER CONT. PERSON')
     A            THTLH         20A         COLHDG('HOLDER TEL.')
     A            THEMH         50A         COLHDG('HOLDER EMAIL')
      * REPRESENTATIVE, HOVEDANSVARLIG
     A            THKNA     R               REFFLD(KN)
     A                                      COLHDG('HOVEDANSVARLIG')
     A            THTINA        17A         COLHDG('ID-NR ANSVARLIG')
     A            THSTA          1S 0       COLHDG('STATUS ANSVARLIG')
     A            THCPA         30A         COLHDG('REPRE. CONT. PERSON')
     A            THTLA         20A         COLHDG('REPRE. TEL.')
     A            THEMA         50A         COLHDG('REPRE. EMAIL')
      * GARANTI RUBR. 52
     A            THGKD          1A         COLHDG('GARATIKODE')
     A            THGFT1        24A         COLHDG('GARANTI')
     A            THGFT2        35A         COLHDG('ANNEN GARANTI')
     A            THGADK         4A         COLHDG('ADGANGSKODE')
     A            THGBL         11S 0       COLHDG('GARATIBELØP')
     A            THGPR          3S 1       COLHDG('GARATI % AV FRAKT')
     A            THGVK          3A         COLHDG('VALUTA')
      *
      *----------------------------------------------------
      * CONSIGNMENT
      *----------------------------------------------------
      *  AVSENDELAND  RUBR. 15A
     A            THALK     R               REFFLD(LKA)
     A                                      COLHDG('LANDK TIL A.LAND')
      *  BESTEMMELSES LAND  RUBR. 17A
     A            THBLK     R               REFFLD(LKA)
     A                                      COLHDG('LANDK TIL B.LAND')
      *  CONTAINER  RUBR. 19
     A            THKDC     R               REFFLD(KDC)
     A            THTMIN         1S 0       COLHDG('INLAND TR.MOD.')
     A            THTMGR         1S 0       COLHDG('GRENSE TR.MOD.')
     A            THVKB         13S 3       COLHDG('BRUTTOVEKT')
     A            THUCR         35A         COLHDG('UCR REFERANSE NR')
      *  CARRIER
     A            THTINC        17A         COLHDG('TIN-KODE TRANSPORTØR')
     A            THKNC     R               REFFLD(KN)
     A                                      COLHDG('Kundenr TRANSPORTØR')
     A            THNAC         30A         COLHDG('NAVN TRANSPORTØR')
     A            THTLC         20A         COLHDG('TRANSP. TEL.')
     A            THEMC         50A         COLHDG('TRANSP. EMAIL')
      *  SELGER
     A            THKNS     R               REFFLD(KN)
     A                                      COLHDG('Kundenr Selger')
     A            THTINS        17A         COLHDG('TIN-KODE SELGER')
     A            THNAS         30A         COLHDG('NAVN SELGER')
     A            THAD1S        30A         COLHDG('ADRESSE-1 SELGER')
     A            THPNS         17A         COLHDG('POSTNR SELGER')
     A            THPSS         24A         COLHDG('POSTSTED SELGER')
     A            THLKS          2A         COLHDG('LANDKODE SELGER')
     A            THCPS         30A         COLHDG('KONTAKTPERSON SELGER')
     A            THTLS         20A         COLHDG('SELGER TEL.')
     A            THEMS         50A         COLHDG('EMAIL K.PERS. SELGER')
      *  KJØPER
     A            THKNK     R               REFFLD(KN)
     A                                      COLHDG('Kundenr Kjøper')
     A            THTINK        17A         COLHDG('TIN-KODE KJØPER')
     A            THNAK         30A         COLHDG('NAVN KJØPER')
     A            THAD1K        30A         COLHDG('ADRESSE-1 KJØPER')
     A            THPNK         17A         COLHDG('POSTNR KJØPER')
     A            THPSK         24A         COLHDG('POSTSTED KJØPER')
     A            THLKK          2A         COLHDG('LANDKODE KJØPER')
      *  ADDITIONAL SUPPLY CHAIN ACTOR
     A            THROLE         3A         COLHDG('ROLE')
     A            THIDR         17A         COLHDG('ROLE ID.NR')
      *  TRANSPORT EQUIPMENT
     A            THCNR         17A         COLHDG('CONTAINE')
     A            THDANT         4S 0       COLHDG('ANTALL FORSEGLING')
      *   SEAL
     A            THDFKD        20A         COLHDG('FORSEGLING-ID KODE')
      *   GOODS REFERENCE
     A            THGINR         5S 0       COLHDG('GOODS ITEM NUMBER')
      *  LOCATION OF GOODS
     A            THLGTY         1A         COLHDG('TYPE')
     A            THLGID         1A         COLHDG('QUALIFIER OF IDENT.')
     A            THLGAU        35A         COLHDG('AUTORISATION NUMBER')
     A            THLGAI         4A         COLHDG('ADDITIONAL IDENT.')
     A            THLGUN        17A         COLHDG('UN LOCCODE')
      *   CUSTOMS OFFICE
     A            THLGTS         8A         COLHDG('LOC. TOLLSTED')
      *   GNSS
     A            THLGLA        17A         COLHDG('LOC. LATITUDE')
     A            THLGLO        17A         COLHDG('LOC. LONGITUDE')
      *   ECONOMIC OPERATOR
     A            THKNE     R               REFFLD(KN)
     A                                      COLHDG('Kundenr ØKONOMI OPR')
     A            THTINE        17A         COLHDG('TIN-KODE ØKONOMI OPR')
     A            THADE1        30A         COLHDG('ADR-1 ØKONOMI OPR')
     A            THPNE         17A         COLHDG('POSTNR ØKONOMI OPR')
     A            THPSE         24A         COLHDG('POSTSTED ØKONOMI OPR')
     A            THLKE          2A         COLHDG('LANDKODE ØKONOMI OPR')
     A            THADE2        17A         COLHDG('HUS.NR ØKONOMI OPR')
     A            THPNE2        17A         COLHDG('POSRNR HUS ØKONOMI')
     A            THLKE2         2A         COLHDG('LANDKODE HUS ØKONOMI')
     A            THCPE         30A         COLHDG('KONTAKTPERSON ØKONOM')
     A            THTLE         20A         COLHDG('TLF K.PERS. ØKONOMI')
     A            THEME         50A         COLHDG('EMAIL K.PERS. ØKONOM')
      *  TRANSPORTMÅTE  RUBR. 25
     A            THTRM     R               REFFLD(TRM)
      *  TRANSPORTMIDDEL VED AVGANG  RUBR. 18
     A            THTAID        35A         COLHDG('IDENTITET')
     A            THTALK    R               REFFLD(LKA)
     A                                      COLHDG('Landk Tr.middel')
      *  COUNTRY OF ROUTING OF CONSIGNMENT
     A            THLKR1         2A         COLHDG('LANDKODE 1')
     A            THLKR2         2A         COLHDG('LANDKODE 2')
     A            THLKR3         2A         COLHDG('LANDKODE 3')
     A            THLKR4         2A         COLHDG('LANDKODE 4')
     A            THLKR5         2A         COLHDG('LANDKODE 5')
     A            THLKR6         2A         COLHDG('LANDKODE 6')
      *  ACTIVE BORDER TRANSPORT MEANS
     A            THTGTS         8A         COLHDG('TOLLSTED VED GRENSE')
     A            THTRMG    R               REFFLD(TRM)
     A            THTGID        35A         COLHDG('IDENTITET')
     A            THTGLK    R               REFFLD(LKA)
     A                                      COLHDG('Landk Tr.middel')
     A            THTGCN        17A         COLHDG('CONVOYANCE REF.NR')
      *  PLACE OF LOADING
     A            THLSUN        17A         COLHDG('LASTESTED UN KODE')
     A            THLSLK         2A         COLHDG('LASTESTED LANDKODE')
     A            THLSNA        35A         COLHDG('LASTESTED NAVN')
      *  PLACE OF UNLOADING
     A            THLOUN        17A         COLHDG('LOSSESTED UN KODE')
     A            THLOLK         2A         COLHDG('LOSSESTED LANDKODE')
     A            THLONA        35A         COLHDG('LOSSESTED NAVN')
      *  PREVIOUS DOCUMENT
     A            THPDTY         4A         COLHDG('DOK. TYPEE')
     A            THPDRF        70A         COLHDG('DOK. REF.NR')
     A            THPDIN        35A         COLHDG('KOMPL. INFO.')
      *  SUPPORTING DOCUMENT
     A            THSDTY         4A         COLHDG('DOK. TYPEE')
     A            THSDRF        70A         COLHDG('DOK. REF.NR')
     A            THSDLN         5S 0       COLHDG('LINE ITEM NUMBER')
     A            THSDIN        35A         COLHDG('KOMPL. INFO.')
      *  TRANSPORT DOCUMENT
     A            THTDTY         4A         COLHDG('DOK. TYPEE')
     A            THTDRF        70A         COLHDG('DOK. REF.NR')
      *  ADDITIONAL DOCUMENT
     A            THADTY         4A         COLHDG('DOK. TYPEE')
     A            THADRF        70A         COLHDG('DOK. REF.NR')
      *  ADDITIONAL INFORMATION
     A            THAICD         5A         COLHDG('INFO. CODE')
     A            THAITX        70A         COLHDG('INFO. TEXT')
      *  TRANSPORT CHARGES
     A            THTRCH         1A         COLHDG('BETALINGSMÅTE')
      * RUBR. D/J
     A            THDDT          8S 0       COLHDG('Release date')
     A            THDKR          2A         COLHDG('Control result code')
     A            THDDTK         8S 0       COLHDG('Date of control')
     A            THDATS         8A         COLHDG('KRL. AVG.TOLLSTED')
     A            THDKAV        35A         COLHDG('KONTROLLERT AV')
     A            THDKTX       512A         COLHDG('Control text')
      * TILBAKE SENDES TIL
     A            THTSSD         8A         COLHDG('TILBAKE SENDES T.ST.')
     A            THTSN1        35A         COLHDG('TOLLSTED NAVN')
     A            THTSN2        35A         COLHDG('GATENAVN OG NR')
     A            THTSPN         9A         COLHDG('POSTNR')
     A            THTSPS        24A         COLHDG('POSTSTED')
     A            THTSLK         2A         COLHDG('LANDKODE')
     A            THTSSK         2A         COLHDG('SPRÅKKODE')
      * ANDRE
     A            THLSTL         4S 0       COLHDG('ANTALL LASTELISTE')
     A            THVPOS         5S 0       COLHDG('VAREPOST')
     A            THNTK     R               REFFLD(NTK)
     A            THDST     R               REFFLD(DST)
     A            THTARF        16          COLHDG('TARIFFØRSNAVN')
     A            THWS      R               REFFLD(WS)
      * TRANSISTTERINGSNUMMER OG GODKJENNINGSDATO
     A            THLRN         22A         COLHDG('LRN')
     A            THTRNR        18A         COLHDG('TRANSITTERINGSNR')
     A            THTRDT         8S 0       COLHDG('GODKJENNINGSDATO')
     A            THOMD          1S 0       COLHDG('OMDIRIGERING FORBUDT')
     A            THBIND       120A         COLHDG('BINDENDE REISERUTE')
     A            THTET          1S 0       COLHDG('TET RERUTEKSEMPLAR')
      * TVINN - VARIABLER
     A            THMF      R               REFFLD(MF)
     A            THMP      R               REFFLD(MP)
     A            THPKL     R               REFFLD(PK)
     A                                      COLHDG('Print ved linje ?')
     A            THKDV     R               REFFLD(KDV)
     A            TH0035    R               REFFLD(N0035)
      *
      * PRINTERSTYRING
     A            THNTTD         1S 0       COLHDG('ANTALL T-DOK')
     A            THNTLL         1S 0       COLHDG('ANTALL LASTELISTE')
     A            THFMLL         1A         COLHDG('TRYKK L.L.FORMULAR ?')
      * NØDPROSEDYRE NUMMERSERIE
     A            THNPNS         1A         COLHDG('NØDPROS. NR.SERIE')
      *
      * TVINN - VARIABLER
     A            S0004     R               REFFLD(N0004)
     A            S0010     R               REFFLD(N0010)
     A            S0035     R               REFFLD(N0035)
     A            S0026     R               REFFLD(N0026)
      *
     A            THEKST         1A         COLHDG('EKSTERNE DATA')
      *
     A          K THAVD
     A          K THTDN
