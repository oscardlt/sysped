     A                                      DSPSIZ(27 132 *DS4)
     A                                      PRINT
     A          R NOERASE
     A                                      ASSUME
     A                                  1  2' '
     A          R WINDOWSZ
     A                                      WINDOW(*DFT 20 110 *NOMSGLIN)
     A                                      OVERLAY
      *
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  4  1VALUES(' ' '1')
     A            SVPR_PR        4A  O  4  4
     A            SVPR_TX1     100A  O  4 10
     A            SVPR_TX2     100A  O  5 10
     A            SVPR_TX3     100A  O  6 10
     A            SVPR_TX4     100A  O  7 10
     A            SVPR_TX5     100A  O  8 10
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0004)
     A                                      SFLPAG(0003)
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A                                      CA03(03 'Forrige')
     A                                      CA05(05 'Frisk')
     A                                      CA12(12 'Forrige')
     A                                  1  1'SVT006D'
     A                                      COLOR(BLU)
     A                                  1 36'Søk och plocka Førfarandekoder'
     A                                      COLOR(WHT)
     A                                  2  1'Vælj option:'
     A                                      COLOR(BLU)
     A                                  2 16'1=Plocka'
     A                                      COLOR(BLU)
     A                                  3  1'Op Kod   Beskrivning     '
     A                                      COLOR(WHT)
     A            ZRECNO         4S 0O  3 25SFLRCDNBR
     A                                      DSPATR(ND)
      *
     A          R BUNN
     A                                      WINDOW(WINDOWSZ)
     A                                      SLNO(20)
     A                                      CLRL(01)
     A                                      OVERLAY
     A            MSGNR          7A  P
     A N30                              1  2'F3/F12=Avbryt   F5=Førnya'
     A                                      COLOR(BLU)
     A  30        MSGTXT        38A  O  1  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
      *
     A          R NYWRK
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A                                      CA03(03 'Forrige')
     A                                      CA05(05 'Frisk')
     A                                      CA12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SVT006D'
     A                                      COLOR(BLU)
     A                                  1 36'Søk och plocka Førfarandekoder'
     A                                      COLOR(WHT)
     A                                  2  2'Velg option:  1 = Plocka'
     A                                      COLOR(BLU)
     A                                  3  2'Op Kod  Beskrivning'
     A                                      COLOR(WHT)
     A                                 11  2'F3/F12=Avbryt   F5=Førnya'
     A                                      COLOR(BLU)
