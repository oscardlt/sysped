     FSAD105FS  CF   E             WORKSTN
     F                                     SFILE(RECB:ZRECNO)
     FKODTSIA   UF a E           K DISK
     FKODTSI    IF   E           K DISK
     FFIRM      IF   E           K DISK
     FCUNDL01   IF   E           K DISK
     FKODTA     IF   E           K DISK
     D                 DS
     D  KSINAV                 1     16
     D  SFSIGN                 1     10
     D                 DS
     D  KNAVN                  1     30
     D  SFKUNDE                1     15
     D                 DS
     D  KOANVN                 1     33
     D  SFAVD                  1     15
      *//////////////
      *  Hodelogikk /
      *//////////////
     C                   EXSR      SRA
     C                   EXSR      SRB
     C                   MOVE      '1'           *INLR
      *////////////////////////////////////////////////////////////
      *  Initiering                                           SRA /
      *////////////////////////////////////////////////////////////
     C     SRA           BEGSR
      *
     C     *ENTRY        PLIST
     C                   PARM                    UUSIG             3
     C                   PARM                    ALLWS             1
     C                   PARM                    UPDATE            1
      *__________________
      * Definiere Nøkkler
      *""""""""""""""""""
     C     KEY           KLIST
     C                   KFLD                    WSSIG
     C                   KFLD                    WSKN
     C                   KFLD                    WSAVD
     C     KEYGM         KLIST
     C                   KFLD                    SIASIG
     C                   KFLD                    SIAKN
     C                   KFLD                    SIAAVD
     C     KunKey        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    WSKN
     C     SigKey        KLIST
     C                   KFLD                    KSIUNI
     C                   KFLD                    WSSIG
     C                   move      'SI'          KSIUNI
     C     KoaKey        KLIST
     C                   KFLD                    KOAUNI
     C                   KFLD                    WSAVD
     C                   move      'A'           KOAUNI
      *
     C     *LIKE         DEFINE    ZRECNO        XRECNO
     C                   READ      FIRM                                   55
     C* 'J' I ALLWS  : SKAL SE ALLE USER-ID
     C* 'N' I ALLWS  : LÅST TIL EGEN USER-ID
     C* ' ' I ALLWS  : DEFAULT VIS EGEN / KAN OVERSTYRE TIL ALLE...
     C                   MOVE      UUSIG         W2SIG
     C                   MOVE      UUSIG         WSSIG
     C* Endring av USER tillatt ??
     C     ALLWS         COMP      'N'                                    51     *PROTECT
     C* Update tillatt ??
     C     UPDATE        COMP      'J'                                5050       *NONDISP
      *_____________
      * Init av felt
      *"""""""""""""
      *___________________
      * Definiere Workfelt
      *"""""""""""""""""""
     C     *LIKE         DEFINE    W2SIG         WWSIG
     C     *LIKE         DEFINE    W2AVD         WWAVD
     C     *LIKE         DEFINE    W2KN          WWKN
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Bearbeide Formater                                   SRB /
      *////////////////////////////////////////////////////////////
     C     SRB           BEGSR
      *______________
      * Tømme Subfile
      *""""""""""""""
      *
     C     START         TAG
      * bevar sist utførte søk
     C                   MOVE      W2SIG         WWSIG
     C                   MOVE      W2AVD         WWAVD
     C                   MOVE      W2KN          WWKN
      *
     C                   MOVE      '1'           *IN93
     C                   WRITE     RECC
     C                   MOVEA     '000'         *IN(93)
     C                   Z-ADD     *ZERO         ZRECNO
     C     W2SIG         COMP      *BLANKS                                56     *LES VIA SIGN
     C     W2SIG         SETLL     KODTSIA
      *_____________
      * Fyll Subfile (med alle poster uavhengig av linjer pr side)
      *"""""""""""""
     C  N56W2SIG         READE     KODTSIA                                94
     C   56              READ      KODTSIA                                94
     C     *IN94         DOWEQ     '0'
      *
     C     W2AVD         IFNE      *ZEROS
     C     W2AVD         ANDNE     SIAAVD
     C                   GOTO      NESTE
     C                   END
     C     W2KN          IFNE      *ZEROS
     C     W2KN          ANDNE     SIAKN
     C                   GOTO      NESTE
     C                   END
      *
     C                   MOVE      *BLANKS       KSINAV
     C                   MOVE      *BLANKS       KNAVN
     C                   MOVE      *BLANKS       KOANVN
     c                   move      SIASIG        WSSIG
     C     SigKey        CHAIN     KODTSI                             33
     c                   move      SIAKN         WSKN
     C     KunKey        CHAIN     CUNDL01                            33
     c                   move      SIAAVD        WSAVD
     C     KoaKey        CHAIN     KODTA                              33

     C                   ADD       1             ZRECNO
     C                   WRITE     RECB
      *
     C     NESTE         TAG
     C  N56W2SIG         READE     KODTSIA                                94
     C   56              READ      KODTSIA                                94
     C  N94              END
      *
     C                   MOVE      ZRECNO        XRECNO
      *
     C     ZRECNO        IFEQ      0
     C                   MOVE      'JOV1589'     ZMSGNO
     C                   MOVE      '1'           *IN30
     C                   WRITE     RECØ
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     5             POSNBR
     C   51              Z-ADD     17            POSNBR
     C                   EXFMT     RECA
     C                   MOVE      '0'           *IN30
     C     *IN06         CABEQ     '1'           LAGNY
     C     *IN03         CABEQ     '1'           SRB9
     C                   GOTO      START
     C                   END
     C                   Z-ADD     1             ZRECNO
      *_______________
      * Behandle bilde
     C***********************************************
     C     SRB4          TAG
     C***********************************************
      *
     C                   MOVEA     '11'          *IN(91)
     C  N30              MOVE      'SY00000'     ZMSGNO
     C                   WRITE     RECØ
     C                   WRITE     RECD
     C                   Z-ADD     7             LINNBR
     C                   Z-ADD     2             POSNBR
     C                   EXFMT     RECC
      *
     C     *IN12         CABEQ     '1'           SRB9
     C     *IN03         IFEQ      '0'
      *
     C                   MOVEA     '00'          *IN(91)
      *_____________
      * F5=Frisk opp
      *"""""""""""""
     C     *IN05         CABEQ     '1'           START
      *
     C     LAGNY         TAG
     c     *in06         ifeq      *on
     C                   Z-ADD     23            LINNBR
     C                   Z-ADD     23            POSNBR
     c                   move      *zeros        WSKN
     c                   move      *zeros        WSAVD
     c                   move      'N'           TypNyCh           1
     C                   exsr      NYCHG
     C                   endif
      *_________________
      * Nytt søk/utvalg
      *"""""""""""""""""
     C     W2SIG         IFNE      WWSIG
     C     W2AVD         ORNE      WWAVD
     C     W2KN          ORNE      WWKN
     C                   GOTO      START
     C                   END
      *_______
      * Valg ?
      *"""""""
     C                   MOVE      '0'           *IN52
     C                   MOVE      '0'           *IN30
     C                   MOVE      '0'           *IN95
     C     *IN52         DOWEQ     '0'
      *
     C                   READC     RECB                                   52
     C     *IN52         IFEQ      '0'
     C                   EXSR      ENDRE
     C                   END
      *
     C  N30
     CANN52              END
      *
     C     *IN03         CABEQ     '1'           SRB9
     C     *IN95         CABEQ     '1'           START
     C     *IN95         CABEQ     '0'           SRB4
     C                   END
      *
     C     SRB9          ENDSR
     C***************************************************
     C     ENDRE         BEGSR
     C***************************************************
     C*
     c                   move      SIASIG        WSSIG
     c                   move      SIAKN         WSKN
     c                   move      SIAAVD        WSAVD
     C     WSNR          IFEQ      '4'
     C     UPDATE        ANDEQ     'J'
     C     SIASIG        IFNE      '*D*'
     C     KEY           CHAIN     KODTSIA                            55
     C                   DELETE    KODTSIA
     C                   MOVE      *BLANKS       SIASIG
     C                   MOVEL     '*D*'         SIASIG
     C                   MOVE      *zeros        SIAAVD
     C                   MOVE      *zeros        SIAKN
     C                   MOVE      ' '           WSNR
     C                   UPDATE    RECB
     C                   END
     C                   END
     C     WSNR          IFEQ      '2'
     C     UPDATE        ANDEQ     'J'
     C     SIASIG        IFNE      '*D*'
     C                   Z-ADD     23            LINNBR
     C                   Z-ADD     23            POSNBR
     c                   move      'C'           TypNyCh           1
     C                   exsr      NYCHG
     C                   END
     C                   END
     C                   ENDSR
     C***************************************************
     C     NYCHG         BEGSR
     C***************************************************
     C*
     C     feil          tag
     C                   EXFMT     RECCH
     C                   setoff                                       30
     C                   setoff                                       707172
     C     *IN12         cabeq     '1'           UtNyCh
      *
     C     KEY           CHAIN(N)  KODTSIA                            55
     c     *in55         ifeq      *off
     C                   seton                                        707172
     c                   seton                                        30
     C                   Z-ADD     23            LINNBR
     C                   Z-ADD     23            POSNBR
     C                   MOVE      'JOV1587'     ZMSGNO
     c                   goto      feil
     c                   endif
      *
     C     SigKey        CHAIN     KODTSI                             70
     C     *IN70         IFEQ      '1'
     c                   seton                                        30
     C                   Z-ADD     23            LINNBR
     C                   Z-ADD     06            POSNBR
     C                   MOVE      'SPS0001'     ZMSGNO
     c                   goto      feil
     C                   END
      *
     C     WSKN          IFNE      *zeros
     C     KunKey        CHAIN     CUNDL01                            71
     C     *IN71         IFEQ      '1'
     c                   seton                                        30
     C                   Z-ADD     23            LINNBR
     C                   Z-ADD     23            POSNBR
     C                   MOVE      'SPK0001'     ZMSGNO
     c                   goto      feil
     C                   END
     C                   END
      *
     C     WSAVD         IFNE      *zeros
     C     KoaKey        CHAIN     KODTA                              72
     C     *IN72         IFEQ      '1'
     c                   seton                                        30
     C                   Z-ADD     23            LINNBR
     C                   Z-ADD     50            POSNBR
     C                   MOVE      'SPA0001'     ZMSGNO
     c                   goto      feil
     C                   END
     C                   END
      *
     C     WSKN          IFeq      *zeros
     C     WSAVD         andeq     *zeros
     c                   seton                                        307172
     C                   Z-ADD     23            LINNBR
     C                   Z-ADD     23            POSNBR
     C                   MOVE      'JOV1588'     ZMSGNO
     c                   goto      feil
     C                   END
      *
     C     WSAVD         IFNE      *zeros
     C     KoaKey        CHAIN     KODTA                              72
     C     *IN72         IFEQ      '1'
     c                   seton                                        30
     C                   Z-ADD     23            LINNBR
     C                   Z-ADD     50            POSNBR
     C                   MOVE      'SPA0001'     ZMSGNO
     c                   goto      feil
     c                   endif
     c                   endif
      *
      *
     c     TypNyCh       ifeq      'N'
     c                   move      WSSIG         SIASIG
     c                   move      WSKN          SIAKN
     c                   move      WSAVD         SIAAVD
     C                   ADD       1             XRECNO
     C                   z-add     XRECNO        ZRECNO
     C                   WRITE     KODTSIAR
     C                   write     RECB
     C                   else
      * Endring - alle felt i KEY - slett gm  / lag ny
     C     KEYGM         CHAIN     KODTSIA                            55
     c  N55              delete    KODTSIAR
     c                   move      WSSIG         SIASIG
     c                   move      WSKN          SIAKN
     c                   move      WSAVD         SIAAVD
     C                   WRITE     KODTSIAR
     C                   MOVE      ' '           WSNR
     C                   update    RECB
     C                   endif
      *
     C     UtNyCh        ENDSR
