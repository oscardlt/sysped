      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/TJGE11R MODULE(*LIBL/TJGE11R *LIBL/INCGI)
CRTPGM*        ACTGRP(*NEW) AUT(*USE)
      *
      *********************************************************************
11XXX * 01 JOV PTF12 Ikke hent data dersom oppdrag er 0
      *********************************************************************
      /copy SYSPEDS/qrpgsrcpy,hspecs
      /copy SYSPEDS/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FFREETXT1  IF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy SYSPEDS/qrpgsrcpy,prototypeb
      /copy SYSPEDS/qrpgsrcpy,usec
      /copy SYSPEDS/qrpgsrcpy,variables3
      *
      * Varible for
     D WSUSER          S             10
     D AVD             S              4
     D AVDN            S              4S 0
     D OPD             S              7
     D OPDN            S              7S 0
     D KOD             S              1
     D JSONstring      S          32000
     D Error           S            150
     D AntLest         S              9  0
      *get input-string
      /copy syspeds/qrpgsrcpy,prolog3

      * Parse input
     C                   exsr      Parse
      * Open files etc
     C                   EXSR      INIT
      *
     c                   callp     gethtmlifs(
     C                             '/syspeddev/html/JSON.html':
     C                             '/CGITAG/')
     C                   callp     wrtsection('top')
      *
      * ............................................................................................
      * skriv JSON-Object start..(alltid '{' + x ant param. m/komma mellom (IKKE komma etter siste))
      * ............................................................................................
      *
      /free
        JSONstring='{'
        +'¬user¬ : ¬'+%trim(WSUSER)+'¬, '
        +'¬avd¬ : ¬'+%trim(%editc(AVDN:'Z'))+'¬, '
        +'¬opd¬ : ¬'+%trim(%editc(OPDN:'Z'))+'¬, '
        +'¬kod¬ : ¬'+%trim(KOD)+'¬, '
        +'¬errMsg¬ : ¬'+%trim(Error)+'¬, ';
      /end-free
      * JSONfix escaper " i tekst,  for deretter å bytte ¬->"
     c                   call      'JSONFIX'
     c                   parm                    JSONstring
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')
      *
      * ............................................................................................
      * Skriv JSON-LISTE Start (en liste er EN parameter(fra [ til   )
      * ............................................................................................
     c                   eval      JSONstring ='"freetextlistA" : ['
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')
      *
      * Liste Ærende
      *
     c     Error         ifeq      *blanks
     C     FREEKEY       SETLL     FREETXT1
     C     FREEKEY       READE     FREETXT1                               55
     C     *IN55         DOWEQ     '0'
     C     KOD           IFEQ      *BLANKS
     C     FRTKOD        IFEQ      'A'
     C     FRTKOD        OREQ      '1'
     C                   GOTO      ALES
     C                   ENDIF
     C                   ENDIF
     C     KOD           IFEQ      '*'
     C                   GOTO      ALES
     C                   ENDIF
     C     KOD           IFEQ      'b'
     C     FRTKOD        ANDEQ     *BLANKS
     C                   GOTO      ALES
     C                   ENDIF
     C     KOD           IFNE      *BLANKS
     C     FRTKOD        CABEQ     KOD           ALES
     C                   ENDIF

     C                   GOTO      NYLES

     C     ALES          TAG
     C                   Add       1             AntLest
      * ............................................................................................
      * Skriv en JSON-Array-linje pr menypunkt - komma før hvert nytt element
      * ............................................................................................
      /free
       if AntLest=1;
          JSONstring=*blanks;
          else;
          JSONstring=', ';
          endif;
       JSONstring=%trim(JSONstring)+'{'
          +'¬frtavd¬ : ¬'+%trim(%editc(frtavd:'Z'))+'¬, '
          +'¬frtopd¬ : ¬'+%trim(%editc(frtopd:'Z'))+'¬, '
          +'¬frtdt¬ : ¬'+%trim(%editc(frtdt:'Z'))+'¬, '
          +'¬frtli¬ : ¬'+%trim(%editc(frtli:'Z'))+'¬, '
          +'¬frttxt¬ : ¬'+%trim(frttxt)+'¬, '
          +'¬frtkod¬ : ¬'+%trim(frtkod)+'¬, '
          +'¬frtfak¬ : ¬'+%trim(%editc(frtfak:'Z'))+'¬, '
          +'¬frtles¬ : ¬'+%trim(frtles)+'¬}';
      /end-free
     C                   call      'JSONFIX'
     C                   parm                    JSONstring
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')

     C     NYLES         TAG
     C     FREEKEY       READE     FREETXT1                               55
     C                   ENDDO
     c                   ENDIF
      * ............................................................................................
      * Skriv JSON-Liste/Array  Avslutning
      * ............................................................................................
     c                   eval      JSONstring =']'
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')
      * ............................................................................................
      * Skriv JSON-string Avsluttning
      * ............................................................................................
     c                   eval      JSONstring ='}'
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')
      *
      * Quit
     C                   exsr      Exit


      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Lukk fil
     C                   CLOSE     BRIDF
     C  N50              CLOSE     FREETXT1

      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   MOVE      *ON           *INLR
     C                   return
     C                   endsr
      *
      *********************************************************
     C     Parse         Begsr
      *********************************************************
      * Get input
     C                   EVAL      WSUSER = zhbgetvar('USER')
     C                   EVAL      AVD    = zhbgetvar('AVD')
     C                   EVAL      AVDN   = c2n2(AVD)
     C                   EVAL      OPD    = zhbgetvar('OPD')
     C                   EVAL      OPDN = c2n2(OPD)
     C                   EVAL      KOD    = zhbgetvar('KOD')
     c                   endsr
      *
      *********************************************************
     C     INIT          Begsr
      *********************************************************
     C                   OPEN      BRIDF
      * Test innlogging
     C     WSUSER        CHAIN     BRIDF                              50
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C                   call      BILIBL
     C                   end
      *
     C     FREEKEY       KLIST
     C                   KFLD                    AVDN
     C                   KFLD                    OPDN

     C                   Z-ADD     1             X                 3 0

     C   50              eval      Error = 'Invalid userID'
     C  N50              OPEN      FREETXT1

N01  C                   if        Error = *blanks and
N01  C                             OPDN = *zeros
N01  C                   eval      Error = 'Dpt.no/Order no must be given.'
N01  C                   endif

     c                   endsr
