     A*%%TS  SD  20170321  105558  JOVO        REL-V7R1M0  5770-WDS
N01  A* Manually Created -> 'S=Send,F=Ferd,J=M:'
N02  A* F10= PÅ TUR-NIVÅ
N03  A* SKJERMBREDDE 132
N16   * MSGTXT 120 LANG /BRYTTET EN LINJE NE I BUNN/F13 OVERGANG TIL KOLLIIDS
     A*%%EC
     A                                      DSPSIZ(27 132 *DS4)
     A                                      REF(*LIBL/TRACKF TRACKFR)
     A                                      PRINT
     A                                      HLPDOC('STD' 'HELPNU' 'HLP400')
     A                                      HELP
     A                                      ALTHELP(CA01)
      * "NOERASE" MÅ HA FOR Å IKKE TØMME BILDE SOM MAN KOMME FRA
     A          R NOERASE
     A                                      ASSUME
     A                                  1  2' '
     A* MAN KUN DEFINNERE VINDU PÅ REKORDNIVÅ
S03  A*         R WINDOWSZ
S03  A*                                     OVERLAY
S03  A*                                     WINDOW(*DFT 21 76 *NOMSGLIN)
     A          R SUBFIL                    SFL
     A            SFAVD          4S 0H
     A            SFTEXL        70   H
     A            SFOPD          7S 0H
     A            SFTUR          8S 0H
     A            SFDATE         8S 0H
     A            SFTIME         6S 0H
     A            SFRECNO        9S 0H
     A            WSNR           1A  I  6  2VALUES(' ' '5' '4')
     A            SFFBNR    R        O  6  4REFFLD(TRACKFR/TTFBNR TRACKF)
     A                                      EDTCDE(4)
     A            SFACTI    R        O  6  8REFFLD(TRACKFR/TTACTI TRACKF)
     A*           SFDATX         6Y 0O  6 12EDTWRD('  .  .  ')
     A*           SFTIMX         4Y 0O  6 21EDTWRD('  :  ')
     A            SFDATX         6Y 0O  6 12EDTWRD('0  .  .  ')
     A            SFTIMX         4Y 0O  6 21EDTWRD('0  :  ')
S03  A*           SFTEXX        37A  O  6 27COLOR(WHT)
N03  A            SFTEXX        70A  O  6 27COLOR(WHT)
E03  A            SFUSEX        10A  O  6 99
N03  A            SFDEPO        10A  O  6110
N03  A            SFNAME        10A  O  6121
N03  A            SFMANU         1A  O  6132
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A*%%TS  SD  20141027  085542  SVEIN       REL-V7R1M0  5770-WDS
S11  A*                                     SFLSIZ(0018)
S11  A*                                     SFLPAG(0017)
N11  A                                      SFLSIZ(0021)
N11  A                                      SFLPAG(0020)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A                                      CF02(02 'BYTT SPRÅK')
     A                                      CA03(03 'Avslutt')
     A                                      CF04(04 'SØKING')
     A                                      CA05(05 'Frisk')
N15  A                                      CF20(20 'OPD/TUR')
     A                                      CF08(08 'SØK/FILTER')
     A  50                                  CF10(10 'NY PÅ TUR')
     A                                      CA12(12 'Forrige')
N16  A                                      CF13(13 'SE KOLLI')
     A                                      CF14(14 'BYTT TIL DUPP')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYTT01'
     A                                      COLOR(BLU)
     A                                  1 13MSGCON(030 SHU0506 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 92DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  1101TIME
     A                                      COLOR(BLU)
     A                                  2  2MSGCON(012 SHU1313 *LIBL/SYMSGF)
     A            UAVD0          4Y 0O  2 15EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  2 20'/'
     A            UOPD0          7Y 0O  2 22EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  2 30'/'
     A            UTUR           8Y 0O  2 32EDTCDE(4)
     A                                      COLOR(WHT)
     A            ZRECNO         4S 0O  2 70SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  3  2'Velg Opt: 5=Se på '
     A                                      COLOR(BLU)
     A                                  3 22'1=Opprett manuelt 4=Slett(kun manu-
     A                                      elle)'
     A                                      COLOR(BLU)
     A                                  4  1MSGCON(130 JOV1292 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            W2NR           1A  B  5  2VALUES(' ' '1')
     A                                      DSPATR(UL)
     A            W2FBNR         3A  B  5  4DSPATR(UL)
     A            W2ACTI         3A  B  5  8DSPATR(UL)
     A            W2DATE         6Y 0B  5 13DSPATR(UL)
     A                                      EDTCDE(4)
     A            W2TIME         4Y 0B  5 21DSPATR(UL)
     A                                      EDTCDE(4)
     A            OP1            2A  O  5 27
     A            OP1AV          4S 0O  5 30
     A            OP1OP          7S 0O  5 35
     A                                  5 43'/'
     A            OP2            2A  O  5 45
     A            OP2AV          4S 0O  5 48
     A            OP2OP          7S 0O  5 53
     A          R BUNN
     A*%%TS  SD  20141027  085542  SVEIN       REL-V7R1M0  5770-WDS
E11  A                                      SLNO(26)
     A                                      CLRL(02)
     A                                      OVERLAY
S03  A* *DS3                                WINDOW(WINDOWSZ)
S03  A* *DS4                                WINDOW(WINDOWSZ)
     A            MSGNR          7A  P
N16   * INGEN MERKING MEN MSGTXT FLYTTE TIL LIN 2 (N30 ENDRET TILSVARENDE )
     A                                  1  2MSGCON(029 YBC0237 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  1 48MSGCON(015 JOV1290 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
S11  A* 50                              1 51MSGCON(022 JOV1547 *LIBL/SYMSGF)
S11  A*                                     COLOR(BLU)
E11  A                                  1 32MSGCON(015 SHU1311 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
E11  A                                  1 64MSGCON(024 SHU1343 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
N11  A  50                              1 90MSGCON(022 JOV1547 *LIBL/SYMSGF)
N11  A                                      COLOR(BLU)
E16  A N30                              2  2MSGCON(018 SHU2018 *LIBL/SYMSGF)
N15  A                                      COLOR(BLU)
E16  A  30        MSGTXT       120A  O  2  2MSGID(&MSGNR SYMSGF)
E16  A                                      COLOR(RED)
S03  A* *DS3                                WINDOW(WINDOWSZ)
S03  A* *DS4                                WINDOW(WINDOWSZ)
     A          R NYWRK
     A*%%TS  SD  20170321  105558  JOVO        REL-V7R1M0  5770-WDS
     A                                      OVERLAY
     A                                      CA03(03 'Forrige')
     A                                      CF04(04 'SØKING')
     A                                      CF08(08 'SØK/FILTER')
     A  50                                  CF10(10 'NY PÅ TUR')
     A                                      CA05(05 'Frisk')
N15  A                                      CF20(20 'OPD/TUR')
     A                                      CA12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1  2'SYTT01'
     A                                      COLOR(BLU)
     A                                  1 13MSGCON(030 SHU0506 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1115DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  1124TIME
     A                                      COLOR(BLU)
     A                                  2  2MSGCON(010 YBC0223 *LIBL/SYMSGF)
     A            UAVD           4Y 0O  2 13EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  2 18'/'
     A            UOPD           7Y 0O  2 20EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  3  2'Velg Opt: 5=Se på '
     A                                      COLOR(BLU)
     A                                  3 22'1=Opprett manuelt 4=Slett(kun manu-
     A                                      elle)'
     A                                      COLOR(BLU)
     A                                  4  1MSGCON(130 JOV1292 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            W2NR           1A  B  5  2VALUES(' ' '1')
     A                                      DSPATR(UL)
     A            W2FBNR         3A  B  5  4DSPATR(UL)
     A            W2ACTI         3A  B  5  8DSPATR(UL)
     A            W2DATE         6Y 0B  5 13DSPATR(UL)
     A                                      EDTCDE(4)
E11  A N30                             26  2MSGCON(029 YBC0237 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
E11  A  30        MSGTXT       120A  O 26  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
E11  A N30                             26 34MSGCON(015 JOV1290 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
S11  A* 50                             26 51MSGCON(022 JOV1547 *LIBL/SYMSGF)
S11  A*                                     COLOR(BLU)
E11  A N30                             26 32MSGCON(015 SHU1311 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
E11  A                                 26 64MSGCON(024 SHU1343 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
N11  A  50                             26 90MSGCON(022 JOV1547 *LIBL/SYMSGF)
N11  A                                      COLOR(BLU)
N15  A                                 27  2MSGCON(018 SHU2018 *LIBL/SYMSGF)
N15  A                                      COLOR(BLU)
     A            W2TIME         4Y 0B  5 21DSPATR(UL)
     A                                      EDTCDE(4)
     A          R VARELI
     A*%%TS  SD  20141023  105742  JOVO        REL-V7R1M0  5770-WDS
S03  A* *DS3                                WINDOW(WINDOWSZ)
S03  A* *DS4                                WINDOW(WINDOWSZ)
S11  A*                                     SLNO(16)
S11  A*                                     CLRL(11)
N11  A                                      SLNO(18)
N11  A                                      CLRL(09)
     A                                      OVERLAY
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF04(04 'SØK')
     A                                      CA12(12 'Forrige')
     A            MSGNR          7A  P
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'Se på detaljer Track & Tracs / Hen-
     A                                      delsesloggpost  '
     A                                      COLOR(BLU)
     A                                  2  2'F.b.nr:'
     A            TTFBNR    R        B  2 10EDTCDE(4)
     A                                      COLOR(WHT)
     A  86                                  DSPATR(RI)
     A                                  2 14'Kode:'
     A            TTACTI    R        B  2 20COLOR(WHT)
     A  89                                  DSPATR(RI)
     A            KODTXT        30A  O  2 24COLOR(WHT)
     A                                  2 55'Grp:'
     A            KODGRP         3A  O  2 60COLOR(WHT)
     A                                  2 64'Vis WEB:'
     A            KODWEB         1A  O  2 74COLOR(WHT)
     A                                  3  2'Hendelsestidspkt:'
     A            TTDATE    R        B  3 20EDTWRD('    .  .  ')
     A                                      COLOR(WHT)
     A  87                                  DSPATR(RI)
     A                                  3 31'Kl:'
     A            TTTIME    R        B  3 35EDTWRD('0  :  :  ')
     A                                      COLOR(WHT)
     A  88                                  DSPATR(RI)
s01  A*                                 3 55'Manually created.:'
     A                                  3 45'Status:S=Send,F=Ferdig,J=Man'
     A            TTMANU    R        B  3 74COLOR(WHT)
     A                                  5  2'EN'
     A            TTTEXT    R        B  5  5COLOR(WHT)
     A                                      CHECK(LC)
     A                                  4  2'NO'
     A            TTTEXL    R        B  4  5COLOR(WHT)
     A                                      CHECK(LC)
     A                                  6  2'Event code:'
     A            TTEDEV    R        B  6 14COLOR(WHT)
     A                                  6 20'Reason code:'
     A            TTEDRE    R        B  6 33COLOR(WHT)
     A                                  6 37'Depot/term:'
     A            TTDEPO    R        B  6 49COLOR(WHT)
     A                                  6 60'Name'
     A            TTNAME    R        B  6 65COLOR(WHT)
     A                                  7  2'Loggføringstid..:'
     A            TTDATL    R        B  7 20EDTWRD('    .  .  ')
     A                                      COLOR(BLU)
     A                                      DSPATR(PR)
     A                                  7 31'Kl:'
     A            TTTIML    R        B  7 35EDTWRD('0  :  :  ')
     A                                      COLOR(BLU)
     A                                      DSPATR(PR)
     A                                  7 44'Av bruker ID:'
     A            TTUSER    R        B  7 58COLOR(BLU)
     A                                      DSPATR(PR)
E16  A  30        MSGTXT       120A  O  8  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                              8 44MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A          R FILTERB
S03  A*                                     WINDOW(WINDOWSZ)
     A                                      SLNO(03)
     A                                      CLRL(21)
     A                                      OVERLAY
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CA12(12 'Forrige')
     A            MSGNR          7A  P
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  2  6'Filtrer ut poster. Ta med kun ....-
     A                                      .....           '
     A                                      COLOR(WHT)
     A                                  4  6'Koder.....'
     A            WFKO01         3   B  4 18
     A            WFKO02         3   B  4 22
     A            WFKO03         3   B  4 26
     A            WFKO04         3   B  4 30
     A            WFKO05         3   B  4 34
     A            WFKO06         3   B  4 38
     A            WFKO07         3   B  4 42
     A            WFKO08         3   B  4 46
     A            WFKO09         3   B  4 50
     A            WFKO10         3   B  4 54
     A                                  6  6'Grupper...'
     A            WFGR01         3   B  6 18
     A            WFGR02         3   B  6 22
     A            WFGR03         3   B  6 26
     A                                  8  6'Fraktb.nr.'
     A            WFFBNR         3   B  8 18
     A                                  8 22'(angi 001-999, Blank=ALT, 000=Kun -
     A                                      oppdrag)'
     A                                 10  6'F.o.m'
     A                                 10 12'dato'
     A            WFDATE         6Y 0B 10 18EDTCDE(4)
     A                                 10 26'(Hendelsesdato) '
     A                                 12  6'F.o.m'
     A                                 12 12'dato'
     A            WFDATL         6Y 0B 12 18EDTCDE(4)
     A                                 12 26'(Loggføringsdato'
     A                                 14  6'Intern/eks'
     A            WFINEX         1   B 14 18
     A                                 14 22'(Blank=ALT, E=Kun eksterne(WEB-kod-
     A                                      e) I=Kun int.)'
E120 A  30        MSGTXT       120A  O 18  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                             18 44MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
