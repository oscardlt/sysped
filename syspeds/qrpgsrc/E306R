********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10172 * 01 SH  PTF10 norsk text til Track and trace
********************************************************************
FMAAS * UTENLANDSK DISTRIB SORTERT P≈ LK/POSTNR/MOTTAKER (DOKUF99)
FMAAS * HVIS AGENT/TYPE ER VENLO OG LAND ER DE, SEND TIL FMAAS NETTETAL
FMAAS *       I TILLEGG TIL F.MAAS VENLO -(MERKES DA MED "W" I STATUS
FMAAS *       AV PROG E321R_95A - EKSTRA LESELOOP HER P≈ STATUS "W")
     FHEAD14    IF   E           K DISK
FMAASFDOKUF99   IF   E           K DISK
     FECMSG     IF   E           K DISK
     FFIRM      IF   E             DISK
     FEDIC      UF   E             DISK
     FEUNH      O  A E             DISK
     FEDIM      O  A E             DISK
     FTRACKF    O  A E             DISK
      *___________________________
      * Finner username
      *"""""""""""""""""""""""""""
     D                SDS
     D  ZJOB                 244    253
     D  ZUSER                254    263
     D                 DS
     D  XULTID                 1     14  0
     D  XULTM                  1      6  0
     D  XULDD                  7      8  0
     D  XULMM                  9     10  0
     D  XUL≈≈                 11     14  0
     D                 DS
     D  UL≈≈                   1      4  0
     D  ULMM                   5      6  0
     D  ULDD                   7      8  0
     D  ULDT                   1      8  0
     D                 DS
     D  WTIME                  1     14  0
     D  WTID                   1      6  0
     D  WDD                    7      8  0
     D  WMM                    9     10  0
     D  W≈≈                   11     14  0
     D                 DS
     D  WDATO                  1      8  0
     D  W≈                     1      4  0
     D  WM                     5      6  0
     D  WD                     7      8  0
     D                 DS
     D  W1004                  1     12
     D  WUAVD                  1      4  0
     D  WUPRO                  5     12  0
     D  WUKNA                 13     20  0
     D  WUMN                  21     29  0
     D  WUMNA                 21     29
FMAASD                 DS
FMAASD  DFADOT                 1     30
FMAASD  WSAORD                 1     15
FMAASD  WSTJTY                16     17
FMAASD                 DS
FMAASD  DFADU                  1     26
FMAASD  DSLK                   1      2
      * Track/Trace-tekst
     D TTXED3          C                   CONST('Distribution order -
     D                                     electronically sent ')
n01  D TTXED3L         C                   CONST('Distribusjonsordre -
n01  D                                     elektronisk sendt ')
      */////////////
      * MAIN LOGIC /
      */////////////
     C                   EXSR      SRINIT
     C                   EXSR      SRA
     C     *IN41         CASEQ     '1'           SRB0
     C                   END
FMAASC     VENLDE        IFEQ      'J'
FMAASC                   MOVE      WUKNA         WUVENL            8 0
FMAAS * HENT - VERDIER FOR NETTETAL FOR UNH-SEGMENT
FMAASC                   Z-ADD     10021         WUKNA                             FM NETTE.
FMAASC     ECMKEY        CHAIN     ECMSG                              51
FMAASC     *IN51         IFEQ      '0'
FMAAS * LES P≈ OPPDRAG TILH. AGENT VENLO, MEN N≈ FR.BR.STAT. "W"
FMAASC                   MOVE      WUVENL        WUKNA                             FM VENLO.
FMAASC                   MOVE      'W'           WRI
FMAASC                   EXSR      SRB0
FMAASC                   END
FMAASC                   END
     C                   MOVE      '1'           *INLR
      *////////////////////////////////////////////////////////////
      * SR for init                                        SRINIT /
      *////////////////////////////////////////////////////////////
     C     SRINIT        BEGSR
      * INPUT PARAMETERS
     C     *ENTRY        PLIST
     C                   PARM                    WAVD              4
     C                   PARM                    WPRO              8
     C                   PARM                    WKNA              8
     C                   PARM                    WBRUDD            1
     C                   MOVE      WAVD          WUAVD
     C                   MOVE      WPRO          WUPRO
     C                   MOVE      WKNA          WUKNA
      * HENTE DAGENS DATO OG TID
     C                   TIME                    WTIME
     C                   Z-ADD     W≈≈           W≈
     C                   Z-ADD     WDD           WD
     C                   Z-ADD     WMM           WM
      * INIT AV ARBEIDSFELT
     C     *LIKE         DEFINE    ECSR          WCSR
     C     *LIKE         DEFINE    EC0065        WC0065
     C                   MOVE      'S'           WCSR
     C                   MOVEL     'IFTMIN'      WC0065
     C                   READ      FIRM                                   51
     C     *LIKE         DEFINE    UMN           WMN
      * DEF AV KEYS
     C     HEADKE        KLIST
     C                   KFLD                    WUPRO
     C                   KFLD                    WUKNA
     C     DOKKEY        KLIST
     C                   KFLD                    WRI
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
FMAASC*                    MOVE 'F'       WRI     1
FMAASC                   MOVE      'U'           WRI               1              STATUS
     C     ECMKEY        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    WUKNA
     C                   KFLD                    WCSR
     C                   KFLD                    WC0065
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                    SRA /
      *////////////////////////////////////////////////////////////
     C     SRA           BEGSR
      *_________________________________
      * Send IFTMIN ? and wich release ?
      *"""""""""""""""""""""""""""""""""
     C     ECMKEY        CHAIN     ECMSG                              51
     C     *IN51         CABEQ     '1'           SRA9
     C                   MOVE      '1'           *IN41
     C     SRA9          ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                    SRB0/
      *////////////////////////////////////////////////////////////
     C     SRB0          BEGSR
     C* FOR ALLE OPPD P≈ TUREN
     C     HEADKE        SETLL     HEAD14
     C     HEADKE        READE     HEAD14                                 33
     C     *IN33         DOWEQ     '0'
      * FINN ALLE DISTR.OPPDR
FMAASC*          DOKKEY    SETLLDOKUF
FMAASC*          DOKKEY    READEDOKUF                    34
FMAASC     DOKKEY        SETLL     DOKUF99
FMAASC     DOKKEY        READE     DOKUF99                                34
     C     *IN34         DOWEQ     '0'
FMAASC*          DFST      IFEQ 'U'
FMAASC     DFST          IFEQ      WRI
FMAASC     DSLK          IFEQ      'DE'
FMAASC     WSTJTY        ANDEQ     'VE'
FMAASC                   MOVE      'J'           VENLDE            1
FMAASC                   END
     C                   EXSR      SRB
     C                   END
FMAASC*          DOKKEY    READEDOKUF                    34
FMAASC     DOKKEY        READE     DOKUF99                                34
     C                   END
      *
     C     HEADKE        READE     HEAD14                                 33
     C                   END
      *
     C                   ENDSR
      *
      *
      *////////////////////////////////////////////////////////////
      * SR                                                    SRB /
      *////////////////////////////////////////////////////////////
     C     SRB           BEGSR
      *________________________
      * Retreive message number
      *""""""""""""""""""""""""
     C     1             CHAIN     EDIC                               51
     C     *IN51         CABEQ     '1'           SRB9
     C                   ADD       1             CMN
     C                   UPDATE    EDICR
     C                   Z-ADD     CMN           WMN
      *_____________
      * ADD SEGMENTS
      *"""""""""""""
     C                   EXSR      SRBA
     C*
     C                   EXSR      UNH00
      *______________
      * WRITE TO EDIM
      *""""""""""""""
     C                   EXSR      TVINM0
     C*
     C     SRB9          ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                   SRBA /
      *////////////////////////////////////////////////////////////
     C     SRBA          BEGSR
     C                   MOVEL     'E321R_'      WPGM              9
     C                   MOVE      EC0054        WPGM
     C                   Z-ADD     WMN           WUMN
     C                   CALL      WPGM
     C                   PARM                    DFAVD
     C                   PARM                    DFOPD
     C                   PARM                    DFFBNR
     C                   PARM                    WUMNA
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * UNH00                                               UNH00 /
      *////////////////////////////////////////////////////////////
     C     UNH00         BEGSR
     C                   CLEAR                   UNHR
     C                   Z-ADD     WMN           UMN
     C                   MOVEL     WMN           U0062A
     C                   MOVEL     EC0065        U0065
     C                   MOVEL     'D'           U0052
     C                   MOVEL     EC0054        U0054
     C                   MOVEL     'UN'          U0051
     C                   MOVEL     'NEP   '      U0057
     C                   MOVEL     '621'         U1001
     C                   MOVEL     WSAORD        U1004
     C                   MOVEL     U0062A        U0062B
     C                   WRITE     UNHR
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * TVINM0                                              TVINM0/
      *////////////////////////////////////////////////////////////
     C     TVINM0        BEGSR
     C                   CLEAR                   EDIMR
     C                   MOVEL     ECIIDI        M0004
     C                   MOVEL     ECIIDC        M0010
     C                   MOVE      '1'           M0035
     C                   MOVEL     U0062A        M0062
     C                   MOVEL     U0065         M0065
     C                   MOVEL     U1004         M0068
     C                   MOVEL     U1001         M1001
     C                   MOVEL     U1004         M1004
     C                   Z-ADD     WMN           MMN
     C                   MOVE      'S'           MSR
     C                   MOVEL     WDATO         MDT
     C                   MOVEL     WTID          MTM
     C                   Z-ADD     DFAVD         MAVD
     C                   Z-ADD     DFOPD         MTDN
     C                   Z-ADD     DFFBNR        MFFBNR
     C                   WRITE     EDIMR
      * Skriv til TRACK/Trace fil??
     C     FIURBL        IFEQ      'J'
     C                   EXSR      TTSR
     C                   END
      *
     C                   ENDSR
     C***********************************************
     C     TTSR          BEGSR
     C***********************************************
      * EDI- SENDT
     C                   MOVE      DFAVD         TTAVD
     C                   MOVE      DFOPD         TTOPD
     C                   MOVE      DFFBNR        TTFBNR
     C                   TIME                    XULTID
     C                   MOVE      XULDD         ULDD
     C                   MOVE      XULMM         ULMM
     C                   MOVE      XUL≈≈         UL≈≈
     C                   MOVE      'ED3'         TTACTI
     C                   MOVE      ULDT          TTDATE
     C                   MOVE      XULTM         TTTIME
     C                   MOVE      TTDATE        TTDATL
     C                   MOVE      TTTIME        TTTIML
     C                   MOVE      ZUSER         TTUSER
     C                   MOVEL     TTXED3        TTTEXT
n01  C                   MOVEL     TTXED3L       TTTEXL
     C                   WRITE     TRACKFR
     C                   ENDSR
