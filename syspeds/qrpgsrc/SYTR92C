********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 YBC PTF10 RETTELSE ETTER UTVIDELSE AV OPPDRAGSNR
********************************************************************
     FTURER12   IF   E           K DISK
     F                                     RENAME(TURERR:TUR12R)
     FFREETUR   IF   E           K DISK
     FREIS      IF   E           K DISK
     FTILL      IF   E           K DISK
     FTUFE      IF   E           K DISK
     FHEAD11    IF   E           K DISK
     FFAK2      IF   E           K DISK
     FFIRM      IF   E           K DISK
     FTURER     IF   E           K DISK
     FTRAVH2    IF   E           K DISK
     FTRAVH1    IF   E           K DISK
     F                                     RENAME(TRAVHR:TRAH1R)
     FTRAVV2    IF   E           K DISK
     FWRKRUND   UF A F   75    10AIDISK    KEYLOC(1)
     FXTUR      IF   E           K DISK
     FSYTR91PF  O    E             PRINTER OFLIND(*IN88)
     F                                     USROPN
     D AL              S              1    DIM(5)
     D AL2             S              1    DIM(5)
     D                 DS
     D  TUDT                   1      8  0
     D  TUDTÅ                  1      4  0
     D  TUDTM                  5      6  0
     D  TUDTD                  7      8  0
     D                 DS
     D  TUDTT                  1      8  0
     D  TUDTTÅ                 1      4  0
     D  TUDTTM                 5      6  0
     D  TUDTTD                 7      8  0
     D                 DS
     D  TIDATE                 1      8  0
     D  TIYEAR                 3      4  0
     D  TIMONT                 5      6  0
     D  TIDAY                  7      8  0
     D                 DS
     D  FEDAT2                 1      8  0
     D  FEYEA2                 3      4  0
     D  FEMON2                 5      6  0
     D  FEDAY2                 7      8  0
     D                 DS
     D  DS02                   1     65
     D  TUSDF                  1     20
     D  XUDTD                 22     23  0
     D  XUDTM                 24     25  0
     D  XUDTÅ                 26     27  0
     D  TUSDT                 32     51
     D  XUDTTD                53     54  0
     D  XUDTTM                55     56  0
     D  XUDTTÅ                57     58  0
     D                 DS
     D  DS03                   1     65
     D  TUSJN1                 1     30
     D  TUSJN2                32     61
     D                 DS
     D  DS04                   1     65
     D  RETXT                  1     35
     IWRKRUND   NS
     I                                 11   75  WSTXT
     C*
     C                   EXSR      INIT
     C                   Z-ADD     0             TURUND
     C                   SETOFF                                       04
     C     *LOVAL        SETLL     XTURR
     C     *IN04         DOWEQ     '0'
     C                   READ      XTURR                                  04
     C     *IN04         IFEQ      '0'
     C                   Z-ADD     WRUND         TURUND
     C     TURUND        SETLL     TURER12
     C     TURUND        READE     TURER12                                33
     C*
     C     START         TAG
     C                   SETOFF                                           98
     C                   EXSR      NEWPAG
     C     TURUND        SETLL     TURER12
     C                   Z-ADD     1             PLIN              2 0
      *
     C     START2        TAG
     C*
     C* START PÅ INDIKATOR 90 FOR LESING PÅ SUBFIL
     C*
     C     *IN90         DOUEQ     '1'
     C     TURUND        READE     TURER12                                90
     C  N90              EXSR      LESFIL
     C                   EXSR      LAGAVR
     C                   END
     C*
     C* AVREGNINGSDATA TIL SLUTT - KUN VED UTSKRIFT.
     C*
     C                   EXSR      SKRAVR
     C                   END
     C                   END
     C*
     C* ENTER ER TASTET TEST HVA SOM ER TASTET
     C*
     C     SLUTT         TAG
     C                   CLOSE     SYTR91PF
     C                   SETON                                        LR
     C                   RETURN
     C*
     C***********************************************
     C     LESFIL        BEGSR
     C***********************************************
     C*
     C                   Z-ADD     TUPRO         WSPRO
     C                   Z-ADD     TUAVD         WSAVD
     C                   EXSR      SUMFRA
     C     USKJER        CHAIN     WRKRUND                            34
     C   34              EXCEPT    DET7
     C  N34              EXCEPT    DET
     C  N34USKJER        CHAIN     WRKRUND                            34
     C  N34              EXCEPT    DET8
     C                   MOVE      *BLANKS       WSTXT
     C     USKJER        CHAIN     WRKRUND                            34
     C                   SETON                                        49
     C                   MOVE      WSTXT         WSTXTX           65
     C                   WRITE     PDET
     C   88              EXSR      NEWPAG
     C                   SETOFF                                       49
     C                   MOVE      TUDTD         XUDTD
     C                   MOVE      TUDTM         XUDTM
     C                   MOVE      TUDTÅ         XUDTÅ
     C                   MOVE      TUDTTD        XUDTTD
     C                   MOVE      TUDTTM        XUDTTM
     C                   MOVE      TUDTTÅ        XUDTTÅ
     C                   MOVE      DS02          WSTXT
     C                   WRITE     PDET
     C   88              EXSR      NEWPAG
     C                   MOVE      DS03          WSTXT
     C                   WRITE     PDET
     C   88              EXSR      NEWPAG
     C*
     C* REISEKASSER
     C*
     C                   SETON                                        50
     C                   EXSR      REISSR
     C                   SETOFF                                       50
     C*
     C* FERJEOVERFARTER
     C*
     C                   SETON                                        51
     C                   EXSR      TUFESR
     C                   SETOFF                                       51
     C*
     C* TILLATELSER
     C*
     C                   SETON                                        52
     C                   EXSR      TILLSR
     C                   SETOFF                                       52
     C*
     C*
     C* FRITEKSTER
     C*
     C                   SETON                                        53
     C                   EXSR      FREESR
     C                   SETOFF                                       53
     C* SKILLESTREK MELLOM TURENE
     C                   WRITE     PDETH5
     C*
     C                   ENDSR
     C***********************************************
     C     SUMFRA        BEGSR
     C***********************************************
     C                   MOVE      *ZEROS        TOTFRA            9 2
     C     STSUM1        TAG
     C     TUPRO         SETLL     HEAD11
     C     STSUM2        TAG
     C                   SETOFF                                       33
     C     TUPRO         READE     HEAD11                                 33
     C  N33              DO                                                     1<-B
     C     HEST          IFEQ      'S'
     C                   GOTO      STSUM2
     C                   END
     C     FAK2K         SETLL     FAK2
     C                   SETOFF                                       33
     C     STSUM3        TAG
     C     FAK2K         READE     FAK2                                   33
     C   33              GOTO      STSUM2
     C     FAVK          IFEQ      'FRA'                                         2<-B
     C     FASK          ANDNE     'I'
     C     FAKDA         ANDNE     'D'
     C     FAKDA         ANDNE     'K'
     C                   ADD       FABELN        TOTFRA
     C                   END                                                     2<-E
     C                   GOTO      STSUM3
     C                   END                                                    1<-E
     C*
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     *ENTRY        PLIST
     C                   PARM                    USKJER           10
     C                   PARM                    CALLTY            1
     C                   OPEN      SYTR91PF
     C                   MOVE      *BLANKS       BLAN65           65
     C     TUKEY         KLIST
     C                   KFLD                    TUAVD
     C                   KFLD                    TUPRO
     C     FAK2K         KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C     TRAVVV        KLIST
     C                   KFLD                    THTAVD
     C                   KFLD                    THTUNR
     C                   KFLD                    YYTVST            1
     C                   MOVE      ' '           YYTVST
     C     TURERK        KLIST
     C                   KFLD                    THTAVD
     C                   KFLD                    THTUNR
     C*
     C     UYEAR         IFGT      50
     C     UYEAR         ADD       1900          IDAG
     C                   ELSE
     C     UYEAR         ADD       2000          IDAG
     C                   END
     C                   MULT      10000         IDAG
     C                   MOVEL     UMONTH        IDMMDD            4 0
     C                   MOVE      UDAY          IDMMDD
     C                   MOVE      IDMMDD        IDAG              8 0
     C                   MOVE      UYEAR         IDMMDD
     C                   MOVE      IDMMDD        IDAGX
     C                   MOVEL     UDAY          IDAGX
     C                   READ      FIRM                                   33
     C*
     C     FITAXD        IFNE      *ZEROS
     C     FITAX2        ANDNE     *ZEROS
     C     IDAG          ANDLT     FITAXD
     C                   MOVE      FITAX2        FITAX
     C                   END
     C*
     C                   MOVE      FITAX         PRITAX            4 2
     C                   MOVE      FICURR        XVAL              3
     C*
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     TUFESR        BEGSR
     C***********************************************
     C                   MOVE      *ZEROS        ANTR              5 0
     C     TUKEY         SETLL     TUFE
     C                   SETOFF                                       33
     C     *IN33         DOUEQ     '1'
     C     TUKEY         READE     TUFE                                   33
     C  N33              DO
     C                   ADD       1             ANTR
     C     ANTR          IFEQ      1
     C                   WRITE     PDETH4
     C                   END
     C     USKJER        CHAIN     WRKRUND                            34
     C   34              EXCEPT    DET3
     C  N34              EXCEPT    DET
     C  N34USKJER        CHAIN     WRKRUND                            34
     C  N34              EXCEPT    DET4
     C                   MOVE      *BLANKS       WSTXT
     C     USKJER        CHAIN     WRKRUND                            34
     C                   WRITE     PDET
     C   88              EXSR      NEWPAG
     C                   END
     C                   END
     C                   ENDSR
     C***********************************************
     C     TILLSR        BEGSR
     C***********************************************
     C                   MOVE      *ZEROS        ANTR              5 0
     C     TUKEY         SETLL     TILL
     C                   SETOFF                                       33
     C     *IN33         DOUEQ     '1'
     C     TUKEY         READE     TILL                                   33
     C  N33              DO
     C                   ADD       1             ANTR
     C     ANTR          IFEQ      1
     C                   WRITE     PDETH3
     C                   END
     C     USKJER        CHAIN     WRKRUND                            34
     C   34              EXCEPT    DET5
     C  N34              EXCEPT    DET
     C  N34USKJER        CHAIN     WRKRUND                            34
     C  N34              EXCEPT    DET6
     C                   MOVE      *BLANKS       WSTXT
     C     USKJER        CHAIN     WRKRUND                            34
     C                   WRITE     PDET
     C   88              EXSR      NEWPAG
     C                   END
     C                   END
     C                   ENDSR
     C***********************************************
     C     REISSR        BEGSR
     C***********************************************
     C                   MOVE      *ZEROS        ANTR              5 0
     C     TUKEY         SETLL     REIS
     C                   SETOFF                                       33
     C     *IN33         DOUEQ     '1'
     C     TUKEY         READE     REIS                                   33
     C  N33              DO
     C                   ADD       1             ANTR
     C     ANTR          IFEQ      1
     C                   WRITE     PDETH1
     C                   END
     C     USKJER        CHAIN     WRKRUND                            34
     C   34              EXCEPT    DET1
     C  N34              EXCEPT    DET
     C  N34USKJER        CHAIN     WRKRUND                            34
     C  N34              EXCEPT    DET2
     C                   MOVE      *BLANKS       WSTXT
     C     USKJER        CHAIN     WRKRUND                            34
     C                   WRITE     PDET
     C   88              EXSR      NEWPAG
     C     RETXT         IFNE      *BLANKS
     C                   MOVE      DS04          WSTXT
     C                   WRITE     PDET
     C   88              EXSR      NEWPAG
     C                   END
     C                   END
     C                   END
     C                   ENDSR
     C***********************************************
     C     FREESR        BEGSR
     C***********************************************
     C                   MOVE      *ZEROS        ANTR              5 0
     C     TUKEY         SETLL     FREETUR
     C                   SETOFF                                       33
     C     *IN33         DOUEQ     '1'
     C     TUKEY         READE     FREETUR                                33
     C  N33              DO
     C                   ADD       1             ANTR
     C     ANTR          IFEQ      1
     C                   WRITE     PDETH2
     C                   END
     C                   MOVEL     FRTTXT        WSTXT
     C                   WRITE     PDET
     C   88              EXSR      NEWPAG
     C                   END
     C                   END
     C                   ENDSR
     C***********************************************
     C     LAGAVR        BEGSR
     C***********************************************
     C* DERSOM AVR.DATA IKKE FINNES FRA FØR, ELLER FINNES, MEN IKKE
     C* ER BESKYTTET AV "I ARBEID" STATUS - GENERER DATA.
     C     TUKEY         CHAIN     TRAVH1                             33
     C   33              MOVE      ' '           THST
     C   33              MOVE      ' '           THSTAR
     C     THST          IFEQ      ' '
     C     THSTAR        ANDEQ     ' '
     C                   MOVE      TUAVD         XXAVD             4 0
     C                   MOVE      TUPRO         XXTUNR            8 0
     C                   MOVE      'N'           XXVIS             1
     C                   CALL      'SYTR12A'
     C                   PARM                    XXAVD
     C                   PARM                    XXTUNR
     C                   PARM                    XXVIS
     C                   PARM                    N4                4 0
S01  C*                  PARM                    N5                5 0
N01  C                   PARM                    N7                7 0
     C                   PARM                    N8                8 0
     C                   END
     C                   ENDSR
     C***********************************************
     C     SKRAVR        BEGSR
     C***********************************************
     C                   MOVE      *ZEROS        XTBRT             9 2
     C                   MOVE      *ZEROS        XTPROV            9 2
     C                   MOVE      *ZEROS        XTTREK            9 2
     C                   MOVE      *ZEROS        XTPROS            5 2
     C                   MOVE      *ZEROS        XTOTAL           13 2
     C                   MOVE      *ZEROS        XMVAGR           13 2
     C* HODE  (** AVREGNINGSDATA ** + SKILLESTREK)
     C                   WRITE     PDETH6
     C                   WRITE     PDETH5
     C*
     C                   SETOFF                                       94
     C     TURUND        SETLL     TRAVH2
     C*
     C     TURUND        READE     TRAVH2                                 94
     C     *IN94         DOWEQ     '0'
     C                   EXSR      DECI
     C                   ADD       THTOT         XTOTAL
     C     THKDM         COMP      'J'                                    34
     C   34              ADD       THTOT         XMVAGR
     C*
     C     TURERK        CHAIN     TURER                              33
     C* KONVERTER TURDATO TIL LOKALT FORMAT
     C     *IN33         IFEQ      '1'
     C                   MOVE      *ZEROS        TUDATO
     C                   ELSE
     C                   MOVE      TUDT          TUDATO
     C     FIDTFM        IFNE      3                                            SNU DATO TIL
     C                   CALL      'DATFM3'                                     LOKALT FORMA
     C                   PARM                    FIDTFM
     C                   PARM                    TUDATO
     C                   END
     C                   END
     C     TUTLM         COMP      *ZEROS                                 86
     C     TUTM3         COMP      *ZEROS                                 87
     C     THST          COMP      ' '                                84        *AVREGNET
     C     THSTAR        COMP      ' '                                    89    *UBESKYTTET
     C   88              EXSR      NEWPAG
     C                   WRITE     ADET
     C                   WRITE     ADETST
     C                   EXSR      VARELI
     C     TURUND        READE     TRAVH2                                 94
     C  N94              END
     C*
     C* SKRIV BUNNTOTAL FOR RUNDTUR
     C                   EXSR      TOTAL
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     VARELI        BEGSR
     C***********************************************
     C                   MOVE      *ZEROS        XBRT              9 2
     C                   MOVE      *ZEROS        XPROV             9 2
     C                   MOVE      *ZEROS        XTREK             9 2
     C                   MOVE      *ZEROS        XPROS             5 2
     C* 61 - 67 - TYPE TEKST ER SKREVET.
     C                   MOVEA     '0000000'     *IN(61)
     C                   SETOFF                                       85
     C*
     C   88              EXSR      NEWPAG
     C                   WRITE     ADET2H
     C*
     C     TRAVVV        SETLL     TRAVV2
     C     TRAVVV        READE     TRAVV2                                 85
     C     *IN85         DOWEQ     '0'
     C     TVTYP         COMP      'A'                                    51    *AVTALT
     C     TVTYP         COMP      'B'                                    52    *BR-PROV
     C     TVTYP         COMP      'C'                                    53    *INNH.
     C     TVTYP         COMP      'D'                                    54    *UTKJ.
     C     TVTYP         COMP      'E'                                    55    *FØRT REK
     C     TVTYP         COMP      'F'                                    56    *BUD.REK
     C     TVTYP         COMP      'G'                                    57    *JUSTER.
     C   51TVBLL         IFNE      *ZEROS
     C   88              EXSR      NEWPAG
     C                   WRITE     ADET2
     C                   END
     C  N51
     CAN 88              EXSR      NEWPAG
     C  N51              WRITE     ADET2
     C     TVTYP         IFNE      'A'
     C     TVTYP         IFEQ      'B'
     C     TVBLLB        SUB       TVBLL         XXPROV           11 0
     C                   ADD       TVBLLB        XBRT
     C                   ADD       TVBLLB        XTBRT
     C                   ELSE
     C                   ADD       TVBLL         XTREK                          *LIGGER LAGRET
     C                   ADD       TVBLL         XTTREK                          MED NEG FORTEGN
     C                   END
     C                   END
     C     TRAVVV        READE     TRAVV2                                 85
     C   51              SETON                                            61
     C   52              SETON                                            62
     C   53              SETON                                            63
     C   54              SETON                                            64
     C   55              SETON                                            65
     C   56              SETON                                            66
     C   57              SETON                                            67
     C  N85              END
     C* PROVISJON = BRUTTO MINUS TREKK MINUS TRANSPORTØRHONORAR
     C     XBRT          ADD       XTREK         XPROV                          *XTREK=NEG FORTEGN
     C     XPROV         SUB       THTOT         XPROV                          *THTOT=POS FORTEG
     C                   MULT      -1            XPROV                          *SNU TIL NEGATIVT
     C                   ADD       XPROV         XTPROV
     C* PROVISJON I PROSENT AV BRUTTO
     C     XPROV         IFNE      *ZEROS
     C     XPROV         DIV(H)    XBRT          XHELP             5 4
     C     XHELP         MULT      -100          XPROS
     C                   END
     C* SIKRER AT TOTAL FOR TUREN KOMMER PÅ SAMME SIDE - IKKE TEST OVERFLOW
     C                   WRITE     DETTOT
     C                   ENDSR
     C***********************************************
     C     TOTAL         BEGSR
     C***********************************************
     C     XMVAGR        MULT(H)   FITAX         XMOMS            13 2
     C     XTOTAL        ADD       XMOMS         XBRUTT
     C*
     C* KAN RISIKERE AT MOMS + TOTAL KOMMER PÅ EGEN SIDE. MEN FOR Å UNNGÅ
     C* DET MÅTTE EN HA SATT OVERFLOW TIL 42 OG FÅR LITE UTAV HVER SIDE
     C     XMOMS         IFNE      *ZEROS
     C   88              EXSR      NEWPAG
     C                   WRITE     TOTA
     C                   END
     C* PROVISJON I PROSENT AV BRUTTO
     C     XTPROV        IFNE      *ZEROS
     C     XTPROV        DIV(H)    XTBRT         XHELP             5 4
     C     XHELP         MULT      -100          XTPROS
     C                   END
     C* ALLTID PLASS FOR SLUTTTOTAL ( 1 BLANK + 2 LINJER) - IKKE TEST OVERFL.
     C                   WRITE     TOTC
     C                   ENDSR
     C*
     C***********************************************
     C     DECI          BEGSR
     C***********************************************
     C* TEST FIRMS NUMBER OF DECIMALS
     C*
     C* NO DECIMALS
     C     FIDECI        IFEQ      0
     C                   Z-ADD(H)  THTOT         FABEL0           11 0
     C                   Z-ADD     FABEL0        THTOT
     C                   ELSE
     C* 1 DECIMAL
     C     FIDECI        IFEQ      1
     C                   Z-ADD(H)  THTOT         FABEL1           12 1
     C                   Z-ADD     FABEL1        THTOT
     C                   END
     C                   END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     KDTFM1        BEGSR
     C***********************************************
     C                   MOVE      IDAGX         XKDTFM            4 0
     C                   MOVEL     IDAGX         UDT1              2 0
     C                   MOVEL     XKDTFM        UDT2              2 0
     C                   MOVE      XKDTFM        UDT3              2 0
     C                   EXSR      KDTFMT
     C                   MOVEL     UDT1          IDAGX
     C                   MOVEL     UDT2          XKDTFM
     C                   MOVE      UDT3          XKDTFM
     C                   MOVE      XKDTFM        IDAGX
     C*
     C                   ENDSR
     C***********************************************
     C     KDTFMT        BEGSR
     C***********************************************
     C                   CALL      'DATOFMT'
     C                   PARM                    UDT1
     C                   PARM                    UDT2
     C                   PARM                    UDT3
     C*
     C                   ENDSR
     C*
     C*
     C*
     C***********************************************
     C     NEWPAG        BEGSR
     C***********************************************
     C*  88 ER OVERFLOWINDIKATOR   -
     C                   SETOFF                                       88
     C  N84              EXSR      KDTFM1
     C                   WRITE     PHEAD
     C  N84              EXSR      KDTFM1
     C                   SETON                                        84
     C*
     C                   ENDSR
     C*
     OWRKRUND   E            DET
     O                       BLAN65              75
     OWRKRUND   EADD         DET1
     O                       USKJER              10
     O                       RESJ                18
     O                       RENBR               27
     O                       REVAL               31
     O                       REBEL         J     52
     OWRKRUND   E            DET2
     O                       RESJ                18
     O                       RENBR               27
     O                       REVAL               31
     O                       REBEL         J     52
     OWRKRUND   EADD         DET3
     O                       USKJER              10
     O                       FEFROM              15
     O                       FETO                21
     O                       FELENG              25
     O                       FEDAY2        J     28
     O                       FEMON2        J     30
     O                       FEYEA2        J     32
     O                       FECURR              36
     O                       FEPRI1              50 '           ,  '
     O                       FEPRI2              65 '           ,  '
     OWRKRUND   E            DET4
     O                       FEFROM              15
     O                       FETO                21
     O                       FELENG              25
     O                       FEDAY2        J     28
     O                       FEMON2        J     30
     O                       FEYEA2        J     32
     O                       FECURR              36
     O                       FEPRI1              50 '           ,  '
     O                       FEPRI2              65 '           ,  '
     OWRKRUND   EADD         DET5
     O                       USKJER              10
     O                       TINR                25
     O                       TITYPE              33
     O                       TIDAY               36
     O                       TIMONT              38
     O                       TIYEAR              40
     OWRKRUND   E            DET6
     O                       TINR                25
     O                       TITYPE              33
     O                       TIDAY               36
     O                       TIMONT              38
     O                       TIYEAR              40
     OWRKRUND   EADD         DET7
     O                       USKJER              10
     O                       TUST                11
     O                       TURMND              14
     O                       TURAAR              16
     O                       TUOPDT              19
     O                       TUBILK              23
     O                       TUTAKO              25
     O                       TUTVAL              29
     O                       TUTBEL        J     48
     O                                           62 'BRUTTO FRAKT:'
     O                       TOTFRA        J     75
     OWRKRUND   E            DET8
     O                       TUST                11
     O                       TURMND              14
     O                       TURAAR              16
     O                       TUOPDT              19
     O                       TUBILK              23
     O                       TUTAKO              25
     O                       TUTVAL              29
     O                       TUTBEL        J     48
     O                                           62 'BRUTTO FRAKT:'
     O                       TOTFRA        J     75
