      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/INQANR MODULE(*LIBL/INQANR *LIBL/INCGI)
CRTPGM*         ACTGRP(CGI) AUT(*USE)
      *
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 YBC PTF10 VIS LEVERANDØRS ARTIKKELNR
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FBRPARF    if   e           k disk    usropn
     FLLVARE01  IF   E           K DISK    USROPN
     FLLVARE02  IF   E           K DISK    USROPN
     F                                     RENAME(LLVARER:LLVARER2)
      *********************************************************************
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      /copy syspeds/qrpgsrcpy,variables3
      *
OddEvD OddEven         s             10
      * Client input variables
     D WSANR           s             15
     D SOKANR          s             15
     D SOKNVN          s             35
     D SOKNVN2         s             35
     D USER            s             10
     D WSGRP           s              8
     D W2GRP           s              8  0
     D WSPNR           s              8
     D W2PNR           s              8  0
     D WSVARNV         s             20
     D WSFORMS         s             20
     D WSREFR          s              1
     D REFRESH         s             50
     D MaxV            s              5
     D MaxVis          s              5  0
      * Variables used to load the external html source member
     D Lib             s             10
     D Fn              s             10
     D Mbr             s             10
      * Other variables
      *
     D Spaces          s             12a   inz('&nbsp;&nbsp;')
     D VG              S              5    DIM(10)
     D VE              S              5    DIM(10)
     D                 DS
     D  PAVRDA                 1     50
     D  VV                     1     50    DIM(10)
      *
      *
      *
      /copy syspeds/qrpgsrcpy,prolog3
      *
      * Get program start time for calculating execution time
     C                   time                    timedata1
      * Parse input / cvt to numeric
     C                   exsr      Parse
      * File open / keys
     C                   EXSR      INIT
      * Read output skeleton html member into memory
     C                   exsr      LoadHtml
      * Set section variables
     C                   exsr      SetTop
      * Send section top
     C                   callp     wrtsection('top')
      * Find/send lines
     C                   exsr      LINES
      * Send section bot
      *
     C                   callp     wrtsection('bot')
      * Quit
     C                   exsr      Exit
      *
     C                   eval      *inlr = *on
     C                   return
      *
      *
      *=====================================================================
      * Parse input / cvt to numeric
      *=====================================================================
     C     Parse         begsr
      * Parse variables from QUERY_STRING environment variable
     C                   eval      WSANR   = zhbgetvar('WSANR')
     C                   IF        WSANR = *BLANKS
     C                   eval      SOKANR  = zhbgetvar('SOKANR')
     C                   ELSE
     C                   eval      SOKANR  = WSANR
     C                   END
     C                   eval      SOKNVN  = zhbgetvar('SOKNVN')
     C                   eval      SOKNVN  = uppify(SOKNVN)
     C                   eval      WSGRP   = zhbgetvar('WSGRP')
     C                   eval      W2GRP   = c2n2(WSGRP)
     C                   eval      WSPNR   = zhbgetvar('WSPNR')
     C                   eval      W2PNR   = c2n2(WSPNR)
     C                   eval      WSVARNV = zhbgetvar('WSVARNV')
     C                   IF        WSVARNV = *BLANKS
     C                   EVAL      WSVARNV = 'wsanr'
     C                   END
     C                   eval      WSFORMS = zhbgetvar('WSFORMS')
     C                   IF        WSFORMS = *BLANKS
     C                   EVAL      WSFORMS = 'forms[0]'
     C                   END
     C                   eval      WSREFR  = zhbgetvar('WSREFR')
     C                   IF        ((((WSREFR = 'J') OR
     C                                (WSREFR = 'j')) OR
     C                                (WSREFR = 'Y')) OR
     C                                (WSREFR = 'y'))
     C                   eval      REFRESH = 'self.opener.document.'
     C                             + %trim(WSFORMS)
     C                             + '.submit();'
     C                   END
     C                   eval      MaxV    = zhbgetvar('MaxV')
     C                   eval      MaxVis  = c2n2(MaxV)
     c     MaxVis        ifeq      *zeros
     c                   z-add     50            MaxVis
     c                   end
      * Userid.....
     C                   eval      USER = zhbgetvar('USER')
     C                   endsr
      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   CLOSE     BRIDF
     C                   close     BRPARF
     C                   CLOSE     LLVARE01
     C                   CLOSE     LLVARE02
     C                   return
     C                   endsr
      *=====================================================================
      * Read output skeleton html member into memory
      *=====================================================================
     C     LoadHtml      begsr
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC'
     C                   eval      Mbr = 'INQANR'
     C                   CAT       XSPRÅK:0      MBR
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
     C                   endsr
      *=====================================================================
      *  Set variable data for section /$top
      *=====================================================================
     C     SetTop        begsr
      *
     C* klargjøre HTMLvariable
OddEvC                   callp     updHTMLvar('ROWHEAD':RowHead)
OddEvC                   callp     updHTMLvar('LogoImg':LogoImg)
      *
BODY C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updHTMLvar('USER':USER)
     C                   callp     updHTMLvar('WSGRP':WSGRP)
     C                   callp     updHTMLvar('WSPNR':WSPNR)
     C                   callp     updHTMLvar('wsvarnv':WSVARNV)
     C                   callp     updHTMLvar('wsforms':WSFORMS)
     C                   callp     updHTMLvar('wsrefr':WSREFR)
     C                   callp     updHTMLvar('refresh':REFRESH)
      *
     C                   endsr
      *
      *=====================================================================
      * Fine lins - send to browser
      *=====================================================================
     C     Lines         begsr
     c                   eval      SOKNVN2   = SOKNVN
     c                   Move      *zeros        AntVist           5 0
     C     SOKANR        IFEQ      *BLANKS
     C     SOKNVN        ANDEQ     *BLANKS
     C                   GOTO      UTLES
     C                   END
      *
     C     SOKANR        IFNE      *BLANKS
     C     ANRKEY10      setll     LLVARE01
     C     ANRKEY11      reade     LLVARE01                               33
     C                   ELSE
     C     ANRKEY20      setll     LLVARE02
     C     ANRKEY21      reade     LLVARE02                               33
     C                   END
     C     *in33         DOWEQ     *OFF
     C                   exsr      exclude
     c     EXCL          IFEQ      'N'
      *
     C                   IF        VG(1) <> *BLANKS
     C     1             DO        10            XN                3 0
     C                   IF        VG(XN) <> *BLANKS
     C     VG(XN)        IFEQ      LVAG01
     C     VG(XN)        OREQ      LVAG02
     C     VG(XN)        OREQ      LVAG03
     C     VG(XN)        OREQ      LVAG04
     C     VG(XN)        OREQ      LVAG05
     C                   GOTO      STLES1
     C                   END
     C                   ELSE
     C                   LEAVE
     C                   END
     C                   ENDDO
     C                   goto      STLES3
     C     STLES1        TAG
     C                   END
      *
     C                   IF        VE(1) <> *BLANKS
     C     1             DO        10            XN                3 0
     C                   IF        VE(XN) <> *BLANKS
     C     VE(XN)        IFEQ      LVEIER
     C                   GOTO      STLES2
     C                   END
     C                   ELSE
     C                   LEAVE
     C                   END
     C                   ENDDO
     C                   goto      STLES3
     C     STLES2        TAG
     C                   END
     C* klargjøre HTMLvariable - SEND ROW
     C                   callp     updHTMLvar('LVANR':LVANR)
N01  C                   callp     updHTMLvar('LVANRL':LVANRL)
     C                   callp     updHTMLvar('LVVB':LVVB)
     C                   callp     updhtmlvar('LVSALD':
     C                             %trim(%editc(LVSALD:'Z')))
     C                   IF        LVAKT = 'I'
     C                   callp     updHTMLvar('LVAKT':'Ja')
     C                   ELSE
     C                   callp     updHTMLvar('LVAKT':'')
     C                   END
OddEvc     OddEven       IFEQ      ODD
OddEvc                   eval      OddEven = EVEN
OddEvc                   else
OddEvc                   eval      OddEven = ODD
OddEvc                   end
OddEvC                   callp     updHTMLvar('ODDEVEN':OddEven)
OddEv *
     C                   callp     wrtsection('row')
     c                   Add       1             AntVist           5 0
     c     AntVist       cabge     MaxVis        UtLes
     C     STLES3        TAG
     C                   END
     C     SOKANR        IFNE      *BLANKS
     C     ANRKEY11      reade     LLVARE01                               33
     C                   ELSE
     C     ANRKEY21      reade     LLVARE02                               33
     C                   END
     C                   end
      *
     C     UTLES         TAG
      *
      * Compute run time
     C                   time                    timedata2
     C     timedata2     subdur    timedata1     ms:*ms
     C                   eval      sec = ms / 1000000
     C                   callp     updhtmlvar('runtime':
     C                             %trim(%editw(sec:'     0 .   ')))
      *
     C                   endsr
      *=====================================================================******
      *  Eksluder post basert på...
      *=====================================================================******
     C     EXCLUDE       begsr
     C                   Move      'J'           EXCL              1
      * diverse tester - om ikke bestått hopp ut (med J i EXCL)
      * Posten skal med på liste
     C                   Move      'N'           EXCL              1
      *
     C     UTEXCL        endsr
      *
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
     C                   OPEN      BRIDF
     C     USER          CHAIN     BRIDF                              50
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
      *
OddEv * hent Parametre som går igjen i mange prog:
OddEv *      Odd/Even/Rowhead-farger,Logobilde,Språk,Intern/Ekstern bruker,  mm
OddEvc                   call      'ESGETPAR'
OddEvc                   parm                    BIBRID
OddEvc                   parm                    BIFIRM
OddEvc                   parm                    BIKUNR
OddEvc                   parm                    BIKNG
OddEvc                   parm                    RowHead          10
OddEvc                   parm                    Odd              10
OddEvc                   parm                    Even             10
OddEvc                   parm                    LogoImg          50
OddEvc                   parm                    XSPRÅK            2
OddEvc                   parm                    XINTERN           1
OddEvc                   parm                    ParmDS1        1024
OddEvc                   parm                    ParmDS2        1024
OddEvc                   parm                    ParmDS3        1024
      *
     C                   OPEN      LLVARE01
     C                   OPEN      LLVARE02
     C                   open      BRPARF                               50
      * Key for LLVARE01
     C     ANRKEY10      KLIST
     C                   kfld                    W2GRP
     C                   KFLD                    W2PNR
     C                   KFLD                    SOKANR
     C     ANRKEY11      KLIST
     C                   kfld                    W2GRP
     C                   KFLD                    W2PNR
      * Key for LLVARE02
     C     ANRKEY20      KLIST
     C                   kfld                    W2GRP
     C                   KFLD                    W2PNR
     C                   KFLD                    SOKNVN
     C     ANRKEY21      KLIST
     C                   kfld                    W2GRP
     C                   KFLD                    W2PNR
     c     BRPARFK       klist
     c                   kfld                    PABRID
     c                   kfld                    PAKUNR
     c                   kfld                    PAID
      *
     C                   MOVE      *BLANKS       VG
     C                   MOVE      ' '           XVGNIVÅ           1
      * VAREGRUPPER (BRUKER-NIVÅ)
     C                   EVAL      PABRID = USER
     C                   EVAL      PAKUNR = 0
      *
     C                   MOVEL     'SPRÅK'       PAID
     C     BRPARFK       chain     BRPARF                             33
     C                   IF        (*IN33 OR (PAVRDA <> 'EN'))
     C                   MOVE      *BLANKS       XSPRÅK            2
     C                   ELSE
     C                   MOVE      'EN'          XSPRÅK            2
     C                   END
      *
     C                   EVAL      PAID   = 'LLVG'
     C     BRPARFK       CHAIN     BRPARF                             40
     C                   IF        *IN40 = *OFF
     C                   MOVEA     VV            VG
     C                   MOVE      'B'           XVGNIVÅ           1
     C                   ELSE
      * VAREGRUPPER (KUNDE-NIVÅ)
     C                   EVAL      PABRID = '*KUNDFT*'
     C                   MOVE      BIFIRM        PABRID
     C                   EVAL      PAKUNR = BIKUNR
     C     BRPARFK       CHAIN     BRPARF                             40
     C                   IF        *IN40 = *OFF
     C                   MOVEA     VV            VG
     C                   MOVE      'K'           XVGNIVÅ           1
     C                   ELSE
      * VAREGRUPPER (FIRMA-NIVÅ)
     C                   EVAL      PAKUNR = 0
     C     BRPARFK       CHAIN     BRPARF                             40
     C                   IF        *IN40 = *OFF
     C                   MOVEA     VV            VG
     C                   MOVE      'F'           XVGNIVÅ           1
     C                   END
     C                   END
     C                   END
      *
     C                   MOVE      *BLANKS       VE
     C                   MOVE      ' '           XVENIVÅ           1
      * VAREEIER (BRUKER-NIVÅ)
     C                   EVAL      PABRID = USER
     C                   EVAL      PAKUNR = 0
     C                   EVAL      PAID   = 'LLVE'
     C     BRPARFK       CHAIN     BRPARF                             40
     C                   IF        *IN40 = *OFF
     C                   MOVEA     VV            VE
     C                   MOVE      'B'           XVENIVÅ           1
     C                   ELSE
      * VAREEIER (KUNDE-NIVÅ)
     C                   EVAL      PABRID = '*KUNDFT*'
     C                   MOVE      BIFIRM        PABRID
     C                   EVAL      PAKUNR = BIKUNR
     C     BRPARFK       CHAIN     BRPARF                             40
     C                   IF        *IN40 = *OFF
     C                   MOVEA     VV            VE
     C                   MOVE      'K'           XVENIVÅ           1
     C                   ELSE
      * VAREEIER (FIRMA-NIVÅ)
     C                   EVAL      PAKUNR = 0
     C     BRPARFK       CHAIN     BRPARF                             40
     C                   IF        *IN40 = *OFF
     C                   MOVEA     VV            VE
     C                   MOVE      'F'           XVENIVÅ           1
     C                   END
     C                   END
     C                   END
      *
     C                   endsr
