             PGM
             DCL        VAR(&IN01) TYPE(*LGL) LEN(1) VALUE('0') /* +
                          Hvis &in01 på, betyr det avslutt program +
                          fra valgbilde. */
             DCL        VAR(&AVDF) TYPE(*CHAR) LEN(4) /* Fra avdeling +
                          */
             DCL        VAR(&AVDT) TYPE(*CHAR) LEN(4) /* Til avdeling +
                          */
             DCL        VAR(&OT1) TYPE(*CHAR) LEN(2) /* Oppdragstype */
             DCL        VAR(&OT2) TYPE(*CHAR) LEN(2) /* Oppdragstype */
             DCL        VAR(&OT3) TYPE(*CHAR) LEN(2) /* Oppdragstype */
             DCL        VAR(&OT4) TYPE(*CHAR) LEN(2) /* Oppdragstype */
             DCL        VAR(&OT5) TYPE(*CHAR) LEN(2) /* Oppdragstype */
             DCL        VAR(&OT6) TYPE(*CHAR) LEN(2) /* Oppdragstype */
             DCL        VAR(&OT7) TYPE(*CHAR) LEN(2) /* Oppdragstype */
             DCL        VAR(&OT8) TYPE(*CHAR) LEN(2) /* Oppdragstype */
             DCL        VAR(&OT9) TYPE(*CHAR) LEN(2) /* Oppdragstype */
             DCL        VAR(&OT10) TYPE(*CHAR) LEN(2)
             DCL        VAR(&DT) TYPE(*CHAR) LEN(6) /* Dato */
             DCL        VAR(&DT8) TYPE(*CHAR) LEN(8) /* Dato */
             DCL        VAR(&PRT) TYPE(*CHAR) LEN(10) /* Printernavn */
             DCL        VAR(&A4) TYPE(*CHAR) LEN(1) /* A4-ark */
             DCL        VAR(&IO) TYPE(*CHAR) LEN(1)
             DCL        VAR(&BL) TYPE(*CHAR) LEN(10) /* 10 blanke  +
                          tegn */
    /* */
    /* ============================================================ */
    /* HENTE AVDELING, SIGNATUR OG FIRMAKODE */
             CALL       PGM(SYFA00) PARM(&IN01)
             IF         COND(&IN01 = '1') THEN(GOTO CMDLBL(SLUTT)) /* +
                          Hvis &IN01 på, avslutter program. */
    /* ============================================================ */
    /* */
    /* */
    /* ============================================================ */
    /* LESE UTVALG FRA SKJERMEN */
             CALL       PGM(SYFA95) PARM(&AVDF &AVDT &OT1 &OT2 &OT3 +
                          &OT4 &OT5 &OT6 &OT7 &OT8 &OT9 &OT10 &DT +
                          &PRT &A4 &IN01 &IO)
             IF         COND(&IN01 = '1') THEN(GOTO CMDLBL(SLUTT)) /* +
                          Hvis &IN01 på, avslutter program. */
    /* ============================================================ */
    /* */
    /* */
    /* ============================================================ */
    /* SKRIVE UT OPPDRAG SOM IKKE ER AVHENTET */
             OVRMSGF    MSGF(QUSERMSG) TOMSGF(SYMSGF)
             IF         COND(&A4 = 'J') THEN(DO)
             OVRPRTF    FILE(SYFA96PF) OUTQ(&PRT) PAGESIZE(72 120) +
                          CPI(15) OVRFLW(70)
                        ENDDO
             ELSE       CMD(DO)
             OVRPRTF    FILE(SYFA96PF) OUTQ(&PRT) PAGESIZE(48 120) +
                          CPI(10) OVRFLW(46)
                        ENDDO
             OVRDBF     FILE(HEADF) SHARE(*YES)

             IF         COND(&DT > '501231') THEN(CHGVAR VAR(&DT8) +
                          VALUE('19' *CAT &DT))
             ELSE       CMD(CHGVAR VAR(&DT8) VALUE('20' *CAT &DT))

             IF         COND(&IO = 'I') THEN(DO)
/* NÅR INKLUDER VELGES GJØRES UTVALGET M.H.T OPPD.TY. I RPG.PGM */
/* (OPNQRYF BLIR JÆVLIG KRØKKETE NÅR EN SKAL BLAND AND/OR BETING.) */
             OPNQRYF    FILE((HEADF)) +
                        QRYSLT('HESGM *EQ (''' *CAT &BL *CAT ''') +
                          *AND HEAVD *EQ %RANGE(' *CAT &AVDF *CAT ' +
                                          ' *CAT &AVDT *CAT ') *AND +
                          HEDTR *LE (' *CAT &DT8 *CAT ')') +
                        KEYFLD(*FILE)
                        ENDDO
             ELSE       CMD(DO)
             OPNQRYF    FILE((HEADF)) +
                        QRYSLT('HESGM *EQ (''' *CAT &BL *CAT ''') +
                          *AND HEAVD *EQ %RANGE(' *CAT &AVDF *CAT ' +
                                          ' *CAT &AVDT *CAT ') *AND +
                          HEDTR *LE (' *CAT &DT8 *CAT ') *AND +
                          HEOT *NE (''' *CAT &OT1 *CAT ''') *AND +
                          HEOT *NE (''' *CAT &OT2 *CAT ''') *AND +
                          HEOT *NE (''' *CAT &OT3 *CAT ''') *AND +
                          HEOT *NE (''' *CAT &OT4 *CAT ''') *AND +
                          HEOT *NE (''' *CAT &OT5 *CAT ''') *AND +
                          HEOT *NE (''' *CAT &OT6 *CAT ''') *AND +
                          HEOT *NE (''' *CAT &OT7 *CAT ''') *AND +
                          HEOT *NE (''' *CAT &OT8 *CAT ''') *AND +
                          HEOT *NE (''' *CAT &OT9 *CAT ''') *AND +
                          HEOT *NE (''' *CAT &OT10 *CAT ''')') +
                        KEYFLD(*FILE)
                        ENDDO
             CALL       PGM(SYFA96) PARM(&OT1 &OT2 &OT3 +
                          &OT4 &OT5 &OT6 &OT7 &OT8 &OT9 &OT10 +
                          &IO &A4)
             CLOF       OPNID(HEADF)
    /* ============================================================ */
    /* */
 SLUTT:      ENDPGM
