     FEDIS      IF   E           K DISK
     FEDIM      IF   E           K DISK
     FEUNH      IF   E           K DISK
     FECNI93A   IF   E           K DISK
     FECNT93A   IF   E           K DISK
     FEDGS93A   IF   E           K DISK
     FEDOC93A   IF   E           K DISK
     FEDTM93A   IF   E           K DISK
     FEEQD93A   IF   E           K DISK
     FEFTX93A   IF   E           K DISK
     FEGDS93A   IF   E           K DISK
     FEGID93A   IF   E           K DISK
     FELOC93A   IF   E           K DISK
     FEMEA93A   IF   E           K DISK
     FENAD93A   IF   E           K DISK
     FEPCI93A   IF   E           K DISK
     FERFF93A   IF   E           K DISK
     FESEL93A   IF   E           K DISK
     FETDT93A   IF   E           K DISK
     FETMD93A   IF   E           K DISK
     FETOD93A   IF   E           K DISK
     FBILLOFLG  IF   E           K DISK    PREFIX(Z_)
     F                                     RENAME(BILLOFR:BILLOFG)
     FEDIC      UF   E             DISK
     FZILLOF    O  A E             DISK
     FZLV1      O  A E             DISK
     FZLV2      O  A E             DISK
     FZREETXT   O  A E             DISK
     FZRISOK    O  A E             DISK
     D A               S              1    DIM(79)
     D B               S              1    DIM(18)
     D C               S             35    DIM(100)
     D D               S              1    DIM(35)
     D E               S             17    DIM(100)
     D F               S             51    DIM(100)
      *________________________
      * DS for decimal handling
      *""""""""""""""""""""""""
     D                 DS
     D  ZDEC                   1      7  6
     D  ZDEC6                  2      7
     D  ZHEL                  11     22  0
     D                 DS
     D  WMN2                   1      9  0
     D                 DS
     D  W2380                  1      6
     D  W2380N                 1      6S 0
     D                 DS
     D  WG7224A                1      5
     D  WG7224AN               1      5S 0
      */////////////
      * MAIN LOGIC /
      */////////////
      *___________
      * Initiering
      *"""""""""""
     C                   EXSR      INIT
      *________
      * CONVERT
      *""""""""
     C                   EXSR      RTVD

     C                   MOVE      '1'           *INLR
      *////////////////////////////////////////////////////////////
      * FTX15                                               FTX15 /
      *////////////////////////////////////////////////////////////
     C     FTX15         BEGSR
     C                   MOVE      '0'           *IN52
     C                   Z-ADD     15            ZGN1
     C     KEYMS1        SETLL     EFTX93A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS1        READE     EFTX93A                                52
     C     *IN52         DOWEQ     '0'

     C     F4440A        IFNE      *BLANKS
     C                   ADD       1             FRTLI
     C                   MOVEL(P)  F4451         FRTTXT
     C     FRTTXT        CAT       ':':0         FRTTXT
     C     FRTTXT        CAT       F4440A:1      FRTTXT
     C                   WRITE     FREETXTR
     C                   ENDIF
     C     F4440B        IFNE      *BLANKS
     C                   ADD       1             FRTLI
     C                   MOVEL(P)  F4451         FRTTXT
     C     FRTTXT        CAT       ':':0         FRTTXT
     C     FRTTXT        CAT       F4440B:1      FRTTXT
     C                   WRITE     FREETXTR
     C                   ENDIF

     C     KEYMS1        READE     EFTX93A                                52
     C  N52              ENDDO
     C                   ENDIF

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * FTX33                                               FTX33 /
      *////////////////////////////////////////////////////////////
     C     FTX33         BEGSR
     C                   MOVE      '0'           *IN54
     C                   Z-ADD     33            ZGN3
     C     KEYMS3        SETLL     EFTX93A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS3        READE     EFTX93A                                54
     C     *IN54         DOWEQ     '0'

     C     F4440A        IFNE      *BLANKS
     C                   ADD       1             XV2F
     C                   MOVEL     F4440A        F(XV2F)
     C                   ENDIF
     C     F4440B        IFNE      *BLANKS
     C                   ADD       1             XV2F
     C                   MOVEL     F4440B        F(XV2F)
     C                   ENDIF
     C     F4440C        IFNE      *BLANKS
     C                   ADD       1             XV2F
     C                   MOVEL     F4440C        F(XV2F)
     C                   ENDIF
     C     F4440D        IFNE      *BLANKS
     C                   ADD       1             XV2F
     C                   MOVEL     F4440D        F(XV2F)
     C                   ENDIF
     C     F4440E        IFNE      *BLANKS
     C                   ADD       1             XV2F
     C                   MOVEL     F4440D        F(XV2F)
     C                   ENDIF

     C     KEYMS3        READE     EFTX93A                                54
     C  N54              ENDDO
     C                   ENDIF

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * GDS33                                               GDS33 /
      *////////////////////////////////////////////////////////////
     C     GDS33         BEGSR
     C                   MOVE      '0'           *IN53
     C                   Z-ADD     33            ZGN2
     C     KEYMS2        SETLL     EGDS93A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS2        READE     EGDS93A                                53
     C     *IN53         DOWEQ     '0'

     C                   Z-ADD     GREP0         ZREPD0
     C                   Z-ADD     GREP1         ZREPD1
     C                   Z-ADD     GREP2         ZREPD2
     C                   EXSR      FTX33

     C     KEYMS2        READE     EGDS93A                                53
     C  N53              ENDDO
     C                   ENDIF

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * GID31                                               GID31 /
      *////////////////////////////////////////////////////////////
     C     GID31         BEGSR
     C                   MOVE      '0'           *IN52
     C                   Z-ADD     31            ZGN1
     C     KEYMS1        SETLL     EGID93A                                20
     C     *IN20         IFEQ      '1'
     C                   MOVE      *BLANKS       C
     C                   MOVE      *ZEROS        XNRFF             3 0
     C     KEYMS1        READE     EGID93A                                52
     C     *IN52         DOWEQ     '0'
     C     G1496         IFNE      '99999'

     C                   CLEAR                   BLV1R
     C                   CLEAR                   BLV2R
     C                   MOVE      *BLANKS       E
     C                   MOVE      *BLANKS       F
     C                   MOVE      *ZEROS        XV2M              3 0
     C                   MOVE      *ZEROS        XV2F              3 0

     C                   Z-ADD     BLUNIK        BVUNIK
     C                   MOVEL     BLREFF        BVREFF
     C                   MOVEA     G1496         A
     C                   EXSR      GETNUM
     C     ZNUM          MULT      10            BVLI
     C                   MOVE      BVLI          BVSORT
     C     'STC'         CAT(P)    G7224A:1      WVVS
     C     WVVS          CAT       G7065A:1      WVVS
     C                   Z-ADD     GREP0         ZREPC0
     C                   Z-ADD     GREP1         ZREPC1
     C                   EXSR      MEA34
     C                   EXSR      RFF36
     C                   EXSR      PCI37
     C                   MOVEA     G1496         A
     C                   EXSR      GETNUM
     C     ZNUM          MULT      10            BVLI
     C                   MULT      BVLI          BVSORT

     C                   MOVEA     *BLANKS       A
     C                   MOVEA     G7224A        A
     C                   EXSR      GETNUM
     C                   ADD       ZNUM          WG7224AN
     C                   MOVEL     WG7224A       BLAD1G

     C                   EXSR      GDS33

     C                   EXSR      OPDV2

     C                   ENDIF

     C     KEYMS1        READE     EGID93A                                52
     C  N52              ENDDO

     C                   EXSR      OPDONR

     C                   ENDIF

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *                                                     OPDV2 /
      *////////////////////////////////////////////////////////////
     C     OPDV2         BEGSR
      * Oppdater BLV2
     C     XV2M          IFGT      XV2F
     C                   MOVE      XV2M          XV2F
     C                   ENDIF

     C                   MOVE      *ZEROS        BVLI2
     C     1             DO        XV2F          XV2M
     C     BVLI2         IFGT      4
     C                   Z-ADD     1             BVLI2
     C                   ADD       1             BVLI
     C                   ADD       1             BVSORT
     C                   ELSE
     C                   ADD       1             BVLI2
     C                   END
     C                   MOVE      E(XV2M)       BVMN
     C                   MOVE      F(XV2M)       BVFT
     C                   WRITE     BLV2R
     C                   ENDDO

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *                                                    OPDONR /
      *////////////////////////////////////////////////////////////
     C     OPDONR        BEGSR
      * Oppdater ordrenr på BLV2 (max. 5 ordrenr pr. linje)
     C     XNRFF         IFGT      0
     C                   MOVE      'K'           FRTKOD
     C                   MOVE      *BLANKS       FRTTXT
     C                   Z-ADD     4             BVLI2
     C                   MOVE      *BLANKS       BVMN
     C                   MOVE      *BLANKS       BVFT
     C                   MOVEL     'O/NO:'       BVFT
     C                   MOVE      'N'           BVKD
     C                   Z-ADD     6             XNR               3 0
     C                   MOVE      'N'           XKOMMA            1
     C     1             DO        XNRFF         XNRFF2            3 0
     C                   MOVEA     C(XNRFF2)     D
     C                   Z-ADD     35            XNR2              3 0
     C     XNR2          DOWGT     1
     C     D(XNR2)       IFNE      *BLANKS
     C                   LEAVE
     C                   ENDIF
     C                   SUB       1             XNR2
     C                   ENDDO
     C                   ADD       XNR2          XNR
     C     XNR           IFGT      49
     C     BVLI2         IFEQ      4
     C                   ADD       1             BVLI
     C                   ADD       1             BVSORT
     C                   MOVE      *ZEROS        BVLI2
     C                   END
     C                   ADD       1             BVLI2
     C                   WRITE     BLV2R
     C                   MOVE      *BLANKS       BVFT
     C                   MOVEL     'O/NO:'       BVFT
     C     XNR2          ADD       6             XNR
     C                   MOVE      'N'           XKOMMA            1
     C                   ENDIF
     C     XKOMMA        IFEQ      'J'
     C                   CAT       ',':0         BVFT
     C                   ADD       1             XNR
     C                   ENDIF
     C                   MOVE      'J'           XKOMMA            1
     C                   ADD       1             XNR
     C                   CAT       C(XNRFF2):1   BVFT
     C                   ENDDO

     C     BVLI2         IFEQ      4
     C                   ADD       1             BVLI
     C                   ADD       1             BVSORT
     C                   MOVE      *ZEROS        BVLI2
     C                   END
     C                   ADD       1             BVLI2
     C                   WRITE     BLV2R

     C                   ENDIF

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *                                                      INIT /
      *////////////////////////////////////////////////////////////
     C     INIT          BEGSR
     C     KEYMS0        KLIST
     C                   KFLD                    ZMN
     C                   KFLD                    ZGN0
     C     KEYMS1        KLIST
     C                   KFLD                    ZMN
     C                   KFLD                    ZGN1
     C                   KFLD                    ZREPB0
     C     KEYMS2        KLIST
     C                   KFLD                    ZMN
     C                   KFLD                    ZGN2
     C                   KFLD                    ZREPC0
     C                   KFLD                    ZREPC1
     C     KEYMS3        KLIST
     C                   KFLD                    ZMN
     C                   KFLD                    ZGN3
     C                   KFLD                    ZREPD0
     C                   KFLD                    ZREPD1
     C                   KFLD                    ZREPD2

     C     *LIKE         DEFINE    NMN           ZMN
     C     *LIKE         DEFINE    NGN           ZGN0
     C     *LIKE         DEFINE    NGN           ZGN1
     C     *LIKE         DEFINE    NGN           ZGN2
     C     *LIKE         DEFINE    NGN           ZGN3
     C     *LIKE         DEFINE    NREP0         ZREPB0
     C     *LIKE         DEFINE    NREP0         ZREPC0
     C     *LIKE         DEFINE    NREP0         ZREPD0
     C     *LIKE         DEFINE    NREP1         ZREPC1
     C     *LIKE         DEFINE    NREP1         ZREPD1
     C     *LIKE         DEFINE    NREP2         ZREPD2
     C     *LIKE         DEFINE    BLBBNA        WLBBNA
     C     *LIKE         DEFINE    BLREIS        WLREIS
     C     *LIKE         DEFINE    BLREFF        WLREFF
     C     *LIKE         DEFINE    BLKNG         WEKNA
     C     *LIKE         DEFINE    BLNAG         WENAA
     C     *LIKE         DEFINE    BLMN          WLMN
     C     *LIKE         DEFINE    E8154         W8154
     C     *LIKE         DEFINE    BVVS          WVVS
     C     *LIKE         DEFINE    T3128         W3128
     C     *LIKE         DEFINE    C6066         W6066
      * INIT WORKFIELDS
     C                   MOVE      *BLANKS       WLBBNA
     C                   MOVE      *BLANKS       WLREIS
     C                   Z-ADD     0             WEKNA
     C                   MOVE      *BLANKS       WENAA

     C     *ENTRY        PLIST
     C                   PARM                    WMN1              9
     C                   MOVEL     WMN1          WMN2
     C                   Z-ADD     WMN2          ZMN
      *DELAY THIS JOB
     C                   MOVEL(P)  'DLYJOB D'    QCMDR            15
     C                   CAT       'LY(30)':0    QCMDR
     C                   Z-ADD     15            QCMDLE           15 5
     C                   CALL      'QCMDEXC'                            3131
     C                   PARM                    QCMDR
     C                   PARM                    QCMDLE
      * Get msg and interchange values
     C     ZMN           CHAIN     EUNH                               51
     C     ZMN           CHAIN     EDIM                               51
     C     MSN           CHAIN     EDIS                               51

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * LOC16                                               LOC16 /
      *////////////////////////////////////////////////////////////
     C     LOC16         BEGSR
     C                   MOVE      '0'           *IN52
     C                   Z-ADD     16            ZGN1
     C     KEYMS1        SETLL     ELOC93A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS1        READE     ELOC93A                                52
     C     *IN52         DOWEQ     '0'

     C                   ADD       1             FRTLI
     C                   MOVE      *BLANKS       FRTTXT
     C                   SELECT
     C     L3227         WHENEQ    '9'
     C                   EVAL      FRTTXT='Place of loading..........:'
     C                   MOVEL     L3224         BLNTBS
     C     L3227         WHENEQ    '11'
     C                   EVAL      FRTTXT='Place of discharge........:'
     C                   ENDSL
     C     FRTTXT        CAT       L3224:1       FRTTXT
     C                   WRITE     FREETXTR

     C     KEYMS1        READE     ELOC93A                                52
     C  N52              ENDDO
     C                   ENDIF

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * LOC17                                               LOC17 /
      *////////////////////////////////////////////////////////////
     C     LOC17         BEGSR
     C                   MOVE      '0'           *IN53
     C                   Z-ADD     17            ZGN2
     C     KEYMS2        SETLL     ELOC93A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS2        READE     ELOC93A                                53
     C     *IN53         DOWEQ     '0'

     C                   ADD       1             FRTLI
     C                   MOVE      *BLANKS       FRTTXT
     C                   SELECT
     C     L3227         WHENEQ    '1'
     C                   EVAL      FRTTXT='Place of terms of delivery:'
     C     L3227         WHENEQ    '2'
     C                   EVAL      FRTTXT='Final destination.........:'
     C                   ENDSL
     C     FRTTXT        CAT       L3224:1       FRTTXT
     C                   WRITE     FREETXTR

     C     KEYMS2        READE     ELOC93A                                53
     C  N53              ENDDO
     C                   ENDIF

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * MEA34                                               MEA34 /
      *////////////////////////////////////////////////////////////
     C     MEA34         BEGSR
     C                   MOVE      '0'           *IN53
     C                   Z-ADD     34            ZGN2
     C     KEYMS2        SETLL     EMEA93A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS2        READE     EMEA93A                                53
     C     *IN53         DOWEQ     '0'

     C                   MOVEA     M6314         A
     C                   EXSR      GETNUM
     C                   SELECT
     C     M6311         WHENEQ    'WT '
     C                   ADD       ZNUM          BVVKT
     C     M6311         WHENEQ    'VOL'
     C                   ADD       ZNUM          BVM3
     C                   ENDSL

     C     KEYMS2        READE     EMEA93A                                53
     C  N53              ENDDO
     C                   ENDIF
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * NAD25                                               NAD25 /
      *////////////////////////////////////////////////////////////
     C     NAD25         BEGSR
     C                   MOVE      '0'           *IN52
     C                   Z-ADD     25            ZGN1
     C     KEYMS1        SETLL     ENAD93A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS1        READE     ENAD93A                                52
     C     *IN52         DOWEQ     '0'

     C                   SELECT
     C     N3035         WHENEQ    'CZ'                                         GODSAVSENDER
     C     N3036A        IFNE      *BLANKS
     C                   MOVEL     N3036A        BLNAA
     C                   ENDIF
     C     N3042A        IFNE      *BLANKS
     C                   MOVEL     N3042A        BLAD1A
     C                   ENDIF
     C     N3042B        IFNE      *BLANKS
     C                   MOVEL     N3042B        BLAD2A
     C                   ENDIF
     C     N3042C        IFNE      *BLANKS
     C                   MOVEL     N3042C        BLAD3A
     C                   ENDIF
     C     N3251         IFNE      *BLANKS
     C                   MOVEL     N3251         WEADS1            9
     C                   MOVEL     N3164         WEADS2           21
     C                   MOVEL     WEADS1        BLAD3A
     C                   MOVE      WEADS2        BLAD3A
     C                   ENDIF
     C     N3035         WHENEQ    'CN'                                         LEV.ADR
     C     N3036A        IFNE      *BLANKS
     C                   MOVEL     N3036A        BLNAM
     C                   ENDIF
     C     N3042A        IFNE      *BLANKS
     C                   MOVEL     N3042A        BLAD1M
     C                   ENDIF
     C     N3042B        IFNE      *BLANKS
     C                   MOVEL     N3042B        BLAD2M
     C                   ENDIF
     C     N3042C        IFNE      *BLANKS
     C                   MOVEL     N3042C        BLAD3M
     C                   ENDIF
     C     N3251         IFNE      *BLANKS
     C                   MOVEL     N3251         WEADS1
     C                   MOVEL     N3164         WEADS2
     C                   MOVEL     WEADS1        BLAD3M
     C                   MOVE      WEADS2        BLAD3M
     C                   ENDIF
     C     N3035         WHENEQ    'NI'
     C     N3036A        IFNE      *BLANKS
     C                   MOVEL     N3036A        BLNAN
     C                   ENDIF
     C     N3042A        IFNE      *BLANKS
     C                   MOVEL     N3042A        BLAD1N
     C                   ENDIF
     C     N3042B        IFNE      *BLANKS
     C                   MOVEL     N3042B        BLAD2N
     C                   ENDIF
     C     N3042C        IFNE      *BLANKS
     C                   MOVEL     N3042C        BLAD3N
     C                   ENDIF
     C     N3251         IFNE      *BLANKS
     C                   MOVEL     N3251         WEADS1
     C                   MOVEL     N3164         WEADS2
     C                   MOVEL     WEADS1        BLAD3N
     C                   MOVE      WEADS2        BLAD3N
     C                   ENDIF

     C                   ENDSL

     C     KEYMS1        READE     ENAD93A                                52
     C  N52              ENDDO
     C                   ENDIF

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * PCI37                                               PCI37 /
      *////////////////////////////////////////////////////////////
     C     PCI37         BEGSR
     C                   MOVE      BVLI          XBVLI             3 0
      * Først Containernummer
     C     W6066         IFGT      '1'
     C     'SEAL'        CAT(P)    S9308:1       BVMN
     C                   MOVEL     G7065A        BVVS
     C                   MOVEA     *BLANKS       A
     C                   MOVEA     G7224A        A
     C                   EXSR      GETNUM
     C                   ADD       ZNUM          BVNT
     C                   MOVE      *BLANKS       WVVS
     C                   ELSE
     C                   MOVEL     E8260         BVMN
     C                   Z-ADD     1             BVNT
     C                   ENDIF
     C                   WRITE     BLV1R
     C                   Z-ADD     0             BVNT
     C                   Z-ADD     0             BVVKT
     C                   Z-ADD     0             BVM3
     C                   MOVE      *BLANKS       BVVS
      * Deretter SEAL
     C     W6066         IFEQ      '1'
     C     'SEAL'        CAT(P)    S9308:1       BVMN
     C                   ADD       1             BVLI
     C                   ADD       1             BVSORT
     C                   MOVEL     WVVS          BVVS
     C                   WRITE     BLV1R
     C                   ENDIF
     C                   MOVE      XBVLI         BVLI
     C                   MOVE      *BLANKS       BVFT
     C                   MOVE      *BLANKS       BVKD
      * Deretter merking
     C                   MOVE      '0'           *IN53
     C                   Z-ADD     37            ZGN2
     C     KEYMS2        SETLL     EPCI93A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS2        READE     EPCI93A                                53
     C     *IN53         IFEQ      '0'

     C     P7102A        IFNE      *BLANKS
     C                   ADD       1             XV2M
     C                   MOVEL     P7102A        E(XV2M)
     C                   ENDIF

     C     P7102B        IFNE      *BLANKS
     C                   ADD       1             XV2M
     C                   MOVEL     P7102B        E(XV2M)
     C                   ENDIF
     C     P7102C        IFNE      *BLANKS
     C                   ADD       1             XV2M
     C                   MOVEL     P7102C        E(XV2M)
     C                   ENDIF
     C     P7102D        IFNE      *BLANKS
     C                   ADD       1             XV2M
     C                   MOVEL     P7102D        E(XV2M)
     C                   ENDIF
     C     P7102E        IFNE      *BLANKS
     C                   ADD       1             XV2M
     C                   MOVEL     P7102E        E(XV2M)
     C                   ENDIF

     C                   ENDIF
     C                   ENDIF

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Get numerical value                               GETNUM /
      *////////////////////////////////////////////////////////////
     C     GETNUM        BEGSR
     C                   MOVE      *ZEROS        ZHEL
     C                   MOVE      *ZEROS        ZDEC
     C                   MOVE      *ZEROS        B
     C                   Z-ADD     1             AX                2 0
     C                   Z-ADD     1             BX                2 0
      * Get Integer
     C     A(AX)         DOWGE     '0'
     C     ZHEL          MULT      10            ZHEL
     C                   MOVE      A(AX)         ZHEL
     C                   ADD       1             AX
     C                   ENDDO
      * Decimal point ?
     C     A(AX)         IFEQ      '.'
     C     A(AX)         OREQ      ','
     C                   ADD       1             AX
     C     A(AX)         DOWGE     '0'
     C                   MOVE      A(AX)         B(BX)
     C                   ADD       1             AX
     C                   ADD       1             BX
     C                   ENDDO
     C                   MOVEA     B             ZDEC6
     C                   ENDIF
      * Concatenate
     C                   Z-ADD     ZHEL          ZNUM             18 6
     C                   ADD       ZDEC          ZNUM
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * RFF18                                               RFF18 /
      *////////////////////////////////////////////////////////////
     C     RFF18         BEGSR
     C                   MOVE      '0'           *IN52
     C                   Z-ADD     18            ZGN1
     C     KEYMS1        SETLL     ERFF93A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS1        READE     ERFF93A                                52
     C     *IN52         DOWEQ     '0'

     C                   MOVEL(P)  R1153         FSKODE
     C                   MOVEL(P)  R1154         FSSOK
     C                   WRITE     FRISOKR

     C  N52KEYMS1        READE     ERFF93A                                52
     C  N52              ENDDO
     C                   ENDIF

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * RFF29                                               RFF29 /
      *////////////////////////////////////////////////////////////
     C     RFF29         BEGSR
     C                   MOVE      '0'           *IN52
     C                   Z-ADD     29            ZGN1
     C     KEYMS1        SETLL     ERFF93A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS1        READE     ERFF93A                                52
     C     *IN52         DOWEQ     '0'

     C     R1153         IFEQ      'ACL'
     C*                  MOVEL     R1154         HERFA
     C                   MOVE      '1'           *IN52
     C                   ENDIF

     C  N52KEYMS1        READE     ERFF93A                                52
     C  N52              ENDDO
     C                   ENDIF

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * RFF36                                               RFF36 /
      *////////////////////////////////////////////////////////////
     C     RFF36         BEGSR
     C                   MOVE      '0'           *IN53
     C                   Z-ADD     36            ZGN2
     C     KEYMS2        SETLL     ERFF93A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS2        READE     ERFF93A                                53
     C     *IN53         DOWEQ     '0'

     C     R1153         IFEQ      'ON '
     C* Her skal det skrives til ZRISOK (FRISOK) og ZLV2 (BLV2)
     C*
     C* Ikke skriv hvis verdien (R1154) er lik XY
     C*
     C* I ZRISOK skal verdiene ut i en og en record.
     C*
     C* I ZLV2 skal verdien ut sammen.
     C* Skal starte med:    C/ON:
     C* Etterfølg av alle verdier fortløpende skilt med
     C* kommategn og mellomrom.
     C*
     C* Verdiene skal ut på fakturautskrift.

     C     R1154         IFNE      'XY'

     C     1             DO        100           XNRFF2
     C     C(XNRFF2)     IFEQ      *BLANKS
     C                   ADD       1             XNRFF
     C                   MOVEL     R1154         C(XNRFF)

     C                   MOVEL(P)  R1153         FSKODE
     C                   MOVEL(P)  R1154         FSSOK
     C                   WRITE     FRISOKR

     C                   LEAVE
     C                   ENDIF
     C     C(XNRFF2)     IFEQ      R1154
     C                   LEAVE
     C                   ENDIF
     C                   ENDDO
     C                   ENDIF

     C                   ENDIF

     C  N53KEYMS2        READE     ERFF93A                                53
     C  N53              ENDDO
     C                   ENDIF

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * RETREIVE INFO FROM EDIFACT DATABASE TO SYSPED400     RTVD /
      *////////////////////////////////////////////////////////////
     C     RTVD          BEGSR
      *___________________________
      * Behandle ikke replacements
      *"""""""""""""""""""""""""""
     C*    U2005A        IFEQ      '9'
      *_______________________
      * READ ALL MAIN SEGMENTS
      *"""""""""""""""""""""""
      *______
      * CNT00
      *""""""
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     00            ZGN0
     C     KEYMS0        SETLL     ECNT93A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS0        READE     ECNT93A                                51
     C     *IN51         DOWEQ     '0'

     C     C6069         IFEQ      '10 '
     C                   MOVEL     C6066         W6066
     C                   MOVE      '1'           *IN51
     C                   END

     C  N51KEYMS0        READE     ECNT93A                                51
     C  N51              END
     C                   END
      *______
      * RFF01
      *""""""
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     01            ZGN0
     C     KEYMS0        SETLL     ERFF93A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS0        READE     ERFF93A                                51
     C     *IN51         DOWEQ     '0'

     C     R1153         IFEQ      'FF '
     C                   MOVEL     R1154         WLREFF
     C                   MOVE      '1'           *IN51
     C                   END

     C  N51KEYMS0        READE     ERFF93A                                51
     C  N51              END
     C                   END
      *______
      * TDT05
      *""""""
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     5             ZGN0
     C     KEYMS0        SETLL     ETDT93A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS0        READE     ETDT93A                                51
     C     *IN51         DOWEQ     '0'

     C                   MOVEL     T8028         WLREIS
     C                   MOVEL     T8178         WLBBNA
     C                   MOVEL     T3128         W3128

     C  N51KEYMS0        READE     ETDT93A                                51
     C  N51              END
     C                   END
      *______
      * DTM05
      *""""""
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     5             ZGN0
     C     KEYMS0        SETLL     EDTM93A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS0        READE     EDTM93A                                51
     C     *IN51         DOWEQ     '0'

     C     D2005         IFEQ      '232'
     C                   MOVEL     D2380         W2380
     C                   MOVE      '1'           *IN51
     C                   END

     C  N51KEYMS0        READE     EDTM93A                                51
     C  N51              END
     C                   END
      *______
      * EQD13
      *""""""
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     13            ZGN0
     C     KEYMS0        SETLL     EEQD93A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS0        READE     EEQD93A                                51
     C     *IN51         DOWEQ     '0'

     C                   MOVEL     E8154         W8154

     C                   Z-ADD     EREP0         ZREPB0
     C                   EXSR      SEL13

     C  N51KEYMS0        READE     EEQD93A                                51
     C  N51              END
     C                   END
      *___________________
      * READ CONSIGNEMENTS
      *"""""""""""""""""""
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     15            ZGN0
     C     KEYMS0        SETLL     ECNI93A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS0        READE     ECNI93A                                51
     C     *IN51         DOWEQ     '0'

     C                   EXSR      RTVDB

     C     KEYMS0        READE     ECNI93A                                51
     C  N51              ENDDO
     C                   ENDIF

     C*                  ENDIF

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * RETREIVE INFO FROM EDIFACT DATABASE TO SYSPED400    RTVDB /
      *////////////////////////////////////////////////////////////
     C     RTVDB         BEGSR
      *______________________________
      * Finnes BL allerede i SYSPED ?
      *""""""""""""""""""""""""""""""
     C     12            SUBST(P)  C1004:15      BLBLNR
     C     BLBLNR        CHAIN     BILLOFLG                           55
     C     *IN55         CABEQ     *OFF          RTVDB9
      *__________________
      * INIT. DATAFELTER
      *""""""""""""""""""
      * OPPDRAG
     C                   CLEAR                   BILLOFR
     C                   MOVE      'F'           BLUR
     C                   MOVEL     'EDI'         BLSG
     C                   MOVEL     C1004         BLBLNR
     C     12            SUBST(P)  C1004:15      BLBLNR
     C                   MOVEL     WLBBNA        BLBBNA
     C                   MOVEL     WLREIS        BLREIS
     C                   MOVEL     C1490         BLBREI
     C                   MOVEL     W6066         BLAD2G
     C                   MOVEL     W3128         BLAD4G
     C                   MOVEL     E8260         BLAD3G
     C                   MOVEL     WLMN          BLMN
     C                   MOVEL     W8154         BLMM
     C     W6066         IFGT      '1'
     C                   MOVEL     E8260         BLMN
     C                   ENDIF
     C                   Z-ADD     20000000      BLDTA
     C     W2380         IFNE      *BLANKS
     C                   ADD       W2380N        BLDTA
     C                   ENDIF
     C                   Z-ADD     20            BLACC
     C                   MOVEL     SLIB          BLLIB
     C                   MOVEL     SFILE         BLFILE
     C                   MOVEL     SMBR          BLMBR
     C                   MOVE      'IFCSUM'      BLTYPE
     C                   MOVE      '93A'         BLLVL
     C                   Z-ADD     ZMN           BLSNN
     C     1             CHAIN     EDIC                               20
     C                   ADD       1             CMN
     C                   UPDATE    EDICR
     C                   Z-ADD     CMN           BLUNIK
     C                   MOVEL     BLUNIK        BLREFF
     C                   Z-ADD     0             WG7224AN
      * FRITEKST
     C                   CLEAR                   FREETXTR
     C                   Z-ADD     BLUNIK        FRUNIK
     C                   MOVEL     BLREFF        FRREFF
      * FRIE SØKEVEIER
     C                   CLEAR                   FRISOKR
     C                   Z-ADD     BLUNIK        FSUNIK
     C                   MOVEL     BLREFF        FSREFF
      * Båt
     C                   ADD       1             FRTLI
     C                   MOVE      *BLANKS       FRTTXT
     C                   EVAL      FRTTXT='Vessel....................:'
     C     FRTTXT        CAT       WLBBNA:1      FRTTXT
     C                   WRITE     FREETXTR
      * Container og Type
     C                   ADD       1             FRTLI
     C                   MOVE      *BLANKS       FRTTXT
     C                   EVAL      FRTTXT='Container.................:'
     C     FRTTXT        CAT       E8260:1       FRTTXT
     C     FRTTXT        CAT       E8154:1       FRTTXT
     C                   WRITE     FREETXTR
      *__________________
      * READ ALL SEGMENTS
      *""""""""""""""""""
     C                   Z-ADD     CREP0         ZREPB0
     C                   EXSR      FTX15
     C                   EXSR      LOC16
     C                   EXSR      TOD17
     C                   EXSR      RFF18
     C                   EXSR      NAD25
     C                   EXSR      RFF29
     C                   EXSR      GID31
      *___________________
      * WRITE TO SYSPED400
      *"""""""""""""""""""
     C                   WRITE     BILLOFR

     C     RTVDB9        TAG

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SEL13                                               SEL13 /
      *////////////////////////////////////////////////////////////
     C     SEL13         BEGSR
     C                   MOVE      '0'           *IN52
     C                   Z-ADD     13            ZGN1
     C     KEYMS1        SETLL     ESEL93A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS1        READE     ESEL93A                                52
     C     *IN52         DOWEQ     '0'

     C                   MOVEL     S9308         W9308            10

     C  N52KEYMS1        READE     ESEL93A                                52
     C  N52              ENDDO
     C                   ENDIF

     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      * TOD17                                               TOD17 /
      *////////////////////////////////////////////////////////////
     C     TOD17         BEGSR
     C                   MOVE      '0'           *IN52
     C                   Z-ADD     17            ZGN1
     C     KEYMS1        SETLL     ETOD93A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS1        READE     ETOD93A                                52
     C     *IN52         IFEQ      '0'
     C                   SELECT
     C     T4053         WHENEQ    'FOB'
     C                   MOVE      'C'           BLCP
     C     T4053         WHENEQ    'C+F'
     C                   MOVE      'P'           BLCP
     C                   ENDSL

     C                   Z-ADD     TREP0         ZREPC0
     C                   Z-ADD     TREP1         ZREPC1
     C                   EXSR      LOC17

     C                   ENDIF
     C                   ENDIF

     C                   ENDSR
