      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/TJGE27RL MODULE(*LIBL/TJGE27RL *LIBL/INCGI)
CRTPGM*        ACTGRP(*NEW) AUT(*USE)
      *
      *********************************************************************
      /copy SYSPEDS/qrpgsrcpy,hspecs
      /copy SYSPEDS/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     iF   E           K DISK    USROPN
     FKOSBU     IF   E           K DISK    USROPN
     FKOSBU13   IF   E           K DISK    USROPN
     F                                     RENAME(kosbur:kosbur13)
     FKOSBU12   IF   E           K DISK    USROPN
     F                                     RENAME(kosbur:kosbur12)
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy SYSPEDS/qrpgsrcpy,prototypeb
      /copy SYSPEDS/qrpgsrcpy,usec
      /copy SYSPEDS/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
      * Variables received from the input string
     D WSUSER          S             10
     D AVD             S              4
     D AVDN            S              4S 0
     D OPD             S              7
     D OPDN            S              7S 0
     D TUR             S              8
     D TURN            S              8S 0
     D BNR             S              7
     D BNRN            S              7S 0
     D TYPE            s              1
     D Error           S            150
     D JSONstring      s          64000
     D ANTLEST         s              9  0
      *get input-string
      /copy syspeds/qrpgsrcpy,prolog3
      * Parse input
     C                   EXSR      Parse
      * Open files etc
     C                   EXSR      INIT

      *
     C                   CALLP     gethtmlifs(
     C                             '/syspeddev/html/JSON.html':
     C                             '/CGITAG/')
     C                   CALLP     wrtsection('top')
      * skriv JSON-Object start..(alltid '{' + x ant param. m/komma mellom (IKKE komma etter siste))
      /free
        JSONstring='{'
        +'¬user¬ : ¬'+%trim(WSUSER)+'¬, '
        +'¬avd¬ : ¬'+%trim(%editc(AVDN:'Z'))+'¬, '
        +'¬opd¬ : ¬'+%trim(%editc(OPDN:'Z'))+'¬, '
        +'¬tur¬ : ¬'+%trim(tur)+'¬, '
        +'¬type¬ : ¬'+TYPE+'¬, '
        +'¬errMsg¬ : ¬'+%trim(Error)+'¬, ';
      /end-free
      * JSONfix escaper " i tekst,  for deretter å bytte ¬->"
     C                   CALL      'JSONFIX'
     C                   PARM                    JSONstring
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%ADDr(JSONstring)
     C                                         :%len(JSONstring))
     C                   CALLP     wrtsection('JSONlin')

      * Hent Buds-poster
     C                   EXSR      GetBud
      * Skriv JSON-string Avsluttning
     C                   EVAL      JSONstring ='}'
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%ADDr(JSONstring)
     C                                         :%len(JSONstring))
     C                   CALLP     wrtsection('JSONlin')

     C     SLUTT         TAG
     C                   EXSR      EXIT
     C                   return
      *=====================================================================******
      * Parse input
      *=====================================================================******
     C     Parse         BEGSR
      * Get input
     C                   EVAL      WSUSER = zhbgetvar('USER')
     C                   EVAL      AVD    = zhbgetvar('AVD')
     C                   EVAL      AVDN   = c2n2(AVD)
     C                   EVAL      OPD    = zhbgetvar('OPD')
     C                   EVAL      OPDN = c2n2(OPD)
     C                   EVAL      TUR    = zhbgetvar('TUR')
     C                   EVAL      TURN = c2n2(TUR)
     C                   EVAL      BNR        = zhbgetvar('BNR')
     C                   EVAL      BNRN       = c2n2(BNR)
      * Type : O = Open / A = All
     C                   EVAL      TYPE   = zhbgetvar('TYPE')
     C                   IF        TYPE   = ' '
     C                   EVAL      TYPE   = 'O'                                 Åpne
     C                   ENDIF
     C                   ENDSR
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          BEGSR
      *
     C     OpdKey        klist
     C                   kfld                    AVDN
     C                   kfld                    OPDN
      *
     C                   OPEN      BRIDF
     C     WSUSER        CHAIN(N)  BRIDF                              50
      * En "standardvariant" libl: CALL bound (INCGI=*MODULE) med parameter.
     C     BILIBL        IFEQ      *BLANKS
     C                   CALLb     'INCGI'
     C                   PARM                    BISTDL
     C                   ELSE
      * Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   CALL      BILIBL
     C                   END
     C   50              eval      Error = 'Invalid userID'
     C  N50              OPEN      KOSBU
     C  N50              OPEN      KOSBU13
     C  N50              OPEN      KOSBU12
      *

     C     ENDINI        ENDSR
      *=====================================================================******
      *  AVSLUTT
      *=====================================================================******
     C     EXIT          BEGSR
     C                   CLOSE     BRIDF
     C  N50              CLOSE     KOSBU
     C  N50              CLOSE     KOSBU13
     C  N50              CLOSE     KOSBU12
     C                   CALLP     wrtsection('*fini')
     C                   EVAL      *INlr = *on

     C                   ENDSR
      **************************************************************
     C     GetBud        BEGSR
      **************************************************************
      * Skriv JSON-LISTE Start
     C                   EVAL      JSONstring ='"budgetLines" : ['
     C                   CALLP     UPdHTMLvar2('JSONstring'
     C                                         :%ADDr(JSONstring)
     C                                         :%len(JSONstring))
     C                   CALLP     wrtsection('JSONlin')
      *
     c     Error         IFEQ      *blanks
     c     BNRN          andeq     *zeros
     c     TURN          andeq     *zeros
     c     OPDN          andeq     *zeros
     C                   eval      Error = 'Turnr eller avd/oppdrag (eller EN '
     c                             + 'budsjettprost) må gis'
     c                   ENDIF
     c     Error         IFEQ      *blanks
      * BNR- oppgitt = Hent EN linje
     C     BNRN          IFNE      *ZEROS
     C     BNRN          CHAIN     KOSBU                              33
     c     *in33         ifeq      '1'
     C                   eval      Error = 'Finner ikke busjettpost ( '
     c                                   + %trim(%editc(BNRN:'3')) +' )'
     c                   else
      * ved henting av 1 - ubetinget / uavhengig av status ..
     c                   exsr      SkrivPost
     c                   endif
     c                   goto      UtGet
     c                   endif
      *----------------------------------------------------------------
      * les alle Budsj-poster  / skipp basert på type (O=Open / A=Alle)
      *----------------------------------------------------------------
     C     OPDN          IFNE      *ZEROS
     C     OpdKey        SETLL     KOSBU12
     C     OpdKey        READE     KOSBU12                                33
     C                   ELSE
     C     TURN          SETLL     KOSBU13
     C     TURN          READE     KOSBU13                                33
     C                   ENDIF
     C     *IN33         DOWeq     *OFF

      *
     C                   if        Type <> 'A' and BUST <> ' '                  A=all
     C                   goto      NextBud
     C                   endif
      *
     c                   exsr      SkrivPost
      *
     C     NextBud       tag
     C     OPDN          IFNE      *ZEROS
     C     OpdKey        READE     KOSBU12                                33
     C                   ELSE
     C     TURN          READE     KOSBU13                                33
     C                   ENDIF
     C  N33              ENDDO
      *
     c                   ENDIF                                                  End error=*blanks
      *----------------------------------------------------------------
      * Skriv JSON-Liste/Array  Avslutning
     C     UtGet         tag
     C                   EVAL      JSONstring =']'
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%ADDr(JSONstring)
     C                                         :%len(JSONstring))
     C                   CALLP     wrtsection('JSONlin')

     C                   ENDSR
      *******************************************************
     C     SkrivPost     BEGSR
      *******************************************************
     C                   ADD       1             ANTLEST
      * hente tillegsdata
      * ....
      /free
       if AntLest=1;
          JSONstring=*blanks;
          else;
          JSONstring=', ';
          endif;
       JSONstring=%trim(JSONstring)+'{'
        +'¬bust¬: ¬'+%trim(bust)+'¬, '
        +'¬bubnr¬ : ¬'+%trim(%editc(bubnr:'3'))+'¬, '
        +'¬bupCc¬ : ¬'+%trim(%editc(bupCC:'3'))+'¬, '
        +'¬bupAr¬ : ¬'+%trim(%editc(bupÅR:'3'))+'¬, '
        +'¬bupMn¬ : ¬'+%trim(%editc(bupMN:'3'))+'¬, '
        +'¬butnr¬ : ¬'+%trim(%editc(buTNR:'3'))+'¬, '
        +'¬bulnr¬ : ¬'+%trim(%editc(buLNR:'3'))+'¬, '
        +'¬buval¬ : ¬'+%trim(buVAL)+'¬, '
        +'¬bubl¬ : ¬'+%trim(%editc(buBL:'M'))+'¬, '
        +'¬buvk¬ : ¬'+%trim(buVK)+'¬, '
        +'¬busg¬ : ¬'+%trim(BUSG)+'¬, '
        +'¬butavd¬ : ¬'+%trim(%editc(buTAVD:'3'))+'¬, '
        +'¬butunr¬ : ¬'+%trim(%editc(buTunr:'3'))+'¬, '
        +'¬buoavd¬ : ¬'+%trim(%editc(buOAVD:'3'))+'¬, '
        +'¬buopd¬ : ¬'+%trim(%editc(buOPD:'3'))+'¬, '
        +'¬bublst¬ : ¬'+%trim(buBLST)+'¬, '
        +'¬butype¬ : ¬'+%trim(buTYPE)+'¬, '
        +'¬butxt¬ : ¬'+%trim(buTXT)+'¬, '
        +'¬bubilk¬ : ¬'+%trim(buBILK)+'¬, '
        +'¬bubiln¬ : ¬'+%trim(buBILN)+'¬, '
        +'¬bufedt¬ : ¬'+%trim(%editc(bufedt:'3'))+'¬, '
        +'¬bufer¬ : ¬'+%trim(buFER)+'¬, '
        +'¬bubnr2¬ : ¬'+%trim(%editc(bubnr2:'3'))+'¬, '
        +'¬bubnr3¬ : ¬'+%trim(%editc(bubnr3:'3'))+'¬, '
        +'¬bulnr3¬ : ¬'+%trim(%editc(bulnr3:'3'))+'¬, '
        +'¬bublfd¬ : ¬'+%trim(%editc(buBLFD:'M'))+'¬, '              // Beløp i Valuta
        +'¬bukdm¬ : ¬'+%trim(buKDM)+'¬}';
      /end-free
     C                   CALL      'JSONFIX'
     C                   PARM                    JSONstring
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%ADDr(JSONstring)
     C                                         :%len(JSONstring))
     C                   CALLP     wrtsection('JSONlin')

     C                   ENDSR
