      *
      ***********************************************************
      *** PROGRAM SKAL PLUKKE KODER TIL EL. TRANSITTERING     ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 02 04 07-11 13-24
      *** LEDIGE INDIKATORER 31-34 36 37 39-90 95-97
      ***********************************************************
     FDKXKODL01 IF   E           K DISK
     F                                     RENAME(DKXKODFR:DKXKODFR1)
     FDKXKODL02 IF   E           K DISK
     F                                     RENAME(DKXKODFR:DKXKODFR2)
     FDKXKODL03 IF   E           K DISK
     F                                     RENAME(DKXKODFR:DKXKODFR3)
     FDKX002D   CF   E             WORKSTN
     F                                     SFILE(DK002D11:ZRECNO)
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
      *
     C                   EXSR      INIT
      *
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     DK002D12
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C   27DKXK1K        SETLL     DKXKODL01
     C   28DKXK2K        SETLL     DKXKODL02
     C   29DKXK3K        SETLL     DKXKODL03
     C     START1        TAG
     C   27UUNIK         READE     DKXKODL01                              94
     C   28UUNIK         READE     DKXKODL02                              94
     C   29UUNIK         READE     DKXKODL03                              94
     C  N94
     CAN 38              SELECT
     C     UTYPE         WHENEQ    'ANK'
     C     TKANK         CABEQ     'N'           START1
     C     UTYPE         WHENEQ    'TRS'
     C     TKTRS         CABEQ     'N'           START1
     C     UTYPE         WHENEQ    'AVG'
     C     TKAVG         CABEQ     'N'           START1
     C                   ENDSL
      *
     C     START2        TAG
      *
     C  N94              DO
     C                   DO        5
     C                   ADD       1             ZRECNO
     C     XTYPE         IFEQ      'N'
     C                   MOVE      TKTXTN        TKTXT
     C                   ELSE
     C                   MOVE      TKTXTE        TKTXT
     C                   END
     C                   WRITE     DK002D11
     C     ST21          TAG
     C   27UUNIK         READE     DKXKODL01                              94
     C   28UUNIK         READE     DKXKODL02                              94
     C   29UUNIK         READE     DKXKODL03                              94
     C  N94
     CAN 38              SELECT
     C     UTYPE         WHENEQ    'ANK'
     C     TKANK         CABEQ     'N'           ST21
     C     UTYPE         WHENEQ    'TRS'
     C     TKTRS         CABEQ     'N'           ST21
     C     UTYPE         WHENEQ    'AVG'
     C     TKAVG         CABEQ     'N'           ST21
     C                   ENDSL
     C  N94              END
     C                   END
      *
     C                   Z-ADD     ZRECNO        YRECNO            5 0
     C     *IN35         IFEQ      '0'
     C                   Z-ADD     4             LINNBR
     C                   Z-ADD     16            POSNBR
     C                   MOVE      '1'           *IN35
     C                   ELSE
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     2             POSNBR
     C                   END
      *
     C     START3        TAG
     C                   SETOFF                                       9998
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     DK002D14
     C                   ELSE
     C                   WRITE     DK002D13
     C                   EXFMT     DK002D12
     C                   END
      *
     C                   SETOFF                                       9830
      *** AVSLUTT
     C   03
     COR 12              GOTO      SLUTT
      *** FRISK OPP
     C   05              GOTO      START
      *** LAGE NY
     C   06              DO
     C                   CALL      'DKX001C'
     C                   PARM                    UUNIK
     C                   GOTO      START
     C                   END
      *
      *** BLA VIDERE
     C   25              DO
     C                   Z-ADD     YRECNO        ZRECNO
     C                   SETOFF                                       25
     C                   GOTO      START2
     C                   END
      *
     C     W2NR          IFEQ      '1'
     C     W2KODE        ANDNE     *BLANKS
     C                   MOVE      W2KODE        UKODE
     C                   GOTO      SLUTT
     C                   END
      *
     C                   SELECT
     C     W2KODE        WHENNE    *BLANKS
     C                   MOVEA     '100'         *IN(27)
     C                   GOTO      START
     C     W2TXT         WHENNE    *BLANKS
     C                   MOVE      W2TYPE        XTYPE             1
     C     W2TYPE        IFEQ      'N'
     C                   MOVEA     '010'         *IN(27)
     C                   ELSE
     C                   MOVEA     '001'         *IN(27)
     C                   END
     C                   GOTO      START
     C                   ENDSL
      *
     C     ZRECNO        IFEQ      0
     C                   GOTO      START3
     C                   ELSE
     C                   EXSR      HENT
     C   98              GOTO      START3
     C                   END
      *
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
      *
     C***********************************************
     C     HENT          BEGSR
     C***********************************************
     C                   SETOFF                                       9192
     C     *IN98         DOWEQ     '0'
     C                   READC     DK002D11                               98
     C  N98WSNR          IFEQ      '1'
     C                   MOVE      TKKODE        UKODE
     C                   GOTO      SLHENT
     C                   END
     C                   END
      *
     C     SLHENT        TAG
     C     *IN98         IFEQ      '1'
     C                   MOVE      'DIV0012'     MSGNR
     C                   SETON                                            30
     C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     DKXK1K        KLIST
     C                   KFLD                    UUNIK
     C                   KFLD                    W2KODE
     C     DKXK2K        KLIST
     C                   KFLD                    UUNIK
     C                   KFLD                    W2TXT
     C     DKXK3K        KLIST
     C                   KFLD                    UUNIK
     C                   KFLD                    W2TXT
      *
     C     *ENTRY        PLIST
     C                   PARM                    UUNIK             3
     C                   PARM                    UKODE            10
     C                   PARM                    UTYPE             3
      *
     C                   WRITE     WINDOWSZ
     C                   MOVEA     '100'         *IN(27)
     C                   MOVE      'N'           W2TYPE
     C                   MOVE      'N'           XTYPE
     C     UUNIK         COMP      '106'                                  38
     C                   SELECT
     C     UUNIK         WHENEQ    '012'
     C                   MOVE      'DKX0795'     MSGNRW
     C     UUNIK         WHENEQ    '013'
     C                   MOVE      'DKX0796'     MSGNRW
     C     UUNIK         WHENEQ    '014'
     C                   MOVE      'DKX0797'     MSGNRW
     C     UUNIK         WHENEQ    '017'
     C                   MOVE      'DKX0798'     MSGNRW
     C     UUNIK         WHENEQ    '029'
     C                   MOVE      'DKX0799'     MSGNRW
     C     UUNIK         WHENEQ    '039'
     C                   MOVE      'DKX0800'     MSGNRW
     C     UUNIK         WHENEQ    '047'
     C                   MOVE      'DKX0801'     MSGNRW
     C     UUNIK         WHENEQ    '105'
     C                   MOVE      'DKX0802'     MSGNRW
     C     UUNIK         WHENEQ    '106'
     C                   MOVE      'DKX0803'     MSGNRW
     C     UUNIK         WHENEQ    '041'
     C                   MOVE      'DKX0829'     MSGNRW
     C     UUNIK         WHENEQ    '042'
     C                   MOVE      'DKX0830'     MSGNRW
     C                   ENDSL
      *
     C                   ENDSR
      *
