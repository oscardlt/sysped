*****A**************************************************************
     A* RETTINGER I DETTE PROGRAM:
PTFnrA*Sek Sig Vers Beskrivelse...........................
"""""A*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
P0502A* 01 JOV VER5 GUL FARGE PÅ INPUT-FELT, M.M.
P0509A* 02 JOV Y2K  IKKE EDITKODE PÅ PERIODE   M.
P0509A* 02 JOV CURSOR BILDE 1
PXXXXA* 03 YBC REGISTRERINGSDATO OG TID
PXXXXA* 04 SH  OMBYTTINGER PÅ  SKJERMBILDET
PXXXXA* 05 TR  VARIABEL MVA
PXXXXA* 06 JOV YTTERLIGERE OMBYTTINGER PÅ  SKJERMBILDET (F.DATO/SIGN)
P8068 * 17 TH PTF8 NY HELP - FUNKSJON KUN DDS
P80XX * 18 SH PTF8 FAKTURANUMMER ER ØKT
P80XX * 20 SH PTF8 F23 PÅ BILDE01
*****A**************************************************************
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
S17  A*                                     HLPDOC('SYKS020010' 'SYKS02' 'SY400-
S17  A*                                     LO/TRANS')
N17  A                                      HLPPNLGRP(HLPSCR SYKS02PNL)
N17  A                                      HLPTITLE('KOSTNADSFØRINGSSYSTEM F-
N17  A                                      ØRING AV KOSTNADER')
     A                                      HELP
     A                                      ALTHELP
     A          R BILDE01
     A                                      CF03(03 'Avslutt')
     A                                      CF04(04 'SØK')
     A                                      CF12(03)
     A                                      CF08(08 'PLUKK BUDSJ.P')
     A                                      CF10(10 'TIL FORDELING')
N20  A                                      CF23(23 'GODTA PERIODE')
     A                                      CSRLOC(LINNBR     POSNBR)
S17  A*         H                           HLPDOC('SYKS020020' 'SYKS02' 'SY400-
S17  A*                                     LO/TRANS')
N17  A          H                           HLPPNLGRP(SYKS020020 SYKS02PNL)
     A                                      HLPARA(05 001 05 080)
N17  A          H                           HLPPNLGRP(SYKS020021 SYKS02PNL)
N17  A                                      HLPARA(06 001 06 080)
N17  A          H                           HLPPNLGRP(SYKS020022 SYKS02PNL)
N17  A                                      HLPARA(07 001 07 080)
N17  A          H                           HLPPNLGRP(SYKS020023 SYKS02PNL)
N17  A                                      HLPARA(08 001 08 080)
N17  A          H                           HLPPNLGRP(SYKS020024 SYKS02PNL)
N17  A                                      HLPARA(09 001 09 080)
S17  A*         H                           HLPDOC('SYKS020080' 'SYKS02' 'SY400-
S17  A*                                     LO/TRANS')
N17  A          H                           HLPPNLGRP(SYKS020080 SYKS02PNL)
     A                                      HLPARA(23 001 23 080)
     A            MSGNR          7A  P
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/SYKS02/1'
     A                                      COLOR(BLU)
     A                                  1 24MSGCON(030 DIV2149 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 61DATE
     A                                      EDTCDE(Y)
     A                                  1 71TIME
     A                                  3  2MSGCON(030 JOV0379 *LIBL/SYMSGF)
     A            WSBNR          7S 0O  3 38COLOR(WHT)
     A                                  3 54MSGCON(015 JOV0266 *LIBL/SYMSGF)
     A  88                                  DSPATR(ND)
     A            GMBNR          7S 0O  3 70COLOR(WHT)
     A  88                                  DSPATR(ND)
     A                                  5  2MSGCON(030 DIV2150 *LIBL/SYMSGF)
     A            WSBSR          1A  B  5 38TEXT('BILAGSSERIE')
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A  99                                  DSPATR(PC)
     A                                  6  2MSGCON(030 DIV2152 *LIBL/SYMSGF)
     A            WSBNR2         7Y 0B  6 38TEXT('BILAGSNUMMER')
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A                                      DSPATR(PC)
     A  98                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                  6 54MSGCON(015 JOV0266 *LIBL/SYMSGF)
     A  89                                  DSPATR(ND)
     A            GMBNR2         7S 0O  6 70
     A  89                                  DSPATR(ND)
     A                                      COLOR(WHT)
     A                                  7  2MSGCON(030 DIV2154 *LIBL/SYMSGF)
     A            WSBDT          6Y 0B  7 38TEXT('BILAGSDATO')
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A  97                                  DSPATR(PC)
     A  97                                  DSPATR(RI)
     A                                      EDTWRD('      ')
     A                                  8  2MSGCON(030 DIV2155 *LIBL/SYMSGF)
     A            WSPMN          2Y 0B  8 38TEXT('PERIODE-MÅNED')
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A  96                                  DSPATR(PC)
     A  96                                  DSPATR(RI)
     A            WSPÅR          2Y 0B  8 41TEXT('PERIODE-ÅR')
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A                                  8 50MSGCON(025 JOV0267 *LIBL/SYMSGF)
     A                                  9  2MSGCON(030 DIV2233 *LIBL/SYMSGF)
     A            WSTXT     R        B  9 38REFFLD(KOSBUR/BUTXT *LIBL/KOSBU)
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A                                      CHECK(LC)
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(018 DIV0032 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 42MSGCON(018 JOV0331 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 62MSGCON(018 DIV4222 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                             24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A          R BILDE02
     A                                      CF03(03 'Avslutt')
     A                                      CF04(04 'Søking')
     A                                      CF08(08 'PLUKK BUDSJ.P')
     A                                      CF12(12 'Forrige')
     A                                      CF23(23 'BEKREFTE')
     A                                      CSRLOC(LINNBR     POSNBR)
S17  A*         H                           HLPDOC('SYKS020020' 'SYKS02' 'SY400-
S17  A*                                     LO/TRANS')
N17  A          H                           HLPPNLGRP(SYKS020020 SYKS02PNL)
     A                                      HLPARA(05 001 05 080)
N17  A          H                           HLPPNLGRP(SYKS020021 SYKS02PNL)
N17  A                                      HLPARA(06 001 06 080)
N17  A          H                           HLPPNLGRP(SYKS020022 SYKS02PNL)
N17  A                                      HLPARA(07 001 07 080)
N17  A          H                           HLPPNLGRP(SYKS020023 SYKS02PNL)
N17  A                                      HLPARA(08 001 08 080)
N17  A          H                           HLPPNLGRP(SYKS020024 SYKS02PNL)
N17  A                                      HLPARA(09 001 09 080)
S17  A*         H                           HLPDOC('SYKS020030' 'SYKS02' 'SY400-
S17  A*                                     LO/TRANS')
N17  A          H                           HLPPNLGRP(SYKS020030 SYKS02PNL)
     A                                      HLPARA(10 001 10 080)
S17  A*         H                           HLPDOC('SYKS020030' 'SYKS02' 'SY400-
S17  A*                                     LO/TRANS')
N17  A          H                           HLPPNLGRP(SYKS020031 SYKS02PNL)
N17  A                                      HLPARA(11 001 11 016)
N17  A          H                           HLPPNLGRP(SYKS020032 SYKS02PNL)
N17  A                                      HLPARA(11 018 11 039)
N17  A          H                           HLPPNLGRP(SYKS020033 SYKS02PNL)
N17  A                                      HLPARA(11 041 11 080)
S17  A*         H                           HLPDOC('SYKS020040' 'SYKS02' 'SY400-
S17  A*                                     LO/TRANS')
N17  A          H                           HLPPNLGRP(SYKS020040 SYKS02PNL)
     A                                      HLPARA(12 001 12 016)
N17  A          H                           HLPPNLGRP(SYKS020041 SYKS02PNL)
N17  A                                      HLPARA(12 019 12 038)
N17  A          H                           HLPPNLGRP(SYKS020042 SYKS02PNL)
N17  A                                      HLPARA(12 041 12 055)
S17  A*         H                           HLPDOC('SYKS020080' 'SYKS02' 'SY400-
S17  A*                                     LO/TRANS')
N17  A          H                           HLPPNLGRP(SYKS020080 SYKS02PNL)
     A                                      HLPARA(23 001 23 080)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1  2'SYSPED/SYKS02/2'
     A                                      COLOR(BLU)
     A                                  1 24MSGCON(030 DIV2149 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 61DATE
     A                                      EDTCDE(Y)
     A                                  1 71TIME
     A                                  3  2MSGCON(030 JOV0379 *LIBL/SYMSGF)
     A            WSBNR          7S 0O  3 38COLOR(WHT)
     A                                  5  2MSGCON(030 DIV2150 *LIBL/SYMSGF)
     A            WSBSR          1A  O  5 38TEXT('BILAGSSERIE')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  6  2MSGCON(030 DIV2152 *LIBL/SYMSGF)
     A            WSBNR2         7Y 0O  6 38TEXT('BILAGSNUMMER')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  7  2MSGCON(030 DIV2154 *LIBL/SYMSGF)
     A            WSBDT          6Y 0B  7 38TEXT('BILAGSDATO')
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A                                      EDTWRD('      ')
     A                                  8  2MSGCON(030 DIV2155 *LIBL/SYMSGF)
     A            WSPMN          2Y 0B  8 38TEXT('PERIODE-MÅNED')
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A            WSPÅR          2Y 0B  8 41TEXT('PERIODE-ÅR')
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A                                  8 50MSGCON(025 JOV0267 *LIBL/SYMSGF)
     A                                  9  2MSGCON(030 DIV2233 *LIBL/SYMSGF)
     A            WSTXT     R        B  9 38REFFLD(KOSBUR/BUTXT *LIBL/KOSBU)
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A  92                                  DSPATR(RI)
     A                                      CHECK(LC)
     A                                 10  2MSGCON(030 JOV0285 *LIBL/SYMSGF)
     A            WSLNR          8Y 0B 10 38TEXT('LEVERANDØRSNR.')
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A  99                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                 10 47MSGCON(010 JOV0284 *LIBL/SYMSGF)
     A            WSSNAV        22A  B 10 58TEXT('SØKENAVN')
     A                                      COLOR(YLW)
     A                                 11  2MSGCON(010 DIV2160 *LIBL/SYMSGF)
     A            WSVK           3A  B 11 14TEXT('GEBYRKODE')
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A  98                                  DSPATR(RI)
     A                                 11 18MSGCON(006 DIV2161 *LIBL/SYMSGF)
     A            WSBL          13Y 2B 11 25TEXT('BELØP')
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A  97                                  DSPATR(RI)
     A                                      EDTCDE(M)
     A                                 11 41MSGCON(010 DIV4223 *LIBL/SYMSGF)
     A            WSMVA          1A  B 11 52TEXT('MVA-KODE')
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
S05  A*                                     VALUES(' ' 'P' 'F' '+')
     A                                 11 54MSGCON(010 DIV4224 *LIBL/SYMSGF)
     A            WSBLM         13Y 2B 11 65TEXT('MVA-BELØP')
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A  91                                  DSPATR(RI)
     A                                      EDTCDE(M)
     A                                 12  2MSGCON(011 DIV2162 *LIBL/SYMSGF)
     A            WSVAL          3A  B 12 14TEXT('VALUTAKODE')
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A  96                                  DSPATR(RI)
     A                                 12 19MSGCON(005 DIV2163 *LIBL/SYMSGF)
     A            WSVKU          7Y 4B 12 31TEXT('VALUTAKURS')
     A                                      COLOR(YLW)
     A  94                                  DSPATR(RI)
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COMP(GE ,0000)
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(018 DIV0032 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 42MSGCON(018 JOV0331 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 62MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                             24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A          R BILDE03A
     A N29                                  CF03(03 'Avslutt')
     A N29                                  CF09(09 'Førsteg.+ATTBIL')
     A N29                                  CF10(10 'Førstegangsreg.')
     A                                      CF12(12 'Forrige')
S17  A*         H                           HLPDOC('SYKS020020' 'SYKS02' 'SY400-
S17  A*                                     LO/TRANS')
S17  A*                                     HLPARA(02 001 09 080)
N17  A          H                           HLPPNLGRP(SYKS020020 SYKS02PNL)
N17  A                                      HLPARA(05 001 05 080)
N17  A          H                           HLPPNLGRP(SYKS020021 SYKS02PNL)
N17  A                                      HLPARA(06 001 06 080)
N17  A          H                           HLPPNLGRP(SYKS020022 SYKS02PNL)
N17  A                                      HLPARA(07 001 07 080)
N17  A          H                           HLPPNLGRP(SYKS020023 SYKS02PNL)
N17  A                                      HLPARA(08 001 08 080)
N17  A          H                           HLPPNLGRP(SYKS020024 SYKS02PNL)
N17  A                                      HLPARA(09 001 09 080)
S17  A*         H                           HLPDOC('SYKS020030' 'SYKS02' 'SY400-
S17  A*                                     LO/TRANS')
N17  A          H                           HLPPNLGRP(SYKS020030 SYKS02PNL)
     A                                      HLPARA(10 001 10 080)
S17  A*         H                           HLPDOC('SYKS020030' 'SYKS02' 'SY400-
S17  A*                                     LO/TRANS')
S17  A*         H                           HLPDOC('SYKS020030' 'SYKS02' 'SY400-
S17  A*                                     LO/TRANS')
N17  A          H                           HLPPNLGRP(SYKS020031 SYKS02PNL)
N17  A                                      HLPARA(11 001 11 016)
N17  A          H                           HLPPNLGRP(SYKS020032 SYKS02PNL)
N17  A                                      HLPARA(11 018 11 039)
N17  A          H                           HLPPNLGRP(SYKS020033 SYKS02PNL)
N17  A                                      HLPARA(11 018 11 039)
S17  A*         H                           HLPDOC('SYKS020040' 'SYKS02' 'SY400-
S17  A*                                     LO/TRANS')
N17  A          H                           HLPPNLGRP(SYKS020040 SYKS02PNL)
     A                                      HLPARA(11 001 12 016)
N17  A          H                           HLPPNLGRP(SYKS020041 SYKS02PNL)
N17  A                                      HLPARA(12 019 12 038)
N17  A          H                           HLPPNLGRP(SYKS020042 SYKS02PNL)
N17  A                                      HLPARA(12 041 12 055)
S17  A*         H                           HLPDOC('SYKS020050' 'SYKS02' 'SY400-
S17  A*                                     LO/TRANS')
N17  A          H                           HLPPNLGRP(SYKS020050 SYKS02PNL)
E17  A                                      HLPARA(14 001 14 080)
N17  A          H                           HLPPNLGRP(SYKS020051 SYKS02PNL)
N17  A                                      HLPARA(15 001 15 080)
N17  A          H                           HLPPNLGRP(SYKS020052 SYKS02PNL)
N17  A                                      HLPARA(16 001 16 080)
N17  A          H                           HLPPNLGRP(SYKS020053 SYKS02PNL)
N17  A                                      HLPARA(17 001 17 080)
S17  A*         H                           HLPDOC('SYKS020060' 'SYKS02' 'SY400-
S17  A*                                     LO/TRANS')
N17  A          H                           HLPPNLGRP(SYKS020060 SYKS02PNL)
     A                                      HLPARA(18 001 18 080)
N17  A          H                           HLPPNLGRP(SYKS020061 SYKS02PNL)
N17  A                                      HLPARA(19 001 19 080)
S17  A*         H                           HLPDOC('SYKS020080' 'SYKS02' 'SY400-
S17  A*                                     LO/TRANS')
N17  A          H                           HLPPNLGRP(SYKS020080 SYKS02PNL)
     A                                      HLPARA(23 001 23 080)
     A            KADTE          8Y 0H
     A            KADTR          8Y 0H
     A            MSGNR          7A  P
     A                                  1  2'SYSPED/SYKS02/3A'
     A                                      COLOR(BLU)
     A                                  1 24MSGCON(030 DIV2149 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 61DATE
     A                                      EDTCDE(Y)
     A                                  1 71TIME
     A                                  3  2MSGCON(030 JOV0379 *LIBL/SYMSGF)
     A            WSBNR          7S 0O  3 42COLOR(WHT)
     A                                  5  2MSGCON(030 DIV2150 *LIBL/SYMSGF)
     A            WSBSR          1A  O  5 38TEXT('BILAGSSERIE')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  6  2MSGCON(030 DIV2152 *LIBL/SYMSGF)
     A            WSBNR2         7Y 0O  6 38TEXT('BILAGSNUMMER')
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  7  2MSGCON(030 DIV2154 *LIBL/SYMSGF)
     A            WSBDT          6Y 0O  7 38TEXT('BILAGSDATO')
     A                                      DSPATR(UL)
     A                                      EDTWRD('      ')
     A                                      COLOR(WHT)
     A                                  8  2MSGCON(030 DIV2155 *LIBL/SYMSGF)
     A            WSPMN          2Y 0O  8 38TEXT('PERIODE-MÅNED')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            WSPÅR          2Y 0O  8 41TEXT('PERIODE-ÅR')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  9  2MSGCON(030 DIV2233 *LIBL/SYMSGF)
     A            WSTXT     R        O  9 38REFFLD(KOSBUR/BUTXT *LIBL/KOSBU)
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 10  2MSGCON(030 DIV2158 *LIBL/SYMSGF)
     A            WSLNR          8Y 0O 10 38TEXT('LEVERANDØRSNR.')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            LNAVN         30A  O 10 50DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 11  2MSGCON(010 DIV2160 *LIBL/SYMSGF)
     A            WSVK           3A  O 11 14TEXT('GEBYRKODE')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 11 18MSGCON(006 DIV2161 *LIBL/SYMSGF)
     A            WSBL          13Y 2O 11 25TEXT('BELØP')
     A                                      DSPATR(UL)
     A                                      EDTCDE(M)
     A                                      COLOR(WHT)
     A                                 11 41MSGCON(010 DIV4223 *LIBL/SYMSGF)
     A            WSMVA          1A  O 11 52TEXT('MVA-KODE')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 11 54MSGCON(010 DIV4224 *LIBL/SYMSGF)
     A            WSBLM         13Y 2O 11 65TEXT('MVA-BELØP')
     A                                      DSPATR(UL)
     A                                      EDTCDE(M)
     A                                      COLOR(WHT)
     A                                 12  2MSGCON(011 DIV2162 *LIBL/SYMSGF)
     A            WSVAL          3A  O 12 14TEXT('VALUTAKODE')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 12 19MSGCON(005 DIV2163 *LIBL/SYMSGF)
     A            WSVKU          7Y 4O 12 31TEXT('VALUTAKURS')
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A                                 12 41MSGCON(010 JOV0280 *LIBL/SYMSGF)
     A            KATAVD    R        O 12 52REFFLD(KOSTAR/KATAVD *LIBL/KOSTA)
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 13  2MSGCON(018 DIV2229 *LIBL/SYMSGF)
     A  95                                  DSPATR(ND)
     A            FICURR    R        O 13 21REFFLD(FIRMR/FICURR *LIBL/FIRM)
     A  95                                  DSPATR(ND)
     A            WSBLV         13Y 2O 13 25
     A  95                                  DSPATR(ND)
     A                                      COLOR(WHT)
     A                                      EDTCDE(M)
     A N95                                  DSPATR(UL)
S04  A*                                15  2MSGCON(030 DIV2164 *LIBL/SYMSGF)
S04  A*           WSFNR          9A  B 15 38TEXT('FAKTURANUMMER')
N04  A                                 14  2MSGCON(030 DIV2164 *LIBL/SYMSGF)
S18  A*           WSFNR          9A  B 14 38TEXT('FAKTURANUMMER')
N18  A            WSFNR         13A  B 14 38TEXT('FAKTURANUMMER')
     A                                      DSPATR(UL)
     A  97                                  DSPATR(PC)
     A  97                                  DSPATR(RI)
     A  29                                  DSPATR(PR)
     A  29                                  COLOR(WHT)
     A N29                                  COLOR(YLW)
N04  A                                 15  2MSGCON(030 DIV2169 *LIBL/SYMSGF)
N04  A            WSLKID        25A  B 15 38TEXT('LEV. KID')
N04  A                                      DSPATR(UL)
N14  A  93                                  DSPATR(PC)
N14  A  93                                  DSPATR(RI)
N04  A  29                                  DSPATR(PR)
N04  A  29                                  COLOR(WHT)
N04  A N29                                  COLOR(YLW)
E06  A                                 17  2MSGCON(030 DIV2165 *LIBL/SYMSGF)
E06  A            WSFDT          6Y 0B 17 38TEXT('FAKTURADATO')
     A                                      DSPATR(UL)
     A  99                                  DSPATR(PC)
     A  99                                  DSPATR(RI)
     A  29                                  DSPATR(PR)
     A  29                                  COLOR(WHT)
     A N29                                  COLOR(YLW)
     A                                      EDTWRD('      ')
E06  A                                 17 53MSGCON(020 DIV2239 *LIBL/SYMSGF)
E06  A                                 16  2MSGCON(030 DIV2168 *LIBL/SYMSGF)
E06  A            WSSG           3A  B 16 38TEXT('ATT.KODE/SIGNATUR')
     A                                      DSPATR(UL)
     A  29                                  DSPATR(PR)
     A  29                                  COLOR(WHT)
     A N29                                  COLOR(YLW)
     A                                 18  2MSGCON(030 DIV2167 *LIBL/SYMSGF)
     A            WSBB           2A  B 18 38TEXT('BETAL.BETINGELSE')
     A                                      DSPATR(UL)
     A  29                                  DSPATR(PR)
     A  29                                  COLOR(WHT)
     A N29                                  COLOR(YLW)
     A                                 19  2MSGCON(030 DIV2166 *LIBL/SYMSGF)
     A            WSFFDT         6Y 0B 19 38TEXT('FORFALLSDATO')
     A                                      DSPATR(UL)
     A  98                                  DSPATR(PC)
     A  98                                  DSPATR(RI)
     A  29                                  DSPATR(PR)
     A  29                                  COLOR(WHT)
     A N29                                  COLOR(YLW)
     A                                      EDTWRD('      ')
S04  A*                                20  2MSGCON(030 DIV2169 *LIBL/SYMSGF)
S04  A*           WSLKID        25A  B 20 38TEXT('LEV. KID')
S04  A*                                     DSPATR(UL)
S04  A* 29                                  DSPATR(PR)
S04  A* 29                                  COLOR(WHT)
S04  A*N29                                  COLOR(YLW)
     A                                 21  2MSGCON(030 JOV0288 *LIBL/SYMSGF)
     A            KAUSER        10   O 21 38TEXT('USER')
     A                                      COLOR(WHT)
     A            WSDTE          6  0O 21 60TEXT('DATO')
     A                                      COLOR(WHT)
     A                                      EDTCDE(Y)
     A            KATME          6Y 0O 21 69TEXT('TID')
     A                                      COLOR(WHT)
     A                                      EDTWRD('  :  :  ')
     A  29                             22  2MSGCON(030 JOV0289 *LIBL/SYMSGF)
     A  29        KAJOUR         7Y 0O 22 38TEXT('USER')
     A                                      COLOR(WHT)
     A                                 22 46MSGCON(013 YBC0850 *LIBL/SYMSGF)
     A            WSDTR          6  0O 22 60TEXT('DATO')
     A                                      COLOR(WHT)
     A                                      EDTCDE(Y)
     A            KATDR          6Y 0O 22 69TEXT('TID')
     A                                      COLOR(WHT)
     A                                      EDTWRD('  :  :  ')
     A N29                             23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N29                             23 21MSGCON(018 JOV0418 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N29                             23 41MSGCON(018 JOV0419 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 62MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                             24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A          R BILDE03B
     A N29                                  CF03(03 'Avslutt')
     A N29                                  CF09(09 'Førsteg. M.UTSK')
     A N29                                  CF10(10 'Førstegangsreg.')
     A                                      CF12(12 'Forrige')
S17  A*         H                           HLPDOC('SYKS020020' 'SYKS02' 'SY400-
S17  A*                                     LO/TRANS')
N17  A          H                           HLPPNLGRP(SYKS020020 SYKS02PNL)
     A                                      HLPARA(02 001 09 080)
S17  A*         H                           HLPDOC('SYKS020030' 'SYKS02' 'SY400-
S17  A*                                     LO/TRANS')
N17  A          H                           HLPPNLGRP(SYKS020030 SYKS02PNL)
     A                                      HLPARA(10 001 10 080)
S17  A*         H                           HLPDOC('SYKS020030' 'SYKS02' 'SY400-
S17  A*                                     LO/TRANS')
N17  A          H                           HLPPNLGRP(SYKS020030 SYKS02PNL)
     A                                      HLPARA(11 001 11 043)
S17  A*         H                           HLPDOC('SYKS020040' 'SYKS02' 'SY400-
S17  A*                                     LO/TRANS')
N17  A          H                           HLPPNLGRP(SYKS020040 SYKS02PNL)
     A                                      HLPARA(11 043 11 080)
S17  A*         H                           HLPDOC('SYKS020070' 'SYKS02' 'SY400-
S17  A*                                     LO/TRANS')
N17  A          H                           HLPPNLGRP(SYKS020070 SYKS02PNL)
     A                                      HLPARA(14 001 22 080)
S17  A*         H                           HLPDOC('SYKS020080' 'SYKS02' 'SY400-
S17  A*                                     LO/TRANS')
N17  A          H                           HLPPNLGRP(SYKS020080 SYKS02PNL)
     A                                      HLPARA(23 001 23 080)
     A            KADTE          8Y 0H
     A            KADTR          8Y 0H
     A            MSGNR          7A  P
     A                                  1  2'SYSPED/SYKS02/3B'
     A                                      COLOR(BLU)
     A                                  1 24MSGCON(030 DIV2149 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 61DATE
     A                                      EDTCDE(Y)
     A                                  1 71TIME
     A                                  3  2MSGCON(030 JOV0379 *LIBL/SYMSGF)
     A            WSBNR          7S 0O  3 42COLOR(WHT)
     A                                  5  2MSGCON(030 DIV2150 *LIBL/SYMSGF)
     A            WSBSR          1A  O  5 38TEXT('BILAGSSERIE')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  6  2MSGCON(030 DIV2152 *LIBL/SYMSGF)
     A            WSBNR2         7Y 0O  6 38TEXT('BILAGSNUMMER')
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  7  2MSGCON(030 DIV2154 *LIBL/SYMSGF)
     A            WSBDT          6Y 0O  7 38TEXT('BILAGSDATO')
     A                                      DSPATR(UL)
     A                                      EDTWRD('      ')
     A                                      COLOR(WHT)
     A                                  8  2MSGCON(030 DIV2155 *LIBL/SYMSGF)
     A            WSPMN          2Y 0O  8 38TEXT('PERIODE-MÅNED')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            WSPÅR          2Y 0O  8 41TEXT('PERIODE-ÅR')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  9  2MSGCON(030 DIV2233 *LIBL/SYMSGF)
     A            WSTXT     R        O  9 38REFFLD(KOSBUR/BUTXT *LIBL/KOSBU)
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 10  2MSGCON(030 DIV2158 *LIBL/SYMSGF)
     A            WSLNR          8Y 0O 10 38TEXT('LEVERANDØRSNR.')
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A            LNAVN         30A  O 10 50DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 11  2MSGCON(010 DIV2160 *LIBL/SYMSGF)
     A            WSVK           3A  O 11 14TEXT('GEBYRKODE')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 11 18MSGCON(006 DIV2161 *LIBL/SYMSGF)
     A            WSBL          13Y 2O 11 25TEXT('BELØP')
     A                                      DSPATR(UL)
     A                                      EDTCDE(M)
     A                                      COLOR(WHT)
     A                                 11 41MSGCON(010 DIV4223 *LIBL/SYMSGF)
     A            WSMVA          1A  O 11 52TEXT('MVA-KODE')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 11 54MSGCON(010 DIV4224 *LIBL/SYMSGF)
     A            WSBLM         13Y 2O 11 65TEXT('MVA-BELØP')
     A                                      DSPATR(UL)
     A                                      EDTCDE(M)
     A                                      COLOR(WHT)
     A                                 12  2MSGCON(011 DIV2162 *LIBL/SYMSGF)
     A            WSVAL          3A  O 12 14TEXT('VALUTAKODE')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 12 19MSGCON(005 DIV2163 *LIBL/SYMSGF)
     A            WSVKU          7Y 4O 12 31TEXT('VALUTAKURS')
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A                                 12 41MSGCON(010 JOV0280 *LIBL/SYMSGF)
     A            KATAVD    R        O 12 52REFFLD(KOSTAR/KATAVD *LIBL/KOSTA)
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 13  2MSGCON(018 DIV2229 *LIBL/SYMSGF)
     A            FICURR    R        O 13 21REFFLD(FIRMR/FICURR *LIBL/FIRM)
     A  95                                  DSPATR(ND)
     A            WSBLV         13Y 2O 13 25
     A  95                                  DSPATR(ND)
     A                                      COLOR(WHT)
     A                                      EDTCDE(M)
     A N95                                  DSPATR(UL)
     A                                 16  2MSGCON(030 DIV2170 *LIBL/SYMSGF)
     A            WSRNR          7Y 0B 16 38TEXT('REFERANSENR.')
     A                                      DSPATR(UL)
     A  29                                  DSPATR(PR)
     A  29                                  COLOR(WHT)
     A N29                                  COLOR(YLW)
     A                                      EDTCDE(4)
S04  A*                                17  2MSGCON(030 DIV2171 *LIBL/SYMSGF)
S04  A*           WSFNR          9A  B 17 38TEXT('KREDITNOTA')
N04  A                                 14  2MSGCON(030 DIV2171 *LIBL/SYMSGF)
S18  A*           WSFNR          9A  B 14 38TEXT('KREDITNOTA')
N18  A            WSFNR         13A  B 14 38TEXT('KREDITNOTA')
     A                                      DSPATR(UL)
     A  97                                  DSPATR(PC)
     A  97                                  DSPATR(RI)
     A  29                                  DSPATR(PR)
     A  29                                  COLOR(WHT)
     A N29                                  COLOR(YLW)
N04  A                                 15  2MSGCON(030 DIV2169 *LIBL/SYMSGF)
N04  A            WSLKID        25A  B 15 38TEXT('LEV. KID')
N04  A                                      DSPATR(UL)
N14  A  93                                  DSPATR(PC)
N14  A  93                                  DSPATR(RI)
N04  A  29                                  DSPATR(PR)
N04  A  29                                  COLOR(WHT)
N04  A N29                                  COLOR(YLW)
     A                                 18  2MSGCON(030 DIV2172 *LIBL/SYMSGF)
     A            WSFDT          6Y 0B 18 38TEXT('FAKTURADATO')
     A                                      DSPATR(UL)
     A  99                                  DSPATR(PC)
     A  99                                  DSPATR(HI)
     A  29                                  DSPATR(PR)
     A  29                                  COLOR(WHT)
     A N29                                  COLOR(YLW)
     A                                      EDTWRD('      ')
     A                                 18 53MSGCON(020 DIV2239 *LIBL/SYMSGF)
     A                                 20  2MSGCON(030 DIV2168 *LIBL/SYMSGF)
     A            WSSG           3A  B 20 38TEXT('ATT.KODE/SIGNATUR')
     A                                      DSPATR(UL)
     A  98                                  DSPATR(PC)
     A  98                                  DSPATR(HI)
     A  29                                  DSPATR(PR)
     A  29                                  COLOR(WHT)
     A N29                                  COLOR(YLW)
     A                                 21  2MSGCON(030 JOV0288 *LIBL/SYMSGF)
     A            KAUSER        10   O 21 38TEXT('USER')
     A                                      COLOR(WHT)
     A            WSDTE          6  0O 21 60TEXT('DATO')
     A                                      COLOR(WHT)
     A                                      EDTCDE(Y)
     A            KATME          6  0O 21 69TEXT('TID')
     A                                      COLOR(WHT)
     A                                      EDTWRD('  :  :  ')
     A  29                             22  2MSGCON(030 JOV0289 *LIBL/SYMSGF)
     A  29        KAJOUR         7Y 0O 22 38TEXT('USER')
     A                                      COLOR(WHT)
     A                                 22 46MSGCON(013 YBC0850 *LIBL/SYMSGF)
     A            WSDTR          6Y 0O 22 60TEXT('DATO')
     A                                      COLOR(WHT)
     A                                      EDTCDE(Y)
     A            KATDR          6Y 0O 22 69TEXT('TID')
     A                                      COLOR(WHT)
     A                                      EDTWRD('  :  :  ')
     A N29                             23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N29                             23 21MSGCON(018 JOV0418 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N29                             23 41MSGCON(018 JOV0419 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 62MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                             24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
