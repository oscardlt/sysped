********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 YBC PTF10 BILNR, TRANSPORTØRSNR KAN ENDRES.
********************************************************************
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPDOC('IMFORE09B' 'HELPNU' 'HLP400-
     A                                      ')
     A                                      HELP
     A                                      ALTHELP
     A          R BILDE01
     A                                      CF03(03 'Avslutt')
     A N51                                  CF04(04 'SØKE')
     A N51
     AON80                                  CF06(06 'UTSKRIFT')
     A N80                                  CF07(07 'TILLEGGSINFO')
     A                                      CF12(12 'Avbryt')
     A                                      CF08(08 'Saksgang')
     A N51                                  CF10(10 'FERDIGMELDT')
     A  51
     AA 80                                  CF23(23 'SLETTE')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/SYIS10B'
     A                                      COLOR(BLU)
     A                                  1 21MSGCON(026 TBR0121 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            UAVD           4Y 0O  1 50EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  1 55'/'
     A            UOPD           7Y 0O  1 57EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  1 65'/'
     A            WSSLOP         3Y 0O  1 67EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  2  1'S.nr/T.nr/O.giver/Tr.nr:'
     A            WSSSAK         7Y 0O  2 26COLOR(WHT)
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
     A            WSSTUN         8Y 0O  2 34COLOR(WHT)
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
     A            WSSOPG         8Y 0O  2 43COLOR(WHT)
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
S01  A*           WSSTRF         8Y 0O  2 52COLOR(WHT)
N01  A            WSSTRF         8Y 0B  2 52COLOR(YLW)
N01  A  51                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
     A  68                              2 61MSGCON(007 TBR0144 *LIBL/SYMSGF)
E01  A  68        WSSHEN        10A  B  2 69COLOR(YLW)
     A  51                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A                                  3  1MSGCON(009 TBR0150 *LIBL/SYMSGF)
     A            WSSAVS         8Y 0O  3 11COLOR(WHT)
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
     A            WSSATT        30A  O  3 20COLOR(WHT)
     A                                      DSPATR(UL)
     A                                  3 51MSGCON(007 TBR0143 *LIBL/SYMSGF)
S01  A*           WSSBIL        10A  O  3 59COLOR(WHT)
N01  A            WSSBIL        10A  B  3 59COLOR(YLW)
N01  A  51                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A                                  3 70'Status:'
     A            WSSST          1A  B  3 78COLOR(YLW)
     A  51                                  DSPATR(PR)
     A                                  4  1MSGCON(009 TBR0151 *LIBL/SYMSGF)
     A            WSSMOT         8Y 0O  4 11COLOR(WHT)
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
     A            WSSMTT        30A  O  4 20COLOR(WHT)
     A                                      DSPATR(UL)
     A                                  4 51MSGCON(008 TBR0171 *LIBL/SYMSGF)
     A                                  4 65MSGCON(005 TBR0172 *LIBL/SYMSGF)
     A            WSSOPT         2A  O  4 60COLOR(WHT)
     A                                      DSPATR(UL)
     A            WSSUNT         4A  O  4 71COLOR(WHT)
     A                                      DSPATR(UL)
     A  68                              5  1MSGCON(009 TBR0145 *LIBL/SYMSGF)
     A  68        WSSS1N         8Y 0B  5 11EDTCDE(4)
     A  51                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A  68        WSSS1T        30   O  5 20COLOR(WHT)
     A                                      DSPATR(UL)
     A                                  5 51MSGCON(013 TBR0152 *LIBL/SYMSGF)
     A            WSSADA         6Y 0B  5 65EDTCDE(Y)
     A  51                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A            WSSADK         4Y 0B  5 74EDTWRD('  .  ')
     A  51                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A  68                              6  1MSGCON(009 TBR0147 *LIBL/SYMSGF)
     A  68        WSSS2N         8Y 0B  6 11EDTCDE(4)
     A  51                                  DSPATR(PR)
     A                                      COLOR(WHT)
     A  68        WSSS2T        30   O  6 20COLOR(WHT)
     A                                  6 51MSGCON(013 TBR0153 *LIBL/SYMSGF)
     A            WSSAND         6Y 0B  6 65EDTCDE(Y)
     A  51                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A            WSSANK         4Y 0B  6 74EDTWRD('  .  ')
     A  51                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                  7  1MSGCON(009 TBR0148 *LIBL/SYMSGF)
     A            WSSPFO         5A  B  7 11COLOR(YLW)
     A  51                                  DSPATR(PR)
     A*           WSSPFT        20A  O  7 17COLOR(YLW)
     A                                  7 17MSGCON(013 TBR0140 *LIBL/SYMSGF)
     A            WSSAVA         5A  B  7 31COLOR(YLW)
     A  51                                  DSPATR(PR)
     A*           WSSAVT        20A  O  7 60COLOR(YLW)
     A                                  7 37'Reklama.type:'
     A            WSSRTY         2A  B  7 51COLOR(YLW)
     A  51                                  DSPATR(PR)
     A                                  7 54'Fakt/Kred:'
     A            WSSFN          7Y 0B  7 65COLOR(YLW)
     A  51                                  DSPATR(PR)
     A                                      EDTCDE(4)
     A            WSSKRN         7Y 0B  7 73COLOR(YLW)
     A  51                                  DSPATR(PR)
     A                                      EDTCDE(4)
     A                                  8  1MSGCON(016 TBR0135 *LIBL/SYMSGF)
     A                                  8 19MSGCON(006 TBR0136 *LIBL/SYMSGF)
     A            WSSSKL         7Y 0B  8 26EDTCDE(4)
     A  51                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                  8 35MSGCON(003 TBR0137 *LIBL/SYMSGF)
     A            WSSSKG         9Y 0B  8 39EDTCDE(4)
     A  51                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                  8 50MSGCON(003 TBR0138 *LIBL/SYMSGF)
     A            WSSSLM         9Y 2B  8 54EDTWRD('       .  ')
     A  51                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                  8 66MSGCON(003 TBR0139 *LIBL/SYMSGF)
     A            WSSSM3         9Y 3B  8 70EDTWRD('      .   ')
     A  51                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                  9  1MSGCON(010 TBR0132 *LIBL/SYMSGF)
     A            WSSMDA         6Y 0B  9 12EDTCDE(Y)
     A  51                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                  9 21MSGCON(004 TBR0133 *LIBL/SYMSGF)
     A            WSSMKL         4Y 0B  9 26EDTWRD('  .  ')
     A  51                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                  9 33MSGCON(009 TBR0134 *LIBL/SYMSGF)
     A            WSSLDA         6Y 0B  9 43EDTCDE(Y)
     A  51                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                  9 52MSGCON(004 TBR0133 *LIBL/SYMSGF)
     A            WSSLKL         4Y 0B  9 57EDTWRD('  .  ')
     A  51                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                 10  1'Beskrivelse av avvik'
     A                                      DSPATR(UL)
     A                                 10 33MSGCON(015 TBR0129 *LIBL/SYMSGF)
     A            WSSANS         5A  B 10 50COLOR(YLW)
     A  51                                  DSPATR(PR)
     A            WSSANT        20A  O 10 56COLOR(YLW)
     A            WSSAV1        75A  B 11  1COLOR(YLW)
     A  51                                  DSPATR(PR)
     A N51        WSSFK1         2A  B 11 77COLOR(YLW)
     A            WSSAV2        75A  B 12  1COLOR(YLW)
     A  51                                  DSPATR(PR)
     A N51        WSSFK2         2A  B 12 77COLOR(YLW)
     A            WSSAV3        75A  B 13  1COLOR(YLW)
     A  51                                  DSPATR(PR)
     A N51        WSSFK3         2A  B 13 77COLOR(YLW)
     A                                 14  1MSGCON(020 TBR0128 *LIBL/SYMSGF)
     A                                      DSPATR(UL)
     A            WSSKO1        75A  B 15  1COLOR(YLW)
     A  51                                  DSPATR(PR)
     A N51        WSSFR1         2A  B 15 77COLOR(YLW)
     A  51                                  DSPATR(PR)
     A            WSSKO2        75A  B 16  1COLOR(YLW)
     A  51                                  DSPATR(PR)
     A N51        WSSFR2         2A  B 16 77COLOR(YLW)
     A  51                                  DSPATR(PR)
     A            WSSKO3        75A  B 17  1COLOR(YLW)
     A  51                                  DSPATR(PR)
     A N51        WSSFR3         2A  B 17 77COLOR(YLW)
     A  51                                  DSPATR(PR)
     A            WSSKO4        75A  B 18  1COLOR(YLW)
     A  51                                  DSPATR(PR)
     A N51        WSSFR4         2A  B 18 77COLOR(YLW)
     A  51                                  DSPATR(PR)
     A                                 19  1MSGCON(015 TBR0127 *LIBL/SYMSGF)
     A            WSSGDA         6Y 0B 19 17
     A  40
     AO 51                                  DSPATR(PR)
     A  40                                  COLOR(WHT)
     A N40                                  COLOR(YLW)
     A                                      EDTCDE(Y)
     A                                 19 26'/'
     A            WSSGSI        15A  B 19 28
     A  40
     AO 51                                  DSPATR(PR)
     A  40                                  COLOR(WHT)
     A N40                                  COLOR(YLW)
     A                                 19 44'R.dato/Sign:'
     A            WSSRDA         6Y 0O 19 57EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                 19 66'/'
     A            WSSRSI        10A  O 19 68COLOR(WHT)
     A                                 20  8MSGCON(008 TBR0124 *LIBL/SYMSGF)
     A            WSSFDA         6Y 0B 20 17
     A  41
     AO 51                                  DSPATR(PR)
     A  41                                  COLOR(WHT)
     A N41                                  COLOR(YLW)
     A                                      EDTCDE(Y)
     A                                 20 26'/'
     A            WSSFSI        15A  B 20 28
     A  41
     AO 51                                  DSPATR(PR)
     A  41                                  COLOR(WHT)
     A N41                                  COLOR(YLW)
     A                                 20 44'Sist behand:'
     A            WSSSDA         6Y 0O 20 57EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                 20 66'/'
     A            WSSSSI        10A  O 20 68COLOR(WHT)
     A                                 21  8MSGCON(008 TBR0125 *LIBL/SYMSGF)
     A            WSSUDA         6Y 0B 21 17
     A  42
     AO 51                                  DSPATR(PR)
     A  42                                  COLOR(WHT)
     A N42                                  COLOR(YLW)
     A                                      EDTCDE(Y)
     A                                 21 26'/'
     A            WSSUSI        15A  B 21 28
     A  42
     AO 51                                  DSPATR(PR)
     A  42                                  COLOR(WHT)
     A N42                                  COLOR(YLW)
     A                                 21 44'S.beh:'
     A            ISUSRS        10A  O 21 51COLOR(WHT)
     A                                 21 62'Sendes:'
     A            WSUSRS        10A  B 21 70
     A  42
     AO 51                                  DSPATR(PR)
     A  42                                  COLOR(WHT)
     A N42                                  COLOR(YLW)
     A                                 22  1'Kostnad:'
     A                                 22 10'Krav'
     A            WSSKRA         9Y 2B 22 15COLOR(YLW)
     A  51                                  DSPATR(PR)
     A                                      EDTCDE(4)
     A                                 22 26'Forv'
     A            WSSFOR         9Y 2B 22 31COLOR(YLW)
     A  51                                  DSPATR(PR)
     A                                      EDTCDE(4)
     A                                 22 42'End'
     A            WSSKOS         9Y 2B 22 46COLOR(YLW)
     A  51                                  DSPATR(PR)
     A                                      EDTCDE(4)
     A                                 22 57'Int'
     A            WSSINT         9Y 2B 22 61COLOR(YLW)
     A  51                                  DSPATR(PR)
     A                                      EDTCDE(4)
     A                                 22 72'F8=Saksg'
     A                                      COLOR(BLU)
     A                                 23  2'F3=Avslutt'
     A                                      COLOR(BLU)
     A N51                             23 14'F4=Søk'
     A                                      COLOR(BLU)
     A N51
     AON80                             23 22'F6=Avsl. m/utskr.'
     A                                      COLOR(BLU)
     A  51
     AA 80                             23 22'F23=Bereft slett.'
     A                                      COLOR(BLU)
     A N51                             23 41'F10=Ferdigmeldt'
     A                                      COLOR(BLU)
     A N80                             23 58'F7=Tillegg'
     A                                      COLOR(BLU)
     A                                 23 70MSGCON(010 TBR0015 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30        UTXT          62A  O 24  2COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R VINDU1
     A                                      WINDOW(*DFT 9 25 *NOMSGLIN)
     A                                      CF12(12 'Tilbake')
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CSRLOC(LINNBR     POSNBR)
     A*                                     WDWBORDER((*COLOR BLU) (*CHAR '...:-
     A*                                     :...'))
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  9MSGCON(008 TRO0404 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                  2  2MSGCON(004 TRO0402 *LIBL/SYMSGF)
     A                                  2 12MSGCON(005 TRO0403 *LIBL/SYMSGF)
     A N81        WDATO1         6Y 0O  3  2EDTCDE(Y)
     A                                      COLOR(YLW)
     A N81        WSIGN1        10A  O  3 12COLOR(YLW)
     A N82        WDATO2         6Y 0O  4  2EDTCDE(Y)
     A                                      COLOR(YLW)
     A N82        WSIGN2        10A  O  4 12COLOR(YLW)
     A N83        WDATO3         6Y 0O  5  2EDTCDE(Y)
     A                                      COLOR(YLW)
     A N83        WSIGN3        10A  O  5 12COLOR(YLW)
     A N84        WDATO4         6Y 0O  6  2EDTCDE(Y)
     A                                      COLOR(YLW)
     A N84        WSIGN4        10A  O  6 12COLOR(YLW)
     A N85        WDATO5         6Y 0O  7  2EDTCDE(Y)
     A                                      COLOR(YLW)
     A N85        WSIGN5        10A  O  7 12COLOR(YLW)
     A N86        WDATO6         6Y 0O  8  2EDTCDE(Y)
     A                                      COLOR(YLW)
     A N86        WSIGN6        10A  O  8 12COLOR(YLW)
     A            WPLUSS         1   O  8 23
     A                                  9 11'F12=Forrige'
     A                                      COLOR(BLU)
      *
     A          R BEKREFT
     A                                      WINDOW(*DFT 5 35 *NOMSGLIN)
     A                                      CF12(12 'Tilbake')
     A                                      OVERLAY
     A                                  1  2'Bekreft på ferdigmeldt'
     A                                      DSPATR(HI)
     A                                  2  2'Denne saken har ikke fått endelig'
     A                                      COLOR(YLW)
     A                                  3  2'kostnadog ikke utført.'
     A                                      COLOR(YLW)
     A                                  4  2'Vil du avslutter saken?'
     A            XBEKR          1A  B  4 30DSPATR(UL)
     A                                      VALUES('J' 'N')
     A                                  5  2'F12=Forrige'
     A                                      COLOR(BLU)
