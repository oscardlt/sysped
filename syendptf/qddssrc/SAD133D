      *
     A                                      DSPSIZ(27 132 *DS4)
     A                                      PRINT
     A                                      HELP
     A                                      ALTHELP
      *
     A          R SUBFIL                    SFL
     A            WSNR           2Y 0B  6  2EDTCDE(4)
     A            CLAVD          4Y 0O  6  5TEXT('Avdeling')
     A                                      EDTCDE(4)
     A            CLTDN          7Y 0O  6 10TEXT('Oppdrag')
     A                                      EDTCDE(M)
     A            SINAK         30A  O  6 20TEXT('Consignee')
     A            CL0068A        8Y 0O  6 51TEXT('Dato')
     A                                      EDTCDE(4)
     A            CL0068B        6Y 0O  6 60TEXT('Sekvens')
     A                                      EDTCDE(4)
     A            CLNTK          7Y 0O  6 67TEXT('Kolli')
     A                                      EDTCDE(4)
     A            CLVKB          9Y 0O  6 75TEXT('Total vekt')
     A                                      EDTCDE(4)
     A            CL3039E        6Y 0O  6 85TEXT('Ekspedisjonsenhet')
     A                                      EDTCDE(4)
     A            CLTRID        17A  O  6 92TEXT('Bilnr')
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0019)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A                                      CF12(12 'AVBRYT')
     A                                      CF05(05 'Frisk')
     A                                      CF08(08 'Søk')
     A                                      CF20(20 'Plukk alle')
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A            ZRECNO         4S 0O  1 60SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYSPED/SAD133R'
     A                                      COLOR(BLU)
     A                                  1 20'Plukk oppdrag til E.F. manifest'
     A                                      COLOR(WHT)
     A                                  1120DATE
     A                                      EDTCDE(Y)
     A                                  2120TIME
     A                                  2  2'Avdeling:'
     A            UAVD           4Y 0O  2 12TEXT('AVDELINGSNR')
     A                                      COLOR(BLU)
     A                                      EDTCDE(4)
     A                                  2 18'Turnr:'
     A            UPRO           8Y 0O  2 25TEXT('Turnr')
     A                                      COLOR(BLU)
     A                                      EDTCDE(4)
     A                                  2 35'E.enh:'
     A            EF3039E        6Y 0O  2 42TEXT('Ekspedisjonsenhet')
     A                                      COLOR(BLU)
     A                                  2 49'Bilnr:'
     A            EFKMRK        30A  O  2 56TEXT('Bilnr')
     A                                      COLOR(BLU)
     A                                  3  2'Velg   1=Plukk   4=Slett   F5=Se o-
     A                                      ppdrag   15=Se SAD'
     A                                      COLOR(BLU)
     A                                  4  2'Op Avd. Oppdrag  Mottaker         -
     A                                                     TVINN-DT Sekv.  T.ko-
     A                                      lli Tot. vekt E.enh. Bilnr'
     A                                      COLOR(WHT)
     A            W2NR           2Y 0B  5  2DSPATR(UL)
     A                                      DSPATR(PR)
     A                                      EDTCDE(4)
     A            W2AVD          4Y 0B  5  5DSPATR(UL)
     A                                      EDTCDE(4)
     A            W2TDN          7Y 0B  5 10DSPATR(UL)
     A                                      EDTCDE(M)
     A            W23039E        6Y 0B  5 85DSPATR(UL)
     A                                      EDTCDE(4)
     A            W2TRID        17A  B  5 92DSPATR(UL)
      *
     A          R BUNN
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                 26  2'F5=Frisk   F8=Søk   F12=Avbryt   F-
     A                                      20=Plukk alle'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 27  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R NYVARE
     A                                      CF03(03 'Avslutt program')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'Plukk')
     A                                      CF08(08 'Søk')
     A                                      CF12(12 'AVBRYT')
     A                                      CF20(20 'Plukk alle')
     A                                  1  2'SYSPED/SAD133R'
     A                                      COLOR(BLU)
     A                                  1 20'Plukk oppdrag til E.F. manifest'
     A                                      COLOR(WHT)
     A                                  1120DATE
     A                                      EDTCDE(Y)
     A                                  2120TIME
     A                                  2  2'Avdeling:'
     A            UAVD           4Y 0O  2 12TEXT('AVDELINGSNR')
     A                                      COLOR(BLU)
     A                                      EDTCDE(4)
     A                                  2 18'Turnr:'
     A            UPRO           8Y 0O  2 25TEXT('Turnr')
     A                                      COLOR(BLU)
     A                                      EDTCDE(4)
     A                                  2 35'E.enh:'
     A            EF3039E        6Y 0O  2 42TEXT('Ekspedisjonsenhet')
     A                                      COLOR(BLU)
     A                                  2 49'Bilnr:'
     A            EFKMRK        30A  O  2 56TEXT('Bilnr')
     A                                      COLOR(BLU)
     A                                  3  2'Velg   1=Plukk   4=Slett   F5=Se o-
     A                                      ppdrag   15=Se SAD'
     A                                      COLOR(BLU)
     A                                  4  2'Op Avd. Oppdrag  Mottaker         -
     A                                                     TVINN-DT Sekv.  T.ko-
     A                                      lli Tot. vekt E.enh. Bilnr'
     A                                      COLOR(WHT)
     A            W2NR           2Y 0B  5  2DSPATR(UL)
     A                                      DSPATR(PR)
     A                                      EDTCDE(4)
     A            W2AVD          4Y 0B  5  5DSPATR(UL)
     A                                      EDTCDE(4)
     A            W2TDN          7Y 0B  5 10DSPATR(UL)
     A                                      EDTCDE(M)
     A            W23039E        6Y 0B  5 85DSPATR(UL)
     A                                      EDTCDE(4)
     A            W2TRID        17A  B  5 92DSPATR(UL)
     A                                 26  2'F5=Frisk   F8=Søk   F12=Avbryt   F-
     A                                      20=Plukk alle'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 27  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R VAREL2
     A                                      CF12(12 'Forrige')
     A                                      SLNO(24)
     A                                      CLRL(04)
     A                                  1  2'Tr.middel:'
     A            W3TRID        17A  B  1 14DSPATR(UL)
     A                                      DSPATR(PC)
     A                                  2  2'Eksp.enh:'
     A            W33039E        6Y 0B  2 14DSPATR(UL)
     A                                      EDTCDE(4)
     A                                  3  2'Avdeling:'
     A            W3AVD          4Y 0B  3 14DSPATR(UL)
     A                                      EDTCDE(4)
     A                                  3 22'Oppdragsnr:'
     A            W3TDN          7Y 0B  3 34DSPATR(UL)
     A                                      EDTCDE(M)
     A                                  4  2'F12=Forrige'
     A                                      COLOR(BLU)
