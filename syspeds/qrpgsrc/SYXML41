      *
      ***********************************************************
      *                                                        **
      *** PROGRAM SKAL KONVERTER XML-FIL TIL TET-EXPORT       ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01-24                            ***
      *** LEDIGE INDIKATORER 27-32 34-99                      ***
      ***********************************************************
      *
     FXMLRCVF   IF   E           K DISK
     FCUNDL01   UF A E           K DISK    RENAME(KUNDR:CUNDR)
     FKUNDL01   UF A E           K DISK
     FSYPARL1   UF A E           K DISK
     FSYPARL3   IF   E           K DISK    RENAME(SYPARFR:SYPARFR3)
     F                                     PREFIX(S3_)
     FTELLGE    UF A E           K DISK
     FFIRM      IF   E           K DISK
      *
     D PSN             S             10    DIM(50)
     D PSI             S             10    DIM(50)
     D ACC             S             15    DIM(50)
     D AD1             S             30    DIM(50)
     D AD2             S             30    DIM(50)
     D AD3             S             24    DIM(50)
     D PNR             S              9    DIM(50)
     D LKD             S              2    DIM(50)
      *
     D KNR             S              8    DIM(50)
     D                 DS
     D  XTXT                   1     15
     D  XT                     1     15    DIM(15)
     D                 DS
     D  XTAL                   1     15  3
     D  XTALH                  1     12  0
     D  XTALD1                13     13
     D  XTALD2                14     14
     D  XTALD3                15     15
     D UC              C                   CONST('ABCDEFGHIJKLMNOPQRST-
     D                                     UVWXYZÆØÅ')
     D LC              C                   CONST('abcdefghijklmnopqrst-
     D                                     uvwxyzæøå')
     ICUNDR
     I              BANKG                       C_BANKG
     I              POSTG                       C_POSTG
      *
     C                   EXSR      INIT
      *
     C                   EXSR      SRXML1
      *
     C                   EVAL      *INLR = *ON
     C                   RETURN
      *
      ***********************************************
     C     SRXML1        BEGSR
      ***********************************************
      *
     C                   EVAL      X001_XRLEV1 = 1
     C                   EVAL      X001_XRLEV2 = 0
      *
     C     XMLKEY01      CHAIN     XMLRCVF                            33
     C                   DOW       *IN33 = *OFF                                 1<-B
     C                   SELECT                                                  2<-B
      * HENT INFOR. FRA <ns2:CustomerParty>
     C                   WHEN      XRNV = 'ns2:CustomerParty'                    2<-B
     C                   EXSR      SRXML2
     C                   ENDSL                                                   2<-E
     C                   EVAL      X001_XRLEV1 = X001_XRLEV1 + 1
     C     XMLKEY01      CHAIN     XMLRCVF                            33
     C                   ENDDO                                                  1<-E
      *
     C                   ENDSR
      *
      ***********************************************
     C     SRXML2        BEGSR
      ***********************************************
      * 'ns2:CustomerParty'
      *
     C                   EVAL      X_KUNDNR = 0
      *
     C                   EVAL      X002_XRLEV1 = XRLEV1
     C                   EVAL      X002_XRLEV2 = 1
     C                   EVAL      X002_XRLEV3 = 0
      *
     C     XMLKEY02      CHAIN     XMLRCVF                            33
     C                   DOW       *IN33 = *OFF                                 1<-B
     C                   SELECT                                                  2<-B
     C                   WHEN      XRNV = 'ns3:OrganizationCustomer'             2
     C                   EXSR      SRXML3
     C                   ENDSL                                                   2<-E
      *
     C                   EVAL      X002_XRLEV2 = X002_XRLEV2 + 1
     C     XMLKEY02      CHAIN     XMLRCVF                            33
     C                   ENDDO                                                  1<-E
      *
     C     1             DO        X_ANTK        XN
     C                   IF        ACC(XN) <> *BLANKS
     C                   EXSR      SROPDK
     C                   END
     C                   ENDDO
      *
     C                   ENDSR
      *
      ***********************************************
     C     SRXML3        BEGSR
      ***********************************************
      * 'ns3:OrganizationCustomer'
      *
     C                   EVAL      X003_XRLEV1 = XRLEV1
     C                   EVAL      X003_XRLEV2 = XRLEV2
     C                   EVAL      X003_XRLEV3 = 1
     C                   EVAL      X003_XRLEV4 = 0
      *
     C     XMLKEY03      CHAIN     XMLRCVF                            33
     C                   DOW       *IN33 = *OFF                                 1<-B
     C                   SELECT                                                  2<-B
     C                   WHEN      XRNV = 'ns3:Organization'                     2
     C                   EXSR      SRXML41
     C                   WHEN      XRNV = 'ns3:CustomerAccount'                  2
     C                   EXSR      SRXML42
     C                   ENDSL                                                   2<-E
      *
     C                   EVAL      X003_XRLEV3 = X003_XRLEV3 + 1
     C     XMLKEY03      CHAIN     XMLRCVF                            33
     C                   ENDDO                                                  1<-E
      *
     C                   ENDSR
      *
      ***********************************************
     C     SRXML41       BEGSR
      ***********************************************
      * 'ns3:Organization'
      *
     C                   EVAL      X004_XRLEV1 = XRLEV1
     C                   EVAL      X004_XRLEV2 = XRLEV2
     C                   EVAL      X004_XRLEV3 = XRLEV3
     C                   EVAL      X004_XRLEV4 = 1
     C                   EVAL      X004_XRLEV5 = 0
      *
     C     XMLKEY04      CHAIN     XMLRCVF                            33
     C                   DOW       *IN33 = *OFF                                 1<-B
     C                   SELECT                                                  2<-B
     C                   WHEN      XRNV = 'ns4:CreationDate'                     2
     C                   WHEN      XRNV = 'ns4:LastUpdateDate'                   2
     C                   WHEN      XRNV = 'ns3:Status'                           2
     C                   WHEN      XRNV = 'ns3:PartyNumber'                      2
     C                   EVAL      X_PARTYNR = XRVERD
     C                   WHEN      XRNV = 'ns3:ValidatedFlag'                    2
     C                   WHEN      XRNV = 'ns4:Attribute6'                       2
     C                   MOVEL     XRVERD        XTXT
     C                   EXSR      KNVTAL
     C                   IF        XTAL > 0 AND XTAL < 1000
     C                   EVAL      X_KGRENS = 1
     C                   ELSE
     C                   EVAL      X_KGRENS = XTAL / 1000
     C                   END
     C                   WHEN      XRNV = 'ns4:Attribute12'                      2
     C                   WHEN      XRNV = 'ns3:OrganizationName'                 2
     C                   EVAL      X_KNAVN  = XRVERD
     C                   WHEN      XRNV = 'ns3:DUNSNumber'                       2
     C                   WHEN      XRNV = 'ns3:LegalStatus'                      2
     C                   WHEN      XRNV = 'ns3:FiscalCode'                       2
     C                   EVAL      X_SYRG   = XRVERD
     C                   WHEN      XRNV = 'ns3:PrefFunctionalCurrency'           2
     C                   EVAL      X_VALKOD = XRVERD
     C                   WHEN      XRNV = 'ns3:PartySite'                        2
     C                   EXSR      SRXML415
     C                   WHEN      XRNV = 'ns3:CreditRating'                     2
     C                   ENDSL                                                   2<-E
      *
     C                   EVAL      X004_XRLEV4 = X004_XRLEV4 + 1
     C     XMLKEY04      CHAIN     XMLRCVF                            33
     C                   ENDDO                                                  1<-E
      *
     C                   ENDSR
      *
      ***********************************************
     C     SRXML415      BEGSR
      ***********************************************
      * 'ns3:PartySite'
      *
     C                   EVAL      X005_XRLEV1 = XRLEV1
     C                   EVAL      X005_XRLEV2 = XRLEV2
     C                   EVAL      X005_XRLEV3 = XRLEV3
     C                   EVAL      X005_XRLEV4 = XRLEV4
     C                   EVAL      X005_XRLEV5 = 1
     C                   EVAL      X005_XRLEV6 = 0
     C                   EVAL      X_ANTK      = X_ANTK + 1
     C                   EVAL      X_OKK       = 'N'
      *
     C     XMLKEY05      CHAIN     XMLRCVF                            33
     C                   DOW       *IN33 = *OFF                                 1<-B
     C                   SELECT                                                  2<-B
     C                   WHEN      XRNV = 'ns3:PartySiteNumber'                  2
     C                   EVAL      PSN(X_ANTK) = XRVERD
     C                   WHEN      XRNV = 'ns3:IdentifyingAddressFlag'           2
     C                   WHEN      XRNV = 'ns3:Status'                           2
     C                   WHEN      XRNV = 'ns4:CreationDate'                     2
     C                   WHEN      XRNV = 'ns4:LastUpdateDate'                   2
     C                   WHEN      XRNV = 'ns3:Location'                         2
     C                   EXSR      SRXML4161
     C                   WHEN      XRNV = 'ns3:PartySiteUse'                     2
     C                   EXSR      SRXML4162
     C                   ENDSL                                                   2<-E
      *
     C                   EVAL      X005_XRLEV5 = X005_XRLEV5 + 1
     C     XMLKEY05      CHAIN     XMLRCVF                            33
     C                   ENDDO                                                  1<-E
      *
     C                   IF        X_OKK = 'N'
     C                   EVAL      X_ANTK      = X_ANTK - 1
     C                   END
      *
     C                   ENDSR
      *
      ***********************************************
     C     SRXML4161     BEGSR
      ***********************************************
      * 'ns3:Location'
      *
     C                   EVAL      X006_XRLEV1 = XRLEV1
     C                   EVAL      X006_XRLEV2 = XRLEV2
     C                   EVAL      X006_XRLEV3 = XRLEV3
     C                   EVAL      X006_XRLEV4 = XRLEV4
     C                   EVAL      X006_XRLEV5 = XRLEV5
     C                   EVAL      X006_XRLEV6 = 1
     C                   EVAL      X006_XRLEV7 = 0
      *
     C     XMLKEY06      CHAIN     XMLRCVF                            33
     C                   DOW       *IN33 = *OFF                                 1<-B
     C                   SELECT                                                  2<-B
     C                   WHEN      XRNV = 'ns3:LocationId'                       2
     C                   WHEN      XRNV = 'ns4:CreationDate'                     2
     C                   WHEN      XRNV = 'ns4:LastUpdateDate'                   2
     C                   WHEN      XRNV = 'ns3:Address'                          2
     C                   EXSR      SRXML4171
     C                   ENDSL                                                   2<-E
      *
     C                   EVAL      X006_XRLEV6 = X006_XRLEV6 + 1
     C     XMLKEY06      CHAIN     XMLRCVF                            33
     C                   ENDDO                                                  1<-E
      *
     C                   ENDSR
      *
      ***********************************************
     C     SRXML4171     BEGSR
      ***********************************************
      * 'ns3:Address'
      *
     C                   EVAL      X007_XRLEV1 = XRLEV1
     C                   EVAL      X007_XRLEV2 = XRLEV2
     C                   EVAL      X007_XRLEV3 = XRLEV3
     C                   EVAL      X007_XRLEV4 = XRLEV4
     C                   EVAL      X007_XRLEV5 = XRLEV5
     C                   EVAL      X007_XRLEV6 = XRLEV6
     C                   EVAL      X007_XRLEV7 = 1
     C                   EVAL      X007_XRLEV8 = 0
      *
     C     XMLKEY07      CHAIN     XMLRCVF                            33
     C                   DOW       *IN33 = *OFF                                 1<-B
     C                   SELECT                                                  2<-B
     C                   WHEN      XRNV = 'ns4:AddressLine1'                     2
     C                   EVAL      AD1(X_ANTK) = XRVERD
     C                   WHEN      XRNV = 'ns4:PostalCode'                       2
     C                   EVAL      PNR(X_ANTK) = XRVERD
     C                   WHEN      XRNV = 'ns4:City'                             2
     C                   EVAL      AD3(X_ANTK) = XRVERD
     C                   WHEN      XRNV = 'ns4:CountryCode'                      2
     C                   EVAL      LKD(X_ANTK) = XRVERD
     C                   ENDSL                                                   2<-E
      *
     C                   EVAL      X007_XRLEV7 = X007_XRLEV7 + 1
     C     XMLKEY07      CHAIN     XMLRCVF                            33
     C                   ENDDO                                                  1<-E
      *
     C                   ENDSR
      *
      ***********************************************
     C     SRXML4162     BEGSR
      ***********************************************
      * 'ns3:PartySiteUse'
      *
     C                   EVAL      X006_XRLEV1 = XRLEV1
     C                   EVAL      X006_XRLEV2 = XRLEV2
     C                   EVAL      X006_XRLEV3 = XRLEV3
     C                   EVAL      X006_XRLEV4 = XRLEV4
     C                   EVAL      X006_XRLEV5 = XRLEV5
     C                   EVAL      X006_XRLEV6 = 1
     C                   EVAL      X006_XRLEV7 = 0
      *
     C     XMLKEY06      CHAIN     XMLRCVF                            33
     C                   DOW       *IN33 = *OFF                                 1<-B
     C                   SELECT                                                  2<-B
     C                   WHEN      XRNV = 'ns3:PartySiteNumber'                  2
     C                   EVAL      PSN(X_ANTK) = XRVERD
     C                   WHEN      XRNV = 'ns3:PartySiteId'                      2
     C                   EVAL      PSI(X_ANTK) = XRVERD
     C                   WHEN      XRNV = 'ns3:SiteUseType'                      2
     C                   IF        XRVERD = 'BILL_TO'                             3<-B
     C                   EVAL      X_OKK = 'J'
     C                   END                                                      3<-E
     C                   WHEN      XRNV = 'ns3:PrimaryPerType'                   2
     C                   ENDSL                                                   2<-E
      *
     C                   EVAL      X006_XRLEV6 = X006_XRLEV6 + 1
     C     XMLKEY06      CHAIN     XMLRCVF                            33
     C                   ENDDO                                                  1<-E
      *
     C                   ENDSR
      *
      ***********************************************
     C     SRXML42       BEGSR
      ***********************************************
      * 'ns3:CustomerAccount'
      *
     C                   EVAL      X004_XRLEV1 = XRLEV1
     C                   EVAL      X004_XRLEV2 = XRLEV2
     C                   EVAL      X004_XRLEV3 = XRLEV3
     C                   EVAL      X004_XRLEV4 = 1
     C                   EVAL      X004_XRLEV5 = 0
     C                   EVAL      X_ACCNR  = *BLANKS
     C                   EVAL      X_KNSYS = *BLANKS
      *
     C     XMLKEY04      CHAIN     XMLRCVF                            33
     C                   DOW       *IN33 = *OFF                                 1<-B
     C                   SELECT                                                  2<-B
     C                   WHEN      XRNV = 'ns3:AccountNumber'                    2
     C                   EVAL      X_ACCNR = XRVERD
     C                   WHEN      XRNV = 'ns3:AccountName'                      2
     C                   WHEN      XRNV = 'ns3:Status'                           2
     C                   WHEN      XRNV = 'ns3:SourceSystemInformation'          2
     C                   EXSR      SRXML4251
     C                   WHEN      XRNV = 'ns3:CustomerAccountSite'              2
     C                   EXSR      SRXML4252
     C                   ENDSL                                                   2<-E
      *
     C                   EVAL      X004_XRLEV4 = X004_XRLEV4 + 1
     C     XMLKEY04      CHAIN     XMLRCVF                            33
     C                   ENDDO                                                  1<-E
      *
     C                   ENDSR
      *
      ***********************************************
     C     SRXML4251     BEGSR
      ***********************************************
      * 'ns3:SourceSystemInformation'
      *
     C                   EVAL      X005_XRLEV1 = XRLEV1
     C                   EVAL      X005_XRLEV2 = XRLEV2
     C                   EVAL      X005_XRLEV3 = XRLEV3
     C                   EVAL      X005_XRLEV4 = XRLEV4
     C                   EVAL      X005_XRLEV5 = 1
     C                   EVAL      X005_XRLEV6 = 0
      *
     C     XMLKEY05      CHAIN     XMLRCVF                            33
     C                   DOW       *IN33 = *OFF                                 1<-B
     C                   SELECT                                                  2<-B
     C                   WHEN      XRNV = 'ns3:StartDateActive'                  2
     C                   WHEN      XRNV = 'ns4:OrigSystem'                       2
     C                   IF        XRVERD <> 'SYSPED'                             3<-B
     C                   LEAVE
     C                   END                                                      3<-E
     C                   WHEN      XRNV = 'ns4:OrigSystemReference'              2
     C                   EVAL      X_KNSYS = XRVERD
     C                   IF        %SUBST(XRVERD:1:1) < '0' OR
     C                             %SUBST(XRVERD:1:1) > '9'
     C                   EVAL      X_KNSYS = *BLANKS
     C                   END                                                      3<-E
     C                   LEAVE
     C                   ENDSL                                                   2<-E
      *
     C                   EVAL      X005_XRLEV5 = X005_XRLEV5 + 1
     C     XMLKEY05      CHAIN     XMLRCVF                            33
     C                   ENDDO                                                  1<-E
      *
     C                   ENDSR
      *
      ***********************************************
     C     SRXML4252     BEGSR
      ***********************************************
      * 'ns3:CustomerAccountSite'
      *
     C                   EVAL      X005_XRLEV1 = XRLEV1
     C                   EVAL      X005_XRLEV2 = XRLEV2
     C                   EVAL      X005_XRLEV3 = XRLEV3
     C                   EVAL      X005_XRLEV4 = XRLEV4
     C                   EVAL      X005_XRLEV5 = 1
     C                   EVAL      X005_XRLEV6 = 0
      *
     C     XMLKEY05      CHAIN     XMLRCVF                            33
     C                   DOW       *IN33 = *OFF                                 1<-B
     C                   SELECT                                                  2<-B
     C                   WHEN      XRNV = 'ns3:PartySiteId'                      2
     C                   EVAL      X_PSI = XRVERD
     C                   WHEN      XRNV = 'ns3:CustomerAccountSiteUse'           2
     C                   EXSR      SRXML42526
     C                   LEAVE
     C                   ENDSL                                                   2<-E
      *
     C                   EVAL      X005_XRLEV5 = X005_XRLEV5 + 1
     C     XMLKEY05      CHAIN     XMLRCVF                            33
     C                   ENDDO                                                  1<-E
      *
     C                   ENDSR
      *
      ***********************************************
     C     SRXML42526    BEGSR
      ***********************************************
      * 'ns3:CustomerAccountSiteUse'
      *
     C                   EVAL      X006_XRLEV1 = XRLEV1
     C                   EVAL      X006_XRLEV2 = XRLEV2
     C                   EVAL      X006_XRLEV3 = XRLEV3
     C                   EVAL      X006_XRLEV4 = XRLEV4
     C                   EVAL      X006_XRLEV5 = XRLEV5
     C                   EVAL      X006_XRLEV6 = 1
     C                   EVAL      X006_XRLEV7 = 0
      *
     C     XMLKEY06      CHAIN     XMLRCVF                            33
     C                   DOW       *IN33 = *OFF                                 1<-B
     C                   SELECT                                                  2<-B
     C                   WHEN      XRNV = 'ns3:AccountSiteUseCode'               2
     C                   IF        XRVERD = 'BILL_TO'                             3<-B
     C     1             DO        X_ANTK        XN                3 0             4<-B
     C                   IF        PSI(XN) = X_PSI                                  5<-B
     C                   EVAL      ACC(XN) = X_ACCNR
     C                   EVAL      KNR(XN) = X_KNSYS
     C                   LEAVE
     C                   END                                                        5<-E
     C                   ENDDO                                                     4<-E
     C                   END                                                      3<-E
     C                   ENDSL                                                   2<-E
      *
     C                   EVAL      X006_XRLEV6 = X006_XRLEV6 + 1
     C     XMLKEY06      CHAIN     XMLRCVF                            33
     C                   ENDDO                                                  1<-E
      *
     C                   ENDSR
      *
      ***********************************************
     C     SROPDK        BEGSR
      ***********************************************
      *
     C                   IF        KNR(XN) = *BLANKS
     C                   EVAL      S3_SYVRDA = X_PARTYNR + PSI(XN) + ACC(XN)
     C     SYPARL3K      CHAIN     SYPARL3                            33
     C                   IF        *IN33
     C                   EVAL      X_KUNDNR = 0
     C                   CALL      'VKUNDNYL1'
     C                   PARM                    FIFIRM
     C                   PARM                    X_KUNDNR
     C                   ELSE
     C                   EVAL      X_KUNDNR = S3_SYKUNR
     C                   END
     C                   ELSE
     C                   MOVEL     KNR(XN)       XTXT
     C                   EXSR      KNVTAL
     C                   EVAL      X_KUNDNR = XTAL
     C                   END
      *
     C     CUNDL01K      CHAIN     CUNDL01                            33
     C   33              CLEAR                   CUNDR
      *
     C                   EVAL      AKTKOD = 'A'
     C                   EVAL      FIRMA  = FIFIRM
     C                   EVAL      KUNDNR = X_KUNDNR
     C                   EVAL      KNAVN  = X_KNAVN
     C   33              EVAL      SONAVN = X_KNAVN
     C   33LC:UC         XLATE     SONAVN        SONAVN
     C                   EVAL      ADR1   = AD1(XN)
     C                   IF        LKD(XN) = 'NO'
     C                   MOVEL     PNR(XN)       POSTNR
     C                   ELSE
     C                   EVAL      SYPOGE = PNR(XN)
     C                   END
     C                   EVAL      ADR3   = AD3(XN)
     C                   EVAL      SYLAND = LKD(XN)
     C                   EVAL      VALKOD = X_VALKOD
     C                   EVAL      SYRG   = X_SYRG
     C                   EVAL      KGRENS = X_KGRENS
      *
     C                   IF        *IN33
     C                   WRITE     CUNDR
     C                   ELSE
     C                   UPDATE    CUNDR
     C                   END
      *
     C     KUNDL01K      CHAIN     KUNDL01                            33
     C   33              CLEAR                   KUNDR
      *
     C                   EVAL      AKTKOD = 'A'
     C                   EVAL      FIRMA  = FIFIRM
     C                   EVAL      KUNDNR = X_KUNDNR
     C                   EVAL      KNAVN  = X_KNAVN
     C   33              EVAL      SONAVN = X_KNAVN
     C   33LC:UC         XLATE     SONAVN        SONAVN
     C                   EVAL      ADR1   = AD1(XN)
     C                   IF        LKD(XN) = 'NO'
     C                   MOVEL     PNR(XN)       POSTNR
     C                   ELSE
     C                   EVAL      POSTNU = PNR(XN)
     C                   END
     C                   EVAL      ADR3   = AD3(XN)
     C                   EVAL      LAND   = LKD(XN)
     C                   EVAL      VALKOD = X_VALKOD
     C                   MOVEL     X_SYRG        FORETN
     C                   EVAL      KGRENS = X_KGRENS
      *
     C                   IF        *IN33
     C                   WRITE     KUNDR
     C                   ELSE
     C                   UPDATE    KUNDR
     C                   END
      *
     C     SYPARL1K      CHAIN     SYPARL1                            33
     C                   IF        *IN33
     C                   CLEAR                   SYPARFR
     C     'SYPAR   '    CHAIN     TELLGE                             33
     c                   IF        *IN33
     C                   EVAL      GECO = 'SYPAR   '
     C                   EVAL      GENO = 1
     C                   WRITE     TELLGER
     c                   ELSE
     C                   EVAL      SYRECN = GENO
     C                   EVAL      GENO   = GENO + 1
     C                   UPDATE    TELLGER
     c                   END
     C                   EVAL      SYPAID = 'ZKNIN'
     C*                  EVAL      SYUSER = 'SYXML41'
     C                   EVAL      SYKUNR = KUNDNR
     C                   EVAL      SYVRDN = 0
     C                   EVAL      SYVRDA = X_PARTYNR + PSI(XN) + ACC(XN)
     C                   WRITE     SYPARFR
     C                   ELSE
     C                   EVAL      SYVRDA = X_PARTYNR + PSI(XN) + ACC(XN)
     C                   UPDATE    SYPARFR
     C                   END
      *
     C*                  MOVE      KUNDNR        USYSKN
     C                   MOVE      X_KUNDNR      X_KUNDNRA
     C     1             DO        8             XN2               3 0
     C                   IF        %SUBST(X_KUNDNRA:XN2:1) <> '0'
     C                   LEAVE
     C                   END
     C                   ENDDO
     C                   EVAL      USYSKN = %SUBST(X_KUNDNRA:XN2:9-XN2)
     C                   EVAL      UBRIKN = ACC(XN)
     C                   CALL      'SYXML42'
     C                   PARM                    FILIBF
     C                   PARM                    USYSKN            8
     C                   PARM                    UBRIKN           15
      *
     C                   ENDSR
      *
      ***********************************************
     C     KNVTAL        BEGSR
      ***********************************************
     C                   MOVE      *ZEROS        XTAL
     C     1             DO        15            XN                3 0          1<-B
     C*                  IF        XT(XN) = *BLANKS OR XT(XN) = '.'              2<-B
     C                   IF        XT(XN) = '.' OR XT(XN) = ','                  2<-B
     C                   LEAVE
     C                   ENDIF                                                   2<-E
     C                   IF        XT(XN) <> *BLANKS                             2<-B
     C                   MULT      10            XTALH
     C                   MOVE      XT(XN)        XTALH
     C                   ENDIF                                                   2<-E
     C                   ENDDO                                                  1<-E
      *
     C                   IF        XT(XN) = '.'                                 1<-B
     C                   ADD       1             XN
     C                   MOVE      XT(XN)        XTALD1
     C                   ADD       1             XN
     C                   IF        XT(XN) <> ' '                                 2<-B
     C                   MOVE      XT(XN)        XTALD2
     C                   ADD       1             XN
     C                   IF        XT(XN) <> ' '                                  3<-B
     C                   MOVE      XT(XN)        XTALD3
     C                   END                                                      3<-E
     C                   END                                                     2<-E
     C                   END                                                    1<-E
      *
     C                   ENDSR
      *
      ***********************************************
     C     INIT          BEGSR
      ***********************************************
     C     *ENTRY        PLIST
     C                   PARM                    USN               9
      *
     C     CUNDL01K      KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    X_KUNDNR
     C     KUNDL01K      KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    X_KUNDNR
     C     SYPARL1K      KLIST
     C                   KFLD                    X_KUNDNR
     C                   KFLD                    SYPAID
     C     SYPARL3K      KLIST
     C                   KFLD                    SYPAID
     C                   KFLD                    S3_SYVRDA
     C     XMLKEY01      KLIST
     C                   KFLD                    XRSN
     C                   KFLD                    X001_XRLEV1
     C                   KFLD                    X001_XRLEV2
     C     XMLKEY02      KLIST
     C                   KFLD                    XRSN
     C                   KFLD                    X002_XRLEV1
     C                   KFLD                    X002_XRLEV2
     C                   KFLD                    X002_XRLEV3
     C     XMLKEY03      KLIST
     C                   KFLD                    XRSN
     C                   KFLD                    X003_XRLEV1
     C                   KFLD                    X003_XRLEV2
     C                   KFLD                    X003_XRLEV3
     C                   KFLD                    X003_XRLEV4
     C     XMLKEY04      KLIST
     C                   KFLD                    XRSN
     C                   KFLD                    X004_XRLEV1
     C                   KFLD                    X004_XRLEV2
     C                   KFLD                    X004_XRLEV3
     C                   KFLD                    X004_XRLEV4
     C                   KFLD                    X004_XRLEV5
     C     XMLKEY05      KLIST
     C                   KFLD                    XRSN
     C                   KFLD                    X005_XRLEV1
     C                   KFLD                    X005_XRLEV2
     C                   KFLD                    X005_XRLEV3
     C                   KFLD                    X005_XRLEV4
     C                   KFLD                    X005_XRLEV5
     C                   KFLD                    X005_XRLEV6
     C     XMLKEY06      KLIST
     C                   KFLD                    XRSN
     C                   KFLD                    X006_XRLEV1
     C                   KFLD                    X006_XRLEV2
     C                   KFLD                    X006_XRLEV3
     C                   KFLD                    X006_XRLEV4
     C                   KFLD                    X006_XRLEV5
     C                   KFLD                    X006_XRLEV6
     C                   KFLD                    X006_XRLEV7
     C     XMLKEY07      KLIST
     C                   KFLD                    XRSN
     C                   KFLD                    X007_XRLEV1
     C                   KFLD                    X007_XRLEV2
     C                   KFLD                    X007_XRLEV3
     C                   KFLD                    X007_XRLEV4
     C                   KFLD                    X007_XRLEV5
     C                   KFLD                    X007_XRLEV6
     C                   KFLD                    X007_XRLEV7
     C                   KFLD                    X007_XRLEV8
      *
     C     *LIKE         DEFINE    XRLEV1        X001_XRLEV1
     C     *LIKE         DEFINE    XRLEV2        X001_XRLEV2
     C     *LIKE         DEFINE    XRLEV1        X002_XRLEV1
     C     *LIKE         DEFINE    XRLEV2        X002_XRLEV2
     C     *LIKE         DEFINE    XRLEV3        X002_XRLEV3
     C     *LIKE         DEFINE    XRLEV1        X003_XRLEV1
     C     *LIKE         DEFINE    XRLEV2        X003_XRLEV2
     C     *LIKE         DEFINE    XRLEV3        X003_XRLEV3
     C     *LIKE         DEFINE    XRLEV4        X003_XRLEV4
     C     *LIKE         DEFINE    XRLEV1        X004_XRLEV1
     C     *LIKE         DEFINE    XRLEV2        X004_XRLEV2
     C     *LIKE         DEFINE    XRLEV3        X004_XRLEV3
     C     *LIKE         DEFINE    XRLEV4        X004_XRLEV4
     C     *LIKE         DEFINE    XRLEV5        X004_XRLEV5
     C     *LIKE         DEFINE    XRLEV1        X005_XRLEV1
     C     *LIKE         DEFINE    XRLEV2        X005_XRLEV2
     C     *LIKE         DEFINE    XRLEV3        X005_XRLEV3
     C     *LIKE         DEFINE    XRLEV4        X005_XRLEV4
     C     *LIKE         DEFINE    XRLEV5        X005_XRLEV5
     C     *LIKE         DEFINE    XRLEV6        X005_XRLEV6
     C     *LIKE         DEFINE    XRLEV1        X006_XRLEV1
     C     *LIKE         DEFINE    XRLEV2        X006_XRLEV2
     C     *LIKE         DEFINE    XRLEV3        X006_XRLEV3
     C     *LIKE         DEFINE    XRLEV4        X006_XRLEV4
     C     *LIKE         DEFINE    XRLEV5        X006_XRLEV5
     C     *LIKE         DEFINE    XRLEV6        X006_XRLEV6
     C     *LIKE         DEFINE    XRLEV7        X006_XRLEV7
     C     *LIKE         DEFINE    XRLEV1        X007_XRLEV1
     C     *LIKE         DEFINE    XRLEV2        X007_XRLEV2
     C     *LIKE         DEFINE    XRLEV3        X007_XRLEV3
     C     *LIKE         DEFINE    XRLEV4        X007_XRLEV4
     C     *LIKE         DEFINE    XRLEV5        X007_XRLEV5
     C     *LIKE         DEFINE    XRLEV6        X007_XRLEV6
     C     *LIKE         DEFINE    XRLEV7        X007_XRLEV7
     C     *LIKE         DEFINE    XRLEV8        X007_XRLEV8
     C     *LIKE         DEFINE    KUNDNR        X_KUNDNR
     C     *LIKE         DEFINE    KNAVN         X_KNAVN
     C     *LIKE         DEFINE    SYRG          X_SYRG
     C     *LIKE         DEFINE    VALKOD        X_VALKOD
     C     *LIKE         DEFINE    KGRENS        X_KGRENS
      *
     C                   MOVE      USN           XRSN
     C                   EVAL      SYPAID = 'ZKNIN'
     C                   MOVE      *BLANKS       X_KUNDNRA         8
     C                   MOVE      *BLANKS       X_PARTYNR        15
     C                   MOVE      *BLANKS       X_ACCNR          15
     C                   MOVE      *BLANKS       X_ACCNR2         15
     C                   MOVE      *BLANKS       X_KNSYS           8
     C                   MOVE      *BLANKS       X_PSI            10
     C                   MOVE      *BLANKS       X_OKK             1
     C                   MOVE      *BLANKS       X_ANTK            3 0
     C                   MOVE      *BLANKS       XAN01             1
      *
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVE      WDT           WDT08             8 0
      *
     C     *LOVAL        SETLL     FIRM
     C                   READ      FIRM                                   33
      *
     C                   ENDSR
      *
