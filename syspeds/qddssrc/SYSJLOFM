     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HELP
     A          R BILDE
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF03(03 'AVSLUTT')
     A                                      CF04(04 'SØK')
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/SYSJLO'
     A                                      COLOR(BLU)
     A                                  1 61DATE
     A                                      EDTCDE(Y)
     A                                  1 70TIME
     A                                  1 20MSGCON(040 DIV3692 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                  5  2MSGCON(020 DIV3678 *LIBL/SYMSGF)
     A            WSSG           3A  B  5 28DSPATR(UL)
     A  99                                  DSPATR(RI)
     A                                  7  2MSGCON(020 YBC0578 *LIBL/SYMSGF)
     A            WSDT           6Y 0B  7 28DSPATR(UL)
     A  98                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                  7 35'-'
     A            WSDTT          6Y 0B  7 37DSPATR(UL)
     A  98                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                  8  2MSGCON(020 DIV3693 *LIBL/SYMSGF)
     A            WSSORT         1S 0B  8 28DSPATR(UL)
     A  97                                  DSPATR(RI)
     A                                  8 35MSGCON(040 DIV3694 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                 11  2MSGCON(020 DIV3696 *LIBL/SYMSGF)
     A            WSAVD1         4Y 0B 11 28DSPATR(UL)
     A  96                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A            WSAVD2         4Y 0B 11 38DSPATR(UL)
     A  95                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A            WSAVD3         4Y 0B 11 48DSPATR(UL)
     A  94                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A            WSAVD4         4Y 0B 11 58DSPATR(UL)
     A  93                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A            WSAVD5         4Y 0B 11 68DSPATR(UL)
     A  92                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                 12  2MSGCON(020 DIV3697 *LIBL/SYMSGF)
     A            WSTR1          4A  B 12 28DSPATR(UL)
     A  91                                  DSPATR(RI)
     A            WSTR2          4A  B 12 38DSPATR(UL)
     A            WSTR3          4A  B 12 48DSPATR(UL)
     A            WSTR4          4A  B 12 58DSPATR(UL)
     A            WSTR5          4A  B 12 68DSPATR(UL)
     A                                 13  2MSGCON(020 DIV3698 *LIBL/SYMSGF)
     A                                 13 28MSGCON(010 DIV3699 *LIBL/SYMSGF)
     A            WSÅRF          2Y 0B 13 39DSPATR(UL)
     A                                      EDTCDE(4)
     A                                 13 44MSGCON(005 DIV3700 *LIBL/SYMSGF)
     A            WSUKEF         2Y 0B 13 50DSPATR(UL)
     A  90                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                 13 55MSGCON(010 DIV3701 *LIBL/SYMSGF)
     A            WSÅRT          2Y 0B 13 66DSPATR(UL)
     A                                      EDTCDE(4)
     A                                 13 71MSGCON(005 DIV3700 *LIBL/SYMSGF)
     A            WSUKET         2Y 0B 13 77DSPATR(UL)
     A  89                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                 14  2MSGCON(020 DIV3702 *LIBL/SYMSGF)
     A            WSOT1          2A  B 14 28DSPATR(UL)
     A  88                                  DSPATR(RI)
     A            WSOT2          2A  B 14 38DSPATR(UL)
     A  87                                  DSPATR(RI)
     A            WSOT3          2A  B 14 48DSPATR(UL)
     A  86                                  DSPATR(RI)
     A            WSOT4          2A  B 14 58DSPATR(UL)
     A  85                                  DSPATR(RI)
     A            WSOT5          2A  B 14 68DSPATR(UL)
     A  84                                  DSPATR(RI)
     A                                 15  2MSGCON(020 DIV3703 *LIBL/SYMSGF)
     A            WSCO1          2A  B 15 28DSPATR(UL)
     A  83                                  DSPATR(RI)
     A            WSCO2          2A  B 15 38DSPATR(UL)
     A  82                                  DSPATR(RI)
     A            WSCO3          2A  B 15 48DSPATR(UL)
     A  81                                  DSPATR(RI)
     A            WSCO4          2A  B 15 58DSPATR(UL)
     A  80                                  DSPATR(RI)
     A            WSCO5          2A  B 15 68DSPATR(UL)
     A  79                                  DSPATR(RI)
     A                                 16  2MSGCON(020 DIV3704 *LIBL/SYMSGF)
     A            WSLB1          5A  B 16 28DSPATR(UL)
     A  78                                  DSPATR(RI)
     A            WSLB2          5A  B 16 38DSPATR(UL)
     A  77                                  DSPATR(RI)
     A            WSLB3          5A  B 16 48DSPATR(UL)
     A  76                                  DSPATR(RI)
     A            WSLB4          5A  B 16 58DSPATR(UL)
     A  75                                  DSPATR(RI)
     A            WSLB5          5A  B 16 68DSPATR(UL)
     A  74                                  DSPATR(RI)
     A                                 17  2MSGCON(020 DIV3705 *LIBL/SYMSGF)
     A            WSOB1          5A  B 17 28DSPATR(UL)
     A  73                                  DSPATR(RI)
     A            WSOB2          5A  B 17 38DSPATR(UL)
     A  72                                  DSPATR(RI)
     A            WSOB3          5A  B 17 48DSPATR(UL)
     A  71                                  DSPATR(RI)
     A            WSOB4          5A  B 17 58DSPATR(UL)
     A  70                                  DSPATR(RI)
     A            WSOB5          5A  B 17 68DSPATR(UL)
     A  69                                  DSPATR(RI)
     A                                 18  2MSGCON(020 DIV3706 *LIBL/SYMSGF)
     A            WSKNR1         8Y 0B 18 28DSPATR(UL)
     A  68                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A            WSKNR2         8Y 0B 18 38DSPATR(UL)
     A  67                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A            WSKNR3         8Y 0B 18 48DSPATR(UL)
     A  66                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A            WSKNR4         8Y 0B 18 58DSPATR(UL)
     A  65                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A            WSKNR5         8Y 0B 18 68DSPATR(UL)
     A  64                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                 19  4MSGCON(020 YBC0540 *LIBL/SYMSGF)
     A            WSKKN          8Y 0B 19 28DSPATR(UL)
     A  63                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A            WSKKD          2A  B 19 38DSPATR(UL)
     A  62                                  DSPATR(RI)
     A                                      VALUES('  ' 'R ' 'A ' 'M ' 'N ' 'SA-
     A                                      ' 'K ' 'KA' 'F ')
     A                                 19 42'(R/A/M/N/SA/K/KA/F)'
     A                                 20  4'Kun for region      '
     A            WSRGF          2   B 20 28
     A                                 20 31'-'
     A            WSRGT          2   B 20 33
     A                                 20 42'(Fra - til)'
     A                                 21  2MSGCON(024 DIV1510 *LIBL/SYMSGF)
     A            WSPRT         10A  B 21 28DSPATR(UL)
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(018 DIV0032 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                             24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
