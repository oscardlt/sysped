      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/TDCE005R MODULE(*LIBL/TDCE005R *LIBL/INCGI)
CRTPGM*        ACTGRP(*NEW) AUT(*USE)
      *
      *********************************************************************
      /copy SYSPEDS/qrpgsrcpy,hspecs
      /copy SYSPEDS/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FDKXHK     UF A E           K DISK    USROPN
     F                                     PREFIX(X_)
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy SYSPEDS/qrpgsrcpy,prototypeb
      /copy SYSPEDS/qrpgsrcpy,usec
      /copy SYSPEDS/qrpgsrcpy,variables3
      *
      * Varible for
     D WSUSER          S             10
     D TKAVD_A         S              4
     D TKAVD           S              4  0
     D TKTDN_A         S              7
     D TKTDN           S              7  0
     D TKFT1           S             70
     D TKFT2           S             70
     D TKFT3           S             70
     D TKFT4           S             70
     D TKFT5           S             70
     D TKSK            S              2
     D JSONstring      S          32000
     D Error           S            150
      *get input-string
      /Copy syspeds/qrpgsrcpy,prolog3

      * Parse input
     C                   EXSR      Parse
      * Open files etc
     C                   EXSR      INIT

     C                   callp     gethtmlifs(
     C                             '/syspeddev/html/JSON.html':
     C                             '/CGITAG/')
     C                   callp     wrtsection('top')
      * Teste input og utfør
     C  N50              EXSR      TEST
      * ............................................................................................
      * skriv JSON-Object start..(alltid '{' + x ant param. m/komma mellom (IKKE komma etter siste))
      * ............................................................................................
      *
      /free
        JSONstring='{'
        +'¬user¬ : ¬'+%trim(WSUSER)+'¬, '
        +'¬tkavd¬ : ¬'+%trim(%editc(TKAVD:'Z'))+'¬, '
        +'¬tktdn¬ : ¬'+%trim(%editc(TKTDN:'Z'))+'¬, '
        +'¬tkft1¬ : ¬'+%trim(TKFT1)+'¬, '
        +'¬tkft2¬ : ¬'+%trim(TKFT2)+'¬, '
        +'¬tkft3¬ : ¬'+%trim(TKFT3)+'¬, '
        +'¬tkft4¬ : ¬'+%trim(TKFT4)+'¬, '
        +'¬tkft5¬ : ¬'+%trim(TKFT5)+'¬, '
        +'¬tksk¬ : ¬'+%trim(TKSK)+'¬, '
        +'¬errMsg¬ : ¬'+%trim(Error)+'¬, ';
      /end-free
      * JSONfix escaper " i tekst,  for deretter å bytte ¬->"
     C                   call      'JSONFIX'
     C                   parm                    JSONstring
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')
      *
      * ............................................................................................
      * Skriv JSON-LISTE Start (en liste er EN parameter(fra [ til   )
      * ............................................................................................
     C                   eval      JSONstring ='"sendie014" : ['
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')
      * ............................................................................................
      * Skriv JSON-Liste/Array  Avslutning
      * ............................................................................................
     C                   eval      JSONstring =']'
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')
      * ............................................................................................
      * Skriv JSON-string Avsluttning
      * ............................................................................................
     C                   eval      JSONstring ='}'
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')

     C                   exsr      Exit
      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Lukk fil
     C                   CLOSE     BRIDF
     C  N50              CLOSE     DKXHK

      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   MOVE      *ON           *INLR
     C                   return
     C                   endsr
      *********************************************************
     C     Parse         Begsr
      *********************************************************
      * Get input
     C                   EVAL      WSUSER   = zhbgetvar('USER')
     C                   EVAL      TKAVD_A  = zhbgetvar('TKAVD')
     C                   EVAL      TKAVD    = c2n2(TKAVD_A)
     C                   EVAL      TKTDN_A  = zhbgetvar('TKTDN')
     C                   EVAL      TKTDN    = c2n2(TKTDN_A)
     C                   EVAL      TKFT1    = zhbgetvar('TKFT1')
     C                   EVAL      TKFT2    = zhbgetvar('TKFT2')
     C                   EVAL      TKFT3    = zhbgetvar('TKFT3')
     C                   EVAL      TKFT4    = zhbgetvar('TKFT4')
     C                   EVAL      TKFT5    = zhbgetvar('TKFT5')
     C                   EVAL      TKSK     = zhbgetvar('TKSK')
     C                   ENDSR
      *********************************************************
     C     INIT          Begsr
      *********************************************************
     C                   OPEN      BRIDF
      * Test innlogging
     C     WSUSER        CHAIN     BRIDF                              50
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C                   call      BILIBL
     C                   end

     C     DKXHKey       klist
     C                   kfld                    TKAVD
     C                   kfld                    TKTDN

     C   50              eval      Error = 'Invalid userID'
     C  N50              OPEN      DKXHK

     C                   endsr
      *______________________________________________________
      * TEST                                             TEST
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     TEST          BEGSR
      * Hæmta angivelse
     C     DKXHKey       CHAIN     DKXHK                              55
     C                   IF        *IN55=*ON
     C                   CLEAR                   DKXHKR
     C                   EVAL      X_TKAVD=TKAVD
     C                   EVAL      X_TKTDN=TKTDN
     C                   EVAL      X_TKFT1=TKFT1
     C                   EVAL      X_TKFT2=TKFT2
     C                   EVAL      X_TKFT3=TKFT3
     C                   EVAL      X_TKFT4=TKFT4
     C                   EVAL      X_TKFT5=TKFT5
     C                   EVAL      X_TKSK=TKSK
     C                   WRITE     DKXHKR
     C                   ELSE
     C                   EVAL      X_TKFT1=TKFT1
     C                   EVAL      X_TKFT2=TKFT2
     C                   EVAL      X_TKFT3=TKFT3
     C                   EVAL      X_TKFT4=TKFT4
     C                   EVAL      X_TKFT5=TKFT5
     C                   EVAL      X_TKSK=TKSK
     C                   UPDATE    DKXHKR
     C                   ENDIF
     C                   EXSR      SRS
     C                   ENDSR
      *______________________________________________________
      * Skicka                                            SRS
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     SRS           BEGSR
     C                   eval      Error = *blanks
      *____________________
      * Skapa edifact + fil
      *""""""""""""""""""""
     C                   CALL      'DKX013R'
     C                   PARM                    TKAVD
     C                   PARM                    TKTDN
     C                   PARM      '014'         X1N07             3
     C                   PARM                    UFLAGG            1 0
     C                   ENDSR
