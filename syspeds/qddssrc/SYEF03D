********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr *Sek Sig Vers Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
P8068 * 01 TH  PTF8 NY HELPFUNKSJON  KUN DDS
********************************************************************
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      REF(*LIBL/AIRLH)
     A                                      PRINT
S01  A*                                     HLPDOC('SYEF03010' 'SYEF03 ' 'SY400-
S01  A*                                     LO/MAINT')
N01  A                                      HLPPNLGRP(HLPSCR SYEF03PNL)
N01  A                                      HLPTITLE('VEDLIKEHOLD AV FLYFRAKTRA-
N01  A                                      TER')
     A                                      HELP
     A                                      ALTHELP
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  6  2
     A            AIRLIN    R        O  6  5REFFLD(AIRLHR/AIRLIN)
     A  51                                  COLOR(WHT)
     A            XNAF          15   O  6  9
     A  51                                  COLOR(WHT)
     A            AIRPFR    R        O  6 25REFFLD(AIRLHR/AIRPFR)
     A  52                                  COLOR(WHT)
     A            XSDF          15   O  6 29
     A  52                                  COLOR(WHT)
     A            AIRPTO    R        O  6 45REFFLD(AIRLHR/AIRPTO)
     A  53                                  COLOR(WHT)
     A            XSDT          15   O  6 49
     A  53                                  COLOR(WHT)
     A            AIRCUR    R        O  6 65REFFLD(AIRLHR/AIRCUR)
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A  91N93                               CF03(03 'AVSLUTT')
     A  91N93                               CF04(04 'SØK')
     A  91N93                               CF05(05 'Frisk')
     A  91N93                               CF06(06 'LAGE NY')
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A                                      SFLSIZ(0014)
     A                                      SFLPAG(0013)
     A                                  1  2'SYSPED/SYEF03R'
     A                                      COLOR(BLU)
     A                                  1 20MSGCON(30 YBC0755 SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 61DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  1 70TIME
     A                                      COLOR(BLU)
     A            ZRECNO         4S 0O  2 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  3  2MSGCON(78 YBC0756 SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(78 YBC0757 SYMSGF)
     A                                      COLOR(WHT)
     A            W2NR           1A  I  5  2
     A            W2RLIN    R        B  5  5REFFLD(AIRLHR/AIRLIN)
     A            W2RPFR    R        B  5 25REFFLD(AIRLHR/AIRPFR)
     A            W2RPTO    R        B  5 45REFFLD(AIRLHR/AIRPTO)
     A          R BUNN
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(018 DIV0032 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 42MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 62MSGCON(018 DIV0022 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  98                             24  2MSGCON(78 DIV0012 SYMSGF)
     A                                      COLOR(WHT)
     A          R NYWRK
     A                                      CF03(03 'AVSLUTT')
     A                                      CF04(04 'SØK')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                  1  2'SYSPED/SYEF03R'
     A                                      COLOR(BLU)
     A                                  1 20MSGCON(30 YBC0755 SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 61DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  1 70TIME
     A                                      COLOR(BLU)
     A                                  3  2MSGCON(78 YBC0756 SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(78 YBC0757 SYMSGF)
     A                                      COLOR(WHT)
     A            W2NR           1A  I  5  2
     A            W2RLIN    R        B  5  5REFFLD(AIRLHR/AIRLIN)
     A            W2RPFR    R        B  5 25REFFLD(AIRLHR/AIRPFR)
     A            W2RPTO    R        B  5 45REFFLD(AIRLHR/AIRPTO)
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 42MSGCON(018 DIV0022 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 62MSGCON(018 DIV0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A          R BILDE01
     A                                      CF12(12 'forrige')
     A N27                                  CF04(04 'SØK')
     A                                      WINDOW(7 10 7 60 *NOMSGLIN)
     A                                      OVERLAY
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1  2'SYSPED/SYEF03R'
     A                                      COLOR(BLU)
     A N27                              1 20MSGCON(25 YBC0758 SYMSGF)
     A                                      COLOR(WHT)
     A  27                              1 20MSGCON(25 YBC0759 SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 50DATE
     A                                      EDTCDE(Y)
     A                                  2 50TIME
     A                                  3  2MSGCON(020 SHU0068 *LIBL/SYMSGF)
     A            WSRLIN    R        B  3 23REFFLD(AIRLHR/AIRLIN)
     A                                      EDTCDE(4)
     A  90                                  DSPATR(RI)
     A  27                                  DSPATR(PR)
     A            WSNAF         30A  O  3 29
     A                                  4  2MSGCON(020 SHU0069 *LIBL/SYMSGF)
     A            WSRPFR    R        B  4 23REFFLD(AIRLHR/AIRPFR)
     A  89                                  DSPATR(RI)
     A  27                                  DSPATR(PR)
     A            WSSDF         30A  O  4 29
     A                                  5  2MSGCON(020 SHU0070 *LIBL/SYMSGF)
     A            WSRPTO    R        B  5 23REFFLD(AIRLHR/AIRPTO)
     A  88                                  DSPATR(RI)
     A  27                                  DSPATR(PR)
     A            WSSDT         30A  O  5 29
     A N27                              6  2MSGCON(018 DIV0032 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  6 22MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        58A  O  7  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A          R BILDE02
     A                                      CF12(12 'forrige')
     A                                      CF04(04 'SØK')
     A                                      WINDOW(7 8 11 60 *NOMSGLIN)
     A                                      OVERLAY
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1  2'SYSPED/SYEF03R'
     A                                      COLOR(BLU)
     A                                  1 20MSGCON(26 YBC0760 SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 50DATE
     A                                      EDTCDE(Y)
     A                                  2 50TIME
     A                                  3  2MSGCON(020 SHU0068 *LIBL/SYMSGF)
     A            WSRLI1    R        B  3 23REFFLD(AIRLHR/AIRLIN)
     A                                      EDTCDE(4)
     A  90                                  DSPATR(RI)
     A            WSRLI2    R        B  3 28REFFLD(AIRLHR/AIRLIN)
     A                                      EDTCDE(4)
     A  89                                  DSPATR(RI)
     A            WSRLI3    R        B  3 33REFFLD(AIRLHR/AIRLIN)
     A                                      EDTCDE(4)
     A  88                                  DSPATR(RI)
     A            WSRLI4    R        B  3 38REFFLD(AIRLHR/AIRLIN)
     A                                      EDTCDE(4)
     A  87                                  DSPATR(RI)
     A            WSRLI5    R        B  3 43REFFLD(AIRLHR/AIRLIN)
     A                                      EDTCDE(4)
     A  86                                  DSPATR(RI)
     A            WSRLI6    R        B  3 48REFFLD(AIRLHR/AIRLIN)
     A                                      EDTCDE(4)
     A  85                                  DSPATR(RI)
     A            WSRLI7    R        B  3 53REFFLD(AIRLHR/AIRLIN)
     A                                      EDTCDE(4)
     A  84                                  DSPATR(RI)
     A            WSRLI8    R        B  3 58REFFLD(AIRLHR/AIRLIN)
     A                                      EDTCDE(4)
     A  83                                  DSPATR(RI)
     A*           WSAIRL        30A  O  3 29
     A                                  4  2MSGCON(020 SHU0069 *LIBL/SYMSGF)
     A            WSRPF1    R        B  4 23REFFLD(AIRLHR/AIRPFR)
     A  82                                  DSPATR(RI)
     A            WSRPF2    R        B  4 28REFFLD(AIRLHR/AIRPFR)
     A  81                                  DSPATR(RI)
     A            WSRPF3    R        B  4 33REFFLD(AIRLHR/AIRPFR)
     A  80                                  DSPATR(RI)
     A            WSRPF4    R        B  4 38REFFLD(AIRLHR/AIRPFR)
     A  79                                  DSPATR(RI)
     A            WSRPF5    R        B  4 43REFFLD(AIRLHR/AIRPFR)
     A  78                                  DSPATR(RI)
     A            WSRPF6    R        B  4 48REFFLD(AIRLHR/AIRPFR)
     A  77                                  DSPATR(RI)
     A            WSRPF7    R        B  4 53REFFLD(AIRLHR/AIRPFR)
     A  76                                  DSPATR(RI)
     A            WSRPF8    R        B  4 58REFFLD(AIRLHR/AIRPFR)
     A  75                                  DSPATR(RI)
     A*           WSFROM        30A  O  4 29
     A                                  5  2MSGCON(020 SHU0070 *LIBL/SYMSGF)
     A            WSRPT1    R        B  5 23REFFLD(AIRLHR/AIRPTO)
     A  74                                  DSPATR(RI)
     A            WSRPT2    R        B  5 28REFFLD(AIRLHR/AIRPTO)
     A  73                                  DSPATR(RI)
     A            WSRPT3    R        B  5 33REFFLD(AIRLHR/AIRPTO)
     A  72                                  DSPATR(RI)
     A            WSRPT4    R        B  5 38REFFLD(AIRLHR/AIRPTO)
     A  71                                  DSPATR(RI)
     A            WSRPT5    R        B  5 43REFFLD(AIRLHR/AIRPTO)
     A  70                                  DSPATR(RI)
     A            WSRPT6    R        B  5 48REFFLD(AIRLHR/AIRPTO)
     A  69                                  DSPATR(RI)
     A            WSRPT7    R        B  5 53REFFLD(AIRLHR/AIRPTO)
     A  68                                  DSPATR(RI)
     A            WSRPT8    R        B  5 58REFFLD(AIRLHR/AIRPTO)
     A  67                                  DSPATR(RI)
     A*           WSTO          30A  O  5 29
     A                                  6  2MSGCON(040 SHU0229 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  7  2MSGCON(020 SHU0068 *LIBL/SYMSGF)
     A            AIRLIN    R        O  7 23REFFLD(AIRLHR/AIRLIN)
     A                                      EDTCDE(4)
     A                                  8  2MSGCON(020 SHU0069 *LIBL/SYMSGF)
     A            AIRPFR    R        O  8 23REFFLD(AIRLHR/AIRPFR)
     A                                  9  2MSGCON(020 SHU0070 *LIBL/SYMSGF)
     A            AIRPTO    R        O  9 23REFFLD(AIRLHR/AIRPTO)
     A                                 10  2MSGCON(018 DIV0032 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 10 22MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        58A  O 11  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
