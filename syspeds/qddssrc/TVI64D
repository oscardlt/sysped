     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      REF(*LIBL/EDIM)
     A                                      CF03(03 'Avslutt program')
     A                                      PRINT
     A                                      HLPDOC('STD' 'HELPNU' 'HLP400')
     A                                      HELP
     A          R TVI64DA
     A                                      OVERLAY
     A                                  1  2'TVINN'
     A                                      COLOR(BLU)
     A                                  1 26'Meldinger klare for sending'
     A                                      COLOR(WHT)
     A                                  1 73'TVI64D A'
     A                                      COLOR(BLU)
     A                                  2 37'Start'
     A                                      COLOR(WHT)
     A                                  4  2'Velg og trykk Enter:'
     A                                      COLOR(BLU)
     A                                  6  2'Avdeling . . . . . . . . . . . . .-
     A                                       . :'
     A            ZAVD      R     D  B  6 43REFFLD(MAVD)
     A  31                                  COLOR(RED)
     A                                  6 50'Nummer,  0=alle'
     A                                  8  2'Ventestatus  . . . . . . . . . . .-
     A                                       . :'
     A            ZVEN           4A  B  8 43
     A                                  8 50'Tegn,  *ALL'
     A                                 23  2'F3=Gå ut'
     A                                      COLOR(BLU)
     A          R TVI64DB                   SFL
     A            MSN       R        H
     A            MMN       R        H
     A            MSR       R        H
     A            ZOP            1A  I 10  3VALUES(' ' '2' '4' '5')
     A  30                                  COLOR(RED)
     A  30                                  DSPATR(PC)
     A            MAVD      R        O 10  6EDTCDE(4)
     A            MTDN      R        O 10 11EDTCDE(4)
     A            M0065     R        H
     A            WSIMEX         6   O 10 19
     A            M1N07     R        O 10 26
     A            M0068A    R        O 10 32EDTCDE(4)
     A            M0068B    R        O 10 41EDTCDE(4)
     A            M0068C    R        O 10 48EDTCDE(4)
     A            M3039E    R        O 10 51EDTCDE(4)
     A            MVEN      R        O 10 61
     A            M0035     R        O 10 66
     A          R TVI64DC                   SFLCTL(TVI64DB)
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0012)
     A                                      TEXT('TVIN Meldingslogg, Heading')
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                      CF05(05 'Forny')
     A                                      CF12(12 'Annuller')
     A                                      CF14(14 'Forandre ventestatus')
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A                                  1  2'TVINN'
     A                                      COLOR(BLU)
     A                                  1 27'Meldinger klare for sendning'
     A                                      COLOR(WHT)
     A                                  1 73'TVI64D B'
     A                                      COLOR(BLU)
     A                                  2 38'Valg'
     A                                      COLOR(WHT)
     A                                  4  2'Avdeling:'
     A            ZAVD      R        O  4 12REFFLD(MAVD)
     A                                      EDTCDE(4)
     A                                  4 19'Ventestatus:'
     A            ZVEN           4A  O  4 32
     A                                  6  2'Velg og trykk Enter:'
     A                                      COLOR(BLU)
     A                                  7  4'2=Endre   4=Slette   5=Vise'
     A                                      COLOR(BLU)
     A            ZRECNO         4S 0O  8 68SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  9  2'Alt'
     A                                      COLOR(WHT)
     A                                  9  6'Avd.'
     A                                      COLOR(WHT)
     A                                  9 11'Tdek.nr'
     A                                      COLOR(WHT)
     A                                  9 19'MSG ID'
     A                                      COLOR(WHT)
     A                                  9 26'Funk'
     A                                      COLOR(WHT)
     A                                  9 32'Dato'
     A                                      COLOR(WHT)
     A                                  9 40'Sekvens'
     A                                      COLOR(WHT)
     A                                  9 48'Ver'
     A                                      COLOR(WHT)
     A                                  9 53'Eksp'
     A                                      COLOR(WHT)
     A                                  9 59'Vent'
     A                                      COLOR(WHT)
     A                                  9 65'Tst'
     A                                      COLOR(WHT)
     A          R TVI64DD
     A                                      OVERLAY
     A                                 23  2'F3=Gå ut   F5=Forny   F12=Annuller-
     A                                         F14=Forandre ventestatus'
     A                                      COLOR(BLU)
     A          R TVI64DE
     A                                      OVERLAY
     A                                 23  2'Forandre ventestatus for alle meld-
     A                                      inger fra . . :'
     A                                      COLOR(WHT)
     A            XVENF     R        B 23 54REFFLD(MVEN)
     A                                 23 60'Til . . . :'
     A                                      COLOR(WHT)
     A            XVENT     R        B 23 74REFFLD(MVEN)
     A                                      DSPATR(PC)
     A          R TVI64DØ
     A                                      OVERLAY
     A            ZMSGTX        79A  O 24  2MSGID(&ZMSGNO SYMSGF)
     A N30                                  COLOR(WHT)
     A  30                                  COLOR(RED)
     A            ZMSGNO         7A  P
