     A*%%TS  SD  20181010  211522  JOVO        REL-V7R1M0  5770-WDS
*****A**************************************************************
     A* RETTINGER I DETTE PROGRAM:
PTFnrA* FØRSTE LOSSESTED FRIGJØRES  .....................
"""""A*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10326A* 01 YBC PTF10 SVENSK TOLLAGER
10326A* 02 YSH PTF11 OVERLAPP
10326A* 03 SH  SKILLEARK
*****A**************************************************************
     A*%%EC
     A                                      DSPSIZ(27 132 *DS4)
     A                                      REF(*LIBL/LOGF)
     A                                      HLPPNLGRP(HLPSCR SYTL01PNL)
     A                                      HLPTITLE('Godsregistrering')
     A                                      PRINT
     A                                      HELP
     A                                      ALTHELP(CA01)
      *
     A          R STL01B10
     A                                      CF03(03 'Exit')
     A                                      CA12(12 'Cancel')
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1  2'SYSPED/SYTL01'
     A                                      COLOR(BLU)
     A  64                              1 22MSGCON(045 TRO0431 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A  61                              1 22MSGCON(045 SHU0963 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A  62                              1 22MSGCON(045 SHU0964 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A  63                              1 22MSGCON(045 SHU0981 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 72TIME
     A                                  4  3MSGCON(015 TRO0433 *LIBL/SYMSGF)
     A                                  6  3MSGCON(015 TRO0425 *LIBL/SYMSGF)
     A            SEGN          15A  B  6 20DSPATR(HI)
     A                                  8  3MSGCON(010 TRO0453 *LIBL/SYMSGF)
     A                                  9  3'Kun enhetsnr..:'
     A            SEENH          1A  B  9 20DSPATR(HI)
     A                                 10  3MSGCON(015 TRO0427 *LIBL/SYMSGF)
     A            SEBILN        13A  B 10 20DSPATR(HI)
     A                                 11  3MSGCON(015 TRO0428 *LIBL/SYMSGF)
     A            SEAVG         15A  B 11 20DSPATR(HI)
     A                                 12  3MSGCON(015 TRO0429 *LIBL/SYMSGF)
     A            SETRNR        20A  B 12 20DSPATR(HI)
     A                                 13  3MSGCON(015 TRO0452 *LIBL/SYMSGF)
     A            SEDATF         6Y 0B 13 20DSPATR(HI)
     A                                      EDTCDE(4)
     A                                      DSPATR(UL)
     A            SEDATT         6Y 0B 13 27DSPATR(HI)
     A                                      EDTCDE(4)
     A                                      DSPATR(UL)
     A                                 14  3MSGCON(015 JOV0724 *LIBL/SYMSGF)
     A            SETTYP         3A  B 14 20DSPATR(HI)
     A                                 15  3MSGCON(015 SHU0957 *LIBL/SYMSGF)
     A            SETURN        17   B 15 20DSPATR(HI)
E01  A                                 26  3MSGCON(010 TRO0002 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
E01  A                                 26 65MSGCON(015 TRO0424 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
E01  A  30        MSGTXT        78A  O 27  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A*
     A          R STL01B20
     A*%%TS  SD  20181010  211522  JOVO        REL-V7R1M0  5770-WDS
     A                                      CF03(03 'Exit')
     A                                      CA12(12 'Cancel')
N04  A  49                                  CF10(10 'AKTIVER BEHOLDN')
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1  2'SYSPED/SYTL01'
     A                                      COLOR(BLU)
     A N61N63                           1 20MSGCON(045 TRO0431 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A  61                              1 22MSGCON(045 SHU0963 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A  63                              1 22MSGCON(045 SHU0964 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A N42N43N41                        2 20'Nytt godsnummer registreres'
     A                                      COLOR(RED)
     A  42N43N41                        2 20'Endring av registrert godsnummer'
     A                                      COLOR(RED)
     A  43N42N41                        2 20'Tillegg av transitt på godsnummer'
     A                                      COLOR(RED)
     A  41 45                           2 20'Se på Godsregistreringsdata      '
     A                                      COLOR(RED)
     A  41N45                           2 20'SLETTE Godsregistreringsdata      '
     A                                      COLOR(RED)
     A                                      DSPATR(BL)
     A                                  2 72TIME
     A  41 45                           3 20MSGCON(013 SHU1083 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A  41 45     GOHUSR        10A  O  3 35COLOR(WHT)
     A  41 45     GOHDAT         8Y 0O  3 47EDTWRD('    .  .  ')
     A                                      COLOR(WHT)
     A  41 45     GOHTIM         6Y 0O  3 59COLOR(WHT)
     A                                      EDTWRD('  :  :  ')
     A                                  5 41'Tildelt godsnr..:'
     A            GOGN          15A  O  5 60COLOR(WHT)
     A*           SISTGB         3A  O  5 73COLOR(WHT)
     A  43N42                           6  3'Tillegg til.GN:'
     A  42N43                           6  3'ENDRING AV GN.:'
     A N42N43                           6  3'Nytt godsnr...:'
     A* 42N43
     A* 43N42     WRGNA         12A  O  6 20COLOR(WHT)
     A* 43N42
     A* 42N43     WRGNB          3A  O  6 33COLOR(WHT)
S01  A*           WRGNA         12A  O  6 20COLOR(WHT)
S01  A*           WRGNB          3A  O  6 33COLOR(WHT)
     A N39        WRGNA         12A  O  6 20COLOR(WHT)
     A  39        WRGNSEA        7A  O  6 20COLOR(WHT)
     A  39        WRGNSEB        8A  O  6 28COLOR(WHT)
     A N39        WRGNB          3A  O  6 33COLOR(WHT)
     A                                  6 41'Siste transittnr:'
     A            SISTTR        20A  O  6 60COLOR(WHT)
     A                                  7 20'Manuellt nr:'
     A                                      COLOR(YLW)
S01  A*           GOMAN          3Y 0B  7 33EDTCDE(4)
     A N39        GOMAN          3Y 0B  7 33EDTCDE(4)
     A  39        GOMANSE        5Y 0B  7 33EDTCDE(4)
     A                                  8  3'Grensepassering'
     A            WRGREN    R        B  8 20REFFLD(GODSJR/GOGREN *LIBL/GODSJF)
     A  76                                  DSPATR(RI)
     A                                      DSPATR(UL)
     A  41                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A            WRGRDT         6Y 0B  8 41DSPATR(UL)
     A  41                                  DSPATR(PR)
     A N71                                  COLOR(YLW)
     A  71                                  COLOR(WHT)
     A                                      EDTCDE(Y)
     A            WRGRKL    R        B  8 50REFFLD(GODSJR/GOGRKL *LIBL/GODSJF)
     A                                      EDTWRD('    ')
     A                                      DSPATR(UL)
     A  41                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                  8 56'(Sted / dato / kl. )'
     A                                  9  3MSGCON(015 SHU0957 *LIBL/SYMSGF)
     A            WRTURN        17A  B  9 20
     A  41                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A  75                                  DSPATR(RI)
     A                                 10  3MSGCON(015 TRO0427 *LIBL/SYMSGF)
     A            WRBILN        13A  B 10 20DSPATR(UL)
     A  41                                  DSPATR(PR)
     A  78                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 11  3'Type last.....:'
     A            WRTYPE    R        B 11 20REFFLD(GODSJR/GOTYPE *LIBL/GODSJF)
     A                                      DSPATR(UL)
     A  41                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                 12  3MSGCON(015 TRO0426 *LIBL/SYMSGF)
     A            WRMOTT        15A  B 12 20DSPATR(UL)
     A  41                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A  79                                  DSPATR(RI)
     A                                 13  3MSGCON(015 JOV0724 *LIBL/SYMSGF)
     A  61                                  DSPATR(ND)
     A            WRTRTY         5A  B 13 20TEXT('Tpapir-type')
     A                                      DSPATR(UL)
     A  61
     AO 41                                  DSPATR(PR)
     A  61                                  DSPATR(ND)
     A                                      COLOR(YLW)
     A                                 13 27MSGCON(015 JOV0725 *LIBL/SYMSGF)
     A  61                                  DSPATR(ND)
     A                                 14  3MSGCON(015 TRO0429 *LIBL/SYMSGF)
     A  61                                  DSPATR(ND)
     A            WRTRNR        20A  B 14 20DSPATR(UL)
     A  61
     AO 41                                  DSPATR(PR)
     A  61                                  DSPATR(ND)
     A                                      COLOR(YLW)
     A  74                                  DSPATR(RI)
     A                                 15  3MSGCON(015 TRO0430 *LIBL/SYMSGF)
     A  61                                  DSPATR(ND)
     A            WRTRDT         6Y 0B 15 20DSPATR(UL)
     A                                      EDTCDE(Y)
     A  61
     AO 41                                  DSPATR(PR)
     A  61                                  DSPATR(ND)
     A N70                                  COLOR(YLW)
     A  70                                  COLOR(WHT)
     A  73                                  DSPATR(RI)
     A                                 16  3MSGCON(015 TRO0428 *LIBL/SYMSGF)
     A  61                                  DSPATR(ND)
     A            WRAVG         15A  B 16 20DSPATR(UL)
     A  61
     AO 41                                  DSPATR(PR)
     A  61                                  DSPATR(ND)
     A                                      COLOR(YLW)
N04  A                                 17  3MSGCON(015 JOV1776 *LIBL/SYMSGF)
N04  A  61                                  DSPATR(ND)
N04  A            WRORTN        20A  B 17 20DSPATR(UL)
N04  A  61
N04  AO 41                                  DSPATR(PR)
N04  A  61                                  DSPATR(ND)
N04  A                                      COLOR(YLW)
N04  A  74                                  DSPATR(RI)
N04  A                                 17 42MSGCON(020 JOV1777 *LIBL/SYMSGF)
N04  A  61                                  DSPATR(ND)
N04  A                                 17 63'Type:'
N04  A  61                                  DSPATR(ND)
N04  A            WRORTY         5A  B 17 69TEXT('Tpapir-type')
N04  A                                      DSPATR(UL)
N04  A  61
N04  AO 41                                  DSPATR(PR)
N04  A  61                                  DSPATR(ND)
N04  A                                      COLOR(YLW)
N04  A                                 17 76'(Blank -> T2)'
N04  A  61                                  DSPATR(ND)
N04  A  49                             18 42MSGCON(050 JOV1778 *LIBL/SYMSGF)
N04  A                                      COLOR(BLU)
N04  A  61                                  DSPATR(ND)
     A                                 20  3'Første lossest:'
     A            WRLSEN    R        B 20 20REFFLD(GODSJR/GOLSEN *LIBL/GODSJF)
     A                                      DSPATR(UL)
     A  41                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A            WRLSDT         6Y 0B 20 41DSPATR(HI)
     A  41                                  DSPATR(PR)
     A N70                                  COLOR(YLW)
     A  70                                  COLOR(WHT)
     A                                      EDTCDE(Y)
     A            WRLSKL    R        B 20 50REFFLD(GODSJR/GOLSKL *LIBL/GODSJF)
     A                                      EDTWRD('    ')
     A                                      DSPATR(UL)
     A  41                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                 20 55'(Sted / dato / kl. )'
     A                                 21  3'Reiserute.....:'
     A  61                                  DSPATR(ND)
     A            WRREI1    R        B 21 20REFFLD(GODSJR/GOREI1 *LIBL/GODSJF)
     A                                      DSPATR(UL)
     A  61
     AO 41                                  DSPATR(PR)
     A  61                                  DSPATR(ND)
     A                                      COLOR(YLW)
     A            WRREI2    R        B 21 41REFFLD(GODSJR/GOREI2 *LIBL/GODSJF)
     A                                      DSPATR(UL)
     A  61
     AO 41                                  DSPATR(PR)
     A  61                                  DSPATR(ND)
     A                                      COLOR(YLW)
     A            WRREI3    R        B 22 20REFFLD(GODSJR/GOREI3 *LIBL/GODSJF)
     A  61
     AO 41                                  DSPATR(PR)
     A  61                                  DSPATR(ND)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WRREI4    R        B 22 41REFFLD(GODSJR/GOREI4 *LIBL/GODSJF)
     A                                      DSPATR(UL)
     A  61
     AO 41                                  DSPATR(PR)
     A  61                                  DSPATR(ND)
     A                                      COLOR(YLW)
     A                                 26  3MSGCON(010 TRO0002 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N41N45                          26 20'Tast inn - trykk ENTER'
     A                                      COLOR(WHT)
     A  41N45                          26 20'Trykk ENTER for å SLETTE'
     A                                      COLOR(WHT)
     A                                 26 65MSGCON(015 TRO0424 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 27  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
      *
     A          R STL01B30
     A                                      CF03(03 'Avslutt')
     A                                      CA12(12 'Cancel')
     A                                      CF06(06 'Lag ny')
     A                                      CF08(08 'SØK')
     A                                      OVERLAY
     A                                      PUTOVR
     A            MSGNR          7A  P
     A                                  1  2'SYSPED/SYTL01'
     A                                      COLOR(BLU)
     A N61N63                           1 22MSGCON(045 TRO0431 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A  61                              1 22MSGCON(045 SHU0963 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A  63                              1 22MSGCON(045 SHU0964 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2 22MSGCON(011 SHU0954 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            GFLBKO         5   O  2 35COLOR(WHT)
S01  A*                                 3 22MSGCON(011 SHU0953 *LIBL/SYMSGF)
N01  A N39                              3 22MSGCON(011 SHU0953 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
S01  A*           WSDATO         6  0O  3 35COLOR(WHT)
S01  A*                                 3 42'/'
N01  A N39        WSDATO         6  0O  3 35COLOR(WHT)
N01  A N39                              3 42'/'
     A                                      COLOR(WHT)
S01  A*           WSDAGN         3  0O  3 44COLOR(WHT)
N01  A N39        WSDAGN         3  0O  3 44COLOR(WHT)
     A                                  2 72TIME
     A                                  4  4MSGCON(014 SHU0955 *LIBL/SYMSGF)
     A            WSDANY         6  0I  4 20
     A                                  5  4MSGCON(007 TRO0007 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5 14MSGCON(008 TRO0009 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5 25MSGCON(008 TRO0010 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
S01  A*                                 6  2MSGCON(079 TRO0421 *LIBL/SYMSGF)
N01  A                                  6  2MSGCON(130 YBC2007 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                  9  3MSGCON(040 TRO0423 *LIBL/SYMSGF)
E01  A                                 26  3MSGCON(008 TRO0002 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
E01  A                                 26 29MSGCON(010 TRO0422 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
E01  A                                 26 58MSGCON(010 TRO0432 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
E01  A  30        MSGTXT        78A  O 27  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
      *
     A          R STL01B40
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF03(03 'Exit')
     A                                      CA12(12 'Cancel')
N05  A                                      CF05(05 'REFRESH')
     A                                      CF06(06 'Lag ny')
     A                                      CF08(08 'Søk')
E01  A                                      SLNO(26)
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A  30        MSGTXT        78A  O  2  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A                                  1  2MSGCON(018 TRO0002 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  1 22MSGCON(018 TRO0422 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  1 42MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  1 62MSGCON(018 TRO0432 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A*
     A          R STL01H50                  SFL
     A*%%TS  SD  20181010  122652  JOVO        REL-V7R1M0  5770-WDS
     A                                      SFLNXTCHG
     A            SFRRN          9Y 0H
     A            WSNR           1A  B  7  2
     A  30                                  DSPATR(PC)
     A                                      VALUES(' ' '2' '4' '5' '1' 'M' 'P' -
E03  A                                      'F' 'D' 'H' 'S')
     A            SFFGN         15A  O  7  4
     A  50                                  DSPATR(HI)
     A            SFTRNR        18A  O  7 20
     A  56                                  DSPATR(RI)
S01  A*           SFTURN        26A  O  7 39
     A            SFTURN        17A  O  7 45
     A            SFBILN        13A  O  7 63
     A            SFAVG         10A  O  7 77
     A            SFTRDT         6Y 0O  7 88EDTCDE(4)
     A            SFTRTY         5   O  7 39
     A*
     A          R STL01K60                  SFLCTL(STL01H50)
     A*%%TS  SD  20181010  122652  JOVO        REL-V7R1M0  5770-WDS
     A                                      SFLSIZ(0018)
     A                                      SFLPAG(0017)
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                      CF03(03 'Avslutt')
     A                                      CA12(12 'Cancel')
     A N63                                  CF06(06 'Lag ny')
     A                                      CF08(08 'Søk')
     A                                      CF05(05 'REFRESH')
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  31                                  SFLMSG('Tastet valg er ikke gyldig')
     A          H                           HLPPNLGRP(SYTL010010 SYTL01PNL)
     A                                      HLPARA(01 022 01 048)
     A          H                           HLPPNLGRP(SYTL010020 SYTL01PNL)
     A                                      HLPARA(02 022 02 054)
     A          H                           HLPPNLGRP(SYTL010030 SYTL01PNL)
     A                                      HLPARA(03 022 03 054)
     A          H                           HLPPNLGRP(SYTL010040 SYTL01PNL)
     A                                      HLPARA(04 004 04 026)
     A          H                           HLPPNLGRP(SYTL010050 SYTL01PNL)
     A                                      HLPARA(04 030 04 047)
     A          H                           HLPPNLGRP(SYTL010050 SYTL01PNL)
     A                                      HLPARA(05 014 05 080)
     A          H                           HLPPNLGRP(SYTL010050 SYTL01PNL)
     A                                      HLPARA(06 002 20 002)
     A          H                           HLPPNLGRP(SYTL010060 SYTL01PNL)
     A                                      HLPARA(06 004 20 014)
     A          H                           HLPPNLGRP(SYTL010070 SYTL01PNL)
     A                                      HLPARA(06 016 20 033)
     A            CURS1          4S 0H      SFLRCDNBR(CURSOR)
     A                                  1  2'SYSPED/SYTL01'
     A                                      COLOR(BLU)
     A  64                              1 22MSGCON(045 TRO0431 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A  61                              1 22MSGCON(045 SHU0963 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A  62                              1 22MSGCON(045 SHU0964 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A  63                              1 22MSGCON(045 SHU0981 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  2 22MSGCON(011 SHU0954 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            GFLBKO         5A  O  2 35COLOR(WHT)
     A            GFLBS1        30A  O  2 41COLOR(WHT)
     A                                  2 72TIME
     A                                      COLOR(BLU)
S01  A*                                 3 22MSGCON(011 SHU0953 *LIBL/SYMSGF)
     A N39                              3 22MSGCON(011 SHU0953 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
S01  A*           WSDATO         6Y 0O  3 35COLOR(WHT)
     A N39        WSDATO         6Y 0O  3 35COLOR(WHT)
     A                                      EDTWRD('  .  .  ')
S01  A*                                 3 44'/'
     A N39                              3 44'/'
     A                                      COLOR(WHT)
S01  A*           WSDAGN         3S 0O  3 46COLOR(WHT)
     A N39        WSDAGN         3S 0O  3 46COLOR(WHT)
     A                                  4  4MSGCON(014 SHU0955 *LIBL/SYMSGF)
     A            WSDANY         6S 0I  4 20
S01  A*N61N63                           4 30MSGCON(018 SHU0959 *LIBL/SYMSGF)
S01  A*                                     COLOR(BLU)
S01  A*                                 4 51MSGCON(015 SHU1078 *LIBL/SYMSGF)
S01  A*                                     COLOR(BLU)
     A  60                              5  4MSGCON(008 JOV1260 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N63                              5 14MSGCON(007 TRO0007 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N63                              5 25MSGCON(008 TRO0009 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5 35MSGCON(008 TRO0010 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N61N62                           5 45MSGCON(017 SHU0888 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N61N62                           5 64MSGCON(017 SHU0916 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N61N63                           5 83MSGCON(018 SHU0959 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
E03  A                                  5103MSGCON(010 SHU1078 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
N03  A                                  5115MSGCON(011 SHU2038 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
S01  A*                                 6  2MSGCON(078 TRO0421 *LIBL/SYMSGF)
     A                                  6  2MSGCON(130 JOV1775 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
