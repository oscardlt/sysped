********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
11016 * 01 YBC PTF11 VIS OMBEREGNINGS OPPDRAG
11XXX * 02 YBC PTF11 VIS LØPENR/EKSP.NR TIL OMBEREGNING
11XXX * 03 YBC PTF11 VIS BLANK LØPENR NÅR OMBEREGNING IKKE ER SENDT
11XXX * 04 YBC PTF11 VIS LØPENR VED NYTT LØPENR <> GAMMELT LØPENR
12149 * 05 SH  PTF11 IKKE VISE PERSONNUMMER
xxxxxx*xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
12XXX * 06 YBC PTF12 Parameter UFAKNO er endret lengde
12251 * 07 YBC PTF12 Endring fra midlertidig innførsel til ordinær
12XXX * 08 YBC PTF12 Prosedyre 6021 er bearbeiding
12291 * 09 YBC PTF12 VIS FELT FOR KUNST IMPORT
********************************************************************
      *
      **************************************************************
      *** PROGRAM SKAL SPØRRE PÅ TOLLDEK. (SAD-IMPORT) HOVEDDEL  ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER       ***
      **************************************************************
      *
     FSADH      IF   E           K DISK
N07  FSADHMO    IF   E           K DISK
N01  FSADHOM    IF   E           K DISK    RENAME(SADHR:SADHROM)
     FSADW      IF   E           K DISK
     FSAD005FA  CF   E             WORKSTN
     FFRISOK    IF   E           K DISK
N08  FSYPTFF    IF   E           K DISK
      *** SI27 I SADH ER TATT I BRUK (FØRST 15 POS.)
     D                 DS
     D  SI27                   1     27
     D  SIBELC                 1     11  2
     D  SIRAB                 12     15  2
     D  SIRABX                12     15
      *
     D                 DS
     D  SIFIDÅ                 1      4  0
     D  SIFIDM                 5      6  0
     D  SIFIDD                 7      8  0
     D  SIFID                  1      8  0
     D                 DS
     D  SIDTÅ                  1      4  0
     D  SIDTM                  5      6  0
     D  SIDTD                  7      8  0
     D  SIDT                   1      8  0
     D                 DS
     D  WSDTD                  1      2  0
     D  WSDTM                  3      4  0
     D  WSDTÅ                  5      6  0
     D  WSDT                   1      6  0
     D                 DS
     D  WSFIDD                 1      2  0
     D  WSFIDM                 3      4  0
     D  WSFIDÅ                 5      6  0
     D  WSFID                  1      6  0
N05  D                 DS
N05  D  SIRG                   1     11
N05  D  PRIVAT                10     11
     D                 DS
     D  SITRT                  1      7  0
     D  SISLAA                 1      1  0
     D  SIDFVF                 2      2  0
N09  D  SIKUNS                 3      3  0
     C*
     C                   EXSR      INIT
     C*
     C     START         TAG
N01  C                   IF        UTDN < 0
N04  C                   EVAL      UTDN = UTDN * -1
N04  C     SADHK         CHAIN     SADH                               99
N04  C                   MOVE      SITLL         X_SITLL          10
N04  C                   EVAL      UTDN = UTDN * -1
N01  C     SADHK         CHAIN     SADH                               99
N05  c     PRIVAT        ifne      '  '
N05  c                   move      '***********' sirg
N05  c                   end
S04  C*    SIST          COMP      'P'                                    39
N04  C     SITLL         COMP      X_SITLL                            3939
N03  C                   IF        SIMF <> 'DEB' AND SIMF <> 'DRE'
N03  C                             AND SIMF <> 'DSO'
N04  C                             AND SITLL = X_SITLL
N03  C                   EVAL      SITLL = *BLANKS
N03  C                   EVAL      SITLE = *BLANKS
N03  C                   EVAL      SIDTG = *BLANKS
N03  C                   END
N01  C                   EXFMT     BILDEOM
N01  C                   ELSE
     C                   EXFMT     BILDE
N01  C                   END
     C                   SETOFF                                       99
     C*
     C   01              Z-ADD     1             UFLAGG
     C   03              Z-ADD     3             UFLAGG
     C   12              Z-ADD     9             UFLAGG
     C   01
     COR 03
     COR 12              GOTO      SLUTT
     C   04              DO
     C                   EXSR      VISR28
     C                   GOTO      START
     C                   END
     C   14              DO
     C                   EXSR      SRM
     C   03              GOTO      SLUTT
     C                   GOTO      START
     C                   END
N07   *
N07  C     SADHK         CHAIN     SADHMO                             33
N07  C                   IF        *IN33 = *OFF AND SISTMO = ' '
N07  C                   EXFMT     BILDEMO
N07  C   12              GOTO      START
N07  C                   END
     C*
     C                   EXSR      UTDEL2
     C  N01
     CANN03              GOTO      START
     C*
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
     C*
     C***********************************************
     C     VISR28        BEGSR
     C***********************************************
     C     SIFID         IFNE      99999999
     C                   SETON                                        99
     C                   ELSE
     C                   CALL      'VISRUB28'
     C                   PARM                    UAVD
     C                   PARM                    UTDN
     C                   PARM                    UFLAGG
     C                   END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     UTDEL2        BEGSR
     C***********************************************
     C     SADHK         CHAIN     SADW                               33
     C     *IN33         IFEQ      '1'
     C                   CALL      'SAD005B'
     C                   PARM                    UAVD
     C                   PARM                    UTDN
     C                   PARM                    UFLAGG
     C                   ELSE
     C                   MOVE      '9'           UFLAGG
     C                   CALL      'SAD061B'
     C                   PARM                    UFIRM             2
     C                   PARM                    UAVD
     C                   PARM                    UTDN
     C                   PARM                    UUR               1
S06  C*                  PARM                    UFAKNO            9 0
N06  C                   PARM                    UFAKNO           11 0
     C                   PARM                    UFT              17
     C                   PARM                    UWS              10
     C                   PARM                    UTVIN             1
     C                   PARM                    UIN28             1
     C                   PARM                    UIN29             1
     C                   PARM                    UIN36             1
     C                   PARM      SIDT          USIDT             8 0
     C                   PARM                    UFLAGG
     C                   END
     C     UFLAGG        COMP      1                                      01
     C     UFLAGG        COMP      3                                      03
     C     UFLAGG        COMP      9                                      12
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     SADHK         KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    UTDN
     C     FRIKEY        KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    UTDN
     C                   KFLD                    WSKODE
     C     *LIKE         DEFINE    FSKODE        WSKODE
     C                   MOVEL     '*CR'         WSKODE
     C*
     C     *ENTRY        PLIST
     C                   PARM                    UAVD              4 0
     C                   PARM                    UTDN              7 0
     C                   PARM                    UFLAGG            1 0
     C                   Z-ADD     0             UFLAGG
     C                   MOVE      *ZEROS        SIDT
     C                   MOVE      *ZEROS        WSDT
N08   *
N08  C     'TVI00011'    CHAIN     SYPTFF                             33
N08  C   33              Z-ADD     0             X_4SIFF           8 0
N08  C  N33              MOVE      PTFDT         X_4SIFF
     C*
     C     SADHK         CHAIN     SADH                               99
N05  c     PRIVAT        ifne      '  '
N05  c                   move      '***********' sirg
N05  c                   end
N01  C                   IF        UTDN < 0
N01  C     SADHK         CHAIN     SADHOM                             99
N01  C                   END
     C     FRIKEY        CHAIN     FRISOK                             59
     C  N59              EVAL      EXTREF=FSSOK
     C   59              EVAL      EXTREF=*BLANKS
     C* RETURNCODE 0 MEANS NOT FOUND
     C   99              SETON                                        LR
     C   99              RETURN
N08  C                   IF        SIDT < X_4SIFF
     C     SIDP          COMP      64                                     51
     C  N51SIDP          COMP      65                                     51
     C  N51SIDP          COMP      49                                     51
N08  C                   ELSE
N08  C                   IF        SIDP = 60 AND SIDP2 = 21
N08  C                   EVAL      *IN51 = *ON
N08  C                   END
N08  C                   END
     C     SIRABX        IFEQ      *BLANKS
     C                   MOVE      *ZEROS        WSRAB
     C                   ELSE
     C                   MOVE      SIRAB         WSRAB
     C                   END
     C                   MOVE      SIFIDD        WSFIDD
     C                   MOVE      SIFIDM        WSFIDM
     C                   MOVE      SIFIDÅ        WSFIDÅ
     C                   MOVE      SIDTD         WSDTD
     C                   MOVE      SIDTM         WSDTM
     C                   MOVE      SIDTÅ         WSDTÅ
     C*
     C                   ENDSR
     C*
      *__________________________________________________________
      * SR for å vise TVINN Meldingslogg                      SRM
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     SRM           BEGSR
      *________________________
      * Call på eksternt CL-pgm
      *""""""""""""""""""""""""
     C                   MOVE      '0'           WIN03             1
     C                   CALL      'SAD003'
     C                   PARM                    UAVD
     C                   PARM                    UTDN
     C                   PARM                    WIN03
     C     WIN03         IFEQ      '1'
     C                   SETON                                        03
     C                   Z-ADD     3             UFLAGG
     C                   END
     C*
     C                   ENDSR
     C*
