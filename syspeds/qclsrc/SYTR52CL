    /* UTSKRIFT AV FERJEFAKTURA */
             PGM
             DCL        VAR(&IN01) TYPE(*LGL) LEN(1) /* Hvis &in01 +
                          på, betyr det avslutt program fra +
                          avdelingsvalg. */
             DCL        VAR(&VAL) TYPE(*CHAR) LEN(3) /* VALUTAKODE */
             DCL        VAR(&PRT) TYPE(*CHAR) LEN(10) /* Printernavn */
             DCL        VAR(&PT) TYPE(*CHAR) LEN(10) /* Papirtype */
             DCL        VAR(&PRT2) TYPE(*CHAR) LEN(10) /* PrT GIRO */
             DCL        VAR(&PT2) TYPE(*CHAR) LEN(10) /* Papir GIRO*/
             DCL        VAR(&HE) TYPE(*CHAR) LEN(1) /* HEADING */
             DCL        VAR(&FHE) TYPE(*CHAR) LEN(1) /* FAKT HAEDING */
             DCL        VAR(&FBBS) TYPE(*CHAR) LEN(1) /* FAKT BBS */
             DCL        VAR(&AWB) TYPE(*CHAR) LEN(1) /* SEPAR. GIRO */
             DCL        VAR(&SG) TYPE(*CHAR) LEN(3) /* SIGNATUR */
             DCL        VAR(&AVD) TYPE(*CHAR) LEN(4) /*AVD*/
             DCL        VAR(&TYPE) TYPE(*CHAR) LEN(1) /*T=TEST K=KOPI*/
             DCL        VAR(&JOBQ) TYPE(*CHAR) LEN(1) VALUE('J') /*JOBKØ*/
             DCL        VAR(&MSG) TYPE(*CHAR) LEN(7) +
                          VALUE('DIV0000') /*MELDING OM FORIGE UTSKRIFT*/
             DCL        VAR(&TNR) TYPE(*CHAR) LEN(8) /*TRANSP*/
             DCL        VAR(&PER) TYPE(*CHAR) LEN(2) /*PERMND*/
             DCL        VAR(&AAR) TYPE(*CHAR) LEN(2) /*PERÅR*/
             OVRMSGF    MSGF(QUSERMSG) TOMSGF(SYMSGF)
             RTVDTAARA  DTAARA(*LDA (15 4)) RTNVAR(&AVD)
             RTVDTAARA  DTAARA(*LDA (19 3)) RTNVAR(&SG)
 START:      CALL       PGM(SYTR52A) PARM(&IN01 &AVD &SG &PRT &PT +
                          &FHE &FBBS &AWB &HE &JOBQ &TYPE &MSG &TNR +
                          &AAR &PER &PRT2 &PT2 &VAL)
             CHGDTAARA  DTAARA(*LDA (15 4)) VALUE(&AVD)
             CHGDTAARA  DTAARA(*LDA (19 3)) VALUE(&SG)
             IF         COND(&IN01 = '1') THEN(GOTO CMDLBL(SLUTT)) /* +
                          Hvis &IN01 på, avslutter program. */
    /* */
    /* SKRIV FERJEFAKTURA (1STK)*/
             IF         COND(&JOBQ = 'J') THEN(DO)
             SBMJOB     CMD(CALL PGM(SYTR52CLJ) PARM(&TNR &AAR &PER +
                          &PRT &PT &FHE &FBBS &AWB &AVD &SG &PRT2 +
                          &PT2 &TYPE &VAL)) JOB(FERJEFAKT) +
                          JOBD(*USRPRF) JOBQ(SYJOBQPRT)
                        ENDDO
             ELSE       CMD(DO)
             CALL       PGM(SYTR52CLJ) PARM(&TNR &AAR &PER &PRT &PT +
                          &FHE &FBBS &AWB &AVD &SG &PRT2 &PT2 &TYPE +
                          &VAL)
                        ENDDO
     /* Dersom utskrift er tatt for alle transportører +
        ikke returner til valgbilde  */
             IF         COND(&TNR = '00000000') THEN(DO)
             SNDPGMMSG  MSGID(&MSG) MSGF(SYMSGF)
             GOTO       CMDLBL(SLUTT)
             ENDDO
     /* */
             GOTO       CMDLBL(START)
 SLUTT:      ENDPGM
