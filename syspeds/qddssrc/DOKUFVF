     A*_______________________________
     A* SYSPED / Fraktbrev - varelinje/FARLIG GODS
     A*"""""""""""""""""""""""""""""""
     A                                      REF(SR)
     A          R DOKUFVFR
     A            FFAVD     R               REFFLD(AVD)
     A            FFOPD     R               REFFLD(OPD)
     A            FFFBNR    R               REFFLD(FRBN)
     A            FFLINR         2S 0       COLHDG('Linjenr.')
     A            FFLIN2         2S 0       COLHDG('SUBLinjenr.')
     A            FFUNNR         4          COLHDG('UN-NR')
     A            FFEMBG         3          COLHDG('EMBALASJEGRP')
     A            FFKLAS         4          COLHDG('FAREKLASSE')
     A            FFSEDD        10          COLHDG('FARESEDDEL')
     A            FFTRES        10          COLHDG('TUNELLRESTRIKSJ')
     A            FFFAKT         4S 0       COLHDG('FAKTOR')
     A            FFANTK         5S 0       COLHDG('Ant kll')
     A            FFANTE         7S 0       COLHDG('Ant av akt enh')
     A            FFENH          3          COLHDG('ENHET,LTR/KGM')
     A            FFPOEN         5S 0       COLHDG('ADR-POENG')
     A            FFINDX         1          COLHDG('INDEX V/LIK UN/EM')
     A          K FFAVD
     A          K FFOPD
     A          K FFFBNR
     A          K FFLINR
     A          K FFLIN2
