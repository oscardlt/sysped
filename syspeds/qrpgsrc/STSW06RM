      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/STSW06RM   MODULE(*LIBL/STSW06RM *LIBL/INCGI)
CRTPGM*         ACTGRP(*NEW) AUT(*USE)
      *
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 JOV PTF10 Tillat punktum som desimaltegn/blank godkj. dersom endret.
10XXX *          Dersom cargoscan sumpost finns - ikke lov under den/ellers ikke under HE-verdi
10172 * 02 SH  PTF10 norsk text til Track and trace
********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FHeadf     UF   E           K DISK    USROPN
N01  FCARGOO    IF   E           K DISK    USROPN
     FTRACKF    O  A E           K DISK    USROPN
      *********************************************************************
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      /copy syspeds/qrpgsrcpy,variables3
      *
      * Client input variables
     D WSuser          s             10
     D Avd             s              4
     D AvdN            s              4  0
n04  D Opd             s              7
n04  D OpdN            s              7  0
     D LM              s             10
     D LMN             s              4  2
     D Volum           s             10
     D VolumN          s              7  3
     D Vekt            s             10
     D VektN           s              9  0
     D NumBig          s             19  9
     D LinTxt          s            500
     D VERSJON         s             10
     D                 DS
     D  XULTID                 1     14  0
     D  XULTM                  1      6  0
     D  XULDD                  7      8  0
     D  XULMM                  9     10  0
     D  XULÅÅ                 11     14  0
     D  XULDDMM                7     10  0
     D  XULÅÅ2                13     14  0
     D                 DS
     D  ULÅÅ                   1      4  0
     D  ULMM                   5      6  0
     D  ULDD                   7      8  0
     D  ULDT                   1      8  0
      *
      * Receive query string from the browser
      /copy syspeds/qrpgsrcpy,prolog3
      *
     C                   time                    timedata1
      * Parse input / cvt to numeric
     C                   exsr      Parse
      * File open / keys
     C                   EXSR      INIT
      * Set section variables
     C                   exsr      SetAll
      * Quit
     C                   exsr      Exit
     C                   eval      *inlr = *on
     C                   return
      *
      **************************************************
     C     Parse         begsr
      **************************************************
      * Parse variables from QUERY_STRING environment variable
     C                   eval      WSuser = zhbgetvar('user')
     C                   eval      VERSJON = zhbgetvar('V')
     C                   eval      AVD  = zhbgetvar('AVD')
     C                   eval      AVDN    = c2n2(AVD)
     C                   eval      OPD  = zhbgetvar('OPD')
     C                   eval      OPDN    = c2n2(OPD)
     C                   eval      Vekt    = ZhbGetVar('Vekt')
     C                   eval      NumBig  = c2n2(Vekt)
     C     NumBig        ifle      999999999
     C                   eval      VektN   = NumBig
     C                   end
     C                   eval      Volum   = ZhbGetVar('Volum')
N01  c     '.':','       xlate     Volum         Volum
     C                   eval      NumBig  = c2n2(Volum)
     C     NumBig        ifle      9999.999
     C                   eval      VolumN  = NumBig
     C                   end
     C                   eval      Lm      = ZhbGetVar('LM')
N01  c     '.':','       xlate     Lm            Lm
     C                   eval      NumBig  = c2n2(LM)
     C     NumBig        ifle      99.99
     C                   eval      LmN     = NumBig
     c                   end
     C                   endsr
      **************************************************
     C     Exit          begsr
      **************************************************
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   CLOSE     BRIDF
     C                   CLOSE     HEADF
N01  C                   CLOSE     CARGOO
     C                   CLOSE     TRACKF
     C                   endsr
      **************************************************
     C     SetAll        begsr
      **************************************************
     C                   exsr      TTSR
      * foreløpig ubetinget..
      * (nå med PTF01  burde en returnere et flagg - vellukket endret ??
      *  og en burde returnere/oppdatere verdien i PDA siden verdi kan være overstyrt)
     C                   callp     wrtsection('all')
     C
     C                   endsr
     C
     C***********************************************
     C     TTSR          BEGSR
     C***********************************************
     C                   MOVE      AvdN          TTAVD
     C                   MOVE      OpdN          TTOPD
     C                   MOVE      *ZEROS        TTFBNR
     C                   TIME                    XULTID
     C                   MOVE      XULDD         ULDD
     C                   MOVE      XULMM         ULMM
     C                   MOVE      XULÅÅ         ULÅÅ
     C                   MOVE      ULDT          TTDATE
     C                   MOVE      XULTM         TTTIME
     C                   MOVE      TTDATE        TTDATL
     C                   MOVE      TTTIME        TTTIML
     C                   MOVEL     wsUser        TTUSER
     C                   MOVEL     wsUser        TTNAME
     C                   MOVE      *blanks       TTDEPO
      * Endringer i antall /vekt volum....
     C     HeadKey       Chain     Headf                              33
     C     *IN33         IFEQ      *OFF
N01   * Ikke lov til å gå under Originalverdi dersom den har vært via CS(eller ikke under HE..)
N01  C     HeadKey       Chain     CARGOO                             33
N01  c   33              z-add     HEVKT         COGVKT
N01  c   33              z-add     HEM3          COGM3
N01  c   33              z-add     HELM          COGLM
N01  C     VektN         iflt      COGVKT
N01  c                   z-add     COGVKT        VektN
N01  C                   endif
N01  C     VolumN        iflt      COGM3
N01  c                   z-add     COGM3         VolumN
N01  C                   endif
N01  C     LmN           iflt      COGLM
N01  c                   z-add     COGLM         LmN
N01  C                   endif
     c*
     C     VektN         IFne      HEVKT
     C     VolumN        ORNE      HEM3
     C     LmN           ORNE      HELM
     C                   MOVE      'CHG'         TTACTI
     C                   eval      TTTEXT = 'Changed'
n02  C                   eval      TTTEXL = 'Endret'
     C     VektN         IFNE      HEVKT
     C                   eval      TTTEXT = %trim(TTTEXT) +' Wt:'+
     C                             %trim(%editc(HEVKT:'3'))+'->'+
     C                             %trim(%editc(VektN:'3'))
n02  C                   eval      TTTEXL = %trim(TTTEXL) +' Vkt:'+
n02  C                             %trim(%editc(HEVKT:'3'))+'->'+
n02  C                             %trim(%editc(VektN:'3'))
     C                   END
     C     VolumN        IFNE      HEM3
     C                   eval      TTTEXT = %trim(TTTEXT) +' Vol:'+
     C                             %trim(%editc(HEM3:'L'))+'->'+
     C                             %trim(%editc(VolumN:'L'))
n02  C                   eval      TTTEXL = %trim(TTTEXL) +' Vol:'+
n02  C                             %trim(%editc(HEM3:'L'))+'->'+
n02  C                             %trim(%editc(VolumN:'L'))
     C                   END
     C     LmN           IFNE      HELM
     C                   eval      TTTEXT = %trim(TTTEXT) +' Lm:'+
     C                             %trim(%editc(HELM:'L'))+'->'+
     C                             %trim(%editc(LmN:'L'))
n02  C                   eval      TTTEXL = %trim(TTTEXL) +' Lm:'+
n02  C                             %trim(%editc(HELM:'L'))+'->'+
n02  C                             %trim(%editc(LmN:'L'))
     C                   END
     C                   WRITE     TRACKFR
     C                   eval      HEVKT = VektN
     C                   eval      HEM3  = VolumN
     C                   eval      HELM  = LmN
N01  c                   move      *blanks       HXBLGK
     C                   END                                                    end vkt/M3/LM endret
     C                   Update    HEADFR
     C                   END
     C                   ENDSR
      **************************************************
     C     INIT          begsr
      **************************************************
     C                   OPEN      BRIDF
     C     WSUSER        CHAIN     BRIDF                              50
      * Ugyldig userID
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
     C                   OPEN      HEADF
N01  C                   OPEN      CARGOO
     C                   OPEN      TRACKF
     C     HeadKey       KLIST
     C                   KFLD                    AvdN
     C                   KFLD                    OpdN
      * LOAD HTML
     C                   callp     gethtml('HTMLSRC':
     C                             '*LIBL':
     C                             'STSW06T':'/CGITAG/')
     C                   ENDSR
