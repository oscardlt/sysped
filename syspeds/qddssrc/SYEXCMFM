********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
11XXX * 01 YBC PTF11 PRINTERSTYRING FOR LABEL
********************************************************************
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPDOC('STD' 'HELPNU' 'HLP400')
     A                                      HELP
     A                                      ALTHELP
     A          R BILDE
     A                                      CF03(03 'Avslutt')
     A                                  1  3'SYSPED/SYEXCM'
     A                                      COLOR(BLU)
     A            MSG010        40A  O  1 20MSGID(DIV 1402 SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 62DATE
     A                                      EDTCDE(Y)
     A            MSG011        40A  O  2 20MSGID(DIV 2221 SYMSGF)
     A                                      COLOR(WHT)
     A                                  2 62TIME
     A                                  4 19'Tast inn avdelingsnr.:'
     A            WSAVD          4Y 0B  4 43DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A  99                                  ERRMSG('Tastet kostnadssted er ikke-
     A                                       registrert ' 99)
     A                                  6 19'Tast inn signatur:'
     A            WSSG           3A  B  6 43COLOR(YLW)
     A  98                                  ERRMSG('Tastet signatur er ikke reg-
     A                                      istrert.' 98)
     A                                      DSPATR(UL)
     A                                  6 53MSGCON(012 DIV1513 *LIBL/SYMSGF)
     A                                  7 19'Kun CMR for tur nr..:'
     A            WSPRO          8Y 0B  7 43DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  7 53MSGCON(012 DIV1518 *LIBL/SYMSGF)
     A                                  8 19'Kun CMR for oppdrag.:'
     A            WSOPD          7Y 0B  8 43DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  8 53MSGCON(012 DIV1518 *LIBL/SYMSGF)
     A                                  9 19'Skal jobkø benyttes?:'
     A            WSJBK          1A  B  9 43DSPATR(UL)
     A                                      VALUES(' ' 'J' 'N')
     A                                      COLOR(YLW)
     A                                  9 53'(J = Ja)'
     A                                 11 19'Avvikende printer:'
     A            WSPRT         10A  B 11 43DSPATR(UL)
     A                                      COLOR(YLW)
N01  A                                 12 19'Avvik. ML.printer:'
N01  A            WSPRT2        10A  B 12 43DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 23 59'F3 = Avslutt '
     A                                      COLOR(BLU)
     A N30                             24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
