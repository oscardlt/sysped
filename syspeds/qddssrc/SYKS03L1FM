     A*%%TS  SD  20160913  080310  SVEIN       REL-V7R1M0  5770-WDS
*****A**************************************************************
     A* RETTINGER I DETTE PROGRAM:
PTFnrA*Sek Sig Vers Beskrivelse...........................
"""""A*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
P8068A* 01 TH  PTF8 NY HELPFUNKSJON  KUN DDS
*****A**************************************************************
     A*%%EC
     A                                      DSPSIZ(27 132 *DS4)
     A                                      REF(*LIBL/KODTLK)
     A                                      PRINT
     A                                      HLPPNLGRP(HLPSCR SYEX05PNL)
     A                                      HLPTITLE('VEDLIKEHOLD AV FLYFRAKTRA-
     A                                      TER')
     A                                      HELP
     A                                      ALTHELP
     A          R SUBFIL                    SFL
     A*%%TS  SD  20160913  073919  SVEIN       REL-V7R1M0  5770-WDS
     A            RRNP          10Y 0H
     A            KBKDMV    R        H      REFFLD(KOSTBR/KBKDMV *LIBL/KOSTB)
     A            WSNR           1A  I  6  3
     A  98                                  DSPATR(RI)
     A  98                                  DSPATR(PC)
     A  28
     AO 29                                  DSPATR(PR)
     A  28
     AO 29                                  DSPATR(ND)
     A                                      VALUES(' ' '2' '4' 'A' 'R' 'O' 'S')
     A            KAVAL          3A  O  6  7
     A  28                                  COLOR(YLW)
     A            KBBLVA    R        O  6 12REFFLD(KOSTBR/KBBLHB *LIBL/KOSTB)
     A                                      EDTCDE(M)
     A  28                                  COLOR(YLW)
     A            KBBLHB    R        O  6 29REFFLD(KOSTBR/KBBLHB *LIBL/KOSTB)
     A                                      EDTCDE(M)
     A            KBREFCKO       5Y 0O  6 45
     A            KBREFCAV       4Y 0O  6 51
     A            KBREFCBA       4Y 0O  6 56
     A            KBREFCPR       8Y 0O  6 61
     A            KBKDPF    R        O  6 70REFFLD(KOSTBR/KBKDPF *LIBL/KOSTB)
     A            KBKDM     R        O  6 72REFFLD(KOSTBR/KBKDM *LIBL/KOSTB)
     A*           KBKDMV    R        O  6 74REFFLD(KOSTBR/KBKDMV *LIBL/KOSTB)
     A            KBPMN     R        O  6 75REFFLD(KOSTBR/KBPMN *LIBL/KOSTB)
     A            KBPÅR     R        O  6 78REFFLD(KOSTBR/KBPÅR *LIBL/KOSTB)
     A            KBSGG     R        O  6 81REFFLD(KOSTBR/KBSGG *LIBL/KOSTB)
     A                                      COLOR(RED)
     A            KBREKL         2   O  6 85
     A  79                                  DSPATR(RI)
     A            KBBILT    R        O  6116REFFLD(KOSTBR/KBBILT *LIBL/KOSTB)
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A*%%TS  SD  20160913  080310  SVEIN       REL-V7R1M0  5770-WDS
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                  1  2'SYSPED/SYKS03L1'
     A                                      COLOR(BLU)
     A                                  1 18MSGCON(030 SHU1641 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A  29                              1 53MSGCON(010 SHU1652 *LIBL/SYMSGF)
     A                                      COLOR(RED)
     A                                  1 68MSGCON(009 SHU1643 *LIBL/SYMSGF)
     A            KABNR2    R        O  1 78REFFLD(KOSTAR/KABNR2 *LIBL/KOSTA)
     A                                      COLOR(WHT)
     A                                  1 86MSGCON(007 SHU1645 *LIBL/SYMSGF)
     A            KAFNR     R        O  1 95REFFLD(KOSTAR/KAFNR *LIBL/KOSTA)
     A                                      COLOR(WHT)
     A                                  1115DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  1124TIME
     A                                      COLOR(BLU)
     A                                  2  2MSGCON(011 SHU1646 *LIBL/SYMSGF)
     A            KALNR     R        O  2 14REFFLD(KOSTAR/KALNR *LIBL/KOSTA)
     A                                      COLOR(WHT)
     A            LNAVN         30A  O  2 23COLOR(WHT)
     A                                  2 68MSGCON(009 SHU1644 *LIBL/SYMSGF)
     A            KABNR     R        O  2 78REFFLD(KOSTAR/KABNR *LIBL/KOSTA)
     A                                      COLOR(WHT)
     A                                  2 86MSGCON(008 SHU1647 *LIBL/SYMSGF)
     A            DATOFA         6Y 0O  2 95EDTWRD('  .  .  ')
     A                                      COLOR(WHT)
     A            ZRECNO         4S 0O  2127SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  3  2MSGCON(011 SHU1649 *LIBL/SYMSGF)
     A            KAVAL     R        O  3 14REFFLD(KOSTAR/KAVAL *LIBL/KOSTA)
     A                                      COLOR(WHT)
     A            KABL      R        O  3 18REFFLD(KOSTAR/KABL *LIBL/KOSTA)
     A                                      COLOR(WHT)
     A                                      EDTCDE(M)
     A                                  3 34MSGCON(005 SHU1650 *LIBL/SYMSGF)
     A            KAVKU     R        O  3 40REFFLD(KOSTAR/KAVKU *LIBL/KOSTA)
     A                                      EDTCDE(M)
     A                                      COLOR(WHT)
     A                                  2105MSGCON(008 SHU1648 *LIBL/SYMSGF)
     A            DATOFO         6Y 0O  2115EDTWRD('  .  .  ')
     A                                      COLOR(WHT)
     A                                  4  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 33MSGCON(017 DIV0017 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 52MSGCON(016 SHU1653 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3105MSGCON(008 SHU1651 *LIBL/SYMSGF)
     A            KAPMN          2S 0O  3115COLOR(WHT)
     A            KAPÅR          2S 0O  3118COLOR(WHT)
     A                                  5  2MSGCON(120 SHU1642 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                      DSPATR(UL)
     A                                  4 70MSGCON(044 SHU1689 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A          R BUNN
     A*%%TS  SD  20160826  071924  SVEIN       REL-V7R1M0  5770-WDS
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF03(03 'Avslutt')
     A                                      CF05(05 'Frisk')
     A N29                                  CF06(06 'LAGE NY')
     A N29                                  CF16(16 'KUN UATTTEST')
     A N29                                  CF18(18 'OPPHEV ATT')
     A N29 95                               CF10(10 'FERDIGMELD')
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      SLNO(21)
     A                                      CLRL(06)
     A                                      OVERLAY
     A            MSGNR          7A  P
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  2 85MSGCON(010 JOV0342 *LIBL/SYMSGF)
     A            KAVAL          3A  O  2 97DSPATR(HI)
     A            KABL          13Y 2O  2101EDTCDE(M)
     A                                      DSPATR(HI)
     A                                  3 85MSGCON(008 DIV2183 *LIBL/SYMSGF)
     A            SUMF          13Y 2O  3101EDTCDE(M)
     A                                      DSPATR(HI)
     A                                  4 85MSGCON(006 DIV2184 *LIBL/SYMSGF)
     A  95                              4 94MSGCON(006 SHU1165 *LIBL/SYMSGF)
     A                                      COLOR(RED)
     A                                      DSPATR(BL)
     A N95        DIFF          13Y 2O  4101EDTCDE(M)
     A                                      DSPATR(HI)
     A  95                                  COLOR(RED)
     A                                  3  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N29                              3 42MSGCON(018 DIV0022 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 62MSGCON(018 SHU1654 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  95                              4101MSGCON(018 JOV0333 *LIBL/SYMSGF)
     A                                      COLOR(RED)
     A  30        MSGTXT        78A  O  5  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A                                  4  2MSGCON(018 SHU1684 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A          R VARELI
     A*%%TS  SD  20160318  131859  SVEIN       REL-V7R1M0  5770-WDS
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF12(12 'Forrige')
     A                                      CF04(04 'SOK')
     A                                      SLNO(20)
     A                                      CLRL(05)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1  2MSGCON(130 SHU1642 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                      DSPATR(UL)
     A            KAVAL          3A  O  2  7
     A            WSBEL         13Y 2B  2 12EDTCDE(M)
     A            WSBELN        13Y 2O  2 28EDTCDE(M)
     A                                      COLOR(WHT)
     A            WSKONT         5Y 0B  2 45EDTCDE(Z)
     A  71                                  DSPATR(RI)
     A            WSKSTE         4Y 0B  2 51EDTCDE(Z)
     A  72                                  DSPATR(RI)
     A            WSBARE         4Y 0B  2 56EDTCDE(Z)
     A  73                                  DSPATR(RI)
     A            WSPROS         8Y 0B  2 61EDTCDE(Z)
     A  74                                  DSPATR(RI)
     A            WSPF           1A  B  2 70VALUES('P' 'F' '+')
     A  76                                  DSPATR(RI)
     A            WSPF2          1S 0O  2 72
     A            WSMND          2Y 0B  2 75EDTCDE(Z)
     A  75                                  DSPATR(RI)
     A            WSAAR          2Y 0B  2 78EDTCDE(Z)
     A            WSSG           3A  O  2 81
     A                                  2101MSGCON(010 JOV0342 *LIBL/SYMSGF)
     A            KAVAL2         3A  O  2113DSPATR(HI)
     A            KABL          13Y 2O  2117EDTCDE(M)
     A                                      DSPATR(HI)
     A                                  3101MSGCON(008 DIV2183 *LIBL/SYMSGF)
     A            SUMF          13Y 2O  3117EDTCDE(M)
     A                                      DSPATR(HI)
     A                                  4  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 21MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4101MSGCON(006 DIV2184 *LIBL/SYMSGF)
     A  95                              4110MSGCON(006 SHU1165 *LIBL/SYMSGF)
     A N95        DIFF          13Y 2O  4117EDTCDE(M)
     A                                      COLOR(RED)
     A                                      DSPATR(BL)
     A  30        MSGTXT        78A  O  5  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A          R VAREL2
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  2  2MSGCON(12 DIV0026 SYMSGF)
     A            XYZ            2A  I  2 16DSPATR(UL)
     A                                      DSPATR(PC)
     A                                  2  2MSGCON(18 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A          R VAREL3
     A                                      CF12(12 'Forrige')
     A                                      SLNO(20)
     A                                      CLRL(05)
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2MSGCON(79 DIV1581 SYMSGF)
     A                                      COLOR(WHT)
     A                                      DSPATR(HI)
     A                                  3  2MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A          R NYWRK
     A                                      CF03(03 'Avslutt')
     A                                      CF05(05 'Frisk')
     A N29                                  CF06(06 'LAGE NY')
     A N29                                  CF16(16 'KUN UATTTEST')
     A                                  1  2'SYSPED/SYKS03L1'
     A                                      COLOR(BLU)
     A                                  1 18MSGCON(030 SHU1641 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A  29                              1 53MSGCON(010 SHU1652 *LIBL/SYMSGF)
     A                                      COLOR(RED)
     A                                  1 68MSGCON(009 SHU1643 *LIBL/SYMSGF)
     A            KABNR2    R        O  1 78REFFLD(KOSTAR/KABNR2 *LIBL/KOSTA)
     A                                      COLOR(WHT)
     A                                  1 86MSGCON(007 SHU1645 *LIBL/SYMSGF)
     A            KAFNR     R        O  1 95REFFLD(KOSTAR/KAFNR *LIBL/KOSTA)
     A                                      COLOR(WHT)
     A                                  1115DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  1124TIME
     A                                      COLOR(BLU)
     A                                  2  2MSGCON(011 SHU1646 *LIBL/SYMSGF)
     A            KALNR     R        O  2 14REFFLD(KOSTAR/KALNR *LIBL/KOSTA)
     A                                      COLOR(WHT)
     A            LNAVN         30A  O  2 23COLOR(WHT)
     A                                  2 68MSGCON(009 SHU1644 *LIBL/SYMSGF)
     A            KABNR     R        O  2 78REFFLD(KOSTAR/KABNR *LIBL/KOSTA)
     A                                      COLOR(WHT)
     A                                  2 86MSGCON(008 SHU1647 *LIBL/SYMSGF)
     A            DATOFA         6Y 0O  2 95EDTWRD('  .  .  ')
     A                                      COLOR(WHT)
     A                                  3  2MSGCON(011 SHU1649 *LIBL/SYMSGF)
     A            KAVAL     R        O  3 14REFFLD(KOSTAR/KAVAL *LIBL/KOSTA)
     A                                      COLOR(WHT)
     A            KABL      R        O  3 18REFFLD(KOSTAR/KABL *LIBL/KOSTA)
     A                                      COLOR(WHT)
     A                                      EDTCDE(M)
     A                                  3 34MSGCON(005 SHU1650 *LIBL/SYMSGF)
     A            KAVKU     R        O  3 40REFFLD(KOSTAR/KAVKU *LIBL/KOSTA)
     A                                      EDTCDE(M)
     A                                      COLOR(WHT)
     A                                  3 86MSGCON(008 SHU1648 *LIBL/SYMSGF)
     A            DATOFO         6Y 0O  3 95EDTWRD('  .  .  ')
     A                                      COLOR(WHT)
     A                                  4  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 33MSGCON(015 DIV0017 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 50MSGCON(015 SHU1653 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 86MSGCON(008 SHU1651 *LIBL/SYMSGF)
     A            KAPMN          2S 0O  4 98COLOR(WHT)
     A            KAPÅR          2S 0O  4101COLOR(WHT)
     A                                  5  2MSGCON(120 SHU1642 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                      DSPATR(UL)
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N29                             23 42MSGCON(018 DIV0022 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 62MSGCON(018 SHU1654 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 22 85MSGCON(010 JOV0342 *LIBL/SYMSGF)
     A            KAVAL2         3A  O 22 97DSPATR(HI)
     A            KABL2         13Y 2O 22101EDTCDE(M)
     A                                      DSPATR(HI)
     A                                 23 85MSGCON(008 DIV2183 *LIBL/SYMSGF)
     A            SUMF          13Y 2O 23101EDTCDE(M)
     A                                      DSPATR(HI)
     A                                 24 85MSGCON(006 DIV2184 *LIBL/SYMSGF)
     A  95                             24 94MSGCON(006 SHU1165 *LIBL/SYMSGF)
     A                                      COLOR(RED)
     A                                      DSPATR(BL)
     A N95        DIFF          13Y 2O 24101EDTCDE(M)
     A                                      DSPATR(HI)
     A  95                                  COLOR(RED)
     A          R BUNN2
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF03(12 'FORRIGE')
     A                                      CF12(12 'FORRIGE')
     A                                      CF04(04 'Søking')
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      SLNO(19)
     A                                      CLRL(06)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1  2MSGCON(010 JOV0342 *LIBL/SYMSGF)
     A            KAVAL          3A  O  1 14DSPATR(HI)
     A            KABL          13Y 2O  1 18EDTCDE(M)
     A                                      DSPATR(HI)
     A                                  1 34MSGCON(008 DIV2183 *LIBL/SYMSGF)
     A            SUMF          13Y 2O  1 43EDTCDE(M)
     A                                      DSPATR(HI)
     A                                  1 59MSGCON(006 DIV2184 *LIBL/SYMSGF)
     A            DIFF          13Y 2O  1 66EDTCDE(M)
     A                                      DSPATR(HI)
     A                                  2  2MSGCON(078 JOV0382 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  3  2MSGCON(030 DIV2150 *LIBL/SYMSGF)
     A            WSBSR          1A  B  3 42TEXT('BILAGSSERIE')
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A                                  4  2MSGCON(030 DIV2152 *LIBL/SYMSGF)
     A            WSBNR2         7Y 0B  4 42TEXT('BILAGSNUMMER')
     A                                      COLOR(YLW)
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
     A                                  5 61MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  6  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A*
