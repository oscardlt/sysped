      *********************************************************************
      *****  DELETE  av et oppdrag                                    *****
      *********************************************************************
CRTPG+*  CRTPGM SYSPED/TJEOPD6LD MODULE(*LIBL/TJEOPD6LD *LIBL/INCGI)
CRTPGM*        ACTGRP(*NEW) AUT(*USE)
      *********************************************************************
      *********************************************************************
      *  01 PTF11 JOV
      *********************************************************************
      /copy SYSPEDS/qrpgsrcpy,hspecs
      /copy SYSPEDS/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FHEADF     UF   E           K DISK    USROPN
     FDISP      UF   E           K DISK    USROPN
     FKOSBU9    UF   E           K DISK    USROPN
     FFAK4      UF   E           K DISK    USROPN
     FFRISOK    UF   E           K DISK    USROPN
     FSAML1     UF   E           K DISK    USROPN
     FDOKUF     UF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy SYSPEDS/qrpgsrcpy,prototypeb
      /copy SYSPEDS/qrpgsrcpy,usec
      /copy SYSPEDS/qrpgsrcpy,variables3
      *
      * Varible for
     D P               S              5U 0
     D WSUSER          S             10
     D HEAVDA          S              4
     D HEOPDA          S              7
     D JSONstring      S          32000
     D Error           S            150
     D                 DS
     D  DFBREF                 1     17
     D  DFBRAV                 6      9
     D  DFBROP                11     17
     D                 DS
     D  XULTID                 1     14  0
     D  XULTM                  1      6  0
     D  XULDD                  7      8  0
     D  XULMM                  9     10  0
     D  XULÅÅ                 11     14  0
     D                 DS
     D  ULÅÅ                   1      4  0
     D  ULMM                   5      6  0
     D  ULDD                   7      8  0
     D  ULDT                   1      8  0

      *get input-string
      /copy syspeds/qrpgsrcpy,prolog3

      * Parse input
     C                   EXSR      Parse
      * Open files etc
     C                   EXSR      INIT
      *
     c                   callp     gethtmlifs(
     C                             '/syspeddev/html/JSON.html':
     C                             '/CGITAG/')
     C                   callp     wrtsection('top')
      * Teste input og utfør
     C  N50              EXSR      TEST

      * ............................................................................................
      * skriv JSON-Object start..(alltid '{' + x ant param. m/komma mellom (IKKE komma etter siste))
      * ............................................................................................
      *
      /free
        JSONstring='{'
        +'¬user¬ : ¬'+%trim(WSUSER)+'¬, '
        +'¬heavd¬ : ¬'+%trim(%editc(HEAVD:'Z'))+'¬, '
        +'¬heopd¬ : ¬'+%trim(%editc(HEOPD:'Z'))+'¬, '
        +'¬errMsg¬ : ¬'+%trim(Error)+'¬, ';
      /end-free
      * JSONfix escaper " i tekst,  for deretter å bytte ¬->"
     c                   call      'JSONFIX'
     c                   parm                    JSONstring
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      *
      * ............................................................................................
      * Skriv JSON-LISTE Start (en liste er EN parameter(fra [ til   )
      * ............................................................................................
     C                   eval      JSONstring ='"orderDelete" : ['
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      * ............................................................................................
      * Skriv JSON-Liste/Array  Avslutning
      * ............................................................................................
     c                   eval      JSONstring =']'
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      * ............................................................................................
      * Skriv JSON-string Avsluttning
      * ............................................................................................
     c                   eval      JSONstring ='}'
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      *
      * Quit
     C                   exsr      Exit


      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Lukk fil
     C                   CLOSE     BRIDF
     C  N50              CLOSE     HEADF
     C  N50              CLOSE     DISP
     C  N50              CLOSE     KOSBU9
     C  N50              CLOSE     FAK4
     C  N50              CLOSE     FRISOK
     C  N50              CLOSE     SAML1
     C  N50              CLOSE     DOKUF

      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   MOVE      *ON           *INLR
     C                   return
     C                   endsr
      *********************************************************
     C     Parse         Begsr
      *********************************************************
      * Get input
     C                   EVAL      WSUSER  = zhbgetvar('USER')
     C                   EVAL      HEAVDA = zhbgetvar('AVD')
     C                   EVAL      HEAVD  = c2n2(HEAVDA)
     C                   EVAL      HEOPDA = zhbgetvar('OPD')
     C                   EVAL      HEOPD  = c2n2(HEOPDA)
     C                   ENDSR
      *********************************************************
     C     INIT          Begsr
      *********************************************************
     C                   OPEN      BRIDF
      * Test innlogging
     C     WSUSER        CHAIN     BRIDF                              50
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C                   call      BILIBL
     C                   end

     C     HEADKEY       KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C     KKKHE0        KLIST
     C                   KFLD                    YYAVD0
     C                   KFLD                    YYOPD0
     C     KOSB9K        KLIST
     C                   KFLD                    YYST              1
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C     DOKUF0K       KLIST
     C                   KFLD                    DFRI
     C                   KFLD                    YYAVD0
     C                   KFLD                    YYOPD0
     C                   move      'F'           DFRI

     C   50              EVAL      Error = 'Invalid userID'
     C  N50              OPEN      HEADF
     C  N50              OPEN      DISP
     C  N50              OPEN      KOSBU9
     C  N50              OPEN      FAK4
     C  N50              OPEN      FRISOK
     C  N50              OPEN      SAML1
     C  N50              OPEN      DOKUF
      *_________________________________
      * Hæmta dagens datum och klockslag
      *"""""""""""""""""""""""""""""""""
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6

     c                   ENDSR
      *
     ****************************************************************
     C     TEST          BEGSR
     ****************************************************************
      * se også sr SLETTO i SYTR33
     C     HEADKEY       CHAIN(N)  HEADF                              33
     C     *in33         IFeq      '1'
     C                   eval      Error = 'Ukjent oppdragsnr '
     c                             +%trim(%editc(HEAVD:'Z'))+'/'
     c                             +%trim(%editc(HEOPD:'Z'))
     c                   goto      UtTest
     c                   ENDIF
      * Oppdragsstatus
     C     HEST          IFNE      ' '
     C                   eval      Error = 'Kan ikke slettes - feil status'
     c                   goto      UtTest
     c                   ENDIF
      * IKKE TILATT Å SLETTE NÅR DUP-OPPDRAG LIGGER UNDER.....
      *
     C     TROPD1        IFNE      *ZEROS
     C     TROPD2        ORNE      *ZEROS
     C                   eval      Error = 'Kan ikke slettes - Har DUP under '
     C                             + 'seg. DUP må slettes først. '
     c                   goto      UtTest
     C                   END
      *
      * SJEKK MOT FAKTURALINJER
      *
     C                   move      'W'           SLETTE                         =Kall fra web
     C                   CALL      'SYFA93B'
     C                   PARM                    HEAVD
     C                   PARM                    HEOPD
     C                   PARM                    SLETTE            1
     C     SLETTE        IFEQ      'N'
     C                   eval      Error = 'Kan ikke slettes - Har fakturalin'
     C                             + 'er med stats som ikke kan slettes.'
     c                   goto      UtTest
     C                   END
      *
      * gjennom alle tester - slett oppdrag
     c                   exsr      SRD
      *
     c     UtTest        ENDSR
     ****************************************************************
     C     SRD           BEGSR
     ****************************************************************
     C     HEADKEY       CHAIN     HEADF                              33
     C                   EVAL      HEST='S'
     C                   EVAL      HENAS='*SLETTET*'
     C                   EVAL      HEPK3='J'                                    ?? likt som sytr33
     C                   IF        HXSNDN <> *blanks
     C                   MOVEL     '*D*'         HXSNDN
     C                   END
     C  N33              UPDATE    HEADFR
      *
     C     HEADKEY       CHAIN     DISP                               33
     C  N33              MOVE      'D'           DISTA1
     C  N33              UPDATE    DISPR
      *
      * Slett REKVISISJONER
      *
     C     KOSB9K        CHAIN     KOSBU9                             33
     C     *IN33         DOWNE     '1'
     C                   MOVE      'S'           BUST
     C     BUBLST        IFEQ      'T'
     C                   MOVE      'E'           BUBLST
     C                   END
     C                   UPDATE    KOSBUR
     C     KOSB9K        READE     KOSBU9                                 33
     C                   END
      *
      * Slett fakturalinjer
      *
     C     HEADKEY       SETLL     FAK4
     C     HEADKEY       READE     FAK4                                   33
     C     *IN33         DOWEQ     '0'
     C                   DELETE    FAKTR
     C     HEADKEY       READE     FAK4                                   33
     C                   END
      *
      * Slettemerk frisok med å sette *D i pos 34/35 (f.eks IFB benyttes ofte for UNIK-test)
      *
     C     HEADKEY       SETLL     FRISOK
     C     HEADKEY       READE     FRISOK                                 33
     C     *IN33         DOWEQ     '0'
     C                   move      '*D'          FSSOK
     C                   UPDATE    FRISOKR
     C     HEADKEY       READE     FRISOK                                 33
     C                   END
      *
      * Fjern evt post i samlefakt-kø.
      *
     C     HEADKEY       CHAIN     SAML1                              33
     C  N33              DELETE    SAMLR
      *
      * Slettemerk kolli ID ( 0 i fbnr = på alle fraktbrev...)
     C                   MOVE      *ZEROS        XXFBNR            3 0
     C                   Call      'SYFI10Z'
     C                   Parm                    HEAVD
     C                   Parm                    HEOPD
     C                   Parm                    XXFBNR
      * FJERN EVT. OPPADGÅENDE XREF (TIL OVERORDNET)
      * (DET FINNES INGEN UNDER - DET ER TESTET LENGER OPPE)
      *
     C     HEADKEY       CHAIN     HEADF                              33
     C     *IN33         IFEQ      '0'                                          B1
     C                   MOVE      HEAVD         YYAVD             4 0           *DETTE
     C                   MOVE      HEOPD         YYOPD             7 0
     C                   MOVE      TRAVD0        YYAVD0            4 0           *OVER SEG
     C                   MOVE      TROPD0        YYOPD0            7 0
     C                   MOVE      *ZEROS        TRAVD0
     C                   MOVE      *ZEROS        TROPD0
     C                   UPDATE    HEADFR
      *
      * ER EN SLAVE AV ET ANNET OPPDRAG - SLETTE XREFEN I OVERORDNET.
      *
     C     YYOPD0        IFNE      *ZEROS                                        B2
     C     KKKHE0        CHAIN     HEADF                              33
     C     *IN33         IFEQ      '0'                                            B3
     C                   Move      'J'           XDupFb            1
     C     TROPD1        IFEQ      YYOPD
     C     TRAVD1        ANDEQ     YYAVD                                           B4
     C                   MOVE      *ZEROS        TRAVD1
     C                   MOVE      *ZEROS        TROPD1
     C                   MOVE      ' '           TRSTAF
     C                   Move      'N'           XDupFb
     C                   END                                                       E4
     C     TROPD2        IFEQ      YYOPD
     C     TRAVD2        ANDEQ     YYAVD                                           B4
     C                   MOVE      *ZEROS        TRAVD2
     C                   MOVE      *ZEROS        TROPD2
     C                   MOVE      ' '           TRSTAE
     C                   Move      'N'           XDupFb
     C                   END                                                       E4
     C                   UPDATE    HEADFR
      * Om det IKKE var DUP fra oppdrag må det være fra fbrev.
     C     XDupFb        IFEQ      'J'
     C                   MOVE      YYAVD         YYAVDA            4
     C                   MOVE      YYOPD         YYOPDA            7
     C     DOKUF0K       SETLL     DOKUF
     C     DOKUF0K       READE     DOKUF                                  33
     C     *in33         DOWEQ     '0'
     C     YYAVDA        IFEQ      DFBRAV
     C     YYOPDA        ANDEQ     DFBROP
     C                   MOVE      *BLANKS       DFBREF
     C                   UPDATE    DOKUFR
     C                   goto      UtFrDup
     C                   ELSE
     C                   UPDATE    DOKUFR
     C                   END
     C     DOKUF0K       READE     DOKUF                                  33
     C                   END
     C     UtFrDup       Tag
     C                   END
     C                   END                                                      E3
     C                   END                                                     E2
     C                   MOVE      YYAVD         HEAVD                           *DETTE
     C                   MOVE      YYOPD         HEOPD
     C* SLETT      EVT FRAKTBREV (GJENBRUK AV SENDINGSNR I INNLANDSYSTEM..)
     C                   MOVE      HEAVD         YYAVD0
     C                   MOVE      HEOPD         YYOPD0
     C     DOKUF0K       CHAIN     DOKUF                              33
     C     *IN33         ifeq      *OFF
     C                   delete    dokufr
     C                   END                                                    E1
     C                   END                                                    E1
     C                   ENDSR
