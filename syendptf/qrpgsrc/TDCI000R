      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/TDCI000R MODULE(*LIBL/TDCI000R *LIBL/INCGI)
CRTPGM*        ACTGRP(*NEW) AUT(*USE)
      *
      *********************************************************************
      /copy SYSPEDS/qrpgsrcpy,hspecs
      /copy SYSPEDS/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FEDIM4     IF   E           K DISK    USROPN
     FBRPARF    IF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy SYSPEDS/qrpgsrcpy,prototypeb
      /copy SYSPEDS/qrpgsrcpy,usec
      /copy SYSPEDS/qrpgsrcpy,variables3
     ‚*  Prototype for 'JSONFIX'
     d jsonfix         pr                  extpgm('JSONFIX')
     d jsonstring_                         like(jsonstring)
     ‚*  Prototype for 'INCGI'
     d incgi           pr                  extproc('INCGI')
     d bistdl_                             like(bistdl)
     ‚*  Prototype for variabel program
     D Varpgm          Pr                  ExtPgm(BILIBL)
     ‚*  Prototype for 'SYGE20'
     d syge20          pr                  extpgm('SYGE20')
     d ufunk_                              like(ufunk)
     d udat1_                              like(udat1)
     d udat2_                              like(udat2)
     d udgr_                               like(udgr)
     d WhereC          s            512a
     d FirstW          s              1A
     d cQuote          C                   const('''')
     d SqlStm          s            512a
     d myrows          s             10i 0
     D WSUSER          S             10
     D AVD             S              4
     D AVDN            S              4  0
     D SIGN            S              3
     D OPD             S              7
     D OPDN            S              7S 0
     D GN              S             35
     D MRN             S             18
     D DATUM           S              8
     D DATUMN          S              8S 0
     D DATUMT          S              8
     D DATUMTN         S              8S 0
     D DATUMFR         S              8
     D DATUMFRN        S              8S 0
     D STATUS          S              1
     D FORENKL         S              1
     D ANSNAVN         S             35
     D JSONstring      s          32000
     D Error           S            150
     D AntLest         S              9  0
     D Jn043           S              1
     d X               S             10  0
     d hopp            S              1
     d udat1           s              8  0
     d udat2           s              8  0
     d udgr            s              5  0
     d ufunk           s              3
     d wpabrid         s                   like(pabrid)
     d wpakunr         s                   like(pakunr)
     d wpasort         s                   like(pasort)
     d MyPoDS          ds                  Qualified  dim(100)
     d  TIAVD                         4S 0
     d  TITDN                         7S 0
     d  TI0035                        1
     d  TISG                          3
     d  TIGN                         35
     d  TITRNR                       18
     d  TIDT                          8S 0
     d  TIST                          1
     d  TIDTF                         8S 0
     d  TIENKL                        1
     d  TINA                         30
     d  TIVERS                        1
      *get input-string
      /copy syspeds/qrpgsrcpy,prolog3

      /free
         exsr ParseSr;
         exsr init;

       gethtmlifs(
       '/syspeddev/html/JSON.html':
       '/CGITAG/');
       wrtsection('top');

       //...........................................................................................
       //skriv JSON-Object start (alltid '{' + x ant param. m/komma mellom (IKKE komma etter siste))
       //...........................................................................................

        JSONstring='{'
        +'¬user¬ : ¬'+%trim(WSUSER)+'¬, '
        +'¬avd¬ : ¬'+%trim(%editc(AVDN:'Z'))+'¬, '
        +'¬sign¬ : ¬'+%trim(SIGN)+'¬, '
        +'¬opd¬ : ¬'+%trim(OPD)+'¬, '
        +'¬godsNr¬ : ¬'+%trim(GN)+'¬, '
        +'¬mrn¬ : ¬'+%trim(MRN)+'¬, '
        +'¬datum¬ : ¬'+%trim(DATUM)+'¬, '
        +'¬datumt¬ : ¬'+%trim(DATUMT)+'¬, '
        +'¬status¬ : ¬'+%trim(STATUS)+'¬, '
        +'¬forenklad¬ : ¬'+%trim(FORENKL)+'¬, '
        +'¬ansNavn¬ : ¬'+%trim(ANSNAVN)+'¬, '
        +'¬datumFr¬ : ¬'+%trim(DATUMFR)+'¬, '
        +'¬errMsg¬ : ¬'+%trim(Error)+'¬, ';

       //JSONfix escaper " i tekst,  for deretter å bytte ¬->"

       jsonfix ( jsonstring );

       updhtmlvar2('JSONstring'
                  :%addr(jsonstring)
                  :%len(jsonstring));
       wrtsection('JSONlin');

       //...........................................................................................
       // Skriv JSON-LISTE Start (en liste er EN parameter(fra [ til   )
       //..........................................................................................
       jsonstring ='"orderList" : [';

       updhtmlvar2('JSONstring'
                  :%addr(jsonstring)
                  :%len(jsonstring));
       wrtsection('JSONlin');

       if error = *blanks;
         exsr SelSr;
         if myrows > 0;
           exsr LesSr;
         endif;
       endif;
       // ..........................................................................................
       // Skriv JSON-Liste/Array  Avslutning
       // ..........................................................................................
       jsonstring =']';
       updhtmlvar2('JSONstring'
                  :%addr(jsonstring)
                  :%len(jsonstring));
       wrtsection('JSONlin');
       // ..........................................................................................
       // Skriv JSON-string Avsluttning
       // ..........................................................................................
       jsonstring ='}';
       updhtmlvar2('JSONstring'
                  :%addr(jsonstring)
                  :%len(jsonstring));
       wrtsection('JSONlin');
         exsr Exit;

       //-----------------------------------------------------------------
       // Select fra fil via SQL
       //-----------------------------------------------------------------
       begsr     SelSr;

       If    AVDN > 0;
         AVD = %editc(AVDN:'X');
         Exsr  AddAnd;
         Eval  WhereC = ' TIAVD  = ' + AVD;
       EndIf;

       If    SIGN  <> *blanks;
         Exsr  AddAnd;
         Eval  WhereC =  %trim(WhereC)  +
                       ' TISG = ' + cQuote + SIGN + cQuote +' ';
       EndIf;

       If    OPDN > 0;
         OPD = %editc(OPDN:'X');
         Exsr  AddAnd;
         Eval  WhereC = ' TITDN  = ' + OPD;
       EndIf;

       If    GN  <> *blanks;
         Exsr  AddAnd;
         Eval  WhereC =  %trim(WhereC)  +
         ' TIGN LIKE ' + cQuote + %trim(GN) + '%' + cQuote;
        EndIf;

       If    MRN  <> *blanks;
         Exsr  AddAnd;
         Eval  WhereC =  %trim(WhereC)  +
         ' TITRNR LIKE ' + cQuote + %trim(MRN) + '%' + cQuote;
        EndIf;

       If    DATUMN > 0;
         DATUM = %editc(DATUMN:'X');
         Exsr  AddAnd;
         Eval  WhereC =  %trim(WhereC)  +
         ' TIDT  >= ' + DATUM;
       EndIf;

       If    DATUMTN > 0;
         DATUMT = %editc(DATUMTN:'X');
         Exsr  AddAnd;
         Eval  WhereC =  %trim(WhereC)  +
         ' TIDT  <= ' + DATUMT;
       EndIf;

       If    STATUS  <> *blanks and STATUS <> 'b';
         Exsr  AddAnd;
         Eval  WhereC =  %trim(WhereC)  +
         ' TIST = ' + cQuote + STATUS + cQuote;
        EndIf;

       If    ANSNAVN  <> *blanks;
         Exsr  AddAnd;
         Eval  WhereC =  %trim(WhereC)  +
         ' TINA LIKE ' + cQuote + '%' + %trim(ANSNAVN) + '%' + cQuote;
        EndIf;

       If    DATUMFRN > 0;
         DATUMFR = %editc(DATUMFRN:'X');
         Exsr  AddAnd;
         Eval  WhereC =  %trim(WhereC)  +
         ' TIDTF >= ' + DATUMFR;
       EndIf;

         Eval      WhereC =  ' Where ' + %trim(WhereC);

         Eval       SqlStm = 'Select TIAVD, TITDN, TI0035, ' +
                             'TISG, TIGN, TITRNR, TIDT, TIST, ' +
                             'TIDTF, TIENKL, TINA, TIVERS ' +
                             'from DKNH ' +
                              %trim(WhereC)  +
                             ' Order by '       +
                             'TIDT desc ' +
                             'FOR READ ONLY ';

         Exec SQL PREPARE DynSel from :SqlStm;
         EXEC SQL DECLARE  A33  cursor for DynSel;
         Exec SQL OPEN A33;
         Myrows = %elem(MyPoDS);
         exec sql FETCH A33   for :MyRows rows
                  INTO :MyPoDs;
       endsr;
       //-----------------------------------------------------------------
       // Set inn "AND" mellom where settninger
       //-----------------------------------------------------------------
       begsr     AddAnd;
       If        FirstW =   'N';
         Eval      WhereC =  %trim(WhereC) + ' and ';
       EndIf;
         Eval      FirstW =   'N';
       endsr;
       //-----------------------------------------------------------------
       // hent input
       //-----------------------------------------------------------------
       begsr     ParseSr;
         wsuser  = zhbgetvar('USER');
         avd     = zhbgetvar('AVD');
         avdn    = c2n2(avd);
         sign    = zhbgetvar('SIGN');
         opd     = zhbgetvar('OPD');
         opdn    = c2n2(opd);
         gn      = zhbgetvar('GODSNR');
         mrn     = zhbgetvar('MRN');
         datum   = zhbgetvar('DATUM');
         datumn  = c2n2(datum);
         datumt  = zhbgetvar('DATUMT');
         datumtn = c2n2(datumt);
         status  = zhbgetvar('STATUS');
         forenkl = zhbgetvar('FORENKLAD');
         ansnavn = zhbgetvar('ANSNAVN');
         datumfr = zhbgetvar('DATUMFR');
         datumfrn= c2n2(datumfr);
        endsr;
        // __________________________________________________
        // Læs array                                    LESSR
        // """"""""""""""""""""""""""""""""""""""""""""""""""
           begsr LesSr;
       for x = 1 to myrows;

       if mypods(x).tisg = *blanks;
         leave;
       endif;

       exsr sr043;
       hopp='J';
       if forenkl <> *blanks and forenkl <> 'b';
       if jn043 <> FORENKL;
           hopp='N';
         endif;
       endif;

         if hopp ='J';

        // Lægg ut Json stræng
           AntLest = AntLest + 1;

        // Skriv en JSON-Array-linje pr menypunkt - komma før hvert nytt element

       if AntLest=1;
          JSONstring=*blanks;
          else;
          JSONstring=', ';
          endif;
       JSONstring=%trim(JSONstring)+'{'
          +'¬avd¬ : ¬'+%trim(%editc(mypods(x).TIAVD:'Z'))+'¬, '
          +'¬opd¬ : ¬'+%trim(%editc(mypods(x).TITDN:'Z'))+'¬, '
          +'¬tst¬ : ¬'+%trim(mypods(x).TI0035)+'¬, '
          +'¬sign¬ : ¬'+%trim(mypods(x).TISG)+'¬, '
          +'¬godsNr¬ : ¬'+%trim(mypods(x).TIGN)+'¬, '
          +'¬mrnNr¬ : ¬'+%trim(mypods(x).TITRNR)+'¬, '
          +'¬datum¬ : ¬'+%trim(%editc(mypods(x).TIDT:'Z'))+'¬, '
          +'¬status¬ : ¬'+%trim(mypods(x).TIST)+'¬, '
          +'¬datumFr¬ : ¬'+%trim(%editc(mypods(x).TIDTF:'Z'))+'¬, '
          +'¬forenklad¬ : ¬'+%trim(mypods(x).TIENKL)+'¬, '
          +'¬jn043¬ : ¬'+%trim(JN043)+'¬, '
          +'¬ansNavn¬ : ¬'+%trim(mypods(x).TINA)+'¬, '
          +'¬tivers¬ : ¬'+%trim(mypods(x).TIVERS)+'¬}';
         jsonfix (JSONstring);
       updhtmlvar2('JSONstring'
                  :%addr(jsonstring)
                  :%len(jsonstring));
         wrtsection ('JSONlin');

          endif;
        endfor;

       endsr;
        // __________________________________________________
        // Finnes IE43 ?                                SR043
        // """"""""""""""""""""""""""""""""""""""""""""""""""
           begsr Sr043;
           jn043='N';
           setll ('R': mypods(x).TIAVD: mypods(x).TITDN) EDIM4;
           reade ('R': mypods(x).TIAVD: mypods(x).TITDN) EDIM4;
           dow not %eof;
             if M1N07='043';
               jn043='J';
             leave;
             endif;
           reade ('R': mypods(x).TIAVD: mypods(x).TITDN) EDIM4;
           enddo;
           endsr;
       //********************************************************
       begsr init;
       //********************************************************
         open bridf;
         // Test innlogging
         chain wsuser bridf;

         if %found;
           if bilibl = *blanks;
             incgi ( bistdl );
           else;

             Monitor;
                   Varpgm();
                 On-Error;
                   //
                 EndMon;

           endif;
         else;

           error = 'Invalid userID';
         endif;
         open edim4;
         open brparf;

         // Er dato= blank eller 0, sett  da dato 7 dager tilbake
         if opdn=*zeros and gn=*blanks and mrn=*blanks and datumn=*zeros;

           wpabrid=wsuser;
           wpakunr=0;
           wpasort=0;
           chain (WPABRID: WPAKUNR: 'DFTDG': WPASORT) BRPARF;
           if not %found or pavrdn = 0;
              pavrdn=7;
           endif;

           ufunk = 'SUB';
           udat1 = datumn;
           udgr = pavrdn;
           syge20 ( ufunk : udat1 : udat2 : udgr );
           datumn=udat2;
         endif;


       endsr;
       //=====================================================================
       // Close output html and quit
       //=====================================================================
       begsr exit;
         // Lukk fil
         close bridf;
         close edim4;
         close brparf;

         wrtsection('*fini');
         *inlr = *on;
         return;
       endsr;
      /end-free
