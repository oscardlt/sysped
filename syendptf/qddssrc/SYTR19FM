     A*%%TS  SD  20170321  131546  SVEIN       REL-V7R1M0  5770-WDS
*****A**************************************************************
     A* RETTINGER I DETTE PROGRAM:
PTFnrA*Sek Sig Vers Beskrivelse...........................
"""""A*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10261A* 01 YBC PTF10 VEKT/KUBIKK I TURER ER ENDRET TIL 9.0, 7.3
     A*              INGEN MERKING
*****A**************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3                  -
     A                                             27 132 *DS4)
     A                                      PRINT
     A                                      HELP
     A                                      ALTHELP(CA01)
      * "NOERASE" MÅ HA FOR Å IKKE TØMME BILDE SOM MAN KOMME FRA
     A          R NOERASE
     A                                      ASSUME
     A                                  1  2' '
      * MAN KUN DEFINNERE VINDU PÅ REKORDNIVÅ
     A          R WINDOWSZ
     A                                      WINDOW(*DFT 17 75 *NOMSGLIN)
     A                                      OVERLAY
     A          R SUBFIL                    SFL
     A            UNRETU    R        H      REFFLD(UNITR/UNRETU *LIBL/UNIT)
     A            UNSAVD    R        H      REFFLD(UNITR/UNSAVD *LIBL/UNIT)
     A            UNSPRO    R        H      REFFLD(UNITR/UNSPRO *LIBL/UNIT)
     A            UNRUND    R        H      REFFLD(UNITR/UNRUND *LIBL/UNIT)
     A            WSNR           1A  B  5  2VALUES(' ' '1')
     A            UNBILN    R        O  5  4REFFLD(UNITR/UNBILN *LIBL/UNIT)
     A            UNTILH    R        O  5 15REFFLD(UNITR/UNTILH *LIBL/UNIT)
     A            UNAVD     R        O  5 26REFFLD(UNITR/UNAVD *LIBL/UNIT)
     A            UNLAND    R        O  5 31REFFLD(UNITR/UNLAND *LIBL/UNIT)
     A            UNVEKT    R        O  5 34REFFLD(UNITR/UNVEKT *LIBL/UNIT)
     A                                      EDTCDE(Z)
     A            UNTARA    R        O  5 44REFFLD(UNITR/UNTARA *LIBL/UNIT)
     A                                      EDTCDE(Z)
     A            UNM3      R        O  5 50REFFLD(UNITR/UNM3 *LIBL/UNIT)
     A                                      EDTCDE(4)
     A            UNLM      R        O  5 59REFFLD(UNITR/UNLM *LIBL/UNIT)
     A                                      EDTCDE(4)
     A            UNTRME    R        H      REFFLD(UNITR/UNTRME *LIBL/UNIT)
SV5  A*           UNDAY     R        H      REFFLD(UNITR/UNDAY *LIBL/UNIT)
SV5  A*           UNMONT    R        H      REFFLD(UNITR/UNMONT *LIBL/UNIT)
SV5  A*           UNYEAR    R        H      REFFLD(UNITR/UNYEAR *LIBL/UNIT)
NV5  A            UNDATE    R        H      REFFLD(UNITR/UNDATE *LIBL/UNIT)
     A            UNTRMA    R        H      REFFLD(UNITR/UNTRMA *LIBL/UNIT)
     A            UNUNTY         4A  O  5 67
     A            WSNAVN        30A  O  6  2COLOR(WHT)
     A                                      DSPATR(UL)
     A            TUSTEF    R        O  6 33REFFLD(TURERR/TUSTEF *LIBL/TURER)
     A  88                                  DSPATR(ND)
     A                                      COLOR(WHT)
     A            WSDATF         6S 0O  6 39
     A  88                                  DSPATR(ND)
     A                                      COLOR(WHT)
     A            TUTM      R        H      REFFLD(TURERR/TUTM *LIBL/TURER)
     A                                  6 46'->'
     A  88                                  DSPATR(ND)
     A                                      COLOR(RED)
     A            TUSTET    R        O  6 49REFFLD(TURERR/TUSTET *LIBL/TURER)
     A  88                                  DSPATR(ND)
     A                                      COLOR(WHT)
     A            WSDATT         6S 0O  6 55
     A  88                                  DSPATR(ND)
     A                                      COLOR(WHT)
     A            TUTMT     R        H      REFFLD(TURERR/TUTMT *LIBL/TURER)
     A            TUPRO     R        H      REFFLD(TURERR/TUPRO *LIBL/TURER)
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A*%%TS  SD  20170321  131546  SVEIN       REL-V7R1M0  5770-WDS
     A  *DS3                                SFLSIZ(0007)
     A  *DS4                                SFLSIZ(0007)
     A  *DS3                                SFLPAG(0006)
     A  *DS4                                SFLPAG(0006)
     A  *DS3                                WINDOW(WINDOWSZ)
     A  *DS4                                WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A  91N93N94                            ROLLUP(25)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A                                      CA03(03 'Forrige')
     A                                      CA04(04 'Søking')
     A                                      CA05(05 'Frisk')
     A                                      CA12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            ZRECNO         4S 0O  2 50SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYSPED/SYTR19'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(045 DIV3793 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                  2  2MSGCON(025 DIV1034 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3  2MSGCON(069 YBC0333 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A            W2NR           1A  I  4  2VALUES(' ' '1')
     A                                      DSPATR(UL)
     A            W2BILN    R        I  4  4REFFLD(UNITR/UNBILN *LIBL/UNIT)
     A                                  4 20MSGCON(018 YBC0334 *LIBL/SYMSGF)
     A            W2TRAN         8S 0I  4 39DSPATR(UL)
     A            W2UNTY         4A  I  4 67DSPATR(UL)
     A          R BUNN
     A                                      WINDOW(WINDOWSZ)
     A                                      SLNO(17)
     A                                      CLRL(01)
     A                                      OVERLAY
     A N98                              1  2MSGCON(060 YBC0335 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  98                              1  2MSGCON(060 DIV0012 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A          R NYWRK
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A                                      CA03(03 'Forrige')
     A                                      CA04(04 'Søking')
     A                                      CA05(05 'Frisk')
     A                                      CA12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/SYTR19'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(045 DIV3793 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                  2  2MSGCON(25 DIV1034 SYMSGF)
     A                                      COLOR(BLU)
     A                                  3  2MSGCON(069 YBC0333 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A            W2NR           1A  I  4  2VALUES(' ' '1')
     A                                      DSPATR(UL)
     A            W2BILN    R        I  4  4REFFLD(UNITR/UNBILN *LIBL/UNIT)
     A                                  4 20MSGCON(018 YBC0334 *LIBL/SYMSGF)
     A            W2TRAN         8S 0I  4 39DSPATR(UL)
     A            W2UNTY         4A  I  4 60DSPATR(UL)
     A N98                             17  2MSGCON(060 YBC0335 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  98                             17  2MSGCON(060 DIV0012 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
