     A*%%TS  SD  20140519  183943  TROND       REL-V7R1M0  5770-WDS
*****A**************************************************************
     A* RETTINGER I DETTE PROGRAM:
PTFnrA*Sek Sig Vers  Beskrivelse...........................
"""""A*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10217A* 01 YBC PTF10 DEFAULT PRINTER FOR PLUKKLISTE OG MANIFEST
*****A**************************************************************
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3                  -
     A                                             27 132 *DS4)
     A                                      PRINT
      *
     A          R GSS001DA                  SFL
     A            WSNR           1A  I  5  2VALUES(' ' '2' '4')
     A            BIUSID        10A  O  5  4COLOR(WHT)
     A            BINM          30A  H
     A            WXNM          16A  O  5 15
     A            BILOC          5A  O  5 32
     A            BIAVD          4Y 0O  5 38EDTCDE(4)
     A            BICGR          2A  O  5 43
     A            BICNO          8Y 0O  5 46EDTCDE(4)
     A            BIFC           3A  O  5 55
     A            BIPRT         10A  O  5 59
     A            BIPRTM        10A  O  5 70
N01  A            BIPRTP        10A  H
N01  A            BIPRTI        10A  H
     A*
     A          R GSS001DB                  SFLCTL(GSS001DA)
     A*%%TS  SD  20140519  183943  TROND       REL-V7R1M0  5770-WDS
     A  *DS3                                SFLSIZ(0017)
     A  *DS4                                SFLSIZ(0017)
     A  *DS3                                SFLPAG(0016)
     A  *DS4                                SFLPAG(0016)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                  1  2'SHIPSYS/GSS001D'
     A                                      COLOR(BLU)
     A                                  1 21'Work with user-id'
     A                                      DSPATR(HI)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                  1 70TIME
     A            ZRECNO         4S 0O  2 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  3  2'Choose option, press ENTER'
     A                                      COLOR(BLU)
     A                                  3 32'2=Change  4=Delete'
     A                                      COLOR(BLU)
     A                                  4  1'Op User-id    Users name       Loc-
     A                                      at Dep. CG Cust.no. Com Printer-P  -
     A                                      Printer-M '
     A                                      DSPATR(HI)
      *
     A          R GSS001DC
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CA03(03 'END')
     A                                      CF05(05 'Refresh')
     A                                      CF06(06 'New entry')
     A                                      CF08(08 'Search')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  3  2'F3=End F5=Refresh F6=New entry F8=-
     A                                      Search'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  4  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R GSS001DD
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF04(04 'Search')
     A                                      CF12(12 'Previous')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'Bruker-id  Brukers navn           -
N01  A                                              KG Kundenr. Fir PRT pck.l  -
N01  A                                      PRT label '
S01  A*                                             KG Kundenr. Fir Printer-P  -
S01  A*                                     Printer-M '
     A                                      COLOR(WHT)
     A            WSUSID        10A  B  2  2DSPATR(UL)
     A  40                                  DSPATR(HI)
     A  40                                  DSPATR(PR)
     A  90                                  DSPATR(RI)
     A                                      CHECK(LC)
     A            WSNM          30A  B  2 13DSPATR(UL)
     A  89                                  DSPATR(RI)
     A            WSCG           2A  B  2 44DSPATR(UL)
     A  88                                  DSPATR(RI)
     A            WSCNO          8Y 0B  2 47DSPATR(UL)
     A  87                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A            WSFC           3A  B  2 56DSPATR(UL)
     A  86                                  DSPATR(RI)
     A            WSPRT         10A  B  2 60DSPATR(UL)
     A  85                                  DSPATR(RI)
     A            WSPRTM        10A  B  2 71DSPATR(UL)
     A  84                                  DSPATR(RI)
     A                                  3  2'F4=Search F12=Previous'
     A                                      COLOR(BLU)
S01  A*                                 3 34'Avdeling:'
S01  A*           WSAVD          4Y 0B  3 44DSPATR(UL)
N01  A                                  3 25'Dept:'
N01  A            WSAVD          4Y 0B  3 31DSPATR(UL)
     A  83                                  DSPATR(RI)
     A                                      EDTCDE(4)
S01  A*                                 3 54'Location:'
S01  A*           WSLOC          5A  B  3 64DSPATR(UL)
N01  A                                  3 36'Loc:'
N01  A            WSLOC          5A  B  3 41DSPATR(UL)
     A  82                                  DSPATR(RI)
N01  A                                  3 47'Pl.l:'
N01  A            WSPRTP        10A  B  3 53DSPATR(UL)
N01  A                                  3 64'Pinv:'
N01  A            WSPRTI        10A  B  3 70DSPATR(UL)
     A  30        MSGTXT        78A  O  4  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R GSS001DE
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A                                  2  2'Søk fra bruker-id  --> '
     A            W2USID        10A  I  2 26DSPATR(UL)
     A                                      DSPATR(PC)
     A                                  3  2'F12=Forrige'
     A                                      COLOR(BLU)
      *
     A          R GSS001DF
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A                                  1  2'Bruker-id  Brukers navn           -
N01  A                                              KG Kundenr. Fir PRT pck.l  -
N01  A                                      PRT label '
S01  A*                                             KG Kundenr. Fir Printer-P  -
S01  A*                                     Printer-M '
     A                                      COLOR(WHT)
     A            BIUSID        10A  O  2  2DSPATR(UL)
     A                                      DSPATR(PC)
     A                                      DSPATR(HI)
     A            BINM          30A  O  2 13DSPATR(UL)
     A                                      DSPATR(PC)
     A                                      DSPATR(HI)
     A            BICGR          2A  O  2 44DSPATR(UL)
     A                                      DSPATR(HI)
     A            BICNO          8Y 0O  2 47DSPATR(UL)
     A                                      DSPATR(HI)
     A                                      EDTCDE(4)
     A            BIFC           3A  O  2 56DSPATR(UL)
     A                                      DSPATR(HI)
     A            BIPRT         10A  O  2 60DSPATR(UL)
     A                                      DSPATR(HI)
     A            BIPRTM        10A  O  2 71DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  3  2'F12=Forrige'
     A                                      COLOR(BLU)
S01  A*                                 3 34'Avdeling:'
S01  A*           BIAVD          4Y 0O  3 44DSPATR(UL)
N01  A                                  3 25'Dept:'
N01  A            BIAVD          4Y 0O  3 31DSPATR(UL)
     A                                      DSPATR(HI)
     A                                      EDTCDE(4)
S01  A*                                 3 54'Location:'
S01  A*           BILOC          5A  O  3 64DSPATR(UL)
N01  A                                  3 36'Loc:'
N01  A            BILOC          5A  O  3 41DSPATR(UL)
N01  A                                  3 47'Pl.l:'
N01  A            BIPRTP        10A  O  3 53DSPATR(UL)
N01  A                                  3 64'Pinv:'
N01  A            BIPRTI        10A  O  3 70DSPATR(UL)
      *
     A          R GSS001DG
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CA03(03 'AVSLUTT')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SHIPSYS/GSS001D'
     A                                      COLOR(BLU)
     A                                  1 21'Arbeide med bruker-id'
     A                                      DSPATR(HI)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                  1 70TIME
     A                                  3  2'Velg opsjon, trykk ENTER:'
     A                                      COLOR(BLU)
     A                                  3 32'2=Endre   4=Slett'
     A                                      COLOR(BLU)
     A                                  4  1'Op Bruker-id  Brukers navn     Loc-
N01  A                                      at Avd. KG Kundenr. Fir PRT pck.l  -
N01  A                                      PRT label '
S01  A*                                     at Avd. KG Kundenr. Fir Printer-P  -
S01  A*                                     Printer-M '
     A                                      DSPATR(HI)
     A                                  6  4'Ingen bruker-id finnes'
     A                                      COLOR(RED)
     A                                 23  2'F3=Avslutt   F5=Forny   F6=Lag ny -
     A                                        F8=Søk'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A            MSGNR          7A  P
