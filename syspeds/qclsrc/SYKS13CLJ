    /* UTSKRIFT AVSETNINGER VED PERIODEAVSLUTNING */
             PGM        PARM(&AVD &PERF &PERT &SORT &DET &PRT &PT +
                          &FIL &WS)
             DCL        VAR(&PRT) TYPE(*CHAR) LEN(10) /* Printernavn */
             DCL        VAR(&PT) TYPE(*CHAR) LEN(10) /* Papirtype */
             DCL        VAR(&DET) TYPE(*CHAR) LEN(1) /* DETALJERT */
             DCL        VAR(&SORT) TYPE(*CHAR) LEN(1) /* SORTERING*/
             DCL        VAR(&AVD) TYPE(*CHAR) LEN(4)
             DCL        VAR(&PERF) TYPE(*CHAR) LEN(4)
             DCL        VAR(&PERT) TYPE(*CHAR) LEN(4)
             DCL        VAR(&JOBNR) TYPE(*CHAR) LEN(6)
             DCL        VAR(&MBR) TYPE(*CHAR) LEN(7) /* MEMBERNAVN +
                          til register WRKDISP. */
             DCL        VAR(&FIL) TYPE(*CHAR) LEN(1)
             DCL        VAR(&WS) TYPE(*CHAR) LEN(10) /* WSID FOR MELD*/
             OVRMSGF    MSGF(QUSERMSG) TOMSGF(SYMSGF)
    /* */
    /* FIX PRINTERFILE          */
    /* */
             OVRPRTF    FILE(SYKS13PF) PAGESIZE(48 132) +
                           OVRFLW(46) OUTQ(&PRT) FORMTYPE(&PT)
    /* HVIS KUN TIL DATAFIL, HOLD UTKØEN                    */
             IF         COND(&FIL = 'F') THEN(OVRPRTF FILE(SYKS13PF) +
                          OUTQ(*DEV) HOLD(*YES))
    /* */
 RTVUNIK:    RTVJOBA    NBR(&JOBNR)
             CHGVAR     VAR(&MBR) VALUE('W' *CAT &JOBNR)
             RMVM       FILE(WRKAVS1) MBR(&MBR)
             MONMSG     MSGID(CPF7310)
             RMVM       FILE(WRKAVS) MBR(&MBR)
             MONMSG     MSGID(CPF7310)
             ADDPFM     FILE(WRKAVS) MBR(&MBR)
             MONMSG     MSGID(CPF7306) EXEC(GOTO CMDLBL(RTVUNIK))
             OVRDBF     FILE(WRKAVS) TOFILE(WRKAVS) MBR(&MBR)
             IF         COND(&SORT = '2') THEN(DO)
             ADDLFM     FILE(WRKAVS1) MBR(&MBR) DTAMBRS((WRKAVS +
                          (&MBR)))
             OVRDBF     FILE(WRKAVS1) TOFILE(WRKAVS1) MBR(&MBR)
                        ENDDO
    /* */
             CALL       PGM(SYKS13) PARM(&AVD &PERF &PERT &SORT &DET)
    /* */
    /* */
             RMVM       FILE(WRKAVS1) MBR(&MBR)
             MONMSG     MSGID(CPF7310)
             IF         COND(&FIL = ' ') THEN(DO)
             RMVM       FILE(WRKAVS) MBR(&MBR)
             MONMSG     MSGID(CPF7310)
                        ENDDO
             ELSE       CMD(DO)
     /*      STRPCO                                     */
     /*      MONMSG     MSGID(IWS4010)                  */
     /*      STRPCCMD   PCCMD('C:\pcs\rtopcb wrkavso')  */
             SNDBRKMSG  MSG('  Komponent (member) ' *BCAT &MBR *BCAT +
                          ' i filen WRKAVS er bevart etter  "Utskr. +
                          avsetn.forslag v/per.slutt".   Kan hentes +
                          inn til f.eks regneark via +
                          PC-support.          OBS! Filen vil +
                          alltid være detaljert. Vil en kun ha +
                          bruddsummer over til PC, må ny fil skapes +
                          via f.eks. QUERY.          OBS! +
                          Komponenten bør slettes når en er +
                          ferdig... (WRKMBRPDM WRKAVS)') TOMSGQ(&WS)
             MONMSG     MSGID(CPF0000)
    /* HVIS KUN TIL DATAFIL, SLETT UTKØEN                    */
             IF         COND(&FIL = 'F') THEN(DO)
             DLTSPLF    FILE(SYKS13PF) SPLNBR(*LAST)
             MONMSG     MSGID(CPF0000)
                        ENDDO
    /* */
                        ENDDO
    /* */
 SLUTT:      ENDPGM
