/*SYSTEMA ENESTE ENDRING: UBETINGET HOPP OVER ALLE KOMMUNIKASJ.RUT.  */
/*SYSTEMA (SAMT STJERNE UT EN RJE-KOMMANDO FOR KOMPILERINGENS SKYLD) */

/* ***************************************************************** */
/* *   5797-AWE    IBM NORWAY 1988                                 * */
/* *   5797-AWE    COPYRIGHT IBM CORP 1988                         * */
/* *   REFER TO INSTRUCTIONS ON COPYRIGHT NOTICE NO. D120-2083     * */
/* ***************************************************************** */
/* *                                                               * */
/*      Endringer er foretatt ifølge endringsmelding nr.21 og 22     */
/* *                                                               * */
/*********************************************************************/
/*           ---- K C 0 8 8 B ----                                   */
/*                                                                   */
/*    MOTTAK AV BANKTRANSAKSJONER                                    */
/*                                                                   */
/*********************************************************************/

             PGM

 /*****************************************************************/
 /*                                                               */
 /*                                                               */
 /*   LID     =  RJE LINJE BESKRIVELSE               (&LID)       */
 /*   CTLD    =  RJE KONTROLLER BESKRIVELSE          (&CTLD)      */
 /*   DEVA    =  FØRSTE RJE-DEVICE (F.EKS: NETRJEA)  (&DEVA)      */
 /*   DEVB    =  ANDRE  RJE-DEVICE (F.EKS: NETRJEB)  (&DEVB)      */
 /*   DEVC    =  TREDJE RJE-DEVICE (F.EKS: NETRJEC)  (&DEVC)      */
 /*   DEVD    =  FJERDE RJE-DEVICE (F.EKS: NETRJED)  (&DEVD)      */
 /*   SES     =  RJE SESJON BESKRIVELSE              (&SES)       */
 /*   DEVPRFX =  TILSVARER CRTRJECFG .......         (&DEVPRFX)   */
 /*   LIB     =  RJE BIBLIOTEK                       (&LIB)       */
 /*                                                               */
 /*   WPCFIL  =  'J', INGEN KJØRING AV RJE                        */
 /*   JCLDEF  =  ENDRES TIL 'J' FOR KUNDER AV SPARNET             */
 /*              ALLE ANDRE BENYTTER 'N'                          */
 /*                                                               */
 /*****************************************************************/

             DCL        VAR(&LID) TYPE(*CHAR) LEN(10)
             DCL        VAR(&CTLD) TYPE(*CHAR) LEN(10)
             DCL        VAR(&DEVA) TYPE(*CHAR) LEN(10)
             DCL        VAR(&DEVB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&DEVC) TYPE(*CHAR) LEN(10)
             DCL        VAR(&DEVD) TYPE(*CHAR) LEN(10)
             DCL        VAR(&SES) TYPE(*CHAR) LEN(10)
             DCL        VAR(&DEVPRFX) TYPE(*CHAR) LEN(3)
             DCL        VAR(&LIB) TYPE(*CHAR) LEN(10)

 /*****************************************************************/
 /*                                                               */
 /*  Verdien av de 9 ovenstående variabler hentes fra LDA'en,     */
 /*  posisjon 600 - 682                                           */
 /*                                                               */
 /*****************************************************************/

             DCL        VAR(&WPCFIL) TYPE(*CHAR) LEN(1)
             DCL        VAR(&JCLDEF) TYPE(*CHAR) LEN(1)
             DCL        VAR(&WFEIL) TYPE(*CHAR) LEN(1) VALUE('0')
             DCL        VAR(&WOCR) TYPE(*CHAR) LEN(1) VALUE(' ')
             DCL        VAR(&BLANK) TYPE(*CHAR) LEN(10) +
                          VALUE('          ')
             DCL        VAR(&I) TYPE(*CHAR) LEN(7)
             DCL        VAR(&RDR) TYPE(*CHAR) LEN(10)
             DCL        VAR(&MSG) TYPE(*CHAR) LEN(100)
             DCL        VAR(&STSCODE) TYPE(*DEC) LEN(5 0)
             DCL        VAR(&DONE) TYPE(*DEC) LEN(2 0) VALUE(0)
             DCL        VAR(&VRYDOFF) TYPE(*DEC) LEN(5 0) VALUE(0)
             DCL        VAR(&RCYPND) TYPE(*DEC) LEN(5 0) VALUE(80)
             DCL        VAR(&RCYCNL) TYPE(*DEC) LEN(5 0) VALUE(90)
             DCL        VAR(&FAILED) TYPE(*DEC) LEN(5 0) VALUE(100)
             DCL        VAR(&VRYONPND) TYPE(*DEC) LEN(5 0) VALUE(20)
             DCL        VAR(&VRYDON) TYPE(*DEC) LEN(5 0) VALUE(30)
             DCL        VAR(&ACTIVE) TYPE(*DEC) LEN(5 0) VALUE(60)

             DCL        VAR(&WMEMBN) TYPE(*CHAR) LEN(8)
             DCL        VAR(&WIN01) TYPE(*LGL) LEN(1) VALUE('0')
             DCL        VAR(&WIN03) TYPE(*LGL) LEN(1) VALUE('0')
             DCL        VAR(&HOLD) TYPE(*CHAR) LEN(4)
             DCL        VAR(&SAVE) TYPE(*CHAR) LEN(4)
             DCL        VAR(&FIRMA) TYPE(*CHAR) LEN(2)
             DCL        VAR(&WFORM) TYPE(*CHAR) LEN(10)
             DCL        VAR(&WWSKID) TYPE(*CHAR) LEN(10)
             DCL        VAR(&WWSKKO) TYPE(*CHAR) LEN(10)
             DCL        VAR(&WWANTK) TYPE(*CHAR) LEN(2)
             DCL        VAR(&WWTEGN) TYPE(*CHAR) LEN(4)
             DCL        VAR(&WWLENG) TYPE(*CHAR) LEN(6)
             DCL        VAR(&WWSAVE) TYPE(*CHAR) LEN(1)
             DCL        VAR(&WWHOLD) TYPE(*CHAR) LEN(1)
             DCL        VAR(&WWSKTY) TYPE(*CHAR) LEN(1)
             DCL        VAR(&WWOVFL) TYPE(*CHAR) LEN(6)
             DCL        VAR(&WJOBNR) TYPE(*CHAR) LEN(6)
             DCL        VAR(&BRUKER) TYPE(*CHAR) LEN(10)
             DCL        VAR(&WNESTE) TYPE(*CHAR) LEN(1) VALUE('N')
             DCL        VAR(&WWBIBL) TYPE(*CHAR) LEN(10)
             DCL        VAR(&FONT) TYPE(*CHAR) LEN(4)
             DCL        VAR(&CPI) TYPE(*CHAR) LEN(4)
/*E24*/      DCL        VAR(&WORK) TYPE(*CHAR) LEN(400)

/*E24*/      CHGVAR     VAR(&WPCFIL)  VALUE(%SST(*LDA 517 1))
/*E24*/      CHGVAR     VAR(&LID)     VALUE(%SST(*LDA 600 10))
/*E24*/      CHGVAR     VAR(&CTLD)    VALUE(%SST(*LDA 610 10))
/*E24*/      CHGVAR     VAR(&DEVA)    VALUE(%SST(*LDA 620 10))
/*E24*/      CHGVAR     VAR(&DEVB)    VALUE(%SST(*LDA 630 10))
/*E24*/      CHGVAR     VAR(&DEVC)    VALUE(%SST(*LDA 640 10))
/*E24*/      CHGVAR     VAR(&DEVD)    VALUE(%SST(*LDA 650 10))
/*E24*/      CHGVAR     VAR(&SES)     VALUE(%SST(*LDA 660 10))
/*E24*/      CHGVAR     VAR(&DEVPRFX) VALUE(%SST(*LDA 670 3))
/*E24*/      CHGVAR     VAR(&LIB)     VALUE(%SST(*LDA 673 10))
/*E24*/      CHGVAR     VAR(&WORK)    VALUE(%SST(*LDA 1 400))
/*                                                                   */
/* Test om klargjøring til PC-fil skal kjøres                        */
/*                                                                   */
             IF         COND(&WPCFIL *EQ 'J') THEN(GOTO CMDLBL(START))
/*SYSTEMA*/  GOTO CMDLBL(START)
/*SYSTEMA SLETTET ALLE LINJER FRA HER TIL LABEL START */
                                                                  /**/
START:       CHGDTAARA  DTAARA(*LDA (290 10)) VALUE('KR088U01')
             CHGDTAARA  DTAARA(*LDA (213 10)) VALUE('KC088B')
             CALL       PGM(VR001) PARM(&WIN01 &WIN03)
             IF         COND(&WIN03 = '1') THEN(GOTO CMDLBL(START))
             IF         COND(&WIN01 = '1') THEN(GOTO CMDLBL(START))
                                                                 /**/
/*  HENT DATA FRA LDA                                              */
                                                                 /**/
             RTVJOBA    USER(&BRUKER) NBR(&WJOBNR)
             CHGVAR     VAR(%SST(*LDA 55 6)) VALUE(&WJOBNR)
             CHGVAR     VAR(&FIRMA) VALUE(%SST(*LDA 3 2))
             CHGVAR     VAR(&WFORM) VALUE(%SST(*LDA 107 10))
             CHGVAR     VAR(&WWSKID) VALUE(%SST(*LDA 117 10))
             CHGVAR     VAR(&WWSKKO) VALUE(%SST(*LDA 127 10))
             CHGVAR     VAR(&WWANTK) VALUE(%SST(*LDA 137 2))
             CHGVAR     VAR(&WWTEGN) VALUE(%SST(*LDA 139 4))
             CHGVAR     VAR(&WWLENG) VALUE(%SST(*LDA 143 6))
             CHGVAR     VAR(&WWSAVE) VALUE(%SST(*LDA 149 1))
             CHGVAR     VAR(&WWHOLD) VALUE(%SST(*LDA 150 1))
             CHGVAR     VAR(&WWSKTY) VALUE(%SST(*LDA 151 1))
             CHGVAR     VAR(&WWOVFL) VALUE(%SST(*LDA 152 6))
                                                                  /**/
             IF         COND(&WWHOLD *EQ 'J') THEN(CHGVAR VAR(&HOLD) +
                          VALUE('*YES'))
             IF         COND(&WWHOLD *EQ ' ') THEN(CHGVAR VAR(&HOLD) +
                          VALUE('*NO'))
             IF         COND(&WWSAVE *EQ 'J') THEN(CHGVAR VAR(&SAVE) +
                          VALUE('*YES'))
             IF         COND(&WWSAVE *EQ ' ') THEN(CHGVAR VAR(&SAVE) +
                          VALUE('*NO'))
/**/
/*           For å unngå mer enn 1 OVRPRTF uansett skrivertype,    */
/*           settes verdier i &FONT og &CPI i henhold til type.    */
/**/
             IF         COND(&WWSKTY *EQ '3') THEN(DO)
             CHGVAR     VAR(&FONT) VALUE(&WWTEGN)
             CHGVAR     VAR(&CPI) VALUE('*N')
             ENDDO
             ELSE       (DO)
             CHGVAR     VAR(&FONT) VALUE('*CPI')
             CHGVAR     VAR(&CPI) VALUE(&WWTEGN)
             ENDDO
/**/
                                                                 /**/
/*------------------------------------------------------------------*/
/*  Overstyr liste i henhold til inntastede verdier.                */
/*------------------------------------------------------------------*/
             OVRPRTF    FILE(KR084U01) PAGESIZE(&WWLENG) CPI(&CPI) +
                          OVRFLW(&WWOVFL) FONT(&FONT) +
                          OUTQ(*LIBL/&WWSKKO) FORMTYPE(&WFORM) +
                          COPIES(&WWANTK) HOLD(&HOLD) SAVE(&SAVE)
                                                                 /**/
             OVRPRTF    FILE(KR088U01) PAGESIZE(&WWLENG) CPI(&CPI) +
                          OVRFLW(&WWOVFL) FONT(&FONT) +
                          OUTQ(*LIBL/&WWSKKO) FORMTYPE(&WFORM) +
                          COPIES(&WWANTK) HOLD(&HOLD) SAVE(&SAVE)
                                                                 /**/
             CALL       PGM(KR084)
/*E24*/      CHGVAR     VAR(&WFEIL) VALUE(%SST(*LDA 500 1))
             IF         COND(&WFEIL *EQ '1') THEN(DO)
             SNDUSRMSG  MSG('Feil i mottaksfilen TELIF. Rutinen +
                          avbrytes. Se feilliste. DET MÅ MANUELT +
                          RYDDES OPP I FILEN TELIF !')
             GOTO       CMDLBL(EXIT)
             ENDDO
                                                                 /**/
 /* ***************************************************************/
 /*     Her er det lagt inn en rutine for å splitte retur fra bank*/
 /*     til riktig databibliotek ut fra feltet BETFOR i SYSAF.    */
 /*     PGM kr089 kontrolerer om TELIF inneholder poster som skal */
 /*     styres til TBNKF i annet databibliotek                    */
 /*     WWBYTT=B Poster skal bytte firmakode og styrers til annet */
 /*              bibliotek                                        */
 /*     WWBIBL inneholder TBNKF's databibliotek                   */
 /* ***************************************************************/
             RTVJOBA    CURLIB(&WWBIBL)
/*E24*/      CHGDTAARA  DTAARA(*LDA (861 10)) VALUE(&WWBIBL)
/*E24*/      CHGDTAARA  DTAARA(*LDA (871 1)) VALUE(&WNESTE)
             OPNDBF     FILE(SYSAL03) OPTION(*ALL)
             CALL       PGM(KR089)
/*E24*/      CHGVAR     VAR(&WFEIL) VALUE(%SST(*LDA 872 1))
             IF         COND(&WFEIL *EQ '1') THEN(DO)
             SNDUSRMSG  MSG('DET MÅ FYLLES INN UNIK FIRMAKODE +
                          I FILEN SYSAF FOR ALLE FIRMAKODENE.')
             GOTO       CMDLBL(EXIT1)
             ENDDO
                                                                 /**/
 NESTE:      CHGVAR     VAR(&WWBIBL) VALUE(%SST(*LDA 861 10)) /*E24*/
/*E24*/      CHGVAR     VAR(&WNESTE) VALUE(%SST(*LDA 871 1))
             IF         COND(&WNESTE *EQ ' ') THEN(GOTO +
                        CMDLBL(EXIT1))
             OVRDBF     FILE(TBNKF) TOFILE(&WWBIBL/TBNKF)
             OVRDBF     FILE(TEBBF) TOFILE(&WWBIBL/TEBBF)
             OVRDBF     FILE(SYSRF) TOFILE(&WWBIBL/SYSRF)
             OVRDBF     FILE(SYSBL01) TOFILE(&WWBIBL/SYSBL01)
             OVRDBF     FILE(FORSL02) TOFILE(&WWBIBL/FORSL02)
             OVRDBF     FILE(LRESL09) TOFILE(&WWBIBL/LRESL09)
             OVRDBF     FILE(KRESL14) TOFILE(&WWBIBL/KRESL14)
             CHKOBJ     OBJ(&WWBIBL/BBSAF) OBJTYPE(*FILE) MBR(BBSOCR)
             MONMSG     CPF9800 EXEC(DO)
             ADDPFM     FILE(&WWBIBL/BBSAF) MBR(BBSOCR)
             GOTO       CMDLBL(NOBBS)
             ENDDO
/*E24*/      CHGDTAARA  DTAARA(*LDA (400 1)) VALUE('J')
             CHGVAR     VAR(&WOCR) VALUE(%SST(*LDA 400 1)) /*E24*/
 NOBBS:      OVRDBF     FILE(BBSAF) TOFILE(&WWBIBL/BBSAF) +
                          MBR(BBSOCR)
             CALL       PGM(KR088)
/*E24*/      CHGVAR     VAR(&WOCR) VALUE(%SST(*LDA 400 1))
             IF        COND(&WOCR *EQ ' ')  THEN(DO)
             CHKOBJ     OBJ(&WWBIBL/BBSAF) OBJTYPE(*FILE) MBR(BBSOCR)
             MONMSG     CPF9800 EXEC(GOTO CMDLBL(NORMV))
             RMVM       FILE(&WWBIBL/BBSAF) MBR(BBSOCR)
             ENDDO
 NORMV:      DLTOVR     FILE(TBNKF)
             MONMSG     MSGID(CPF0000)
             CLOF       OPNID(TBNKF)
             MONMSG     MSGID(CPF0000)
             DLTOVR     FILE(BBSAF)
             MONMSG     MSGID(CPF0000)
             CLOF       OPNID(BBSAF)
             MONMSG     MSGID(CPF0000)
             DLTOVR     FILE(TEBBF)
             MONMSG     MSGID(CPF0000)
             CLOF       OPNID(TEBBF)
             MONMSG     MSGID(CPF0000)
             DLTOVR     FILE(SYSRF)
             MONMSG     MSGID(CPF0000)
             CLOF       OPNID(SYSRF)
             MONMSG     MSGID(CPF0000)
             DLTOVR     FILE(SYSBL01)
             MONMSG     MSGID(CPF0000)
             CLOF       OPNID(SYSBL01)
             MONMSG     MSGID(CPF0000)
             DLTOVR     FILE(FORSL02)
             MONMSG     MSGID(CPF0000)
             CLOF       OPNID(FORSL02)
             MONMSG     MSGID(CPF0000)
             DLTOVR     FILE(LRESL09)
             MONMSG     MSGID(CPF0000)
             CLOF       OPNID(LRESL09)
             MONMSG     MSGID(CPF0000)
             DLTOVR     FILE(KRESL14)
             MONMSG     MSGID(CPF0000)
             CLOF       OPNID(KRESL14)
             MONMSG     MSGID(CPF0000)
             GOTO       CMDLBL(NESTE)
EXIT1:
             CLOF       OPNID(SYSAL03)
             MONMSG     MSGID(CPF0000)
EXIT:
/*E24*/      CHGDTAARA  DTAARA(*LDA (401 623)) VALUE(' ')
/*E24*/      CHGDTAARA  DTAARA(*LDA (1 400)) VALUE(&WORK)
             ENDSBS     SBS(&SES) OPTION(*CNTRLD) /* stopp delsystem */
             MONMSG     MSGID(CPF0000)
             RMVLIBLE   LIB(&LIB)
             MONMSG     MSGID(CPF0000)
             RMVLIBLE   LIB(QRJE)
             MONMSG     MSGID(CPF0000)
 END:        ENDPGM
                                                                  /**/
