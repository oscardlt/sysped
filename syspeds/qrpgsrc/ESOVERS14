      *********************************************************************
      * UTSKRIFT AV BOOKING
      * PROGRAM BEHANDLER INPUT FRA ESOVERS5
      *
CRTPG+*  CRTPGM SYSPED/ESOVERS14 MODULE(*LIBL/ESOVERS14
CRTPGM*        *LIBL/CHECKUSR *LIBL/INCGI) ACTGRP(CGI) AUT(*USE)
      *
      *********************************************************************
      * MAIN PROGRAM FLOW
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
      * Boats for sale data base file (LF on BOATSALE by boat serial number)
     FBRIDF     UF   E           K DISK    USROPN
     FBRPARF    UF   E           K DISK    USROPN
     FHEADF     IF   E           K DISK    USROPN
     FBILLOF    IF   E           K DISK    USROPN
     FBLV1      IF   E           K DISK    USROPN
     FCDBOAT    IF   E           K DISK    USROPN
     FKODTA     IF   E           K DISK    USROPN
     FKODTLK    IF   E           K DISK    USROPN
     FESOSF2    UF A E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Prototype definitions and standard system API error structure
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      * Variables common to CGI programs
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
      * VARIABLES SPECIFIC TO THIS PROGRAM
      *--------------------------------------------------------------------
      * Variables received from the input string
     D SubmPressed     s              1
     D lng             s              2
     D BckGnd          s             10
     D UsrSpcName      s             10
     D WSUSER          S             10
     D WSAVD           S              4
     D BSAVD           S              4  0
     D WSOPD           S              7
     D BSOPD           S              7  0
     D WSTO            S             50
     D WSCONT          S             50
     D WSLDT           S             10
     D BSLDT10         S             10  0
     D WSLTM           S              5
     D BSLTM5          S              5  0
     D WSBCD           S              5
     D WSSNV           S             50
     D WSSAD1          S             50
     D WSSAD2          S             50
     D WSSAD3          S             50
     D WSSLK           S              2
     D WSSLKNV         S             30
     D WSNT            S              7
     D BSNT            S              7  0
     D WSDSC           S             40
     D WSVKT           S              9
     D BSVKT           S              9  0
     D WSKNV           S             50
     D WSKAD1          S             50
     D WSKAD2          S             50
     D WSKAD3          S             50
     D WSKLK           S              2
     D WSKLKNV         S             30
     D WSDREF          S             30
     D WSTARI          S             50
      *Array for fri-tekster
     D TX              S             70    DIM(15)
     D PGM             C                   CONST('SYSPED/CHECKUSR')
      * Variables used to load the external html source member
     D Lib             s             10
     D Fn              s             10
     D Mbr             s             10
      *
     D Spaces          s             12a   inz('&nbsp;&nbsp;')
     D FMELDNR         S              2
     D FEILMELDING     S             80

     D                 DS
     D BSLDTÅÅ                 1      4  0
     D BSLDTMM                 5      6  0
     D BSLDTDD                 7      8  0
     D BSLDT                   1      8  0
     D                 DS
     D BSLTMH                  1      2  0
     D BSLTMM                  3      4  0
     D BSLTM                   1      4  0
      *
     D FEIL01          C                   CONST('Subcontractor is missing.')
     D FEIL02          C                   CONST('Type of container can not be -
     D                                     blank.')
     D FEIL03          C                   CONST('Loading date is not valid.')
     D FEIL04          C                   CONST('Loading time is not valid.')
     D FEIL05          C                   CONST('Vessel code is not valid.')
     D FEIL06          C                   CONST('Name of shipper is missing.')
     D FEIL07          C                   CONST('Address is missing.')
     D FEIL08          C                   CONST('Postal place can not be -
     D                                     blank.')
     D FEIL09          C                   CONST('Country code is not valid.')
     D FEIL10          C                   CONST('Number of packages can not -
     D                                     be 0.')
     D FEIL11          C                   CONST('Goods discription should be -
     D                                     given.')
     D FEIL12          C                   CONST('Weight can not be 0.')
     D FEIL13          C                   CONST('Name of reveiver is missing.')
     D FEIL14          C                   CONST('Address is missing.')
     D FEIL15          C                   CONST('Postal place can not be -
     D                                     blank.')
     D FEIL16          C                   CONST('Country code is not valid.')
     D FEIL17          C                   CONST('Delivery ref. is missing.')
     D FEIL18          C                   CONST('Agreed rate is missing.')
      * For xlate mellom Upper Case og Lower Case
     D UC              C                   CONST('ABCDEFGHIJKLMNOPQRST-
     D                                     UVWXYZÆØÅ')
     D LC              C                   CONST('abcdefghijklmnopqrst-
     D                                     uvwxyzæøå')
     D                 DS
      *
      *--------------------------------------------------------------------
      * Prolog common to CGI programs
      * -Receive input from the remote browser
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prolog3
      *=====================================================================
      * MAIN PROCESSING LINE
      *=====================================================================
      * Parse input string
     C                   exsr      Parse
      *
     C                   EXSR      INIT
      *
      * Hvis første kall OG en...
     C                   IF        SubmPressed = 'Y'
     C                   EXSR      TESTB
     C                   ELSE
     C                   EXSR      GetData
     C                   END
      *
      * Set output skeleton html member name
     C                   exsr      SetSkl
      * Read skeleton output html into core
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
      * Issue html section /$top
     C                   exsr      SetTop
     C                   callp     wrtsection('top')
     C                   callp     wrtsection('BUNN')

      *
     C                   exsr      SetEnd
     C                   callp     wrtsection('end')
      *
     C                   IF        AVSLUTT = 'J'
     C                   MOVE      BSAVD         WSAVD
     C                   MOVE      BSOPD         WSOPD
     C                   call      'ESOVERS16C'
     C                   parm                    WSAVD
     C                   parm                    WSOPD
     C                   PARM                    IFSFIL           50
     C                   END
      *
      * Send output buffer and quit
     C     SLUTT         TAG
     C                   exsr      Exit
      *
      *
      *
      *=====================================================================
      * Parse input string
      *=====================================================================
     C     Parse         begsr
     C                   eval      SubmPressed = zhbgetvar('SubmPressed')
     C                   eval      lng     = zhbgetvar('lng')
     C                   eval      BckGnd  = zhbgetvar('bckgnd')

     C                   eval      WSUSER  = zhbgetvar('USER')
     C                   EVAL      UsrSpcName  = zhbgetvar('UsrSpcName')
      *
     C                   eval      WSAVD   = zhbgetvar('WSAVD')
     C                   eval      BSAVD   = c2n2(WSAVD)
     C                   eval      WSOPD   = zhbgetvar('WSOPD')
     C                   eval      BSOPD   = c2n2(WSOPD)
     C                   eval      WSTO    = zhbgetvar('WSTO')
     C                   eval      WSCONT  = zhbgetvar('WSCONT')
     C                   eval      WSLDT   = zhbgetvar('WSLDT')
     C                   eval      BSLDT10 = c2n2(WSLDT)
     C                   MOVE      BSLDT10       BSLDT
     C                   eval      WSLTM   = zhbgetvar('WSLTM')
     C                   eval      BSLTM5  = c2n2(WSLTM)
     C                   MOVE      BSLTM5        BSLTM
     C                   eval      WSBCD   = zhbgetvar('WSBCD')
     C     LC:UC         XLATE     WSBCD         WSBCD
     C                   eval      WSSNV   = zhbgetvar('WSSNV')
     C                   eval      WSSAD1  = zhbgetvar('WSSAD1')
     C                   eval      WSSAD2  = zhbgetvar('WSSAD2')
     C                   eval      WSSAD3  = zhbgetvar('WSSAD3')
     C                   eval      WSSLK   = zhbgetvar('WSSLK')
     C     LC:UC         XLATE     WSSLK         WSSLK
     C                   eval      WSNT    = zhbgetvar('WSNT')
     C                   eval      BSNT    = c2n2(WSNT)
     C                   eval      WSDSC   = zhbgetvar('WSDSC')
     C                   eval      WSVKT   = zhbgetvar('WSVKT')
     C                   eval      BSVKT   = c2n2(WSVKT)
     C                   eval      WSKNV   = zhbgetvar('WSKNV')
     C                   eval      WSKAD1  = zhbgetvar('WSKAD1')
     C                   eval      WSKAD2  = zhbgetvar('WSKAD2')
     C                   eval      WSKAD3  = zhbgetvar('WSKAD3')
     C                   eval      WSKLK   = zhbgetvar('WSKLK')
     C     LC:UC         XLATE     WSKLK         WSKLK
     C                   eval      WSDREF  = zhbgetvar('WSDREF')
     C     LC:UC         XLATE     WSDREF        WSDREF
     C                   eval      WSTARI  = zhbgetvar('WSTARI')

     C                   endsr
      *=====================================================================
      * Set html output skeleton VED FEIL
      *=====================================================================
     C     SetSkl        begsr
     C                   eval      lng = uppify(lng)
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC' + lng
     C                   eval      Mbr = 'ESOVERS14'
     C*                  CAT       XSPRÅK:0      MBR
     C                   endsr
      *=====================================================================
      * Set html output variables for section /$top
      *=====================================================================
     C     SetTop        begsr
      * Repeat national language for the next invocation
     C                   callp     updhtmlvar('LNG':lng:
     C                             InitHTMLVars)
      * Repeat background color for the next invocation
     C                   callp     updhtmlvar('TXTCOLOR':'black')
     C                   callp     updhtmlvar('BOLD':' ')
     C                   callp     updhtmlvar('BGCOLOR':BIFARG)
     C                   callp     updhtmlvar('LINK':'0000FF')
     C                   callp     updhtmlvar('VLINK':'FF0000')
     C                   callp     updhtmlvar('ALINK':'00FF00')
      * KUNDE
BODY C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updHtmlVar('USER':WSUSER)
     C                   callp     updHtmlVar('UsrSpcName':UsrSpcName)
     C                   callp     updHtmlVar('KUNDE':
     C                             %trim(%editc(BIKUNR:'Z')))
     C                   callp     updHTMLvar('sdato':
     C                             %trim(%editw(BIDTV:'    .  .  ')))
     C                   callp     updHTMLvar('stid':
     C                             %trim(%editw(BITIDV:'  .  .  ')))
      *
     C                   callp     updhtmlvar('WSAVD':
     C                             %trim(%editc(BSAVD:'Z')))
     C                   callp     updhtmlvar('WSOPD':
     C                             %trim(%editc(BSOPD:'Z')))
     C                   callp     updHtmlVar('WSTO':WSTO)
     C                   callp     updHtmlVar('WSCONT':WSCONT)
     C                   callp     updHTMLvar('WSLDT':
     C                             %trim(%editw(BSLDT:'    .  .  ')))
     C                   callp     updHTMLvar('WSLTM':
     C                             %trim(%editw(BSLTM:'  :  ')))
     C                   callp     updHtmlVar('WSBCD':WSBCD)
     C                   callp     updHtmlVar('CBNAB':CBNAB)
     C                   callp     updHtmlVar('WSSNV':WSSNV)
     C                   callp     updHtmlVar('WSSAD1':WSSAD1)
     C                   callp     updHtmlVar('WSSAD2':WSSAD2)
     C                   callp     updHtmlVar('WSSAD3':WSSAD3)
     C                   callp     updHtmlVar('WSSLK':WSSLK)
     C                   callp     updHtmlVar('WSSLKNV':WSSLKNV)
     C                   callp     updhtmlvar('WSNT':
     C                             %trim(%editc(BSNT:'Z')))
     C                   callp     updHtmlVar('WSDSC':WSDSC)
     C                   callp     updhtmlvar('WSVKT':
     C                             %trim(%editc(BSVKT:'Z')))
     C                   callp     updHtmlVar('WSKNV':WSKNV)
     C                   callp     updHtmlVar('WSKAD1':WSKAD1)
     C                   callp     updHtmlVar('WSKAD2':WSKAD2)
     C                   callp     updHtmlVar('WSKAD3':WSKAD3)
     C                   callp     updHtmlVar('WSKLK':WSKLK)
     C                   callp     updHtmlVar('WSKLKNV':WSKLKNV)
     C                   callp     updHtmlVar('WSDREF':WSDREF)
     C                   callp     updHtmlVar('WSTARI':WSTARI)
     C                   callp     updHtmlVar('AVSLUTT':AVSLUTT)
      *
     C                   callp     updhtmlvar('FEILMELDING':
     C                             FEILMELDING)
     C                   callp     updhtmlvar('FMELDNR':FMELDNR)
      *
     C                   endsr
      *
      *=====================================================================******
      *  Set output variables for section /$end
      *=====================================================================******
     C     SetEnd        begsr
      * Compute run time
     C                   time                    timedata2
     C     timedata2     subdur    timedata1     ms:*ms
     C                   eval      sec = ms / 1000000
     C                   callp     updhtmlvar('runtime':
     C                             %trim(%editw(sec:'     0 .   ')))
      *
     C                   endsr
      *
      *////////////////////////////////////////////////////////////
     C     TESTB         BEGSR
      *////////////////////////////////////////////////////////////
      *
      * TEST SUBCONTRACTOR
     C                   IF        WSTO = *BLANKS
     C                   MOVEL     FEIL01        FEILMELDING
     C                   MOVE      '01'          FMELDNR
     C                   GOTO      SLTBDT
     C                   END
      * TEST CONTAINER TYPE
     C                   IF        WSCONT = *BLANKS
     C                   MOVEL     FEIL02        FEILMELDING
     C                   MOVE      '02'          FMELDNR
     C                   GOTO      SLTBDT
     C                   END
      * TEST LOADING DATE
     C                   MOVE      BSLDTDD       UDD               2 0
     C                   MOVE      BSLDTMM       UMM               2 0
     C                   MOVE      BSLDTÅÅ       UÅÅ               2 0
     C                   CALL      'TESTDATO'
     C                   PARM                    UDD
     C                   PARM                    UMM
     C                   PARM                    UÅÅ
     C                   PARM                    UFLAGG            1 0
     C     UFLAGG        IFEQ      1
     C                   MOVEL     FEIL03        FEILMELDING
     C                   MOVE      '03'          FMELDNR
     C                   GOTO      SLTBDT
     C                   ENDIF
      * TEST LOADING TIME
     C                   IF        BSLTMH > 23 OR BSLTMM > 59
     C                   MOVEL     FEIL04        FEILMELDING
     C                   MOVE      '04'          FMELDNR
     C                   GOTO      SLTBDT
     C                   END
      * TEST VESSEL CODE
     C     WSBCD         CHAIN     CDBOAT                             33
     C                   IF        *IN33
     C                   MOVEL     FEIL05        FEILMELDING
     C                   MOVE      '05'          FMELDNR
     C                   GOTO      SLTBDT
     C                   ENDIF
      * TEST SHIPPING NAME
     C                   IF        WSSNV = *BLANKS
     C                   MOVEL     FEIL06        FEILMELDING
     C                   MOVE      '06'          FMELDNR
     C                   GOTO      SLTBDT
     C                   ENDIF
      * TEST SHIPPING ADDRESS
     C                   IF        WSSAD1 = *BLANKS
     C                   MOVEL     FEIL07        FEILMELDING
     C                   MOVE      '07'          FMELDNR
     C                   GOTO      SLTBDT
     C                   ENDIF
      * TEST SHIPPING POSTAL PLACE
     C                   IF        WSSAD3 = *BLANKS
     C                   MOVEL     FEIL08        FEILMELDING
     C                   MOVE      '08'          FMELDNR
     C                   GOTO      SLTBDT
     C                   ENDIF
      * TEST SHIPPING COUNTRY CODE
     C     KODTLKKS      CHAIN     KODTLK                             33
     C                   IF        *IN33
     C                   MOVEL     FEIL09        FEILMELDING
     C                   MOVE      '09'          FMELDNR
     C                   GOTO      SLTBDT
     C                   ENDIF
      * TEST NUMBER OF PACKAGES
     C                   IF        BSNT = 0
     C                   MOVEL     FEIL10        FEILMELDING
     C                   MOVE      '10'          FMELDNR
     C                   GOTO      SLTBDT
     C                   ENDIF
      * TEST GOODS DISCRIPTION
     C                   IF        WSDSC = *BLANKS
     C                   MOVEL     FEIL11        FEILMELDING
     C                   MOVE      '11'          FMELDNR
     C                   GOTO      SLTBDT
     C                   ENDIF
      * TEST WEIGHT
     C                   IF        BSVKT = 0
     C                   MOVEL     FEIL12        FEILMELDING
     C                   MOVE      '12'          FMELDNR
     C                   GOTO      SLTBDT
     C                   ENDIF
      * TEST RECEIVER NAME
     C                   IF        WSKNV = *BLANKS
     C                   MOVEL     FEIL13        FEILMELDING
     C                   MOVE      '13'          FMELDNR
     C                   GOTO      SLTBDT
     C                   ENDIF
      * TEST RECEIVER ADDRESS
     C                   IF        WSKAD1 = *BLANKS
     C                   MOVEL     FEIL14        FEILMELDING
     C                   MOVE      '14'          FMELDNR
     C                   GOTO      SLTBDT
     C                   ENDIF
      * TEST RECEIVER POSTAL PLACE
     C                   IF        WSSAD3 = *BLANKS
     C                   MOVEL     FEIL15        FEILMELDING
     C                   MOVE      '15'          FMELDNR
     C                   GOTO      SLTBDT
     C                   ENDIF
      * TEST SHIPPING COUNTRY CODE
     C     KODTLKKK      CHAIN     KODTLK                             33
     C                   IF        *IN33
     C                   MOVEL     FEIL16        FEILMELDING
     C                   MOVE      '16'          FMELDNR
     C                   GOTO      SLTBDT
     C                   ENDIF
      * TEST DELIVERY REFERENCE
     C                   IF        WSDREF = *BLANKS
     C                   MOVEL     FEIL17        FEILMELDING
     C                   MOVE      '17'          FMELDNR
     C                   GOTO      SLTBDT
     C                   ENDIF
      * TEST AGREED RATE
     C                   IF        WSTARI = *BLANKS
     C                   MOVEL     FEIL18        FEILMELDING
     C                   MOVE      '18'          FMELDNR
     C                   GOTO      SLTBDT
     C                   ENDIF
      *
     C                   EXSR      OPDDOC
      *
     C     SLTBDT        ENDSR
      *
      *=====================================================================
      *  Oppdater informasjon
      *=====================================================================
     C     OPDDOC        begsr
      *
     C     HEADFK        CHAIN     ESOSF2                             33
     C                   EXSR      MOVEWF1
     C                   IF        *IN33
     C                   MOVE      BSAVD         O2AVD
     C                   MOVE      BSOPD         O2OPD
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVE      WDT           O2RDT
     C                   MOVE      WTM           O2RTM
     C                   WRITE     ESOSF2R
      *
     C                   ELSE
      * Endring
     C                   UPDATE    ESOSF2R
     C                   ENDIF
      *
     C                   MOVE      'J'           AVSLUTT
      *
     C                   ENDSR
      *
      *=====================================================================
      *  OPPDATER FELTER I FIL
      *=====================================================================
     C     MOVEWF1       BEGSR
     C                   EVAL      O2TO   = WSTO
     C                   EVAL      O2CONT = WSCONT
     C                   EVAL      O2LDT  = BSLDT
     C                   EVAL      O2LTM  = BSLTM
     C                   EVAL      O2BCD  = WSBCD
     C                   EVAL      O2SNV  = WSSNV
     C                   EVAL      O2SAD1 = WSSAD1
     C                   EVAL      O2SAD2 = WSSAD2
     C                   EVAL      O2SAD3 = WSSAD3
     C                   EVAL      O2SLK  = WSSLK
     C                   EVAL      O2NT   = BSNT
     C                   EVAL      O2DSC  = WSDSC
     C                   EVAL      O2VKT  = BSVKT
     C                   EVAL      O2KNV  = WSKNV
     C                   EVAL      O2KAD1 = WSKAD1
     C                   EVAL      O2KAD2 = WSKAD2
     C                   EVAL      O2KAD3 = WSKAD3
     C                   EVAL      O2KLK  = WSKLK
     C                   EVAL      O2DREF = WSDREF
     C                   EVAL      O2TARI = WSTARI
      *
     C                   ENDSR
      *
      *=====================================================================
      *  Hent data fra fra fil ved først "syklus"  / endring av post
      *=====================================================================
     C     GetData       begsr
     C     HEADFK        CHAIN(N)  ESOSF2                             33
      *
     C                   IF        *IN33
     C     HEADFK        CHAIN     HEADF                              33
     C     HEADFK        CHAIN     BILLOF                             33
     C     HEADFK        CHAIN     BLV1                               33
     C     KODTAK        CHAIN     KODTA                              33
     C                   EVAL      WSTO   = *BLANKS
     C                   EVAL      WSCONT = %TRIM(BVKDC) + ' ' + %TRIM(BVVS)
     C                   EVAL      BSLDT  = 0
     C                   EVAL      BSLTM  = 0
     C                   EVAL      WSBCD  = BLOB
     C     WSBCD         CHAIN     CDBOAT                             33
     C                   EVAL      WSSNV  = HENAS
     C                   EVAL      WSSAD1 = HEADS1
     C                   EVAL      WSSAD2 = HEADS2
     C                   EVAL      WSSAD3 = HEADS3
     C                   IF        KOAIE  = 'E'
     C                   EVAL      WSSLK  = HETRI
     C                   ELSE
     C                   EVAL      WSSLK  = HELKA
     C                   END
     C     KODTLKKS      CHAIN     KODTLK                             33
     C  N33              EVAL      WSSLKNV = KLKNVN
     C   33              EVAL      WSSLKNV = *BLANKS
     C                   EVAL      BSNT   = HENT
     C                   EVAL      WSDSC  = %TRIM(HEVS1) + ' ' + %TRIM(HEVS2)
     C                   EVAL      BSVKT  = HEVKT
     C                   EVAL      WSKNV  = HENAK
     C                   EVAL      WSKAD1 = HEADK1
     C                   EVAL      WSKAD2 = HEADK2
     C                   EVAL      WSKAD3 = HEADK3
     C                   IF        KOAIE  = 'E'
     C                   EVAL      WSKLK  = HELKA
     C                   ELSE
     C                   EVAL      WSKLK  = HETRI
     C                   END
     C     KODTLKKK      CHAIN     KODTLK                             33
     C  N33              EVAL      WSKLKNV = KLKNVN
     C   33              EVAL      WSKLKNV = *BLANKS
     C                   EVAL      WSDREF = *BLANKS
     C                   EVAL      WSTARI = *BLANKS
      *
     C                   ELSE
      *
     C                   EVAL      WSTO   = O2TO
     C                   EVAL      WSCONT = O2CONT
     C                   EVAL      BSLDT  = O2LDT
     C                   EVAL      BSLTM  = O2LTM
     C                   EVAL      WSBCD  = O2BCD
     C     WSBCD         CHAIN     CDBOAT                             33
     C                   EVAL      WSSNV  = O2SNV
     C                   EVAL      WSSAD1 = O2SAD1
     C                   EVAL      WSSAD2 = O2SAD2
     C                   EVAL      WSSAD3 = O2SAD3
     C                   EVAL      WSSLK  = O2SLK
     C     KODTLKKS      CHAIN     KODTLK                             33
     C  N33              EVAL      WSSLKNV = KLKNVN
     C   33              EVAL      WSSLKNV = *BLANKS
     C                   EVAL      BSNT   = O2NT
     C                   EVAL      WSDSC  = O2DSC
     C                   EVAL      BSVKT  = O2VKT
     C                   EVAL      WSKNV  = O2KNV
     C                   EVAL      WSKAD1 = O2KAD1
     C                   EVAL      WSKAD2 = O2KAD2
     C                   EVAL      WSKAD3 = O2KAD3
     C                   EVAL      WSKLK  = O2KLK
     C     KODTLKKK      CHAIN     KODTLK                             33
     C  N33              EVAL      WSKLKNV = KLKNVN
     C   33              EVAL      WSKLKNV = *BLANKS
     C                   EVAL      WSDREF = O2DREF
     C                   EVAL      WSTARI = O2TARI
      *
     C                   END
      *
     C                   ENDSR
      *
      *=====================================================================
      *  Initier
      *=====================================================================
     C     INIT          begsr
      * Get program start time for calculating execution time
     C                   time                    timedata1
      *
     C                   open      BRIDF                                50
     C     WSUSER        CHAIN(N)  BRIDF                              50
      *
      * En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
      * Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
      *
     C                   EXSR      TSTUSER
      *
     C                   open      HEADF                                50
     C                   open      BILLOF                               50
     C                   open      BLV1                                 50
     C                   open      KODTA                                50
     C                   open      KODTLK                               50
     C                   open      CDBOAT                               50
     C                   open      ESOSF2                               50
     C                   open      BRPARF                               50
      *
     C     HEADFK        klist
     C                   kfld                    BSAVD
     C                   kfld                    BSOPD
     C     KODTAK        klist
     C                   kfld                    KOAUNI
     C                   kfld                    BSAVD
     C                   EVAL      KOAUNI = 'A'
     C     KODTLKKS      klist
     C                   kfld                    KLKUNI
     C                   kfld                    WSSLK
     C                   EVAL      KLKUNI = 'LK'
     C     KODTLKKK      klist
     C                   kfld                    KLKUNI
     C                   kfld                    WSKLK
     C     BrParKey      klist
     C                   kfld                    PABRID
     C                   kfld                    PAKUNR
     C                   kfld                    PAID
      *
     C                   MOVE      BIBRID        PABRID
     C                   MOVE      *zeros        PAKUNR
      * HENT SPRÅKKODE
     C                   MOVEL     'SPRÅK'       PAID
     C     BrParKey      Chain     BRPARF                             33
     C                   IF        (*IN33 OR (PAVRDA <> 'EN'))
     C                   MOVE      *BLANKS       XSPRÅK            2
     C                   ELSE
     C                   MOVE      'EN'          XSPRÅK            2
     C                   END
      *
     C                   MOVE      'N'           AVSLUTT           1
     c                   move      *blanks       feilmelding
     c                   move      *blanks       fmeldnr
      *
     C                   endsr
      *////////////////////////////////////////////////////////////
     C     TSTUSER       BEGSR
      *////////////////////////////////////////////////////////////
      /copy syspeds/qrpgsrcpy,tstuser
      *
     C                   endsr
      *=====================================================================
      * Send output buffer and quit
      *=====================================================================
     C     Exit          begsr
      * Compute and display run time
     C                   time                    timedata2
     C     timedata2     subdur    timedata1     ms:*ms
     C                   eval      sec = ms / 1000000
     C                   callp     updhtmlvar('runtime':
     C                             %trim(%editw(sec:'     0 .   ')))
     C                   callp     wrtsection('endhtml')
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      *
     C                   close     BRIDF
     C                   close     HEADF
     C                   close     BILLOF
     C                   close     BLV1
     C                   close     KODTA
     C                   close     KODTLK
     C                   close     CDBOAT
     C                   close     ESOSF2
     C                   close     BRPARF
     C                   eval      *inlr = *on
     C                   return
     C                   endsr
