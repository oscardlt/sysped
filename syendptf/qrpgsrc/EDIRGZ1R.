     fedis1     uf   e           k disk
     fedire1k   uf   e           k disk    usropn
     fedise1k   uf   e           k disk    usropn
     ‚* Prototype for 'SYGE15C'
     d syge15c         pr                  extpgm('SYGE15C')
     d wdt_                                like(wdt)
     d wtm_                                like(wtm)
     ‚* Prototype for 'SYGE20'
     d syge20          pr                  extpgm('SYGE20')
     d ufunksjon_                          like(ufunksjon)
     d udato1_                             like(udato1)
     d udato2_                             like(udato2)
     d udager_                             like(udager)
     d runcmd          pr                  extpgm('QCMDEXC')
     d  cmd                          36a   const
     d  len                          15p 5 const
     ‚*----------------------------------------------------------------------
     ‚* Stand Alone Fields - TOP
     ‚*----------------------------------------------------------------------
     d udager          s              5  0
     d udato1          s              8  0
     d udato2          s              8  0
     d ufunksjon       s              3
     d wdt             s              8
     d wdtf            s              8  0
     d wsdt            s              8P 0
     d wtm             s              6
     d wcmd            s             36
     ‚*----------------------------------------------------------------------
     ‚* Stand Alone Fields - BOTTOM
     ‚*----------------------------------------------------------------------
     ‚*//////////////
     ‚*  Hodelogikk /
     ‚*//////////////
      /free
       exsr sra;
       exsr srb;
       *inlr=*on;
       //////////////////////////////////////////////////////////////
       //  Initiering                                           SRA /
       //////////////////////////////////////////////////////////////
       begsr sra;

       wcmd = *blanks;
       wcmd = 'OVRDBF FILE(EDIRE1K) FRCRATIO(*NONE)';
       runcmd(wcmd:36);
       open edire1k;

       wcmd = *blanks;
       wcmd = 'OVRDBF FILE(EDISE1K) FRCRATIO(*NONE)';
       runcmd(wcmd:36);
       open edise1k;

       endsr;
       //////////////////////////////////////////////////////////////
       //  Les utvekslingslogg                                  SRB /
       //////////////////////////////////////////////////////////////
       begsr srb;
         //_________________________________
         // Hente dagens dato og klokkeslett
         //"""""""""""""""""""""""""""""""""
         syge15c ( wdt : wtm );
      /end-free
     c                   move      wdt           wdtf
     ‚*___________________________________
     ‚* Subtraher 60 dager fra dagens dato
     ‚*"""""""""""""""""""""""""""""""""""
      /free
         ufunksjon = 'SUB';
         udato1 = wdtf;
         udager = 60;
         syge20 ( ufunksjon : udato1 : udato2 :
         udager );
         wsdt = udato2;
         //____________________
         // Les utvekslingslogg
         //""""""""""""""""""""
         setll *loval edis1;
         dou %eof(edis1);
           read edis1;
           if %eof(edis1);
             leave;
           endif;
           // Avslutt ved maks 60 dager
           if sdt > wsdt;
             leave;
           endif;
           // Hvis REMOVED er den allerede vekk
           if smbr <> 'REMOVED';
             exsr srbb;
           endif;

         enddo;
       endsr;
       //////////////////////////////////////////////////////////////
       //  Slett                                               SRBB /
       //////////////////////////////////////////////////////////////
       begsr srbb;

         select;

         when sfile = 'EDIRE1K';
           setll smbr edire1k;
           dou %eof(edire1k);
             reade smbr edire1k;
             if %eof(edire1k);
               leave;
             endif;
             delete edirer;
           enddo;
           smbr='REMOVED   ';
           update edisr;

         when sfile = 'EDISE1K';
           setll smbr edise1k;
           dou %eof(edise1k);
             reade smbr edise1k;
             if %eof(edise1k);
               leave;
             endif;
             delete ediser;
           enddo;
           smbr='REMOVED   ';
           update edisr;

         endsl;
       endsr;
      /end-free
