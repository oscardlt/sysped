      *
      ***********************************************************
      *** PROGRAM SKAL OPPDATERE SADEFCF                      ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01-02 06-08 10 11 13-26
      *** LEDIGE INDIKATORER 27-29 31 32 34-48 50-81          ***
      ***********************************************************
      *
     FKODTA     IF   E           K DISK
     FCUNDL01   IF   E           K DISK
     FKOFAST    IF   E           K DISK
     FFIRM      IF   E           K DISK
     FEDIM4     IF   E           K DISK
     FSVEH      IF   E           K DISK
     FSVZH      IF   E           K DISK
     FSADEFCL2  IF A E           K DISK
     FTRACKF    O  A E             DISK
     D                SDS
     D  UUSR                 254    263
      *
     C                   EXSR      INIT
      *
     C     SADEFCL2K     CHAIN     SADEFCL2                           33
     C                   IF        *IN33
      * PROSEDYRE
     C                   EVAL      CLPR   = '01'
     C                   EVAL      KFTYP = 'SADEFPR'
     C                   EVAL      KFKOD = '01'
     C     KOFASTK       CHAIN     KOFAST                             33
     C                   IF        *IN33
     C                   EVAL      CLPRT  = 'IMMEDIATE_RELEASE_IMPORT'
     C                   ELSE
     C                   EVAL      CLPRT  = KFTXT
     C                   END
      * EKSPORTTYPE
     C     SADEFCL2K     CHAIN     SVEH                               33
     C                   SELECT
     C                   WHEN      *IN33
     C                   WHEN      SVEH_MTYP = 'UGE'
     C                   EVAL      CLETYP = '05'
     C                   EVAL      KFTYP = 'SADEFETYPE'
     C                   EVAL      KFKOD = '05'
     C     KOFASTK       CHAIN     KOFAST                             33
     C                   IF        *IN33
     C                   EVAL      CLETYPT = 'UGE-EXPORT'
     C                   ELSE
     C                   EVAL      CLETYPT = KFTXT
     C                   END
     C                   ENDSL
      * EKSPORT-ID
     C  N33SVEH_TUID     CHAIN     SVZH                               33
     C                   IF        *IN33 = *OFF
     C                   EVAL      CLEID = SVZH_MRNN
     C                   END
     C     *LOVAL        SETLL     FIRM
     C                   READ      FIRM                                   33
     C     KODTAK        CHAIN     KODTA                              33
     C     CUNDL01K      CHAIN     CUNDL01                            33
     C                   EVAL      CLRG    = SYRG
     C                   MOVE      U0068A        CL0068A
     C                   MOVE      U0068B        CL0068B
     C                   MOVE      UNTK          CLNTK
     C                   MOVE      UVKB          CLVKB
     C                   MOVE      UTRID         CLTRID
     C     EDIM4K        CHAIN     EDIM4                              33
     C                   MOVE      M3039E        CL3039E
     C                   WRITE     SADEFCFR
     C                   END
      *
     C                   EXSR      TTSR
      *
     C     SLUTT         TAG
     C                   MOVE      '1'           *INLR
     C                   RETURN
      *
      ***********************************************
     C     INIT          BEGSR
      ***********************************************
     C     SADEFCL2K     KLIST
     C                   KFLD                    CLAVD
     C                   KFLD                    CLTDN
     C     KODTAK        KLIST
     C                   KFLD                    KOAUNI
     C                   KFLD                    CLAVD
     C     CUNDL01K      KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    KOAKNR
     C     EDIM4K        KLIST
     C                   KFLD                    MSR
     C                   KFLD                    CLAVD
     C                   KFLD                    CLTDN
     C                   KFLD                    CL0068A
     C                   KFLD                    CL0068B
     C                   KFLD                    M0068C
     C     KOFASTK       KLIST
     C                   KFLD                    KFTYP
     C                   KFLD                    KFKOD
      *
     C     *ENTRY        PLIST
     C                   PARM                    UAVD              4
     C                   PARM                    UTDN              7
     C                   PARM                    U0068A            8
     C                   PARM                    U0068B            6
     C                   PARM                    UNTK              7
     C                   PARM                    UVKB              9
     C                   PARM                    UTRID            17
      *
     C                   MOVE      UAVD          CLAVD
     C                   MOVE      UTDN          CLTDN
     C                   EVAL      KOAUNI = 'A'
     C                   EVAL      MSR    = 'S'
     C                   EVAL      M0068C = 1
      *
     C                   ENDSR
      *
      ***********************************************
     C     TTSR          BEGSR
      ***********************************************
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   EVAL      TTAVD  = CLAVD
     C                   EVAL      TTOPD  = CLTDN
     C                   EVAL      TTTUR  = 0
     C                   EVAL      TTACTI = 'EFC'
     C                   MOVE      WDT           TTDATE
     C                   MOVE      WTM           TTTIME
     C                   MOVE      TTDATE        TTDATL
     C                   MOVE      TTTIME        TTTIML
     C                   MOVE      UUSR          TTUSER
     C                   EVAL      TTTEXT = 'Oppdrag er klar til E.F. '
     C                             + 'manifest.'
     C                   EVAL      TTTEXL = 'Oppdrag er klar til E.F. '
     C                             + 'manifest.'
     C                   EVAL      TTEDEV = 'RDY'
      *
     C                   WRITE     TRACKFR
      *
     C                   ENDSR
      *
