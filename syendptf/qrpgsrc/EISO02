      *********************************************************************
      * PROGRAM VISE VALGT SAK
      *
CRTPG+*  CRTPGM SYSPED/EISO02 MODULE(*LIBL/EISO02
CRTPGM*        *LIBL/CHECKUSR *LIBL/INCGI) ACTGRP(CGI) AUT(*USE)
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 YBC PTF10 VIS FAKTURA KUNDENR
xxxxxx*xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
12XXX * 02 YBC PTF12 Utvide max vedlegg fra 20 til 50
********************************************************************
      *
      *********************************************************************
      * MAIN PROGRAM FLOW
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
     FBRIDF     UF   E           K DISK    USROPN
     FBRPARF    IF   E           K DISK    USROPN
     FISOFL8    IF   E           K DISK    USROPN
     FISOLFL1   IF   E           K DISK    USROPN
     FISOLOGL1  IF   E           K DISK    USROPN
     FISOKDA    IF   E           K DISK    USROPN
     FISOKDF    IF   E           K DISK    USROPN
     FISOKDP    IF   E           K DISK    USROPN
     FISOKDR    IF   E           K DISK    USROPN
     FISOKDS    IF   E           K DISK    USROPN
     FISOKDT    IF   E           K DISK    USROPN
     FKOFAST    IF   E           K DISK    USROPN
     FARKIVL06  IF   E           K DISK    USROPN
     FCUNDL01   IF   E           K DISK    USROPN
     FTRAN      IF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Prototype defintions and standard system API error structure
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      *--------------------------------------------------------------------
      * Variables common to CGI programs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
     D OddEven         s             10
      * Variables received from the input string
     D lng             s              2
     D BckGnd          s             10
     D lstbrd          s              1
     D UsrSpcName      s             10
     D WSUSER          S             10
     D WSNA            S             30
     D WSSAKSNR        S              7
     D XSAKSNR         S              7  0
     D WSKEY           S            128
     D WSLEDET1        S             15
     D WSLEDET2        S             15
     D WSLEDET3        S             15
     D WSLEDET4        S             15
     D WSLEDET5        S             15
     D WSLEDET6        S             15
     D WSRES           S              9  2
N01  D XFAKTKUN        S             16
     D PGM             C                   CONST('SYSPED/CHECKUSR')
      * Variables received from the input string
     D Lib             s             10
     D Fn              s             10
     D Mbr             s             10
      * FOR KORT STRENG, 120->200  INGEN MERKING.
     D                 DS
     D  updf1                  1    200
     D  updf2                201    400
     D  updf3                401    600
     D  updf4                601    800
     D  updf5                801   1000
     D  updf6               1001   1200
     D  updf7               1201   1400
     D  updf8               1401   1600
     D  updf9               1601   1800
     D  updf10              1801   2000
     D  updf11              2001   2200
     D  updf12              2201   2400
     D  updf13              2401   2600
     D  updf14              2601   2800
     D  updf15              2801   3000
     D  updf16              3001   3200
     D  updf17              3201   3400
     D  updf18              3401   3600
     D  updf19              3601   3800
     D  updf20              3801   4000
N02  D  updf21              4001   4200
N02  D  updf22              4201   4400
N02  D  updf23              4401   4600
N02  D  updf24              4601   4800
N02  D  updf25              4801   5000
N02  D  updf26              5001   5200
N02  D  updf27              5201   5400
N02  D  updf28              5401   5600
N02  D  updf29              5601   5800
N02  D  updf30              5801   6000
N02  D  updf31              6001   6200
N02  D  updf32              6201   6400
N02  D  updf33              6401   6600
N02  D  updf34              6601   6800
N02  D  updf35              6801   7000
N02  D  updf36              7001   7200
N02  D  updf37              7201   7400
N02  D  updf38              7401   7600
N02  D  updf39              7601   7800
N02  D  updf40              7801   8000
N02  D  updf41              8001   8200
N02  D  updf42              8201   8400
N02  D  updf43              8401   8600
N02  D  updf44              8601   8800
N02  D  updf45              8801   9000
N02  D  updf46              9001   9200
N02  D  updf47              9201   9400
N02  D  updf48              9401   9600
N02  D  updf49              9601   9800
N02  D  updf50              9801  10000
S02  D* updf                   1   4000    DIM(20)
N02  D  updf                   1  10000    DIM(50)
      *
      *--------------------------------------------------------------------
      * Prolog common to CGI programs
      * -Receive input from the remote browser
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prolog3
      *=====================================================================******
      * MAIN PROCESS LINE
      *=====================================================================******
      * Get program start time for calculating execution time
     C                   time                    timedata1
      * Parse input
     C                   exsr      Parse
      *
     C                   EXSR      INIT
      *
      * Set output skeleton html member name
     C                   exsr      SetSkl
      * Read skeleton output html, etc.
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
      * Retrieve environment variables
     C                   exsr      RtvEnvVar
      *------------------                                                   ******
      * Write HTML sections
      *
     C     XSAKSNR       CHAIN     ISOFL8                             33
     C                   exsr      SetTop
     C                   EXSR      HENTDOK
     C                   callp     wrtsection('top')
      *
     C     XSAKSNR       SETLL     ISOLFL1
     C     XSAKSNR       READE     ISOLFL1                                33
     C  N33              callp     wrtsection('tablestr')
     C                   DOW       *IN33 = *OFF
     C     ISLPFK        CHAIN     ISOKDF                             33
     C                   SELECT
     C                   WHEN      *IN33
     C                   callp     updhtmlvar('ISLPFK':ISLPFK)
     C                   callp     updhtmlvar('PAFARGE':'')
     C                   WHEN      FFARGE = *BLANKS
     C                   callp     updhtmlvar('ISLPFK':FTEXT)
     C                   callp     updhtmlvar('PAFARGE':'')
     C                   OTHER
     C                   callp     updhtmlvar('ISLPFK':FTEXT)
     C                   EVAL      XFARGE = 'BGCOLOR="#'
     C                   CAT       FFARGE:0      XFARGE           30
     C                   CAT       '"':0         XFARGE
     C                   callp     updhtmlvar('PAFARGE':XFARGE)
     C                   ENDSL
     C     ISLANS        CHAIN     ISOKDP                             33
     C                   SELECT
     C                   WHEN      *IN33
     C                   callp     updhtmlvar('ISLANS':ISLANS)
     C                   callp     updhtmlvar('ANFARGE':'')
     C                   WHEN      PFARGE = *BLANKS
     C                   callp     updhtmlvar('ISLANS':PTEXT)
     C                   callp     updhtmlvar('ANFARGE':'')
     C                   OTHER
     C                   callp     updhtmlvar('ISLANS':PTEXT)
     C                   EVAL      XFARGE = 'BGCOLOR="#'
     C                   CAT       PFARGE:0      XFARGE           30
     C                   CAT       '"':0         XFARGE
     C                   callp     updhtmlvar('ANFARGE':XFARGE)
     C                   ENDSL
     C                   callp     updhtmlvar('ISLAV1':ISLAV1)
     C                   callp     updhtmlvar('ISLAV2':ISLAV2)
     C                   callp     updhtmlvar('ISLAV3':ISLAV3)
      * odde/partalls-linjer i alternativ skyggelegging/bakgrunnsfarge
     C     OddEven       IFEQ      ODD
     C                   eval      OddEven = EVEN
     C                   else
     C                   eval      OddEven = ODD
     C                   end
     C                   callp     updHTMLvar('ODDEVEN':OddEven)
      *
     C                   callp     wrtsection('tablerow')
     C     XSAKSNR       READE     ISOLFL1                                33
     C                   ENDDO
     C                   callp     wrtsection('tableend')
      *
     C                   EXSR      SRTT
      *
      * 6-Section /$endhtml
     C                   exsr      SetEnd
     C     SLUTT         tag
     C                   callp     wrtsection('endhtml')
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
     C                   EXSR      EXIT
     C                   return
      *
      *=====================================================================******
      * Parse input
      *=====================================================================******
     C     Parse         begsr
     C                   eval      lng     = zhbgetvar('lng')
     C                   eval      BckGnd  = zhbgetvar('bckgnd')
     C                   eval      lstbrd  = zhbgetvar('lstbrd')
     C                   EVAL      WSUSER  = zhbgetvar('USER')
     C                   EVAL      UsrSpcName  = zhbgetvar('UsrSpcName')
     C                   EVAL      WSNA    = zhbgetvar('KNAVN')
     C                   EVAL      WSSAKSNR = zhbgetvar('ISSAKN')
     C                   eval      XSAKSNR  = c2n2(WSSAKSNR)
     C                   EVAL      WSKEY    = zhbgetvar('KEY')
      *
     C                   endsr
      *
      *=====================================================================******
      *  Set output variables for section /$top
      *  (search criteria)
      *=====================================================================******
     C     SetTop        begsr
      * Repeat national language for the next invocation
     C                   callp     updhtmlvar('LNG':lng:
     C                             InitHTMLVars)
OddEvC                   callp     updHTMLvar('ROWHEAD':RowHead)
OddEvC                   callp     updHTMLvar('LogoImg':LogoImg)
      * Color for text, background, links, visited links, active links
     C                   callp     updhtmlvar('TXTCOLOR':'black')
     C                   callp     updhtmlvar('BOLD':' ')
     C                   callp     updhtmlvar('BGCOLOR':BIFARG)
     C                   callp     updhtmlvar('LINK':'0000FF')
     C                   callp     updhtmlvar('VLINK':'FF0000')
     C                   callp     updhtmlvar('ALINK':'00FF00')
      * KUNDE
     C                   callp     updhtmlvar('WSNA':WSNA)
BODY C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updhtmlvar('USER':WSUSER)
     C                   callp     updHtmlVar('UsrSpcName':UsrSpcName)
     C                   callp     updHTMLvar('sdato':
     C                             %trim(%editw(BIDTV:'    .  .  ')))
     C                   callp     updHTMLvar('stid':
     C                             %trim(%editw(BITIDV:'  .  .  ')))
      * ISO-SAK
     C                   callp     updhtmlvar('KEY':WSKEY)
     C                   callp     updhtmlvar('ISSAKN':
     C                             %trim(%editc(ISSAKN:'Z')))
     C                   callp     updhtmlvar('ISKNA':
     C                             %trim(%editc(ISKNA:'Z')))
     C     CUNDL01K      CHAIN     CUNDL01                            33
     C                   IF        *IN33
     C                   MOVE      '*UKJENT'     XNAVNA           30
     C                   ELSE
     C                   MOVE      KNAVN         XNAVNA
     C                   END
     C                   callp     updhtmlvar('XNAVNA':XNAVNA)
     C                   callp     updhtmlvar('ISFTUN':
     C                             %trim(%editc(ISFTUN:'Z')))
     C                   callp     updhtmlvar('ISFAVD':
     C                             %trim(%editc(ISFAVD:'Z')))
     C                   callp     updhtmlvar('ISFOPD':
     C                             %trim(%editc(ISFOPD:'Z')))
S01  C*                  callp     updhtmlvar('ISFN':
S01  C*                            %trim(%editc(ISFN:'Z')))
N01  C                   EVAL      XFAKTKUN = %TRIM(%EDITC(ISFN:'Z'))
N01  C                             + '-' + %TRIM(%EDITC(ISFKN:'Z'))
N01  C                   callp     updhtmlvar('ISFN':XFAKTKUN)
     C                   callp     updhtmlvar('ISSTPU':ISSTPU)
     C                   callp     updhtmlvar('ISNYFN':
     C                             %trim(%editc(ISNYFN:'Z')))
     C                   callp     updhtmlvar('ISKRNO':
     C                             %trim(%editc(ISKRNO:'Z')))
     C                   callp     updhtmlvar('ISFNIN':ISFNIN)
     C                   callp     updhtmlvar('ISFINV':ISFINV)
     C                   callp     updhtmlvar('ISFTRF':
     C                             %trim(%editc(ISFTRF:'Z')))
     C     TRANK         CHAIN     TRAN                               33
     C                   IF        *IN33
     C     TRANK         CHAIN     CUNDL01                            33
     C  N33              callp     updhtmlvar('VMNAVN':KNAVN)
     C   33              callp     updhtmlvar('VMNAVN':'')
     C                   ELSE
     C                   callp     updhtmlvar('VMNAVN':VMNAVN)
     C                   END
     C                   callp     updhtmlvar('ISFBIL':ISFBIL)
     C                   callp     updhtmlvar('ISFADA':
     C                             %trim(%editw(ISFADA:'    .  .  ')))
     C                   callp     updhtmlvar('ISFADK':
     C                             %trim(%editw(ISFADK:'  :  ')))
     C                   callp     updhtmlvar('ISFAND':
     C                             %trim(%editw(ISFAND:'    .  .  ')))
     C                   callp     updhtmlvar('ISFANK':
     C                             %trim(%editw(ISFANK:'  :  ')))
      *
     C     KOFASTK1      CHAIN     KOFAST                             33
     C                   IF        *IN33
     C                   EVAL      WSLEDET1 = 'Posttype'
     C                   ELSE
     C                   EVAL      WSLEDET1 = KFTXT
     C                   END
     C                   callp     updHTMLvar('WSLEDET1':WSLEDET1)
     C     KOFASTK2      CHAIN     KOFAST                             33
     C                   IF        *IN33
     C                   EVAL      WSLEDET2 = 'Rekl.type'
     C                   ELSE
     C                   EVAL      WSLEDET2 = KFTXT
     C                   END
     C                   callp     updHTMLvar('WSLEDET2':WSLEDET2)
     C     KOFASTK3      CHAIN     KOFAST                             33
     C                   IF        *IN33
     C                   EVAL      WSLEDET3 = 'Avvikets art'
     C                   ELSE
     C                   EVAL      WSLEDET3 = KFTXT
     C                   END
     C                   callp     updHTMLvar('WSLEDET3':WSLEDET3)
     C     KOFASTK4      CHAIN     KOFAST                             33
     C                   IF        *IN33
     C                   EVAL      WSLEDET4 = 'Ansvarlig part'
     C                   ELSE
     C                   EVAL      WSLEDET4 = KFTXT
     C                   END
     C                   callp     updHTMLvar('WSLEDET4':WSLEDET4)
      *
     C     ISFPFO        CHAIN     ISOKDT                             33
     C                   SELECT
     C                   WHEN      *IN33
     C                   callp     updhtmlvar('TTEXT':ISFPFO)
     C                   callp     updhtmlvar('PTFARGE':'')
     C                   WHEN      TFARGE = *BLANKS
     C                   callp     updhtmlvar('TTEXT':TTEXT)
     C                   callp     updhtmlvar('PTFARGE':'')
     C                   OTHER
     C                   callp     updhtmlvar('TTEXT':TTEXT)
     C                   EVAL      XFARGE = 'BGCOLOR="#'
     C                   CAT       TFARGE:0      XFARGE           30
     C                   CAT       '"':0         XFARGE
     C                   callp     updhtmlvar('PTFARGE':XFARGE)
     C                   ENDSL
     C     ISRTYP        CHAIN     ISOKDR                             33
     C                   SELECT
     C                   WHEN      *IN33
     C                   callp     updhtmlvar('RTEXT':ISRTYP)
     C                   callp     updhtmlvar('REFARGE':'')
     C                   WHEN      RFARGE = *BLANKS
     C                   callp     updhtmlvar('RTEXT':RTEXT)
     C                   callp     updhtmlvar('REFARGE':'')
     C                   OTHER
     C                   callp     updhtmlvar('RTEXT':RTEXT)
     C                   EVAL      XFARGE = 'BGCOLOR="#'
     C                   CAT       RFARGE:0      XFARGE           30
     C                   CAT       '"':0         XFARGE
     C                   callp     updhtmlvar('REFARGE':XFARGE)
     C                   ENDSL
     C     ISOKDAK1      CHAIN     ISOKDA                             33
     C   33ISOKDAK2      CHAIN     ISOKDA                             33
     C                   SELECT
     C                   WHEN      *IN33
     C                   callp     updhtmlvar('ATEXT':ISFAVA)
     C                   callp     updhtmlvar('AVFARGE':'')
     C                   WHEN      AFARGE = *BLANKS
     C                   callp     updhtmlvar('ATEXT':ATEXT)
     C                   callp     updhtmlvar('AVFARGE':'')
     C                   OTHER
     C                   callp     updhtmlvar('ATEXT':ATEXT)
     C                   EVAL      XFARGE = 'BGCOLOR="#'
     C                   CAT       AFARGE:0      XFARGE           30
     C                   CAT       '"':0         XFARGE
     C                   callp     updhtmlvar('AVFARGE':XFARGE)
     C                   ENDSL
     C     ISFANS        CHAIN     ISOKDP                             33
     C                   SELECT
     C                   WHEN      *IN33
     C                   callp     updhtmlvar('PTEXT':ISFANS)
     C                   callp     updhtmlvar('ANFARGE':'')
     C                   WHEN      PFARGE = *BLANKS
     C                   callp     updhtmlvar('PTEXT':PTEXT)
     C                   callp     updhtmlvar('ANFARGE':'')
     C                   OTHER
     C                   callp     updhtmlvar('PTEXT':PTEXT)
     C                   EVAL      XFARGE = 'BGCOLOR="#'
     C                   CAT       PFARGE:0      XFARGE           30
     C                   CAT       '"':0         XFARGE
     C                   callp     updhtmlvar('ANFARGE':XFARGE)
     C                   ENDSL
     C                   IF        ISFMDA > 20000000
     C                   callp     updhtmlvar('ISFMDA':
     C                             %trim(%editw(ISFMDA:'    .  .  ')))
     C                   ELSE
     C                   callp     updhtmlvar('ISFMDA':'')
     C                   END
     C                   callp     updhtmlvar('ISFMKL':
     C                             %trim(%editw(ISFMKL:'  :  ')))
     C                   IF        ISFLDA > 20000000
     C                   callp     updhtmlvar('ISFLDA':
     C                             %trim(%editw(ISFLDA:'    .  .  ')))
     C                   ELSE
     C                   callp     updhtmlvar('ISFLDA':'')
     C                   END
     C                   callp     updhtmlvar('ISFLKL':
     C                             %trim(%editw(ISFLKL:'  :  ')))
     C                   callp     updhtmlvar('ISST':ISST)
     C     ISST          CHAIN     ISOKDS                             33
     C                   IF        *IN33
     C                   callp     updhtmlvar('STEXT':'')
     C                   ELSE
     C                   callp     updhtmlvar('STEXT':STEXT)
     C                   END
     C                   callp     updhtmlvar('ISFAV1':ISFAV1)
     C                   callp     updhtmlvar('ISFAV2':ISFAV2)
     C                   callp     updhtmlvar('ISFAV3':ISFAV3)
     C                   callp     updhtmlvar('ISFKO1':ISFKO1)
     C                   callp     updhtmlvar('ISFKO2':ISFKO2)
     C                   callp     updhtmlvar('ISFKO3':ISFKO3)
     C                   callp     updhtmlvar('ISFKO4':ISFKO4)
     C                   IF        ISFGDA > 20000000
     C                   callp     updhtmlvar('ISFGDA':
     C                             %trim(%editw(ISFGDA:'    .  .  ')))
     C                   ELSE
     C                   callp     updhtmlvar('ISFGDA':'')
     C                   END
     C                   callp     updhtmlvar('ISFGSI':ISFGSI)
     C                   IF        ISFFDA > 20000000
     C                   callp     updhtmlvar('ISFFDA':
     C                             %trim(%editw(ISFFDA:'    .  .  ')))
     C                   ELSE
     C                   callp     updhtmlvar('ISFFDA':'')
     C                   END
     C                   callp     updhtmlvar('ISFFSI':ISFFSI)
     C                   IF        ISFUDA > 20000000
     C                   callp     updhtmlvar('ISFUDA':
     C                             %trim(%editw(ISFUDA:'    .  .  ')))
     C                   ELSE
     C                   callp     updhtmlvar('ISFUDA':'')
     C                   END
     C                   callp     updhtmlvar('ISFUSI':ISFUSI)
     C                   callp     updhtmlvar('ISVAL':ISVAL)
     C                   callp     updhtmlvar('ISKRAU':
     C                             %trim(%editc(ISKRAU:'4')))
     C                   callp     updhtmlvar('ISKRAV':
     C                             %trim(%editc(ISKRAV:'L')))
     C                   callp     updhtmlvar('ISFORV':
     C                             %trim(%editc(ISFORV:'L')))
     C                   callp     updhtmlvar('ISFKOS':
     C                             %trim(%editc(ISFKOS:'L')))
     C                   callp     updhtmlvar('ISINT':
     C                             %trim(%editc(ISINT:'L')))
     C                   EVAL      WSRES = 0 - ISFKOS + ISINT
     C                   callp     updhtmlvar('WSRES':
     C                             %trim(%editc(WSRES:'L')))
     C                   callp     updhtmlvar('ISFSKL':
     C                             %trim(%editc(ISFSKL:'Z')))
     C                   callp     updhtmlvar('ISFSKG':
     C                             %trim(%editc(ISFSKG:'Z')))
     C                   callp     updhtmlvar('ISFSLM':
     C                             %trim(%editc(ISFSLM:'4')))
     C                   callp     updhtmlvar('ISFSM3':
     C                             %trim(%editc(ISFSM3:'4')))
     C                   callp     updhtmlvar('ISSDT':
     C                             %trim(%editw(ISSDT:'    .  .  ')))
     C                   callp     updhtmlvar('ISFRDA':
     C                             %trim(%editw(ISFRDA:'    .  .  ')))
     C                   callp     updhtmlvar('ISFRTM':
     C                             %trim(%editw(ISFRTM:'  :  :  ')))
     C                   callp     updhtmlvar('ISUSRR':ISUSRR)
     C                   callp     updhtmlvar('ISUSRS':ISUSRS)
     C                   callp     updhtmlvar('ISFSDA':
     C                             %trim(%editw(ISFSDA:'    .  .  ')))
     C                   callp     updhtmlvar('ISFSTM':
     C                             %trim(%editw(ISFSTM:'  :  :  ')))
     C                   callp     updhtmlvar('ISFSSI':ISFSSI)
      *
     C     KOFASTK5      CHAIN     KOFAST                             33
     C                   IF        *IN33
     C                   EVAL      WSLEDET5 = 'Parter'
     C                   ELSE
     C                   EVAL      WSLEDET5 = KFTXT
     C                   END
     C                   callp     updHTMLvar('WSLEDET5':WSLEDET5)
     C     KOFASTK6      CHAIN     KOFAST                             33
     C                   IF        *IN33
     C                   EVAL      WSLEDET6 = 'Ansvarlig'
     C                   ELSE
     C                   EVAL      WSLEDET6 = KFTXT
     C                   END
     C                   callp     updHTMLvar('WSLEDET6':WSLEDET6)
      *
     C                   endsr
      *
      *=====================================================================******
      *  Set output variables for section /$endhtml
      *=====================================================================******
     C     SetEnd        begsr
      * Server protocol
     C                   callp     updhtmlvar('PROTOCOL':S_Protocol)
      * Compute run time
     C                   time                    timedata2
     C     timedata2     subdur    timedata1     ms:*ms
     C                   eval      sec = ms / 1000000
     C                   callp     updhtmlvar('runtime':
     C                             %trim(%editw(sec:'     0 .   ')))
      *
     C                   endsr
      *
      *=====================================================================******
      * Set html output skeleton member before its read into memory
      *=====================================================================******
     C     SetSkl        begsr
      *------------------
      * Set html output skeleton member
     C                   eval      lng = uppify(lng)
      * "HTMLSRC  " er dft = NORSK, ved å trykke på FLAGGET i inng.
      * kan dette endre stil "HTMLSRCXX" avh. av støttede språk
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC' + lng
     C                   eval      Mbr = 'EISO02'
     C*                  CAT       XSPRÅK:0      MBR
      *
     C                   endsr
      *=====================================================================******
      *  Retrieve environment variables
      *=====================================================================******
     C     RtvEnvVar     begsr
      * Use getenvp to get this server's protocol and name
     C                   eval      S_Protocol =getenv('SERVER_PROTOCOL':
     C                             qusec)
     C                   eval      S_Name     =getenv('SERVER_NAME':
     C                             qusec)
      *
     C                   endsr
      *=====================================================================
      *  HENT DOKUMENTER
      *=====================================================================
     C     HENTDOK       BEGSR
     C                   MOVE      XSAKSNR       ARUNDE
      *
     C     ARKIVL06K     SETLL     ARKIVL06
     C     ARKIVL06K     READE     ARKIVL06                               33
     C                   MOVE      *BLANKS       UPDF
     C                   MOVE      *ZEROS        XN                3 0
     C                   DOW       *IN33 = *OFF
     C                   ADD       1             XN
     C                   EVAL      UPDF(XN) = '<a href="'
     C                   CAT       XBANE:0       UPDF(XN)
     C                   CAT       ARLINK:0      UPDF(XN)
     C                   CAT       '" target="':0UPDF(XN)
     C                   CAT       'SyPDFWind': 0UPDF(XN)
     C                   CAT       '">':0        UPDF(XN)
     C                   IF        ARRFK = *BLANKS
     C                   CAT       'DOC':0       UPDF(XN)
     C                   MOVE      XN            XAN               3
     C                   CAT       XAN:0         UPDF(XN)
     C                   ELSE
     C                   CAT       ARRFK:0       UPDF(XN)
     C                   END
     C                   CAT       '<img src="':0UPDF(XN)
     C                   CAT       '/syspeddev':0UPDF(XN)
     C                   CAT       '/PDF.gif"':0 UPDF(XN)
     C                   CAT       'ALT="':1     UPDF(XN)
     C                   CAT       'DOC':0       UPDF(XN)
     C                   CAT       XAN:0         UPDF(XN)
     C                   CAT       '"></a>':0    UPDF(XN)
     C     ARKIVL06K     READE     ARKIVL06                               33
     C                   ENDDO
      *
     C                   callp     updHTMLvar('UPDF1':UPDF1)
     C                   callp     updHTMLvar('UPDF2':UPDF2)
     C                   callp     updHTMLvar('UPDF3':UPDF3)
     C                   callp     updHTMLvar('UPDF4':UPDF4)
     C                   callp     updHTMLvar('UPDF5':UPDF5)
     C                   callp     updHTMLvar('UPDF6':UPDF6)
     C                   callp     updHTMLvar('UPDF7':UPDF7)
     C                   callp     updHTMLvar('UPDF8':UPDF8)
     C                   callp     updHTMLvar('UPDF9':UPDF9)
     C                   callp     updHTMLvar('UPDF10':UPDF10)
     C                   callp     updHTMLvar('UPDF11':UPDF11)
     C                   callp     updHTMLvar('UPDF12':UPDF12)
     C                   callp     updHTMLvar('UPDF13':UPDF13)
     C                   callp     updHTMLvar('UPDF14':UPDF14)
     C                   callp     updHTMLvar('UPDF15':UPDF15)
     C                   callp     updHTMLvar('UPDF16':UPDF16)
     C                   callp     updHTMLvar('UPDF17':UPDF17)
     C                   callp     updHTMLvar('UPDF18':UPDF18)
     C                   callp     updHTMLvar('UPDF19':UPDF19)
     C                   callp     updHTMLvar('UPDF20':UPDF20)
N02  C                   callp     updHTMLvar('UPDF21':UPDF21)
N02  C                   callp     updHTMLvar('UPDF22':UPDF22)
N02  C                   callp     updHTMLvar('UPDF23':UPDF23)
N02  C                   callp     updHTMLvar('UPDF24':UPDF24)
N02  C                   callp     updHTMLvar('UPDF25':UPDF25)
N02  C                   callp     updHTMLvar('UPDF26':UPDF26)
N02  C                   callp     updHTMLvar('UPDF27':UPDF27)
N02  C                   callp     updHTMLvar('UPDF28':UPDF28)
N02  C                   callp     updHTMLvar('UPDF29':UPDF29)
N02  C                   callp     updHTMLvar('UPDF30':UPDF30)
N02  C                   callp     updHTMLvar('UPDF31':UPDF31)
N02  C                   callp     updHTMLvar('UPDF32':UPDF32)
N02  C                   callp     updHTMLvar('UPDF33':UPDF33)
N02  C                   callp     updHTMLvar('UPDF34':UPDF34)
N02  C                   callp     updHTMLvar('UPDF35':UPDF35)
N02  C                   callp     updHTMLvar('UPDF36':UPDF36)
N02  C                   callp     updHTMLvar('UPDF37':UPDF37)
N02  C                   callp     updHTMLvar('UPDF38':UPDF38)
N02  C                   callp     updHTMLvar('UPDF39':UPDF39)
N02  C                   callp     updHTMLvar('UPDF40':UPDF40)
N02  C                   callp     updHTMLvar('UPDF41':UPDF41)
N02  C                   callp     updHTMLvar('UPDF42':UPDF42)
N02  C                   callp     updHTMLvar('UPDF43':UPDF43)
N02  C                   callp     updHTMLvar('UPDF44':UPDF44)
N02  C                   callp     updHTMLvar('UPDF45':UPDF45)
N02  C                   callp     updHTMLvar('UPDF46':UPDF46)
N02  C                   callp     updHTMLvar('UPDF47':UPDF47)
N02  C                   callp     updHTMLvar('UPDF48':UPDF48)
N02  C                   callp     updHTMLvar('UPDF49':UPDF49)
N02  C                   callp     updHTMLvar('UPDF50':UPDF50)
      *
     C                   ENDSR
      *
      *=====================================================================******
      * TRACK & TRACE
      *=====================================================================******
     C     SRTT          BEGSR
      *
     C     XSAKSNR       SETLL     ISOLOGL1
     C     XSAKSNR       READE     ISOLOGL1                               33
     C                   callp     wrtsection('tabTTstr')
     C                   DOW       *IN33 = *OFF
     C                   callp     updhtmlvar('ILDTL':
     C                             %trim(%editw(ILDTL:'    .  .  ')))
     C                   callp     updhtmlvar('ILTML':
     C                             %trim(%editw(ILTML:'  :  :  ')))
     C*                  callp     updhtmlvar('ILUSR':ILUSR)
     C                   callp     updhtmlvar('ILUSR1':ILUSR1)
     C                   callp     updhtmlvar('ILST2':ILST2)
     C                   callp     updhtmlvar('ILUSR2':ILUSR2)
     C                   callp     updhtmlvar('ILTXT':ILTXT)
      * odde/partalls-linjer i alternativ skyggelegging/bakgrunnsfarge
     C     OddEven       IFEQ      ODD
     C                   eval      OddEven = EVEN
     C                   else
     C                   eval      OddEven = ODD
     C                   end
     C                   callp     updHTMLvar('ODDEVEN':OddEven)
      *
     C                   callp     wrtsection('tabTTrow')
     C     XSAKSNR       READE     ISOLOGL1                               33
     C                   ENDDO
     C                   callp     wrtsection('tabTTend')
      *
     C                   ENDSR
      *
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
     C                   OPEN      BRIDF
     C     WSUSER        CHAIN(N)  BRIDF                              50
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
      *
     C                   EXSR      TSTUSER
      *
OddEv * hent Parametre som går igjen i mange prog:
OddEv *      Odd/Even/Rowhead-farger,Logobilde,Språk,Intern/Ekstern bruker,  mm
OddEvc                   call      'ESGETPAR'
OddEvc                   parm                    BIBRID
OddEvc                   parm                    BIFIRM
OddEvc                   parm                    BIKUNR
OddEvc                   parm                    BIKNG
OddEvc                   parm                    RowHead          10
OddEvc                   parm                    Odd              10
OddEvc                   parm                    Even             10
OddEvc                   parm                    LogoImg          50
OddEvc                   parm                    XSPRÅK            2
OddEvc                   parm                    XINTERN           1
OddEvc                   parm                    ParmDS1        1024
OddEvc                   parm                    ParmDS2        1024
OddEvc                   parm                    ParmDS3        1024
      *
     C                   open      BRPARF                               50
     C                   open      ISOFL8                               50
     C                   open      ISOLFL1                              50
     C                   open      ISOLOGL1                             50
     C                   open      ISOKDA                               50
     C                   open      ISOKDF                               50
     C                   open      ISOKDP                               50
     C                   open      ISOKDR                               50
     C                   open      ISOKDS                               50
     C                   open      ISOKDT                               50
     C                   OPEN      KOFAST                               50
     C                   OPEN      ARKIVL06                             50
     C                   OPEN      CUNDL01                              50
     C                   OPEN      TRAN                                 50
      *
     C     KOFASTK1      klist
     C                   kfld                    KFTYP
     C                   kfld                    X1KFKOD           5
     C     KOFASTK2      klist
     C                   kfld                    KFTYP
     C                   kfld                    X2KFKOD           5
     C     KOFASTK3      klist
     C                   kfld                    KFTYP
     C                   kfld                    X3KFKOD           5
     C     KOFASTK4      klist
     C                   kfld                    KFTYP
     C                   kfld                    X4KFKOD           5
     C     KOFASTK5      klist
     C                   kfld                    KFTYP
     C                   kfld                    X5KFKOD           5
     C     KOFASTK6      klist
     C                   kfld                    KFTYP
     C                   kfld                    X6KFKOD           5
     C     ISOKDAK1      klist
     C                   kfld                    ISFPFO
     C                   kfld                    ISFAVA
     C     ISOKDAK2      klist
     C                   kfld                    X_ISFPFO          5
     C                   kfld                    ISFAVA
     C     ARKIVL06K     klist
     C                   kfld                    BIFIRM
     C                   kfld                    ARTYPE
     C                   kfld                    ARUNDE
     C     CUNDL01K      klist
     C                   kfld                    BIFIRM
     C                   kfld                    ISKNA
     C     TRANK         klist
     C                   kfld                    BIFIRM
     C                   kfld                    ISFTRF
     C     BRPARFK       klist
     C                   kfld                    PABRID
     C                   kfld                    PAKUNR
     C                   kfld                    PAID
      *
     C                   EVAL      KFTYP   = 'AVLETEKST'
     C                   EVAL      X1KFKOD = 'ISTYP'
     C                   EVAL      X2KFKOD = 'ISREK'
     C                   EVAL      X3KFKOD = 'ISAVA'
     C                   EVAL      X4KFKOD = 'ISANS'
     C                   EVAL      X5KFKOD = 'ISPAR'
     C                   EVAL      X6KFKOD = 'ISANS'
     C                   EVAL      ARTYPE  = 'ZO'
     C                   EVAL      ARUNDE  = 'ISO'
     C                   EVAL      PABRID = WSUSER
     C                   MOVEL     'SPRÅK'       PAID
     C     BRPARFK       chain     BRPARF                             33
     C                   IF        (*IN33 OR (PAVRDA <> 'EN'))
     C                   MOVE      *BLANKS       XSPRÅK            2
     C                   ELSE
     C                   MOVE      'EN'          XSPRÅK            2
     C                   END
      *
     C                   MOVE      'OLBAN'       PAID
     C     BRPARFK       chain     BRPARF                             33
     C  N33              MOVEL     PAVRDA        XBANE            50
      *
     C                   endsr
      *
      *////////////////////////////////////////////////////////////
     C     TSTUSER       BEGSR
      *////////////////////////////////////////////////////////////
      /copy syspeds/qrpgsrcpy,tstuser
      *
     C                   endsr
      *
      *=====================================================================******
      *  AVSLUTT
      *=====================================================================******
     C     EXIT          begsr
     C                   close     BRPARF
     C                   close     BRIDF
     C                   close     ISOFL8
     C                   close     ISOLFL1
     C                   close     ISOLOGL1
     C                   close     ISOKDA
     C                   close     ISOKDF
     C                   close     ISOKDP
     C                   close     ISOKDR
     C                   close     ISOKDS
     C                   close     ISOKDT
     C                   close     KOFAST
     C                   close     ARKIVL06
     C                   close     CUNDL01
     C                   close     TRAN
     C                   eval      *inlr = *on
      *
     C                   endsr
      *
