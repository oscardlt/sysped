      *********************************************************************
      *  After compiling this RPG MODULE,
      *  create the related program with the following command:
      *
CRTPG+*  CRTPGM SYSPED/STSW04RMD
CRTPG+*         MODULE(*LIBL/STSW04RMD *LIBL/INCGI)
CRTPGM*         ACTGRP(*NEW) AUT(*USE)
      *
      *********************************************************************
      *
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
      *--------------------------------------------------------------------
      * Data base files
      *--------------------------------------------------------------------
     FBRIDF     if   e           k disk    usropn
     FTRACKF    O  A E           K DISK    Usropn
      *--------------------------------------------------------------------
      * Prototype defintions and standard system API error structure
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      *
      *Array over adresser en vil sende mankomail til
     D MAIL            S             70    DIM(5)
      *--------------------------------------------------------------------
      * Variables common to CGI programs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
      * Variables received from the input string
     DAVD              s              4
     DwsAVD            s              4  0
     DOPD              s              7
     DwsOPD            s              7  0
     DTUR              s              8
     DwsPro            s              8  0
     DUser             s             10
     D VERSJ           s             10
     D Fn              s             10
     D Lib             s             10
     D Mbr             s             10
     D FeilTEXT        s            250
      *
     D                 DS
     D  XDT                    1      8  0
     D  XDTÅÅ                  1      4  0
     D  XDTMM                  5      6  0
     D  XDTDD                  7      8  0
     D                 DS
     D  XTIME                  1     14  0
     D  XTID                   1      4  0
     D  XDD                    7      8  0
     D  XMM                    9     10  0
     D  XÅÅ                   11     14  0
     D Spaces          s             12a   inz('&nbsp;&nbsp;')
      *--------------------------------------------------------------------
      * Prolog common to CGI programs
      * -Receive input from the remote browser
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prolog3
      *=====================================================================******
      * MAIN PROCESS LINE
      *=====================================================================******
      * Parse input string
     C                   EXSR      PARSE
      ***
      * Set output skeleton html member name
     C                   exsr      SetSkl
      * Read skeleton output html, etc.
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
      * User changed?? - Set libl ..
     C                   exsr      Init
      *------------------
      * Write sections of HTML.
      *
      * Her er HOVED logikk-rutine
     C                   exsr      SetTop
      *
     C                   callp     wrtsection('all')
      *
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      *
      *
     C                   close     BRIDF
     C                   close     trackf
     C                   eval      *inlr = *on
     C                   RETURN
      *
      *=====================================================================******
      * Parse input
      *=====================================================================******
     C     Parse         begsr
     C                   eval      USER     = zhbgetvar('USER')
     C                   eval      AVD      = zhbgetvar('AVD')
     C                   eval      WSAVD    = c2n2(AVD)
     C                   eval      OPD      = zhbgetvar('OPD')
     C                   eval      WSOPD    = c2n2(OPD)
     C                   eval      TUR      = zhbgetvar('TUR')
     C                   eval      WSPRO    = c2n2(TUR)
     C                   eval      VERSJ = zhbgetvar('V')
      *
     C                   endsr
      *=====================================================================
      * EXECUTE LOGIC / Set html output variables for section /$top
      *=====================================================================
     C     SetTop        begsr
      * antar i utgangspunktet at det går bra
     C                   eval      FeilTEXT ='##OK##'
      *
      * ulike feilsituajoner
     c     SlettOK       ifne      'J'
     C                   eval      FeilTEXT ='Du har ikke autorisasjon '
     c                             + 'til å fjerne oppdrag fra tur.'
     c                   goto      UtTop
     c                   endif
      * Fjern fra tur
     c                   MOVE      WSPRO         WSPROA            8
     C                   Call      'SYGE21B'
     C                   PARM                    WSPRO
     C                   PARM                    WSAVD
     C                   PARM                    WSOPD
     C                   PARM                    UFLAGG            1
     c     Uflagg        ifeq      '1'
     C                   eval      FeilTEXT ='Oppdrag kan ikke fjernes '
     C                                      +'(Pga Stautus e.l/kontakt megler)'
     c                   goto      UtTop
     c                   endif
      *
      *
      * Oppdrag vellykket avvist - loggfør
     c                   exsr      LoggAvv
      *
     C     UtTop         tag
     C                   callp     updhtmlvar('FEILTEXT':FEILTEXT)
      *
     C                   endsr
      *
      *
      *=====================================================================******
      *  Different User than last invocation
      *=====================================================================******
     C     Init          begsr
     C                   open      BRIDF
     C     User          CHAIN     BRIDF                              30
      *
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C* (bedre ressursmessig). MODUL må da være med comp. av dette pgm!!
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
      *
     C                   open      Trackf
      *
      *
     C                   move      'J'           SlettOK           1
     C*                  move      'N'           SlettOK           1
      *
     C                   endsr
      *
      *=====================================================================******
      * Set html output skeleton member before its read into memory
      *=====================================================================******
     C     SetSkl        begsr
      *------------------
      * Set html output skeleton member
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC'
     C                   eval      Mbr = 'STSW04DT'
     C                   endsr
     C***********************************************
     C     LoggAvv       BEGSR
     C***********************************************
     C                   MOVE      WSAVD         TTAVD
     C                   MOVE      WSOPD         TTOPD
     C                   MOVE      *ZEROS        TTFBNR
     C                   MOVE      'NEI'         TTACTI
     C                   eval      TTTEXT = 'Rejected in TKTERM.'+
     C                             ' Rmvd from ' + WSPROA
     C                   eval      TTTEXL = 'Avvist i TKTERM.'+
     C                             ' Fjernet fra ' + WSPROA
     C                   eval      TTDEPO  = 'T:'+ WSPROA
     C                   CALL      'SYGE15C'
     C                   PARM                    WDATO             8
     C                   PARM                    WTID              6
     C                   MOVE      WDATO         TTDATL
     C                   MOVE      WTID          TTTIML
     C                   MOVE      WDATO         TTDATE
     C                   MOVE      WTID          TTTIME
     C                   MOVEL     BIBRID        TTNAME
     c                   move      'S'           TTMANU
     C                   WRITE     TRACKFR
     C                   ENDSR
