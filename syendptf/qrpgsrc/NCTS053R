      ************************************************************
      *** PROGRAM SKAL VEDLIKEHOLDE DEFAULT VERDI NCTS-ENTRY   ***
      ************************************************************
     FFIRM      IF   E           K DISK
     FCUNDL01   IF   E           K DISK
     FKODTA     IF   E           K DISK
     FKODTS2    IF   E           K DISK
     FEDII      IF   E           K DISK
     FNCTSISTD  UF A E           K DISK
     FTRKODL01  IF   E           K DISK
     FNCTS053D  CF   E             WORKSTN
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D                 DS
     D  WSDTRD                 1      2  0
     D  WSDTRM                 3      4  0
     D  WSDTRA                 5      6  0
     D  WSDTR                  1      6  0
     D                 DS
     D  TIDTRC                 1      2  0
     D  TIDTRA                 3      4  0
     D  TIDTRM                 5      6  0
     D  TIDTRD                 7      8  0
     D  TIDTR                  1      8  0
     D                 DS
     D  WSDTAD                 1      2  0
     D  WSDTAM                 3      4  0
     D  WSDTAA                 5      6  0
     D  WSDTA                  1      6  0
     D                 DS
     D  TIDTAC                 1      2  0
     D  TIDTAA                 3      4  0
     D  TIDTAM                 5      6  0
     D  TIDTAD                 7      8  0
     D  TIDTA                  1      8  0
     D                 DS
     D  WSTMA                  1      4  0
     D  WSTMAH                 1      2  0
     D  WSTMAM                 3      4  0
     D                 DS
     D  TITMA                  1      6  0
     D  TITMAH                 1      2  0
     D  TITMAM                 3      4  0
     D  TITMAS                 5      6  0
     D                 DS
     D  SYRG                   1     14
     D  SYFOR9                 1      9
     D  SYFORE                 1     11
      *
     C                   EXSR      INIT
     C                   IF        MODE = 'E'
     C                   MOVE      UAVD          WSAAVD
     C                   GOTO      STARTB
     C                   END
      *
     C     START         TAG
     C                   EXFMT     NC053D01
     C   03              GOTO      SLUTT
     C     STARTB        TAG
     C     KODTAK        CHAIN     KODTA                              99
     C   99              GOTO      START
     C                   EXSR      HENT
      *
     C     START2        TAG
     C                   EXFMT     NC053D02
     C                   MOVEA     '000'         *IN(97)
     C                   MOVE      '0'           *IN30
      *
     C   03              GOTO      SLUTT
     C   12              GOTO      SLUTT
     C                   EXSR      TESTB2
     C   30              GOTO      START2
      *
     C                   Z-ADD     3             LINNBR
     C                   Z-ADD     13            POSNBR
     C     START3        TAG
     C                   EXFMT     NC053D03
     C                   MOVEA     '000000000000'*IN(88)
     C                   MOVEA     '0000'        *IN(84)
     C                   MOVE      '0'           *IN30
      *
     C   03              GOTO      SLUTT
     C   04              DO
     C                   EXSR      SOKDTA
     C                   GOTO      START3
     C                   END
     C   12              GOTO      START2
      *
     C                   EXSR      TESTB3
     C   30              GOTO      START3
     C                   EXSR      OPDTRS
      *
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
      *
      ***********************************************
     C     HENT          BEGSR
      ***********************************************
     C     WSAAVD        CHAIN(N)  NCTSISTD                           60
     C     *IN60         IFEQ      '1'
     C                   EXSR      MOVEB
     C                   ELSE
     C                   EXSR      MOVEW
     C                   END
      *
     C                   ENDSR
      *
      ***********************************************
     C     TESTB2        BEGSR
      ***********************************************
     C     WSTDN         COMP      0                                    9999
     C   99
     CANN30              MOVE      'SPT0023'     MSGNR
     C   99              SETON                                        30
      *
     C     W0004         CHAIN     EDII                               51
     C     *IN51         IFEQ      '1'
     C                   MOVE      '1'           *IN30
     C                   MOVE      '1'           *IN98
     C                   MOVE      'SPN0001'     MSGNR
     C                   ENDIF
      *
     C     W0010         CHAIN     EDII                               51
     C     *IN51         IFEQ      '1'
     C                   MOVE      '1'           *IN30
     C                   MOVE      '1'           *IN97
     C                   MOVE      'SPN0001'     MSGNR
     C                   ENDIF
      *
     C                   ENDSR
      *
      ***********************************************
     C     TESTB3        BEGSR
      ***********************************************
     C     WSATYP        IFNE      *BLANKS
     C                   MOVE      '236'         YUNIK
     C                   MOVE      *BLANKS       YKODE
     C                   MOVEL     WSATYP        YKODE
     C     TRK01K        CHAIN     TRKODL01                           99
     C   99
     CANN30              MOVE      'YBC2221'     MSGNR
     C   99              SETON                                        30
     C                   END
      *
      * TEST PÅ COTROLL TOLLSTED (IND.98)
     C     WSTSBA        IFNE      *BLANKS
     C                   MOVE      '106'         YUNIK
     C                   MOVE      *BLANKS       YKODE
     C                   MOVEL     WSTSBA        YKODE
     C     TRK01K        CHAIN     TRKODL01                           98
     C   98
     CANN30              MOVE      'YBC0811'     MSGNR
     C   98              SETON                                        30
     C                   END
      *
     C     WSKNTA        IFNE      0                                            1<-B
     C                   MOVE      WSKNTA        KUNDNR
     C     CUND1K        CHAIN     CUNDL01                            97
     C     *IN97         IFEQ      '1'                                           2<-B
     C  N30              MOVE      'SPK0001'     MSGNR
     C                   SETON                                        30
     C                   ELSE                                                    2
     C                   IF        WSTINTA = *BLANKS                              3<-B
     C                   SELECT                                                    4<-B
     C                   WHEN      EORI = *BLANKS                                  4
     C                   IF        ((SYFOR9 <> *BLANKS) AND (SYFOR9 <> *ZEROS))     5<-B
     C                   IF        SYLAND = *BLANKS                                  6<-B
     C                   EVAL      WSTINTA = 'NO' + SYFOR9
     C                   ELSE                                                        6
     C                   EVAL      WSTINTA = SYLAND + SYFOR9
     C                   END                                                         6<-E
     C                   END                                                        5<-E
     C                   OTHER                                                     4
     C                   EVAL      WSTINTA = EORI
     C                   ENDSL                                                     4<-E
     C                   END                                                      3<-E
     C                   EVAL      WSNATA = KNAVN
     C                   END                                                     2<-E
     C                   END                                                    1<-E
      *
     C     WSTINTA       IFEQ      *BLANKS
     C                   EVAL      *IN96 = *ON
     C                   EVAL      *IN30 = *ON
     C                   MOVE      'YBC2222'     MSGNR
     C                   END
      *
     C     WSSKTA        IFNE      *BLANKS
     C                   MOVE      '012'         YUNIK
     C                   MOVE      *BLANKS       YKODE
     C                   MOVEL     WSSKTA        YKODE
     C     TRK01K        CHAIN     TRKODL01                           95
     C   95
     CANN30              MOVE      'YBC0810'     MSGNR
     C   95              SETON                                        30
     C                   END
      *
     C     WSLCTY        IFNE      *BLANKS
     C                   MOVE      '347'         YUNIK
     C                   MOVE      *BLANKS       YKODE
     C                   MOVEL     WSLCTY        YKODE
     C     TRK01K        CHAIN     TRKODL01                           94
     C   94
     CANN30              MOVE      'YBC2223'     MSGNR
     C   94              SETON                                        30
     C                   END
      *
     C     WSQID         IFNE      *BLANKS
     C                   MOVE      '326'         YUNIK
     C                   MOVE      *BLANKS       YKODE
     C                   MOVEL     WSQID         YKODE
     C     TRK01K        CHAIN     TRKODL01                           93
     C   93
     CANN30              MOVE      'YBC2224'     MSGNR
     C   93              SETON                                        30
     C                   END
      *
     C     WSSD          IFNE      *BLANKS
     C                   MOVE      '244'         YUNIK
     C                   MOVE      *BLANKS       YKODE
     C                   MOVEL     WSSD          YKODE
     C     TRK01K        CHAIN     TRKODL01                           92
     C   92
     CANN30              MOVE      'YBC2225'     MSGNR
     C   92              SETON                                        30
     C                   END
      *
     C     WSTS          IFNE      *BLANKS
     C                   MOVE      '106'         YUNIK
     C                   MOVE      *BLANKS       YKODE
     C                   MOVEL     WSTS          YKODE
     C     TRK01K        CHAIN     TRKODL01                           91
     C   91
     CANN30              MOVE      'YBC0810'     MSGNR
     C   91              SETON                                        30
     C                   END
      *
     C     WSKNE         IFNE      0                                            1<-B
     C                   MOVE      WSKNE         KUNDNR
     C     CUND1K        CHAIN     CUNDL01                            90
     C     *IN90         IFEQ      '1'                                           2<-B
     C  N30              MOVE      'SPK0001'     MSGNR
     C                   SETON                                        30
     C                   ELSE                                                    2
     C                   IF        WSTINE = *BLANKS                               3<-B
     C                   SELECT                                                    4<-B
     C                   WHEN      EORI = *BLANKS                                  4
     C                   IF        ((SYFOR9 <> *BLANKS) AND (SYFOR9 <> *ZEROS))     5<-B
     C                   IF        SYLAND = *BLANKS                                  6<-B
     C                   EVAL      WSTINE = 'NO' + SYFOR9
     C                   ELSE                                                        6
     C                   EVAL      WSTINE = SYLAND + SYFOR9
     C                   END                                                         6<-E
     C                   END                                                        5<-E
     C                   OTHER                                                     4
     C                   EVAL      WSTINE = EORI
     C                   ENDSL                                                     4<-E
     C                   END                                                      3<-E
      *
     C                   EVAL      WSADE1 = ADR1
     C                   IF        SYPOGE = *BLANKS                               3<-B
     C                   EVAL      WSPNE  = *BLANKS
     C                   MOVEL     POSTNR        WSPNE
     C                   ELSE                                                     3
     C                   EVAL      WSPNE  = SYPOGE
     C                   END                                                      3<-E
     C                   EVAL      WSPSE  = ADR3
     C                   IF        SYLAND <> *BLANKS                              3<-B
     C                   EVAL      WSLKE  = SYLAND
     C                   END                                                      3<-E
     C                   IF        WSCPE = *BLANKS                                3<-B
     C                   EVAL      WSCPE  = KPERS
     C                   END                                                      3<-E
     C                   IF        WSTLE = *BLANKS                                3<-B
     C                   EVAL      WSTLE  = TLF
     C                   END                                                      3<-E
     C                   IF        WSEME = *BLANKS                                3<-B
     C                   EVAL      WSEME  = SYEPOS
     C                   END                                                      3<-E
      * Økonomi operatør
     C     WSTINE        IFEQ      *BLANKS                                        3<-B
     C                   EVAL      *IN89 = *ON
     C                   EVAL      *IN30 = *ON
     C                   MOVE      'YBC2226'     MSGNR
     C                   END                                                      3<-E
      *
     C     WSADE1        IFEQ      *BLANKS                                        3<-B
     C                   EVAL      *IN88 = *ON
     C                   EVAL      *IN30 = *ON
     C                   MOVE      'YBC2227'     MSGNR
     C                   END                                                      3<-E
      *
     C     WSPNE         IFEQ      *BLANKS                                        3<-B
     C                   EVAL      *IN87 = *ON
     C                   EVAL      *IN30 = *ON
     C                   MOVE      'YBC2228'     MSGNR
     C                   END                                                      3<-E
      *
     C     WSPSE         IFEQ      *BLANKS                                        3<-B
     C                   EVAL      *IN86 = *ON
     C                   EVAL      *IN30 = *ON
     C                   MOVE      'YBC2229'     MSGNR
     C                   END                                                      3<-E
      *
     C     WSLKE         IFNE      *BLANKS                                        3<-B
     C                   MOVE      WSLKE         KS2LK
     C     KODS2K        CHAIN     KODTS2                             85
     C   85
     CANN30              MOVE      'SPL0001'     MSGNR
     C   85              SETON                                        30
     C                   END                                                      3<-E
      *
     C     WSLKE2        IFNE      *BLANKS                                        3<-B
     C                   MOVE      WSLKE2        KS2LK
     C     KODS2K        CHAIN     KODTS2                             84
     C   84
     CANN30              MOVE      'SPL0001'     MSGNR
     C   84              SETON                                        30
     C                   END                                                      3<-E
      *
     C                   END                                                     2<-E
     C                   END                                                    1<-E
      *
     C   30              EXSR      SETCUR
      *
     C                   ENDSR
      *
      ***********************************************
     C     SETCUR        BEGSR
      ***********************************************
     C   84              Z-ADD     16            LINNBR
     C   84              Z-ADD     49            POSNBR
     C   85              Z-ADD     15            LINNBR
     C   85              Z-ADD     56            POSNBR
     C   86              Z-ADD     15            LINNBR
     C   86              Z-ADD     31            POSNBR
     C   87              Z-ADD     15            LINNBR
     C   87              Z-ADD     13            POSNBR
     C   88              Z-ADD     14            LINNBR
     C   88              Z-ADD     13            POSNBR
     C   89              Z-ADD     13            LINNBR
     C   89              Z-ADD     22            POSNBR
     C   90              Z-ADD     13            LINNBR
     C   90              Z-ADD     13            POSNBR
     C   91              Z-ADD     12            LINNBR
     C   91              Z-ADD     13            POSNBR
     C   92              Z-ADD     11            LINNBR
     C   92              Z-ADD     23            POSNBR
     C   93              Z-ADD     10            LINNBR
     C   93              Z-ADD     23            POSNBR
     C   94              Z-ADD     10            LINNBR
     C   94              Z-ADD     13            POSNBR
     C   95              Z-ADD     8             LINNBR
     C   95              Z-ADD     77            POSNBR
     C   96              Z-ADD     8             LINNBR
     C   96              Z-ADD     53            POSNBR
     C   97              Z-ADD     8             LINNBR
     C   97              Z-ADD     13            POSNBR
     C   98              Z-ADD     7             LINNBR
     C   98              Z-ADD     13            POSNBR
     C   99              Z-ADD     6             LINNBR
     C   99              Z-ADD     28            POSNBR
      *
     C                   ENDSR
      *
      ***********************************************
     C     MOVEW         BEGSR
      ***********************************************
     C                   MOVE      TITDN         WSTDN
     C                   MOVE      TIDTRD        WSDTRD
     C                   MOVE      TIDTRM        WSDTRM
     C                   MOVE      TIDTRA        WSDTRA
     C                   MOVE      TIDTAD        WSDTAD
     C                   MOVE      TIDTAM        WSDTAM
     C                   MOVE      TIDTAA        WSDTAA
     C                   MOVE      TITMAH        WSTMAH
     C                   MOVE      TITMAM        WSTMAM
     C                   MOVE      TITRNR        WSTRNR
     C                   MOVE      TIGN          WSGN
     C                   MOVE      TISPR         WSSPR
     C                   MOVE      TIIFLG        WSIFLG
     C                   MOVE      TIATYP        WSATYP
     C                   MOVE      TIAREF        WSAREF
     C                   MOVE      TITSBA        WSTSBA
     C                   MOVE      TIKNTA        WSKNTA
     C                   MOVE      TINATA        WSNATA
     C                   MOVE      TITINTA       WSTINTA
     C                   MOVE      TISKTA        WSSKTA
     C                   MOVE      TILCTY        WSLCTY
     C                   MOVE      TIQID         WSQID
     C                   MOVE      TIANR         WSANR
     C                   MOVE      TIAID         WSAID
     C                   MOVE      TISD          WSSD
     C                   MOVE      TITS          WSTS
     C                   MOVE      TIKNE         WSKNE
     C                   MOVE      TITINE        WSTINE
     C                   MOVE      TIADE1        WSADE1
     C                   MOVE      TIPNE         WSPNE
     C                   MOVE      TIPSE         WSPSE
     C                   MOVE      TILKE         WSLKE
     C                   MOVE      TIADE2        WSADE2
     C                   MOVE      TIPNE2        WSPNE2
     C                   MOVE      TILKE2        WSLKE2
     C                   MOVE      TICPE         WSCPE
     C                   MOVE      TITLE         WSTLE
     C                   MOVE      TIEME         WSEME
      *
     C                   MOVE      S0004         W0004
     C                   MOVE      S0010         W0010
     C                   MOVE      S0035         W0035
      *
     C                   ENDSR
      *
      ***********************************************
     C     MOVEB         BEGSR
      ***********************************************
     C                   MOVE      *ZEROS        WSTDN
     C                   MOVE      *ZEROS        WSDTR
     C                   MOVE      *ZEROS        WSDTA
     C                   MOVE      *ZEROS        WSTMA
     C                   MOVE      *BLANKS       WSTRNR
     C                   MOVE      *BLANKS       WSGN
     C                   MOVE      *ZEROS        WSSPR
     C                   MOVE      *ZEROS        WSIFLG
     C                   MOVE      *BLANKS       WSATYP
     C                   MOVE      *BLANKS       WSAREF
     C                   MOVE      *BLANKS       WSTSBA
     C                   MOVE      *ZEROS        WSKNTA
     C                   MOVE      *BLANKS       WSNATA
     C                   MOVE      *BLANKS       WSTINTA
     C                   MOVE      *BLANKS       WSSKTA
     C                   MOVE      *BLANKS       WSLCTY
     C                   MOVE      *BLANKS       WSQID
     C                   MOVE      *BLANKS       WSANR
     C                   MOVE      *BLANKS       WSAID
     C                   MOVE      *BLANKS       WSSD
     C                   MOVE      *BLANKS       WSTS
     C                   MOVE      *ZEROS        WSKNE
     C                   MOVE      *BLANKS       WSTINE
     C                   MOVE      *BLANKS       WSADE1
     C                   MOVE      *BLANKS       WSPNE
     C                   MOVE      *BLANKS       WSPSE
     C                   MOVE      *BLANKS       WSLKE
     C                   MOVE      *BLANKS       WSADE2
     C                   MOVE      *BLANKS       WSPNE2
     C                   MOVE      *BLANKS       WSLKE2
     C                   MOVE      *BLANKS       WSCPE
     C                   MOVE      *BLANKS       WSTLE
     C                   MOVE      *BLANKS       WSEME
      *
     C                   MOVE      *BLANKS       W0004
     C                   MOVE      *BLANKS       W0010
     C                   MOVE      *BLANKS       W0035
      *
     C                   ENDSR
      *
      ***********************************************
     C     OPDTRS        BEGSR
      ***********************************************
     C     WSAAVD        CHAIN     NCTSISTD                           60
     C                   EXSR      MOVETR
     C   60              WRITE     NCTSISTDR
     C  N60              UPDATE    NCTSISTDR
      *
     C                   ENDSR
      *
      ***********************************************
     C     MOVETR        BEGSR
      ***********************************************
     C                   MOVE      WSAAVD        TIAVD
     C                   MOVE      WSTDN         TITDN
     C                   MOVE      UDAY          TIDTRD
     C                   MOVE      UMONTH        TIDTRM
     C                   MOVE      UYEAR         TIDTRA
     C                   MOVE      20            TIDTRC
     C                   MOVE      WSDTAD        TIDTAD
     C                   MOVE      WSDTAM        TIDTAM
     C                   MOVE      WSDTAA        TIDTAA
     C                   MOVE      20            TIDTAC
     C                   MOVE      WSTMAH        TITMAH
     C                   MOVE      WSTMAM        TITMAM
     C                   MOVE      *ZEROS        TITMAS
     C                   MOVE      WSTRNR        TITRNR
     C                   MOVE      WSGN          TIGN
     C                   MOVE      WSSPR         TISPR
     C                   MOVE      WSIFLG        TIIFLG
     C                   MOVE      WSATYP        TIATYP
     C                   MOVE      WSAREF        TIAREF
     C                   MOVE      WSTSBA        TITSBA
     C                   MOVE      WSKNTA        TIKNTA
     C                   MOVE      WSNATA        TINATA
     C                   MOVE      WSTINTA       TITINTA
     C                   MOVE      WSSKTA        TISKTA
     C                   MOVE      WSLCTY        TILCTY
     C                   MOVE      WSQID         TIQID
     C                   MOVE      WSANR         TIANR
     C                   MOVE      WSAID         TIAID
     C                   MOVE      WSSD          TISD
     C                   MOVE      WSTS          TITS
     C                   MOVE      WSKNE         TIKNE
     C                   MOVE      WSTINE        TITINE
     C                   MOVE      WSADE1        TIADE1
     C                   MOVE      WSPNE         TIPNE
     C                   MOVE      WSPSE         TIPSE
     C                   MOVE      WSLKE         TILKE
     C                   MOVE      WSADE2        TIADE2
     C                   MOVE      WSPNE2        TIPNE2
     C                   MOVE      WSLKE2        TILKE2
     C                   MOVE      WSCPE         TICPE
     C                   MOVE      WSTLE         TITLE
     C                   MOVE      WSEME         TIEME
     C                   MOVE      W0004         S0004
     C                   MOVE      W0010         S0010
     C                   MOVE      W0035         S0035
      *
     C                   ENDSR
      *
      ***********************************************
     C     SOKDTA        BEGSR
      ***********************************************
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
      * Forenklet prosedyre
     C     LINNBR        IFEQ      4
     C     POSNBR        ANDGE     13
     C     POSNBR        ANDLE     13
     C                   MOVE      '027'         UUNIK             3
     C                   MOVE      *BLANKS       UKODE            10
     C                   MOVEL     WSSPR         UKODE
     C                   CALL      'TR002R'
     C                   PARM                    UUNIK
     C                   PARM                    UKODE
     C                   PARM                    UTYPE2
     C                   MOVEL     UKODE         WSSPR
     C                   Z-ADD     43            POSNBR
     C                   GOTO      SLSØK
     C                   END
      * Hendelses flagg
     C     LINNBR        IFEQ      4
     C     POSNBR        ANDGE     43
     C     POSNBR        ANDLE     43
     C                   MOVE      '027'         UUNIK             3
     C                   MOVE      *BLANKS       UKODE            10
     C                   MOVEL     WSIFLG        UKODE
     C                   CALL      'TR002R'
     C                   PARM                    UUNIK
     C                   PARM                    UKODE
     C                   PARM                    UTYPE2
     C                   MOVEL     UKODE         WSIFLG
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     28            POSNBR
     C                   GOTO      SLSØK
     C                   END
      * Autorisasjonstype
     C     LINNBR        IFEQ      6
     C     POSNBR        ANDGE     28
     C     POSNBR        ANDLE     31
     C                   MOVE      '236'         UUNIK             3
     C                   MOVE      *BLANKS       UKODE            10
     C                   MOVEL     WSATYP        UKODE
     C                   CALL      'TR002R'
     C                   PARM                    UUNIK
     C                   PARM                    UKODE
     C                   PARM                    UTYPE2
     C                   MOVEL     UKODE         WSATYP
     C                   Z-ADD     38            POSNBR
     C                   GOTO      SLSØK
     C                   END
      * Destinasjons tollsted
     C     LINNBR        IFEQ      7
     C     POSNBR        ANDGE     13
     C     POSNBR        ANDLE     20
     C                   MOVE      '106'         UUNIK             3
     C                   MOVE      *BLANKS       UKODE            10
     C                   MOVEL     WSTSBA        UKODE
     C                   MOVE      'ANK'         UTYPE2            3
     C                   CALL      'TR002R'
     C                   PARM                    UUNIK
     C                   PARM                    UKODE
     C                   PARM                    UTYPE2
     C                   MOVEL     UKODE         WSTSBA
     C                   Z-ADD     8             LINNBR
     C                   Z-ADD     13            POSNBR
     C                   GOTO      SLSØK
     C                   END
      * Trader
     C     LINNBR        IFEQ      8
     C     POSNBR        ANDGE     13
     C     POSNBR        ANDLE     20
     C                   MOVE      *BLANKS       USOKKN           30
     C                   CALL      'SOEKKUN'
     C                   PARM                    WSKNTA
     C                   PARM                    FIFIRM
     C                   PARM                    USOKKN
     C                   Z-ADD     53            POSNBR
     C                   GOTO      SLSØK
     C                   END
      * Språkkode
     C     LINNBR        IFEQ      8
     C     POSNBR        ANDGE     77
     C     POSNBR        ANDLE     78
     C                   MOVE      '012'         UUNIK
     C                   MOVE      *BLANKS       UKODE
     C                   MOVEL     WSSKTA        UKODE
     C                   CALL      'TR002R'
     C                   PARM                    UUNIK
     C                   PARM                    UKODE
     C                   PARM                    UTYPE2
     C                   MOVEL     UKODE         WSSKTA
     C                   Z-ADD     10            LINNBR
     C                   Z-ADD     13            POSNBR
     C                   GOTO      SLSØK
     C                   END
      * Lokasjonstype
     C     LINNBR        IFEQ      10
     C     POSNBR        ANDGE     13
     C     POSNBR        ANDLE     13
     C                   MOVE      '347'         UUNIK
     C                   MOVE      *BLANKS       UKODE
     C                   MOVEL     WSLCTY        UKODE
     C                   CALL      'TR002R'
     C                   PARM                    UUNIK
     C                   PARM                    UKODE
     C                   PARM                    UTYPE2
     C                   MOVEL     UKODE         WSLCTY
     C                   Z-ADD     23            POSNBR
     C                   GOTO      SLSØK
     C                   END
      * Qualifier of identification
     C     LINNBR        IFEQ      10
     C     POSNBR        ANDGE     23
     C     POSNBR        ANDLE     23
     C                   MOVE      '326'         UUNIK
     C                   MOVE      *BLANKS       UKODE
     C                   MOVEL     WSQID         UKODE
     C                   CALL      'TR002R'
     C                   PARM                    UUNIK
     C                   PARM                    UKODE
     C                   PARM                    UTYPE2
     C                   MOVEL     UKODE         WSQID
     C                   Z-ADD     39            POSNBR
     C                   GOTO      SLSØK
     C                   END
      * UN LOCODE
     C     LINNBR        IFEQ      11
     C     POSNBR        ANDGE     23
     C     POSNBR        ANDLE     39
     C                   MOVE      '244'         UUNIK
     C                   MOVE      *BLANKS       UKODE
     C                   MOVEL     WSSD          UKODE
     C                   CALL      'TR002R'
     C                   PARM                    UUNIK
     C                   PARM                    UKODE
     C                   PARM                    UTYPE2
     C                   MOVEL     UKODE         WSSD
     C                   Z-ADD     12            LINNBR
     C                   Z-ADD     13            POSNBR
     C                   GOTO      SLSØK
     C                   END
      * Tollsted
     C     LINNBR        IFEQ      12
     C     POSNBR        ANDGE     13
     C     POSNBR        ANDLE     20
     C                   MOVE      '106'         UUNIK
     C                   MOVE      *BLANKS       UKODE
     C                   MOVEL     WSTS          UKODE
     C                   MOVE      'ANK'         UTYPE2            3
     C                   CALL      'TR002R'
     C                   PARM                    UUNIK
     C                   PARM                    UKODE
     C                   PARM                    UTYPE2
     C                   MOVEL     UKODE         WSTS
     C                   Z-ADD     13            LINNBR
     C                   Z-ADD     13            POSNBR
     C                   GOTO      SLSØK
     C                   END
      * Trader
     C     LINNBR        IFEQ      13
     C     POSNBR        ANDGE     13
     C     POSNBR        ANDLE     20
     C                   MOVE      *BLANKS       USOKKN           30
     C                   CALL      'SOEKKUN'
     C                   PARM                    WSKNE
     C                   PARM                    FIFIRM
     C                   PARM                    USOKKN
     C                   Z-ADD     22            POSNBR
     C                   GOTO      SLSØK
     C                   END
      * Landkode til økonomi operatør
     C     LINNBR        IFEQ      15
     C     POSNBR        ANDGE     56
     C     POSNBR        ANDLE     57
     C                   CALL      'SYFI10L'
     C                   PARM                    WSLKE
     C                   PARM                    USPED
     C                   Z-ADD     16            LINNBR
     C                   Z-ADD     13            POSNBR
     C                   GOTO      SLSØK
     C                   END
      * Landkode til økonomi operatør (postadresse)
     C     LINNBR        IFEQ      16
     C     POSNBR        ANDGE     49
     C     POSNBR        ANDLE     50
     C                   CALL      'SYFI10L'
     C                   PARM                    WSLKE2
     C                   PARM                    USPED
     C                   Z-ADD     17            LINNBR
     C                   Z-ADD     13            POSNBR
     C                   GOTO      SLSØK
     C                   END
      *
     C     SLSØK         ENDSR
      *
      ***********************************************
     C     INIT          BEGSR
      ***********************************************
     C     *ENTRY        PLIST
     C                   PARM                    UAVD              4 0
     C                   PARM                    MODE              1
      *
     C     KODTAK        KLIST
     C                   KFLD                    WSAUNI
     C                   KFLD                    WSAAVD
     C     TRK01K        KLIST
     C                   KFLD                    YUNIK             3
     C                   KFLD                    YKODE            10
     C     CUND1K        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    KUNDNR
     C     KODS2K        KLIST
     C                   KFLD                    KS2UNI
     C                   KFLD                    KS2LK
      *
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVE      WDT           XIDAG             8 0
      *
     C                   MOVE      'A'           WSAUNI            1
     C                   MOVEL     'S2'          KS2UNI
     C                   MOVE      *ZEROS        WSDTR
     C                   MOVE      *ZEROS        TIDTR
     C                   MOVE      *ZEROS        WSDTA
     C                   MOVE      *ZEROS        TIDTA
     C                   MOVE      *ZEROS        WSTMA
     C                   MOVE      *ZEROS        TITMA
     C                   MOVE      'N'           USPED             1
      *
     C     FIFIRM        SETLL     FIRM
     C                   READ      FIRM                                   30
      *
     C                   ENDSR
      *
