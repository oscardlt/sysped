N01   * PRIS=0 GODTAS HVIS BEKREFTET MED F23
N02   * EDTCDE(4) PÅ PRISER I STEDET FOR EDTWRD
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A          R SUBFIL                    SFL
     A            LNAVN         30   H
     A            ADR1          30   H
     A            TFAXNR        15   H
SV5  A*           FEDAY     R        H      REFFLD(FERRR/FEDAY *LIBL/FERR)
SV5  A*           FEMONT    R        H      REFFLD(FERRR/FEMONT *LIBL/FERR)
SV5  A*           FEYEAR    R        H      REFFLD(FERRR/FEYEAR *LIBL/FERR)
NV5  A            FEDATE    R        H      REFFLD(FERRR/FEDATE *LIBL/FERR)
SV5  A*           FEDAY2    R        H      REFFLD(TUFER/FEDAY2 *LIBL/TUFE)
SV5  A*           FEMON2    R        H      REFFLD(TUFER/FEMON2 *LIBL/TUFE)
SV5  A*           FEYEA2    R        H      REFFLD(TUFER/FEYEA2 *LIBL/TUFE)
NV5  A            FEDAT2    R        H      REFFLD(TUFER/FEDAT2 *LIBL/TUFE)
     A            FEBILN    R        H      REFFLD(TUFER/FEBILN *LIBL/TUFE)
     A            WSFIRM    R        H      REFFLD(FERRR/FEFIRM *LIBL/FERR)
     A            WSNR           1A  B  6  2
     A  90                                  DSPATR(ND)
     A  90                                  DSPATR(PR)
     A                                      VALUES(' ' '2' '4' '9')
     A            WSDATE         6Y 0B  6  6EDTCDE(4)
     A            WSTIME         4Y 0B  6 13EDTCDE(4)
     A            FEFROM    R        O  6 18REFFLD(FERRR/FEFROM *LIBL/FERR)
     A            FETO      R        O  6 24REFFLD(FERRR/FETO *LIBL/FERR)
     A            FELENG    R        O  6 30REFFLD(FERRR/FELENG *LIBL/FERR)
     A            FENAME        17A  O  6 34
     A            WSPRI1    R        B  6 52REFFLD(FERRR/FEPRI1 *LIBL/FERR)
S02  A*                                     EDTWRD('           .  ')
N02  A                                      EDTCDE(4)
     A  97                                  DSPATR(ND)
     A            WSPRI2    R        B  6 67REFFLD(FERRR/FEPRI2 *LIBL/FERR)
S02  A*                                     EDTWRD('           .  ')
N02  A                                      EDTCDE(4)
     A  97                                  DSPATR(ND)
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                  1  2'SYSPED/SYTR17'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(045 DIV3786 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                  1 67DATE
     A                                      EDTCDE(Y)
     A            FEAVD          4S 0O  2  2COLOR(WHT)
     A            FETUR          8S 0O  2  7COLOR(WHT)
     A            FERUND         8S 0O  2 16COLOR(WHT)
     A                                  2 67TIME
     A            ZRECNO         4S 0O  2 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  3  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 35MSGCON(040 SHU0508 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(030 DIV3787 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 35MSGCON(040 DIV4016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  2MSGCON(078 DIV3785 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A          R BUNN
     A                                      CF03(03 'EXIT')
     A                                      CF12(03)
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
N01  A                                      CF23(23 'BEKREFTE 0-PRIS')
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  2  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  2 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  2 42MSGCON(018 DIV3788 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  4  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A          R NYWRK
     A                                      CF03(03 'EXIT')
     A                                      CF12(03)
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                  1  2'SYSPED/SYTR17'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(045 DIV3786 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                  1 67DATE
     A                                      EDTCDE(Y)
     A            FEAVD          4S 0O  2  2COLOR(WHT)
     A            FETUR          8S 0O  2  8COLOR(WHT)
     A                                  2 67TIME
     A                                  3  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(030 DIV3787 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  2MSGCON(078 DIV3785 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 42MSGCON(018 DIV3788 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A          R BILDFA
     A                                      CF03(03 'Avslutt')
     A                                      CF04(04 'Avslutt')
     A                                      CF10(10 'send ønsket fax')
     A                                      WINDOW(*DFT 9 75 *NOMSGLIN)
     A            MSGNR          7A  P
     A                                  2  3MSGCON(025 SHU0166 *LIBL/SYMSGF)
     A            FEFIRM         8Y 0B  2 30EDTCDE(4)
     A                                      DSPATR(UL)
     A            LNAVN         30A  B  2 40
     A  25                                  DSPATR(PR)
     A                                  3  3MSGCON(025 SHU0167 *LIBL/SYMSGF)
     A            TFAXNR        15A  B  3 30
     A  71                                  DSPATR(PC)
     A                                      DSPATR(UL)
     A                                  3 47MSGCON(012 SHU0261 *LIBL/SYMSGF)
     A            WSLAND         2A  B  3 60
     A                                  3 63'='
     A            FAXRET         6A  O  3 66
     A                                  4  3MSGCON(025 SHU0343 *LIBL/SYMSGF)
     A            WSCVRP         1A  B  4 30
     A                                  5  3MSGCON(025 SHU0173 *LIBL/SYMSGF)
     A            WSTIMF         6Y 0B  5 30DSPATR(UL)
     A                                      EDTCDE(4)
     A                                  6  3MSGCON(025 SHU0174 *LIBL/SYMSGF)
     A            WSDATF         6Y 0B  6 30DSPATR(UL)
     A                                      EDTCDE(4)
     A  96                                  DSPATR(RI)
     A  96                                  DSPATR(PC)
     A                                  7  3MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  7 23MSGCON(018 DIV0032 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  7 43MSGCON(018 SHU0168 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        70A  O  8  3MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
