     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A          R NOERAS
     A                                      ASSUME
     A                                      OVERLAY
     A                                  1  2' '
     A          R WIND01
     A                                      OVERLAY
S02  A*                                     WINDOW(*DFT 18 72)
N02  A                                      WINDOW(*DFT 18 75)
     A          R SUBFIL                    SFL
     A            SFNR           1A  B  8  1VALUES(' ' '5' 'L')
     A            WFMMRK1A      18A  O  8  3
     A            WKOTERM1       3A  O  8 22
     A            WFMITER        1A  O  8 27
     A            WKOINDT4       4Y 0O  8 29EDTCDE(4)
     A            WKOINTI4       4Y 0O  8 34EDTCDE(4)
     A            WFMUTER        1A  O  8 40
     A            WKOUTDT4       4Y 0O  8 42EDTCDE(4)
     A            WKOUTTI4       4Y 0O  8 47EDTCDE(4)
     A            WKOMVKT        7Y 2O  8 53EDTCDE(4)
     A            WKOMM3         7Y 5O  8 62EDTCDE(4)
     A            WKOMLM         4Y 2O  8 71EDTCDE(4)
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0009)
     A                                      SFLPAG(0008)
     A                                      WINDOW(WIND01)
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
N02  A                                      CF05(05 'REFRESH')
     A                                      CF12(12 'tilbake')
     A                                      CF03(12)
     A                                      KEEP
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND(*MORE)
     A            ZRECNO         4S 0H      SFLRCDNBR
     A                                  1  1'SYTR33K'
     A                                      COLOR(BLU)
     A                                  1 22'Vise kolliidentifikasjoner'
     A                                      COLOR(WHT)
     A                                  1 51'_______Totaler_______'
     A                                  2  1'Velg og trykk Enter.'
     A                                      COLOR(BLU)
     A            HEAVD          4Y 0O  2 27EDTCDE(4)
     A                                      COLOR(WHT)
     A            HEOPD          7Y 0O  2 32EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  2 44'Ant'
     A                                  2 53'Vekt'
     A                                  2 63'M3'
     A                                  2 72'LM'
     A                                  3 27'Opprinnelig:'
     A            COGNTB         5Y 0O  3 42EDTCDE(4)
     A            COGVKT         7Y 0O  3 50EDTCDE(4)
     A            COGM3          7Y 3O  3 60EDTCDE(4)
     A            COGLM          4Y 2O  3 71EDTCDE(4)
     A                                  4  2'5=Se CaroScanInfo      '
     A                                      COLOR(BLU)
     A                                  4 27'CS/Term.INN:'
     A            COMNT          5Y 0O  4 42EDTCDE(4)
     A            COMVKT         7Y 2O  4 52EDTCDE(4)
     A            COMM3          7Y 5O  4 62EDTCDE(4)
     A            COMLM          4Y 2O  4 71EDTCDE(4)
     A                                  5 27'På oppdr NÅ:'
     A            HENT           7Y 0O  5 40EDTCDE(4)
     A            HEVKT          9Y 0O  5 48EDTCDE(4)
     A            HEM3           7Y 3O  5 60EDTCDE(4)
     A            HELM           4Y 2O  5 71EDTCDE(4)
     A                                  6 22'__________Terminal___________'
     A                                      COLOR(WHT)
     A                                  7  1'V Kolliidenifikasjon     In ddmm t-
     A                                      tmm Ut ddmm ttmm    Vekt   Vol/M3  -
     A                                        LM  '
     A                                      COLOR(WHT)
     A                                  5  2'L=Se Loggdetaljer      '
     A                                      COLOR(BLU)
     A          R BUNN
     A                                      WINDOW(WIND01)
     A                                      SLNO(17)
     A                                      CLRL(01)
     A                                      OVERLAY
     A                                  1  2'F12=Avbryt'
     A                                      COLOR(BLU)
     A                                  1 29'Linje-sum..:'
     A            XANT           5Y 0O  1 44EDTCDE(4)
     A            XKOMVKT        7Y 2O  1 53EDTCDE(4)
     A            XKOMM3         7Y 5O  1 62EDTCDE(4)
     A            XKOMLM         4Y 2O  1 71EDTCDE(4)
