     H DATEDIT(*YMD)
      ***********************************************************
      *** PROGRAMMET PLUKKER OPPDRAG FRA Z-FILER              ***
      ***********************************************************
      *
     FHEADF     UF A E           K DISK    RENAME(HEADFR:HEADFRNY)
     FHEAD17    IF   E           K DISK    RENAME(HEADFR:HEADFR17)
     F                                     PREFIX(H17_)
     FDISP      UF A E           K DISK
     FFRISOK    UF A E           K DISK
     FFREETXT   UF A E           K DISK
     FDOKUFV    UF A E           K DISK
     FDOKUFVF   UF A E           K DISK
     FTRACKL08  IF A E           K DISK
     FENADSF    UF A E           K DISK
     FFAKT      IF   E           K DISK
     FKODFRI    IF   E           K DISK
     FZEADL01   UF   E           K DISK
     FZRISOL01  IF   E           K DISK    RENAME(FRISOKR:ZRISOKR)
     FZREETXT1  IF   E           K DISK    RENAME(FREETXTR:ZREETXTR)
     FZOKUFV1   IF   E           K DISK    RENAME(DOKUFVR:ZOKUFVR)
     FZOKUFVF1  IF   E           K DISK    RENAME(DOKUFVFR:ZOKUFVFR)
     FZNADSL1   IF   E           K DISK    RENAME(NADSR:ZNADSR)
     FZTRACKL1  IF   E           K DISK    RENAME(TRACKFR:ZTRACKFR)
     FTELL      UF   E           K DISK
     FKODTRI    IF   E           K DISK
     D                SDS
     D  ZUSER                254    263
     D TTXSTR          C                   CONST('Order entered (proc-
     D                                     essed from EDI-data)')
     D TTXCHG          C                   CONST('Order changed by (proc-
     D                                     essed from EDI-data)')
      *______________________
      * Start
      *""""""""""""""""""""""
     C                   EXSR      INIT
      *
     C                   EXSR      TASTOR
      *
     C                   MOVE      '1'           *INLR
     C                   RETURN
      *
      ***********************************************
     C     INIT          BEGSR
      ***********************************************
     C     HEADFK        KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C     HEAD17K       KLIST
     C                   KFLD                    HERFA
     C     DOKUFVK       KLIST
     C                   KFLD                    FVRI
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C                   EVAL      FVRI = 'F'
     C     H17_HEADFK    KLIST
     C                   KFLD                    H17_HEAVD
     C                   KFLD                    H17_HEOPD
      *
     C     ZFILK1        KLIST
     C                   KFLD                    UREFF
     C                   KFLD                    HEUNIK
     C     ZFILK2        KLIST
     C                   KFLD                    HEUNIK
     C                   KFLD                    UREFF
     C     TRACKL08K     KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C                   KFLD                    TTACTI
      *
     C     *ENTRY        PLIST
     C                   PARM                    UREFF
     C                   PARM                    UUNIK             9
     C                   PARM                    UCOLLA           15
      *
     C     *LIKE         DEFINE    HEREFF        UREFF
     C     *LIKE         DEFINE    HEUNIK        PPUNIK
     C                   MOVE      UUNIK         HEUNIK
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVE      WDT           XIDAG             8 0
      *
     C                   ENDSR
      *
      ***********************************************
     C     TASTOR        BEGSR
      ***********************************************
      *
     C     ZFILK1        CHAIN     ZEADL01                            33
     C                   EVAL      HESG = 'XML'
     C                   MOVE      'P'           HEST
     C                   UPDATE    HEADFR
     C                   MOVE      'N'           XNY               1
      *
     C     HEAD17K       CHAIN     HEAD17                             33
     C                   IF        *IN33
     C                   MOVE      'J'           XNY
      * HENTE OPPDRAGSNUMMER
     C     HEAVD         CHAIN     TELL                               33
     C                   MOVE      TEOPDN        HEOPD
     C                   ADD       1             TEOPDN
     C                   UPDATE    TELLR
     C                   MOVE      'U'           HEST
     C                   MOVE      'H'           HEUR
     C                   MOVE      XIDAG         HEDTR
     C                   MOVE      XIDAG         HEDTOP
     C                   ELSE
      * SJEKK OM FAKTURA FINNES. JA -> IKKE OPPDATERE OPPDRAG
     C     H17_HEADFK    SETLL     FAKT
     C     H17_HEADFK    READE     FAKT                                   33
     C                   DOW       *IN33 = *OFF
     C                   IF        FAKDA <> 'C' AND FAKDA <> 'D'
     C                   GOTO      SLTASTOR
     C                   END
     C     H17_HEADFK    READE     FAKT                                   33
     C                   ENDDO
     C     H17_HEADFK    CHAIN     HEADF                              33
     C                   MOVE      UUNIK         HEUNIK
     C     ZFILK1        CHAIN(N)  ZEADL01                            33
     C                   EVAL      HEAVD  = H17_HEAVD
     C                   EVAL      HEOPD  = H17_HEOPD
     C                   EVAL      HEST   = H17_HEST
     C                   EVAL      HEUR   = H17_HEUR
     C                   EVAL      HEDTR  = H17_HEDTR
     C                   EVAL      HEDTOP = H17_HEDTOP
     C                   EVAL      HEPRO  = H17_HEPRO
     C                   EVAL      TRRUND = H17_TRRUND
     C                   END
      *
     C                   IF        TRSLAG = *BLANKS
     C                   MOVE      'HO'          TRSLAG
     C                   END
     C                   EXSR      GETFRT
      *
     C                   IF        UCOLLA = 'Booking'
JOV  C*                  EVAL      HESTN1 = 'B'
JOV  C                   EVAL      HESTN7 = 'P'                                 =PREBOOKING
     C                   ELSE
     C*                  EVAL      HESTN1 = *BLANKS
     C                   EVAL      HESTN7 = *BLANKS
     C                   END
      *
     C                   IF        XNY = 'J'
     C                   WRITE     HEADFRNY
     C                   ELSE
     C                   UPDATE    HEADFRNY
     C                   END
      *
     C     HEADFK        CHAIN     DISP                               33
     C   33              CLEAR                   DISPR
     C   33              MOVE      HEAVD         DIAVD
     C   33              MOVE      HEOPD         DIOPD
     C                   MOVE      HEOT          DIOT
     C                   MOVE      HESDF         DISTEF
     C                   MOVE      HESDT         DISTET
     C                   MOVE      TRSDFD        DIDATF
     C                   MOVE      TRSDTD        DIDATT
     C                   MOVE      TRSDFK        DITIMF
     C                   MOVE      TRSDTK        DITIMT
     C   33              WRITE     DISPR
     C  N33              UPDATE    DISPR
      * FJERN GAMLE FRISØKEVEIER
     C     HEADFK        SETLL     FRISOK
     C     HEADFK        READE     FRISOK                                 33
     C                   DOW       *IN33 = *OFF
     C                   DELETE    FRISOKR
     C     HEADFK        READE     FRISOK                                 33
     C                   ENDDO
      *
      * FRI SØKEVEI "ORD"=ORDRENR FOR SØK FRA INTERNETT
     C                   MOVE      HEAVD         FSAVD
     C                   MOVE      HEOPD         FSOPD
     C                   MOVE      'ORD'         FSKODE
     C                   MOVEL     HERFA         FSSOK
     C                   MOVE      HEDTOP        FSDTOP
     C     FSKODE        CHAIN     KODFRI                             33
     C  N33              MOVE      KFSODK        FSDOKK
     C   33              MOVE      *BLANKS       FSDOKK
     C                   WRITE     FRISOKR
      *
     C                   IF        (((HELIB  = *BLANKS) AND
     C                               (HEFILE <> *BLANKS)) AND
     C                               (HEMBR = *BLANKS))
      * OPPDATER BRUKER-ID TIL DEN SOM REGISTRERTE WEB-OPPDRAG
     C                   MOVE      HEAVD         FSAVD
     C                   MOVE      HEOPD         FSOPD
     C                   MOVE      'WUI'         FSKODE
     C                   EVAL      FSSOK = HEFILE
     C                   MOVE      HEDTOP        FSDTOP
     C                   MOVE      *BLANKS       FSDOKK
     C     FSKODE        CHAIN     KODFRI                             33
     C  N33              MOVE      KFSODK        FSDOKK
     C                   WRITE     FRISOKR
     C                   END
      *
     C                   EXSR      TTSRWEB
      *
      * GENERERER FRAKTBREV
      *
     C                   CALL      'SYOP30R'
     C                   PARM                    HEUNIK
     C                   PARM                    HEREFF
     C                   PARM                    HEAVD
     C                   PARM                    HEOPD
      *
      * FLYTTER FRI SØKEVEI
      *
     C     ZFILK2        SETLL     ZRISOL01
     C     ZFILK2        READE     ZRISOL01                               33
     C                   DOW       *IN33 = *OFF
     C                   IF        ((FSKODE <> 'ORD') OR (HERFA <> FSSOK))
     C                   Z-ADD     HEAVD         FSAVD
     C                   Z-ADD     HEOPD         FSOPD
     C                   MOVE      HEDTOP        FSDTOP
     C     FSKODE        CHAIN     KODFRI                             33
     C  N33              MOVE      KFSODK        FSDOKK
     C                   WRITE     FRISOKR
     C                   END
     C     ZFILK2        READE     ZRISOL01                               33
     C  N33              ENDDO
      *
      * FJERN GAMLE FRITEKSTER
     C     HEADFK        SETLL     FREETXT
     C     HEADFK        READE     FREETXT                                33
     C                   DOW       *IN33 = *OFF
     C                   DELETE    FREETXTR
     C     HEADFK        READE     FREETXT                                33
     C                   ENDDO
      *
     C                   Z-ADD     0             XRTLI             2 0
     C     ZFILK2        SETLL     ZREETXT1
     C     ZFILK2        READE     ZREETXT1                               33
     C                   DOW       *IN33 = *OFF
     C                   MOVE      XIDAG         FRTDT
     C                   Z-ADD     HEAVD         FRTAVD
     C                   Z-ADD     HEOPD         FRTOPD
     C                   ADD       1             XRTLI
     C                   Z-ADD     XRTLI         FRTLI
     C                   WRITE     FREETXTR
      *
     C     ZFILK2        READE     ZREETXT1                               33
     C  N33              ENDDO
      *
      * FJERN GAMLE DOKUFV
     C     DOKUFVK       SETLL     DOKUFV
     C     DOKUFVK       READE     DOKUFV                                 33
     C                   DOW       *IN33 = *OFF
     C                   DELETE    DOKUFVR
     C     DOKUFVK       READE     DOKUFV                                 33
     C                   ENDDO
      *
     C                   Z-ADD     0             XRTLI             2 0
     C     ZFILK2        SETLL     ZOKUFV1
     C     ZFILK2        READE     ZOKUFV1                                33
     C                   DOW       *IN33 = *OFF
     C                   WRITE     DOKUFVR
     C     ZFILK2        READE     ZOKUFV1                                33
     C  N33              ENDDO
      *
      * FJERN GAMLE DOKUFVF
     C     HEADFK        SETLL     DOKUFVF
     C     HEADFK        READE     DOKUFVF                                33
     C                   DOW       *IN33 = *OFF
     C                   DELETE    DOKUFVFR
     C     HEADFK        READE     DOKUFVF                                33
     C                   ENDDO
      *
     C                   Z-ADD     0             XRTLI             2 0
     C     ZFILK2        SETLL     ZOKUFVF1
     C     ZFILK2        READE     ZOKUFVF1                               33
     C                   DOW       *IN33 = *OFF
     C                   WRITE     DOKUFVFR
     C     ZFILK2        READE     ZOKUFVF1                               33
     C  N33              ENDDO
      * FJERN GAMLE ADRESSE
     C     HEADFK        SETLL     ENADSF
     C     HEADFK        READE     ENADSF                                 33
     C                   DOW       *IN33 = *OFF
     C                   DELETE    NADSR
     C     HEADFK        READE     ENADSF                                 33
     C                   ENDDO
      * ADRESSELINJER
     C     ZFILK2        SETLL     ZNADSL1
     C     ZFILK2        READE     ZNADSL1                                33
     C                   DOW       *IN33 = *OFF
     C                   Z-ADD     HEAVD         NAVD
     C                   Z-ADD     HEOPD         NOPD
     C                   WRITE     NADSR
     C     ZFILK2        READE     ZNADSL1                                33
     C  N33              ENDDO
      *
     C     SLTASTOR      ENDSR
      *
      ***********************************************
     C     TTSRWEB       BEGSR
      ***********************************************
     C                   EVAL      TTACTI = 'STR'
     C     TRACKL08K     CHAIN     TRACKL08                           33
     C                   MOVE      HEAVD         TTAVD
     C                   MOVE      HEOPD         TTOPD
     C                   MOVE      WDT           TTDATE
     C                   MOVE      WTM           TTTIME
     C                   MOVE      WDT           TTDATL
     C                   MOVE      WTM           TTTIML
     C                   MOVE      ZUSER         TTUSER
     C                   IF        *IN33
     C                   MOVEL     TTXSTR        TTTEXT
     C                   MOVEL     TTXSTR        TTTEXL
     C                   ELSE
     C                   EVAL      TTACTI = 'CHG'
     C                   EVAL      TTTEXT = TTXCHG
     C                   EVAL      TTTEXL = TTXCHG
     C                   END
     C                   MOVEL     'XML'         TTEDEV
     C                   WRITE     TRACKFR
      *
     C     ZFILK2        SETLL     ZTRACKL1
     C     ZFILK2        READE     ZTRACKL1                               33
     C                   DOW       *IN33 = *OFF
     C                   Z-ADD     HEAVD         TTAVD
     C                   Z-ADD     HEOPD         TTOPD
     C                   WRITE     TRACKFR
     C     ZFILK2        READE     ZTRACKL1                               33
     C  N33              ENDDO
      *
      *
     C                   ENDSR
      *
      ***********************************************
     C     GETFRT        BEGSR
      ***********************************************
      * SR SOM HENTER VERDI FOR FRAKTTILLEGG (OLJE/VAL...)
     C     HEVALT        IFEQ      *BLANKS
     C                   MOVE      *ZEROS        UVALP
     C     HEKNKF        IFNE      *ZEROS
     C     HEKDFK        ANDNE     'X'                                          VED IMPORT..
     C                   MOVE      HEKNKF        UKNOP                          PRIM. KJØPER
     C                   ELSE
     C                   MOVE      HEKNSF        UKNOP                          SEK. SELGER
     C                   END
     C     UKNOP         IFNE      *ZEROS
     C                   CALL      'SYFA24B'
     C                   PARM                    HEUR              1
     C                   PARM      HEAVD         UAVDOP            4 0
     C                   PARM                    UKNOP             8 0
     C                   PARM                    HEVALT
     C                   PARM                    UVALP             3 1
     C                   MOVE      UVALP         HEVALP
     C                   END
     C                   END
      *
     C                   ENDSR
      *
