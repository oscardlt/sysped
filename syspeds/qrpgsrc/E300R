     FE300D     CF   E             WORKSTN
     F                                     SFILE(E300DB:ZRECNO)
     FWRKAGL01  IF   E           K DISK
     FFIRM      IF   E             DISK
     FCUNDL01   IF   E           K DISK
     FECMSG     IF   E           K DISK
     FEDIM5     IF   E           K DISK
     FHEAD14    IF   E           K DISK
     FXMLINHL1  IF A E           K DISK    USROPN
     D                 DS
     D  EXT0N                  1     20
     D  EXT1N                  1      4  0
     D  EXT2N                  5     12  0
     D  EXT3N                 13     20  0
      *_________________
      * DS for constants
      *"""""""""""""""""
     D                 DS
     D  WTXT01                 1     30    INZ('(No records to displ-
     D                                     ay       )')
     D  WTXT02                31     60    INZ('**Deleted**         -
     D                                               ')
     D  WTXT03                61     90    INZ('Agent kan ikke ta em-
     D                                     ot EDIfact')
     D  WTXT04                91    120    INZ('Allerede Sendt (9=BL-
     D                                     ANK Komm.)')
     D  WTXT05               121    150    INZ('Klar til intern data-
     D                                     overføring')
     D  WTXT06               151    180    INZ('Klar til XML-Instr. -
     D                                     overføring')
      *//////////////
      *  Main logic /
      *//////////////
     C                   EXSR      SRA
     C                   EXSR      SRB
     C                   MOVE      '1'           *INLR
      *////////////////////////////////////////////////////////////
      *  INZ                                                  SRA /
      *////////////////////////////////////////////////////////////
     C     SRA           BEGSR
      * INPUT PARAMETERS
     C     *ENTRY        PLIST
     C                   PARM                    WBRUDD            1
      *__________________
      * Define Workfields
      *""""""""""""""""""
     C     *LIKE         DEFINE    ZRECNO        WRECNO
     C     *LIKE         DEFINE    ECSR          WCSR
     C     *LIKE         DEFINE    EC0065        WC0065
     C     *LIKE         DEFINE    EC0065        W20065
     C     *LIKE         DEFINE    EC0065        W30065
      *_____________
      * Define key's
      *"""""""""""""
     C     KEYC          KLIST
     C                   KFLD                    FIRMA
     C                   KFLD                    KUNDNR
     C     ECMKEY        KLIST
     C                   KFLD                    FIRMA
     C                   KFLD                    KUNDNR
     C                   KFLD                    WCSR
     C                   KFLD                    WC0065
     C     ECMK2         KLIST
     C                   KFLD                    FIRMA
     C                   KFLD                    KUNDNR
     C                   KFLD                    WCSR
     C                   KFLD                    W20065
     C     ECMK3         KLIST
     C                   KFLD                    FIRMA
     C                   KFLD                    KUNDNR
     C                   KFLD                    WCSR
     C                   KFLD                    W30065
     C     HEAD14K       KLIST
     C                   KFLD                    WRPRO
     C                   KFLD                    KUNDNR
     C     XMLINHK       KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
      *________________
      * Init workfields
      *""""""""""""""""
     C                   MOVE      '0'           *IN03
     C                   MOVE      '0'           *IN12
     C                   MOVE      'S'           WCSR
     C                   MOVE      'IFCSUM'      WC0065
     C                   MOVE      'SYSPED'      W20065
     C                   MOVE      'XMLINS'      W30065
      *____________________
      * Read Company record
      *""""""""""""""""""""
     C                   READ      FIRM                                   51
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Calculate formats                                    SRB /
      *////////////////////////////////////////////////////////////
     C     SRB           BEGSR
      *___________
      * Select tag
      *"""""""""""
     C     SRB0          TAG
      *
     C     SRB1          TAG
      *______________
      * Clear Subfile
      *""""""""""""""
     C                   MOVE      '1'           *IN93
     C                   WRITE     E300DC
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZERO         ZRECNO
     C                   Z-ADD     *ZERO         WRECNO
      *_____________
      * Fill Subfile
      *"""""""""""""
     C     *LOVAL        SETLL     WRKAGL01
     C                   READ      WRKAGL01                               94
     C     *IN94         DOWEQ     '0'
     C                   MOVE      '0'           *IN41
      *
     C     KEYC          CHAIN     CUNDL01                            52
     C     *IN52         IFEQ      '0'
     C                   MOVEL     KNAVN         XCCNAM
     C                   ELSE
     C                   MOVE      *BLANKS       XCCNAM
     C                   ENDIF
      *
     C                   MOVE      'N'           XINTOF            1
     C     *IN52         IFEQ      '0'
     C     ECMKEY        CHAIN     ECMSG                              52
     C     *IN52         IFEQ      '0'
     C                   MOVE      *BLANKS       XCOM
     C                   ELSE
     C     ECMK2         CHAIN     ECMSG                              52
     C     *IN52         IFEQ      '0'
     C                   MOVE      'J'           XINTOF            1
     C                   MOVEL     WTXT05        XCOM
     C                   ELSE
     C     ECMK3         CHAIN     ECMSG                              52
     C     *IN52         IFEQ      '0'
     C                   MOVE      'X'           XINTOF            1
     C                   MOVEL     WTXT06        XCOM
     C                   ELSE
     C                   MOVE      '1'           *IN41
     C                   MOVEL     WTXT03        XCOM
     C                   ENDIF
     C                   ENDIF
     C                   ENDIF
     C                   ENDIF
      *
     C     *IN52         IFEQ      '0'
     C     XINTOF        ANDEQ     'N'
      * HER BØR EN ALTERNATIVT SJEKKE MOT 402.... NUMMER
      * NÅR DET BLIR INNFØRT (KUN INNLAND??)
     C                   Z-ADD     WRAVD         EXT1N
     C                   Z-ADD     WRPRO         EXT2N
     C                   Z-ADD     KUNDNR        EXT3N
     C                   MOVEL     EXT0N         TVIKEY           35
     C     TVIKEY        CHAIN     EDIM5                              52
     C     *IN52         IFEQ      '1'
     C                   MOVE      *BLANKS       XCOM
     C                   ELSE
     C                   MOVEL     WTXT04        XCOM
     C                   ENDIF
     C                   ENDIF
      *
     C                   ADD       1             ZRECNO
     C                   WRITE     E300DB
     C                   READ      WRKAGL01                               94
     C  N94              ENDDO
     C                   Z-ADD     ZRECNO        WRECNO
     C     WRECNO        IFEQ      0
     C                   MOVEL     WTXT01        XCCNAM
     C                   MOVE      '1'           *IN41
     C                   ADD       1             ZRECNO
     C                   Z-ADD     ZRECNO        WRECNO
     C                   WRITE     E300DB
     C                   ENDIF
      *______
      * Enter
      *""""""
     C     SRB3          TAG
      *
     C                   MOVEA     '11'          *IN(91)
     C  N30              MOVE      'SY00000'     ZMSGNO
     C                   WRITE     E300DØ
     C                   WRITE     E300DD
     C                   EXFMT     E300DC
      *
      *________
      * F3=Exit
      *""""""""
     C     *IN03         CABEQ     '1'           SRB9
      *___________
      * F12=Cancel
      *"""""""""""
     C     *IN12         CABEQ     '1'           SRB9
      *
     C                   MOVEA     '00'          *IN(91)
      *___________
      * F5=Refresh
      *"""""""""""
     C     *IN05         CABEQ     '1'           SRB1
      *_________
      * F10=Send
      *"""""""""
     C     *IN10         IFEQ      '1'
     C                   EXSR      SRSEND
     C                   GOTO      SRB9
     C                   ENDIF
      *_________
      * Option ?
      *"""""""""
     C                   MOVE      '0'           *IN52
     C                   MOVE      '0'           *IN05
     C     *IN52         DOWEQ     '0'
      *
     C                   READC     E300DB                                 52
     C     *IN52         IFEQ      '0'
     C     ZOP           CASEQ     '4'           SRBD                           Delete
     C     ZOP           CASEQ     '9'           SRBK                           Blank komm
     C                   END
     C                   END
      *
     C  N52              END
      *
     C     *IN03         CABEQ     '1'           SRB9                           End
     C     *IN05         CABEQ     '1'           SRB1                           Refresh
     C     *IN05         CABEQ     '0'           SRB3                           Subfile
      *
     C     SRB9          ENDSR
      *////////////////////////////////////////////////////////////
      *  Delete                                              SRBD /
      *////////////////////////////////////////////////////////////
     C     SRBD          BEGSR
      *______________
      * Delete record
      *""""""""""""""
      *_______________
      * Update subfile
      *"""""""""""""""
     C                   MOVEL     WTXT02        XCOM
     C                   MOVE      *BLANKS       ZOP
     C                   MOVE      '0'           *IN41
     C                   UPDATE    E300DB
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  BLANK kommentar                                     SRBK /
      *////////////////////////////////////////////////////////////
     C     SRBK          BEGSR
      *______________
      * Blank kommentar (muliggjør omkjøring)
      *""""""""""""""
      *_______________
      * Update subfile
      *"""""""""""""""
     C                   MOVEL     XCOM          TSTKOM            8
     C     TSTKOM        IFEQ      'Allerede'
     C                   MOVE      *BLANKS       XCOM
     C                   END
     C                   MOVE      *BLANKS       ZOP
     C                   MOVE      '0'           *IN41
     C                   UPDATE    E300DB
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Send EDIfact                                      SRSEND /
      *////////////////////////////////////////////////////////////
     C     SRSEND        BEGSR
     C                   Z-ADD     1             WRECNO
     C     WRECNO        CHAIN     E300DB                             52
     C     *IN52         DOWEQ     '0'
      *
     C     XCOM          IFEQ      *BLANKS
     C                   MOVE      WRAVD         WAVD              4
     C                   MOVE      WRPRO         WPRO              8
     C                   MOVE      KUNDNR        WKNA              8
     C                   CALL      'E301R'
     C                   PARM                    WAVD
     C                   PARM                    WPRO
     C                   PARM                    WKNA
     C                   PARM                    WBRUDD
     C                   ENDIF
      *
     C     XINTOF        IFEQ      'J'
     C                   MOVE      WRAVD         WAVD              4
     C                   MOVE      WRPRO         WPRO              8
     C                   MOVE      KUNDNR        WKNA              8
     C                   CALL      'IE010C'
     C                   PARM                    WAVD
     C                   PARM                    WPRO
     C                   PARM                    WKNA
     C                   PARM                    ECLIB
     C                   PARM                    ECPGM
     C                   PARM                    WBRUDD
     C                   ELSE
     C                   IF        XINTOF = 'X'
     C                   EXSR      SRXML
     C                   ENDIF
     C                   ENDIF
      *
     C                   ADD       1             WRECNO
     C     WRECNO        CHAIN     E300DB                             52
     C                   ENDDO
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Send XML Instruction                               SRXML /
      *////////////////////////////////////////////////////////////
     C     SRXML         BEGSR
      *
     C     HEAD14K       SETLL     HEAD14
     C     HEAD14K       READE     HEAD14                                 33
      *
     C                   DOW       *IN33 = *OFF
      *
     C                   OPEN      XMLINHL1
     C     XMLINHK       CHAIN     XMLINHL1                           33
     C                   IF        *IN33
     C                   CLEAR                   XMLINHR
     C                   MOVE      HEAVD         XMAVD
     C                   MOVE      HEOPD         XMOPD
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVE      WDT           XMDTO
     C                   MOVE      WTM           XMTMO
     C                   WRITE     XMLINHR
     C                   END
     C                   CLOSE     XMLINHL1
     C                   MOVE      HEAVD         UAVD              4 0
     C                   MOVE      HEOPD         UOPD              7 0
     C                   CALL      'SYXML05'
     C                   PARM                    UAVD
     C                   PARM                    UOPD
      *
     C     HEAD14K       READE     HEAD14                                 33
     C                   ENDDO
      *
     C                   ENDSR
      *
