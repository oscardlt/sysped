      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/TJEOPDFBR MODULE(*LIBL/TJEOPDFBR *LIBL/INCGI)
CRTPGM*        ACTGRP(CGI) AUT(*USE)
      *
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
11XXX * 01 JOV PTF11 tollpass
********************************************************************
      *********************************************************************
      /copy SYSPEDS/qrpgsrcpy,hspecs
      /copy SYSPEDS/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FHEADF     IF   E           K DISK    USROPN
N01  FDOKUF     UF A E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy SYSPEDS/qrpgsrcpy,prototypeb
      /copy SYSPEDS/qrpgsrcpy,usec
      /copy SYSPEDS/qrpgsrcpy,variables3
     D WSUSER          S             10
     D WSAVD           S              4
     D BSAVD           S              4  0
     D WSOPD           S              7
     D BSOPD           S              7  0
     D WSFBN           S              3
     D BSFBN           S              3  0
N01  D WSTOLL          S              4
N01  D BSTOLL          S              4  0
     D IFSFIL          S             50
     D JSONstring      s          32000
     D Error           S            150

      *get input-string
      /copy syspeds/qrpgsrcpy,prolog3

     C                   EXSR      PARSE

     C                   EXSR      INIT
     C                   CALLP     gethtmlifs(
     C                             '/syspeddev/html/JSON.html':
     C                             '/CGITAG/')
     C                   CALLP     wrtsection('top')
      * ............................................................................................
      * skriv JSON-Object start..(alltid '{' + x ant param. m/komma mellom (IKKE komma etter siste))
      * ............................................................................................
      /free
        JSONstring='{'
        +'¬user¬ : ¬'+%trim(WSUSER)+'¬, '
        +'¬wsavd¬ : ¬'+%trim(WSAVD)+'¬, '
        +'¬wsopd¬ : ¬'+%trim(WSOPD)+'¬, '
        +'¬wsfbn¬ : ¬'+%trim(WSFBN)+'¬, '
        +'¬ifsfil¬ : ¬'+%trim(IFSFIL)+'¬, '
        +'¬errMsg¬ : ¬'+%trim(ERROR)+'¬, ';
      /end-free
      * JSONfix escaper " i tekst,  for deretter å bytte ¬->"
     C                   CALL      'JSONFIX'
     C                   PARM                    JSONstring
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   CALLP     wrtsection('JSONlin')

     C                   IF        ERROR=*BLANKS

     C                   EVAL      JSONstring ='"prtPDFfrbrev" : ['
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   CALLP     wrtsection('JSONlin')

     C                   EVAL      JSONstring =']'
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   CALLP     wrtsection('JSONlin')

     C                   EVAL      JSONstring ='}'
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   CALLP     wrtsection('JSONlin')
     C                   ENDIF
      * Quit
     C                   EXSR      EXIT
      *=====================================================================******
      * Parse input
      *=====================================================================******
     C     Parse         BEGSR
     C                   EVAL      WSUSER  = zhbgetvar('USER')
     C                   EVAL      WSAVD   = zhbgetvar('WSAVD')
     C                   EVAL      BSAVD   = c2n2(WSAVD)
     C                   EVAL      WSOPD   = zhbgetvar('WSOPD')
     C                   EVAL      BSOPD   = c2n2(WSOPD)
     C                   EVAL      WSFBN   = zhbgetvar('WSFBN')
     C                   EVAL      BSFBN   = c2n2(WSFBN)
N01  C                   EVAL      WSTOLL  = zhbgetvar('WSTOLL')
N01  C                   EVAL      BSTOLL  = c2n2(WSTOLL)
     C                   ENDSR
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          BEGSR
     C                   OPEN      BRIDF
     C     WSUSER        CHAIN     BRIDF                              50
      * En "standardvariant" libl: CALL bound (INCGI=*MODULE) med parameter.
     C     BILIBL        IFEQ      *blanks
     C                   CALLB     'INCGI'
     C                   PARM                    BISTDL
     C                   ELSE
      * Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   CALL      BILIBL
     C                   ENDIF
     C     HeaKey        KLIST
     C                   KFLD                    BSAVD
     C                   KFLD                    BSOPD

     C   50              eval      Error = 'Invalid userID'
     c   50              goto      UtIni
     c                   OPEN      Headf
N01  c                   OPEN      DOKUF

     C     HeaKey        CHAIN     HEADF                              51
     C   51              eval      Error = 'Invalid Order'
     c   51              goto      UtIni

      *  Kall prog for å generere html-fil eller PDF-fil i IFS.
N01  C                   MOVE      'F'           Utype
N01  C                   IF        BSTOLL <> 0
N01  C                   exsr      Tollpass
N01  C                   Z-ADD     1             BSFBN
N01  C                   MOVE      'T'           Utype
N01  C                   ENDIF
     C                   MOVE      BSAVD         WSAVD
     C                   MOVE      BSOPD         WSOPD
     C                   MOVE      BSFBN         WSFBN
     C                   EVAL      IFSFil = *BLANKS
     C                   call      'SYFA12CES'
     c                   parm                    BIFIRM
     C                   PARM                    WSAVD
     C                   PARM                    WSOPD
     C                   PARM                    WSFBN
E01  C                   PARM                    Utype             1
     C                   PARM                    IFSFIL

     C     UTINI         ENDSR
      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     EXIT          BEGSR

     C                   CLOSE     BRIDF
     C  N50              CLOSE     HEADF
N01  C  N50              CLOSE     DOKUF

      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   MOVE      *ON           *INLR
     C                   RETURN
     C                   ENDSR
      *
      *
      *************************************************************
     C     TOLLPASS      BEGSR
      *************************************************************
      *
     C                   CLEAR                   DOKUFR
     C     DOKUFK        KLIST
     C                   KFLD                    DFRI
     C                   KFLD                    DFAVD
     C                   KFLD                    DFOPD
     C                   KFLD                    DFFBNR
     C                   MOVE      'F'           DFRI
     C                   MOVE      HEAVD         DFAVD
     C                   MOVE      HEOPD         DFOPD
     C                   Z-ADD     1             DFFBNR
     C     DOKUFK        CHAIN     DOKUF                              33
     C     *IN33         IFEQ      '0'
     c                   move      BSTOLL        DFTOLL
     C                   UPDATE    DOKUFR
     c                   else
     C                   MOVE      HESG          DFSG
     c                   move      BSTOLL        DFTOLL
N01  C                   MOVEL     'S'           DFBELA                         DEFAULT
     C     HEFR          IFEQ      'S'
     C     HEFR          OREQ      'M'
     C                   MOVEL     HEFR          DFBELA
     C                   END
     C                   MOVE      HEKNS         DFKNSS
     C                   MOVE      HENAS         DFNAVS
     C                   MOVE      HEADS1        DFAD1S
     C                   MOVE      *BLANKS       DFAD2S
     C                   MOVEL     HEADS3        TMPPUL            4
     C                   TESTN                   TMPPUL               77
     C     *IN77         IFEQ      '1'
     C                   MOVE      TMPPUL        DFPNLS                          LASTESTED
     C                   MOVE      HEADS3        TMPAD3           25
     C                   MOVEL     TMPAD3        DFAD3S
     C                   ELSE
     C                   MOVEL     HEADS3        DFAD3S
     C                   END
     C                   MOVE      HENT          DFNTLA
     C                   MOVE      HENAK         DFNAVL
     C                   MOVE      HEADK1        DFAD1L
     C                   MOVE      *BLANKS       DFAD2L
     C                   MOVEL     HEADK3        TMPPUL            4
     C                   TESTN                   TMPPUL               77
     C     *IN77         IFEQ      '1'
     C                   MOVE      TMPPUL        DFPOUL                          UTL.POSTNR
     C                   MOVE      HEADK3        TMPAD3           25
     C                   MOVEL     TMPAD3        DFADUL                          UTL.STED
     C                   ELSE
     C                   MOVEL     HEADK3        DFADUL
     C                   END
     C                   MOVE      HEKNK         DFKNSM
     C                   MOVE      HENAK         DFNAVM
     C                   MOVE      HEADK1        DFAD1M
     C                   MOVE      HEADK3        DFAD3M
     C     HEGM1         IFEQ      *BLANKS
     C                   MOVEL     HEGM1         DFGM
     C                   ELSE
     C     HEGM1         CAT       HERFA:1       DFGM
     C                   END
     C                   MOVE      HENT          DFNT
     C                   MOVE      HEVS1         DFVS
     C                   MOVE      HEVKT         DFVKT
     C                   MOVE      HELM          DFLM
     C                   MOVE      HEM3          DFM3
     C                   MOVEL     HEGM2         DFGM2
     C                   MOVEL     HEVS2         DFVS2
     C                   MOVE      HEFBV         DFVKTF
     C                   WRITE     DOKUFR
     C                   ENDIF
      *
     C                   ENDSR
