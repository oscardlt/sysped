     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPPNLGRP(HLPSCR STSOPD)
     A                                      HLPTITLE('MANUELL OPPFØLGING AV MAN-
     A                                      KO - ORDRENIVÅ')
     A                                      HELP
     A                                      ALTHELP
      *
     A          R SUBFIL                    SFL
     A            WSNR           1A  B  6  3VALUES(' ' '1' '9' 'M' 'X' 'T')
     A            SOAVD          4Y 0O  6  6EDTCDE(4)
     A            SOOPD          7Y 0O  6 11EDTCDE(4)
     A  50                                  DSPATR(RI)
     A            SFBESK        30A  H
     A            XSFBESK       28A  O  6 19
     A            SFLSM         20A  O  6 48
     A  52                                  COLOR(RED)
     A            SOSTAT         1A  O  6 69TEXT('TERMKODE')
     A                                      COLOR(WHT)
     A            SFMTXT        10A  O  6 71COLOR(RED)
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                  1  2'SYSPED/STSOPD'
     A                                      COLOR(BLU)
     A                                  1 19'Vedlikehold SKYTING uferdige turer-
     A                                      /manko '
     A                                      COLOR(WHT)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                  2 19'Sendings/oppdrags-nivå'
     A                                      COLOR(WHT)
     A            XXTYPE         5A  O  2 44
     A                                  2 50'Tur/L.nr'
     A            XXPRO          8Y 0O  2 59EDTCDE(4)
     A                                  2 70TIME
     A                                  3  2'Velg og tast ENTER'
     A                                      COLOR(BLU)
     A                                  3 23'X=Fj. fra tur(HELma)'
     A                                      COLOR(BLU)
     A            XXBESK        30A  O  3 44
     A            ZRECNO         4S 0O  3 75SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  4  2'1=Neste nivå 9=Switc status M/T=Go-
     A                                      dkj reell manko'
     A                                      COLOR(BLU)
     A                                  4 53'-- Kolli --     Status X=OK'
     A                                      COLOR(WHT)
     A                                  5  2'Valg Avd/Oppdnr  Avsender Mottaker-
     A                                       Sted        Listet Skutt  Manko (s-
     A                                      e hjelp) '
     A                                      COLOR(WHT)
      *
     A          R BUNN
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF03(03 'EXIT')
     A                                      CF12(03)
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A  51                              1  6'Omvedt kontrast'
     A                                      DSPATR(RI)
     A  51                              1 22'betyr tatt ut av turen (men VAR de-
     A                                      r under skytingen.) '
     A N98 30     MSGTXT        78A  O  2  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A                                  3  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 43MSGCON(018 DIV0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 66'F1=Help '
     A                                      COLOR(BLU)
     A  98                              4  2MSGCON(078 DIV0012 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A N98N30                           4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
      *
     A          R NYWRK
     A                                      CF03(03 'EXIT')
     A                                      CF12(03)
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                  1  2'SYSPED/STSOPD'
     A                                      COLOR(BLU)
     A                                  1 19'Vedlikehold SKYTING uferdige turer-
     A                                      /manko '
     A                                      COLOR(WHT)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                  2 19'Sendings/oppdrags-nivå'
     A                                      COLOR(WHT)
     A            XXTYPE         5A  O  2 44
     A                                  2 50'Tur'
     A            XXPRO          8Y 0O  2 54EDTCDE(4)
     A                                  2 70TIME
     A                                  3  2'Velg og tast ENTER'
     A                                      COLOR(BLU)
     A                                  3 23'X=Fj. fra tur(HELma)'
     A                                      COLOR(BLU)
     A            XXBESK        30A  O  3 44
     A                                  4  2'1=Neste nivå 9=Switche status M=Go-
     A                                      dkj reell manko'
     A                                      COLOR(BLU)
     A                                  4 53'-- Kolli --     Status X=OK'
     A                                      COLOR(WHT)
     A                                  5  2'Valg Avd/Oppd  Avsender Mottaker  -
     A                                      Sted         Listet Skutt  Manko (s-
     A                                      e hjelp) '
     A                                      COLOR(WHT)
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 62MSGCON(018 DIV0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
      *
     A          R VARELI
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF12(12 'Forrige')
     A                                      CF04(04 'SØKE')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1  2'Avd  Oppdnr.   Tur.nr Listet Kll m-
     A                                      anu. kontollert (på ikke kollimerke-
     A                                      t gods)'
     A                                      COLOR(WHT)
     A            SOAVD          4Y 0O  2  2COLOR(WHT)
     A                                      EDTCDE(4)
     A            SOOPD          7Y 0O  2  7COLOR(WHT)
     A                                      EDTCDE(4)
     A            HEPRO          8Y 0O  2 15COLOR(WHT)
     A                                      EDTCDE(4)
     A            HENT4          4Y 0O  2 24COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  2 33'Ant'
     A                                      COLOR(WHT)
     A            WSAMKK         4Y 0B  2 37EDTCDE(4)
     A                                  2 42'Merknad'
     A                                      COLOR(WHT)
     A            WSMERK        30A  B  2 50DSPATR(UL)
     A                                  3  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N30                              4  2MSGCON(024 DIV0000 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        77A  O  4  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
