      *********************************************************************
      *  After compiling this RPG MODULE,
      *  create the related program with the following command:
      *
CRTPG+*  CRTPGM SYSPED/ESTKT21B MODULE(*LIBL/ESTKT21B *LIBL/INCGI)
CRTPGM*         ACTGRP(*NEW) AUT(*USE)
      *
      *
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
********************************************************************
      *
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
      *--------------------------------------------------------------------
      * Data base files
      *--------------------------------------------------------------------
     FBRIDF     if   e           k disk    usropn
     Fheadf     uf   e           k disk    usropn
     Fstsopd    uf   e           k disk    usropn
      *--------------------------------------------------------------------
      * Prototype defintions and standard system API error structure
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      *--------------------------------------------------------------------
      * Variables common to CGI programs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
      * Variables received from the input string
     D tur             s              8
     D turN            s              8  0
     D avd             s              4
     D avdN            s              4  0
     D opd             s              7
     D opdN            s              7  0
     D posN            s              3  0
     D posA            s              3
     D quit            s              4
     D TERM            s             20
     D TERM5           s              5
     D UserId          s             10
     D User            s             10
     D JSONstring      s          32000
N02  D ErrMsg          S            150
N02  D InfoMsg         S            150
N02  D SuccessMsg      S            150
      *
     D Spaces          s             12a   inz('&nbsp;&nbsp;')
     D ItemCount       s             10i 0
     D i               s             10i 0
      *--------------------------------------------------------------------
      * Prolog common to CGI programs
      * -Receive input from the remote browser
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prolog3
      *=====================================================================******
      * MAIN PROCESS LINE
      *=====================================================================******
      * Parse input string
     C                   eval      userid = zhbgetvar('user')
     C                   eval      tur    = ZhbGetVar('TURNR')
     C                   eval      turN     = c2n2(tur)
     C                   eval      TERM     = zhbgetvar('TERM')
     C                   eval      TERM5    = TERM
      * Set libl/open files
     C                   exsr      Init
      *
     C                   eval      ItemCount = ZhbGetVarCnt('OPD')
     C                   eval      i = 1
     C     i             DOUGT     ItemCount
     C                   eval      avd    = ZhbGetVar('AVD':i)
     C                   eval      avdN     = c2n2(avd)
     C                   eval      opd    = ZhbGetVar('OPD':i)
     C                   eval      opdN     = c2n2(opd)
     C                   EXSR      UpdPos
     C                   eval      i = i +1
     C                   ENDDO
      *
      * Set output skeleton html member name
     c                   callp     gethtmlifs(
     C                             '/syspeddev/html/JSON.html':'/CGITAG/')
     C                   callp     wrtsection('top')
      *
      /free
        successMsg='Endret posisjonsrekkefølge er lagret';
        JSONstring='{'
        +'¬user¬ : ¬'+%trim(USERID)+'¬, '
        +'¬errMsg¬ : ¬'+%trim(ErrMsg)+'¬, '
        +'¬infoMsg¬ : ¬'+%trim(InfoMsg)+'¬, '
        +'¬successMsg¬ : ¬'+%trim(SuccessMsg)+'¬}';
      /end-free
     c                   call      'JSONFIX'
     c                   parm                    JSONstring
     C*                  callp     updHTMLvar('JSONstring':JSONstring)
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')
      *
      *
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      *
     C                   close     BRIDF
     C                   close     headf
     C                   close     stsopd
     C                   eval      *inlr = *on
     C                   RETURN
      *
     C***********************************************                       N
     C     UpdPos        BEGSR
     C***********************************************
     c                   eval      PosN = i
     c                   move      PosN          PosA              3
      *
      * slett/reskriv til skytefil for å få rett "fysisk" rekkefølge"
     c     StsOpdKey     chain     stsopd
     c                   if        %found ( stsopd )
     c                   eval      SOSORT = POSA
     c                   update    STSOPDR
     c                   endif
      *
      * oppdater også headf , men kun ved utgående
     c                   if        Term = *blanks
     c     Heakey        chain     headf
     c                   if        %found ( headf )
     c                   if        HEPRO = TurN
     c                   eval      HEPOS1 = POSA
     c                   eval      HEPOS2 = *zeros
     c                   endif
     c                   update    HEADFR
     c                   endif
     c                   endif
      *
     C                   ENDSR
      *
      *=====================================================================******
      * Set Keys mm
      *=====================================================================******
     C     init          begsr
      *=====================================================================******
      *
     C                   open      BRIDF
     C     UserID        CHAIN     BRIDF                              30
      * --------------------------------------
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C* (bedre ressursmessig). MODUL må da være med comp. av dette pgm!!
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
     C                   open      headf
     C                   open      stsopd
      *
     c     Heakey        klist
     C                   kfld                    AvdN
     C                   kfld                    OpdN
     c     StsOpdKey     klist
     C                   kfld                    TERM5
     C                   kfld                    TurN
     C                   kfld                    AvdN
     C                   kfld                    OpdN
     C                   endsr
