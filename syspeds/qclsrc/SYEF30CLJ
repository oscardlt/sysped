             PGM        PARM(&IN07 &IN08 &JBK &FAKT)
             DCL        VAR(&FINS) TYPE(*CHAR) LEN(1) VALUE('N')
             DCL        VAR(&AUSER) TYPE(*CHAR) LEN(10)
             DCL        VAR(&ATYPE) TYPE(*CHAR) LEN(2) VALUE('mf')
             DCL        VAR(&JOBN) TYPE(*CHAR) LEN(6)
             DCL        VAR(&UNIK) TYPE(*CHAR) LEN(7) /* Unikkode +
                          til register SORT. */
             DCL        VAR(&IN07) TYPE(*LGL) LEN(1) /* Hvis &in07 +
                          på, betyr uten oppdatering. */
             DCL        VAR(&IN08) TYPE(*LGL) LEN(1) /* Hvis &in08 +
                          på, betyr prøve manifest. */
             DCL        VAR(&FAKT) TYPE(*CHAR) LEN(1) /* Hvis &FAKT +
                          på, betyr at det skal kjøre faktura */
             DCL        VAR(&JBK) TYPE(*CHAR) LEN(1) /* Jobbkø. */
    /* */
             DCL        VAR(&KOPI) TYPE(*CHAR) LEN(1) VALUE('N') /* +
                          BENYTTES VED KOPIUTSKRIFT   */
             DCL        VAR(&FFAK) TYPE(*CHAR) LEN(1) /* Hvis 'J', +
                          kaller SYFA20 for å skrive alle +
                          ferdigmeldte fakturaer. */
             DCL        VAR(&IN05) TYPE(*LGL) LEN(1) /* Hvis &in05 +
                          på, betyr programkallinng fra BORDERAUR. */
             DCL        VAR(&IN06) TYPE(*LGL) LEN(1) VALUE('1') /* +
                          Hvis &in06 på, betyr programkalleing fra +
                          MANEFEST. */
             DCL        VAR(&PRT18) TYPE(*CHAR) LEN(10) /* +
                          Printernavn til manifest. */
             DCL        VAR(&PT18) TYPE(*CHAR) LEN(10) /* Papirtype +
                          til manifest. */
             DCL        VAR(&PRANT) TYPE(*CHAR) LEN(2) /* Antall +
                          utskrifter. */
    /* */
             RTVDTAARA  DTAARA(*LDA (681 10)) RTNVAR(&PRT18)
             RTVDTAARA  DTAARA(*LDA (691 10)) RTNVAR(&PT18)
             RTVJOBA    USER(&AUSER) NBR(&JOBN)
             CHGVAR     VAR(&UNIK) VALUE('W' *CAT &JOBN)
    /* */
    /* SKRIVER UT MANIFEST */
             OVRPRTF    FILE(SYEF30P) OUTQ(&PRT18) PAGESIZE(72 80) +
                          CPI(10) OVRFLW(64) +
                          FORMTYPE(&PT18) COPIES(2)
             RMVM       FILE(SUMMAN) MBR(&UNIK)
             MONMSG     MSGID(CPF7310)
             ADDPFM     FILE(SUMMAN) MBR(&UNIK)
             MONMSG     MSGID(CPF7306)
             OVRDBF     FILE(SUMMAN) TOFILE(SUMMAN) MBR(&UNIK)
             CALL       PGM(SYEF30) PARM(&IN07 &IN08 &PRANT)
             RMVM       FILE(SUMMAN) MBR(&UNIK)
             SNDPGMMSG  MSG(' ' *BCAT &PRANT *BCAT 'manifest er klare +
                          til utskrift på skriver ' *BCAT &PRT18 +
                          *BCAT '.') MSGTYPE(*INFO)
    /* */
             SBMJOB     CMD(CALL PGM(ARC002CL) PARM(&ATYPE &AUSER +
                          &FINS)) JOB(ARKIV) JOBD(*USRPRF) +
                          JOBQ(SYJOBQARK)
    /* */
             IF         COND(&IN07 = '1') THEN(GOTO CMDLBL(SLUTT)) /* +
                          Hvis &IN07 på, avslutter program. */
             IF         COND(&IN08 = '1') THEN(GOTO CMDLBL(SLUTT)) /* +
                          Hvis &IN08 på, avslutter program. */
    /* */
             IF         COND(&FAKT = 'J') THEN(DO)
             IF         COND(&JBK = 'J') THEN(DO)
             SBMJOB     CMD(CALL PGM(SYFA20CLJ) PARM(&FFAK &IN05 +
                          &IN06 &IN07 &IN08 &KOPI)) JOB(MANIFEST) +
                          JOBD(*USRPRF) JOBQ(SYJOBQPRT) +
                          LOG(0 99 *NOLIST) LOGCLPGM(*NO)
                        ENDDO
             ELSE       CMD(DO)
             CALL       PGM(SYFA20CLJ) PARM(&FFAK &IN05 &IN06 &IN07 +
                          &IN08 &KOPI)
                        ENDDO
                        ENDDO
 SLUTT:      ENDPGM
