     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPDOC('TR031001' 'TR031R' 'SY400LO-
     A                                      /TET')
     A                                      HELP
     A                                      ALTHELP
      * "NOERASE" MÅ HA FOR Å IKKE TØMME BILDE SOM MAN KOMME FRA
     A          R NOERASE
     A                                      ASSUME
     A                                  1  2' '
      * MAN KUN DEFINNERE VINDU PÅ REKORDNIVÅ
     A          R WINDOWSZ
     A                                      WINDOW(*DFT 15 64 *NOMSGLIN)
     A                                      OVERLAY
      *
     A          R TR031D01
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A N28                                  CF04(04 'Søking')
     A                                      CF10(10 'Se EDI-logg')
     A                                      CA12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  3'SYSPED/TR031R'
     A                                      COLOR(BLU)
     A                                  1 20'Arbeid med garanti'
     A                                      COLOR(WHT)
     A                                  2  2'Bruker:'
     A            WSUSR         10A  O  2 10COLOR(WHT)
     A                                  2 22'Endr.dato:'
     A            WSDT           6Y 0O  2 33COLOR(WHT)
     A                                      EDTCDE(Y)
     A                                  2 43'Reg.dato:'
     A            WSDTR          6Y 0O  2 53COLOR(WHT)
     A                                      EDTCDE(Y)
     A                                  3  2'Hovedansvarlig:'
     A            WSKNA          8Y 0B  3 20TEXT('Kundenr H.ansvalig')
     A                                      DSPATR(UL)
     A  28                                  DSPATR(PR)
     A  99                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  4  2'Foretaksnr:'
     A            WSTINA        17A  B  4 20TEXT('Tin kode H.ansvalig')
     A                                      DSPATR(UL)
     A  28                                  DSPATR(PR)
     A  98                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                  5  2'Navn:'
     A            WSNAA         30A  B  5 20TEXT('Navn H.ansvalig')
     A                                      DSPATR(UL)
     A  28                                  DSPATR(PR)
     A  97                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                  6  2'Adresse 1:'
     A            WSADA1        30A  B  6 20TEXT('Adresse-1 H.ansvalig')
     A                                      DSPATR(UL)
     A  28                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                  7  2'Postnr/postadr:'
     A            WSPNA          9A  B  7 20TEXT('Postnr H.ansvalig')
     A                                      DSPATR(UL)
     A  28                                  DSPATR(PR)
     A  96                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSPSA         24A  B  7 30TEXT('Poststed H.ansvalig')
     A                                      DSPATR(UL)
     A  28                                  DSPATR(PR)
     A  95                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                  8  2'Landkode:'
     A            WSLKA          2A  B  8 20TEXT('Landkode H.ansvalig')
     A                                      DSPATR(UL)
     A  28                                  DSPATR(PR)
     A  94                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                  9  2'Garantitollsted:'
     A            WSTSD          8A  B  9 20TEXT('AVG.TOLLSTED')
     A                                      DSPATR(UL)
     A  28                                  DSPATR(PR)
     A  93                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 10  2'Garantitype/G.nr:'
     A            WSGTY          1Y 0B 10 20TEXT('Garantitype')
     A                                      DSPATR(UL)
     A  28                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                      VALUES(0 1 6 7 8 9)
     A            WSGNR         24A  B 10 23TEXT('Garantinr')
     A                                      DSPATR(UL)
     A  27
     AO 28                                  DSPATR(PR)
     A  92                                  DSPATR(RI)
     A N27                                  COLOR(YLW)
     A  27                                  COLOR(WHT)
     A                                 10 48'Gjeld f.vare:'
     A            WSGFV          1A  B 10 62TEXT('Garantitype')
     A                                      DSPATR(UL)
     A  28                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                      VALUES('J' 'N')
     A                                 11  2'Ny adgangskode:'
     A            WSAKNY         4A  B 11 20TEXT('Ny adgangskode')
     A                                      DSPATR(UL)
     A  28                                  DSPATR(PR)
     A  91                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 11 32'Gml. adg.kode:'
     A            WSAKGM         4A  B 11 48TEXT('Gml adgangskode')
     A                                      DSPATR(UL)
     A  28                                  DSPATR(PR)
     A  90                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 12  2'Garantibeløp:'
     A            WSGBL         11Y 0B 12 20TEXT('Garantibeløp')
     A                                      DSPATR(UL)
     A  28                                  DSPATR(PR)
     A  89                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 12 32'Valuta:'
     A            WSGVK          3A  B 12 40TEXT('Garantibeløp')
     A                                      DSPATR(UL)
     A  28                                  DSPATR(PR)
     A  88                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 13  2'Brukt g.beløp:'
     A            WSGBLB        11Y 0B 13 20TEXT('Garantibeløp')
     A                                      DSPATR(UL)
     A  28                                  DSPATR(PR)
     A  87                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 14  2'Varsling:'
     A            WSPRM          3Y 0B 14 20TEXT('Varsling')
     A                                      DSPATR(UL)
     A  28                                  DSPATR(PR)
     A  86                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 14 26'(Ved over X % er brukt opp)'
     A N28N30                          15  2'F4=Søking   F10=EDI-logg   F12=For-
     A                                      rige'
     A                                      COLOR(BLU)
     A  28N30                          15  2'F10=EDI-logg   F12=Forrige'
     A                                      COLOR(BLU)
     A  30        MSGTXT        60A  O 15  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
