     FSYFA57FM  CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     F                                     INFDS(STATDS)
     FFIRM      IF   E           K DISK
     FCUNDL01   IF   E           K DISK
     FKODTA     IF   E           K DISK
     FCUSAAV    UF A E           K DISK
     D                 DS
     D  KOANVN                 1     33
     D  AVDNVN                 1     20
     D  SLETT                  1      8
     D STATDS          DS
      *___________________________
      * Finner username
      *"""""""""""""""""""""""""""
     D                SDS
     D  ZJOB                 244    253
     D  ZUSER                254    263
     D  ZJOBNR               264    269  0
      *
      *
     C                   EXSR      INIT
     C                   EXSR      HOVED
     C                   MOVE      '1'           *INLR
      *
      *
      *////////////////////////////////////////////////////////////
     C     INIT          BEGSR
      *////////////////////////////////////////////////////////////
      *___________________
      * Definiere Workfelt
      *"""""""""""""""""""
     C     *LIKE         DEFINE    *IN03         WIN03
     C     *LIKE         DEFINE    *IN30         WIN30
     C     *LIKE         DEFINE    WSKN          WWKN
     C     *LIKE         DEFINE    WSAVD         WWAVD
     C     *ENTRY        PLIST
     C                   PARM                    WSKN
     C                   PARM                    UPDATE            1
      *__________________
      * Definiere Nøkkler
      *""""""""""""""""""
     C     KUNKEY        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    CAKN
     C     KEY           KLIST
     C                   KFLD                    CAKN
     C                   KFLD                    CAAVD
     C     AVDKEY        KLIST
     C                   KFLD                    KOAUNI
     C                   KFLD                    CAAVD
     C                   MOVE      'A'           KOAUNI
      *
     C                   READ      FIRM                                   55
     C* Endring av KUNDE TILlatt ??
     C     WSKN          COMP      *ZEROS                             5151       *PROTECT
     C* Update tillatt ??
     C     UPDATE        COMP      'J'                                5050       *NONDISP
      *
     C                   ENDSR
      *
      *
      *////////////////////////////////////////////////////////////
     C     HOVED         BEGSR
      *////////////////////////////////////////////////////////////
      *______________
      * Tømme Subfile
      *""""""""""""""
      *
     C     START         TAG
     C                   MOVE      WSKN          WWKN
     C                   MOVE      WSAVD         WWAVD
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '000'         *IN(93)
     C                   Z-ADD     *ZERO         ZRECNO
     C     WSKN          COMP      *ZEROS                                 56     *LES ALLE
     C     WSKN          SETLL     CUSAAV
      *_____________
      * Fyll Subfile (med alle poster uavhengig av linjer pr side)
      *"""""""""""""
     C  N56WSKN          READE(N)  CUSAAV                                 94
     C   56              READ(N)   CUSAAV                                 94
     C     *IN94         DOWEQ     '0'
      *
     C     NESTE         TAG
     C     WSAVD         IFNE      *ZEROS
     C     WSAVD         ANDNE     CAAVD
     C  N56WSKN          READE(N)  CUSAAV                                 94
     C   56              READ(N)   CUSAAV                                 94
     C  N94              GOTO      NESTE
     C   94              GOTO      UT
     C                   END
     C     KUNKEY        CHAIN     CUNDL01                            33
     C   33              MOVE      *BLANKS       KNAVN
     C     AVDKEY        CHAIN     KODTA                              33
     C   33              MOVE      *BLANKS       KOANVN
     C                   ADD       1             ZRECNO
     C                   WRITE     SUBFIL
      *
     C  N56WSKN          READE(N)  CUSAAV                                 94
     C   56              READ(N)   CUSAAV                                 94
     C  N94              END
      *
     C     UT            TAG
      *
      *_______________
      * Ingen record fantes??
      *_______________
     C     ZRECNO        IFEQ      0
     C                   MOVE      'JOV0442'     ZMSGNO
     C                   MOVE      '1'           *IN30
     C                   WRITE     BLANKREC
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     5             POSNBR
     C   51              Z-ADD     17            POSNBR
     C                   EXFMT     NOREC
     C                   MOVE      '0'           *IN30
     C                   ELSE
      *
      *_______________
      * Bevar sist brukt recno, vis subfila fra post begynnelsen
      *_______________
      *
     C     *LIKE         DEFINE    ZRECNO        XRECNO
     C                   MOVE      ZRECNO        XRECNO
     C                   Z-ADD     1             ZRECNO
      *
     C     STBILD        TAG
      *
     C                   MOVEA     '11'          *IN(91)
     C  N30              MOVE      'SY00000'     ZMSGNO
     C                   WRITE     BLANKREC
     C                   WRITE     BOTREC
     C                   Z-ADD     7             LINNBR
     C                   Z-ADD     2             POSNBR
     C                   EXFMT     SUBCTL
     C                   END
      *
     C     *IN12         CABEQ     '1'           UTHOV
     C     *IN03         CABEQ     '1'           UTHOV
      *
     C                   MOVEA     '00'          *IN(91)
      *_____________
      * F5=Frisk opp
      *"""""""""""""
     C     *IN05         CABEQ     '1'           START
      *_____________
      * F6=Lag ny
      *"""""""""""""
     C     *IN06         IFEQ      '1'
     C                   EXSR      CRTNEW
     C                   GOTO      START
     C                   END
      *_____________
      * Options på topplinja
      *"""""""""""""
     C     TOPOPT        IFNE      ' '
     C     TOPOPT        IFEQ      '1'
     C                   EXSR      CRTNEW
     C                   ELSE
     C                   MOVE      *BLANKS       SLETT
     C                   MOVE      TOPOPT        WSNR
     C                   MOVE      WSKN          CAKN
     C                   MOVE      WSAVD         CAAVD
     C                   EXSR      OPTION
     C                   END
      * Søk utvalg til gammel verdi.. Vis subfila fra front igjen..
     C                   MOVE      WWKN          WSKN
     C                   MOVE      WWAVD         WSAVD
     C                   MOVE      ' '           TOPOPT
     C                   GOTO      START
     C                   END
      *_________________
      * Nytt søk/utvalg
      *"""""""""""""""""
     C     WSKN          IFNE      WWKN
     C     WSAVD         ORNE      WWAVD
     C                   GOTO      START
     C                   END
      *_______
      * Valg ?
      *"""""""
     C                   MOVE      '0'           *IN52
     C                   MOVE      '0'           *IN30
     C                   MOVE      '0'           *IN95
     C     *IN52         DOWEQ     '0'
      *
     C                   READC     SUBFIL                                 52
     C     *IN52         IFEQ      '0'
     C                   EXSR      OPTION
     C                   END
      *
     C  N30
     CANN52              END
      *
     C     *IN03         CABEQ     '1'           UTHOV
     C     *IN95         CABEQ     '1'           START
     C     *IN95         CABEQ     '0'           STBILD
      *
     C     UTHOV         ENDSR
      *
      *
      *////////////////////////////////////////////////////////////
     C     OPTION        BEGSR
      *////////////////////////////////////////////////////////////
     C*
     C     WSNR          IFEQ      '4'
     C     UPDATE        ANDEQ     'J'
     C     SLETT         IFNE      '*SLETTET'
     C     KEY           CHAIN     CUSAAV                             55
     C  N55              DELETE    CUSAAVR
     C                   MOVE      ' '           WSNR
     C     TOPOPT        IFEQ      ' '
     C                   MOVE      *ZEROS        CAAVD
     C                   MOVE      *BLANKS       AVDNVN
     C                   MOVEL     '*SLETTET'    AVDNVN
     C                   UPDATE    SUBFIL
     C                   END
     C                   END
     C                   END
     C     WSNR          IFEQ      '2'
     C     UPDATE        ANDEQ     'J'
     C     SLETT         IFNE      '*SLETTET'
     C     KEY           CHAIN     CUSAAV                             55
     C     *IN55         IFEQ      '0'
     C                   MOVE      CASFAK        WSSFAK
     C                   Z-ADD     23            LINNBR
     C                   Z-ADD     75            POSNBR
     C                   EXFMT     CHGREC
     C     *IN12         IFNE      '1'
     C                   MOVE      WSSFAK        CASFAK
     C                   UPDATE    CUSAAVR
     C                   MOVE      ' '           WSNR
     C     TOPOPT        IFEQ      ' '
     C                   UPDATE    SUBFIL
     C                   END
     C                   END
     C                   END
     C                   END
     C                   END
     C                   ENDSR
      *
      *
      *////////////////////////////////////////////////////////////
     C     CRTNEW        BEGSR
      *////////////////////////////////////////////////////////////
     C                   MOVE      ' '           W2SFAK
     C                   MOVE      WSKN          W2KN
     C     TOPOPT        IFEQ      '1'
     C                   MOVE      WSAVD         W2AVD
     C                   ELSE
     C                   MOVE      *ZEROS        W2AVD
     C                   END
     C                   Z-ADD     23            LINNBR
     C     W2KN          IFEQ      *ZEROS
     C                   Z-ADD     5             POSNBR
     C                   ELSE
     C     W2AVD         IFEQ      *ZEROS
     C                   Z-ADD     46            POSNBR
     C                   ELSE
     C                   Z-ADD     75            POSNBR
     C                   END
     C                   END
     C                   EXFMT     NEWREC
     C     *IN12         IFNE      '1'
     C                   MOVE      W2KN          CAKN
     C                   MOVE      W2AVD         CAAVD
     C     KEY           CHAIN     CUSAAV                             55
     C     *IN55         IFEQ      '1'
     C                   MOVE      W2SFAK        CASFAK
     C                   WRITE     CUSAAVR
     C     XRECNO        ADD       1             ZRECNO
     C                   WRITE     SUBFIL
     C                   END
     C                   END
     C                   ENDSR
