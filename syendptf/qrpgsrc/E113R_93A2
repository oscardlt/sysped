********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
12XXX * 01 YBC PTF12 UNIK NUMMERTELLER
********************************************************************
     H DATEDIT(*YMD)
     FEDIS      IF   E           K DISK
     FEDIM      IF   E           K DISK
     FECNI93A   IF   E           K DISK
     FEDTM93A   IF   E           K DISK
     FELOC93A   IF   E           K DISK
     FEFTX93A   IF   E           K DISK
     FENAD93A   IF   E           K DISK
     FERFF93A   IF   E           K DISK
     FETOD93A   IF   E           K DISK
     FEGID93A   IF   E           K DISK
     FEMEA93A   IF   E           K DISK
     FEPCI93A   IF   E           K DISK
N01  FFIRM      IF   E           K DISK
N01  FZTELL     UF A E           K DISK
     FZEADF     O  A E             DISK
     FZREETXT   O  A E             DISK
     D A               S              1    DIM(79)
     D B               S              1    DIM(18)
      *________________________
      * DS for decimal handling
      *""""""""""""""""""""""""
     D                 DS
     D  ZDEC                   1      7  6
     D  ZDEC6                  2      7
     D  ZHEL                  11     22  0
     D                 DS
     D  WMN2                   1      9  0
     D                 DS
     D  WPOS1                  1      3  0
     D  WPOS1A                 1      3
      */////////////
      * MAIN LOGIC /
      */////////////
      *___________
      * Initiering
      *"""""""""""
     C                   EXSR      INIT
      *________
      * CONVERT
      *""""""""
     C                   EXSR      RTVD
      *
     C                   MOVE      '1'           *INLR
      *
      *////////////////////////////////////////////////////////////
      *                                                      INIT /
      *////////////////////////////////////////////////////////////
     C     INIT          BEGSR
     C     KEYMS0        KLIST
     C                   KFLD                    ZMN
     C                   KFLD                    ZGN0
     C     KEYMS1        KLIST
     C                   KFLD                    ZMN
     C                   KFLD                    ZGN1
     C                   KFLD                    ZREPB0
      *
     C     *LIKE         DEFINE    NMN           ZMN
     C     *LIKE         DEFINE    NGN           ZGN0
     C     *LIKE         DEFINE    NGN           ZGN1
     C     *LIKE         DEFINE    NREP0         ZREPB0
     C     *LIKE         DEFINE    HEHAWB        WEHAWB
     C     *LIKE         DEFINE    HETRI         WETRI
     C     *LIKE         DEFINE    HETRC         WETRC
     C     *LIKE         DEFINE    HETRCN        WETRCN
     C     *LIKE         DEFINE    HEREFF        WEREFF
     C     *LIKE         DEFINE    D2380         W2380
      * INIT WORKFIELDS
     C                   MOVE      *BLANKS       WEHAWB
     C                   MOVE      *BLANKS       WETRI
     C                   MOVE      *BLANKS       WETRC
     C                   MOVE      *BLANKS       WETRCN
      *
     C     *ENTRY        PLIST
     C                   PARM                    WMN1              9
     C                   MOVEL     WMN1          WMN2
     C                   Z-ADD     WMN2          ZMN
      * Get msg and interchange values
     C     ZMN           CHAIN     EDIM                               51
     C     MSN           CHAIN     EDIS                               51
N01  C     *LOVAL        SETLL     FIRM
N01  C                   READ      FIRM                                   51
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      * RETREIVE INFO FROM EDIFACT DATABASE TO SYSPED400     RTVD /
      *////////////////////////////////////////////////////////////
     C     RTVD          BEGSR
      *_______________________
      * READ ALL MAIN SEGMENTS
      *"""""""""""""""""""""""
      * RFF01
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     01            ZGN0
     C     KEYMS0        SETLL     ERFF93A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS0        READE     ERFF93A                                51
     C     *IN51         DOWEQ     '0'
      *
     C     R1153         IFEQ      'FF '
     C                   MOVEL     R1154         WEREFF
     C                   MOVE      '1'           *IN51
     C                   END
      *
     C  N51KEYMS0        READE     ERFF93A                                51
     C  N51              END
     C                   END
      * DTM05
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     5             ZGN0
     C     KEYMS0        SETLL     EDTM93A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS0        READE     EDTM93A                                51
     C     *IN51         DOWEQ     '0'
      *
     C     D2005         IFEQ      '186'
     C                   MOVEL     D2380         W2380
     C                   MOVE      '1'           *IN51
     C                   END
      *
     C  N51KEYMS0        READE     ERFF93A                                51
     C  N51              END
     C                   END
      *___________________
      * READ CONSIGNEMENTS
      *"""""""""""""""""""
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     15            ZGN0
     C     KEYMS0        SETLL     ECNI93A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS0        READE     ECNI93A                                51
     C     *IN51         DOWEQ     '0'
      *
     C                   EXSR      RTVDB
      *
     C     KEYMS0        READE     ECNI93A                                51
     C  N51              ENDDO
     C                   ENDIF
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * RETREIVE INFO FROM EDIFACT DATABASE TO SYSPED400    RTVDB /
      *////////////////////////////////////////////////////////////
     C     RTVDB         BEGSR
      *__________________
      * INIT. DATAFELTER
      *""""""""""""""""""
     C                   CLEAR                   HEADFR
     C                   MOVE      'A'           HEUR
     C                   Z-ADD     12            HEAVD
     C                   MOVEL     'EDI'         HESG
     C                   MOVEL     'ENG'         HESDF
     C                   MOVEL     'OSLO'        HESDT
     C                   Z-ADD     13256         HEKNA
     C                   MOVEL     '1'           HESDLK
     C                   MOVEL     '1'           HESDL
     C                   MOVEL     'PSL'         HENAA
     C     HENAA         CAT       W2380:2       HENAA
     C                   MOVEL     WEREFF        HERFA
     C                   MOVEL     'GB'          HELKA
     C                   MOVEL     'X'           HEKDFS
     C                   MOVEL     SLIB          HELIB
     C                   MOVEL     SFILE         HEFILE
     C                   MOVEL     SMBR          HEMBR
     C                   MOVE      'IFCSUM'      HETYPE
     C                   MOVE      '93A'         HELVL
     C                   Z-ADD     ZMN           HESNN
S01  C*    MSN           MULT      10            HEUNIK
     C                   MOVEA     C1490         A
     C                   EXSR      GETNUM
S01  C*                  ADD       ZNUM          HEUNIK
N01  C     FIFIRM        CHAIN     ZTELL                              33
N01  C                   IF        *IN33
N01  C                   EVAL      FIRMA = FIFIRM
N01  C                   Z-ADD     700000001     HEUNIK
N01  C                   Z-ADD     700000002     ZUNIK
N01  C                   WRITE     ZTELLR
N01  C                   ELSE
N01  C                   Z-ADD     ZUNIK         HEUNIK
N01  C                   ADD       1             ZUNIK
N01  c                   UPDATE    ZTELLR
N01  c                   END
     C                   Z-ADD     ZNUM          WPOS1
     C                   MOVEL     WPOS1A        HEPOS1
     C                   MOVEL     WEREFF        HEREFF
     C                   MOVEL     WEHAWB        HEHAWB
     C                   MOVEL     WETRI         HETRI
     C                   MOVEL     WETRC         HETRC
     C                   MOVEL     WETRCN        HETRCN
      *__________________
      * READ ALL SEGMENTS
      *""""""""""""""""""
     C                   Z-ADD     CREP0         ZREPB0
     C                   EXSR      LOC17
     C                   EXSR      TOD17
     C                   EXSR      NAD25
     C                   EXSR      RFF29
     C                   EXSR      GID31
     C                   EXSR      LOC31
     C                   EXSR      FTX31
     C                   EXSR      MEA34
     C                   EXSR      PCI37
      *___________________
      * WRITE TO SYSPED400
      *"""""""""""""""""""
     C                   WRITE     HEADFR
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      * LOC17                                               LOC17 /
      *////////////////////////////////////////////////////////////
     C     LOC17         BEGSR
     C                   MOVE      '0'           *IN52
     C                   Z-ADD     17            ZGN1
     C     KEYMS1        SETLL     ELOC93A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS1        READE     ELOC93A                                52
     C     *IN52         DOWEQ     '0'
      *
     C     L3227         IFEQ      '1'
     C                   MOVEL     L3224         HESDT
     C                   MOVE      '1'           *IN52
     C                   END
      *
     C     KEYMS1        READE     ELOC93A                                52
     C  N52              ENDDO
     C                   ENDIF
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      * TOD17                                               TOD17 /
      *////////////////////////////////////////////////////////////
     C     TOD17         BEGSR
     C                   MOVE      '0'           *IN52
     C                   Z-ADD     17            ZGN1
     C     KEYMS1        SETLL     ETOD93A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS1        READE     ETOD93A                                52
     C     *IN52         IFEQ      '0'
      *
     C                   MOVEL     T4053         HEFR
      *
     C                   ENDIF
     C                   ENDIF
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      * NAD25                                               NAD25 /
      *////////////////////////////////////////////////////////////
     C     NAD25         BEGSR
     C                   MOVE      '0'           *IN52
     C                   Z-ADD     25            ZGN1
     C     KEYMS1        SETLL     ENAD93A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS1        READE     ENAD93A                                52
     C     *IN52         DOWEQ     '0'
      *
     C                   SELECT
     C     N3035         WHENEQ    'CZ'                                         GODSAVSENDER
     C     N3124A        IFNE      *BLANKS
     C                   MOVEL     N3124A        HENAS
     C                   MOVEL     N3124A        HENASF
     C                   ENDIF
     C     N3124B        IFNE      *BLANKS
     C                   MOVEL     N3124B        HEADS1
     C                   ENDIF
     C     N3124C        IFNE      *BLANKS
     C                   MOVEL     N3124C        HEADS2
     C                   ENDIF
     C     N3124D        IFNE      *BLANKS
     C                   MOVEL     N3124D        HEADS3
     C                   ENDIF
     C     N3036A        IFNE      *BLANKS
     C                   MOVEL     N3036A        HENAS
     C                   MOVEL     N3036A        HENASF
     C                   ENDIF
     C     N3042A        IFNE      *BLANKS
     C                   MOVEL     N3042A        HEADS1
     C                   ENDIF
     C     N3251         IFNE      *BLANKS
     C                   MOVEL     N3251         WEADS1            9
     C                   MOVEL     N3164         WEADS2           21
     C                   MOVEL     WEADS1        HEADS3
     C                   MOVE      WEADS2        HEADS3
     C                   ENDIF
     C     N3035         WHENEQ    'CN'                                         LEV.ADR
     C     N3124A        IFNE      *BLANKS
     C                   MOVEL     N3124A        HENAK
     C                   ENDIF
     C     N3124B        IFNE      *BLANKS
     C                   MOVEL     N3124B        HEADK1
     C                   ENDIF
     C     N3124C        IFNE      *BLANKS
     C                   MOVEL     N3124C        HEADK2
     C                   ENDIF
     C     N3124D        IFNE      *BLANKS
     C                   MOVEL     N3124D        HEADK3
     C                   ENDIF
     C     N3036A        IFNE      *BLANKS
     C                   MOVEL     N3036A        HENAK
     C                   ENDIF
     C     N3042A        IFNE      *BLANKS
     C                   MOVEL     N3042A        HEADK1
     C                   ENDIF
     C     N3251         IFNE      *BLANKS
     C*                    MOVELN3251     HESDVT
     C                   MOVEL     N3251         WEADS1
     C                   MOVEL     N3164         WEADS2
     C                   MOVEL     WEADS1        HEADK3
     C                   MOVE      WEADS2        HEADK3
     C                   ENDIF
      *
     C                   ENDSL
      *
     C     KEYMS1        READE     ENAD93A                                52
     C  N52              ENDDO
     C                   ENDIF
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      * RFF29                                               RFF29 /
      *////////////////////////////////////////////////////////////
     C     RFF29         BEGSR
     C                   MOVE      '0'           *IN52
     C                   Z-ADD     29            ZGN1
     C     KEYMS1        SETLL     ERFF93A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS1        READE     ERFF93A                                52
     C     *IN52         DOWEQ     '0'
      *
     C     R1153         IFEQ      'ACL'
     C                   MOVEL     R1154         HERFA
     C                   MOVEL     R1154         HEREFF
     C                   MOVE      '1'           *IN52
     C                   ENDIF
      *
     C  N52KEYMS1        READE     ERFF93A                                52
     C  N52              ENDDO
     C                   ENDIF
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      * GID31                                               GID31 /
      *////////////////////////////////////////////////////////////
     C     GID31         BEGSR
     C                   MOVE      '0'           *IN52
     C                   Z-ADD     31            ZGN1
     C     KEYMS1        SETLL     EGID93A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS1        READE     EGID93A                                52
     C     *IN52         DOWEQ     '0'
      *
     C                   MOVEA     G7224A        A
     C                   EXSR      GETNUM
     C     HENT          IFEQ      0
     C                   ADD       ZNUM          HENT
     C                   MOVEL     G7065A        HEVS1
     C                   ELSE
     C                   ADD       1             FRTLI
     C                   MOVEL(P)  G7224A        FRTTXT
     C     FRTTXT        CAT       G7065A:1      FRTTXT
     C                   WRITE     FREETXTR
     C                   ENDIF
      *
     C     KEYMS1        READE     EGID93A                                52
     C  N52              ENDDO
     C                   ENDIF
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      * LOC31                                               LOC31 /
      *////////////////////////////////////////////////////////////
     C     LOC31         BEGSR
     C                   MOVE      '0'           *IN52
     C                   Z-ADD     31            ZGN1
     C     KEYMS1        SETLL     ELOC93A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS1        READE     ELOC93A                                52
     C     *IN52         DOWEQ     '0'
      *
     C     L3227         IFEQ      '14'
     C                   MOVEL     L3224         HESDFF
     C                   MOVE      '1'           *IN52
     C                   END
      *
     C     KEYMS1        READE     ELOC93A                                52
     C  N52              ENDDO
     C                   ENDIF
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      * FTX31                                               FTX31 /
      *////////////////////////////////////////////////////////////
     C     FTX31         BEGSR
     C                   MOVE      '0'           *IN52
     C                   Z-ADD     31            ZGN1
     C     KEYMS1        SETLL     EFTX93A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS1        READE     EFTX93A                                52
     C     *IN52         DOWEQ     '0'
      *
     C     F4451         IFEQ      'AAA'
     C     HEVS1         CAT       F4440A:1      HEVS1
     C                   MOVEL     F4440B        HEVS2
     C                   MOVE      '1'           *IN52
     C                   END
      *
     C     KEYMS1        READE     EFTX93A                                52
     C  N52              ENDDO
     C                   ENDIF
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      * MEA34                                               MEA34 /
      *////////////////////////////////////////////////////////////
     C     MEA34         BEGSR
     C                   MOVE      '0'           *IN52
     C                   Z-ADD     34            ZGN1
     C     KEYMS1        SETLL     EMEA93A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS1        READE     EMEA93A                                52
     C     *IN52         DOWEQ     '0'
      *
     C                   MOVEA     M6314         A
     C                   EXSR      GETNUM
     C                   SELECT
     C     M6311         WHENEQ    'WT '
     C                   Z-ADD     ZNUM          HEVKT
     C     M6311         WHENEQ    'LMT'
     C                   Z-ADD     ZNUM          HELM
     C     M6311         WHENEQ    'VOL'
     C                   Z-ADD     ZNUM          HEM3
     C                   ENDSL
      *
     C     KEYMS1        READE     EMEA93A                                52
     C  N52              ENDDO
     C                   ENDIF
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      * PCI37                                               PCI37 /
      *////////////////////////////////////////////////////////////
     C     PCI37         BEGSR
     C                   MOVE      '0'           *IN52
     C                   Z-ADD     37            ZGN1
     C     KEYMS1        SETLL     EPCI93A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS1        READE     EPCI93A                                52
     C     *IN52         IFEQ      '0'
      *
     C                   MOVEL     P7102A        HEGM1
     C                   MOVEL     P7102B        HEGM2
      *
     C                   ENDIF
     C                   ENDIF
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      *  Get numerical value                               GETNUM /
      *////////////////////////////////////////////////////////////
     C     GETNUM        BEGSR
     C                   MOVE      *ZEROS        ZHEL
     C                   MOVE      *ZEROS        ZDEC
     C                   MOVE      *ZEROS        B
     C                   Z-ADD     1             AX                2 0
     C                   Z-ADD     1             BX                2 0
      * Get Integer
     C     A(AX)         DOWGE     '0'
     C     ZHEL          MULT      10            ZHEL
     C                   MOVE      A(AX)         ZHEL
     C                   ADD       1             AX
     C                   ENDDO
      * Decimal point ?
     C     A(AX)         IFEQ      '.'
     C     A(AX)         OREQ      ','
     C                   ADD       1             AX
     C     A(AX)         DOWGE     '0'
     C                   MOVE      A(AX)         B(BX)
     C                   ADD       1             AX
     C                   ADD       1             BX
     C                   ENDDO
     C                   MOVEA     B             ZDEC6
     C                   ENDIF
      * Concatenate
     C                   Z-ADD     ZHEL          ZNUM             18 6
     C                   ADD       ZDEC          ZNUM
     C                   ENDSR
