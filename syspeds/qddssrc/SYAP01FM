     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A          R SUBFIL                    SFL
     A            WSLETT    R        H      REFFLD(FERRR/FEFIRM *LIBL/FERR)
     A            WSNR           1A  B  6  2VALUES(' ' '2' '4')
     A            FILINE         3S 0O  6  4
     A            FI0001         4Y 2O  6  9EDTWRD('0 ,  ')
     A            FI0002         4Y 2O  6 15EDTWRD('0 ,  ')
     A            FI0003         4Y 2O  6 21EDTWRD('0 ,  ')
     A            FI0004         4Y 2O  6 27EDTWRD('0 ,  ')
     A            FITEXT        40A  O  6 33
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0016)
     A                                      SFLPAG(0015)
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A            ZRECNO         4S 0O  2 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYSPED/SYAP01'
     A                                      COLOR(BLU)
     A                                  1 20'DISIGN OF LETTER IN APF'
     A                                      COLOR(WHT)
     A                                  1 67DATE
     A                                      EDTCDE(Y)
     A                                  2  2'Letter number'
     A                                      COLOR(WHT)
     A            WSLETT         8S 0O  2 19COLOR(WHT)
     A                                  2 67TIME
     A            MSG103        30A  O  3  2MSGID(DIV 0016 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG104        30A  O  4  2MSGID(DIV 0017 SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  2'O Letter(W/H) Position    Text    -
     A                                                                         -
     A                                           '
     A                                      COLOR(WHT)
     A                                      DSPATR(UL)
     A          R BUNN
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF03(03 'EXIT')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSG201        18A  O  3  2MSGID(DIV 0020 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG202        18A  O  3 22MSGID(DIV 0021 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG203        18A  O  3 42MSGID(DIV 0022 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG204        18A  O  3 62MSGID(DIV 0023 SYMSGF)
     A                                      COLOR(BLU)
     A  98        MSG205        78A  O  4  2MSGID(DIV 0012 SYMSGF)
     A                                      COLOR(WHT)
     A          R VARELI
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  3'line  ch wi ch hi pos L pos T Text-
     A                                                                         -
     A                                       '
     A                                      COLOR(WHT)
     A            WSLINE         3Y 0B  2  4EDTCDE(4)
     A            WS0001         4Y 2B  2  9EDTWRD('0 ,  ')
     A            WS0002         4Y 2B  2 15EDTWRD('0 ,  ')
     A            WS0003         4Y 2B  2 21EDTWRD('0 ,  ')
     A            WS0004         4Y 2B  2 27EDTWRD('0 ,  ')
     A            WSTEXT        40A  B  2 33CHECK(LC)
     A            MSG302        18A  O  3 62MSGID(DIV 0024 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  4  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A          R VAREL2
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            W2LINE         3Y 0B  2  3EDTCDE(4)
     A                                      COLOR(WHT)
     A            MSG302        18A  B  3 59MSGID(DIV 0024 SYMSGF)
     A                                      COLOR(BLU)
     A          R VAREL3
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            MSG401        78A  O  1  2MSGID(DIV 3299 SYMSGF)
     A                                      COLOR(WHT)
     A            FILINE         3  0O  2  5
     A            FI0001         4Y 2O  2  9EDTWRD('  .  ')
     A            FI0002         4Y 2O  2 15EDTWRD('  .  ')
     A            FI0003         4Y 2O  2 21EDTWRD('  .  ')
     A            FI0004         4Y 2O  2 27EDTWRD('  .  ')
     A            FITEXT        40A  O  2 33
     A            MSG402        18A  O  3 62MSGID(DIV 0024 SYMSGF)
     A                                      COLOR(BLU)
     A          R NYWRK
     A                                      CF03(03 'EXIT')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                  1  2'SYSPED/SYAP01'
     A                                      COLOR(BLU)
     A                                  1 67DATE
     A                                      EDTCDE(Y)
     A                                  1 19'Design APF-letter'
     A                                      COLOR(WHT)
     A                                  2  2'Letter number'
     A                                      COLOR(WHT)
     A            WSLETT    R        O  2 19REFFLD(FERRR/FEFIRM *LIBL/FERR)
     A                                      COLOR(WHT)
     A                                  2 67TIME
     A            MSG503        30A  O  3  2MSGID(DIV 0016 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG504        30A  O  4  2MSGID(DIV 0017 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG506        18A  O 23  2MSGID(DIV 0020 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG507        18A  O 23 22MSGID(DIV 0021 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG508        18A  O 23 42MSGID(DIV 0022 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG808        18A  O 23 62MSGID(DIV 0023 SYMSGF)
     A                                      COLOR(BLU)
     A          R BILDE1
     A                                      CF03(03 'EXIT')
     A                                      CF04(04 'SEARCH')
     A                                      CF16(16 'PRINT')
     A                                  1  2'SYSPED/SYAP01'
     A                                      COLOR(BLU)
     A                                  1 19'Design IPDSletter'
     A                                      COLOR(WHT)
     A                                  1 67DATE
     A                                      EDTCDE(Y)
     A                                  2 67TIME
     A                                  4  2'Letter number'
     A                                      COLOR(WHT)
     A            WSLETT    R        B  4 20REFFLD(FERRR/FEFIRM *LIBL/FERR)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  7  2'Copy from number'
     A                                      COLOR(WHT)
     A            WSFRA     R        B  7 20REFFLD(FERRR/FEFIRM *LIBL/FERR)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            MSG706        18A  O 23  2MSGID(DIV 0020 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG707        18A  O 23 22MSGID(DIV 0032 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 42'F16 = PRINT'
     A                                      COLOR(BLU)
     A  44        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
