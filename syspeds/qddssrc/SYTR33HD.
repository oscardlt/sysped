     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HELP
     A                                      ALTHELP
      * "NOERASE" MÅ HA FOR Å IKKE TØMME BILDE SOM MAN KOMME FRA
     A          R NOERASE
     A                                      ASSUME
     A                                  1  2' '
     A          R BILDE
     A                                      WINDOW(*DFT 15 73 *NOMSGLIN)
     A                                      CF04(04 'REFRESH')
     A                                      CF05(05 'REFRESH')
     A                                      CF07(07 'KPERSS')
     A                                      CF08(08 'SEND AVS')
     A                                      CF09(09 'SEND TRAN')
     A                                      CF19(19 'SEND EDI TRAN')
     A                                      CF21(21 'SEND SMS TRAN')
     A                                      CF22(22 'SEND SMS TRAN')
     A                                      CF10(10 'SEND ANNEN')
     A                                      CF11(11 'TRACK &TRACE')
     A                                      CF03(03 'Avslutt')
     A                                      CF12(12 'Avslutt')
     A                                      BLINK
     A                                      WDWBORDER((*DSPATR HI) (*CHAR '...:-
     A                                      ::.:'))
     A                                  1  2'SYTR33H'
     A                                      COLOR(BLU)
     A                                  1 12'Håndtering av hentemail mm. '
     A                                      COLOR(WHT)
     A                                  1 43'Avd'
     A            UAVD           4Y 0O  1 47COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  1 52'Oppd'
     A            UOPD           7Y 0O  1 57COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  2  2'Mail ER sendt til:'
     A                                  2 36'Hentestatus:'
     A            HENSTAT       23   O  2 49
     A  50                                  DSPATR(RI)
     A  51                                  COLOR(RED)
     A  52                                  COLOR(PNK)
     A  53                                  COLOR(YLW)
     A  54                                  COLOR(WHT)
     A  55                                  COLOR(TRQ)
     A  56                                  COLOR(BLU)
     A            CMS01         70A  O  3  2COLOR(WHT)
     A            CMS02         70A  O  4  2COLOR(WHT)
     A            CMS03         70A  O  5  2COLOR(WHT)
     A                                  7  2'Avs.K-pers:'
     A                                      COLOR(BLU)
     A            AVSMAIL       50A  O  7 14COLOR(WHT)
     A                                  7 65'(F8=send)'
     A                                      COLOR(BLU)
     A                                  8  2'Avs.Sms...:'
     A            AVSSMS        15A  O  8 14COLOR(WHT)
     A                                  8 31'Avs.Telef.:'
     A            AVSTLF        15A  O  8 43COLOR(WHT)
     A                                  9  2'Tranportør/tildel:'
     A                                      COLOR(BLU)
     A            WSTRAN         8Y 0B  9 21EDTCDE(Z)
     A                                      DSPATR(PC)
     A  72
     AO 73                                  DSPATR(RI)
     A  72                                  COLOR(YLW)
     A  73                                  COLOR(WHT)
     A            VMNAVN        30A  O  9 30
     A  72
     AO 73                                  DSPATR(RI)
     A  72                                  COLOR(YLW)
     A  73                                  COLOR(WHT)
     A                                  9 65'(F9=send)'
     A                                      COLOR(BLU)
     A                                 10  2'Mail:'
     A            WSTMAIL       50A  B 10  8CHECK(LC)
     A                                 10 59'SMS:'
     A            WSTSMS        10A  B 10 64
     A                                 11  2'Annen/ad hoc part:'
     A                                      COLOR(BLU)
     A                                 11 64'(F10=send)'
     A                                      COLOR(BLU)
     A                                 12  2'Mail:'
     A            WSAMAIL       50A  B 12  8CHECK(LC)
     A                                 12 59'SMS:'
     A            WSASMS        10A  B 12 64
     A                                 14  2'Merkn.v/F8,F9,F10:'
     A            WSMERK        50A  B 14 21CHECK(LC)
     A                                 15  2'F4=Søk'
     A                                      COLOR(BLU)
     A                                 15  9'F7=K.pers'
     A                                      COLOR(BLU)
     A                                 15 21'F8/F9/F10=Send mail til....'
     A                                      COLOR(BLU)
     A                                 15 50'F11=T&T '
     A                                      COLOR(BLU)
     A                                 15 62'F12=Forrige'
     A                                      COLOR(BLU)
