      * ***************************************************************
      *    DETTE PROGRAM SKRIVER POSTEN ETIKETT PÅ ELTRON SKRIVER
      * ***************************************************************
      * (BASERT PÅ SYFA12E, SUBR SOM IKKE LENGER TRENGS ER SKRELLET BORT)
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
      * 01 JOV PTF10 TG MYPACK via BRING - hardkodet på PRODKODE
      * 02 SH  PTF12 NY PARAMETER KOLLIID
      *
********************************************************************
      * COMMAND Q=LENGDE / MELLOMROM M.M
      * Q p1,p2 (p1=lengde i dots, p2=mellomrom i dots )
      *
      * MÅLES I "DOTS",  PÅ ORION: 8 DOT PR MM
      * TOTALLENGDE MELLOM PERFORERING = 195MM  = 1560 DOTS
      * LENGDE PÅ DEN ENKELT LAPP      = 192MM  = 1536 DOTS
      * LENGDE PÅ MELLOMROM(GAP)       =   3MM  =   24 DOTS
      * (LENGDE FRA PERF.TIL START LAPP= 1,5MM  =   12 DOTS)
      * SE EPL2en.pdf side 244
     FDOKUF     IF   E           K DISK
     FHEADF     IF   E           K DISK
     FKOFAST    IF   E           K DISK
     FDOKUFM    UF A E           K DISK
     FFIRFB     UF   E           K DISK
     FSYLA03P   O    F   80        PRINTER OFLIND(*INOF)
     D                 DS
     D  HEFT1                  1     51
     D  HEFT1A                 1     30
     D  HEFT1B                31     45
     D                 DS
     D  DFAVD                  1      4  0
     D  DFOPD                  5     11  0
     D  DFFBNR                12     14  0
     D  DFAVDA                 1      4
     D  DFOPDA                 5     11
     D  DFFBNA                12     14
     D                 DS
     D  KOID                   1     18
     D  DSIDPK                 1      1
     D  DSIDLA                 2      3
     D  DSIDLE                 4      8
     D  DSIDNR                 9     18
     D                 DS
     D  WTIME                  1     14  0
     D  WTID                   1      6  0
     D  WDD                    7      8  0
     D  WMM                    9     10  0
     D  WÅÅ                   11     14  0
     D  WTIDA                  1      2
     D  WTIDB                  3      4
     D  WDDA                   7      8
     D  WMMA                   9     10
     D  WÅÅA                  11     14
     D                 DS
     D  WCOPN                  1      4  0
     D  WCOPA                  1      4
      * Input Parameter
     C     *ENTRY        PLIST
     C                   PARM                    WFIRM             2                         ID
     C                   PARM                    WMLAYO            2                         ID
     C                   PARM                    AVD               4 0                       ID
     C                   PARM                    OPD               7 0                       ID
     C                   PARM                    FBN               3 0                       ID
     C                   PARM                    uukopi            1
N02  C                   PARM                    uklid            35
      *
     C                   EXSR      SRINIT
      *
     C     *IN90         IFEQ      *ON
     C                   GOTO      ENDPGM
     C                   ENDIF
      * LOOP FOR NO OF COPIES
     C                   DO        DFNTLA        WCOPN
      * Send field data to printer
     C                   EXSR      SR100
      * END LOOP
     C                   ENDDO
      * End Program
     C     ENDPGM        TAG
     C                   SETON                                        LR
      *_____________________________________________________
      * Send all data fields
      *"""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     SR100         BEGSR
     C                   MOVE      *BLANKS       KOID
     c     uukopi        ifeq      'J'
     c                   z-add     wcopn         FMMKNR
     c                   move      '0'           *in33
N02  c     uklid         ifne      *blanks
N02  c                   movel     uklid         koid
N02  c                   z-add     1             DFNTLA
N02  c                   else
     C     DOKUFMKE2     SETLL     DOKUFM
     C     DOKUFMKE2     READE(N)  DOKUFM                                 33
     C     *IN33         DOWEQ     '0'
     C     FMST          IFEQ      'I'
     C                   MOVEL     FMMRK1        KOID
     C                   goto      utkoid
     C                   END
     C     DOKUFMKE2     READE     DOKUFM                                 33
     C                   END
     C                   END
     C     UTKOID        TAG
      * hvis ikke finner kolliid på posisjonen hentes id
     C     KOID          IFEQ      *blanks
     c     kollii        andeq     'J'
     c                   sub       1             FMMKNR
     C                   end
      * Hent kolli-id, dersom dette er definert
     C     KOLLII        IFEQ      'J'
     c     koid          andeq     *blanks
     C     WFIRM         CHAIN     FIRFB                              33                    3
     C                   ADD       1             FIIDNR
     C                   UPDATE    FIRFBR
     C                   MOVE      '3'           DSIDPK
     C                   MOVE      FIIDLA        DSIDLA
     C                   MOVE      FIIDLE        DSIDLE
     C                   MOVEL     FIIDNR        DSIDNR
     C                   MOVE      *ZEROS        UAN22            22
     C                   MOVE      KOID          UAN22
     C                   CALL      'SYGE08'
     C                   PARM                    UAN22
     C                   MOVE      UAN22         KOID
     C                   ADD       1             FMMKNR
     C                   MOVEL     KOID          FMMRK1
     C                   WRITE     DOKUFMR
     C                   END
N02  C                   ENDIF
      *
      ******************************************************
      * Her begynner selve produksjon av utdata til merkelapp
      ******************************************************
      * Ved lapp 2,3,4, endres kun variable data ( xx AV YY og kol-ID)
     C     *IN50         CABEQ     '1'           VARI1
      * Fra/From Pakket:
      * Param v/A:1=Kol,2=Lin,3=Rot,4=Fon,5=Brd,6=Høy,7Nor/Rev,8=Dta(i "..")
     C                   eval      DATA = 'A40,40,0,2,1,1,N,"'+
     C                             'Fra/From           "'
     C                   EXCEPT    SYLA01
      * utlev.forbehold??  - REV IMAGE / på tvers ute til høyre
     C     HEFT1A        IFNE      *blanks                                       UTLforbehold
     C     HEKFT1        ANDEQ     *blanks                                       IKKE KVITTE
     C                   eval      DATA = 'A755,65,1,3,3,3,R,"'+
     C                             %trim(HEFT1A)+
     C                             '"'
     C                   EXCEPT    SYLA01
     C                   END
      * Fra Navn
      * Param v/A:1=Kol,2=Lin,3=Rot,4=Fon,5=Brd,6=Høy,7Nor/Rev,8=Dta(i "..")
     C                   eval      DATA = 'A40,65,0,1,1,2,N,"'+
     C                             DFNAVS + '"'
     C                   EXCEPT    SYLA01
      *
      * Param v/A:1=Kol,2=Lin,3=Rot,4=Fon,5=Brd,6=Høy,7Nor/Rev,8=Dta(i "..")
     C                   eval      DATA = 'A40,95,0,1,1,2,N,"'+
     C                             DFAD1S + '"'
     C                   MOVEL     DFPNLS        PNRA              4
     C                   EXCEPT    SYLA01
      *
      * Param v/A:1=Kol,2=Lin,3=Rot,4=Fon,5=Brd,6=Høy,7Nor/Rev,8=Dta(i "..")
     C                   eval      DATA = 'A40,125,0,1,1,2,N,"'+
     C                             PNRA+' '+DFAD3S + '"'
     C                   EXCEPT    SYLA01
      *
      * STREK      p1/P2=start x/y   p3/p4 er "utstrekning" horiz/vertikalt
      *                             (lengde/tykkelse ved liggende, tykkelse/lengde ved stående)
      * Horisontal
     C                   eval      DATA = 'LO10,150,700,10'
     C                   EXCEPT    SYLA01
      * Vertikal venstre
     C                   eval      DATA = 'LO10,150,10,340'
     C                   EXCEPT    SYLA01
      * Vertikal høyre
     C                   eval      DATA = 'LO690,150,10,340'
     C                   EXCEPT    SYLA01
      *
      * Til (MOTTAKER)
      * Param v/A:1=Kol,2=Lin,3=Rot,4=Fon,5=Brd,6=Høy,7Nor/Rev,8=Dta(i "..")
     C*                  eval      DATA = 'A40,170,0,2,1,1,N,"Til/To"'
     C                   eval      DATA = 'A40,170,0,2,1,1,N,"Til/To' +
     C                             '                         Date: '+
     C                             WDDA+'/'+WMMA+'/'+WÅÅA+'  '+
     C                             WTIDA+':'+WTIDB+'"'
     C                   EXCEPT    SYLA01
      * Ledetekst
      * Til Navn
      * Param v/A:1=Kol,2=Lin,3=Rot,4=Fon,5=Brd,6=Høy,7Nor/Rev,8=Dta(i "..")
     C                   eval      DATA = 'A40,190,0,4,1,2,N,"'+
     C                             DFNAVL + '"'
     C                   EXCEPT    SYLA01
      * Utlev-ledet.
      * Param v/A:1=Kol,2=Lin,3=Rot,4=Fon,5=Brd,6=Høy,7Nor/Rev,8=Dta(i "..")
     C                   eval      DATA = 'A40,235,0,2,1,1,N,"'+
     C                             'Utleveringsadresse"'
     C                   EXCEPT    SYLA01
      * Postnr 1   .
      * Param v/A:1=Kol,2=Lin,3=Rot,4=Fon,5=Brd,6=Høy,7Nor/Rev,8=Dta(i "..")
     C                   MOVEL     DFPOUL        PNRA              4
     C                   eval      DATA = 'A40,260,0,5,1,2,N,"'+
     C                             PNRA + '"'
     C                   EXCEPT    SYLA01
      * Levadr 1   .
      * Param v/A:1=Kol,2=Lin,3=Rot,4=Fon,5=Brd,6=Høy,7Nor/Rev,8=Dta(i "..")
     C                   eval      DATA = 'A200,270,0,4,1,2,N,"'+
     C                             DFAD1L + '"'
     C                   EXCEPT    SYLA01
      *
      * STREK
     C                   eval      DATA = 'LO10,370,700,3'
     C                   EXCEPT    SYLA01
      * Postadr
      * Param v/A:1=Kol,2=Lin,3=Rot,4=Fon,5=Brd,6=Høy,7Nor/Rev,8=Dta(i "..")
     C                   eval      DATA = 'A40,380,0,2,1,1,N,"'+
     C                             'Postadresse"'
     C                   EXCEPT    SYLA01
      * Telefon - ledetekst
      * Param v/A:1=Kol,2=Lin,3=Rot,4=Fon,5=Brd,6=Høy,7Nor/Rev,8=Dta(i "..")
     C                   eval      DATA = 'A500,380,0,2,1,1,N,"'+
     C                             'Telefon/Phone"'
     C                   EXCEPT    SYLA01
      * Param v/A:1=Kol,2=Lin,3=Rot,4=Fon,5=Brd,6=Høy,7Nor/Rev,8=Dta(i "..")
     C                   eval      DATA = 'A40,400,0,3,1,2,N,"'+
     C                             DFAD1M + '"'
     C                   EXCEPT    SYLA01
      *
      * Param v/A:1=Kol,2=Lin,3=Rot,4=Fon,5=Brd,6=Høy,7Nor/Rev,8=Dta(i "..")
     C                   eval      DATA = 'A40,445,0,3,1,2,N,"'+
     C                             DFAD3M + '"'
     C                   EXCEPT    SYLA01
      *
      * STREK
     C                   eval      DATA = 'LO10,480,680,10'
     C                   EXCEPT    SYLA01
      * KolliID ( i klartekts MIDT PÅ LAPPEN)
      * Param v/A:1=Kol,2=Lin,3=Rot,4=Fon,5=Brd,6=Høy,7Nor/Rev,8=Dta(i "..")
     C                   eval      DATA = 'A40,500,0,2,1,1,N,"'+
     C                             'KolliID/Licence plate no"'
     C                   EXCEPT    SYLA01
      * Param v/A:1=Kol,2=Lin,3=Rot,4=Fon,5=Brd,6=Høy,7Nor/Rev,8=Dta(i "..")
     C                   eval      DATA = 'A400,500,0,2,1,1,N,"'+
     C                             '               Volum dm3"'
     C                   EXCEPT    SYLA01
      * Selve kolli-iD skrives ut i den variable delen i slutten
      *
      * Licence plate
      * Param v/A:1=Kol,2=Lin,3=Rot,4=Fon,5=Brd,6=Høy,7Nor/Rev,8=Dta(i "..")
     C                   eval      DATA = 'A40,570,0,2,1,1,N,"'+
     C                             'SendingsID/Consignment ID"'
     C                   EXCEPT    SYLA01
      * Param v/A:1=Kol,2=Lin,3=Rot,4=Fon,5=Brd,6=Høy,7Nor/Rev,8=Dta(i "..")
     C                   eval      DATA = 'A400,570,0,2,1,1,N,"'+
     C                             'Kolli          Kollivekt"'
     C                   EXCEPT    SYLA01
     C                   eval      DATA = 'A40,590,0,4,1,2,N,"'+
     C                             '401'+DF1004+'"'
     C                   EXCEPT    SYLA01
      * STREK
     C                   eval      DATA = 'LE10,640,700,3'
     C                   EXCEPT    SYLA01
      * TRANSPORT-PRODUKT - prod-id
      * Param v/A:1=Kol,2=Lin,3=Rot,4=Fon,5=Brd,6=Høy,7Nor/Rev,8=Dta(i "..")
     C                   eval      DATA = 'A115,645,0,2,1,1,N,"'+
     C                             'ProduktID"'
     C                   EXCEPT    SYLA01
      *
     C                   MOVE      *BLANKS       PRODKO
     C                   SELECT
     C     DFPROK        WHENEQ    'POZX1'
     C                   MOVEL     '1002'        PRODKO            4
     C     DFPROK        WHENEQ    'POZX2'
     C                   MOVEL     '1854'        PRODKO
     C     DFPROK        WHENEQ    'POZB1'
     C                   MOVEL     '1006'        PRODKO
     C     DFPROK        WHENEQ    'PO7  '
     C                   MOVEL     '1000'        PRODKO
     C     DFPROK        WHENEQ    'PO12 '
     C                   MOVEL     '1202'        PRODKO
     C     DFPROK        WHENEQ    'PO2  '
     C                   MOVEL     '1312'        PRODKO
     C     DFPROK        WHENEQ    'PO2  '
     C                   MOVEL     '1312'        PRODKO
     C     DFPROK        WHENEQ    'POXHD'
     C                   MOVEL     '1736'        PRODKO
     C     DFPROK        WHENEQ    'PO8  '
     C                   MOVEL     '1020'        PRODKO
     C     DFPROK        WHENEQ    'POZX3'
     C                   MOVEL     '1884'        PRODKO
     C     DFPROK        WHENEQ    'PO9  '
     C                   MOVEL     '1988'        PRODKO
     C                   ENDSL
     C                   eval      DATA = 'A115,665,0,4,1,2,N,"'+
     C                             PRODKO+'"'
     C                   EXCEPT    SYLA01
      * TRANSPORT-PRODUKT - KODETEKST
     C     KOKEY         CHAIN     KOFAST                             56
     C     *IN56         IFEQ      '0'
     C                   eval      DATA = 'A115,710,0,4,1,2,N,"'+
     C                             %trim(KFTXT)+'"'
     C                   EXCEPT    SYLA01
     C                   END
      * Kunde..
      * Param v/A:1=Kol,2=Lin,3=Rot,4=Fon,5=Brd,6=Høy,7Nor/Rev,8=Dta(i "..")
     C                   eval      DATA = 'A115,760,0,2,1,1,N,"'+
     C                             'Kundenr/Aktørnr"'
     C                   EXCEPT    SYLA01
     C                   eval      DATA = 'A300,760,0,3,1,2,N,"'+
     C                             DFKNS+'"'
     C                   EXCEPT    SYLA01
      *
      * Prod.prioritet
     C                   MOVE      *BLANKS       PRODKO
     C                   SELECT
     C     DFPROK        WHENEQ    'POZX1'
     C     DFPROK        OREQ      'POZX2'
     C                   MOVEL     '   1'        PRODKO
     C     DFPROK        WHENEQ    'PO7  '
     C     DFPROK        OREQ      'POZB1'
     C     DFPROK        OREQ      'PO8  '
     C     DFPROK        OREQ      'POZX3'
     C                   MOVEL     '   2'        PRODKO
     C     DFPROK        WHENEQ    'PO12'
     C     DFPROK        OREQ      'PO9 '
     C                   MOVEL     '   3'        PRODKO
     C     DFPROK        WHENEQ    'POXHD'
     C                   MOVEL     'H  2'        PRODKO
     C     DFPROK        WHENEQ    'PO2  '
     C                   MOVEL     'HP 2'        PRODKO
     C                   ENDSL
     C                   eval      DATA = 'A570,650,0,5,1,2,N,"'+
     C                             PRODKO+'"'
     C                   EXCEPT    SYLA01
      * STREK (tynn)
     C                   eval      DATA = 'LE10,800,700,3'
     C                   EXCEPT    SYLA01
      * beløp..
      * Param v/A:1=Kol,2=Lin,3=Rot,4=Fon,5=Brd,6=Høy,7Nor/Rev,8=Dta(i "..")
     C                   eval      DATA = 'A40,820,0,1,1,1,N,"'+
     C                             'Beløp kr                           Kontonr"'
     C                   EXCEPT    SYLA01
      * STREK (tynn)
     C                   eval      DATA = 'LE10,900,700,3'
     C                   EXCEPT    SYLA01
      * Transportinstruksjo
      * Param v/A:1=Kol,2=Lin,3=Rot,4=Fon,5=Brd,6=Høy,7Nor/Rev,8=Dta(i "..")
     C                   eval      DATA = 'A40,920,0,1,1,1,N,"'+
     C                             'Transportinstruksjoner"'
     C                   EXCEPT    SYLA01
      * STREK (tynn)
     C                   eval      DATA = 'LE10,1100,700,3'
     C                   EXCEPT    SYLA01
      * Innhold/ref
      * Param v/A:1=Kol,2=Lin,3=Rot,4=Fon,5=Brd,6=Høy,7Nor/Rev,8=Dta(i "..")
     C                   eval      DATA = 'A40,1120,0,1,1,1,N,"'+
     C                             'Innhold/Referanse"'
     C                   EXCEPT    SYLA01
     C                   eval      DATA = 'A40,1140,0,1,2,2,N,"'+
     C                             DFAVDA+'/'+DFOPDA+'/'+DFFBNA+'/'+DFSG+'"'
     C                   EXCEPT    SYLA01
      * STREK (tykk)
     C                   eval      DATA = 'LE10,1220,700,6'
     C                   EXCEPT    SYLA01
      *
      * KolliID ( ledetektst for strekkoden)
      * Param v/A:1=Kol,2=Lin,3=Rot,4=Fon,5=Brd,6=Høy,7Nor/Rev,8=Dta(i "..")
     C                   eval      DATA = 'A40,1240,0,2,2,1,N,"'+
     C                             'LICENCE PLATE No"'
     C                   EXCEPT    SYLA01
      *
      * Data
     C     VARI1         TAG
      * Kolli-ID (midt på lappen)
      * Param v/A:1=Kol,2=Lin,3=Rot,4=Fon,5=Brd,6=Høy,7Nor/Rev,8=Dta(i "..")
     C                   eval      DATA = 'A40,520,0,4,1,2,N,"'+
     C                             '00'+KOID+'"'
     C                   EXCEPT    SYLA01
      * Kolli-ID strekkode
     C*                  eval      DATA = 'B18,1260,0,1E,4,8,220,B,"'+
     C                   eval      DATA = 'B65,1260,0,1E,4,8,220,B,"'+
     C                             '00'+KOID+'"'
     C                   EXCEPT    SYLA01
      * POSTHORN-grafikk
     C                   eval      DATA = 'GG10,660,"POSTHORN"'
     C                   EXCEPT    SYLA01
      * Print 1 eksemplar
     C                   MOVEL     *BLANKS       DATA
     C                   MOVEL     'P1'          DATA
     C                   EXCEPT    SYLA01
      *
     C                   MOVE      *ON           *IN50                           FØRSTE FERD
      *
     C                   ENDSR
      *_____________________________________________________
      * Initialise subroutine, set character set
      *"""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     SRINIT        BEGSR
     C                   MOVE      *OFF          *IN50
      *
     C                   Z-ADD     0             ØX                3 0          Index for Ø
     C                   MOVE      *BLANKS       ØAN              12            Num alpha
     C                   MOVE      'N'           ØADEC             1            Decimal Indicator
     C                   Z-ADD     0             ÆX                3 0
     C                   Z-ADD     0             X                 1 0
     C                   Z-ADD     0             Y                 1 0
     C                   MOVE      *BLANKS       MSGVAR            7            Message Variabel
     C     DOKKEY        KLIST
     C                   KFLD                    DFRI
     C                   KFLD                    AVD
     C                   KFLD                    OPD
     C                   KFLD                    FBN
     C                   MOVE      'F'           DFRI
     C     DOKMKE        KLIST
     C                   KFLD                    FMRI
     C                   KFLD                    FMAVD
     C                   KFLD                    FMOPD
     C                   KFLD                    FMFBNR
     C                   KFLD                    FMLINR
     C     DOKUFMKE2     KLIST
     C                   KFLD                    FMRI
     C                   KFLD                    FMAVD
     C                   KFLD                    FMOPD
     C                   KFLD                    FMFBNR
     C                   KFLD                    FMLINR
     c                   KFLD                    FMMKNR
     C     KOKEY         KLIST
     C                   KFLD                    KFTYP
     C                   KFLD                    DFPROK
     C                   MOVEL     'FRBREVPR'    KFTYP
     C                   MOVE      'O '          KFTYP
     C     HEAKEY        KLIST
     C                   KFLD                    AVD
     C                   KFLD                    OPD
      * HENT FRAKTBREV
     C     DOKKEY        CHAIN     DOKUF                              90
     C     HEAKEY        CHAIN     HEADF                              33
N01  c     DFPROK        ifeq      'TG9M'
N01  c     DFPROK        oreq      'TG9MB'
N01  C                   eval      DFPROK ='P412 '
N01  c                   eval      DFKNS  ='00098287279'
N01  c                   eval      DFKNM  =*blanks
N01  c                   endif
      * SKAL KOLLI-ID TILDELES??
     C                   MOVE      'N'           KOLLII            1
     C     WFIRM         CHAIN(N)  FIRFB                              33                    3
     C     *IN33         IFEQ      '0'
     C     FIIDNR        ANDNE     *ZEROS
     C                   MOVE      'J'           KOLLII
      * SLETT EVT. TIDLIGERE TILDELT KOLLIID
     C                   MOVE      DFRI          FMRI
     C                   MOVE      DFAVD         FMAVD
     C                   MOVE      DFOPD         FMOPD
     C                   MOVE      DFFBNR        FMFBNR
     C                   MOVE      *ZEROS        FMLINR
      * ikke slett hvis uukopi = J
     c     uukopi        ifne      'J'
     C     DOKMKE        SETLL     DOKUFM
     C     DOKMKE        READE     DOKUFM                                 33                   33
     C     *IN33         DOWEQ     '0'
     C     FMST          IFEQ      'I'
     C                   DELETE    DOKUFMR
     C                   ELSE
     C                   UPDATE    DOKUFMR
     C                   END
     C     DOKMKE        READE     DOKUFM                                 33                   33
     C                   END
      * INITIER FOR UTLEGGING AV NYE
     C                   MOVE      *ZEROS        FMMKNR
     C                   MOVE      *ZEROS        FMLONR
     C                   MOVE      *BLANKS       FMMRK1
     C                   MOVE      'I'           FMST
     C                   end
     C                   END
     C* Initiering/ Printparam / label size
     C                   MOVE      *BLANKS       DATA             80
     C                   EXCEPT    SYLA01
     C                   MOVEL     'N       '    DATA
     C                   EXCEPT    SYLA01
     C                   MOVEL     'D7      '    DATA
     C                   EXCEPT    SYLA01
     C                   MOVEL     'S2      '    DATA
     C                   EXCEPT    SYLA01
     C                   MOVEL     'Q1536,24'    DATA
     C                   EXCEPT    SYLA01
     C                   MOVEL     'q760    '    DATA
     C                   EXCEPT    SYLA01
      * HENTE DAGENS DATO OG TID
     C                   TIME                    WTIME
      *
     C                   ENDSR
     OSYLA03P   E            SYLA01
     O                       DATA                80
