     A*%%TS  SD  20160822  102843  SVEIN       REL-V7R1M0  5770-WDS
*****A**************************************************************
     A* RETTINGER I DETTE PROGRAM:
PTFnrA*Sek Sig Vers Beskrivelse...........................
"""""A*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
P5105A* 01 YBC PTF5 LENGDE PÅ ADRESSENR ER ØKT FRA 3.0 TIL 5.0
*****A**************************************************************
     A*%%EC
     A                                      DSPSIZ(27 132 *DS4)
     A                                      PRINT
     A                                      HLPDOC('STD' 'HELPNU' 'HLP400')
     A                                      HELP
     A                                      ALTHELP(CA01)
      * "NOERASE" MÅ HA FOR Å IKKE TØMME BILDE SOM MAN KOMME FRA
     A          R NOERASE
     A                                      ASSUME
     A                                  1  2' '
      * MAN KUN DEFINNERE VINDU PÅ REKORDNIVÅ
     A          R WINDOWSZ
     A                                      WINDOW(*DFT 14 126 *NOMSGLIN)
     A                                      OVERLAY
     A*
     A          R SUBFIL                    SFL
     A*%%TS  SD  20160822  102035  SVEIN       REL-V7R1M0  5770-WDS
     A            VADRN2        30A  H
     A            VALAND         2A  H
     A            WSNR           1A  I  6  2VALUES(' ' '1')
     A            VADRNR         5Y 0O  6  5TEXT('Vareadressenr.')
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            VADRNA        30   O  6 11TEXT('Navn')
     A            VADRN1        30   O  6 42TEXT('Navn')
     A            WADRN2        23   O  6 73
     A            VADRN3        30   O  6 97TEXT('Navn')
     A*
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A*%%TS  SD  20160822  102035  SVEIN       REL-V7R1M0  5770-WDS
     A                                      SFLSIZ(0009)
     A                                      SFLPAG(0008)
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A                                      CA03(03 'Forrige')
     A                                      CA05(05 'Frisk')
     A                                      CA06(06 'LAGE NY')
     A                                      CA12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SOEKVADR'
     A                                      COLOR(BLU)
     A                                  1 10MSGCON(050 DIV1690 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  2  2MSGCON(010 DIV0018 *LIBL/SYMSGF)
     A            WKUNR          8Y 0O  2 14COLOR(BLU)
     A                                      EDTCDE(4)
     A            KNAVN         30A  O  2 24TEXT('Kundenavn')
     A                                      COLOR(BLU)
     A                                  3  2MSGCON(025 DIV1034 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A            ZRECNO         4S 0O  3 68SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  4  2MSGCON(070 YBC0250 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            W2NR           1A  I  5  2VALUES(' ' '1' '5')
     A                                      DSPATR(UL)
     A            W2KOD          5D 0I  5  5DSPATR(UL)
     A            W2SØK         30A  I  5 11DSPATR(UL)
     A            W2PNR          4A  I  5 97DSPATR(UL)
      *
     A          R BUNN
     A                                      WINDOW(WINDOWSZ)
     A                                      SLNO(14)
     A                                      CLRL(01)
     A                                      OVERLAY
     A N30                              1  2MSGCON(29 YBC0212 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        28A  O  1  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R NYWRK
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A                                      CA03(03 'Forrige')
     A                                      CA05(05 'Frisk')
     A                                      CA06(06 'LAGE NY')
     A                                      CA12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1  2'SOEKVADR'
     A                                      COLOR(BLU)
     A                                  1 10MSGCON(050 DIV1690 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  2  2MSGCON(010 DIV0018 *LIBL/SYMSGF)
     A            WKUNR          8Y 0B  2 14COLOR(BLU)
     A                                      EDTCDE(4)
     A            KNAVN         30A  O  2 24TEXT('Kundenavn')
     A                                      COLOR(BLU)
     A                                  3  2MSGCON(025 DIV1034 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(070 YBC0250 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            W2NR           1A  I  5  2VALUES(' ' '1' '5')
     A                                      DSPATR(UL)
     A            W2KOD          5D 0I  5  5DSPATR(UL)
     A            W2SØK         30A  I  5 11DSPATR(UL)
     A            W2PNR          4A  I  5 63DSPATR(UL)
     A N30                             14  2MSGCON(029 YBC0212 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        28A  O 14  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
