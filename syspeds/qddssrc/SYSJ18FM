********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10199 * 01 YBC PTF10 TO NYE DATO FOR GATE IN OG OUT, OPSJON 2 FOR ENDRING
********************************************************************
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HELP
     A          R SUBFIL                    SFL
S01  A*           WSNR           1A  I  5  2VALUES(' ' '5')
N01  A            WSNR           1A  I  5  2VALUES('2' '5' '9')
N01  A            BVLI           3  0H
0N1  A            BLST           1A  H
     A            BLMN          17   H
     A            BLMN15        15   O  5  4
     A  31                                  COLOR(RED)
     A            BLPRO          8  0O  5 20EDTCDE(4)
     A  31                                  COLOR(RED)
     A            XDTA           6  0O  5 29EDTCDE(4)
     A  31                                  COLOR(RED)
     A            BLAVD          4  0O  5 36EDTCDE(4)
     A  31                                  COLOR(RED)
     A            BLOPD          7  0O  5 41EDTCDE(4)
     A  31                                  COLOR(RED)
     A            BLSG           3   O  5 49
     A  31                                  COLOR(RED)
     A            BLKNR          8  0O  5 53EDTCDE(4)
     A  31                                  COLOR(RED)
     A            BLTR           4   O  5 62
     A  31                                  COLOR(RED)
     A            BLLB           5   O  5 67
     A  31                                  COLOR(RED)
     A            BLOB           5   O  5 73
     A  31                                  COLOR(RED)
     A            BLUR           1   O  5 79
     A  31                                  COLOR(RED)
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0017)
     A                                      SFLPAG(0016)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94 95 96
     AA 97                                  SFLEND
     A  91N93N94
     AO 91N93N95
     AO 91N93N96
     AO 91N93N97                            ROLLUP(25)
     A  91N93N94
     AO 91N93N95
     AO 91N93N96
     AO 91N93N97                            ROLLDOWN(26)
     A            ZRECNO         4S 0O  1 77SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYSPED/SYSJ18'
     A                                      COLOR(BLU)
     A                                  1 61DATE
     A                                      EDTCDE(Y)
     A                                  1 70TIME
     A                                  1 20MSGCON(40 DIV3656 SYMSGF)
     A                                      COLOR(WHT)
N01  A                                  2 61'2=Endre B/L'
N01  A                                      COLOR(BLU)
     A                                  3  2MSGCON(26 DIV0016 SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 32MSGCON(22 DIV3658 SYMSGF)
     A                                      COLOR(BLU)
N01  A                                  3 61'9=Endre Gate in/out'
N01  A                                      COLOR(BLU)
     A                                  4  1MSGCON(79 DIV3657 SYMSGF)
     A                                      COLOR(WHT)
     A          R BUNN
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF03(03 'Avslutt')
     A                                      CF05(05 'Frisk')
     A                                      CF08(08 'SØK')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  3  2MSGCON(18 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 22MSGCON(18 DIV0023 SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 42MSGCON(18 DIV0021 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  4  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                              4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A          R VAREL3
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A                                  2  2MSGCON(27 DIV3659 SYMSGF)
     A            W2MN          17A  I  2 30DSPATR(UL)
     A                                      DSPATR(PC)
     A                                  2 49MSGCON(9 DIV3660 SYMSGF)
     A            W2AVD          4  0I  2 59DSPATR(UL)
     A                                  2 64MSGCON(8 DIV3661 SYMSGF)
     A            W2OPD          7  0I  2 73DSPATR(UL)
     A                                  3  2MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A          R NYVARE
     A                                      OVERLAY
     A                                  1  2'SYSPED/SYSJ18'
     A                                      COLOR(BLU)
     A                                  1 61DATE
     A                                      EDTCDE(Y)
     A                                  1 70TIME
     A                                  1 20MSGCON(40 DIV3656 SYMSGF)
     A                                      COLOR(WHT)
N01  A                                  2 61'2=Endre B/L'
N01  A                                      COLOR(BLU)
     A                                  3  2MSGCON(26 DIV0016 SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 32MSGCON(22 DIV3658 SYMSGF)
     A                                      COLOR(BLU)
N01  A                                  3 61'9=Endre Gate in/out'
N01  A                                      COLOR(BLU)
     A                                  4  1MSGCON(79 DIV3657 SYMSGF)
     A                                      COLOR(WHT)
N01   *
N01  A          R BILDE01
N01  A                                      WINDOW(*DFT 4 41 *NOMSGLIN)
N01  A                                      OVERLAY
N01  A                                  1  2'SYSJ18'
N01  A                                      COLOR(BLU)
N01  A                                  1 10MSGCON(30 DIV3656 SYMSGF)
N01  A                                      DSPATR(HI)
N01  A                                  2  2'Gate out:'
N01  A            WSGTUT         6Y 0B  2 12DSPATR(UL)
N01  A                                      EDTCDE(Z)
N01  A  90                                  DSPATR(RI)
N01  A  90                                  DSPATR(PC)
N01  A                                  3  2'Gate in: '
N01  A            WSGTIN         6Y 0B  3 12DSPATR(UL)
N01  A                                      EDTCDE(Z)
N01  A  89                                  DSPATR(RI)
N01  A  89                                  DSPATR(PC)
N01  A  30        MSGTXT2       40A  O  4  2MSGID(&MSGNR SYMSGF)
N01  A                                      COLOR(WHT)
N01  A            MSGNR          7A  P
