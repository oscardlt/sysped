********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
P7XXX * 01 JOV PTF7 HENT GRUPPE VIA KUNDENR
P8XXX * 02 YBC PTF8 FIRE NYE FRITEKST LINJER
P8XXX * 03 YBC PTF8 LANDKODE
P11XX * 04 SH  PTF11 innført valutakode
P11XX * 05 SH  PTF11 vise fra recordnummer 1
P11XX * 06 SH  PTF11 B=Bekreft
********************************************************************
      *
      ***********************************************************
      *** PROGRAM SKAL ARBEIDE MED STYKK PR ARTIKKEL          ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01 02 07-11 13-24                ***
      *** LEDIGE INDIKATORER 29 31 32 34-39 41-49 51-84 95-97 ***
      **************************************************b********
      *
N04  FFIRM      UF A E           K DISK
     FGSSAKF    UF A E           K DISK
     FGSSORF    IF   E           K DISK
     FGSSPOL01  IF   E           K DISK
     FGSSPAF    IF   E           K DISK
     FLLVARE01  IF   E           K DISK
N01  FLLGRPK05  IF   E           K DISK
N03  FKODTLK    IF   E           K DISK
     FGSS013D   CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     F                                     INFDS(FBAREA)
     D PO              S             20    DIM(6)
     D FBAREA          DS
     D  WSCLOC               382    383B 0
     D  WSSUBN               378    379B 0
      *
     C                   EXSR      INIT
      *
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
     C                   MOVE      *ZEROS        XLNO              3 0
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     UONO          SETLL     GSSAKF
     C     UONO          READE(N)  GSSAKF                                 94
     C     *IN94         DOWEQ     '0'
     C                   ADD       1             ZRECNO
     C                   MOVE      AKLNO         XLNO
     C                   WRITE     SUBFIL
     C     UONO          READE(N)  GSSAKF                                 94
     C  N94              ENDDO
     C                   ADD       1             XLNO
     C                   Z-ADD     ZRECNO        YRECNO            4 0
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     2             POSNBR
      *
     C     START3        TAG
     C                   SETOFF                                       9998
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYWRK
     C                   ELSE
n05  c                   z-add     1             zrecno
     C                   WRITE     BUNN
     C                   EXFMT     SUBCTL
     C                   END
      *
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C                   Z-ADD     WSSUBN        XRECNO            4 0
      *
     C                   SETOFF                                       9830
      *** AVSLUTT
     C   03
     COR 12              GOTO      SLUTT
      *** FRISK OPP
     C   05              GOTO      START
      *** LAGE NY
     C   06              DO
     C                   EXSR      MOVB2
     C                   GOTO      START5
     C                   END
      *
     C     ZRECNO        IFNE      0
     C                   EXSR      HENT
     C                   END
     C   98XRECNO        IFNE      0
     C                   Z-ADD     XRECNO        ZRECNO
     C                   END
     C                   GOTO      START3
      *
     C     START5        TAG
     C                   EXFMT     BILDE01
S03  C*                  MOVEA     '00000'       *IN(86)
N03  C                   MOVEA     '000000'      *IN(85)
     C                   MOVE      '0'           *IN30
      *
     C   04              DO
     C                   EXSR      SØKDTA
     C                   GOTO      START5
     C                   END
     C   12              EXSR      MOVB2
     C  N12              EXSR      TESTB1
     C  N12
     CAN 30              GOTO      START5
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     2             POSNBR
     C                   GOTO      START3
      *
     C     SLUTT         TAG
     C  N27              EXSR      OPDORD
     C   03              Z-ADD     3             UFLAGG
     C   12              Z-ADD     1             UFLAGG
     C                   SETON                                        LR
     C                   RETURN
      *
     C***********************************************
     C     TESTB1        BEGSR
     C***********************************************
     C     LVAR1K        CHAIN     LLVARE01                           90
     C     *IN90         IFEQ      '1'
     C                   MOVE      'YBC0585'     MSGNR
     C                   SETON                                        30
     C                   Z-ADD     3             LINNBR
     C                   Z-ADD     12            POSNBR
     C                   END
N03   *
N03  C                   IF        WSLK <> *BLANKS
N03  C     KODTLKK       CHAIN     KODTLK                             86
N03  C  N30*IN86         IFEQ      '0'
N03  C                   MOVE      'SPL0001'     MSGNR
N03  C                   Z-ADD     3             LINNBR
N03  C                   Z-ADD     36            POSNBR
N03  C                   SETON                                        30
N03  C                   END
N03  C                   END
      *
     C     WSCONO        COMP      *BLANKS                                89
     C     *IN89         IFEQ      '0'
     C                   EXSR      TESTPO
     C  N33              MOVE      '1'           *IN30
     C                   ELSE
     C     *IN30         IFEQ      '0'
     C                   MOVE      'YBC1849'     MSGNR
     C                   Z-ADD     4             LINNBR
     C                   Z-ADD     12            POSNBR
     C                   SETON                                        30
     C                   END
     C                   END
      *
     C     WSSTK         COMP      *ZEROS                                 88
     C  N30*IN88         IFEQ      '1'
     C                   MOVE      'YBC1838'     MSGNR
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     12            POSNBR
     C                   SETON                                        30
     C                   END
      *
E03  C     WSPR          COMP      0                                      85
E03  C  N30*IN85         IFEQ      '1'
     C                   MOVE      'GSS0065'     MSGNR
     C                   Z-ADD     7             LINNBR
     C                   Z-ADD     12            POSNBR
     C                   SETON                                        30
     C                   END
      *
     C   30              GOTO      SLTB1
      *
      *
      *** OPPDATERE PÅ REGISTER OG SUBFIL
     C   40AKKEY1        CHAIN     GSSAKF                             33
     C  N40AKKEY2        CHAIN     GSSAKF                             33
     C  N33
     CANN40              GOTO      SLTB1
      *
     C                   EXSR      MOVWAK
     C  N33              UPDATE    GSSAKFR
     C   33              MOVE      'M'           AKEDI
     C   33              WRITE     GSSAKFR
      *
     C   40              UPDATE    SUBFIL
     C  N40              ADD       1             YRECNO
     C  N40              Z-ADD     YRECNO        ZRECNO
     C  N40              WRITE     SUBFIL
     C                   WRITE     SUBCTL
     C                   EXSR      MOVB2
      *
     C     SLTB1         ENDSR
      *
     C***********************************************
     C     TESTPO        BEGSR
     C***********************************************
     C     WSCONO        SETLL     GSSPOL01
     C     WSCONO        READE     GSSPOL01                               33
     C     *IN33         DOWEQ     '0'
     C     UONO          IFNE      POONO
     C     POONO         CHAIN     GSSORF                             50
     C     ORBCD         IFEQ      UBCD
     C     POONO         CHAIN     GSSPAF                             50
     C  N50PAPKD         COMP      ' '                                5050
     C                   MOVE      'N'           WSOK
     C                   EXFMT     BILDE03
     C     WSOK          COMP      'N'                                3333
     C                   LEAVE
     C                   END
     C                   END
     C     WSCONO        READE     GSSPOL01                               33
     C  N33              ENDDO
      *
     C                   ENDSR
      *
     C***********************************************
     C     HENT          BEGSR
     C***********************************************
     C                   Z-ADD     0             LINNBR
     C                   SETOFF                                       919299
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C   98              DO
     C   99              SETOFF                                       98
     C                   Z-ADD     1             LINNBR
     C                   GOTO      SLHENT
     C                   END
      *
     C     ZRECNO        DIV       15            LINNBR
     C                   MVR                     XLIN              2 0
     C     XLIN          IFEQ      0
     C                   Z-ADD     15            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
      *
      *
      * 2 = ENDRING
      *
     C  N27WSNR          IFEQ      '2'
     C                   MOVE      ' '           WSNR
     C     WSANR         COMP      '*DELETE'                              98
     C   98              GOTO      SLHENT
     C                   EXSR      CHGAK
     C   12              GOTO      SLHENT
     C                   END
      *
      * 4 = SLETTING
      *
     C  N27WSNR          IFEQ      '4'
     C     AKEDI         ANDNE     'E'
     C                   MOVE      ' '           WSNR
     C     WSANR         COMP      '*DELETE'                              98
     C   98              GOTO      SLHENT
     C                   EXSR      DLTAK
     C   12              GOTO      SLHENT
     C                   END
      *
      *
      * 5 = VISE
      *
     C     WSNR          IFEQ      '5'
     C                   MOVE      ' '           WSNR
     C     WSANR         COMP      '*DELETE'                              98
     C   98              GOTO      SLHENT
     C                   EXSR      DSPAK
     C   12              GOTO      SLHENT
     C                   END
      *
      *
N06   * B = BEKREFT
      *
N06  C     WSNR          IFEQ      'B'
N06  C     AKEDI         IFEQ      'B'
N06  C                   MOVE      'SHU1527'     MSGNR
N06  C                   SETON                                            30
N06  C                   MOVE      ' '           WSNR
N06  C                   UPDATE    SUBFIL
N06  C                   GOTO      SLHENT2
N06  C                   ELSE
N06  C                   MOVE      ' '           WSNR
N06  C     WSANR         COMP      '*DELETE'                              98
N06  C   98              GOTO      SLHENT
N06  C                   EXSR      BEKREFT
N06  C                   END
N06  C                   END
      *
     C                   END
      *
     C     SLHENT        TAG
     C                   ADD       4             LINNBR
     C                   Z-ADD     2             POSNBR
     C                   SETOFF                                       99
     C   98              DO
     C                   MOVE      'DIV0012'     MSGNR
     C                   SETON                                            30
     C                   END
      *
S06  C*                  ENDSR
N06  C     SLHENT2       ENDSR
      *
     C***********************************************
     C     CHGAK         BEGSR
     C***********************************************
     C     AKEDI         COMP      'E'                                    28
     C                   EXSR      MOVAKW
     C                   SETON                                        99
     C     STCHG         TAG
     C                   EXFMT     BILDE01
S03  C*                  MOVEA     '00000'       *IN(86)
N03  C                   MOVEA     '000000'      *IN(85)
     C                   MOVE      '0'           *IN30
      *
     C   04              DO
     C                   EXSR      SØKDTA
     C                   GOTO      STCHG
     C                   END
     C   12              DO
     C                   UPDATE    SUBFIL
     C                   EXSR      MOVB2
     C                   END
     C  N12              EXSR      TESTB1
     C  N12
     CAN 30              GOTO      STCHG
     C     XLIN          IFEQ      0
     C                   Z-ADD     9             LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     DLTAK         BEGSR
     C***********************************************
     C                   SETON                                        99
     C                   EXFMT     BILDE02
      *
     C  N12              DO
     C     AKKEY1        CHAIN     GSSAKF                             33
     C  N33              DELETE    GSSAKFR
     C                   EXSR      MOVB1
     C                   END
     C                   UPDATE    SUBFIL
     C     XLIN          IFEQ      0
     C                   Z-ADD     9             LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     DSPAK         BEGSR
     C***********************************************
     C                   SETON                                        99
     C                   EXFMT     BILDE02
      *
     C                   UPDATE    SUBFIL
     C     XLIN          IFEQ      0
     C                   Z-ADD     9             LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
      *
     C                   ENDSR
N06  C***********************************************
N06  C     BEKREFT       BEGSR
N06  C***********************************************
N06  C                   SETON                                        99
N06  C                   MOVE      UONO          UONOA             7
N06  C                   MOVE      AKLNO         AKLNOA            3
N06  C                   CALL      'SIEM03R'
N06  C                   PARM                    UONOA
N06  C                   PARM                    AKLNOA
N06  C     AKKEY1        CHAIN     GSSAKF                             33
N06  C                   move      'B'           AKEDI
N06  C  N33              UPDATE    GSSAKFR
N06   *
N06  C                   UPDATE    SUBFIL
N06  C     XLIN          IFEQ      0
N06  C                   Z-ADD     9             LINNBR
N06  C                   ELSE
N06  C                   Z-ADD     XLIN          LINNBR
N06  C                   END
N06   *
N06  C                   ENDSR
      *
     C***********************************************
     C     MOVWAK        BEGSR
     C***********************************************
     C     *IN33         IFEQ      '1'
     C                   MOVE      UONO          AKONO
     C                   MOVE      XLNO          AKLNO
     C                   ADD       1             XLNO
     C                   END
     C                   MOVE      WSANR         AKANR
     C                   MOVE      WSCONO        AKCONO
     C                   MOVE      WSSTK         AKSTK
     C                   MOVE      WSSTKM        AKSTKM
     C                   MOVE      WSPR          AKPR
     C                   MOVE      WSTXT         AKTXT
N02  C                   MOVE      WSTXT2        AKTXT2
N02  C                   MOVE      WSTXT3        AKTXT3
N02  C                   MOVE      WSTXT4        AKTXT4
N02  C                   MOVE      WSTXT5        AKTXT5
N03  C                   MOVE      WSLK          AKLK
N04  C                   MOVE      WSVAL         AKVAL
      *
     C                   ENDSR
      *
     C***********************************************
     C     MOVAKW        BEGSR
     C***********************************************
     C                   MOVE      AKANR         WSANR
     C                   MOVE      AKCONO        WSCONO
     C                   MOVE      AKSTK         WSSTK
     C                   MOVE      AKSTKM        WSSTKM
     C                   MOVE      AKPR          WSPR
     C                   MOVE      AKTXT         WSTXT
N02  C                   MOVE      AKTXT2        WSTXT2
N02  C                   MOVE      AKTXT3        WSTXT3
N02  C                   MOVE      AKTXT4        WSTXT4
N02  C                   MOVE      AKTXT5        WSTXT5
N03  C                   MOVE      AKLK          WSLK
N04  C                   MOVE      AKVAL         WSVAL
     C                   SETON                                        40
     C  N28              Z-ADD     3             LINNBR
     C   28              Z-ADD     6             LINNBR
     C                   Z-ADD     12            POSNBR
      *
     C                   ENDSR
      *
     C***********************************************
     C     MOVB1         BEGSR
     C***********************************************
     C                   MOVE      *BLANKS       AKANR
     C                   MOVEL     '*DELETE'     AKANR
     C                   MOVE      *BLANKS       AKCONO
     C                   MOVE      *ZEROS        AKSTK
     C                   MOVE      *ZEROS        AKSTKM
     C                   MOVE      *ZEROS        AKPR
     C                   MOVE      *BLANKS       AKTXT
N02  C                   MOVE      *BLANKS       AKTXT2
N02  C                   MOVE      *BLANKS       AKTXT3
N02  C                   MOVE      *BLANKS       AKTXT4
N02  C                   MOVE      *BLANKS       AKTXT5
N03  C                   MOVE      *BLANKS       AKLK
N04  C                   MOVE      *BLANKS       AKVAL
      *
     C                   ENDSR
      *
     C***********************************************
     C     MOVB2         BEGSR
     C***********************************************
     C                   SETOFF                                       4028
     C                   MOVE      *BLANKS       WSANR
     C                   MOVE      *ZEROS        WSSTK
     C                   MOVE      *ZEROS        WSSTKM
     C                   MOVE      *ZEROS        WSPR
     C                   MOVE      *BLANKS       WSTXT
N02  C                   MOVE      *BLANKS       WSTXT2
N02  C                   MOVE      *BLANKS       WSTXT3
N02  C                   MOVE      *BLANKS       WSTXT4
N02  C                   MOVE      *BLANKS       WSTXT5
N03  C                   MOVE      *BLANKS       WSLK
N04  C                   MOVE      FICURR        WSLK
     C                   Z-ADD     3             LINNBR
     C                   Z-ADD     12            POSNBR
      *
     C                   ENDSR
      *
     C***********************************************
     C     OPDORD        BEGSR
     C***********************************************
     C                   MOVE      'J'           XINIT             1
     C     1             DO        YRECNO        ZRECNO
     C     ZRECNO        CHAIN     SUBFIL                             33
     C  N33AKANR         IFNE      '*DELETE'
     C     XINIT         IFEQ      'J'
     C                   MOVE      *BLANKS       PO
     C                   MOVE      'N'           XINIT
     C                   END
     C     1             DO        6             XN                1 0
     C     PO(XN)        IFEQ      *BLANKS
     C                   MOVE      AKCONO        PO(XN)
     C                   END
     C     PO(XN)        IFEQ      AKCONO
     C                   LEAVE
     C                   END
     C                   ENDDO
     C                   END
     C                   ENDDO
      *
     C                   ENDSR
      *
     C***********************************************
     C     SØKDTA        BEGSR
     C***********************************************
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
      *
     C     LINNBR        IFEQ      3
     C     POSNBR        ANDGE     12
     C     POSNBR        ANDLE     26
     C                   CALL      'LL002R'
S01  C*                    PARM           UGRP    80
N01  C                   PARM                    UGRUP
     C                   PARM                    UCNO              8 0
     C                   PARM                    WSANR
     C                   PARM                    UANRL            15
     C                   PARM                    UTXT             35
     C                   PARM                    UFLAGG            1 0
     C                   Z-ADD     4             LINNBR
     C                   Z-ADD     12            POSNBR
     C                   GOTO      SLSØK
     C                   END
N03   *
N03  C     LINNBR        IFEQ      3
N03  C     POSNBR        ANDGE     36
N03  C     POSNBR        ANDLE     37
N03  C                   MOVE      'J'           USPED             1
N03  C                   CALL      'SYFI10L'
N03  C                   PARM                    WSLK
N03  C                   PARM                    USPED
N03  C                   Z-ADD     4             LINNBR
N03  C                   Z-ADD     12            POSNBR
N03  C                   GOTO      SLSØK
N03  C                   END
      *
     C                   MOVE      'DIV1037'     MSGNR
     C                   SETON                                        30
      *
     C     SLSØK         ENDSR
      *
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     AKKEY1        KLIST
     C                   KFLD                    UONO
     C                   KFLD                    AKLNO
     C     AKKEY2        KLIST
     C                   KFLD                    UONO
     C                   KFLD                    XLNO
     C     LVAR1K        KLIST
     C                   KFLD                    UGRUP
     C                   KFLD                    UCNO
     C                   KFLD                    WSANR
N03  C     KODTLKK       KLIST
N03  C                   KFLD                    KLKUNI
N03  C                   KFLD                    WSLK
      *
     C     *ENTRY        PLIST
     C                   PARM                    UCNO              8 0
     C                   PARM                    UONO              7 0
     C                   PARM                    UBCD              5
     C                   PARM                    PO
     C                   PARM                    UTYPE             3
     C                   PARM                    UFLAGG            1 0
N04  C                   READ      FIRM                                   33
      *
     C     UTYPE         COMP      'DSP'                                  27
     C                   WRITE     WINDOWSZ
     C                   MOVE      *ZEROS        UGRUP             8 0
N01  C     UCNO          CHAIN     LLGRPK05                           33
N01  C  N33              Z-ADD     GKGRP         UGRUP
N03  C                   MOVE      'LK'          KLKUNI
      *
     C                   ENDSR
      *
