********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 YBC PTF10 SØK KUN EN KUNDE AV GANGEN
********************************************************************
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      ALTHELP
     A                                      HELP
      *
     A          R BILDE01
N01  A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF03(03 'Avslutt program')
     A                                      CF04(04 'Søk kunde/leverandør')
N01  A            LINNBR         3S 0H
N01  A            POSNBR         3S 0H
     A                                  1  2'SYSPED/SAD040'
     A                                      COLOR(BLU)
     A N28                              1 20'Arbeide med SAD-transitt'
     A                                      COLOR(WHT)
     A  28                              1 20'Spørring på SAD-transitt'
     A                                      COLOR(WHT)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                  2 70TIME
     A                                  4 13'Avdeling:'
     A            WSAAVD         4Y 0B  4 23TEXT('AVDELINGSNR')
     A                                      DSPATR(UL)
     A  99                                  DSPATR(RI)
     A  99                                  DSPATR(PC)
     A                                      COLOR(BLU)
     A                                      EDTCDE(4)
     A  99                                  ERRMSG('Tastet avdeling er ikke reg-
     A                                      istrert.' 99)
     A            KOANVN        33A  O  4 30TEXT('AVDELINGSNAVN')
     A                                      COLOR(BLU)
     A                                  7 20'Velg søkevei med startpunkt'
     A                                      COLOR(WHT)
     A                                  9 20'Dato:'
     A            WSDT           6Y 0B  9 35TEXT('Registreringsdato')
     A                                      DSPATR(UL)
     A  98                                  DSPATR(RI)
     A  41
     AO 98                                  DSPATR(PC)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A  98                                  ERRMSG('Du må velge et av de startp-
     A                                      unktene.' 98)
     A                                 10 20'Tolldek.nr.'
     A            WSTDN          7Y 0B 10 35TEXT('Tolldeklarasjonsnr')
     A                                      DSPATR(UL)
     A  42                                  DSPATR(PC)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 11 20'Avsendersnr.'
     A            WSKNK          8Y 0B 11 35TEXT('Kundenr Selger')
     A                                      DSPATR(UL)
     A  97                                  DSPATR(RI)
     A  43
     AO 97                                  DSPATR(PC)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 11 48'Dato:'
     A            WSDTK          6Y 0B 11 55TEXT('Registreringsdato')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 12 20'Mottakersnr.'
     A            WSKNS          8Y 0B 12 35TEXT('Kundenr Kjøper')
     A                                      DSPATR(UL)
     A  96                                  DSPATR(RI)
     A  44
     AO 96                                  DSPATR(PC)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 12 48'Dato:'
     A            WSDTS          6Y 0B 12 55TEXT('Registreringsdato')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 13 20'Tariffør:'
     A            WSSG           3A  B 13 35TEXT('Signatur')
     A                                      DSPATR(UL)
     A  45                                  DSPATR(PC)
     A                                      COLOR(YLW)
     A                                 13 48'Dato:'
     A            WSDTT          6Y 0B 13 55TEXT('Registreringsdato')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 14 20'Løpenummer:'
     A            WSTLL         10A  B 14 35TEXT('Tollnr Løpenr')
     A                                      DSPATR(UL)
     A  46                                  DSPATR(PC)
     A                                      COLOR(YLW)
     A                                 15 20'Transitt.nr.'
     A            WSCTN          5Y 0B 15 35TEXT('STATUS')
     A                                      DSPATR(UL)
     A  47                                  DSPATR(PC)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 16 20MSGCON(014 YBC0053 *LIBL/SYMSGF)
     A            WSST           1A  B 16 35TEXT('STATUS')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PC)
     A                                      COLOR(YLW)
     A                                 23 20'F3 = Avslutt'
     A                                      COLOR(BLU)
     A                                 23 36'F4 = Søk kunde/leverandør'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R SUBFIL                    SFL
     A            SEVKB          9S 0H
     A            SENTK          7S 0H
     A            WSNR           2Y 0B  6  2EDTCDE(4)
     A            SEAVD          4Y 0O  6  5TEXT('Tolldeklarasjonsnr')
     A                                      EDTCDE(4)
     A            SETDN          7Y 0O  6 10TEXT('Tolldeklarasjonsnr')
     A                                      EDTCDE(4)
     A  42                                  COLOR(TRQ)
     A            SESG           3A  O  6 18TEXT('Signatur')
     A  45                                  COLOR(TRQ)
     A            WSDT2          6S 0O  6 22TEXT('Registreringsdato')
     A  41                                  COLOR(TRQ)
     A            SETLL         10A  H
     A            XSETLL         8A  O  6 29TEXT('Tollnr Løpenr')
     A  46                                  COLOR(TRQ)
     A            SETLE          6A  O  6 38TEXT('Tollnr Ekspsted')
     A  46                                  COLOR(TRQ)
     A            SEST           1A  O  6 46TEXT('Status')
     A N49                                  COLOR(RED)
     A  49                                  COLOR(TRQ)
     A            WSNAK         10   O  6 48
     A  43                                  COLOR(TRQ)
     A            WSVKB          7Y 0O  6 59TEXT('Bruttovekt')
     A                                      EDTCDE(4)
     A            SECTN          5Y 0O  6 67TEXT('Transitt.nr.')
     A  47                                  COLOR(TRQ)
     A                                      EDTCDE(4)
     A            SEDTY          2A  O  6 75
     A            SEDK           2A  O  6 78
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0017)
     A                                      SFLPAG(0016)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A                                      CF03(03 'Avslutt program')
     A                                      CF05(05 'Frisk')
     A N28                                  CF06(06 'Lag ny')
     A                                      CF08(08 'Søk')
     A N28                                  CF15(15 'Skriv ut')
     A N28                                  CF16(16 'Lag flere nye')
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A          H                           HLPDOC('SAD040010' 'SAD040' 'SY400L-
     A                                      O/SADTVINN')
     A                                      HLPARA(01 001 01 080)
     A          H                           HLPDOC('SAD040020' 'SAD040' 'SY400L-
     A                                      O/SADTVINN')
     A                                      HLPARA(02 001 02 080)
     A          H                           HLPDOC('SAD040030' 'SAD040' 'SY400L-
     A                                      O/SADTVINN')
     A                                      HLPARA(03 001 03 080)
     A          H                           HLPDOC('SAD040040' 'SAD040' 'SY400L-
     A                                      O/SADTVINN')
     A                                      HLPARA(04 001 04 080)
     A          H                           HLPDOC('SAD040050' 'SAD040' 'SY400L-
     A                                      O/SADTVINN')
     A                                      HLPARA(05 001 05 080)
     A          H                           HLPDOC('SAD040030' 'SAD040' 'SY400L-
     A                                      O/SADTVINN')
     A                                      HLPARA(06 001 06 004)
     A          H                           HLPDOC('SAD040060' 'SAD040' 'SY400L-
     A                                      O/SADTVINN')
     A                                      HLPARA(06 005 06 050)
     A          H                           HLPDOC('SAD040065' 'SAD040' 'SY400L-
     A                                      O/SADTVINN')
     A                                      HLPARA(06 051 06 080)
     A          H                           HLPDOC('SAD040010' 'SAD040' 'SY400L-
     A                                      O/SADTVINN')
     A                                      HLPARA(07 001 21 080)
     A            ZRECNO         4S 0O  1 60SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYSPED/SAD040'
     A                                      COLOR(BLU)
     A N28                              1 20'Arbeide med SAD-transitt'
     A                                      COLOR(WHT)
     A  28                              1 20'Spørring på SAD-transitt'
     A                                      COLOR(WHT)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                  2 70TIME
     A                                  2  2'Avdeling:'
     A            WSAAVD         4Y 0O  2 12COLOR(BLU)
     A                                      EDTCDE(4)
     A            KOANVN        33A  O  2 17TEXT('AVDELINGSNAVN')
     A                                      COLOR(BLU)
     A N28                              3  2'Velg,  1=Lage  2=Endre  3=Kopiere -
     A                                       5=Vise  6=Print  11=Lage flere'
     A                                      COLOR(BLU)
E01  A  28                              3  2'Velg opsjon,  5=Vis SAD  6=Print'
     A                                      COLOR(BLU)
     A                                  4  2'Op Avd. Tdeknr. Tar Dato.. Løpenr.-
     A                                      Eksp.st.  S Avsender.. Vekt... Tran-
     A                                      .nr Type '
     A                                      COLOR(WHT)
     A            W2NR           2Y 0B  5  2DSPATR(UL)
     A                                      EDTCDE(4)
     A            W2AVD          4Y 0B  5  5DSPATR(UL)
     A                                      EDTCDE(4)
     A            W2TDN          7Y 0B  5 10DSPATR(UL)
     A                                      EDTCDE(4)
     A            W2SG           3A  B  5 18DSPATR(UL)
     A            W2DTT          6Y 0B  5 22DSPATR(UL)
     A                                      EDTCDE(4)
     A            W2TLL         10A  B  5 29DSPATR(UL)
     A            W2ST           1A  B  5 46DSPATR(UL)
     A            W2CTN          5Y 0B  5 67DSPATR(UL)
     A                                      EDTCDE(4)
      *
     A          R BUNN
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      SLNO(23)
     A                                      CLRL(02)
     A                                      OVERLAY
     A          H                           HLPDOC('SAD040999' 'SAD040' 'SY400L-
     A                                      O/SADTVINN')
     A                                      HLPARA(01 001 02 080)
     A N28                              1  2'F3=Avslutt   F5=Frisk   F6=Lage ny-
     A                                         F8=Søk   F15=Skriv ut   F16=Lage-
     A                                       flere'
     A                                      COLOR(BLU)
     A  28                              1  2'F3=Avslutt   F5=Frisk   F8=Søk'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  2  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R NYVARE
     A                                      CF03(03 'Avslutt program')
     A                                      CF05(05 'Frisk')
     A N28                                  CF06(06 'Lag ny')
     A                                      CF08(08 'Søk')
     A N28                                  CF15(15 'Skriv ut')
     A N28                                  CF16(16 'Lag flere nye')
     A          H                           HLPDOC('SAD040010' 'SAD040' 'SY400L-
     A                                      O/SADTVINN')
     A                                      HLPARA(01 001 01 080)
     A          H                           HLPDOC('SAD040020' 'SAD040' 'SY400L-
     A                                      O/SADTVINN')
     A                                      HLPARA(02 001 02 080)
     A          H                           HLPDOC('SAD040030' 'SAD040' 'SY400L-
     A                                      O/SADTVINN')
     A                                      HLPARA(03 001 03 080)
     A          H                           HLPDOC('SAD040040' 'SAD040' 'SY400L-
     A                                      O/SADTVINN')
     A                                      HLPARA(04 001 04 080)
     A          H                           HLPDOC('SAD040050' 'SAD040' 'SY400L-
     A                                      O/SADTVINN')
     A                                      HLPARA(05 001 05 080)
     A          H                           HLPDOC('SAD040010' 'SAD040' 'SY400L-
     A                                      O/SADTVINN')
     A                                      HLPARA(06 001 22 080)
     A          H                           HLPDOC('SAD040999' 'SAD040' 'SY400L-
     A                                      O/SADTVINN')
     A                                      HLPARA(23 001 24 080)
     A                                  1  2'SYSPED/SAD040'
     A                                      COLOR(BLU)
     A N28                              1 20'Arbeide med SAD-transitt'
     A                                      COLOR(WHT)
     A  28                              1 20'Spørring på SAD-transitt'
     A                                      COLOR(WHT)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                  2 70TIME
     A                                  2  2'Avdeling:'
     A            WSAAVD         4Y 0O  2 12COLOR(BLU)
     A                                      EDTCDE(4)
     A            KOANVN        33A  O  2 17TEXT('AVDELINGSNAVN')
     A                                      COLOR(BLU)
     A N28                              3  2'Velg,  1=Lage  2=Endre  3=Kopiere -
     A                                       5=Vise  6=Print  11=Lage flere'
     A                                      COLOR(BLU)
E01  A  28                              3  2'Velg opsjon,  5=Vis SAD  6=Print'
     A                                      COLOR(BLU)
     A                                  4  2'Op Avd. Tdeknr. Tar Dato.. Løpenr.-
     A                                      Eksp.st.  S Avsender.. Vekt... Tran-
     A                                      .nr Type '
     A                                      COLOR(WHT)
     A            W2NR           2Y 0B  5  2DSPATR(UL)
     A                                      EDTCDE(4)
     A            W2AVD          4Y 0B  5  5DSPATR(UL)
     A                                      EDTCDE(4)
     A            W2TDN          7Y 0B  5 10DSPATR(UL)
     A                                      EDTCDE(4)
     A            W2SG           3A  B  5 18DSPATR(UL)
     A            W2DTT          6Y 0B  5 22DSPATR(UL)
     A                                      EDTCDE(4)
     A            W2TLL         10A  B  5 29DSPATR(UL)
     A            W2ST           1A  B  5 46DSPATR(UL)
     A            W2CTN          5Y 0B  5 67DSPATR(UL)
     A                                      EDTCDE(4)
     A N28                             23  2'F3=Avslutt   F5=Frisk   F6=Lage ny-
     A                                         F8=Søk   F15=Skriv ut   F16=Lage-
     A                                       flere'
     A                                      COLOR(BLU)
     A  28                             23  2'F3=Avslutt   F5=Frisk   F8=Søk'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
