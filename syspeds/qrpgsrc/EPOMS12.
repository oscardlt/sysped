      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/EPOMS12 MODULE(*LIBL/EPOMS12
CRTPGM*        *LIBL/CHECKUSR *LIBL/INCGI) ACTGRP(CGI) AUT(*USE)
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 YBC PTF10 SJEKK OM DET ER LOV Å PLUKKE HELE PO
10XXX * 02 JOV PTF10 Oppdater Navn på avsender/leverandør
********************************************************************
      *
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     UF   E           K DISK    USROPN
     FBRPARF    IF   E           K DISK    USROPN
     FEPOH      UF   E           K DISK    USROPN
     F                                     RENAME(EPOHR:EPOHRH)
     FEPOH02    UF   E           K DISK    USROPN
     FEPOL      UF   E           K DISK    USROPN
     F                                     RENAME(EPOLR:EPOLRH)
     FEPOL02    UF   E           K DISK    USROPN
     FEPOTT     O  A E             DISK    USROPN
     FEPOPOS01  UF A E           K DISK    USROPN
     FHEADF     UF   E           K DISK    USROPN
     FTURER14   UF   E           K DISK    USROPN
      *********************************************************************
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      /copy syspeds/qrpgsrcpy,variables3
     D IfsMultIndicators...
     d                 ds
     D  NoErrors                       n
     D  NameTooLong                    n
     D  NotAccessible                  n
     D  NoFilesUsable                  n
     D  DupSections                    n
     D  FileIsEmpty                    n
      *
      * Client input variables
     D user            s             10
     D UsrSpcName      s             10
     D WSKN            S              8
     D BSKN            S              8  0
     D XXKN            S              8  0
     D WSTUR           s              8
     D WBTUR           s              8  0
     D WSAVD           s              4
     D WBAVD           s              4  0
     D WSOPD           s              7
     D WBOPD           s              7  0
     D WSSNR           S              1
     D WSSST           S              1
     D WSSAVD          S              4
     D WBSAVD          s              4  0
     D WSFRAMES        s              1
     D WSREMOVE        s              1
     D WSVISPO         s              1
     D WSSLETTET       s              1
     D WSBES           s             10
     D WSLIN           s              7
     D WBLIN           s              7  0
     D WSULIN          s              5
     D WBULIN          s              5  0
     D WSSTA3          s              5
     D WSCAN           s              1
     D WSCHG           s              1
     D WSSTART         s              1
     D*From            s              8
     D*To              s              8
     D ZZVKT           s             10  2
     D WSPOS           S              5
     D WSPOSN          S              5  0
     D WSPBES          s             10
     D WSPLIN          s              7
     D WBPLIN          s              7  0
     D WSPULIN         s              5
     D WBPULIN         s              5  0
     D WSPOREF         s             17
     D ItemCount       s             10i 0
     D i               s             10i 0
     D PGM             C                   CONST('SYSPED/CHECKUSR')
      *
     D OddEven         s             10
      * For xlate mellom Upper Case og Lower Case
     D UC              C                   CONST('ABCDEFGHIJKLMNOPQRST-
     D                                     UVWXYZÆØÅ')
     D LC              C                   CONST('abcdefghijklmnopqrst-
     D                                     uvwxyzæøå')
      *
N02  D                 DS
N02  D  NyLnavAd               1    150
N02  D  EHLNAV                 1     30
N02  D  EHLAD1                31     60
N02  D  EHLAD2                61     90
N02  D  EHLAD3                91    120
N02  D  EHLAD4               121    150
N02  D                 DS
N02  D  GmLnavAd               1    150
N02  D  LevNav                 1     30
N02  D  LevAd1                31     60
N02  D  LevAd2                61     90
N02  D  LevAd3                91    120
N02  D  LevAd4               121    150
     D                 DS
     D  ParmOS1                1   1024
     D  OSCHG                  1      1
     D  OSOT16                 2     49
     D  OT                     2     49    DIM(16)
     D  OSST08               150    197
     D  STD                  150    197    DIM(8)
     D  AV                   198    247    DIM(10)
      *
      /copy syspeds/qrpgsrcpy,prolog3
      *
      * Get program start time for calculating execution time
     C                   time                    timedata1
      * Parse input / cvt to numeric
     C                   exsr      Parse
      * File open / keys
     C                   EXSR      INIT
      *
     C                   CALLB     PGM
     C                   PARM                    BIBRID
     C                   PARM                    UsrSpcName
     C                   PARM                    UINN              1
     C                   IF        UINN = 'N'
     C                   GOTO      SLUTT
     C                   END
      * dato/tid siste vellykkede operasjon.
     C     USER          CHAIN     BRIDF                              50
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVE      WDT           BIDTF
     C                   MOVE      WTM           BITIDF
     C                   UPDATE    BRIDFR
      *
     C                   SELECT
     C                   WHEN      WSCAN    = 'J'
     C                   WHEN      WSREMOVE = 'Y'
     C                   EXSR      SRREMOVE
     C                   ENDSL
      * SUBMIT INNE  fra - da er itemcount (lik ant poster ) satt
     C                   eval      ItemCount = ZhbGetVarCnt('WSPOS')
     C     ItemCount     IFGT      *ZEROS
     c                   exsr      UPDPOS
     c                   endif
      *
      * Read output skeleton html member into memory
     C                   exsr      LoadHtml
      * Set section variables
     C                   exsr      Settop
     C*                  callp     wrtsection('stdtop')
     C                   callp     wrtsection('StdtopTab')
     C                   callp     wrtsection('top')
      *
      *
      * HOVEDRUTINE
      * Vis PO på oppdrag
     C                   IF        WBAVD <> 0
     C                   EVAL      ELLIN = 0
     C                   EVAL      ELULIN= 0
N02  C                   move      *blanks       LevNav           30
N02  C                   move      *blanks       LevAd1           30
N02  C                   move      *blanks       LevAd2           30
N02  C                   move      *blanks       LevAd3           30
N02  C                   move      *blanks       LevAd4           30
      * Hele POer
     C     HEADFK        SETLL     EPOH02
     C     HEADFK        READE(N)  EPOH02                                 35
     C                   DOW       *IN35 = *OFF
N02  c                   exsr      TstLev
     C                   EXSR      SETROW
     C                   callp     wrtsection('row')
     C                   callp     wrtsection('row1')
     C                   callp     wrtsection('row3')
     C     HEADFK        READE(N)  EPOH02                                 35
     C                   ENDDO
      * Løse linjer
     C     HEADFK        SETLL     EPOL02
     C     HEADFK        READE(N)  EPOL02                                 35
     C                   DOW       *IN35 = *OFF
     C     EPOHK         CHAIN(N)  EPOH                               33
N02  c                   exsr      TstLev
     C                   EVAL      EHVKT = ELVKTL
     C                   EVAL      EHM3  = ELM3L
N01  C                   IF        ELFSTE <> *BLANKS
N01  C                   EVAL      EHFSTE = ELFSTE
N01  C                   END
N01  C                   IF        ELTSTE <> *BLANKS
N01  C                   EVAL      EHTSTE = ELTSTE
N01  C                   END
     C                   EXSR      SETROW
     C                   callp     wrtsection('row')
     C                   callp     wrtsection('row2')
     C                   callp     wrtsection('row3')
     C     HEADFK        READE(N)  EPOL02                                 35
     C                   ENDDO
N02   *
N02   * Oppdater Lev.navn
N02  C     Levnav        Ifne      *blanks
N02  C     HEADFK        CHAIN     HEADF                              33
N02  C                   if        *IN33 = *off
N02  C                   MOVE      LevNav        HENAS
N02  C                   MOVE      LevAd1        HEADS1
N02  C                   MOVE      LevAd2        HEADS2
N02  C                   MOVE      LevAd3        HEADS3
N02  C                   if        LevAd4<>*blanks
N02  C                   MOVE      LevAd4        HEADS3
N02  C                   endif
N02  C                   UPDATE    HEADFR
N02  C                   endif
N02  C                   endif
      *
     C                   END
      *
     C                   callp     wrtsection('bot')
     C                   callp     wrtsection('stdbot')
      *
     C     SLUTT         TAG
     C                   exsr      Exit
      *
     C                   eval      *inlr = *on
     C                   return
      *
N02   ****************************************
N02  C     TstLev        begsr
N02   ****************************************
N02  C     Levnav        Ifeq      *blanks
N02  C                   move      EHLNAV        LevNav
N02  C                   move      EHLAD1        LevAd1
N02  C                   move      EHLAD2        LevAd2
N02  C                   move      EHLAD3        LevAd3
N02  C                   move      EHLAD4        LevAd4
N02  C                   else
N02  C     NyLnavAd      Ifne      GmLnavAd
N02  C                   move      ' (M.FLERE)'  LevNav
N02  C                   endif
N02  C                   endif
N02  C                   endsr
      ****************************************
     C     UPDPOS        begsr
      ****************************************
      *
     C                   eval      i = 1
     C     i             DOUGT     ItemCount
     C                   eval      WSPOS  = ZhbGetVar('WSPOS':i)
     C                   eval      WSPOSN  = c2n2(WSPOS)
     c     WSPOS         ifne      *blanks
     C                   eval      EPBES   = ZhbGetVar('WSPBES':i)
     C                   eval      WSPLIN  = ZhbGetVar('WSPLIN':i)
     C                   EVAL      EPLIN   = c2n2(WSPLIN)
     C                   eval      WSPULIN = ZhbGetVar('WSPULIN':i)
     C                   EVAL      EPULIN  = c2n2(WSPULIN)
     c     PosKey        Chain     EPOPOS01                           33
     C                   IF        *IN33 = *OFF
     c                   eval      EPPOS = WSPOSN
     c*                  if        EPPOS = '0'
     c*                  eval      EPPOS = *zeros
     c*                  endif
     c                   update    EPOPOSR
     c                   endif
     c                   endif
      *
     C                   eval      i = i +1
     C                   ENDDO
     C                   endsr
      *
      *
      *
      *=====================================================================
      * Parse input / cvt to numeric
      *=====================================================================
     C     Parse         begsr
      * Parse variables from QUERY_STRING environment variable
     C                   eval      user     = zhbgetvar('user')
     C                   EVAL      UsrSpcName  = zhbgetvar('UsrSpcName')
     C                   eval      WSKN    = zhbgetvar('WSKN')
     C                   eval      BSKN    = c2n2(WSKN)
     C                   eval      WSTUR    = zhbgetvar('WSTUR')
     C                   EVAL      WBTUR    = c2n2(WSTUR)
     C                   eval      WSAVD    = zhbgetvar('WSAVD')
     C                   EVAL      WBAVD    = c2n2(WSAVD)
     C                   eval      WSOPD    = zhbgetvar('WSOPD')
     C                   EVAL      WBOPD    = c2n2(WSOPD)
     C                   eval      WSFRAMES = zhbgetvar('FRAMES')
     C                   eval      WSREMOVE = zhbgetvar('WSREMOVE')
     C                   eval      WSBES    = zhbgetvar('WSBES')
     C                   eval      WSLIN    = zhbgetvar('WSLIN')
     C                   EVAL      WBLIN    = c2n2(WSLIN)
     C                   eval      WSULIN   = zhbgetvar('WSULIN')
     C                   EVAL      WBULIN   = c2n2(WSULIN)
     C                   eval      WSSTA3   = zhbgetvar('WSSTA3')
     C                   eval      WSCAN    = zhbgetvar('WSCAN')
     C                   eval      WSSTART  = zhbgetvar('WSSTART')
     C                   eval      WSSNR    = zhbgetvar('WSSNR')
     C                   eval      WSSST    = zhbgetvar('WSSNR')
     C                   eval      WSSAVD   = zhbgetvar('WSSAVD')
     C                   EVAL      WBSAVD   = c2n2(WSSAVD)
     C                   eval      WSVISPO  = zhbgetvar('WSVISPO')
     C*                  IF        WSVISPO  = 'J'
     C*                  EVAL      WSVISPO  = 'Y'
     C*                  END
      *
     C                   endsr
      *
      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   CLOSE     BRIDF
     C                   CLOSE     BRPARF
     C                   CLOSE     TURER14
     C                   CLOSE     HEADF
     C                   CLOSE     EPOH
     C                   CLOSE     EPOH02
     C                   CLOSE     EPOL
     C                   CLOSE     EPOL02
     C                   CLOSE     EPOTT
     C                   CLOSE     EPOPOS01
      *
     C                   endsr
      *
      *=====================================================================
      * Read output skeleton html member into memory
      *=====================================================================
     C     LoadHtml      begsr
cccccC                   eval      IfsMultIndicators = gethtmlifsmult(
cccccC                             '/syspeddev/html/eSpedTop.html -
cccccC                             /syspeddev/html/EPOMS12.html -
cccccC                             /syspeddev/html/eSpedBot.html':
cccccC                             '/CGITAG/')
     C                   endsr
      *=====================================================================******
      *  FILTRERE POSTER
      *=====================================================================******
     C     SRFILTER      begsr
     C                   MOVE      'N'           XVIS              1
     C                   MOVE      'J'           XVIS
      * hele SR bør skrives om..
      *   .. er startet som noen få IFsetninger som senere er blitt mer og mer uoversiktlige
      *   MYE ryddigere om en gjør test for test og dersom oppd. eksluderes hopp til slutten
      *
     C     SLFILTER      TAG
     C                   ENDSR
      *
      *=====================================================================
      *  Set variable data for section /$top
      *=====================================================================
     C     SetTop        begsr
      *
     C                   callp     updHTMLvar('ROWHEAD':RowHead)
     C                   callp     updHTMLvar('LogoImg':LogoImg)
     C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updHTMLvar('USER':USER)
     C                   callp     updHtmlVar('UsrSpcName':UsrSpcName)
     C                   callp     updHtmlVar('WSKN':
     C                             %trim(%editc(BSKN:'Z')))
     C                   callp     updHTMLvar('FRAMES':WSFRAMES)
     C                   callp     updHTMLvar('WSCHG':WSCHG)
     C                   callp     updHTMLvar('WSSNR':WSSNR)
     C                   callp     updHTMLvar('WSSST':WSSST)
     C                   callp     updHTMLvar('WSTUR':WSTUR)
     C                   callp     updHTMLvar('WSAVD':WSAVD)
     C                   callp     updHTMLvar('WSOPD':WSOPD)
     C                   callp     updHTMLvar('WSSDF':HESDF)
     C                   callp     updHTMLvar('WSSDT':HESDT)
     C                   callp     updHTMLvar('WSSAVD':WSSAVD)
     C                   callp     updHTMLvar('WSVISPO':WSVISPO)
     C                   callp     updHTMLvar('WSSLETTET':WSSLETTET)
     C                   IF        WBLIN = 0
     C                   callp     updHTMLvar('WSLKUND':'')
     C                   callp     updHTMLvar('WSLBES':'')
     C                   ELSE
     C                   callp     updHtmlVar('WSLKUND':
     C                             %trim(%editc(ELKUND:'Z')))
     C                   callp     updHTMLvar('WSLBES':WSBES)
     C                   END
     C                   callp     updHTMLvar('HENT':
     C                             %trim(%editc(HENT:'Z')))
     C                   callp     updHTMLvar('HEVKT':
     C                             %trim(%editc(HEVKT:'3')))
     C                   callp     updHTMLvar('HEM3':
     C                             %trim(%editc(HEM3:'3')))
     C                   callp     updHTMLvar('HELM':
     C                             %trim(%editc(HELM:'3')))
      *
     C                   endsr
      *
      *=====================================================================
      *  Set variable data for section /$row
      *=====================================================================
     C     Setrow        begsr
      *
     C                   eval      EPAVD = WBAVD
     C                   eval      EPOPD = WBOPD
     C                   eval      EPKUND= EHKUND
     C                   eval      EPBES = EHBES
     C                   eval      EPLIN = ELLIN
     C                   eval      EPULIN= ELULIN
     c     PosKey        Chain(N)  EPOPOS01                           33
     c     *in33         ifeq      *on
     c                   eval      EPPOS = *zeros
     c                   write     EPOPOSR
     c                   endif
      *
     C                   callp     updHTMLvar('EHBES':EHBES)
     C                   callp     updHTMLvar('ELLIN':
     C                             %trim(%editc(ELLIN:'Z')))
     C                   callp     updHTMLvar('ELULIN':
     C                             %trim(%editc(ELULIN:'Z')))
     C                   SELECT
     C                   WHEN      ELLIN = 0
     C                   callp     updHTMLvar('POREF':EHBES)
     C                   callp     updHTMLvar('EHETD':
     C                             %trim(%editc(EHETD:'Z')))
     C                   WHEN      ELULIN = 0
     C                   MOVE      ELLIN         XAN03             3
     C                   EVAL      WSPOREF = %TRIM(ELBES) + '-' + %TRIM(XAN03)
     C                   callp     updHTMLvar('POREF':WSPOREF)
     C                   callp     updHTMLvar('EHETD':
     C                             %trim(%editc(ELETD:'Z')))
     C                   OTHER
     C                   MOVE      ELLIN         XAN03             3
     C                   MOVE      ELULIN        XAN02             2
     C                   EVAL      WSPOREF = %TRIM(ELBES) + '-' + %TRIM(XAN03)
     C                             + '-' + %TRIM(XAN02)
     C                   callp     updHTMLvar('POREF':WSPOREF)
     C                   callp     updHTMLvar('EHETD':
     C                             %trim(%editc(ELETD:'Z')))
     C                   ENDSL
     C                   callp     updHTMLvar('ELART':ELART)
     C                   callp     updHTMLvar('ELSTA3':ELSTA3)
     C                   callp     updHTMLvar('EPPOS':
     C                             %trim(%editc(EPPOS:'Z')))
     C                   callp     updHTMLvar('EHDATO':
     C                             %trim(%editc(EHDATO:'Z')))
     C                   callp     updHTMLvar('EHLNAV':EHLNAV)
     c*                  eval      From = EHFLK +'-'+EHFSTE
     C*                  callp     updHTMLvar('FROM':FROM)
     C                   callp     updHTMLvar('EHFSTE':EHFSTE)
     c*                  eval      To   = EHTLK +'-'+EHTSTE
     C*                  callp     updHTMLvar('TO':TO)
     C                   callp     updHTMLvar('EHTSTE':EHTSTE)
     C                   callp     updHTMLvar('EHTRKO':EHTRKO)
     C                   EVAL(H)   ZZVKT = EHVKT
     C                   callp     updHTMLvar('EHVKT':
     C                             %trim(%editc(ZZVKT:'3')))
     C                   callp     updHTMLvar('EHM3':
     C                             %trim(%editc(EHM3:'3')))
      *
     C                   endsr
      *
      *=====================================================================******
      *  SRREMOVE
      *=====================================================================******
     C     SRREMOVE      begsr
     C                   MOVE      'Y'           WSVISPO
     C                   MOVE      'J'           WSSLETTET
     C                   MOVE      *ZEROS        XNT
     C                   MOVE      *ZEROS        XVKT
     C                   MOVE      *ZEROS        XM3
      *
     C                   IF        WBLIN = 0
      * SLETT ET PO
     C     EPOHK2        CHAIN     EPOH                               33
     C                   IF        *IN33 = *OFF
      * T&T
     C                   EVAL      ETKUND = EHKUND
     C                   EVAL      ETBES  = EHBES
     C                   EVAL      ETACTI = 'RMV'
     C                   EVAL      ETTEXT = 'PO is removed from order '
     C                             + %TRIM(%EDITC(EHAVD:'Z')) + '/'
     C                             + %TRIM(%EDITC(EHOPD:'Z'))
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVE      WDT           ETDATE
     C                   MOVE      WTM           ETTIME
     C                   MOVE      BIBRID        ETUSER
     C                   WRITE     EPOTTR
      *
     C                   MOVE      *ZEROS        EHAVD
     C                   MOVE      *ZEROS        EHOPD
     C                   EVAL      EHSTA = '1'                                  Tilbake til Conf.
     C                   EVAL      EHSTA2= 'S'                                  SEND
     C                   UPDATE    EPOHRH
     C                   ADD       EHANTK        XNT
     C                   ADD       EHVKT         XVKT
     C                   ADD       EHM3          XM3
      * Update på linjenivå,
     C     EPOLK         SETLL     EPOL
     C     EPOLK         READE     EPOL                                   33
     C                   DOW       *IN33 = *OFF
     C                   EVAL      ELSTA = EHSTA
     C                   EVAL      ELSTA2= 'S'                                  SEND
     C                   UPDATE    EPOLR
     C     EPOLK         READE     EPOL                                   33
     C                   ENDDO
     C                   END
      *
     C                   ELSE
      * SLETT EN PO-LINJE
     C     EPOLK         CHAIN     EPOL                               33
     C                   DOW       *IN33 = *OFF
     C                   IF        WSSTA3 = ELSTA3
     C                   LEAVE
     C                   END
     C                   UPDATE    EPOLR
     C     EPOLK         READE     EPOL                                   33
     C                   ENDDO
      *
     C                   IF        *IN33 = *OFF
      * T&T
     C                   EVAL      ETKUND = ELKUND
     C                   EVAL      ETBES  = ELBES
     C                   EVAL      ETLIN  = ELLIN
     C                   EVAL      ETULIN = ELULIN
     C                   EVAL      ETACTI = 'RMV'
     C                   EVAL      ETTEXT = 'PO line is removed from order '
     C                             + %TRIM(%EDITC(ELAVD:'Z')) + '/'
     C                             + %TRIM(%EDITC(ELOPD:'Z'))
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVE      WDT           ETDATE
     C                   MOVE      WTM           ETTIME
     C                   MOVE      BIBRID        ETUSER
     C                   WRITE     EPOTTR
      *
     C                   MOVE      *ZEROS        ELAVD
     C                   MOVE      *ZEROS        ELOPD
     C                   EVAL      ELSTA = '1'                                  Tilbake til Conf.
     C                   EVAL      ELSTA2= 'S'                                  SEND
     C                   UPDATE    EPOLRH
     C                   ADD       ELNTSL        XNT
     C                   ADD       ELVKTL        XVKT
     C                   ADD       ELM3L         XM3
      * OPPDATER EPOH
     C     EPOHK2        CHAIN     EPOH                               33
     C  N33              IF        EHAVD = 9999 AND EHOPD = 9999999
     C                   EVAL      EHAVD = 0
     C                   EVAL      EHOPD = 0
     C                   END
     C  N33              UPDATE    EPOHRH
     C                   END
      *
     C                   END
      * fjen psosten som lagrer evt POSnummer
     C                   eval      EPBES = WSBES
     C                   eval      EPLIN = WBLIN
     C                   eval      EPULIN= WBULIN
     c     PosKey        Chain     EPOPOS01                           33
     C                   IF        *IN33 = *OFF
     c                   Delete    EPOPOSR
     c                   endif
      *
     C     HEADFK        CHAIN     HEADF                              33
     C                   IF        *IN33 = *OFF
     C                   SUB(H)    XNT           HENT
     C                   SUB(H)    XVKT          HEVKT
     C                   SUB(H)    XM3           HEM3
     C                   UPDATE    HEADFR
     C                   END
      *
     C     WBTUR         CHAIN     TURER14                            33
     C                   IF        *IN33 = *OFF
     C                   SUB(H)    XNT           TUTS
     C                   SUB(H)    XVKT          TUTVKT
     c     XM3           iflt      TUTM3                                        Dirty men.....
     C                   SUB(H)    XM3           TUTM3
     c                   else
     C                   move      *zeros        TUTM3
     c                   endif
     C                   UPDATE    TURERR
     C                   END
      *
     C                   EVAL      WSCHG = 'Y'
      *
     C                   endsr
      *
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
     C                   OPEN      BRIDF
     C     USER          CHAIN(N)  BRIDF                              50
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
      *
      * hent Parametre som går igjen i mange prog:
      *      Odd/Even/Rowhead-farger,Logobilde,Språk,Intern/Ekstern bruker,  mm
     c                   call      'ESGETPAR'
     c                   parm                    BIBRID
     c                   parm                    BIFIRM
     c                   parm                    BIKUNR
     c                   parm                    BIKNG
     c                   parm                    RowHead          10
     c                   parm                    Odd              10
     c                   parm                    Even             10
     c                   parm                    LogoImg          50
     c                   parm                    XSPRÅK            2
     c                   parm                    XINTERN           1
     c                   parm                    ParmDS1        1024
     c                   parm                    ParmDS2        1024
     c                   parm                    ParmDS3        1024
      * HENTER PARAMETER
     C                   CALL      'ESGETOS'
     C                   parm                    BIBRID
     C                   parm                    BIFIRM
     C                   parm                    BIKUNR
     C                   parm                    BIKNG
     C                   parm                    ParmOS1        1024
      *
     C                   OPEN      BRPARF
     C                   OPEN      TURER14
     C                   OPEN      HEADF
     C                   OPEN      EPOH
     C                   OPEN      EPOH02
     C                   OPEN      EPOL
     C                   OPEN      EPOL02
     C                   OPEN      EPOTT
     C                   OPEN      EPOPOS01
      *
     C     HEADFK        klist
     C                   kfld                    WBAVD
     C                   kfld                    WBOPD
     C     EPOHK         klist
     C*                  kfld                    BIKUNR
     C*                  kfld                    BSKN
     C                   kfld                    XXKN
     C                   kfld                    ELBES
     C     EPOHK2        klist
     C*                  kfld                    BIKUNR
     C*                  kfld                    BSKN
     C                   kfld                    XXKN
     C                   kfld                    WSBES
     C     EPOLK         klist
     C*                  kfld                    BIKUNR
     C*                  kfld                    BSKN
     C                   kfld                    XXKN
     C                   kfld                    WSBES
     C                   kfld                    WBLIN
     C                   kfld                    WBULIN
     C     PosKey        klist
     C                   kfld                    WBAVD
     C                   kfld                    WBOPD
     C*                  kfld                    BSKN
     C                   kfld                    XXKN
     C                   kfld                    EPBES
     C                   kfld                    EPLIN
     C                   kfld                    EPULIN
      *
     C     *LIKE         DEFINE    ELNTSL        XNT
     C     *LIKE         DEFINE    ELVKTL        XVKT
     C     *LIKE         DEFINE    ELM3L         XM3
     C     HEADFK        CHAIN(N)  HEADF                              33
     C                   IF        *IN33
     C                   EVAL      XXKN   = BSKN
     C                   ELSE
     C                   EVAL      XXKN   = TRKNFA
     C                   IF        BSKN   <> TRKNFA
     C                   EVAL      BSKN   = TRKNFA
     C                   EVAL      WSSLETTET = 'J'                              VIS TURENSBILDE
     C                   END
     C                   END
      *
     C                   endsr
      *
