     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      ALTHELP
     A                                      HELP
      * "NOERASE" MÅ HA FOR Å IKKE TØMME BILDE SOM MAN KOMME FRA
     A          R NOERASE
     A                                      ASSUME
     A                                  1  2' '
      * MAN KUN DEFINNERE VINDU PÅ REKORDNIVÅ
     A          R WINDOWSZ
     A                                      WINDOW(*DFT 15 69 *NOMSGLIN)
     A                                      OVERLAY
      *
     A          R GSS047DA                  SFL
     A            WSNR           1A  B  4  2VALUES(' ' '5')
     A            BXBOX          3Y 0O  4  5EDTCDE(4)
     A            BXVKT          7Y 1O  4  9EDTCDE(4)
     A            BXM3           7Y 4O  4 18EDTCDE(4)
     A            BXLM           5Y 2O  4 27EDTCDE(4)
     A            BXLEN          5Y 0O  4 34EDTCDE(4)
     A            BXBRE          5Y 0O  4 40EDTCDE(4)
     A            BXHØY          5Y 0O  4 46EDTCDE(4)
     A            XXIDCN        17A  O  4 52
     A            BXCN          17A  H
     A            BXID          17A  H
     A            BXST           1A  H
     A            BXGSL          1A  H
      *
     A          R GSS047DB                  SFLCTL(GSS047DA)
     A                                      WINDOW(WINDOWSZ)
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      CA03(03 'TILBAKE')
     A                                      CA05(05 'Frisk')
     A                                      CA10(10 'OPPDATER OPPDRAG')
     A                                      CA12(12 'TILBAKE')
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/GSS047D'
     A                                      COLOR(BLU)
     A                                  1 21'Informations of boxes'
     A                                      COLOR(WHT)
     A            ZRECNO         4S 0O  1 50SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  2  2'T. boxes:'
S01  A*           BXTOT          3Y 0O  2 12EDTCDE(4)
N01  A            WSBOXT         3Y 0O  2 12EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  2 16'T. weight:'
     A            WSVKTT         7Y 1O  2 27EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  2 36'T. M3:'
     A            WSM3T          7Y 4O  2 43EDTCDE(4)
     A                                      COLOR(WHT)
     A            WSLMT          5Y 2H
     A                                  2 54'Opt: 5=Disp'
     A                                      COLOR(BLU)
     A                                  3  2'Op No.   Weight    M3      LM   Le-
     A                                      ngt Width Heigh Box-id/Container'
     A                                      COLOR(WHT)
      *
     A          R GSS047DC
     A                                      WINDOW(WINDOWSZ)
     A                                      SLNO(14)
     A                                      CLRL(02)
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'F3/F12=End   F5=Refresh  F10=Updat-
     A                                      e order'
     A                                      COLOR(BLU)
     A  30        MSGTXT        50A  O  2  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R GSS047DD
     A                                      WINDOW(WINDOWSZ)
     A                                      CA12(12 'TILBAKE')
     A                                      SLNO(14)
     A                                      CLRL(02)
     A                                      OVERLAY
     A                                  1  2'Length:'
     A            BXLEN          5Y 0O  1 10EDTCDE(4)
     A                                      DSPATR(PC)
     A                                      COLOR(WHT)
     A                                  1 17'Width:'
     A            BXBRE          5Y 0O  1 24EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  1 31'Height:'
     A            BXHØY          5Y 0O  1 39EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  2  2'F12=End'
     A                                      COLOR(BLU)
      *
     A          R GSS047DG
     A                                      WINDOW(WINDOWSZ)
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CA03(03 'TILBAKE')
     A                                      CA05(05 'Frisk')
     A                                      CA12(12 'TILBAKE')
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/GSS047D'
     A                                      COLOR(BLU)
     A                                  1 21'Informations of boxes'
     A                                      COLOR(WHT)
     A                                  2  2'T. boxes:'
     A            BXTOT          3Y 0O  2 12EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  2 16'T. weight:'
     A            WSVKTT         7Y 1O  2 27EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  2 36'T. M3:'
     A            WSM3T          7Y 4O  2 43EDTCDE(4)
     A                                      COLOR(WHT)
     A            WSLMT          5Y 2H
     A                                  2 54'Opt: 5=Disp'
     A                                      COLOR(BLU)
     A                                  3  2'Op No.   Weight    M3      LM   Le-
     A                                      ngt Width Heigh Box-id/Container'
     A                                      COLOR(WHT)
     A                                 14  2'F3/F12=End   F5=Refresh'
     A                                      COLOR(BLU)
     A  30        MSGTXT        65A  O 15  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
