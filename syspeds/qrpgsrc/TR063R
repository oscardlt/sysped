      ***********************************************************
      * PROGRAM SKAL BE OM UTSKRIFT (EL. TRANSITTERING)       ***
      * INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER      ***
      * LEDIGE INDIKATORER 01 02 05-07 09 11 13-16 18-26      ***
      * LEDIGE INDIKATORER 29 35-88 91-96                     ***
      ***********************************************************
     FTRIH      UF   E           K DISK
     FEDIM1     UF A E           K DISK
     FEDIM2     IF   E           K DISK
     F                                     RENAME(EDIMR:EDIMR2)
     FEDIM4     IF   E           K DISK
     F                                     RENAME(EDIMR:EDIMR4)
     FEDIC      UF   E             DISK
     FTVIND     UF A E           K DISK
     FTRISTD    IF   E           K DISK
     FGODSJL05  IF   E           K DISK
     FTR063D    CF   E             WORKSTN USROPN
     D                 DS
     D  TIAVD                  1      4  0
     D  TITDN                  5     11  0
     D  TISG                  12     14
     D  X14S                   1     14
     D                 DS
     D  X0068Æ                 1      8
     D  X0068A                 9     16  0
     D  X0068B                17     22  0
     D  X0068Å                 1     22
     D                 DS
     D  UAVD                   1      4  0
     D  UTDN                   5     11  0
     D  UIN27                 12     12
      *** UIN34 = '1' -> SENDES PÅ TVINN
     D  UIN34                 13     13
     D  UIN36                 14     14
     D  U1N07                 15     17
     D  UWS                   18     27
      * OBS! HUSK INITIERE ALLE FELTER PÅ TR061R
     D  UPARM2                 1    128
     D                 DS
     D  WSDT                   1      6  0
     D  WSDTD                  1      2  0
     D  WSDTM                  3      4  0
     D  WSDTÅ                  5      6  0
     D                 DS
     D  MDT                    1      8  0
     D  MDTÅ                   1      4  0
     D  MDTM                   5      6  0
     D  MDTD                   7      8  0
     D                 DS
     D  WSTD                   1      4  0
     D  WSTDT                  1      2  0
     D  WSTDM                  3      4  0
     D                 DS
     DXISODATO                         D   DATFMT(*ISO)
     D                 DS
     DXISODT                   1     10
     DXISODTÅ                  1      4
     DXISOhyf1                 5      5
     DXISODTM                  6      7
     DXISOhyf2                 8      8
     DXISODTD                  9     10
     D                 DS
     D  GOGRDT                 1      8  0
     D  GOGRDÅ                 1      4  0
     D  GOGRDM                 5      6  0
     D  GOGRDD                 7      8  0
      *
     C                   OPEN      TR063D
     C                   EXSR      INIT
      *
      *=========================================
      *
     C   34              DO
     C                   EXSR      TSTMTY
     C     START2        TAG
     C                   EXFMT     TR063D01
     C                   MOVEA     '00'          *IN(89)
     C                   MOVE      '0'           *IN30
      *
     C   12
     COR 03              GOTO      START3
      *
     C   99              GOTO      START2
     C                   EXSR      TESTB
     C   30              GOTO      START2
     C                   EXSR      OPDFIL
      *
     C                   END
      *
     C     START3        TAG
     C     TRHKEY        CHAIN     TRIH                               33
     C   03
     COR 12
     CORN34TIST          IFEQ      'E'                                           2<-B
     C     TINAA         IFEQ      *BLANKS                                        3<-B
     C     TINAS         ANDEQ     *BLANKS
     C                   MOVE      ' '           TIST
     C                   ELSE
     C                   MOVE      'U'           TIST
     C                   END
     C                   MOVE      *ZEROS        TIMP
     C                   END                                                     2<-E
      *
      *** OPPDATER KOMMUNIKASJONDATA PÅ SAD-REGISTER
     C  N12
     CANN03              DO                                                      2<-B
     C   34              DO                                                       3<-B
      * OBS! UTVIN = J --> SKRIV UT TOLLDEK.
     C                   MOVE      'Q'           TIST
     C                   MOVE      W0035         TI0035
     C                   MOVE      W1N07         TIMF
     C                   MOVE      WVEN          TIKDV
     C                   END                                                      3<-E
     C                   END                                                     2<-E
     C                   UPDATE    TRIHR
      *
     C  N12
     CANN03
     CAN 34              EXSR      KOMLIN
     C                   MOVE      'J'           XWIN              1
      *
     C     SLUTT         TAG
     C   03              Z-ADD     3             UFLAGG
     C   17              Z-ADD     2             UFLAGG
     C                   CLOSE     TR063D
     C                   RETURN
      *
     C***********************************************
     C     TESTB         BEGSR
     C***********************************************
      *** TEST SENDINGSDATO
     C     WSDT          IFNE      0
     C                   CALL      'TESTDATO'
     C                   PARM      WSDTD         UDD               2 0
     C                   PARM      WSDTM         UMM               2 0
     C                   PARM      WSDTÅ         UÅÅ               2 0
     C                   PARM                    UFLAGG            1 0
     C     UFLAGG        COMP      1                                      90
     C   90
     CANN30              MOVE      'SPD0001'     MSGNR
     C   90              SETON                                        30
     C                   END
      *** TEST SENDINGSTID
     C     WSTD          IFNE      0
     C     WSTDT         COMP      23                                 89
     C  N89WSTDM         COMP      59                                 89
     C   89              DO
     C                   MOVE      'YBC1863'     MSGNR
     C                   SETON                                        30
     C                   ENDDO
     C                   END
      *
     C                   ENDSR
      *
      ***********************************************
     C     TSTMTY        BEGSR
      ***********************************************
     C     M2KDCK        SETLL     EDIM2                              33
     C     STTSTM        TAG
     C     M2KD2K        READPE    EDIM2                                  33
     C     *IN33         IFEQ      '0'
     C                   SELECT
      * BRUKER VIL SENDE LOSSEMERKNADER
     C     W1N07         WHENEQ    '044'
     C                   SELECT
     C     M1001         WHENNE    'TEI'
     C                   GOTO      STTSTM
     C     M1N07         WHENEQ    '007'                                        SENDT ANKOMSTMEDLING
     C     M1N07         OREQ      '016'                                        ANKOMSTMELDING ER AV
     C     M1N07         OREQ      '025'                                        TRANSITTERING AVSLUT
     C                   MOVE      '1'           *INLR
     C                   RETURN
     C                   OTHER
      * OK, IE43 (LOSSETILLATELSE HAR FUNNET)
     C                   GOTO      SLTSTM
     C                   ENDSL
      *
      * BRUKER VIL SENDE ANKOMSTMELDING
     C     W1N07         WHENEQ    '007'
     C                   SELECT
     C     M1001         WHENNE    'TEI'
     C                   GOTO      STTSTM
     C     M1N07         WHENEQ    '043'                                        LOSSETILLATELSE ER K
     C     M1N07         OREQ      '044'                                        LOSSEMERKNADER ER SE
     C                   MOVE      '1'           *IN99
     C                   GOTO      SLTSTM
     C                   OTHER
      * OK FOR SENDING AV ANKOMSTMELDING
     C                   GOTO      SLTSTM
     C                   ENDSL
     C                   ENDSL
      *
     C                   END
      *
     C     W1N07         IFEQ      '007'
     C                   MOVEL     TIGN          GOGN
     C                   MOVEL     TITRNR        GOTRNR
     C     GODS5K        CHAIN     GODSJL05                           33
     C     *IN33         IFEQ      '0'
     C     GOGREN        ANDNE     *BLANKS
     C                   MOVEL     GOGRKL        WSTD
     C                   IF        GOGRKL >= 2300
     C                   EVAL      WSTD = WSTD - 2300
     C                   MOVE      GOGRDÅ        XISODTÅ
     C                   MOVE      GOGRDM        XISODTM
     C                   MOVE      GOGRDD        XISODTD
     C                   MOVE      '-'           XISOhyf1
     C                   MOVE      '-'           XISOhyf2
     C                   MOVE      XISODT        XISODATO
     C     XISODATO      ADDDUR    1:*DAYS       XISODATO
     C                   MOVE      XISODATO      XISODT
     C                   MOVE      XISODTÅ       WSDTÅ
     C                   MOVE      XISODTM       WSDTM
     C                   MOVE      XISODTD       WSDTD
     C                   ELSE
     C                   EVAL      WSTD = WSTD + 100
     C                   MOVE      GOGRDÅ        WSDTÅ
     C                   MOVE      GOGRDM        WSDTM
     C                   MOVE      GOGRDD        WSDTD
     C                   END
     C                   END
     C                   END
      *
     C     SLTSTM        ENDSR
      *
      ***********************************************
     C     OPDFIL        BEGSR
      ***********************************************
      *
     C                   ENDSR
      *
      ***********************************************
     C     KOMLIN        BEGSR
      ***********************************************
     C     W1N07         IFEQ      '007'
     C                   MOVE      '1'           *IN31
     C                   GOTO      STKL2
     C                   END
      *
      * HENT SISTE DATO/SEKVENSNR.
      *   1. HVIS DET FINNES OG IKKE ER 'CTL'
      *   2. HVIS DET IKKE PKT.1, HENTER NY DATO/SEKV.NR.
     C     MAKDCK        SETLL     EDIM4                              31
      * FOR Å FÅ UNIK NUMMERERING PÅ ALLE TVINNSAKER SKAL VERSJ.NR ØKER
      * ALLTID (OGSÅ VED FEIL I INP.KTR.!!! - NÅR NY SAK I TVINN (ELLER
      * BARE DFI HITTIL SKAL NY DATO/SEKV.NR. ALLTID TILDELES))
     C     STKL          TAG
     C     MAKD2K        READPE    EDIM4                                  31
     C  N31M0065         IFNE      'CUSDEC'                                     1<-B
     C     M0065         ANDNE     'CUSRES'
     C                   GOTO      STKL
     C                   END                                                    1<-E
     C  N31MST           IFEQ      'D'                                          1<-B
     C     M1001         ORNE      'TEI'
     C                   GOTO      STKL
     C                   END                                                    1<-E
     C  N31M1N07         IFEQ      'CTL'                                        1<-B
     C                   GOTO      STKL
     C                   END                                                    1<-E
      *
      *** DET FINNES IKKE TAD-SVAR DATO/SEKVENS/VERSJON MÅ KONSTRUERES
      *** ORDNE DEKLARANTENS DATO
     C     STKL2         TAG
     C   31              DO
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVEL     WDT           M0068A
      *** HENT SEKVENSNR.
     C     TVINDK        CHAIN     TVIND                              32
     C   32              Z-ADD     M0068A        D0068A
     C   32              Z-ADD     0             D0068B
     C                   ADD       1             D0068B
     C   32              WRITE     TVINDF
     C  N32              UPDATE    TVINDF
     C                   MOVE      D0068B        M0068B
     C                   END
      *
      *** HENTE MELDINGSNR
     C     1             CHAIN     EDIC                               32
     C                   ADD       1             CMN
     C                   UPDATE    EDICR
      *
     C                   EXSR      MOVKD1
     C     TVIM1K        SETLL     EDIM1                              32
     C     STKL3         TAG
     C     TVIM1K        READE     EDIM1                                  32
     C  N32M0065         IFEQ      'CUSDEC'
     C     M1001         ANDEQ     'TEI'
     C                   DELETE    EDIMR
     C                   ELSE
     C                   UPDATE    EDIMR
     C                   GOTO      STKL3
     C                   END
     C                   EXSR      MOVKD2
     C                   WRITE     EDIMR
      *
     C                   ENDSR
      *
      ***********************************************
     C     MOVKD1        BEGSR
      ***********************************************
     C                   MOVE      M0068A        X0068A
     C                   MOVE      M0068B        X0068B
      *
     C                   ENDSR
      *
      ***********************************************
     C     MOVKD2        BEGSR
      ***********************************************
     C                   MOVE      X0004         M0004
     C                   MOVE      X0010         M0010
     C                   MOVE      W0035         M0035
     C                   MOVE      *BLANKS       M0062
     C                   MOVEL     X14S          M0062
     C                   MOVE      'CUSDEC'      M0065
     C                   MOVE      X0068A        M0068A
     C                   MOVE      X0068B        M0068B
     C                   MOVE      *ZEROS        M0068C
     C                   MOVE      *BLANKS       M0068
     C                   MOVEL     X0004         X0068Æ
     C                   MOVEL     X0068Å        M0068
     C                   MOVE      *ZEROS        M0068D
     C                   MOVE      *ZEROS        M0068E
     C                   MOVE      *ZEROS        M0068F
     C                   MOVE      'TEI'         M1001
     C                   MOVE      *BLANKS       M1004
     C                   MOVE      W1N07         M1225
     C                   MOVE      *ZEROS        M2005B
     C                   MOVE      *ZEROS        M3039D
     C                   MOVE      *ZEROS        M3039E
     C                   MOVE      *ZEROS        M5004D
     C                   MOVE      W1N07         M1N07
     C                   MOVE      *BLANKS       M1N08
      *
     C                   MOVE      *ZEROS        MSN
     C                   Z-ADD     CMN           MMN
     C                   MOVE      'S'           MSR
     C                   MOVE      *BLANKS       MST
     C     WSDT          IFEQ      0
     C                   MOVE      *ZEROS        MDT
     C                   ELSE
     C                   MOVE      WSDTD         MDTD
     C                   MOVE      WSDTM         MDTM
     C     WSDTÅ         ADD       2000          MDTÅ
     C                   END
     C                   MOVE      *ZEROS        MTM
     C                   MOVEL     WSTD          MTM
     C                   MOVE      TIAVD         MAVD
     C                   MOVE      TITDN         MTDN
     C                   MOVE      *ZEROS        MFFBNR
     C                   MOVE      WVEN          MVEN
      *
     C                   ENDSR
      *
      ***********************************************
     C     INIT          BEGSR
      ***********************************************
     C     *LIKE         DEFINE    MSR           XSR1
     C     *LIKE         DEFINE    MSR           X1STA
     C     *LIKE         DEFINE    M0068A        MAX0B
     C     *LIKE         DEFINE    M0068B        MAX0C
     C     *LIKE         DEFINE    M0068C        MAX0D
     C     *LIKE         DEFINE    S0004         X0004
     C     *LIKE         DEFINE    S0010         X0010
     C     TRHKEY        KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    UTDN
     C     TRK01K        KLIST
     C                   KFLD                    YUNIK             3
     C                   KFLD                    YKODE            10
     C     MAKDCK        KLIST
     C                   KFLD                    XSR1
     C                   KFLD                    UAVD
     C                   KFLD                    UTDN
     C                   KFLD                    MAX0B
     C                   KFLD                    MAX0C
     C                   KFLD                    MAX0D
     C     MAKD2K        KLIST
     C                   KFLD                    XSR1
     C                   KFLD                    UAVD
     C                   KFLD                    UTDN
     C     M2KDCK        KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    UTDN
     C                   KFLD                    MAX0B
     C                   KFLD                    MAX0C
     C                   KFLD                    MAX0D
     C     M2KD2K        KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    UTDN
     C     TVIM1K        KLIST
     C                   KFLD                    X1STA
     C                   KFLD                    TIAVD
     C                   KFLD                    TITDN
     C     TVINDK        KLIST
     C                   KFLD                    M0068A
     C     GODS5K        KLIST
     C                   KFLD                    GOGN
     C                   KFLD                    GOTRNR
      *
     C     *ENTRY        PLIST
     C     UPARM2        PARM                    UPARM1          128
     C                   PARM                    UFLAGG            1 0
      *
     C                   MOVE      UIN34         *IN34
     C                   MOVE      U1N07         W1N07
      *
     C                   MOVE      *ZEROS        WSDT
     C                   MOVE      *ZEROS        WSTD
     C                   MOVE      'R'           XSR1
     C                   MOVE      *ZEROS        M0068A
     C                   MOVE      '99999999'    MAX0B
     C                   MOVE      '999999'      MAX0C
     C                   MOVE      '99'          MAX0D
      *
     C     UAVD          CHAIN     TRISTD                             33
     C                   MOVE      S0035         W0035
     C                   MOVE      S0004         X0004
     C                   MOVE      S0010         X0010
     C                   MOVE      *BLANKS       WVEN
     C     TRHKEY        CHAIN(N)  TRIH                               33
      *
     C                   MOVE      '0'           *IN03
     C                   MOVE      '0'           *IN12
     C                   MOVE      '0'           *IN17
      *
     C                   ENDSR
      *
