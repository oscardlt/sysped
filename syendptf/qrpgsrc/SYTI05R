     H DFTACTGRP(*NO)
      *
      ***********************************************************
      *** PROGRAM SKAL KONVERTER LANDGRUPPER FRA TVINN        ***
      ***********************************************************
      *
     FXMLRCAF   IF   E           K DISK
     FXMLRCVF   IF   E           K DISK
     FTVIN01F   UF A E           K DISK
     FKODTS2    IF   E           K DISK
      *
     D                sds
     D ZUSER                 254    263
      *  Prototype for syti05r
     d syti05r         pr
     d usn_                                like(usn)
      *  *ENTRY Interface for Main Procedure
     d syti05r         pi
     d usn                            9
      *----------------------------------------------------------------------
      * Stand Alone Fields - TOP
      *----------------------------------------------------------------------
     d xan08           s              8A
     d x1_xrlev1       s                   like(xrlev1)
     d x1_xrlev2       s                   like(xrlev2)
     d x2_xrlev1       s                   like(xrlev1)
     d x2_xrlev2       s                   like(xrlev2)
     d x2_xrlev3       s                   like(xrlev3)
     d x_t01grp        s                   like(t01grp)
     d x_t01txt        s                   like(t01txt)
     d x_t01pre        s                   like(t01pre)
     d x_t01dtf        s                   like(t01dtf)
     D UP              C                   CONST('ABCDEFGHIJKLMNOPQRST-
     D                                     UVWXYZÆØÅ')
     D LO              C                   CONST('abcdefghijklmnopqrst-
     D                                     uvwxyzæøå')

      *----------------------------------------------------------------------
      /free

       exsr init;

       exsr srxml1;

       *inlr = *on;
       return;

       //**********************************************                ------
       begsr srxml1;
       //**********************************************

         X1_XRLEV1 = 1;
         X1_XRLEV2 = 0;

         chain (xrsn:x1_xrlev1:x1_xrlev2) xmlrcvf;
         dow %found;
           select;
           when xrnv = 'versjon';
           when xrnv = 'landgruppe';
             exsr srxml2;
           endsl;
           x1_xrlev1 = x1_xrlev1 + 1;
           chain (xrsn:x1_xrlev1:x1_xrlev2) xmlrcvf;
         enddo;

         endsr;

       //**********************************************
       begsr srxml2;
       //**********************************************

         x2_xrlev1 = xrlev1;
         x2_xrlev2 = 1;
         x2_xrlev3 = 0;

         chain (xrsn:x2_xrlev1:x2_xrlev2:x2_xrlev3) xmlrcvf;
         dow %found;
           select;
           when xrnv = 'landgruppekode';
             x_t01grp = xrverd;
           when xrnv = 'landgruppenavn';
             x_t01txt = xrverd;
             x_t01txt = %xlate(lo:up:x_t01txt);
           when xrnv = 'preferansekode';
             x_t01pre = xrverd;
           when xrnv = 'fomdato';
             xan08    = %subst(xrverd:1:4) + %subst(xrverd:6:2)
                      + %subst(xrverd:9:2);
             x_t01dtf = %dec(xan08:8:0);
           when xrnv = 'tomdato';
           endsl;
           x2_xrlev2 = x2_xrlev2 + 1;
           chain (xrsn:x2_xrlev1:x2_xrlev2:x2_xrlev3) xmlrcvf;
         enddo;

         exsr opdgrp;

       endsr;

       //**********************************************
       begsr opdgrp;
       //**********************************************

         chain (x_t01grp) tvin01f;
         t01txt = x_t01txt;
         t01pre = x_t01pre;
         t01dtf = x_t01dtf;
         if %found;
           update tvin01fr;
         else;
           t01grp = x_t01grp;
           t01i01 = *blanks;
           t01i02 = *blanks;
           t01i03 = *blanks;
           t01i04 = *blanks;
           t01i05 = *blanks;
           t01i06 = *blanks;
           t01i07 = *blanks;
           t01i08 = *blanks;
           t01i09 = *blanks;
           t01i10 = *blanks;
           t01e01 = *blanks;
           t01e02 = *blanks;
           t01e03 = *blanks;
           t01e04 = *blanks;
           t01e05 = *blanks;
           t01e06 = *blanks;
           t01e07 = *blanks;
           t01e08 = *blanks;
           t01e09 = *blanks;
           t01e10 = *blanks;

           if %subst(t01grp:1:1) = 'T';
             select;
             when t01grp = 'TALL';
             when t01grp = 'TEF' or t01grp = 'TEFT' or t01grp = 'TOES';
             when t01grp = 'TIST';
               t01i01 = 'IN';
               t01i02 = 'LK';
               t01i03 = 'TH';
             when t01grp = 'TGCC';
               t01i01 = 'SA';
               t01i02 = 'AE';
               t01i03 = 'QA';
               t01i04 = 'BH';
               t01i05 = 'KW';
               t01i06 = 'OM';
             when t01grp = 'TGS1' or t01grp = 'TGS2' or t01grp = 'TGSP';
             when t01grp = 'TGS7';
               t01i01 = 'BW';
               t01i02 = 'NA';
             when t01grp = 'TGS8';
               t01i01 = 'SZ';
             when t01grp = 'TGS9';
               t01i01 = 'BW';
               t01i02 = 'NA';
               t01i03 = 'SZ';
             when t01grp = 'TH';
               t01i01 = 'GL';
             when t01grp = 'TI';
               t01i01 = 'FO';
             when t01grp = 'TSAC';
               t01i01 = 'BW';
               t01i02 = 'NA';
               t01i03 = 'SZ';
               t01i04 = 'ZA';
               t01i05 = 'LS';
             when t01grp = 'TTYR';
               t01i01 = 'TR';
             when t01grp = 'TXI';
               t01i01 = 'PS';
             other;
               chain ('S2 ': %subst(t01grp:2:2)) kodts2;
               if %found;
                 t01i01 = %subst(t01grp:2:2);
               endif;
             endsl;
           endif;

           write tvin01fr;
         endif;

       endsr;

       //**********************************************
       begsr init;
       //**********************************************

         xrsn = %dec(usn:9:0);

       endsr;

      /end-free
