     H DATEDIT(*YMD)
      ***********************************************************
      *** ROUTINGFOREPØRSEL/SVAR FIR/FIA                      ***
      ***                                                     ***
      *** INDICATORS  01 - 26 FUNCTION  / ROLL KEYS           ***
      *** INDICATORS USED                                     ***
      ***                                                     ***
      ***  03 -  EXIT                                         ***
      ***  09 -  SELECT NEW PARTICIPANT ID                    ***
      ***  10 -  SEND REQUEST                                 ***
      ***  12 -  CANCEL                                       ***
      ***                                                     ***
      ***  30 -  GENERAL ERROR IN SCREEN                      ***
      ***  31-65 ERROR IN SCREEN                              ***
      ***  80 -  SYSTEM ERROR CNFF                            ***
      ***  81 -  SYSTEM ERROR LOCF                            ***
      ***  99 -  FOR DUMMY READ                               ***
      ***********************************************************
      *
     FFIR00F    IF A E             DISK
     FLOGF      O    E             DISK
     FCNFF      UF   E             DISK
     FLOCL01    IF   E           K DISK
     FLOGL03    IF   E           K DISK
     F                                     RENAME(LOGR:LOGR3)
     FFIA04L    IF   E           K DISK
     FFIA00L    IF   E           K DISK
     FCPFL01    IF   E           K DISK
     FTDV51D    CF   E             WORKSTN
     F                                     SFILE(TDV51H30:ZRECNO)
     F                                     SFILE(TDV51H50:ZRECNO)
      ***********************************************************
      *____________________________________________________________
      * NUM contains only numeric values for check purpose
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
     D NUM             C                   CONST('0123456789')
      *____________________________________________________________
      * Definition of the parameter BUTINF
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
     D                 DS
     D  BUTTRM                 1      1
     D  BUTAOR                 2      4
     D  BUTADE                 5      7
     D  BUTDA1                 8      9
     D  BUTMN1                10     12
     D  BUTPID                13     29
     D  BUTRA1                30     32
     D  BUTRB1                33     39
     D  BUTRC1                40     41  0
     D  BUT1                  30     41
     D  BUTRA2                42     44
     D  BUTRB2                45     51
     D  BUTRC2                52     53  0
     D  BUT2                  42     53
     D  BUTRA3                54     56
     D  BUTRB3                57     63
     D  BUTRC3                64     65  0
     D  BUT3                  54     65
     D  BUTYKL                66     69  0
     D  BUTYAI                70     72
     D  BUTINF                 1     72
     D                 DS
     D  WV047                  1      3
     D  WZB                    4      5
     D  WV044                  6     10
     D  WRKDAY                11     12  0
     D  WZZDS                  1     12
      *____________________________________________________________
      * Parameter list
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     *ENTRY        PLIST
     C                   PARM                    WWAUTI           72
     C                   PARM                    WWUSER           17
      *____________________________________________________________
      * Initialize subroutine
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C                   EXSR      INIT
      *____________________________________________________________
      * Set autotransmit values
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     WWAUTI        IFNE      *BLANKS
     C                   MOVEL     WWAUTI        BUTINF
     C                   MOVEL     BUTPID        DEFADR
     C                   MOVEL     BUTAOR        I0231
     C                   MOVEL     BUTADE        I0232
     C     NUM           CHECK     BUTDA1        TEST              1 0
     C     TEST          IFEQ      0
     C                   MOVEL     BUTDA1        I0251
     C                   ELSE
     C                   Z-ADD     0             I0251
     C                   ENDIF
     C                   MOVEL     BUTMN1        I0252
     C                   MOVEL     BUTYAI        I027
     C                   Z-ADD     BUTYKL        I0291
     C     I0231         IFEQ      *BLANKS
     C     I0232         OREQ      *BLANKS
     C     I0251         OREQ      *ZEROS
     C     I0252         OREQ      *BLANKS
     C                   GOTO      START1
     C                   ENDIF
     C     BUTTRM        IFEQ      '1'                                          <AUTO
     C                   EXSR      SNDRQS
     C                   EXSR      RCVRQS
     C     *IN30         IFEQ      '1'
     C                   MOVE      '9'           BUTTRM
     C                   MOVEL     BUTINF        WWAUTI
     C                   ENDIF
     C                   GOTO      ENDPGM
     C                   ENDIF                                                  <AUTTRM
     C                   ENDIF                                                  <WWAUTI
      *____________________________________________________________
      * Main logic
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     START1        TAG
     C                   EXFMT     TDV51B10
      *
     C                   MOVEA     '00000000'    *IN(30)
     C                   MOVEA     '00000000'    *IN(38)
      *____________________________________________________________
      * Check functions keys
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
      *____________________________________________________________
      * F3=Exit
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     *IN03         IFEQ      '1'
     C                   GOTO      ENDPGM
     C                   END                                                    <*IN03
      *____________________________________________________________
      * *IN80/81=System error. Requested action can't take place
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     *IN80         IFEQ      '1'
     C                   MOVE      *ON           *IN30
     C                   MOVE      'SMA0022'     MSGNR
     C                   GOTO      START1
     C                   END                                                    <*IN80
     C     *IN81         IFEQ      '1'
     C                   MOVE      *ON           *IN30
     C                   MOVE      'SMA0023'     MSGNR
     C                   GOTO      START1
     C                   END                                                    <*IN80
      *____________________________________________________________
      * F9=Select new participant ID
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     *IN09         IFEQ      '1'
     C                   EXSR      NEWPAR
     C                   GOTO      START1
     C                   END                                                    <*IN09
      *____________________________________________________________
      * F10=Send Request and wait for answer
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     *IN10         IFEQ      '1'
     C                   EXSR      VALSR
     C     *IN30         CABEQ     '1'           START1
     C                   EXSR      SNDRQS
     C                   EXSR      RCVRQS
     C     *IN12         IFEQ      '1'
     C                   GOTO      START1
     C                   ENDIF
     C                   GOTO      ENDPGM
     C                   END                                                    <*IN10
      *____________________________________________________________
      * When Enter, perform validity check and display format again
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C                   EXSR      VALSR
     C                   GOTO      START1
      *
     C     ENDPGM        TAG
     C                   MOVEL     BUTINF        WWAUTI
     C                   MOVE      *ON           *INLR
     C                   RETURN
      *****************************************************************
     C     SELRTG        BEGSR
      * Select preferred routing
      *****************************************************************
      *______________
      * Clear Subfil
      *""""""""""""""
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     TDV51K60
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     0             ZRECNO
      *_____________
      * Fyll Subfil
      *"""""""""""""
     C     RRECNR        SETLL     FIA04L                             77
     C     RRECNR        READE     FIA04L                                 94
      *
     C     *IN94         DOWEQ     '0'
     C                   ADD       1             ZRECNO
     C                   WRITE     TDV51H50
     C     RRECNR        READE     FIA04L                                 94
     C                   ENDDO                                                  <*IN94
      *
     C                   Z-ADD     ZRECNO        YRECNO            5 0
      *________________
      * Display Subfil
      *""""""""""""""""
     C     START3        TAG
     C     ZRECNO        IFEQ      0
     C                   MOVEA     '10'          *IN(91)
     C                   ELSE
     C                   Z-ADD     1             ZRECNO
     C                   MOVEA     '11'          *IN(91)
     C                   ENDIF
      *
     C                   WRITE     TDV51B20
     C                   EXFMT     TDV51K60
      *
     C                   MOVEA     '0000000'     *IN(30)
      *
     C                   SETOFF                                       9998
      *** F3=Gå ut
     C     *IN03         CABEQ     '1'           SELRT9
      *** F12=Annuller
     C     *IN12         CABEQ     '1'           SELRT9
      *** Finn valg
     C     ZRECNO        IFNE      0
     C                   SETOFF                                       919299
     C     *IN98         DOWEQ     '0'
     C                   READC     TDV51H50                               98
     C     *IN98         CABEQ     '1'           SELRT9
      *** blank
     C     WSNR          IFEQ      ' '
     C                   MOVE      *BLANK        WSNR
     C                   UPDATE    TDV51H50
     C                   END                                                    <WSNR
      *** 1,2 eller 3. Angir også rekkefølge på routing
      *** Er 1,1,1 angitt brukes fysisk rekkefølge
     C     WSNR          IFEQ      '1'
     C     WSNR          OREQ      '2'
     C     WSNR          OREQ      '3'
     C                   EXSR      CRTRTG
     C                   MOVE      *BLANK        WSNR
     C                   UPDATE    TDV51H50
     C                   END                                                    <WSNR
      *
     C                   ENDDO                                                  <*IN98
     C                   END                                                    <ZRECNO
      *
     C                   GOTO      START3
     C     SELRT9        ENDSR
      *****************************************************************
     C     CRTRTG        BEGSR
      * Create routing for parameterlist
      *****************************************************************
     C                   MOVEL     *BLANKS       WZZDS
     C                   MOVEL     V043          WZB
     C                   MOVEL     V044          WV044
     C                   MOVEL     V047          WV047
     C                   Z-ADD     V0251         WRKDAY
      * Check Day Change Indicator to find date
     C                   SELECT
     C     V0411         WHENEQ    'P'                                          Previous
     C                   Z-SUB     1             W
     C     V0411         WHENEQ    'N'                                          Next
     C                   Z-ADD     1             W
     C     V0411         WHENEQ    'S'                                          Second
     C                   Z-ADD     2             W
     C     V0411         WHENEQ    'T'                                          Third
     C                   Z-ADD     3             W
     C                   OTHER                                                  Third
     C                   Z-ADD     0             W
     C                   ENDSL
     C     WRKDAY        ADD       W             WRKDAY
     C     WRKDAY        IFLE      0
     C                   SELECT
     C     UMONTH        WHENEQ    02
     C                   Z-ADD     28            WRKDAY
     C     UMONTH        WHENEQ    04
     C     UMONTH        OREQ      06
     C     UMONTH        OREQ      09
     C     UMONTH        OREQ      11
     C                   Z-ADD     30            WRKDAY
     C     UMONTH        WHENEQ    01
     C     UMONTH        OREQ      03
     C     UMONTH        OREQ      05
     C     UMONTH        OREQ      07
     C     UMONTH        OREQ      08
     C     UMONTH        OREQ      10
     C     UMONTH        OREQ      12
     C                   Z-ADD     31            WRKDAY
     C                   ENDSL
     C                   ENDIF
     C     WRKDAY        IFGT      31
     C                   Z-ADD     1             WRKDAY
     C                   ENDIF
     C                   SELECT
     C     UMONTH        WHENEQ    02
     C     WRKDAY        IFGT      28
     C                   Z-ADD     1             WRKDAY
     C                   ENDIF
     C     UMONTH        WHENEQ    04
     C     UMONTH        OREQ      06
     C     UMONTH        OREQ      09
     C     UMONTH        OREQ      11
     C     WRKDAY        IFGT      30
     C                   Z-ADD     1             WRKDAY
     C                   ENDIF
     C                   ENDSL
     C     WSNR          IFEQ      '1'
     C                   MOVEL     WZZDS         BUT1
     C                   ENDIF
     C     WSNR          IFEQ      '2'
     C                   MOVEL     WZZDS         BUT2
     C                   ENDIF
     C     WSNR          IFEQ      '3'
     C                   MOVEL     WZZDS         BUT3
     C                   ENDIF
     C     CRTRT9        ENDSR
      *****************************************************************
     C     DSPMES        BEGSR
      * Display message received when not Answer Detail
      *****************************************************************
      *______________
      * Clear Subfil
      *""""""""""""""
     C                   MOVE      '1'           *IN93
     C                   WRITE     TDV51K40
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     0             ZRECNO
      *_____________
      * Fyll Subfil
      *"""""""""""""
     C     RRECNR        SETLL     CPFL01                             77
     C     RRECNR        READE     CPFL01                                 94
      *
     C     *IN94         DOWEQ     '0'
     C                   ADD       1             ZRECNO
     C                   MOVEL     CPDATA        DATA
     C                   WRITE     TDV51H30
     C     RRECNR        READE     CPFL01                                 94
     C                   ENDDO                                                  <*IN94
      *
     C                   Z-ADD     ZRECNO        YRECNO            5 0
      *________________
      * Display Subfil
      *""""""""""""""""
     C     DSPME3        TAG
     C     ZRECNO        IFEQ      0
     C                   MOVEA     '10'          *IN(91)
     C                   ELSE
     C                   Z-ADD     1             ZRECNO
     C                   MOVEA     '11'          *IN(91)
     C                   ENDIF
      *
     C                   WRITE     TDV51B20
     C                   EXFMT     TDV51K40
      *
     C                   MOVEA     '0000000'     *IN(30)
      *
     C                   SETOFF                                       9998
      *** F3=Gå ut
     C     *IN03         IFEQ      '1'
     C                   GOTO      DSPME9
     C                   END                                                    <*IN03
      *** F12=Cancel
     C     *IN12         IFEQ      '1'
     C                   GOTO      DSPME9
     C                   END                                                    <*IN03
      *
     C                   GOTO      DSPME3
     C     DSPME9        ENDSR
      *****************************************************************
     C     INIT          BEGSR
      *****************************************************************
     C                   Z-ADD     0             W                 1 0
     C                   Z-ADD     0             X                 3 0
     C                   Z-ADD     5             A                 2 0
     C                   Z-ADD     45            B                 3 0
     C                   Z-ADD     0             RRECNR            9 0
     C                   MOVEL     'SMADTATQ'    QTNAME           10
     C                   MOVEL     'SMADTARQ'    QRNAME           10
     C                   MOVEL     '*LIBL'       QTLIB            10
     C                   MOVEL     '*LIBL'       QRLIB            10
     C                   MOVEL     *BLANKS       QRFIEL            9
     C                   Z-ADD     9             QTLEN             5 0
     C                   Z-ADD     9             QRLEN             5 0
     C     1             CHAIN(N)  CNFF                               90
     C     *IN90         IFEQ      '0'
     C                   Z-ADD     CNTFIR        QWAIT             5 0
     C                   ADD       5             QWAIT
     C                   ELSE
     C                   MOVE      *ON           *IN30
     C                   MOVE      *ON           *IN80
     C                   MOVE      'SMA0022'     MSGNR
     C                   Z-ADD     10            QWAIT
     C                   END
     C     WWUSER        CHAIN     LOCL01                             90
     C     *IN90         IFEQ      '1'
     C                   MOVE      *ON           *IN30
     C                   MOVE      *ON           *IN81
     C                   MOVE      'SMA0023'     MSGNR
     C                   END                                                    <*IN90
     C                   MOVE      'EQ'          QORDER            2
     C                   Z-ADD     9             QKILEN            3 0
     C                   MOVEL     *BLANKS       QKEY              9
     C                   MOVEL     *BLANKS       QBACK             9
     C                   MOVEL     *BLANKS       QTFIEL            9
     C                   Z-ADD     10            QSNDRL            3 0
     C                   MOVEL     *BLANKS       QSNDR            10
     C                   MOVEL     LODEFA        DEFADR           17
      *____________________________________________________________
      * Dummy read of FIR00F
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C                   MOVE      *OFF          *IN99
     C  N99
     CAN 99              READ      FIR00F                                 90
      *
     C                   ENDSR
      *
      *****************************************************************
     C     NEWPAR        BEGSR
      * Display window for retreive of new participant ID
      *       (For whom this message is to be sent)
      *****************************************************************
     C                   CALL      'SM900R'
     C                   PARM      *BLANKS       WDEFAD           17
     C                   PARM                    A
     C                   PARM                    B
     C                   PARM      '0'           WIN12             1
     C                   PARM      '0'           WPNONE            1
     C     WPNONE        IFEQ      '1'
     C                   MOVE      *ON           *IN30
     C                   MOVE      'SMA0018'     MSGNR
     C                   GOTO      ENDPAR
     C                   END                                                    <WPNONE
     C                   MOVE      *ON           *IN30
     C     WIN12         IFEQ      '0'
     C     WDEFAD        ANDNE     *BLANKS
     C                   MOVEL     WDEFAD        DEFADR
     C     LODEFA        IFEQ      DEFADR
     C                   MOVE      'SMA0020'     MSGNR
     C                   ELSE                                                   <LODEFA
     C                   MOVE      'SMA0019'     MSGNR
     C                   END                                                    <LODEFA
     C                   ELSE                                                   <WIN12
     C                   MOVE      'SMA0021'     MSGNR
     C                   END                                                    <WIN12
      *
     C     ENDPAR        ENDSR
      *
      *****************************************************************
     C     RCVRQS        BEGSR
      * Receive FIA
      *****************************************************************
     C                   MOVEL     CNRECN        QKEY
     C                   MOVEL     CNRECN        QBACK
     C                   CALL      'QRCVDTAQ'
     C                   PARM                    QRNAME
     C                   PARM                    QRLIB
     C                   PARM                    QRLEN
     C                   PARM                    QRFIEL
     C                   PARM                    QWAIT
     C                   PARM                    QORDER
     C                   PARM                    QKILEN
     C                   PARM                    QKEY
     C                   PARM                    QSNDRL
     C                   PARM                    QSNDR
      *____________________________________________________________
      * Check if data has ben received within time limit
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     QRLEN         IFNE      0
      *
     C     QKEY          IFEQ      QBACK                                        <Correct ID
      *____________________________________________________________
      * Reply is from Dataq, check LOGF and display member
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C                   MOVEL     QRFIEL        RRECNR
     C     RRECNR        CHAIN     LOGL03                             90
     C     *IN90         IFEQ      '0'
     C     LGMSID        IFEQ      'FIA'
     C     LGMSID        OREQ      'FNA'
     C     LGMSID        OREQ      'FMA'
      * If Answer Details exists present SF to select routing else
      * present SF to display message received.
     C     RRECNR        SETLL     FIA04L                                 90
     C     *IN90         IFEQ      '1'                                          <FOUND
     C     RRECNR        CHAIN     FIA00L                             90
     C                   EXSR      SELRTG
     C                   ELSE                                                   <NOT FOUND
     C                   EXSR      DSPMES
     C                   ENDIF                                                  <*IN90
     C     *IN03         IFEQ      '1'
     C     *IN12         OREQ      '1'
     C                   MOVE      *BLANKS       BUT1
     C                   MOVE      *BLANKS       BUT2
     C                   MOVE      *BLANKS       BUT3
     C                   MOVE      *ZEROS        BUTRC1
     C                   MOVE      *ZEROS        BUTRC2
     C                   MOVE      *ZEROS        BUTRC3
     C                   ENDIF
     C                   ELSE                                                   <LGMSID
      *______________________________________________________________
      * Received Message ID is not correct, display message
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C                   MOVE      *ON           *IN30
     C                   MOVE      'SMA0011'     MSGNR
     C                   END                                                    <LGMSID
     C                   ELSE                                                   <*IN90
      *______________________________________________________________
      * Corresponding record not found in LOGF, display message
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C                   MOVE      *ON           *IN30
     C                   MOVE      'SMA0012'     MSGNR
     C                   END                                                    <*IN90
     C                   ELSE                                                   <QKEY
      *______________________________________________________________
      * Received Record ID from DATAQ is not correct, display message
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C                   MOVE      *ON           *IN30
     C                   MOVE      'SMA0009'     MSGNR
     C                   END                                                    <QKEY
     C                   ELSE                                                   <QRLEN
      *_____________________________________________________________
      * Data has not ben received within time limit, display message
      *"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C                   MOVE      *ON           *IN30
     C                   MOVE      'SMA0010'     MSGNR
     C                   END                                                    <QRLEN
      *
     C                   ENDSR
      *
      *****************************************************************
     C     SNDRQS        BEGSR
      * Send FRR
      *****************************************************************
      * Initialize LOGF
     C                   CLEAR                   LOGR
      *____________________________________________________________
      * Retrieve last used recordnumber from CNFF and update
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     1             CHAIN     CNFF                               90
     C     *IN90         IFEQ      '0'
     C                   ADD       1             CNRECN
     C                   UPDATE    CNFR
     C                   Z-ADD     CNRECN        I00REC
     C                   Z-ADD     CNRECN        LGRECN                         <LOGF
     C                   Z-ADD     CNRECN        LGTRID                         <LOGF
      *____________________________________________________________
      * Retreive data from local file
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C                   MOVEL     LOID          LGSID                          <LOGF
     C                   MOVEL     DEFADR        LGID
      *____________________________________________________________
      * Write record to FIR master file
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C                   WRITE     FIR00R
      *____________________________________________________________
      * Write record to LOG file
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C                   MOVEL     *BLANKS       LGIDNE
     C                   MOVEL     'FIR'         LGMSID
     C                   MOVEL     *BLANKS       LGSNET
     C                   MOVEL     'T'           LGTR
     C                   MOVEL     'A'           LGSTAT
     C                   MOVEL     UDATE         LGDATE
     C                   TIME                    LGTIME
      *
     C                   WRITE     LOGR
      *_____________________________________________________________
      * Send record to dataq SMADTATQ and wait for reply in SMADTARQ
      *"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C                   MOVEL     LGRECN        QTFIEL
     C                   CALL      'QSNDDTAQ'
     C                   PARM                    QTNAME
     C                   PARM                    QTLIB
     C                   PARM                    QTLEN
     C                   PARM                    QTFIEL
      *
     C                   END                                                    <*IN90
      *
     C                   ENDSR
      *
      *****************************************************************
     C     VALSR         BEGSR
      *****************************************************************
     C                   MOVEA     '00000000'    *IN(30)
     C                   MOVEA     '00000000'    *IN(38)
      *_______________________
      * Perform validity check
      *"""""""""""""""""""""""
     C     I02111        IFNE      *ZEROS                                       <Day
     C     I02112        ORNE      *BLANKS                                      <Month
     C     I02113        ORNE      *ZEROS                                       <Time
     C     I02111        IFEQ      *ZEROS                                       <Day
     C     I02112        OREQ      *BLANKS                                      <Month
     C                   MOVE      *ON           *IN30
     C                   MOVE      *ON           *IN36
     C                   MOVE      'SMA0041'     MSGNR
     C                   END                                                    <I02111
     C                   END                                                    <I02111
     C     I0291         IFNE      *ZEROS                                       <Time
     C     I02111        ORNE      *ZEROS                                       <Day
     C     I02112        ORNE      *BLANKS                                      <Month
     C     I02113        ORNE      *ZEROS                                       <Time
     C     I0251         IFEQ      *ZEROS                                       <Day
     C                   MOVE      *ON           *IN30
     C                   MOVE      *ON           *IN35
     C                   MOVE      'SMA0040'     MSGNR
     C                   END                                                    <I0251
     C                   END                                                    <I0291
     C     I02132        IFNE      *BLANKS                                      <Airport
     C     I02131        IFNE      'E'                                          <C.R. Indic.
     C     I02131        ANDNE     'L'                                          <C.R. Indic.
     C                   MOVE      *ON           *IN30
     C                   MOVE      *ON           *IN34
     C                   MOVE      'SMA0039'     MSGNR
     C                   END                                                    <I02131
     C                   END                                                    <I02132
     C     I0231         IFEQ      *BLANKS                                       Airport
     C                   MOVE      *ON           *IN30
     C                   MOVE      *ON           *IN31
     C                   MOVE      'SMA0003'     MSGNR
     C                   END                                                    <I0231
     C     I0232         IFEQ      *BLANKS                                       Airport
     C                   MOVE      *ON           *IN30
     C                   MOVE      *ON           *IN32
     C                   MOVE      'SMA0003'     MSGNR
     C                   END                                                    <I0232
     C     I0252         IFEQ      *BLANKS                                       Month
     C                   MOVE      *ON           *IN30
     C                   MOVE      *ON           *IN33
     C                   MOVE      'SMA0003'     MSGNR
     C                   END                                                    <I0252
      *
     C                   ENDSR
