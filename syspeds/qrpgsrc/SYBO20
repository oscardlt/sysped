********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 SH  PTF10 NY PARAMETER SYFA20  ...
********************************************************************
      *
      *******************************************************
      * DETTE PROGRAM FINNER FRAM DE AKTUELLE  OPPDRAG
      * FOR SÅ Å KALLE FAKTURAUTSKRIFT
      *******************************************************
     FKODTV     IF   E           K DISK
     FDUMBO     O  A E           K DISK    USROPN
     FKODTH     IF   E           K DISK
     FHEAD8     IF   E           K DISK
     FTURER     UF   E           K DISK
     FSYBO20FM  CF   E             WORKSTN
     F                                     SFILE(BILD1:ZRECNO)
     D                UDS
     D  FIRMA                  3      4
     D  WAVDUU                15     18  0
     D  WTUR                  22     29  0
      *//////////////
      *  Hodelogikk /
      *//////////////
     C                   EXSR      SRA
     C                   EXSR      SRB
     C                   EXSR      SRC
     C   16              EXSR      SRD
     C                   MOVE      '1'           *INLR
      *////////////////////////////////////////////////////////////
      *  Initiering                                           SRA /
      *////////////////////////////////////////////////////////////
     C     SRA           BEGSR
      *__________________
      * Definiere Nøkkler
      *""""""""""""""""""
     C     KOVKEY        KLIST
     C                   KFLD                    KOVUNI
     C                   KFLD                    KOVAVD
     C     KOHKEY        KLIST
     C                   KFLD                    KOHUNI
     C                   KFLD                    KOHAVD
     C     TUKEY         KLIST
     C                   KFLD                    TUAVD
     C                   KFLD                    TUPRO
     C     HEAKEY        KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEPRO
     C                   KFLD                    HEPOS1
      *_________________
      * Input parametrer
      *"""""""""""""""""
     C     *ENTRY        PLIST
     C                   PARM                    UKOPI             1
      *__________________
      * Output parametrer
      *""""""""""""""""""
     C     PLISTA        PLIST
     C                   PARM                    PA1
N01  C                   PARM                    PA1E
N01  C                   PARM                    PA1S
     C                   PARM                    PA2
     C                   PARM                    PA3
     C                   PARM                    PA4
     C                   PARM                    PA5
     C                   PARM                    PA6
     C                   PARM                    UKOPI
     C                   PARM                    UEMAIL            1
     C                   PARM                    UIBRUK            1
      *_____________
      * Init av felt
      *"""""""""""""
     C                   MOVE      WAVDUU        WAVD              4 0
     C                   MOVE      WAVDUU        KOVAVD
     C                   MOVE      WAVDUU        KOHAVD
     C                   MOVE      WAVD          TUAVD
     C                   MOVE      WTUR          TUPRO
     C                   MOVE      'V'           KOVUNI
     C                   MOVEL     'H'           KOHUNI
     C                   MOVE      '00'          PA1               2
N01  C                   MOVE      '00'          PA1E              2
N01  C                   MOVE      '00'          PA1S              2
     C                   MOVE      'N'           PA2               1
     C                   MOVE      '1'           PA3               1
     C                   MOVE      '0'           PA4               1
     C                   MOVE      '0'           PA5               1
     C                   MOVE      '0'           PA6               1
     C     *LIKE         DEFINE    ZRECNO        WRECNO
     C     *LIKE         DEFINE    ZRECNO        WX
      *______________________
      * Hente standardverdier
      *""""""""""""""""""""""
     C* FINNER OM AVDELING HAR AVRUNDING
     C     KOVKEY        CHAIN     KODTV                              33
     C                   MOVE      KOVAVR        FIRAVR            1
     C* FINNER OM AVDELING HAR HEADING
     C     KOHKEY        CHAIN     KODTH                              33
     C     TUKEY         CHAIN(N)  TURER                              60
      *______________
      * Tømme Subfile
      *""""""""""""""
     C                   MOVE      '1'           *IN93
     C                   WRITE     BILD2
     C                   MOVEA     '000'         *IN(93)
     C                   Z-ADD     *ZERO         ZRECNO
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Les HEADF                                            SRB /
      *////////////////////////////////////////////////////////////
     C     SRB           BEGSR
     C* HENTER OPPLYSNINGER FRA HEADINGREGISTER fra alle avdelinger
     C                   MOVE      *ZEROS        KOVAVD
     C                   Z-ADD     0             ANTOPD            5 0
     C     KOVKEY        SETLL     KODTV
     C*
     C     STAFIR        TAG
     C                   SETOFF                                       99
     C                   READ      KODTV                                  99
     C   99              GOTO      SLUTT
     C                   MOVE      KOVAVD        HEAVD
     C                   MOVE      WTUR          HEPRO
     C                   MOVE      *BLANKS       HEPOS1
     C     HEAKEY        SETLL     HEAD8
     C*
     C     STL           TAG
     C                   SETOFF                                       33
     C                   READ      HEAD8                                  33
     C   33              GOTO      SLUTT
     C* GÅR TIL NESTE AVDELING VED IKKE FLERE TURER
     C     WTUR          IFNE      HEPRO
     C                   GOTO      STAFIR
     C                   END
     C* GÅR TIL NESTE HVIS IKKE OPPDRAG HAR BLANK STATUS
     C                   ADD       1             ZRECNO
     C                   WRITE     BILD1
     C                   GOTO      STL
     C     SLUTT         TAG
     C     ZRECNO        IFEQ      0
     C                   MOVE      *ZEROS        HEKNKF
     C                   MOVE      *BLANKS       ZOP
     C                   MOVE      *ZEROS        HEAVD
     C                   MOVE      *ZEROS        HEOPD
     C                   MOVE      *BLANKS       HEPOS1
     C                   ADD       1             ZRECNO
     C                   WRITE     BILD1
     C                   END
     C                   MOVE      '1'           *IN94
     C                   Z-ADD     ZRECNO        WRECNO
     C                   Z-ADD     1             ZRECNO
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  BEARBEIDE SUBFILE                                    SRC /
      *////////////////////////////////////////////////////////////
     C     SRC           BEGSR
     C     SRC3          TAG
      *
     C                   MOVEA     '11'          *IN(91)
     C  N30              MOVE      'SY00000'     ZMSGNO
     C                   WRITE     BILDØ
     C                   EXFMT     BILD2
      *
     C     *IN03         IFEQ      '0'
     C     *IN12         ANDEQ     '0'
     C     *IN16         ANDEQ     '0'
      *
     C                   MOVEA     '00'          *IN(91)
      *_______
      * Valg ?
      *"""""""
     C                   MOVE      '0'           *IN52
     C                   MOVE      '0'           *IN95
     C     *IN52         DOWEQ     '0'
      *
     C                   READC     BILD1                                  52
     C     *IN52         IFEQ      '0'
     C     ZOP           CASEQ     '4'           SRCD                           Slette
     C                   END
     C                   END
      *
     C                   END
      *
     C     *IN03         CABEQ     '1'           SRC9
     C     *IN95         CABEQ     '1'           SRC3
     C     *IN95         CABEQ     '0'           SRC3
      *
     C                   END
      *
     C     SRC9          ENDSR
      *////////////////////////////////////////////////////////////
      *  Slette                                              SRCD /
      *////////////////////////////////////////////////////////////
     C     SRCD          BEGSR
      *_________________
      * Oppdatere SUBFIL
      *"""""""""""""""""
     C                   MOVE      *ZEROS        HEKNKF
     C                   MOVE      *BLANKS       ZOP
     C                   MOVE      *ZEROS        HEAVD
     C                   MOVE      *ZEROS        HEOPD
     C                   MOVE      *BLANKS       HEPOS1
     C                   UPDATE    BILD1
     C                   MOVE      '1'           *IN95
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Les Subfile og fakturer                              SRD /
      *////////////////////////////////////////////////////////////
     C     SRD           BEGSR
      *
     C     TUKEY         CHAIN     TURER                              60
     C  N60UKOPI         IFNE      'U'
     C                   MOVE      'F'           TUST
     C                   EXCEPT    OPDTUR
     C                   ELSE
     C                   EXCEPT    DUMTUR
     C                   END
      *
     C                   OPEN      DUMBO
     C     1             DO        WRECNO        WX
     C     WX            CHAIN     BILD1                              52
     C     *IN52         IFEQ      '0'
     C     HEOPD         IFGT      0
     C                   MOVE      HEAVD         WAVD
     C                   ADD       1             ANTOPD
     C                   MOVE      HEKNKF        SKKN
     C                   MOVE      HEPOS1        SKPOS
     C                   MOVE      HEAVD         SKAVD
     C                   MOVE      HEOPD         SKOPD
     C                   WRITE     SKPOSR
     C                   END
     C                   END
     C                   END
     C                   CLOSE     DUMBO
     C* KALLER FAKTURERING HVIS ANTALL > 0
     C     ANTOPD        IFGT      *ZEROS
     C                   CALL      'SYFA20'      PLISTA
     C                   END
     C                   ENDSR
     OTURERR    E            OPDTUR
     O                       TUST
     OTURERR    E            DUMTUR
