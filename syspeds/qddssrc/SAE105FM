     A                                      DSPSIZ(27 132 *DS3)
     A                                      PRINT
      *
     A          R SUBFIL                    SFL
     A            HENAK         30   H
     A            HENT           7Y 0H
     A            HEVKT          9Y 0H
     A            ODDEVE         1Y 0H
     A            WSNR           1A  B  6  2VALUES(' ' '1' '2' '4' '6' 'W' 'P' -
     A                                      'L' '8')
     A            SFDATO         6Y 0O  6  4
     A  80                                  DSPATR(UL)
     A            SFTARF         3A  O  6 17
     A  80                                  COLOR(WHT)
     A            HEAVD          4Y 0O  6 25EDTCDE(4)
     A  80                                  COLOR(WHT)
     A            HEOPD          7Y 0O  6 30EDTCDE(4)
     A  80                                  COLOR(WHT)
     A            SFEXTR        15A  O  6 38
     A  80                                  COLOR(WHT)
     A            SFARK          1A  O  6 54
     A  80                                  COLOR(WHT)
     A            SFMOTT    R        O  6 56REFFLD(DOKUFR/DFAD1L *LIBL/DOKUF)
     A  80                                  COLOR(WHT)
     A            SFANTK         5Y 0O  6 87EDTCDE(4)
     A  80                                  COLOR(WHT)
     A            SFVKT          7Y 0O  6 93EDTCDE(4)
     A  80                                  COLOR(WHT)
     A            HEGN          15A  O  6101
     A  80                                  COLOR(WHT)
     A            SFSIST         1A  O  6117
     A  80                                  COLOR(WHT)
     A            SFMERK         1A  O  6121COLOR(RED)
     A            NYSIG          3A  I  6 21
     A  53                                  DSPATR(PR)
     A  80                                  COLOR(WHT)
     A            SFDAGE         5  0O  6 11EDTCDE(Z)
     A                                      COLOR(WHT)
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0019)
     A                                      CF03(03 'EXIT/AVBRYT')
     A                                      CF05(05 'Frisk')
     A                                      CF08(08 'SØK')
     A                                      CF09(09 'BYTTSG')
     A                                      CF10(10 'PLUKKMODE')
     A                                      CF12(12 'TILBAKE')
     A                                      CF20(20 'BYTT SORT')
     A                                      CF23(23 'TILDEL')
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/SAD105'
     A                                      COLOR(BLU)
     A                                  1 21'Arbeide med ufortollede oppdrag'
     A                                      COLOR(WHT)
     A                                  1 63'Se kun Tarifør...:'
     A                                      COLOR(WHT)
     A            W2SG           3A  O  1 82COLOR(WHT)
     A                                  1 86'(F8=endre)'
     A                                  1121DATE
     A                                      EDTCDE(Y)
     A                                  2  2MSGCON(019 SHU1357 *LIBL/SYMSGF)
     A            WSMERK         1A  B  2 22VALUES('J' 'N' 'K')
     A                                  2 26'Vis sendte til tvinn.'
     A            WSTVIN         1A  B  2 49VALUES('J' 'N')
     A  51                              2 63'Plukke til Tarif.:'
     A                                      COLOR(WHT)
     A  51        W3SG           3A  O  2 82COLOR(WHT)
     A  51                              2 86'(Plukkemode er slått på)'
     A                                      COLOR(RED)
     A            ZRECNO         4S 0B  2115SFLRCDNBR
     A                                      DSPATR(ND)
     A                                      DSPATR(PR)
     A                                  2121TIME
     A                                  3  2'Velg og trykk Enter: 1=Plukk til i-
     A                                       plukkemode 2=arbeide med  8=Fritek-
     A                                      st/Merknad       L=Se T&T-Logg P=Se-
     A                                       arkiverte/PDF'
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(026 SHU1358 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  2'O O.Dato Dager Tar  Ny Avdn Opdnr -
     A                                        Extern ref        Mottager       -
     A                                                      A.klli  Vekt  Godsn-
     A                                      ummer     Sad Mrk'
     A                                      COLOR(WHT)
     A                                      DSPATR(UL)
     A                                  4 29'Input ny signatur:'
     A            WSSIGI         1   B  4 49VALUES('J' 'N')
      *
     A          R BUNN
     A                                      CF03(03 'EXIT/AVBRYT')
     A                                      CF05(05 'Frisk')
     A                                      CF08(08 'SØK')
     A                                      CF09(09 'BYTTSG')
     A                                      CF10(10 'PLUKKMODE')
     A                                      CF12(12 'TILBAKE')
     A                                      CF20(20 'bytt sortering')
     A                                      CF23(23 'TILDEL')
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      SLNO(25)
     A                                      CLRL(03)
     A                                      OVERLAY
     A  50                              1  2MSGCON(078 JOV1483 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  2  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  2 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  2 42MSGCON(018 DIV0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  2 61'F9=Bytt signatur   '
     A                                      COLOR(BLU)
     A  98                              3  2MSGCON(078 DIV0012 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A  51                              2 81'F10=Slå av plukkmode'
     A                                      COLOR(BLU)
     A N51                              2 81'F10=Slå på plukkmode'
     A                                      COLOR(BLU)
     A                                  2103'F20=bytt sortering'
     A                                      COLOR(BLU)
      *
     A          R VAREL2
     A                                      CF12(12 'Forrige')
     A                                      SLNO(24)
     A                                      CLRL(04)
     A                                      OVERLAY
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  5'VIS kun poster som tilfredsstiller-
     A                                       underutvalg...                    -
     A                                                                         -
     A                                         '
     A                                      COLOR(WHT)
     A            W2SG           3A  B  2 11
     A            W2AVD          4Y 0B  2 15EDTCDE(4)
     A            W2OPD          7Y 0B  2 20EDTCDE(4)
     A            W2HESG         3A  B  2 28
     A            W2EXTR        15A  B  2 32
     A            W2ARK          1A  B  2 48
     A            W2MOTT        15A  B  2 50
     A                                  2 66'<--Inneholder..'
     A            W2GN          15A  B  2 95
     A                                  3  5'(Tar: *UT =Vis Utildelte,Blank/*AL-
     A                                      =Vis ALLE)'
     A                                  3 72MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
      *
     A          R NYWRK
     A                                      CF03(03 'EXIT/AVBRYT')
     A                                      CF05(05 'Frisk')
     A                                      CF08(08 'SØK')
     A                                      CF09(09 'BYTTSG')
     A                                      CF10(10 'PLUKKMODE')
     A                                      CF12(12 'TILBAKE')
     A                                      CF20(20 'bytt sortering')
     A                                  1  2'SYSPED/SAD105'
     A                                      COLOR(BLU)
     A                                  1 21'Arbeide med ufortollede oppdrag'
     A                                      COLOR(WHT)
     A                                  1 63'Se kun Tarifør...:'
     A                                      COLOR(WHT)
     A            W2SG           3A  O  1 82COLOR(WHT)
     A                                  1 86'(F8=endre)'
     A                                  1121DATE
     A                                      EDTCDE(Y)
     A  51                              2 63'Mode=Plukk -> Tar:'
     A                                      COLOR(WHT)
     A  51        W3SG           3A  O  2 82COLOR(WHT)
     A  51                              2 86'(F10=slå av plukkmode)'
     A                                  2121TIME
     A                                  3  2'Velg og trykk Enter: 1=Tildel 2=En-
     A                                      dre/fortolle               8=Fritek-
     A                                      st/Merknad       L=Se T&T-Logg P=Se-
     A                                       arkiverte/PDF        '
     A                                      COLOR(BLU)
     A                                  4  2'                                  -
     A                                                                         -
     A                                                                         -
     A                                                            '
     A                                      COLOR(BLU)
     A                                  5  2'O O.Dato Tar  Avd Oppd.nr Sig Ekst-
     A                                      ern Ref    HF Mottaker             -
     A                                                A.Kll    Vekt Godsnummer -
     A                                          SAD   Merkn.          '
     A                                      COLOR(WHT)
     A N50                             15 15'INGEN OPPDRAG TILFREDSTILLER UTVAL-
     A                                      GET'
     A  50                             15 15MSGCON(060 JOV0536 *LIBL/SYMSGF)
     A  50                             17 15'Tast F8 for å gjøre nytt Søk/Under-
     A                                      utvalg'
     A                                 26  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 26 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 26 42MSGCON(018 DIV0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 26 61'F9=Bytt signatur   '
     A                                      COLOR(BLU)
     A                                 26 81'F10=Plukk/Tildel'
     A                                      COLOR(BLU)
     A                                 26103'F20=bytt sortering'
     A                                      COLOR(BLU)
      *
     A          R VAREL3
     A                                      CF12(12 'Forrige')
     A                                      SLNO(24)
     A                                      CLRL(04)
     A                                      OVERLAY
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            W3SG           3A  B  2 20
     A                                  3  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  1  5'Endre mode (F10) Sett ny signatur -
     A                                      som skal plukkes til ved opsjon 1'
     A                                      COLOR(WHT)
     A                                  2  5'Plukke til :'
      *
     A          R FRITXT
     A                                      WINDOW(*DFT 14 70 *NOMSGLIN)
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A                                      CF12(12 'TILBAKE')
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1  2'SYSPED/SAD105'
     A                                      COLOR(BLU)
     A                                  1 18'Merknad til SAD'
     A                                      COLOR(WHT)
     A            HEAVD          4S 0O  1 35COLOR(WHT)
     A                                  1 40'/'
     A            HEOPD          7S 0O  1 42COLOR(WHT)
     A            SFEXTR        15A  O  1 51COLOR(WHT)
     A                                  2  2'Tildelt:'
     A            SAISG          3A  O  2 11COLOR(WHT)
     A                                  2 15'Av:'
     A            SAIUSE        10A  O  2 19COLOR(WHT)
     A                                  2 32'Dato/Kl:'
     A            SAIUSD         8Y 0O  2 41COLOR(WHT)
     A                                      EDTWRD('    .  .  ')
     A                                  2 52'/'
     A            SAIUSK         6Y 0O  2 54COLOR(WHT)
     A                                      EDTWRD('  :  :  ')
     A            WSIME1        65A  B  3  2DSPATR(UL)
     A                                      CHECK(LC)
     A            WSIME2        65A  B  4  2DSPATR(UL)
     A                                      CHECK(LC)
     A            WSIME3        65A  B  5  2DSPATR(UL)
     A                                      CHECK(LC)
     A            WSIME4        65A  B  6  2DSPATR(UL)
     A                                      CHECK(LC)
     A            WSIME5        65A  B  7  2DSPATR(UL)
     A                                      CHECK(LC)
     A            WSIME6        65A  B  8  2DSPATR(UL)
     A                                      CHECK(LC)
     A            WSIME7        65A  B  9  2DSPATR(UL)
     A                                      CHECK(LC)
     A            WSIME8        65A  B 10  2DSPATR(UL)
     A                                      CHECK(LC)
     A            WSIME9        65A  B 11  2DSPATR(UL)
     A                                      CHECK(LC)
     A            WSIME10       65A  B 12  2DSPATR(UL)
     A                                      CHECK(LC)
     A  30        MSGTXT        58A  O 14  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                             14 22MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
      *
     A          R VISLINK
     A                                      WINDOW(*DFT 4 105 *NOMSGLIN)
     A                                      OVERLAY
     A                                      CF03(03 'Avslutt program')
     A                                      CF12(03)
     A                                  1  1'SAD105'
     A                                      COLOR(BLU)
     A                                  1 20'Klikk linken for å vise i browser.-
     A                                                '
     A                                      COLOR(WHT)
     A            HFLINK       100A  O  2  1COLOR(BLU)
     A                                      DSPATR(UL)
     A                                  3  1'For å komme tilbake til SAD tast F-
     A                                      3(el Enter) med SAD-vinduet i fokus-
     A                                      (blå topp) (IKKE fokus på browser)'
     A                                  4  1'OBS! Dersom du åpner svært mange d-
     A                                      okument, husk å lukke - elles spise-
     A                                      s PCens memory opp.'
     A                                  4 94'F3=avslutt'
     A                                      COLOR(BLU)
      *
     A          R VAREL4
     A                                      CF12(12 'Forrige')
     A                                      SLNO(24)
     A                                      CLRL(04)
     A                                      OVERLAY
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            W3SG           3A  B  2 33
     A                                  3  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  1  4'Bytter alle i oversiktsbildet til -
     A                                      den signatur du taster under'
     A                                      COLOR(RED)
     A                                  2  5'Alle skal ha signaturkode:'
