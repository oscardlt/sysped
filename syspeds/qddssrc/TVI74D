     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      CF03(03 'Avslutt program')
     A                                      CF12(12 'Annuller')
     A                                      HLPDOC('STD' 'HELPNU' 'HLP400')
     A                                      HELP
     A          R TVI74DA
     A                                      TEXT('Valg av Nytt passord')
     A                                      OVERLAY
     A                                  1  2'TVINN'
     A                                      COLOR(BLU)
     A                                  1 24'Forandre Racv Passord for IBM IN'
     A                                      COLOR(WHT)
     A                                  1 73'TVI74D A'
     A                                      COLOR(BLU)
     A                                  3  2'Trykk Enter for å fortsette:'
     A                                      COLOR(BLU)
     A                                  5  6'Racv Identitet . . . . . . . . . :'
     A            ZIBRID         8A  O  5 43
     A                                  7  8'Gjeldende passord  . . . . . . :'
     A            ZIBOPW         8A  O  7 43
     A                                  9  8'Nytt passord . . . . . . . . . :'
     A            ZIBNPW         8A  B  9 43
     A  31                                  DSPATR(PC)
     A                                  9 56'Tegn'
     A                                 20 72'Slutt '
     A                                      COLOR(WHT)
     A                                 23  2'F3=Gå ut   F12=Annuller'
     A                                      COLOR(BLU)
     A          R TVI74DB
     A                                      TEXT('Anmodan sendes IBM IN')
     A                                      OVERLAY
     A                                  1  2'TVINN'
     A                                      COLOR(BLU)
     A                                  1 24'Forandre Racv Passord for IBM IN'
     A                                      COLOR(WHT)
     A                                  1 73'TVI74D B'
     A                                      COLOR(BLU)
     A                                  3  2'Informasjonsbilde, vent, ikke tryk-
     A                                      k noen taster:'
     A                                      COLOR(BLU)
     A                                  5  4'Anmodan sendes til IBM IN.'
     A                                      COLOR(RED)
     A                                      DSPATR(BL)
     A                                 20 72'Slutt '
     A                                      COLOR(WHT)
     A          R TVI74DC
     A                                      TEXT('Dette program gjelder kun IBM-
     A                                       IN')
     A                                      OVERLAY
     A                                  1  2'TVINN'
     A                                      COLOR(BLU)
     A                                  1 24'Forandre Racv Passord for IBM IN'
     A                                      COLOR(WHT)
     A                                  1 73'TVI74D C'
     A                                      COLOR(BLU)
     A                                  3  2'Trykk Enter for å fortsette:'
     A                                      COLOR(BLU)
     A                                  5  4'Denne rutinen kjøres kun for IBM I-
     A                                      N.'
     A                                      COLOR(RED)
     A                                  7  4'Rutinen avbrytes.'
     A                                 20 72'Slutt '
     A                                      COLOR(WHT)
     A          R TVI74DØ
     A                                      TEXT('Errormesseage')
     A                                      OVERLAY
     A            ZMSGTX        79A  O 24  2MSGID(&ZMSGNO SYMSGF)
     A N30                                  COLOR(WHT)
     A  30                                  COLOR(RED)
     A            ZMSGNO         7A  P
