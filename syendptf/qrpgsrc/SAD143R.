      *
      **************************************************************
      *** PROGRAM SKAL VEDLIKEHOLDE EKSPEDISJONSENHET            ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER       ***
      *** LEDIGE INDIKATORER 01 02 04 07 09-11 13-24             ***
      *** LEDIGE INDIKATORER 27-29 31 32 34-39 41-88 95-97       ***
      **************************************************************
      *
     FSADEFEEF  UF A E           K DISK
     FSAD143D   CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
      *
     C                   EXSR      INIT
      *
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     *LOVAL        SETLL     SADEFEEF
     C                   READ(N)   SADEFEEF                               94
     C                   DOW       *IN94 = *OFF
     C                   ADD       1             ZRECNO
     C                   WRITE     SUBFIL
     C                   READ(N)   SADEFEEF                               94
     C                   ENDDO
      *
     C                   Z-ADD     ZRECNO        YRECNO            5 0
     C                   IF        ZRECNO > 1
     C                   EVAL      ZRECNO = 1
     C                   END
      *
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     2             POSNBR
      *
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYVARE
     C                   ELSE
     C                   WRITE     SUBCTL
     C                   EXFMT     BUNN
     C                   END
      *
      ***
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C                   Z-ADD     WSSUBN        XRECNO            4 0
     C     XRECNO        IFEQ      0
     C                   Z-ADD     ZRECNO        XRECNO            4 0
     C                   END
      ***
      *
     C                   SETOFF                                       9899
      *
      *** AVSLUTT
     C   03              GOTO      SLUTT
      *
      *** FRISK OPP
     C   05              GOTO      START
      *
      *** LAGE NY
     C   06              DO
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     5             POSNBR
     C                   MOVE      *ZEROS        WS3039E
     C                   MOVE      *BLANKS       WSSTDNV
     C                   GOTO      START5
     C                   END
      *
     C     ZRECNO        IFNE      0
     C                   EXSR      HENT
     C                   END
     C   98XRECNO        IFNE      0
     C                   Z-ADD     XRECNO        ZRECNO
     C                   END
     C                   GOTO      START3
      *
      *** LAGE NY
     C     START5        TAG
     C                   EXFMT     VARELI
     C                   SETOFF                                       309089
     C   12              EXSR      MOVB2
     C  N12              EXSR      TESTB1
     C  N12
     CAN 30              GOTO      START5
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     5             POSNBR
     C                   GOTO      START3
      *
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
      *
      ***********************************************
     C     TESTB1        BEGSR
      ***********************************************
     C  N40              DO
     C     WS3039E       COMP      0                                      90
     C   90              MOVE      'YBC2117'     MSGNR
     C  N90WS3039E       CHAIN(N)  SADEFEEF                           33
     C  N90
     CANN33              DO
     C                   SETON                                        90
     C                   MOVE      'YBC2117'     MSGNR
     C                   END
     C   90              Z-ADD     5             POSNBR
     C   90              SETON                                        30
     C                   END
      *
     C     WSSTDNV       COMP      *BLANKS                                89
     C   89
     CANN30              MOVE      'YBC2118'     MSGNR
     C   89
     CANN30              Z-ADD     12            POSNBR
     C   89              SETON                                        30
      *
     C   30              GOTO      SLTB
      *
      *** OPPDATERE PÅ REGISTER OG SUBFIL
     C     WS3039E       CHAIN     SADEFEEF                           33
     C                   EXSR      MOVWSE
     C  N33              UPDATE    SADEFEEFR
     C   33              WRITE     SADEFEEFR
     C   40              UPDATE    SUBFIL
     C  N40              ADD       1             YRECNO
     C  N40              Z-ADD     YRECNO        ZRECNO
     C  N40              WRITE     SUBFIL
     C                   WRITE     SUBCTL
     C                   EXSR      MOVB2
      *
     C     SLTB          ENDSR
      *
      ***********************************************
     C     HENT          BEGSR
      ***********************************************
     C                   Z-ADD     0             LINNBR
     C                   SETOFF                                       919299
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C   98              DO
     C   99              SETOFF                                       98
     C                   Z-ADD     1             LINNBR
     C                   GOTO      SLHENT
     C                   END
     C     ZRECNO        DIV       14            LINNBR
     C                   MVR                     XLIN              2 0
     C     XLIN          IFEQ      0
     C                   Z-ADD     14            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C     WSNR          IFEQ      2                                            ENDRE
     C                   MOVE      ' '           WSNR
     C     EESTDNV       COMP      '*DELETE'                              98
     C   98              GOTO      SLHENT
     C                   EXSR      CHGEE
     C   12              GOTO      SLHENT
     C                   END
     C     WSNR          IFEQ      4                                            SLETTE
     C                   MOVE      ' '           WSNR
     C     EESTDNV       COMP      '*DELETE'                              98
     C   98              GOTO      SLHENT
     C                   EXSR      DLTEE
     C   12              GOTO      SLHENT
     C                   END
     C                   END
      *
     C     SLHENT        TAG
     C                   ADD       4             LINNBR
     C                   Z-ADD     5             POSNBR
      *
     C                   ENDSR
      *
      ***********************************************
     C     DLTEE         BEGSR
      ***********************************************
     C                   SETON                                        99
     C                   MOVE      EE3039E       WS3039E
     C                   EXFMT     VAREL3
     C  N12              DO
     C     WS3039E       CHAIN     SADEFEEF                           33
     C  N33              DELETE    SADEFEEFR
     C                   EXSR      MOVB1
     C                   END
     C     XLIN          IFEQ      0
     C                   Z-ADD     14            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C                   UPDATE    SUBFIL
     C                   WRITE     SUBCTL
      *
     C                   ENDSR
      *
      ***********************************************
     C     CHGEE         BEGSR
      ***********************************************
     C                   EXSR      MOVEEW
     C                   SETON                                        99
     C     STCHG         TAG
     C                   EXFMT     VARELI
     C                   SETOFF                                       309089
     C   12              DO
     C                   UPDATE    SUBFIL
     C                   EXSR      MOVB2
     C                   END
     C  N12              EXSR      TESTB1
     C  N12
     CAN 30              GOTO      STCHG
     C     XLIN          IFEQ      0
     C                   Z-ADD     14            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
      *
     C                   ENDSR
      *
      ***********************************************
     C     MOVB1         BEGSR
      ***********************************************
     C                   EVAL      EE3039E = 0
     C                   EVAL      EESTDNV = '*DELETE'
      *
     C                   ENDSR
      *
      ***********************************************
     C     MOVB2         BEGSR
      ***********************************************
     C                   SETOFF                                       40
     C                   EVAL      WS3039E = 0
     C                   EVAL      WSSTDNV = *BLANKS
      *
     C                   ENDSR
      *
      ***********************************************
     C     MOVEEW        BEGSR
      ***********************************************
     C                   EVAL      WS3039E = EE3039E
     C                   EVAL      WSSTDNV = EESTDNV
     C                   SETON                                        40
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     12            POSNBR
      *
     C                   ENDSR
      *
      ***********************************************
     C     MOVWSE        BEGSR
      ***********************************************
     C   33              EVAL      EE3039E = WS3039E
     C                   EVAL      EESTDNV = WSSTDNV
      *
     C                   ENDSR
      *
      ***********************************************
     C     INIT          BEGSR
      ***********************************************
      *
     C                   ENDSR
      *
