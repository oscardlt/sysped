      *
      **************************************************************
      *** PROGRAM SKAL SPØRRE PÅ BILL OF LADING                  ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER       ***
      *** LEDIGE INDIKATORER 02 04 06-11 13-24                   ***
      *** LEDIGE INDIKATORER 28 29 31 32 34-39 49-90 95-97 99    ***
      **************************************************************
      *
     FKODTA     IF   E           K DISK
     FHEADF     IF   E           K DISK
     FBILLOF    IF   E           K DISK
     FBILLOFL1  IF   E           K DISK
     F                                     RENAME(BILLOFR:BILLOF1R)
     FBILLOFL2  IF   E           K DISK
     F                                     RENAME(BILLOFR:BILLOF2R)
     FBILLOFL3  IF   E           K DISK
     F                                     RENAME(BILLOFR:BILLOF3R)
     FBILLOFL4  IF   E           K DISK
     F                                     RENAME(BILLOFR:BILLOF4R)
     FBILLOFL5  IF   E           K DISK
     F                                     RENAME(BILLOFR:BILLOF5R)
     FBILLOFL6  IF   E           K DISK
     F                                     RENAME(BILLOFR:BILLOF6R)
     FBILLOFL7  IF   E           K DISK
     F                                     RENAME(BILLOFR:BILLOF7R)
     FCUNDL01   IF   E           K DISK
     FCDBOAT    IF   E           K DISK
     FSYSJ52FK  CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     D                 DS
     D  XÅR4                   1      4  0
     D  BLACC                  1      2  0
     D  BLAAR                  3      4  0
     D                 DS
     D  BLDTAÅ                 1      4  0
     D  BLDTAM                 5      6  0
     D  BLDTAD                 7      8  0
     D  BLDTA                  1      8  0
     D                 DS
     D  XDTAD                  1      2  0
     D  XDTAM                  3      4  0
     D  XDTAÅ                  5      6  0
     D  XDTA                   1      6  0
     D                UDS
     D  UFIRM                  1      2
     C*
     C                   EXSR      INIT
     C*
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C                   EXSR      SETKEY
     C     START1        TAG
     C                   EXSR      LESREC
      *
     C     START2        TAG
      *
     C  N94              DO
     C                   DO        17
     C                   ADD       1             ZRECNO
     C     HEADFK        CHAIN     HEADF                              33
     C   33              MOVE      *ZEROS        HENT
     C   33              MOVE      *ZEROS        HEVKT
     C     HENT          IFGT      9999
     C                   Z-ADD     9999          WSNT
     C                   ELSE
     C                   Z-ADD     HENT          WSNT
     C                   END
     C     HEVKT         IFGT      9999999
     C                   Z-ADD     9999999       WSVKT
     C                   ELSE
     C                   Z-ADD     HEVKT         WSVKT
     C                   END
     C                   MOVE      *BLANKS       XTXT
     C   41              MOVEL     BLBLNR        XTXT
     C  N41
     CANN45              MOVEL     BLREIS        XTXT
     C   45              MOVE      BLKNR         KUNDNR
     C   45KUND1K        CHAIN     CUNDL01                            33
     C   45
     CANN33              MOVEL     KNAVN         XTXT
     C  N41              MOVE      BLTR          XTXT5             5
     C  N41              MOVE      XTXT5         XTXT
     C                   EXSR      KDTFMT
     C                   WRITE     SUBFIL
     C                   EXSR      LESREC
     C  N94              END
     C                   END
     C*
     C                   MOVE      ZRECNO        XRECNO            4 0
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   WRITE     NYVARE
     C                   ELSE
     C                   WRITE     SUBCTL
     C                   END
     C                   EXFMT     BUNN
     C*** AVSLUTT
     C   01
     COR 03
     COR 12              GOTO      SLUTT
     C*
     C                   SETOFF                                       9830
     C   05              GOTO      START
     C*
     C*** BLA VIDERE
     C   25              DO
     C                   Z-ADD     XRECNO        ZRECNO
     C                   SETOFF                                       25
     C                   GOTO      START2
     C                   END
     C*
     C     ZRECNO        IFEQ      0
     C                   GOTO      START3
     C                   ELSE
     C                   EXSR      BHENT
     C     WSNR          IFEQ      '2'
     C                   MOVE      BLOPD         UOPD
     C                   Z-SUB     3             UFLAGG
     C                   GOTO      SLUTT
     C                   END
     C   98              GOTO      START3
     C                   END
     C*
     C                   MOVE      ' '           WSNR
     C                   UPDATE    SUBFIL
     C*
     C                   EXSR      UTBL
     C  N01
     CANN03              GOTO      START3
     C*
     C     SLUTT         TAG
     C   01              MOVE      *ZEROS        UAVD
     C   01              Z-ADD     1             UFLAGG
     C   03              Z-ADD     3             UFLAGG
     C   12              Z-ADD     9             UFLAGG
     C                   SETON                                        LR
     C                   RETURN
     C*
     C***********************************************
     C     UTBL          BEGSR
     C***********************************************
     C                   Z-ADD     1             UFLAGG            1 0
     C                   CALL      'SYSJ52I'
     C                   PARM                    BLAVD
     C                   PARM                    BLOPD
     C                   PARM                    BLSG
     C                   PARM                    UFLAGG            1 0
     C     UFLAGG        COMP      1                                      01
     C     UFLAGG        COMP      3                                      03
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     BHENT         BEGSR
     C***********************************************
     C                   MOVEA     '00'          *IN(91)
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C  N98
     CAN 27WSNR          IFEQ      '2'
     C                   GOTO      SLHENT
     C                   END
     C  N98WSNR          IFEQ      '5'
     C                   GOTO      SLHENT
     C                   END
     C                   END
     C*
     C     SLHENT        TAG
     C   98              DO
     C                   MOVE      'DIV0012'     MSGNR
     C                   SETON                                            30
     C                   END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     SETKEY        BEGSR
     C***********************************************
     C   41              MOVE      UBLNR         YBLNR            16
     C   42              MOVE      UREIS         YREIS            14
     C   43              MOVE      UOB           YOB               5
     C   44              MOVE      ULB           YLB               5
     C   45              MOVE      UKNR          YKNR              8 0
     C   46              MOVEL     UÅR4          YCC               2 0
     C   46              MOVE      UÅR4          YÅR               2 0
     C   46              MOVE      UUKE          YUKE              2 0
     C   47              MOVE      UTR           YTR               4
     C  N40              DO
     C   41BILL1K        SETLL     BILLOFL1
     C   42BILL2K        SETLL     BILLOFL2
     C   43BILL3K        SETLL     BILLOFL3
     C   44BILL4K        SETLL     BILLOFL4
     C   45BILL5K        SETLL     BILLOFL5
     C   46BILL6K        SETLL     BILLOFL6
     C   47BILL7K        SETLL     BILLOFL7
     C   48BILLK         SETLL     BILLOF
     C                   END
     C   40              DO
     C   41BIL21K        SETLL     BILLOFL1
     C   42BIL22K        SETLL     BILLOFL2
     C   43BIL23K        SETLL     BILLOFL3
     C   44BIL24K        SETLL     BILLOFL4
     C   45BIL25K        SETLL     BILLOFL5
     C   46BIL26K        SETLL     BILLOFL6
     C   47BIL27K        SETLL     BILLOFL7
     C   48BIL2K         SETLL     BILLOF
     C                   END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     LESREC        BEGSR
     C***********************************************
     C     STLES         TAG
     C  N40              DO
     C   41UAVD          READE     BILLOFL1                               94
     C   42UAVD          READE     BILLOFL2                               94
     C   43UAVD          READE     BILLOFL3                               94
     C   44UAVD          READE     BILLOFL4                               94
     C   45UAVD          READE     BILLOFL5                               94
     C   46UAVD          READE     BILLOFL6                               94
     C   47UAVD          READE     BILLOFL7                               94
     C   48UAVD          READE     BILLOF                                 94
     C   43
     CANN94UOB           COMP      BLOB                               9494
     C   44
     CANN94ULB           COMP      BLLB                               9494
     C   45
     CANN94UKNR          COMP      BLKNR                              9494
     C   47
     CANN94UTR           COMP      BLTR                               9494
     C                   END
     C   40              DO
     C   41YAVD          READE     BILLOFL1                               94
     C   42YAVD          READE     BILLOFL2                               94
     C   43YAVD          READE     BILLOFL3                               94
     C   44YAVD          READE     BILLOFL4                               94
     C   45YAVD          READE     BILLOFL5                               94
     C   46BIL26K        READE     BILLOFL6                               94
     C   47YAVD          READE     BILLOFL7                               94
     C   48BIL2K         READE     BILLOF                                 94
     C   41
     CANN94UBLNR         COMP      BLBLNR                             9494
     C   42
     CANN94UKNR          COMP      BLKNR                              9494
     C   43
     CANN94UOB           COMP      BLOB                               9494
     C   44
     CANN94ULB           COMP      BLLB                               9494
     C   45
     CANN94UKNR          COMP      BLKNR                              9494
     C   47
     CANN94UTR           COMP      BLTR                               9494
     C   94YAVD          IFNE      9999
     C                   ADD       1             YAVD
     C                   SETOFF                                       33
     C     KODTAK        SETLL     KODTA
     C                   READ      KODTA                                  33
     C  N33              MOVE      KOAAVD        YAVD
     C  N33              EXSR      SETKEY
     C  N33              SETOFF                                       94
     C  N33              GOTO      STLES
     C                   END
     C                   END
     C*
     C  N48
     CANN94UDT8          IFGT      BLDTA
     C   41              MOVE      BLBLNR        YBLNR
     C   42              MOVE      BLREIS        YREIS            14
     C   43              MOVE      BLOB          YOB               5
     C   44              MOVE      BLLB          YLB               5
     C   45              MOVE      BLKNR         YKNR              8 0
     C   46              MOVE      BLACC         YCC               2 0
     C   46              MOVE      BLAAR         YÅR               2 0
     C   46              MOVE      BLUKE         YUKE              2 0
     C   47              MOVE      BLTR          YTR               4
     C                   GOTO      STLES
     C                   END
     C  N41
     CANN48
     CANN94UBLNR         IFNE      *BLANKS
     C     UBLNR         ANDNE     BLBLNR
     C                   GOTO      STLES
     C                   END
     C  N42
     CANN48
     CANN94UREIS         IFNE      *BLANKS
     C     UREIS         ANDNE     BLREIS
     C                   GOTO      STLES
     C                   END
     C  N43
     CANN48
     CANN94UOB           IFNE      *BLANKS
     C     UOB           ANDNE     BLOB
     C                   GOTO      STLES
     C                   END
     C  N44
     CANN48
     CANN94ULB           IFNE      *BLANKS
     C     ULB           ANDNE     BLLB
     C                   GOTO      STLES
     C                   END
     C  N45
     CANN48
     CANN94UKNR          IFNE      0
     C     UKNR          ANDNE     BLKNR
     C                   GOTO      STLES
     C                   END
     C   46
     CANN94UÅR4          COMP      XÅR4                               9494
     C   46
     CANN94UUKET         COMP      BLUKE                                94
     C*
     C  N46
     CANN48
     CANN94UUKE          IFNE      0
     C     UÅR4          IFNE      XÅR4
     C                   GOTO      STLES
     C                   END
     C     UUKE          IFNE      BLUKE
     C                   GOTO      STLES
     C                   END
     C                   END
     C  N47
     CANN48
     CANN94UTR           IFNE      *BLANKS
     C     UTR           ANDNE     BLTR
     C                   GOTO      STLES
     C                   END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     KODTAK        KLIST
     C                   KFLD                    YAUNI
     C                   KFLD                    YAVD
     C     HEADFK        KLIST
     C                   KFLD                    BLAVD
     C                   KFLD                    BLOPD
     C     BILLK         KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    UOPD
     C     BILL1K        KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    YBLNR
     C                   KFLD                    UDT8
     C     BILL2K        KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    YREIS
     C                   KFLD                    UDT8
     C     BILL3K        KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    YOB
     C                   KFLD                    UDT8
     C     BILL4K        KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    YLB
     C                   KFLD                    UDT8
     C     BILL5K        KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    YKNR
     C                   KFLD                    UDT8
     C     BILL6K        KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    YCC
     C                   KFLD                    YÅR
     C                   KFLD                    YUKE
     C     BILL7K        KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    YTR
     C                   KFLD                    UDT8
     C     BIL2K         KLIST
     C                   KFLD                    YAVD
     C                   KFLD                    UOPD
     C     BIL21K        KLIST
     C                   KFLD                    YAVD
     C                   KFLD                    YBLNR
     C                   KFLD                    UDT8
     C     BIL22K        KLIST
     C                   KFLD                    YAVD
     C                   KFLD                    YREIS
     C                   KFLD                    UDT8
     C     BIL23K        KLIST
     C                   KFLD                    YAVD
     C                   KFLD                    YOB
     C                   KFLD                    UDT8
     C     BIL24K        KLIST
     C                   KFLD                    YAVD
     C                   KFLD                    YLB
     C                   KFLD                    UDT8
     C     BIL25K        KLIST
     C                   KFLD                    YAVD
     C                   KFLD                    YKNR
     C                   KFLD                    UDT8
     C     BIL26K        KLIST
     C                   KFLD                    YAVD
     C                   KFLD                    YCC
     C                   KFLD                    YÅR
     C                   KFLD                    YUKE
     C     BIL27K        KLIST
     C                   KFLD                    YAVD
     C                   KFLD                    YTR
     C                   KFLD                    UDT8
     C     KUND1K        KLIST
     C                   KFLD                    UFIRM
     C                   KFLD                    KUNDNR
     C*
     C     *ENTRY        PLIST
     C                   PARM                    UAVD              4 0
     C                   PARM                    UDT               6 0
     C                   PARM                    UNR               2 0
     C                   PARM                    UBLNR            16
     C                   PARM                    UREIS            14
     C                   PARM                    UOB               5
     C                   PARM                    ULB               5
     C                   PARM                    UKNR              8 0
     C                   PARM                    UÅR               2 0
     C                   PARM                    UUKE              2 0
     C                   PARM                    UUKET             2 0
     C                   PARM                    UTR               4
     C                   PARM                    UOPD              7 0
     C                   PARM                    UFLAGG            1 0
      *
     C     UDT           IFEQ      0
     C                   MOVE      *ZEROS        UDT8              8 0
     C                   ELSE
     C     UDT           IFGT      501231
     C     UDT           ADD       19000000      UDT8
     C                   ELSE
     C     UDT           ADD       20000000      UDT8
     C                   END
     C                   END
     C     UÅR           IFGT      50
     C     UÅR           ADD       1900          UÅR4              4 0
     C                   ELSE
     C     UÅR           ADD       2000          UÅR4
     C                   END
     C     UFLAGG        COMP      1                                      27
     C     UAVD          COMP      0                                      40
     C   40              Z-ADD     1             YAVD              4 0
     C*
     C                   MOVE      'A'           YAUNI             1
     C                   MOVE      *ZEROS        BLDTA
     C                   MOVE      *ZEROS        XDTA
     C*
     C     UNR           ADD       40            XN                2 0
     C                   MOVE      '1'           *IN(XN)
     C   43UOB           CHAIN     CDBOAT                             33
     C   44ULB           CHAIN     CDBOAT                             33
     C   45              MOVE      UKNR          KUNDNR
     C   45KUND1K        CHAIN     CUNDL01                            33
     C   45              MOVE      KNAVN         XKNAVN
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     KDTFMT        BEGSR
     C***********************************************
     C                   MOVE      BLDTAD        UDT1              2 0
     C                   MOVE      BLDTAM        UDT2              2 0
     C                   MOVE      BLDTAÅ        UDT3              2 0
     C                   CALL      'DATOFMT'
     C                   PARM                    UDT1
     C                   PARM                    UDT2
     C                   PARM                    UDT3
     C                   MOVE      UDT1          XDTAD
     C                   MOVE      UDT2          XDTAM
     C                   MOVE      UDT3          XDTAÅ
     C*
     C                   ENDSR
     C*
