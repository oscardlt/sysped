     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HELP
     A          R BILDE01
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF03(03 'Avslutt')
     A                                      CF04(04 'Søke')
     A                                      CF10(10 'B/L VARELINJE TYPE 1')
     A                                      CF11(11 'B/L VARELINJE TYPE 2')
     A                                      CF12(12 'Avbryt registrering av opp-
     A                                      drag')
     A                                      CF14(14 'Vise opplysning')
     A                                      CF18(18 'Freetext')
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/SYSJ50D'
     A                                      COLOR(BLU)
     A                                  1 61DATE
     A                                      EDTCDE(Y)
     A                                  1 70TIME
     A                                  1 20MSGCON(040 DIV3137 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                  2  5MSGCON(004 DIV3163 *LIBL/SYMSGF)
     A            BOAVD          4Y 0O  2 10DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  2 15MSGCON(006 DIV3164 *LIBL/SYMSGF)
     A            BOOPD          7Y 0O  2 22DSPATR(UL)
     A N29                                  DSPATR(BL)
     A N29                                  COLOR(RED)
     A  29                                  COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  2 30MSGCON(005 DIV3155 *LIBL/SYMSGF)
     A            WSDTR          6S 0O  2 36DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  2 44MSGCON(005 DIV3165 *LIBL/SYMSGF)
     A            BOSG           3A  O  2 50DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  2 55MSGCON(003 DIV3166 *LIBL/SYMSGF)
     A            BOUKE          2Y 0O  2 59DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  2 63MSGCON(004 DIV3167 *LIBL/SYMSGF)
     A            BOAAR          2Y 0O  2 68DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  4  2MSGCON(003 DIV3142 *LIBL/SYMSGF)
     A            WSKNA          8Y 0B  4  6DSPATR(UL)
     A  99                                  DSPATR(RI)
     A N99 51                               COLOR(RED)
     A N99N51                               COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSNAA         30A  B  4 19DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSAD1A        30A  B  4 51DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSAD2A        30A  B  5 19DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSAD3A        30A  B  5 51DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  6  2MSGCON(003 DIV3143 *LIBL/SYMSGF)
     A            WSKNM          8Y 0B  6  6DSPATR(UL)
     A  98                                  DSPATR(RI)
     A N98 52                               COLOR(RED)
     A N98N52                               COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSNAM         30A  B  6 19DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSAD1M        30A  B  6 51DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSAD2M        30A  B  7 19DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSAD3M        30A  B  7 51DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  8  2MSGCON(003 DIV3144 *LIBL/SYMSGF)
     A            WSKNN          8Y 0B  8  6DSPATR(UL)
     A  97                                  DSPATR(RI)
     A N97 53                               COLOR(RED)
     A N97N53                               COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSNAN         30A  B  8 19DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSAD1N        30A  B  8 51DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSAD2N        30A  B  9 19DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSAD3N        30A  B  9 51DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 10  2MSGCON(016 DIV3145 *LIBL/SYMSGF)
     A            WSKDP          5A  B 10 19DSPATR(UL)
     A  96                                  DSPATR(HI)
     A                                      COLOR(YLW)
     A                                 10 26MSGCON(010 DIV3146 *LIBL/SYMSGF)
     A            WSMN          17A  B 10 37DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 10 56MSGCON(003 DIV3193 *LIBL/SYMSGF)
     A            WSBLNR        16A  B 10 60DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 12  2MSGCON(012 DIV3147 *LIBL/SYMSGF)
     A                                 13  2MSGCON(055 DIV3148 *LIBL/SYMSGF)
     A            WSFT          20A  B 13 59DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 14  2MSGCON(034 DIV3149 *LIBL/SYMSGF)
     A            WSKDSA         1A  B 14 37DSPATR(UL)
     A  95                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 15  2MSGCON(034 DIV3150 *LIBL/SYMSGF)
     A            WSKDRA         1A  B 15 37DSPATR(UL)
     A  94                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 16  2MSGCON(034 DIV3151 *LIBL/SYMSGF)
     A            WSKDTF         1A  B 16 37DSPATR(UL)
     A  93                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 17  2MSGCON(011 DIV3152 *LIBL/SYMSGF)
     A            WSKDCP         1A  B 17 14DSPATR(UL)
     A  92                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 17 17MSGCON(010 DIV3153 *LIBL/SYMSGF)
     A            WSNTBS        15A  B 17 28DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 17 45MSGCON(005 DIV3154 *LIBL/SYMSGF)
     A            WSSD           5A  B 17 51DSPATR(UL)
     A  91                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 17 58MSGCON(005 DIV3155 *LIBL/SYMSGF)
     A            WSDT           6Y 0B 17 64DSPATR(UL)
     A  90                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(9)
     A                                 21 47MSGCON(026 DIV3162 *LIBL/SYMSGF)
     A            WSOPDL         7Y 0B 21 74DSPATR(UL)
     A  89                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 22  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 22 22MSGCON(018 DIV0032 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 22 42MSGCON(018 DIV3572 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23  2MSGCON(018 DIV3169 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(018 DIV3170 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 42MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 62MSGCON(018 DIV0037 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                             24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R BILDE02
     A                                      CF03(03 'Avslutt')
     A                                      CF12(12 'AVBRYT')
     A                                  1  2'SYSPED/SYSJ50D'
     A                                      COLOR(BLU)
     A                                  1 61DATE
     A                                      EDTCDE(Y)
     A                                  1 70TIME
     A                                  1 20MSGCON(40 DIV3137 SYMSGF)
     A                                      DSPATR(HI)
     A                                  3  2MSGCON(10 DIV3116 SYMSGF)
     A            BOAVD          4Y 0O  3 14DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  3 22MSGCON(12 DIV3117 SYMSGF)
     A            BOOPD          7Y 0O  3 36DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  3 45MSGCON(10 DIV3118 SYMSGF)
     A            BOSG           3A  O  3 57DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  8  8MSGCON(30 DIV3273 SYMSGF)
     A            WSPRT          1   B  8 41DSPATR(UL)
     A                                      VALUES(' ' 'J' 'N')
     A                                  8 49'(J/N)'
     A                                 10  8MSGCON(30 DIV2971 SYMSGF)
     A            WSLSTS         1   B 10 41DSPATR(UL)
     A                                      VALUES(' ' 'J' 'N')
     A                                 10 49'(J/N)'
     A                                 12 14MSGCON(20 DIV3274 SYMSGF)
     A            WSJBK          1   B 12 43DSPATR(UL)
     A                                      VALUES(' ' 'J' 'N')
     A                                 12 49'(J/N)'
     A                                 23  2MSGCON(18 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A                                 24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
