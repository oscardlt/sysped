     FTVI64D    CF   E             WORKSTN
     F                                     SFILE(TVI64DB:ZRECNO)
     FEDIMA     IF   E           K DISK
     F                                     RENAME(EDIMR:EDIMRA)
     FEDIM      UF   E           K DISK
     D                 DS
     D  ZVEN                   1      4
     D  ZVEN1                  1      1
     D  ZVEN2                  2      4
      *//////////////
      *  Hodelogikk /
      *//////////////
     C                   EXSR      SRA
     C                   EXSR      SRB
     C                   MOVE      '1'           *INLR
      *////////////////////////////////////////////////////////////
      *  Initiering                                           SRA /
      *////////////////////////////////////////////////////////////
     C     SRA           BEGSR
      *___________________
      * Definiere Workfelt
      *"""""""""""""""""""
     C     *LIKE         DEFINE    *IN03         WIN03
     C     *LIKE         DEFINE    *IN30         WIN30
     C     *LIKE         DEFINE    MAVD          WAVD
     C     *LIKE         DEFINE    MTDN          WTDN
     C     *LIKE         DEFINE    MSR           WSR
     C     *LIKE         DEFINE    MST           WST
     C     *LIKE         DEFINE    ZRECNO        XRECNO
      *_____________
      * Init av felt
      *"""""""""""""
     C                   MOVE      'S'           WSR
     C                   MOVE      *BLANKS       WST
     C                   Z-ADD     0             ZAVD
     C                   MOVE      '*ALL'        ZVEN
      *__________________
      * Definiere Nøkkler
      *""""""""""""""""""
     C     KEYA          KLIST
     C                   KFLD                    WSR
     C                   KFLD                    WST
     C     KEYB          KLIST
     C                   KFLD                    MMN
     C     KEYC          KLIST
     C                   KFLD                    ZAVD
      *__________________
      * Output parametrer
      *""""""""""""""""""
     C     PLISTA        PLIST
     C                   PARM      MMN           MMN
     C     *IN03         PARM                    WIN03
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Bearbeide Formater                                   SRB /
      *////////////////////////////////////////////////////////////
     C     SRB           BEGSR
      *___________
      * Startbilde
      *"""""""""""
     C     SRB1          TAG
     C  N30              MOVE      'SY00000'     ZMSGNO
     C                   WRITE     TVI64DØ
     C                   EXFMT     TVI64DA
     C                   MOVEA     '0000000'     *IN(30)
     C     *IN03         CABEQ     '1'           SRB9
      * Teste ventekode.
     C     ZVEN          IFNE      '*ALL'
     C     ZVEN2         ANDNE     *BLANKS
     C                   MOVE      'SPA0001'     ZMSGNO
     C                   MOVEA     '101'         *IN(30)
     C                   GOTO      SRB1
     C                   END
      *______________
      * Tømme Subfile
      *""""""""""""""
     C     SRB2          TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     TVI64DC
     C                   MOVEA     '000'         *IN(93)
     C                   Z-ADD     *ZERO         ZRECNO
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     KEYA          SETLL     EDIMA
     C     KEYA          READE     EDIMA                                  94
      *
     C     *IN94         DOWEQ     '0'
     C                   MOVE      '0'           *IN21
     C     ZAVD          COMP      0                                  20
     C   20MAVD          COMP      ZAVD                               2020
     C  N20ZVEN2         COMP      *BLANKS                                21
     C  N20
     CAN 21MVEN          COMP      ZVEN1                              2121
     C  N20
     CANN21              DO
     C                   ADD       1             ZRECNO
     C     M1001         IFEQ      '929'
     C                   MOVE      'TVIN-I'      WSIMEX
     C                   ELSE
     C     M1001         IFEQ      '830'
     C                   MOVE      'TVIN-E'      WSIMEX
     C                   ELSE
     C                   MOVE      M0065         WSIMEX
     C                   END
     C                   END
     C                   WRITE     TVI64DB
     C                   END
     C     KEYA          READE     EDIMA                                  94
     C  N94              END
     C     ZRECNO        IFEQ      0
     C                   MOVE      'SPR0002'     ZMSGNO
     C                   MOVE      '1'           *IN30
     C                   GOTO      SRB1
     C                   END
     C                   Z-ADD     1             ZRECNO
      *_______________
      * Behandle bilde
      *"""""""""""""""
     C     SRB3          TAG
      *
     C                   MOVEA     '11'          *IN(91)
     C  N30              MOVE      'SY00000'     ZMSGNO
     C                   WRITE     TVI64DØ
     C                   WRITE     TVI64DD
     C                   EXFMT     TVI64DC
      *_______________
      * F3 eller F12 ?
      *"""""""""""""""
     C     *IN12         CABEQ     '1'           SRB1
     C     *IN03         IFEQ      '0'
      *
     C                   MOVEA     '00'          *IN(91)
      *_____________
      * F5=Frisk opp
      *"""""""""""""
     C     *IN05         CABEQ     '1'           SRB2
      *_________________________
      * F14=Forandre Ventestatus
      *"""""""""""""""""""""""""
     C     *IN14         IFEQ      '1'
     C                   EXSR      SRBJ
     C                   GOTO      SRB2
     C                   END
      *_______
      * Valg ?
      *"""""""
     C                   MOVE      '0'           *IN52
     C                   MOVE      '0'           *IN30
     C                   MOVE      '0'           *IN95
     C     *IN52         DOWEQ     '0'
      *
     C                   READC     TVI64DB                                52
     C     *IN52         IFEQ      '0'
     C     ZOP           CASEQ     '2'           SRBB
     C     ZOP           CASEQ     '4'           SRBD
     C     ZOP           CASEQ     '5'           SRBE
     C                   END
     C                   END
      *
     C  N30
     CANN52              END
      *
     C     *IN03         CABEQ     '1'           SRB9
     C     *IN95         CABEQ     '1'           SRB2
     C     *IN95         CABEQ     '0'           SRB3
     C                   END
      *
     C     SRB9          ENDSR
      *////////////////////////////////////////////////////////////
      *  Endre melding                                       SRBB /
      *////////////////////////////////////////////////////////////
     C     SRBB          BEGSR
      *________________________
      * Call på eksternt CL-pgm
      *""""""""""""""""""""""""
     C                   CALL      'EDI69R'      PLISTA
      *_________________
      * Oppdatere SUBFIL
      *"""""""""""""""""
     C     KEYB          CHAIN(N)  EDIM                               20
     C                   MOVE      *BLANKS       ZOP
     C                   UPDATE    TVI64DB
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Slette melding                                      SRBD /
      *////////////////////////////////////////////////////////////
     C     SRBD          BEGSR
      *________________________
      * Call på eksternt CL-pgm
      *""""""""""""""""""""""""
     C                   CALL      'EDI60R'      PLISTA
      *_________________
      * Oppdatere SUBFIL
      *"""""""""""""""""
     C     KEYB          CHAIN(N)  EDIM                               20
     C     *IN20         IFEQ      '1'
     C                   Z-ADD     0             MSN
     C                   Z-ADD     0             MMN
     C                   MOVE      *BLANKS       MSR
     C                   Z-ADD     0             MAVD
     C                   Z-ADD     0             MTDN
     C                   MOVE      *BLANKS       MVEN
     C                   MOVE      *BLANKS       M0035
     C                   MOVE      *BLANKS       M0065
     C                   MOVE      *BLANKS       M1N07
     C                   Z-ADD     0             M0068A
     C                   Z-ADD     0             M0068B
     C                   Z-ADD     0             M0068C
     C                   Z-ADD     0             M3039E
     C                   END
     C                   MOVE      *BLANKS       ZOP
     C                   UPDATE    TVI64DB
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Vise hele meldingsloggen.                           SRBE /
      *////////////////////////////////////////////////////////////
     C     SRBE          BEGSR
      *________________________
      * Call på eksternt CL-pgm
      *""""""""""""""""""""""""
     C                   CALL      'TVI67R'      PLISTA
      *_________________
      * Oppdatere SUBFIL
      *"""""""""""""""""
     C                   MOVE      *BLANKS       ZOP
     C                   UPDATE    TVI64DB
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Forandre ventestatus for alle meldinger             SRBJ /
      *////////////////////////////////////////////////////////////
     C     SRBJ          BEGSR
      *_______________
      * Behandle bilde
      *"""""""""""""""
     C     ZVEN2         IFEQ      *BLANKS
     C                   MOVE      ZVEN1         XVENF
     C                   ELSE
     C                   MOVE      *BLANKS       XVENF
     C                   END
     C                   EXFMT     TVI64DE
      *____________
      * Les Subfile
      *""""""""""""
     C                   Z-ADD     1             XRECNO
     C     XRECNO        CHAIN     TVI64DB                            52
     C     *IN52         DOWEQ     '0'
     C     MVEN          IFEQ      XVENF
      *_________________
      * Oppdatere Record
      *"""""""""""""""""
     C     KEYB          CHAIN     EDIM                               20
     C  N20              MOVE      XVENT         MVEN
     C  N20              UPDATE    EDIMR
      *_________________
      * Oppdatere SUBFIL
      *"""""""""""""""""
     C  N20              UPDATE    TVI64DB
      *
     C                   END
      *
     C                   ADD       1             XRECNO
     C     XRECNO        CHAIN     TVI64DB                            52
     C                   END
      *
     C                   ENDSR
