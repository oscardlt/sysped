     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      REF(*LIBL/PAPA)
     A                                      PRINT
     A          R SUBFIL                    SFL
     A            PPSADT    R        H
N02  A            PPSTDT    R        H      REFFLD(PAPAR/PPSTDT *LIBL/PAPA)
     A            PPAVRE    R        H
     A            PPALFA    R        H
     A            WSNR           1A  B  6  2VALUES(' ' '2' '4')
     A            PPNR      R        O  6  4EDTCDE(4)
     A            PPKUNR    R        O  6 13EDTCDE(4)
     A            PPNAVN    R        O  6 22
     A            PPKATE    R        O  6 53REFFLD(PAPAR/PPKATE *LIBL/PAPA)
EV5  A            PPSPDT    R        H      REFFLD(PAPAR/PPSPDT *LIBL/PAPA)
NV5  A            WPSPDT         6  0O  6 55
     A                                      EDTWRD('  .  .  ')
     A            PPSALP    R        O  6 64EDTCDE(L)
     A            PPSALT    R        O  6 73EDTCDE(L)
     A*
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                  1  2'SYSPED/SYPA04'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(045 JOV0588 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                  1 67DATE
     A                                      EDTCDE(Y)
     A                                  2 67TIME
     A            ZRECNO         4S 0O  2 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  3  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(030 DIV0017 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  1MSGCON(079 JOV0589 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A*
     A          R BUNN
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF03(03 'EXIT')
     A                                      CF12(03)
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  3  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 42MSGCON(018 DIV0022 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 62MSGCON(018 DIV0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  98                              4  2MSGCON(079 DIV0012 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
      *
     A          R VARELI
     A                                      CF12(12 'Forrige')
     A                                      CF03(12)
     A                                      CF04(04 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A            PPSPDT    R        H      REFFLD(PAPAR/PPSPDT *LIBL/PAPA)
     A            PPSADT    R        H      REFFLD(PAPAR/PPSADT *LIBL/PAPA)
     A                                  1  2'SYSPED/SYPA04'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(045 JOV0588 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                  1 67DATE
     A                                      EDTCDE(Y)
     A                                  2 67TIME
     A                                  3  2MSGCON(018 JOV0590 *LIBL/SYMSGF)
     A            WSPPNR    R        B  3 21REFFLD(PPNR)
     A                                      EDTCDE(4)
     A                                      DSPATR(UL)
     A  70                                  DSPATR(RI)
     A  40                                  COLOR(BLU)
     A  40                                  DSPATR(PR)
     A            WSNAVN    R        B  3 32REFFLD(PPNAVN)
     A                                      COLOR(WHT)
     A                                      DSPATR(UL)
     A                                      DSPATR(PR)
     A                                  4  2MSGCON(018 JOV0605 *LIBL/SYMSGF)
     A            WSKUNR    R        B  4 21REFFLD(PPKUNR)
     A                                      DSPATR(UL)
     A  71                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                  5  2MSGCON(018 JOV0947 *LIBL/SYMSGF)
     A            WSTRNR    R        B  5 21REFFLD(PPKUNR)
     A                                      DSPATR(UL)
     A  73                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                 11 32MSGCON(045 JOV1267 *LIBL/SYMSGF)
     A                                      COLOR(RED)
     A                                  6  2MSGCON(018 JOV0606 *LIBL/SYMSGF)
     A            WSALF     R        B  6 21REFFLD(PAPAR/PPALFA *LIBL/PAPA)
     A                                  6 32MSGCON(030 JOV0624 *LIBL/SYMSGF)
     A                                  8  2MSGCON(018 JOV0625 *LIBL/SYMSGF)
     A            WSKATE    R        B  8 21REFFLD(PAPAR/PPKATE *LIBL/PAPA)
     A  72                                  DSPATR(RI)
     A                                      DSPATR(UL)
     A                                 11  2MSGCON(018 JOV0951 *LIBL/SYMSGF)
     A                                 12  2MSGCON(018 JOV0949 *LIBL/SYMSGF)
     A            WPSPDT         6Y 0O 12 22EDTWRD('  .  .  ')
     A                                      COLOR(WHT)
     A                                 13  2MSGCON(018 JOV0952 *LIBL/SYMSGF)
     A            WPSTDT         6Y 0O 13 22EDTWRD('  .  .  ')
     A                                      COLOR(WHT)
     A                                 14  2MSGCON(018 JOV0950 *LIBL/SYMSGF)
     A            PPSALP    R        O 14 21REFFLD(PAPAR/PPSALP *LIBL/PAPA)
     A                                      EDTCDE(J)
     A                                      COLOR(WHT)
     A                                 16  2MSGCON(018 JOV0608 *LIBL/SYMSGF)
     A            PPSALT    R        O 16 21REFFLD(PAPAR/PPSALT *LIBL/PAPA)
     A                                      EDTCDE(J)
     A                                      COLOR(WHT)
     A                                 19  2MSGCON(018 JOV0610 *LIBL/SYMSGF)
     A            WPSADT         6Y 0O 19 22EDTWRD('  .  .  ')
     A                                      COLOR(WHT)
     A                                 20  2MSGCON(018 JOV0609 *LIBL/SYMSGF)
     A            PPAVRE    R        O 20 21REFFLD(PAPAR/PPAVRE *LIBL/PAPA)
     A                                      EDTCDE(J)
     A                                      COLOR(WHT)
     A                                 23  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(018 DIV0032 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A                                  8 32MSGCON(030 JOV1266 *LIBL/SYMSGF)
     A                                  3 30'*'
     A                                      COLOR(RED)
     A                                  5 32MSGCON(045 JOV0948 *LIBL/SYMSGF)
     A                                 12 35MSGCON(045 JOV1268 *LIBL/SYMSGF)
     A                                      COLOR(RED)
     A                                 13 35MSGCON(045 JOV1269 *LIBL/SYMSGF)
     A                                      COLOR(RED)
     A*
     A          R VAREL2
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF12(12 'Forrige')
     A                                      CF03(12)
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  4MSGCON(050 JOV0591 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            W2PPNR    R        B  2  4REFFLD(PPNR)
     A                                      EDTCDE(4)
     A            WSALFA        10A  B  2 24
     A                                  3  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A*
     A          R VAREL3
     A                                      CF12(12 'Forrige')
     A                                      CF03(12)
     A                                      OVERLAY
     A                                  1  2'SYSPED/SYPA04'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(045 JOV0588 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                  1 67DATE
     A                                      EDTCDE(Y)
     A                                  2 67TIME
     A                                  3  2MSGCON(018 JOV0590 *LIBL/SYMSGF)
     A            PPNR      R        O  3 21REFFLD(PPNR)
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A            PPNAVN    R        O  3 32REFFLD(PPNAVN)
     A                                      COLOR(WHT)
     A                                  4  2MSGCON(018 DIV5050 *LIBL/SYMSGF)
     A            PPKUNR    R        O  4 21REFFLD(PPKUNR)
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A            KNAVN         30A  O  4 32COLOR(WHT)
     A                                 21  2MSGCON(078 JOV0592 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A*
     A          R NYWRK
     A                                      CF03(03 'EXIT')
     A                                      CF12(03)
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                  1  2'SYSPED/SYPA04'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(045 JOV0588 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                  1 67DATE
     A                                      EDTCDE(Y)
     A                                  2 67TIME
     A                                  3  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(030 DIV0017 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  1MSGCON(079 JOV0589 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 42MSGCON(018 DIV0022 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 62MSGCON(018 DIV0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
