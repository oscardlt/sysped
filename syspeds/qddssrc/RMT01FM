     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      REF(*LIBL/LOGF)
     A                                      PRINT
     A                                      ALTHELP
     A                                      HELP
      *
     A          R RMT01B20
     A                                      CF03(03 'Exit')
     A                                      CA12(12 'Cancel')
     A  61                                  CF11(11 'Sletting')
     A                                      OVERLAY
     A                                      PUTOVR
     A                                  1  3'RMT01'
     A                                  1 26'Starte overgang (STRPASTHR)'
     A                                      DSPATR(HI)
     A                                  1 62'Dato:'
     A                                  1 68DATE
     A                                      EDTCDE(Y)
     A                                  2  3USER
     A  55
     AO 57
     AO 60                              2 32'Forespørsel'
     A                                      DSPATR(HI)
     A  61                              2 33'SLETTING'
     A                                      DSPATR(HI)
     A  62                              2 33'ENDRING'
     A                                      DSPATR(HI)
     A  63                              2 33'Ny kode'
     A                                      DSPATR(HI)
     A                                  2 62'Tid :'
     A                                  2 68TIME
     A                                      OVRATR
     A  61                              3  5'Trykk F11 for å bekrefte sletting'
     A                                      COLOR(BLU)
     A                                  5  2'Beskrivelse  . . . .'
     A            WSTEXT        50   B  5 25
     A                                  6  2'Fjerntliggende lokalitet . . . . >'
     A            WSP01          8   B  6 37
     A                                  6 51'Navn, *CNNDEV'
     A                                  7  2'Virtuell styreenhet  . . . . . .  '
     A            WSP02         10   B  7 37
     A                                  7 51'Navn, *NONE'
     A                                  8  2'Virtuell dataskjerm  . . . . . .  '
     A            WSP03         10   B  8 37
     A                                  8 51'Navn, *NONE'
     A                                  9  2'Modus  . . . . . . . . . . . . .  '
     A            WSP04          8   B  9 37
     A                                  9 51'Navn, *NETATR'
     A                                 10  2'Lokal lokalitet  . . . . . . . .  '
     A            WSP05          8   B 10 37
     A                                 10 51'Navn, *LOC, *NETATR'
     A                                 11  2'ID til fjerntliggende nettverk    '
     A            WSP06          8   B 11 37
     A                                 11 51'Navn, *LOC, *NETATR, *NONE'
     A                                 12  2'Systemets anmodningsprogram  . .  '
     A            WSP07         10   B 12 37
     A                                 12 51'Navn, *SRQMNU'
     A                                 13  2'  Bibliotek  . . . . . . . . . .'
     A            WSP08         10   B 13 39
     A                                 15 29'Tilleggsparemetere'
     A                                 17  2'Brukerprofil . . . . . . . . . .'
     A            WSP09         10   B 17 37
     A                                 17 51'Navn, *NONE, *CURRENT'
     A                                 18  2'Brukerpassord  . . . . . . . . .'
     A            WSP10         10   B 18 37
     A                                 18 51'Navn, *NONE'
     A                                 19  2'Første progr. som skal anropes  '
     A            WSP11         10   B 19 37
     A                                 19 51'Navn, *RMTUSRPRF *NONE'
     A                                 20  2'Første meny  . . . . . . . . . .'
     A            WSP12         10   B 20 37
     A                                 20 51'Navn, *RMTUSRPRF *SIGNOFF'
     A                                 21  2'Gjeldende bibliotek  . . . . . .'
     A            WSP13         10   B 21 37
     A                                 21 51'Navn, *RMTUSRPRF'
     A                                 22  2'Vise alternativ  . . . . . . . .'
     A            WSP14          4   B 22 37
     A                                 22 51'Navn, *YES, *NO'
     A                                 23  4'F3=Avslutt'
     A                                      COLOR(BLU)
     A                                 23 29'F11=Bekreft sletting '
     A                                      COLOR(BLU)
     A N61                                  DSPATR(ND)
     A                                 23 65'F12=Tilbake'
     A                                      COLOR(BLU)
     A            MSGTXT        75A  O 24  2DSPATR(HI)
     A N30                                  DSPATR(ND)
     A                                      OVRDTA
     A                                      OVRATR
      *
     A          R RMT01B30
     A                                      CF03(03 'Avslutt')
     A  56                                  CF06(06 'Lag ny')
     A                                      OVERLAY
     A                                      PUTOVR
     A                                  1  3'RMT01'
     A  55                              1 26'Starte overgang STRPASTHR'
     A                                      DSPATR(HI)
     A  56                              1 26'Starte overgang (STRPASTHR)'
     A                                      DSPATR(HI)
     A  57                              1 26'Starte overgang (STRPASTHR)'
     A                                      DSPATR(HI)
     A                                  1 61'Dato:'
     A                                  1 67DATE
     A                                      EDTCDE(Y)
     A                                  2  3USER
     A            SKODET        20A  O  2 27DSPATR(HI)
     A                                  2 61'Tid :'
     A                                  2 67TIME
     A                                  4  2'Tast valg, trykk Enter'
     A                                      COLOR(BLU)
     A  57                              5  4'1=Start'
     A                                      COLOR(BLU)
     A  56                              5 12'2=Endre'
     A                                      COLOR(BLU)
     A  56                              5 21'4=Slette'
     A                                      COLOR(BLU)
     A                                  5 31'5=Vise'
     A                                      COLOR(BLU)
     A                                  7  2'Valg'
     A                                      DSPATR(HI)
     A                                  7  7'Tekst'
     A                                      DSPATR(HI)
     A                                  9  2' ( Det finnes ingen å vise )      -
     A                                                 '
     A                                 23  3'F3=Avslutt'
     A                                      COLOR(BLU)
     A  56                             23 31'F6=Lag ny'
     A                                      COLOR(BLU)
      *
     A          R RMT01B40
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF03(03 'Exit')
     A                                      CA12(12 'Subset')
     A                                      CA21(21 'Print list')
     A                                      SLNO(23)
     A                                      OVERLAY
     A                                  1  3'F3=Avslutt'
     A                                      COLOR(BLU)
     A                                  1 31'F5=Frisk opp'
     A                                      COLOR(BLU)
     A N56                                  DSPATR(ND)
     A  56                              1 64'F6=Lag ny'
     A                                      COLOR(BLU)
      *
     A          R RMT01H50                  SFL
     A                                      SFLNXTCHG
     A            WSNR           1A  B  8  4
     A  30                                  DSPATR(PC)
     A                                      VALUES(' ' '1' '2' '4' '5')
     A            ZSTEXT        50   O  8  8
      *
     A          R RMT01K60                  SFLCTL(RMT01H50)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                      CF03(03 'Avslutt')
     A  56                                  CA05(05 'Frisk opp')
     A  56                                  CF06(06 'Lag ny')
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  31                                  SFLMSG('Tastet valg er ikke gyldig')
     A                                  1  3'RMT01'
     A  55                              1 26'Starte overgang (STRPASTHR)'
     A                                      DSPATR(HI)
     A  56                              1 26'Starte overgang (STRPASTHR)'
     A                                      DSPATR(HI)
     A  57                              1 26'Starte overgang (STRPASTHR)'
     A                                      DSPATR(HI)
     A                                  1 61'Dato:'
     A                                  1 67DATE
     A                                      EDTCDE(Y)
     A                                  2  3USER
     A            SKODET        20A  O  2 29DSPATR(HI)
     A                                  2 61'Tid :'
     A                                  2 67TIME
     A            CURS1          4S 0H      SFLRCDNBR(CURSOR)
     A                                  4  2'Tast valg, trykk Enter'
     A                                      COLOR(BLU)
     A  57                              5  4'1=Start'
     A                                      COLOR(BLU)
     A  56                              5 12'2=Endre'
     A                                      COLOR(BLU)
     A  56                              5 21'4=Slette'
     A                                      COLOR(BLU)
     A                                  5 31'5=Vise'
     A                                      COLOR(BLU)
     A                                  7  2'Valg'
     A                                      DSPATR(HI)
     A                                  7  8'Bruker ID'
     A                                      DSPATR(HI)
     A                                  7 20'Kundenr. fra'
     A                                      DSPATR(HI)
     A                                  7 36'Kundenr. til'
     A                                      DSPATR(HI)
