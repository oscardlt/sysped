     A*%%TS  SD  20170131  185842  SVEIN       REL-V7R1M0  5770-WDS
     A*
*****A**************************************************************
     A* RETTINGER I DETTE PROGRAM:
PTFnrA*Sek Sig Vers  Beskrivelse...........................
"""""A*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXXA* 01 YBC PTF10 SØK KUN EN KUNDE AV GANGEN
10XXXA* 02 SH  PTF10 SØKE PÅ SEKVENS
10XXXA* 03 sh  PTF10 rettelse søk tollnummer i skjermbilde
10XXXA* 04 YBC PTF10 UTVIDER TIL 27X132, INGEN MERKING
10XXXA* 05 jov PTF10 Filter på satus ved søk via signatur(OG annet) - cursor-pla
10XXXA* 06 YBC PTF10 OPSJON 18 = NOTISBLOKK
XXXXXA*XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
11007A* 07 YBC PTF11 VIS J/N PÅ OM EPOST ER SENDT TIL KUNDE
11016A* 08 YBC PTF11 OPSJON 12 = OMBEREGNING, 15 = VIS OMBEREGNING
11XXXA* 09 YBC PTF11 VIS LØPENR FOR VIS OMBEREGNING
11XXXA* 10 YBC PTF11 OPSJON 16 = PRINT OMBEREGNING
11108A* 11 YBC PTF11 OMBEREGNING PÅ OMBEREGNING
11183A* 12 YBC PTF11 EKSP.ENHET MED SØKING AV LØPENR
11XXXA* 13 TH  PTF11 FEIL OPSJON SPØRRING PÅ OMBEREGRNING
XXXXXA*XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
12050 * 14 YBC PTF12 SØK OPPDRAG VIA FAKTURANR, MERKING/CONTAINER/UNDERST.
12106 * 15 YBC PTF12 SØK OMBEREGNING VIA STATUS.
12135 * 16 YBC PTF12 SØK KUNDENAVN
12251 * 17 YBC PTF12 Opsjon 13 = oppgjør av midlertidig import til ordinær
XXXXX * 41 SH  PTF12 FRI SØKEVEI
*****A**************************************************************
     A*
     A*%%EC
     A                                      DSPSIZ(27 132 *DS4)
     A                                      PRINT
     A                                      HLPPNLGRP(HLPSCR SAD061PNL)
     A                                      HLPTITLE('SAD IMPORT')
     A                                      HELP
     A                                      ALTHELP
      *
     A          R BILDE01
     A                                      CSRLOC(LINNBR     POSNBR)
N16  A                                      CF02(02 'PROTECT FILTER')
     A                                      CF03(03 'Avslutt program')
     A                                      CF12(03)
     A                                      CF04(04 'Søk kunde/leverandør')
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            HEAMSG         7A  P
     A            MSGNR          7A  P
     A                                  1  2'SYSPED/SAD005'
     A                                      COLOR(BLU)
     A            MSGTXH        30A  O  1 20MSGID(&HEAMSG SYMSGF)
     A                                      COLOR(WHT)
     A                                  1120DATE
     A                                      EDTCDE(Y)
     A                                  2120TIME
     A                                  4 13MSGCON(012 DIV1705 *LIBL/SYMSGF)
     A            WSAAVD         4Y 0B  4 26TEXT('AVDELINGSNR')
     A                                      DSPATR(UL)
     A  99                                  DSPATR(RI)
     A  99                                  DSPATR(PC)
     A                                      COLOR(BLU)
     A                                      EDTCDE(4)
     A            KOANVN        33A  O  4 32TEXT('AVDELINGSNAVN')
     A                                      COLOR(BLU)
N16  A                                  4 70MSGCON(020 JOV1536 *LIBL/SYMSGF)
N16  A                                      COLOR(WHT)
N16  A  32                              4 91MSGCON(010 JOV1556 *LIBL/SYMSGF)
N16  A                                      COLOR(BLU)
N16  A N32                              4 91MSGCON(010 JOV1557 *LIBL/SYMSGF)
N16  A                                      COLOR(BLU)
N16  A                                  5 70MSGCON(041 YBC2027 *LIBL/SYMSGF)
N16  A  32                                  DSPATR(ND)
N16  A                                  6 70'Avsender:'
N16  A  32                                  DSPATR(ND)
N16  A            WSANAV        12A  B  6 80
N16  A  32                                  DSPATR(PR)
N16  A  32                                  DSPATR(ND)
N16  A                                  6 93'Mottaker:'
N16  A  32                                  DSPATR(ND)
N16  A            WSMNAV        12A  B  6103
N16  A  32                                  DSPATR(PR)
N16  A  32                                  DSPATR(ND)
     A                                  7 20MSGCON(035 DIV1901 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  8 20'EKSTERN REF'
     A            WFSSOK        35A  B  8 35TEXT('CL-REF')
     A                                      DSPATR(UL)
     A                                      DSPATR(PC)
     A                                      COLOR(YLW)
     A                                  9 20MSGCON(006 DIV1902 *LIBL/SYMSGF)
     A            WSDT           6Y 0B  9 35TEXT('Registreringsdato')
     A                                      DSPATR(UL)
     A  98                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 10 20MSGCON(014 DIV1903 *LIBL/SYMSGF)
     A            WSTDN          7Y 0B 10 35TEXT('Tolldeklarasjonsnr')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 11 20MSGCON(014 DIV1904 *LIBL/SYMSGF)
     A            WSKNS          8Y 0B 11 35TEXT('Kundenr Selger')
     A                                      DSPATR(UL)
     A  97                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 11 48MSGCON(006 DIV1902 *LIBL/SYMSGF)
     A            WSDTS          6Y 0B 11 55TEXT('Registreringsdato')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 12 20MSGCON(014 DIV1905 *LIBL/SYMSGF)
     A            WSKNK          8Y 0B 12 35TEXT('Kundenr Kjøper')
     A                                      DSPATR(UL)
     A  96                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 12 48MSGCON(006 DIV1902 *LIBL/SYMSGF)
     A            WSDTK          6Y 0B 12 55TEXT('Registreringsdato')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 13 20MSGCON(014 DIV1906 *LIBL/SYMSGF)
     A            WSSG           3A  B 13 35TEXT('Signatur')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 13 39'Stat:'
     A            WSSGST         1A  B 13 45TEXT('STATUS')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PC)
     A                                      COLOR(YLW)
     A                                 13 62'Vis'
     A            WSKUF          1A  B 13 66TEXT('STATUS')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PC)
     A                                      COLOR(YLW)
     A                                 13 48MSGCON(006 DIV1902 *LIBL/SYMSGF)
     A            WSDTT          6Y 0B 13 55TEXT('Registreringsdato')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 13 68'O=Oppfølg TAD'
     A                                 14 20MSGCON(014 DIV1907 *LIBL/SYMSGF)
     A            WSTLL         10A  B 14 35TEXT('Tollnr Løpenr')
     A                                      DSPATR(UL)
     A  46                                  DSPATR(PC)
     A                                      COLOR(YLW)
     A                                 14 48MSGCON(010 SHU0698 *LIBL/SYMSGF)
     A                                 14 68'U=Uferdige...'
N12  A                                 15 23'Eksp.enhet:'
N12  A            WSTLE          6Y 0B 15 38DSPATR(UL)
N12  A                                      EDTCDE(Z)
N12  A                                      COLOR(YLW)
N12   * START  LINJE PÅ ALLE FELTER ER + 1.
     A                                 16 20MSGCON(014 DIV1908 *LIBL/SYMSGF)
     A            WSAAR          4A  B 16 35TEXT('Tollnr Løpenr')
     A                                      DSPATR(UL)
     A  46                                  DSPATR(PC)
     A                                      COLOR(YLW)
     A            WSGN          11A  B 16 40TEXT('Godsnummer')
     A                                      DSPATR(UL)
     A  47                                  DSPATR(PC)
     A                                      COLOR(YLW)
     A                                 17 20MSGCON(014 YBC0053 *LIBL/SYMSGF)
     A            WSST           1A  B 17 35TEXT('STATUS')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PC)
     A                                      COLOR(YLW)
N15  A                                 17 48'OMB status:'
N15  A            WSSTOMB        1A  B 17 60TEXT('STATUS')
N15  A                                      DSPATR(UL)
N15  A  51                                  DSPATR(PC)
N15  A                                      COLOR(YLW)
     A                                 18 20MSGCON(014 DIV1909 *LIBL/SYMSGF)
     A            WSMI           1A  B 18 35TEXT('Midlertidig  /I')
     A                                      DSPATR(UL)
     A  48                                  DSPATR(PC)
     A                                      VALUES(' ' 'I')
     A                                      COLOR(YLW)
     A                                 18 43MSGCON(015 DIV1910 *LIBL/SYMSGF)
     A                                 19 20MSGCON(013 SHU1234 *LIBL/SYMSGF)
     A            WSSEKV         6Y 0B 19 35DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(Z)
     A                                 19 48MSGCON(006 DIV1902 *LIBL/SYMSGF)
     A            WSSEKD         6Y 0B 19 55TEXT('Registreringsdato')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 20 20MSGCON(013 SHU1279 *LIBL/SYMSGF)
     A            WSRG          11A  B 20 35DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 20 48MSGCON(006 DIV1902 *LIBL/SYMSGF)
     A            WSDTRG         6Y 0B 20 55TEXT('Registreringsdato')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
N14  A                                 21 20MSGCON(013 YBC2022 *LIBL/SYMSGF)
N14  A            WSFN          17A  B 21 35DSPATR(UL)
N14  A                                      COLOR(YLW)
N14  A                                 22 20MSGCON(013 YBC2023 *LIBL/SYMSGF)
N14  A            WSMRK         17A  B 22 35DSPATR(UL)
N14  A                                      COLOR(YLW)
N41  A                                 23 20MSGCON(013 SHU2041 *LIBL/SYMSGF)
N41  A            WSFRI1         3A  B 23 35DSPATR(UL)
N41  A                                      COLOR(YLW)
N41  A            WSFRI2        35A  B 23 39DSPATR(UL)
N41  A                                      COLOR(YLW)
N41   * SLUTT  LINJE PÅ ALLE FELTER ER + 1.
     A                                 26 20'F3=Avslutt   F4=Søk kundenr'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 27  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A*
     A          R SUBFIL                    SFL
     A*%%TS  SD  20170131  185842  SVEIN       REL-V7R1M0  5770-WDS
     A            XIN50          1A  H
     A            XIN36          1A  H
N17  A            SIDP           2Y 0H
     A            WSNR           2Y 0B  6  2EDTCDE(4)
     A            SIAVD          4Y 0O  6  5TEXT('Tolldeklarasjonsnr')
     A                                      EDTCDE(4)
     A            SITDN          7Y 0O  6 10TEXT('Tolldeklarasjonsnr')
     A                                      EDTCDE(4)
     A  42                                  COLOR(TRQ)
     A  36                                  DSPATR(RI)
     A            SISG           3A  O  6 18TEXT('Signatur')
     A  45                                  COLOR(TRQ)
     A            WSDT2          6S 0O  6 22TEXT('Registreringsdato')
     A  41                                  COLOR(TRQ)
     A            SIKNK          8S 0H
     A            SIDTG         10A  H
     A            SITLL         10A  H
     A            SITLE          6A  H
     A            XSITLL        10A  O  6 29TEXT('Tollnr Løpenr')
     A  38
     AO 46                                  COLOR(TRQ)
S02  A*           SITLE          6A  O  6 38TEXT('Tollnr Ekspsted')
     A            XSITLE         6A  O  6 40TEXT('Tollnr Ekspsted')
     A  38                                  COLOR(TRQ)
     A            SIST           1A  O  6 47TEXT('Status')
     A  50                                  DSPATR(UL)
     A  49                                  COLOR(TRQ)
     A N49                                  COLOR(PNK)
S09  A*           WSNAK         30A  O  6 49
     A            WSNAK         26A  O  6 49
     A  40
N14  AO 34
N14  AO 35
     AO 44                                  COLOR(TRQ)
     A            SIVKB          9Y 0O  6 76TEXT('Bruttovekt')
     A                                      EDTCDE(4)
     A            SIMI           1A  O  6 86TEXT('Midlertidig  /I')
     A  48                                  COLOR(TRQ)
     A            SIGN          15A  O  6 88TEXT('Godsnummer')
     A  47                                  COLOR(TRQ)
     A            WSEPJN         1A  O  6105COLOR(RED)
     A            O2_SIMF        3A  O  6107
     A            O2_SIST        1A  O  6111COLOR(PNK)
     A            WSDT3          6Y 0O  6114TEXT('Registreringsdato')
     A                                      EDTCDE(Z)
     A            O2_SITLL      10A  O  6121TEXT('Tollnr Løpenr')
     A  46                                  COLOR(TRQ)
     A*
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A*%%TS  SD  20170131  185842  SVEIN       REL-V7R1M0  5770-WDS
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0019)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A N28                                  CF02(02 'ARB M/UFORT')
     A                                      CF03(03 'Avslutt program')
     A                                      CF12(03)
     A                                      CF05(05 'Frisk')
     A N28                                  CF06(06 'Lag ny')
     A N28                                  CF16(16 'Lag flere nye')
     A                                      CF08(08 'Søk')
     A N28                                  CF15(15 'Skriv ut')
     A                                      CF23(23 'Fler opsjoner')
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A          H                           HLPPNLGRP(SAD005010 SAD005PNL)
     A                                      HLPARA(01 001 01 132)
     A          H                           HLPPNLGRP(SAD005020 SAD005PNL)
     A                                      HLPARA(02 001 02 132)
     A          H                           HLPPNLGRP(SAD005030 SAD005PNL)
     A                                      HLPARA(03 001 03 132)
     A          H                           HLPPNLGRP(SAD005040 SAD005PNL)
     A                                      HLPARA(04 001 04 132)
     A          H                           HLPPNLGRP(SAD005050 SAD005PNL)
     A                                      HLPARA(05 001 05 132)
     A          H                           HLPPNLGRP(SAD005030 SAD005PNL)
     A                                      HLPARA(06 001 06 004)
     A          H                           HLPPNLGRP(SAD005060 SAD005PNL)
     A                                      HLPARA(06 005 06 089)
     A          H                           HLPPNLGRP(SAD005065 SAD005PNL)
     A                                      HLPARA(06 090 06 132)
     A          H                           HLPPNLGRP(SAD005010 SAD005PNL)
     A                                      HLPARA(07 001 24 132)
     A            ZRECNO         4S 0O  1 60SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYSPED/SAD005'
     A                                      COLOR(BLU)
     A            MSGTXH        30A  O  1 20MSGID(&HEAMSG SYMSGF)
     A                                      COLOR(WHT)
     A            HEAMSG         7A  P
     A                                  1120DATE
     A                                      EDTCDE(Y)
     A                                  2120TIME
     A                                  2  2MSGCON(012 DIV1705 *LIBL/SYMSGF)
     A            WSAAVD         4Y 0O  2 15TEXT('AVDELINGSNR')
     A                                      COLOR(BLU)
     A                                      EDTCDE(4)
     A            KOANVN        33A  O  2 20TEXT('AVDELINGSNAVN')
     A                                      COLOR(BLU)
N17  A N28                              2 81'13=Oppgj av midl.'
N17  A                                      COLOR(BLU)
     A N28N77                           2100'17=Angre OMB på OMB'
     A                                      COLOR(BLU)
     A  77                              2100'19=Se på epost'
     A                                      COLOR(BLU)
     A N28N77                           3  2'Velg   1=Lage 2=Endre 3=Kopiere 5=-
     A                                      Se Tdek. 6=Print 9=TVINN-logg 11=La-
     A                                      ge flere 12=Omberegn. 15=Vis omb. 1-
     A                                      6=Print OMB 18=Notisblokk'
     A                                      COLOR(BLU)
s13  A*N28 77                           3  2'Velg   8=Send fax  22=Vis PDF   51-
s13  A*                                     =Arkiverte dokumenter  52=Prt  skil-
s13  A*                                     leark'
     A N28 77                           3  2'Velg   8=Send fax  22=Vis PDF  25=-
     A                                      Se T&T-logg 51=Arkiverte dokumenter-
     A                                        52=Prt  skilleark 7=Frisøk 25=Tra-
     A                                        ck&Trace 88=omsend Emma  89=Omsen-
     A                                      ding omberegning Emma'
     A                                      COLOR(BLU)
     A  28                              3  2'Velg opsjon,   5=Vis SAD  6=Print -
E13  A                                       9=Vis TVINN-logg  15=Vis omberegn.'
     A                                      COLOR(BLU)
S02  A*                                 4  2MSGCON(078 YBC0045 *LIBL/SYMSGF)
     A N38                              4  2MSGCON(130 YBC0045 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A  38                              4  2MSGCON(130 SHU1235 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            W2NR           2Y 0B  5  2DSPATR(UL)
     A                                      EDTCDE(4)
     A            W2AVD          4Y 0B  5  5DSPATR(UL)
     A                                      EDTCDE(4)
     A            W2TDN          7Y 0B  5 10DSPATR(UL)
     A                                      EDTCDE(4)
     A            W2SG           3A  B  5 18DSPATR(UL)
     A            W2DTT          6Y 0B  5 22DSPATR(UL)
     A                                      EDTCDE(4)
S03  A*           W2TLL         10A  B  5 27DSPATR(UL)
     A            W2TLL         10A  B  5 29DSPATR(UL)
     A            W2ST           1A  B  5 47DSPATR(UL)
     A            W2MI           1A  B  5 86DSPATR(UL)
     A                                      VALUES(' ' 'I')
     A            W2AAR          4A  B  5 88DSPATR(UL)
     A            W2GN          11A  B  5 93DSPATR(UL)
N15  A            W2STOMB        1A  B  5111DSPATR(UL)
     A  76                              2 73MSGCON(026 SHU1707 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
      *
     A          R BUNN
     A          H                           HLPPNLGRP(SAD005999 SAD005PNL)
     A                                      HLPARA(01 001 02 130)
     A N28                             26  2'F3=Avslutt  F5=Frisk  F6=Lage ny  -
     A                                      F8=Søk  F15=Skriv ut F2=Arb.m/ufort-
     A                                      ollede  F16=Lag flere nye  F23=Fler-
     A                                      opsjoner'
     A                                      COLOR(BLU)
     A  28                             26  2'F3=Avslutt   F5=Frisk   F8=Søk'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 27  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R NYVARE
N09  A N28                                  CF02(02 'ARB M/UFORT')
     A                                      CF03(03 'Avslutt program')
     A                                      CF05(05 'Frisk')
     A N28                                  CF06(06 'Lag ny')
     A N28                                  CF16(16 'Lag flere nye')
     A                                      CF08(08 'Søk')
     A N28                                  CF15(15 'Skriv ut')
     A          H                           HLPPNLGRP(SAD005010 SAD005PNL)
     A                                      HLPARA(01 001 01 130)
     A          H                           HLPPNLGRP(SAD005020 SAD005PNL)
     A                                      HLPARA(02 001 02 130)
     A          H                           HLPPNLGRP(SAD005030 SAD005PNL)
     A                                      HLPARA(03 001 03 130)
     A          H                           HLPPNLGRP(SAD005040 SAD005PNL)
     A                                      HLPARA(04 001 04 130)
     A          H                           HLPPNLGRP(SAD005050 SAD005PNL)
     A                                      HLPARA(05 001 05 130)
     A          H                           HLPPNLGRP(SAD005010 SAD005PNL)
     A                                      HLPARA(06 001 22 130)
     A          H                           HLPPNLGRP(SAD005999 SAD005PNL)
     A                                      HLPARA(23 001 24 130)
     A                                  1  2'SYSPED/SAD005'
     A                                      COLOR(BLU)
     A            MSGTXH        30A  O  1 20MSGID(&HEAMSG SYMSGF)
     A                                      COLOR(WHT)
     A            HEAMSG         7A  P
     A                                  1120DATE
     A                                      EDTCDE(Y)
     A                                  2120TIME
     A                                  2  2MSGCON(012 DIV1705 *LIBL/SYMSGF)
     A            WSAAVD         4Y 0O  2 15TEXT('AVDELINGSNR')
     A                                      COLOR(BLU)
     A                                      EDTCDE(4)
     A            KOANVN        33A  O  2 20TEXT('AVDELINGSNAVN')
     A                                      COLOR(BLU)
     A N28                              3  2'Velg   1=Lage 2=Endre 3=Kopiere 5=-
     A                                      Se Tdek. 6=Print 9=TVINN-logg 11=La-
N08  A                                      ge flere 12=Omberegn. 15=Vis omb. 1-
E10  A                                      6=Print OMB 18=Notisblokk'
S08  A*                                     ge flere'
     A                                      COLOR(BLU)
     A  28                              3  2'Velg opsjon,   5=Vis SAD  6=Print -
N08  A                                       9=Vis TVINN-logg  15=Vis omberegn.'
S08  A*                                      9=Vis TVINN-logg'
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(130 YBC0045 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            W2NR           2Y 0B  5  2DSPATR(UL)
     A                                      EDTCDE(4)
     A            W2AVD          4Y 0B  5  5DSPATR(UL)
     A                                      EDTCDE(4)
     A            W2TDN          7Y 0B  5 10DSPATR(UL)
     A                                      EDTCDE(4)
     A            W2SG           3A  B  5 18DSPATR(UL)
     A            W2DTT          6Y 0B  5 22DSPATR(UL)
     A                                      EDTCDE(4)
     A            W2TLL         10A  B  5 29DSPATR(UL)
     A            W2ST           1A  B  5 47DSPATR(UL)
E09  A            W2MI           1A  B  5 86DSPATR(UL)
     A                                      VALUES(' ' 'I')
E09  A            W2AAR          4A  B  5 88DSPATR(UL)
E09  A            W2GN          11A  B  5 93DSPATR(UL)
N15  A            W2STOMB        1A  B  5111DSPATR(UL)
     A N28                             26  2'F3=Avslutt  F5=Frisk  F6=Lage ny  -
     A                                      F8=Søk  F15=Skriv ut F2=Arb.m/ufort-
     A                                      ollede  F16=Lag flere nye  F23=Fler-
     A                                      opsjoner'
     A                                      COLOR(BLU)
     A  28                             26  2'F3=Avslutt   F5=Frisk   F8=Søk '
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 27  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R BILDE02
     A                                      WINDOW(*DFT 6 35 *NOMSGLIN)
     A                                      CF12(12 'GÅ TILBAKE')
     A                                      OVERLAY
     A                                  2  2'Printer til hovedark:'
     A            W3PRT1        10A  B  2 24DSPATR(UL)
     A                                  3  2'Printer til till.ark:'
     A            W3PRT2        10A  B  3 24DSPATR(UL)
     A                                  4  2'Printer til datalist:'
     A            W3PRT3        10A  B  4 24DSPATR(UL)
     A                                  5  2'F12=Avbryt'
     A                                      COLOR(BLU)
N11   *
N11  A          R BNYOMB
N11  A                                      WINDOW(*DFT 7 35 *NOMSGLIN)
N11  A                                      CF12(12 'GÅ TILBAKE')
N11  A                                      OVERLAY
N11  A                                  2  2'Denne fortollingen er allerede'
N11  A                                  3  2'omberegnet.'
N11  A                                  4  2'Ny omberegning ?'
N11  A            WSNYOMB        1A  B  4 24DSPATR(UL)
N11  A                                      VALUES('J' 'N')
N11  A                                  4 26'(J/N)'
N11  A                                  5  2'Basert på siste omb.?'
N11  A            WSSOMB         1A  B  5 24DSPATR(UL)
N11  A                                      VALUES('J' 'N')
N11  A                                  5 26'(J/N)'
N11  A                                  6  2'N=Original fortolling'
N17   *
N17  A          R BOPPGJ
N17  A                                      WINDOW(*DFT 5 40 *NOMSGLIN)
N17  A                                      CF12(12 'GÅ TILBAKE')
N17  A                                      OVERLAY
N17  A                                  2  2'Denne fortollingen er allerede'
N17  A                                  3  2'tatt oppgjør.'
N17  A                                  4  2'Oppgjør er på avd/oppd:'
N17  A            M1_SIAVD       4Y 0O  4 26DSPATR(UL)
N17  A                                      EDTCDE(Z)
N17  A                                  4 31'/'
N17  A            M1_SITDN       7Y 0O  4 33DSPATR(UL)
N17  A                                      EDTCDE(Z)
N17  A                                  5  2'ENTER = Tilbake'
N17  A                                      COLOR(BLU)
