********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 YBC PTF10 BIAKT=L ER ENDRET TIL Å TESTE AUTORISASJON
********************************************************************
      *********************************************************************
      * Lage ny mottagersadresse
      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/SYL009 MODULE(*LIBL/SYL009
CRTPGM*        *LIBL/CHECKUSR *LIBL/INCGI) ACTGRP(CGI) AUT(*USE)
      *
      *********************************************************************
      * MAIN PROGRAM FLOW
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
     FBRIDF     UF   E           K DISK    USROPN
     FBRPARF    if   e           k disk    usropn
     FVADR      UF   E           K DISK    USROPN
     FVADR1     IF   E           K DISK    USROPN
     F                                     RENAME(VADRR:VADRR1)
     FKUNKO     UF   E           K DISK    USROPN
     FCUNDL01   UF A E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Prototype defintions and standard system API error structure
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      *--------------------------------------------------------------------
      * Variables common to CGI programs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
      * Variables received from the input string
     D OddEven         s             10
     D lstbrd          s              1
     D lng             s              2
     D BckGnd          s             10
     D UsrSpcName      s             10
     D INNTYPE         S              1
     D WSUSER          S             10
     D WSDRNR          S              5
     D WBDRNR          S              5  0
     D ItemCount       s             10i 0
     D SernoC          s            105a
     D i               s             10i 0
     D PGM             C                   CONST('SYSPED/CHECKUSR')
      *
     D Lib             s             10
     D Fn              s             10
     D Mbr             s             10
      *
     D Spaces          s             12a   inz('&nbsp;&nbsp;')
      *
      *--------------------------------------------------------------------
      * Prolog common to CGI programs
      * -Receive input from the remote browser
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prolog3
      *=====================================================================******
      * MAIN PROCESS LINE
      *=====================================================================******
      * Get program start time for calculating execution time
     C                   time                    timedata1
      * Parse input
     C                   exsr      Parse
      *
     C                   EXSR      INIT
      *
     C                   EXSR      SLETT
      *
      * Set output skeleton html member name
     C                   exsr      SetSkl
      * Read skeleton output html, etc.
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
      *
      * 1-Section /$top
     C                   exsr      SetTop
     C                   callp     wrtsection('top')
      *------------------                                                   ******
      * Go to the last Recd already shown (1. time = from top)
     C                   exsr      GotoRecord
      *------------------                                                   ******
      * Write HTML sections
      * 2a-Section /$0found
     C     XRRN          IFEQ      9999999
     C                   callp     wrtsection('0found')
     C                   goto      endhtml
     C                   endif
      *
      * 3-Section /$tablestr
     C                   exsr      SetTabStr
     C                   callp     wrtsection('tablestr')
S01  C*                  IF        BIAKT = 'L'
N01  C                   IF        XAUT = 'L'
     C                   callp     wrtsection('Slett1')
     C                   END
     C                   callp     wrtsection('Slett2')
      * 4-Sections /$tablerow
      *
     C     VADR1K        SETLL     VADR1
     C     VADR1K        READE     VADR1                                  50
      *
     C                   z-add     0             bseq              5 0
     C                   DOW       *IN50 = *OFF
     C                   eval      bseq=bseq + 1
     C                   exsr      SetTabRow
     C                   callp     wrtsection('tablerow')
S01  C*                  IF        BIAKT = 'L'
N01  C                   IF        XAUT = 'L'
     C                   callp     wrtsection('Slett3')
     C                   END
     C                   callp     wrtsection('Slett4')
     C     VADR1K        READE     VADR1                                  50
     C                   ENDDO                                                  *hival
      *
      * 5-Section /$tableend
     C                   callp     wrtsection('tableend')
      *
      * 5a-Section /$foundsome
     C     XRRN          IFLT      9999999
     C                   exsr      SetFndSome
     C                   callp     wrtsection('foundsome2')
S01  C*                  IF        BIAKT = 'L'
N01  C                   IF        XAUT = 'L'
     C                   callp     wrtsection('Slett5')
     C                   END
     C                   callp     wrtsection('Slett6')
     C                   endif
      * 6-Section /$endhtml
     C     endhtml       tag
     C                   exsr      SetEnd
      *
     C                   EXSR      EXIT
     C                   return
      *
      *=====================================================================******
      * Parse input
      *=====================================================================******
     C     Parse         begsr
     C                   eval      lng     = zhbgetvar('lng')
     C                   eval      INNTYPE = zhbgetvar('INNTYPE')
     C                   eval      WSUSER  = zhbgetvar('USER')
     C                   EVAL      UsrSpcName  = zhbgetvar('UsrSpcName')
      *
     C                   endsr
      *
      *=====================================================================******
      *  Set output variables for section /$top
      *  (search criteria)
      *=====================================================================******
     C     SetTop        begsr
      * Repeat national language for the next invocation
     C                   callp     updhtmlvar('LNG':lng:
     C                             InitHTMLVars)
OddEvC                   callp     updHTMLvar('ROWHEAD':RowHead)
OddEvC                   callp     updHTMLvar('LogoImg':LogoImg)
      * Color for text, background, links, visited links, active links
     C                   callp     updhtmlvar('TXTCOLOR':'black')
     C                   callp     updhtmlvar('BOLD':' ')
     C                   callp     updhtmlvar('BGCOLOR':BIFARG)
     C                   callp     updhtmlvar('LINK':'0000FF')
     C                   callp     updhtmlvar('VLINK':'FF0000')
     C                   callp     updhtmlvar('ALINK':'00FF00')
BODY C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updhtmlvar('WSKN':
     C                             %trim(%editc(BIKUNR:'Z')))
     C                   callp     updhtmlvar('WSNAVN':KNAVN)
     C                   callp     updHtmlVar('USER':WSUSER)
     C                   callp     updHtmlVar('WSNA':BIBESK)
     C                   callp     updHtmlVar('UsrSpcName':UsrSpcName)
     C                   callp     updHtmlVar('INNTYPE':INNTYPE)
     C                   callp     updHTMLvar('sdato':
     C                             %trim(%editw(BIDTV:'    .  .  ')))
     C                   callp     updHTMLvar('stid':
     C                             %trim(%editw(BITIDV:'  .  .  ')))
      *
     C                   endsr
      *=====================================================================******
      * Set html output skeleton member before its read into memory
      *=====================================================================******
     C     SetSkl        begsr
      *------------------
      * Set html output skeleton member
     C                   eval      lng = uppify(lng)
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC' + lng
     C                   eval      Mbr = 'SYL009'
     C                   CAT       XSPRÅK:0      MBR
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$tablestr (table start)
      *=====================================================================******
     C     SetTabStr     begsr
     C     lstbrd        ifeq      'Y'
     C                   callp     updhtmlvar('LB':'0')
     C                   else
     C                   callp     updhtmlvar('LB':'1')
     C                   endif
      *
     C                   endsr
      *
      *=====================================================================******
      *  Set output variables for section /$tablerow (table row)
      *=====================================================================******
     C     SetTabRow     begsr
      * odde/partalls-linjer i alternativ skyggelegging/bakgrunnsfarge
     c     OddEven       IFEQ      ODD
     c                   eval      OddEven = EVEN
     c                   else
     c                   eval      OddEven = ODD
     c                   end
     C                   callp     updHTMLvar('ODDEVEN':OddEven)
     c* Sequence of this Recd (numeric, converted to char)
     C                   move      bseq          bseqchar          5
     C                   callp     updhtmlvar('SEQ':bseqchar)
     C                   callp     updhtmlvar('VADRNR':
     C                             %trim(%editc(VADRNR:'Z')))
     C                   callp     updhtmlvar('VAKURE':VAKURE)
     C                   callp     updhtmlvar('VADRNA':VADRNA)
     C                   callp     updhtmlvar('VADRN1':VADRN1)
     C                   callp     updhtmlvar('VADRN2':VADRN2)
     C                   callp     updhtmlvar('VADRN3':VADRN3)
      *
     C                   endsr
      *
      *=====================================================================******
      *  Set output variables for section /$foundsome (some Recds were found)
      *=====================================================================******
     C     SetFndSome    begsr
      * Repeat background color
     C                   callp     updhtmlvar('BCKGND':bckgnd)
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$endhtml
      *=====================================================================******
     C     SetEnd        begsr
      * Server protocol
     C                   callp     updhtmlvar('PROTOCOL':S_Protocol)
      * Compute run time
     C                   time                    timedata2
     C     timedata2     subdur    timedata1     ms:*ms
     C                   eval      sec = ms / 1000000
     C                   callp     updhtmlvar('runtime':
     C                             %trim(%editw(sec:'     0 .   ')))
      *
     C                   endsr
      *=====================================================================******
     C*  Go to the last Recd aleady shown
      *=====================================================================******
     C     GotoRecord    begsr
     C                   MOVE      *ZEROS        XRRN              7 0
      *
     C     VADR1K        SETLL     VADR1
     C     VADR1K        READE     VADR1                                  50
      *
     C   50              MOVE      999999999     XRRN
      *
     C                   endsr
      *
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
     C                   OPEN      BRIDF
     C     WSUSER        CHAIN(N)  BRIDF                              50
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
      *
     C                   EXSR      TSTUSER
      *
OddEv * hent Parametre som går igjen i mange prog:
OddEv *      Odd/Even/Rowhead-farger,Logobilde,Språk,Intern/Ekstern bruker,  mm
OddEvc                   call      'ESGETPAR'
OddEvc                   parm                    BIBRID
OddEvc                   parm                    BIFIRM
OddEvc                   parm                    BIKUNR
OddEvc                   parm                    BIKNG
OddEvc                   parm                    RowHead          10
OddEvc                   parm                    Odd              10
OddEvc                   parm                    Even             10
OddEvc                   parm                    LogoImg          50
OddEvc                   parm                    XSPRÅK            2
OddEvc                   parm                    XINTERN           1
OddEvc                   parm                    ParmDS1        1024
OddEvc                   parm                    ParmDS2        1024
OddEvc                   parm                    ParmDS3        1024
      *
     C                   OPEN      VADR
     C                   OPEN      VADR1
     C                   OPEN      KUNKO
     C                   open      BRPARF                               50
     C                   open      CUNDL01                              50
     C     KUNKEY        klist
     C                   kfld                    BIFIRM
     C                   kfld                    BIKUNR
     C     KUNKEY        chain     cundl01
      *
     C     VADRK         klist
     C                   kfld                    BIFIRM
     C                   kfld                    BIKUNR
     C                   kfld                    WBDRNR
      *
     C     VADR1K        klist
     C                   kfld                    BIFIRM
     C                   kfld                    BIKUNR
      *
     C     KUNKOK        klist
     C                   kfld                    BIKUNR
     C                   kfld                    KUNDNR
     C                   kfld                    WBDRNR
N01  C     BrParKey      klist
N01  C                   kfld                    PABRID
N01  C                   kfld                    PAKUNR
N01  C                   kfld                    PAID
N01  C                   EVAL      PABRID = WSUSER
N01   *
N01  C                   MOVE      'AUTOR'       PAID
N01  C     BrParKey      chain     BRPARF                             33
N01  C                   IF        *IN33
N01  C                   MOVE      'N'           XAUT              1
N01  C                   ELSE
N01  C                   MOVEL     PAVRDA        XAUT
N01  C                   END
      *
     C                   endsr
      *
      *////////////////////////////////////////////////////////////
     C     TSTUSER       BEGSR
      *////////////////////////////////////////////////////////////
      /copy syspeds/qrpgsrcpy,tstuser
      *
     C                   endsr
      *
      *=====================================================================******
      *  AVSLUTT
      *=====================================================================******
     C     EXIT          begsr
     C                   callp     wrtsection('endhtml')
     C                   callp     wrtsection('*fini')
     C                   close     BRIDF
     C                   close     BRPARF
     C                   close     VADR
     C                   close     VADR1
     C                   close     KUNKO
     C                   close     cundl01
     C                   eval      *inlr = *on
      *
     C                   endsr
      *
      *=====================================================================
      *  Slett merket linje
      *=====================================================================
     C     SLETT         begsr
      *
     C                   EVAL      ItemCount = ZhbGetVarCnt('WSDRNR')
     C                   IF        ItemCount <> 0
     C                   DOW       i < ItemCount
     C                   eval      i = i + 1
     C                   eval      SernoC = ZhbGetVar('WSDRNR':i)
     C                   MOVEL     SERNOC        WSDRNR
     C                   eval      WBDRNR  = c2n2(WSDRNR)
      *
     C     VADRK         CHAIN     VADR                               33
     C                   IF        *IN33 = *OFF
     C     KUNKOK        CHAIN     KUNKO                              33
     C  N33              DELETE    KUNKOR
     C                   DELETE    VADRR
     C                   END
      *
     C                   ENDDO
     C                   END
      *
     C                   endsr
      *
