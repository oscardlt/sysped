     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      REF(LLHEAD LLHEADR)
     A                                      PRINT
     A                                      CA03(03 'Exit')
      *
     A          R NOREC
     A                                      TEXT('Select')
     A                                      CA12(12)
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/LL032R'
     A                                      COLOR(BLU)
     A                                  1 20MSGCON(50 YBC1457 SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 73DATE
     A                                      EDTCDE(Y)
     A                                  2  2MSGCON(13 YBC1458 SYMSGF)
     A            U1PNR          8Y 0O  2 16EDTCDE(4)
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            U1PNV         30A  O  2 26DSPATR(HI)
     A                                  2 73TIME
     A                                  4  2MSGCON(78 YBC1459 SYMSGF)
     A                                      COLOR(WHT)
     A                                  5  2MSGCON(78 YBC1460 SYMSGF)
     A                                      COLOR(WHT)
     A                                 23  2MSGCON(18 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
      *
     A          R SUBFIL                    SFL
     A            LLMN           7S 0H
     A            LLVN           3S 0H
     A            WSNR           1A  B  6  2VALUES(' ' '5' '7')
     A  40                                  DSPATR(PR)
     A            LLANR         15A  O  6  6
     A                                      DSPATR(HI)
     A            XLVB           6A  O  6 22
     A            LLWNT0         7  0O  6 29EDTCDE(4)
     A            LLWNTL         7  0O  6 37EDTCDE(4)
     A            LLAEH1         3A  H
     A            LLWNT1         5  0O  6 45EDTCDE(4)
     A            LLAEH2         3A  H
     A            LLWNT2         5  0O  6 51EDTCDE(4)
     A            LLAEH3         3A  H
     A            LLWNT3         5  0O  6 57EDTCDE(4)
     A            LLWVEK         7  2O  6 63EDTCDE(4)
     A            LLWKUB         7  4O  6 72EDTCDE(4)
     A            LHBNR     R        O  7  5DSPATR(UL)
     A            LHTDT          8  0O  7 23EDTCDE(4)
     A                                      DSPATR(UL)
     A            LHORDR    R        O  7 32DSPATR(UL)
     A            LHPAK     R        O  7 50DSPATR(UL)
     A            LHALN     R        O  7 68EDTCDE(4)
     A                                      DSPATR(UL)
     A            LHFLN     R        H
     A            LHAVD     R        O  7 76DSPATR(UL)
     A                                      EDTCDE(4)
     A            LHOPD     R        H
     A            LHFBN     R        H
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0008)
     A                                      SFLPAG(0007)
     A                                      TEXT('SUBCTL')
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                      CA05(05 'Forny')
     A                                      CA07(07 'UTSKRIFT')
     A                                      CA12(12 'Annuller')
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/LL032R'
     A                                      COLOR(BLU)
     A                                  1 20MSGCON(50 YBC1457 SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 73DATE
     A                                      EDTCDE(Y)
     A                                  2  2MSGCON(13 YBC1458 SYMSGF)
     A            U1PNR          8Y 0O  2 16EDTCDE(4)
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            U1PNV         30A  O  2 26DSPATR(HI)
     A            ZRECNO         4S 0O  2 65SFLRCDNBR
     A                                  2 73TIME
     A                                  3  2MSGCON(50 YBC1461 SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(78 YBC1459 SYMSGF)
     A                                      COLOR(WHT)
     A                                  5  2MSGCON(78 YBC1460 SYMSGF)
     A                                      COLOR(WHT)
      *
     A          R BOTREC
     A                                      OVERLAY
     A                                 23  2MSGCON(18 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(18 DIV0021 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 42MSGCON(18 YBC1752 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 62MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
      *
     A          R BLANKREC                  TEXT('Message')
     A                                      OVERLAY
     A  30        ZMSGTX        78A  O 24  2MSGID(&ZMSGNO SYMSGF)
     A                                      COLOR(WHT)
     A            ZMSGNO         7A  P
      *
     A          R VISLIN
     A                                      CA12(12 'Annuller')
     A                                      OVERLAY
     A                                  1  2'SYSPED/LL032R'
     A                                      COLOR(BLU)
     A                                  1 20MSGCON(50 YBC1457 SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 73DATE
     A                                      COLOR(BLU)
     A                                  2  2MSGCON(13 YBC1458 SYMSGF)
     A            U1PNR          8Y 0O  2 16EDTCDE(4)
     A            U1PNV         30A  O  2 26DSPATR(HI)
     A                                  2 73TIME
     A                                      COLOR(BLU)
     A                                  3  2MSGCON(05 YBC1458 SYMSGF)
     A            LHOPR     R        O  3  8COLOR(WHT)
     A                                  3 12MSGCON(09 YBC1463 SYMSGF)
     A            LHBNR     R        O  3 22COLOR(WHT)
     A                                  5  2MSGCON(16 YBC1464 SYMSGF)
     A            LLVN           3Y 0O  5 19EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  7  2MSGCON(16 YBC1465 SYMSGF)
     A            LLANR         15A  O  7 19DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  8  2MSGCON(16 YBC1466 SYMSGF)
     A            LLVB          35A  O  8 19DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  9  2MSGCON(16 YBC1467 SYMSGF)
     A                                      DSPATR(ND)
     A            LLHK           3A  O  9 19DSPATR(UL)
     A                                      DSPATR(ND)
     A                                      COLOR(WHT)
     A                                 10  2MSGCON(16 YBC1468 SYMSGF)
     A            LLLOK1         5A  O 10 19DSPATR(UL)
     A            LLLOK2        20A  O 10 25DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 10 52MSGCON(16 YBC1469 SYMSGF)
     A            LLNTLK         3Y 0O 10 69DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A                                 11  2MSGCON(16 YBC1470 SYMSGF)
     A            LLPRI         13Y 2O 11 19DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 12  2MSGCON(16 YBC1471 SYMSGF)
     A            LLSER         25A  O 12 19DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 13  2MSGCON(16 YBC1472 SYMSGF)
     A            LLALLO         1A  O 13 19DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 14  2MSGCON(16 YBC1473 SYMSGF)
     A            LLANT0         7Y 0O 14 19DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 14 42MSGCON(16 YBC1474 SYMSGF)
     A            LLWNT0         7Y 0O 14 59DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 15  2MSGCON(16 YBC1475 SYMSGF)
     A            LLANTL         7Y 0O 15 19DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 15 42MSGCON(16 YBC1476 SYMSGF)
     A            LLWNTL         7Y 0O 15 59DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 15 67MSGCON(05 YBC1477 SYMSGF)
     A                                 15 73'STK'
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 16  2MSGCON(16 YBC1445 SYMSGF)
     A            LLANT1         5Y 0O 16 19DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 16 42MSGCON(16 YBC1478 SYMSGF)
     A            LLWNT1         5Y 0O 16 59DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 16 67MSGCON(05 YBC1477 SYMSGF)
     A            LLAEH1         3A  O 16 73DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 17  2MSGCON(16 YBC1479 SYMSGF)
     A            LLANT2         5Y 0O 17 19DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 17 42MSGCON(16 YBC1480 SYMSGF)
     A            LLWNT2         5Y 0O 17 59DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 17 67MSGCON(05 YBC1477 SYMSGF)
     A            LLAEH2         3A  O 17 73DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 18  2MSGCON(16 YBC1481 SYMSGF)
     A            LLANT3         5Y 0O 18 19DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 18 42MSGCON(16 YBC1482 SYMSGF)
     A            LLWNT3         5Y 0O 18 59DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 18 67MSGCON(05 YBC1477 SYMSGF)
     A            LLAEH3         3A  O 18 73DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 19  2MSGCON(16 YBC1483 SYMSGF)
     A            LLVEK          7Y 2O 19 19DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 19 42MSGCON(16 YBC1484 SYMSGF)
     A            LLWVEK         7Y 2O 19 59DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 20  2MSGCON(16 YBC1485 SYMSGF)
     A            LLKUB          7Y 4O 20 19DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 20 42MSGCON(16 YBC1486 SYMSGF)
     A            LLWKUB         7Y 4O 20 59DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 21  2MSGCON(16 YBC1487 SYMSGF)
     A            LHAVD          4Y 0O 21 19EDTCDE(4)
     A                                      COLOR(WHT)
     A                                 21 24'/'
     A                                      COLOR(WHT)
     A            LHOPD          7Y 0O 21 26EDTCDE(4)
     A                                      COLOR(WHT)
     A                                 21 34'/'
     A                                      COLOR(WHT)
     A            LHFBN          3Y 0O 21 36EDTCDE(4)
     A                                      COLOR(WHT)
     A                                 22  2'Årsak til manko:'
     A            LLMKD          5A  O 22 19DSPATR(UL)
     A                                      COLOR(WHT)
     A            LLMTXT        35A  O 22 26DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 23  2MSGCON(18 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
      *
     A          R VISHOD
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CA12(12 'Annuller')
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/LL032R'
     A                                      COLOR(BLU)
     A                                  1 20MSGCON(40 YBC1413 SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 73DATE
     A                                      EDTCDE(Y)
     A                                  2  2MSGCON(13 YBC1415 SYMSGF)
     A            U1PNR          8Y 0O  2 16EDTCDE(4)
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            U1PNV         30A  O  2 26DSPATR(HI)
     A                                  2 73TIME
     A                                      COLOR(BLU)
     A                                  3  2MSGCON(16 YBC1430 SYMSGF)
     A            LHSTA     R        O  3 19COLOR(WHT)
     A                                  4  2MSGCON(16 YBC1431 SYMSGF)
     A            LHMN      R        O  4 19COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  5  2MSGCON(16 YBC1432 SYMSGF)
     A            LHBNR     R        O  5 19
     A            KUKUN2         8Y 0O  5 37EDTCDE(4)
     A            KUVADR         5Y 0O  5 46EDTCDE(4)
     A            LHBNA     R        O  6 19
     A            LHBA1     R        O  6 51
     A            LHBA2     R        O  7 19
     A            LHBPN     R        O  7 51EDTCDE(4)
     A            LHBA3     R        O  7 57
     A                                  8 19MSGCON(16 YBC1433 SYMSGF)
     A            LHBLK     R        O  8 36
     A                                  8 42MSGCON(17 YBC1434 SYMSGF)
     A            LHBPU     R        O  8 60
     A                                  9  2MSGCON(16 YBC1435 SYMSGF)
     A            LHTDT     R        O  9 19EDTWRD('    .  .  ')
     A                                  9 41MSGCON(18 YBC1436 SYMSGF)
     A            LHTN      R        O  9 60EDTCDE(4)
     A                                  9 69'Prod.'
     A            LHPRKD    R        B  9 75REFFLD(LLHEADR/LHPRKD LLHEAD)
     A                                 10  2MSGCON(16 YBC1437 SYMSGF)
     A            LHPAK     R        O 10 19
     A                                 11  2MSGCON(16 YBC1438 SYMSGF)
     A            LHORDR    R        O 11 19
     A                                 12  2MSGCON(16 YBC1439 SYMSGF)
     A            LHFLN     R        O 12 19
     A                                 13  2MSGCON(16 YBC1440 SYMSGF)
     A            LHALN     R        O 13 19EDTCDE(4)
     A                                      COLOR(WHT)
     A                                 14  2MSGCON(16 YBC1441 SYMSGF)
     A            LHAVD     R        O 14 19EDTCDE(4)
     A                                      COLOR(WHT)
     A                                 14 24'/'
     A                                      COLOR(WHT)
     A            LHOPD     R        O 14 26EDTCDE(4)
     A                                      COLOR(WHT)
     A                                 14 34'/'
     A                                      COLOR(WHT)
     A            LHFBN     R        O 14 36EDTCDE(4)
     A                                      COLOR(WHT)
     A                                 15  2MSGCON(16 YBC1443 SYMSGF)
     A            LHANT0    R        O 15 19EDTCDE(4)
     A                                 15 30MSGCON(14 YBC1444 SYMSGF)
     A            LHANTL    R        O 15 45EDTCDE(4)
     A                                 15 53'STK'
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 16 30MSGCON(16 YBC1446 SYMSGF)
     A            LHANT1    R        O 16 47EDTCDE(4)
     A            LHAEH1    R        O 16 53DSPATR(HI)
     A                                 17 30MSGCON(16 YBC1447 SYMSGF)
     A            LHANT2    R        O 17 47EDTCDE(4)
     A            LHAEH2    R        O 17 53DSPATR(HI)
     A                                 18 30MSGCON(16 YBC1448 SYMSGF)
     A            LHANT3    R        O 18 47EDTCDE(4)
     A            LHAEH3    R        O 18 53DSPATR(HI)
     A                                 19  2MSGCON(16 YBC1449 SYMSGF)
     A            LHVEK     R        O 19 19EDTCDE(M)
     A                                 19 42MSGCON(16 YBC1450 SYMSGF)
     A            LHWVEK    R        O 19 59EDTCDE(M)
     A                                 20  2MSGCON(16 YBC1451 SYMSGF)
     A            LHKUB     R        O 20 19EDTCDE(M)
     A                                 20 42MSGCON(16 YBC1452 SYMSGF)
     A            LHWKUB    R        O 20 59EDTCDE(M)
     A                                 21  2MSGCON(16 YBC1453 SYMSGF)
     A            LHST1     R        O 21 19
     A                                 21 21'/'
     A            LHST3     R        O 21 23
     A                                 21 42MSGCON(16 YBC1454 SYMSGF)
     A            LHTLIN    R        O 21 59EDTCDE(4)
     A                                 22  2MSGCON(16 YBC1455 SYMSGF)
     A            LHKLL     R        O 22 19EDTCDE(4)
     A            LHMLP     R        O 22 26EDTCDE(4)
     A            LHST2     R        O 22 32
     A                                 22 42MSGCON(16 YBC1456 SYMSGF)
     A            LHBPAL    R        O 22 59EDTCDE(4)
     A            LHAPAL    R        O 22 66EDTCDE(4)
     A                                 23  2MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
      *
     A          R PRTLIS
     A                                      WINDOW(*DFT 7 45 *NOMSGLIN)
     A                                      OVERLAY
     A                                      CF04(04 'SØK')
     A                                      CA12(12 'AVBRYT')
     A                                  1  2'LL032R'
     A                                      COLOR(BLU)
     A                                  1 10MSGCON(30 YBC1753 SYMSGF)
     A                                      DSPATR(HI)
     A                                  2  2MSGCON(12 YBC1741 SYMSGF)
     A            WSANR         15A  B  2 15DSPATR(UL)
     A  90                                  DSPATR(HI)
     A  90                                  DSPATR(PC)
     A                                  3  2MSGCON(12 YBC1742 SYMSGF)
     A            WSDTF          6  0B  3 15DSPATR(UL)
     A  89                                  DSPATR(HI)
     A  89                                  DSPATR(PC)
     A                                      EDTCDE(4)
     A                                  3 25MSGCON(08 YBC1750 SYMSGF)
     A                                  4  2MSGCON(12 YBC1743 SYMSGF)
     A            WSDTT          6  0B  4 15DSPATR(UL)
     A  88                                  DSPATR(HI)
     A  88
     AON30                                  DSPATR(PC)
     A                                      EDTCDE(4)
     A                                  4 25MSGCON(08 YBC1750 SYMSGF)
     A                                  5  2MSGCON(12 YBC1755 SYMSGF)
     A            WSBNR         17A  B  5 15DSPATR(UL)
     A  87                                  DSPATR(HI)
     A  87                                  DSPATR(PC)
     A                                  5 33MSGCON(12 YBC1756 SYMSGF)
     A                                  6  2MSGCON(12 YBC1754 SYMSGF)
     A            WSPRT         10A  B  6 15DSPATR(UL)
     A  86                                  DSPATR(HI)
     A  86                                  DSPATR(PC)
     A N30                              7  2MSGCON(18 DIV0032 SYMSGF)
     A                                      COLOR(BLU)
     A N30                              7 22MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A  30        ZMSG36        36A  O  7  2MSGID(&ZMSGNO SYMSGF)
     A                                      COLOR(WHT)
     A            ZMSGNO         7A  P
