      *********************************************************************
      *  WEB "Wraper" for å kunne kalle SVE006R print tollkvittering eksport.
********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/TSVE014R MODULE(*LIBL/TSVE014R *LIBL/INCGI)
CRTPGM*        ACTGRP(*NEW) AUT(*USE)
      *
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
12XXX * 01 JOV PTF12 endre parameter M-Kun print til C = print OG arkiver
12XXX *              kall opså prog for å skrive/arkivere ZEM
********************************************************************
      *********************************************************************
      /copy SYSPEDS/qrpgsrcpy,hspecs
      /copy SYSPEDS/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
N01  FSVEH      IF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy SYSPEDS/qrpgsrcpy,prototypeb
      /copy SYSPEDS/qrpgsrcpy,usec
      /copy SYSPEDS/qrpgsrcpy,variables3
      *
      * Varible for
     D WSUSER          S             10
     D AVD             S              4
     D AVDA            S              4
     D AVDN            S              4  0
     D OPD             S              7
     D OPDA            S              7
     D OPDN            S              7  0
     D JSONstring      s          32000
     D Error           S            150
     D AntLest         S              9  0

      *get input-string
      /copy syspeds/qrpgsrcpy,prolog3

      * Parse input
     C                   exsr      Parse
      * Open files etc
     C                   EXSR      INIT
      * Skriv ut
     C                   EXSR      SRTE
      *
     c                   callp     gethtmlifs(
     C                             '/syspeddev/html/JSON.html':
     C                             '/CGITAG/')
     C                   callp     wrtsection('top')
      *
      * ............................................................................................
      * skriv JSON-Object start..(alltid '{' + x ant param. m/komma mellom (IKKE komma etter siste))
      * ............................................................................................
      *
      /free
        JSONstring='{'
        +'¬user¬ : ¬'+%trim(WSUSER)+'¬, '
        +'¬avd¬ : ¬'+%trim(%editc(AVDN:'Z'))+'¬, '
        +'¬opd¬ : ¬'+%trim(%editc(OPDN:'Z'))+'¬, '
        +'¬errMsg¬ : ¬'+%trim(Error)+'¬, ';
      /end-free
      * JSONfix escaper " i tekst,  for deretter å bytte ¬->"
     c                   call      'JSONFIX'
     c                   parm                    JSONstring
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')
      *
      * ............................................................................................
      * Skriv JSON-LISTE Start (en liste er EN parameter(fra [ til   )
      * ............................................................................................
     C                   eval      JSONstring ='"utskrift" : ['
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')
     c                   eval      JSONstring =']'
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')
      * ............................................................................................
      * Skriv JSON-string Avsluttning
      * ............................................................................................
     c                   eval      JSONstring ='}'
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')
      *
      * Quit
     C                   exsr      Exit


      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Lukk fil
     C                   CLOSE     BRIDF
N01  C  N50              CLOSE     SVEH

      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   MOVE      *ON           *INLR
     C                   return
     C                   endsr
      *
      *********************************************************
     C     Parse         Begsr
      *********************************************************
      * Get input
     C                   EVAL      WSUSER  = zhbgetvar('USER')
     C                   EVAL      AVD     = zhbgetvar('AVD')
     C                   eval      AVDN    = c2n2(AVD)
     C                   MOVE      AVDN          AVDA
     C                   EVAL      OPD     = zhbgetvar('OPD')
     C                   eval      OPDN    = c2n2(OPD)
     C                   MOVE      OPDN          OPDA
     c                   endsr
      *
      *********************************************************
     C     INIT          Begsr
      *********************************************************
     C                   OPEN      BRIDF
      * Test innlogging
     C     WSUSER        CHAIN     BRIDF                              50
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C                   call      BILIBL
     C                   end

n01  C  N50              OPEN      SVEH
     C   50              eval      Error = 'Invalid userID'
N01  c     SvehKey       KLIST
N01  c                   KFLD                    AVDN
N01  c                   KFLD                    OPDN

     c                   endsr
      *___________________________________________
      * Skriv ut                              SRTE
      *"""""""""""""""""""""""""""""""""""""""""""
     C     SRTE          BEGSR
S01  C*                  MOVE      'M'           CALLTYP
N01  C                   MOVE      'C'           CALLTYP                        Print OG arkiver
     C                   MOVE      AVDN          SVEHASYAV         4
     C                   MOVE      OPDN          SVEHASYOP         7
     C                   CALL      'SVE006R'
     C                   PARM                    SVEHASYAV
     C                   PARM                    SVEHASYOP
     C                   PARM                    CALLTYP           1
N01   * Skriv/arkiver også ZEM om vilkår er til stede record i SVZH (ingen record->SVZ002 hopper ut)
N01  C     SvehKey       CHAIN     SVEH
N01  C                   if        %found and SVEH_TUID <> *blanks
N01  C                   CALL      'SVZ002R'
N01  C                   PARM                    SVEH_TUID
N01  C                   PARM      'P'           CALLTYP           1
N01  C                   endif
     C                   ENDSR
