      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/TNOE000R2 MODULE(*LIBL/TNOE000R2 *LIBL/INCGI)
CRTPGM*        ACTGRP(*NEW) AUT(*USE)
      *
      *********************************************************************
      /copy SYSPEDS/qrpgsrcpy,hspecs
      /copy SYSPEDS/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FBRPARF    IF   E           K DISK    USROPN
     FFRISOK    IF   E           K DISK    USROPN
     FSAEH      IF   E           K DISK    USROPN
     FSAEHO     IF   E           K DISK    USROPN
     F                                     PREFIX(O2_)
     F                                     RENAME(SAEHR:SAEHRO2)
     FWRKINV    UF A E           K DISK    USROPN
     FARKLOGH4  IF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy SYSPEDS/qrpgsrcpy,prototypeb
      /copy SYSPEDS/qrpgsrcpy,usec
      /copy SYSPEDS/qrpgsrcpy,variables3
     ‚*  Prototype for 'JSONFIX'
     d jsonfix         pr                  extpgm('JSONFIX')
     d jsonstring_                         like(jsonstring)
     ‚*  Prototype for 'INCGI'
     d incgi           pr                  extproc('INCGI')
     d bistdl_                             like(bistdl)
     ‚*  Prototype for variabel program
     D Varpgm          Pr                  ExtPgm(BILIBL)

     d qcmdexc         Pr                  extpgm('QCMDEXC')
     d cmdline_                            like(cmdline)
     d cmdlen_                             like(cmdlen)

     d TNOE000R2A      Pr                  extpgm('TNOE000R2A')
     d UsrSpcname_                         like(UsrSpcname)
     d WinvNo_                             like(WinvNo)

     d TNOE000R2B      Pr                  extpgm('TNOE000R2B')
     d UsrSpcname_                         like(UsrSpcname)
     d WinvNo_                             like(WinvNo)

     D WSUSER          S             10
     D UsrSpcName      s             10
     D WinvNo          s             17
     D CmdLine         s            512
     D CmdLen          s             15  5
     D JSONstring      s          32000
     D Error           S            150
     D AntLest         S              9  0
     D Jn043           S              1
     d X               S             10  0
     d wspll           S              1
     d EPJN            S              1
     d pos             S              5U 0
     d wpabrid         s                   like(pabrid)
     d wpakunr         s                   like(pakunr)
     d wpasort         s                   like(pasort)
     d wsetdn          s                   like(o2_setdn)
      *get input-string
      /copy syspeds/qrpgsrcpy,prolog3

      /free
         exsr ParseSr;
         exsr init;

       gethtmlifs(
       '/syspeddev/html/JSON.html':
       '/CGITAG/');
       wrtsection('top');

       //...........................................................................................
       //skriv JSON-Object start (alltid '{' + x ant param. m/komma mellom (IKKE komma etter siste))
       //...........................................................................................

        JSONstring='{'
        +'¬user¬ : ¬'+%trim(WSUSER)+'¬, '
        +'¬usrspcname¬ : ¬'+%trim(USRSPCNAME)+'¬, '
        +'¬inv¬ : ¬'+%trim(WINVNO)+'¬, '
        +'¬errMsg¬ : ¬'+%trim(Error)+'¬, ';

       //JSONfix escaper " i tekst,  for deretter å bytte ¬->"

       jsonfix ( jsonstring );

       updhtmlvar2('JSONstring'
                  :%addr(jsonstring)
                  :%len(jsonstring));
       wrtsection('JSONlin');

       //...........................................................................................
       // Skriv JSON-LISTE Start (en liste er EN parameter(fra [ til   )
       //..........................................................................................
       jsonstring ='"orderList" : [';

       updhtmlvar2('JSONstring'
                  :%addr(jsonstring)
                  :%len(jsonstring));
       wrtsection('JSONlin');

       if error = *blanks;
         exsr SelSr;
           exsr LesSr;
       endif;
       // ..........................................................................................
       // Skriv JSON-Liste/Array  Avslutning
       // ..........................................................................................
       jsonstring =']';
       updhtmlvar2('JSONstring'
                  :%addr(jsonstring)
                  :%len(jsonstring));
       wrtsection('JSONlin');
       // ..........................................................................................
       // Skriv JSON-string Avsluttning
       // ..........................................................................................
       jsonstring ='}';
       updhtmlvar2('JSONstring'
                  :%addr(jsonstring)
                  :%len(jsonstring));
       wrtsection('JSONlin');
         exsr Exit;

       //-----------------------------------------------------------------
       // Select fra filer
       //-----------------------------------------------------------------
       begsr     SelSr;
           Setll usrSpcName Wrkinv;
           reade usrSpcName Wrkinv;
             dow not %eof(Wrkinv);
                 delete wrkinvr;
               reade usrSpcName Wrkinv;
             enddo;

           tnoe000r2a ( UsrSpcName : WinvNo );
           tnoe000r2b ( UsrSpcName : WinvNo );
       endsr;
       //-----------------------------------------------------------------
       // hent input
       //-----------------------------------------------------------------
       begsr     ParseSr;
         wsuser  = zhbgetvar('USER');
         usrspcname = zhbgetvar('USRSPCNAME');
         winvno  = zhbgetvar('inv');

        endsr;
        // __________________________________________________
        // Læs array                                    LESSR
        // """"""""""""""""""""""""""""""""""""""""""""""""""
           begsr LesSr;
           Setll usrSpcName Wrkinv;
           reade usrSpcName Wrkinv;
           dow not %eof(Wrkinv);

           chain (wrkavd: wrktdn) SAEH;

           chain (wrkavd: wrktdn: '*CR') FRISOK;
           if not %found;
              fssok = *blanks;
           endif;

        // Finnes omberegning ?

           wsetdn = wrktdn * -1;
           chain (wrkavd: wsetdn) SAEHO;
           if not %found;
              O2_SEST = *BLANKS;
              O2_SEMF = *BLANKS;
              O2_SETLL = *BLANKS;
              O2_SEDT = 0;
           else;
              if O2_SETLL = setll;
                O2_SETLL = *BLANKS;
              endif;
           endif;

        // Er e-post sendt ?
           chain (wrkavd: wrktdn) ARKLOGH4;
           if %found;
              epjn = 'E';
           else;
              epjn = *BLANKS;
           endif;

        // Videre hvis ikke noen feil !

           if error = *blanks;

        // Lægg ut Json stræng
           AntLest = AntLest + 1;

        // Skriv en JSON-Array-linje pr menypunkt - komma før hvert nytt element

       if AntLest=1;
          JSONstring=*blanks;
          else;
          JSONstring=', ';
          endif;
       JSONstring=%trim(JSONstring)+'{'
          +'¬avd¬ : ¬'+%trim(%editc(SEAVD:'Z'))+'¬, '
          +'¬opd¬ : ¬'+%trim(%editc(SETDN:'Z'))+'¬, '
          +'¬tst¬ : ¬'+%trim(SE0035)+'¬, '
          +'¬setll¬ : ¬'+%trim(SETLL)+'¬, '
          +'¬setle¬ : ¬'+%trim(SETLE)+'¬, '
          +'¬aart¬ : ¬'+%trim(SEDTY)+'¬, '
          +'¬sg¬ : ¬'+%trim(SESG)+'¬, '
          +'¬datum¬ : ¬'+%trim(%editc(SEDT:'Z'))+'¬, '
          +'¬sevkb¬ : ¬'+%trim(%editc(SEVKB:'4'))+'¬, '
          +'¬status¬ : ¬'+%trim(SEST)+'¬, '
          +'¬avsNavn¬ : ¬'+%trim(SENAK)+'¬, '
          +'¬motNavn¬ : ¬'+%trim(SENAS)+'¬, '
          +'¬segn¬ : ¬'+%trim(SEGN)+'¬, '
          +'¬semi¬ : ¬'+%trim(SEMI)+'¬, '
          +'¬h_xref¬ : ¬'+%trim(FSSOK)+'¬, '
          +'¬invno¬ : ¬'+%trim(wrksif)+'¬, '
          +'¬epjn¬ : ¬'+%trim(epjn)+'¬, '
          +'¬o2_sest¬ : ¬'+%trim(o2_sest)+'¬, '
          +'¬o2_semf¬ : ¬'+%trim(o2_semf)+'¬, '
          +'¬o2_setll¬ : ¬'+%trim(o2_setll)+'¬, '
          +'¬o2_sedt¬ : ¬'+%trim(%editc(o2_sedt:'Z'))+'¬}';
         jsonfix (JSONstring);
       updhtmlvar2('JSONstring'
                  :%addr(jsonstring)
                  :%len(jsonstring));
         wrtsection ('JSONlin');

        endif;
           reade usrSpcName Wrkinv;
        enddo;
       endsr;
       //********************************************************
       begsr init;
       //********************************************************
         open bridf;
         // Test innlogging
         chain wsuser bridf;

         if %found;
           if bilibl = *blanks;
             incgi ( bistdl );
           else;

             Monitor;
                   Varpgm();
                 On-Error;
                   //
                 EndMon;

           endif;
         else;

           error = 'Invalid userID';
         endif;
         open brparf;
         open frisok;
         open wrkinv;
         open saeh;
           cmdline = 'OVRDBF FILE(SAEHO) TOFILE(SAEH)';
           cmdlen = %len(%trim(cmdline));
           qcmdexc ( cmdline : cmdlen );
         open saeho;
         open arklogh4;

       endsr;
       //=====================================================================
       // Close output html and quit
       //=====================================================================
       begsr exit;
         // Lukk fil
         close bridf;
         close brparf;
         close frisok;
         close wrkinv;
         close saeh;
         close saeho;
         close arklogh4;

         wrtsection('*fini');
         *inlr = *on;
         return;
       endsr;
      /end-free
