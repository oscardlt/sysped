      *********************************************************************
      * PROGRAM BEHANDLER INPUT FRA SS221 OG VISER LISTE
      *
CRTPG+*  CRTPGM SYSPED/SS222 MODULE(*LIBL/SS222
CRTPGM*        *LIBL/CHECKUSR *LIBL/INCGI) ACTGRP(CGI) AUT(*USE)
      *
      *********************************************************************
      * MAIN PROGRAM FLOW
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
     FGSSORF    IF   E           K DISK    USROPN
     FGSSAKF    IF   E           K DISK    USROPN
     FGSSLGL03  IF   E           K DISK    USROPN
     FBRIDF     UF   E           K DISK    USROPN
     FCUNDL01   IF   E           K DISK    USROPN
     FFIRM      if   e           k disk    usropn
      *--------------------------------------------------------------------
      * Prototype defintions and standard system API error structure
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      *--------------------------------------------------------------------
      * Variables common to CGI programs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
     D IfsMultIndicators...
     d                 ds
     D  NoErrors                       n
     D  NameTooLong                    n
     D  NotAccessible                  n
     D  NoFilesUsable                  n
     D  DupSections                    n
     D  FileIsEmpty                    n
      * Variables received from the input string
     D OddEven         s             10
     D lng             s              2
     D BckGnd          s             10
     D UsrSpcName      s             10
     D WSUSER          S             10
     D WSNA            S             30
     D WSONO           S              7
     D BWSONO          S              7  0
     D PGM             C                   CONST('SYSPED/CHECKUSR')
      *
     D Lib             s             10
     D Fn              s             10
     D Mbr             s             10
      *--------------------------------------------------------------------
      * Prolog common to CGI programs
      * -Receive input from the remote browser
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prolog3
      *=====================================================================******
      * MAIN PROCESS LINE
      *=====================================================================******
      * Get program start time for calculating execution time
     C                   time                    timedata1
      * Parse input
     C                   exsr      Parse
      *
     C                   EXSR      INIT
     C                   CALLB     PGM
     C                   PARM                    BIBRID
     C                   PARM                    UsrSpcName
     C                   PARM                    UINN              1
     C                   IF        UINN = 'N'
     C                   GOTO      SLUTT
     C                   END
      * dato/tid siste vellykkede operasjon.
     C     WSUSER        CHAIN     BRIDF                              50
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVE      WDT           BIDTF
     C                   MOVE      WTM           BITIDF
     C                   UPDATE    BRIDFR
      *
      * Set output skeleton html member name
     C                   exsr      SetSkl
      * Read skeleton output html, etc.
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
      *------------------                                                   ******
      * Write HTML sections
      *
      * 1-Section /$top
     C                   exsr      SetTop
     C                   callp     wrtsection('top')
      * 3-Section /$tablestr
     C                   exsr      SetTabStr
     C                   callp     wrtsection('tablestr')
      * 4-Sections /$tablerow
      *
     C                   EXSR      HENTART
      *
      * 5-Section /$tableend
     C                   callp     wrtsection('tableend')
      *
      * 6-Section /$endhtml
     C     endhtml       tag
     C                   exsr      SetEnd
     C                   callp     wrtsection('endhtml')
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
     C     SLUTT         tag
     C                   EXSR      EXIT
     C                   return
      *=====================================================================******
      * Parse input
      *=====================================================================******
     C     Parse         begsr
     C                   eval      lng     = zhbgetvar('lng')
     C                   eval      BckGnd  = zhbgetvar('bckgnd')
     C                   EVAL      WSUSER  = zhbgetvar('USER')
     C                   EVAL      UsrSpcName  = zhbgetvar('UsrSpcName')
      *
     C                   eval      WSONO   = zhbgetvar('WSONO')
     C                   eval      BWSONO  = c2n2(WSONO)
     C                   eval      AKONO   = BWSONO
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$top
      *  (search criteria)
      *=====================================================================******
     C     SetTop        begsr
      * Repeat national language for the next invocation
     C                   callp     updhtmlvar('LNG':lng:
     C                             InitHTMLVars)
     C                   callp     updHTMLvar('ROWHEAD':RowHead)
     C                   callp     updHTMLvar('LogoImg':LogoImg)
      * Color for text, background, links, visited links, active links
     C                   callp     updhtmlvar('TXTCOLOR':'black')
     C                   callp     updhtmlvar('BOLD':' ')
     C                   callp     updhtmlvar('BGCOLOR':BIFARG)
     C                   callp     updhtmlvar('LINK':'0000FF')
     C                   callp     updhtmlvar('VLINK':'FF0000')
     C                   callp     updhtmlvar('ALINK':'00FF00')
      * KUNDE
     C     CUNDL01K      CHAIN     CUNDL01                            33
     C                   callp     updhtmlvar('KUNDE':
     C                             %trim(%editc(KUNDNR:'Z')))
     C                   callp     updhtmlvar('KNAVN':KNAVN)
OddEvC                   callp     updHTMLvar('ROWHEAD':RowHead)
OddEvC                   callp     updHTMLvar('LogoImg':LogoImg)
BODY C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updhtmlvar('USER':WSUSER)
     C                   callp     updHtmlVar('UsrSpcName':UsrSpcName)
      *
     C                   callp     updhtmlvar('WSONO':
     C                             %trim(%editc(AKONO:'Z')))
      *
     C                   callp     updHTMLvar('sdato':
     C                             %trim(%editw(BIDTV:'    .  .  ')))
     C                   callp     updHTMLvar('stid':
     C                             %trim(%editw(BITIDV:'  .  .  ')))
      *
     C                   endsr
      *
      *=====================================================================******
      *  Set output variables for section /$tablestr (table start)
      *=====================================================================******
     C     SetTabStr     begsr
      *
     C                   endsr
      *
      *=====================================================================******
      *  Set output variables for section /$tablerow (table row)
      *=====================================================================******
     C     SetTabRow     begsr
      *
     C                   callp     updHTMLvar('ODDEVEN':OddEven)
     C                   callp     updhtmlvar('AKLNO':
     C                             %trim(%editc(AKLNO:'Z')))
     C                   callp     updhtmlvar('AKANR':AKANR)
     C                   callp     updhtmlvar('AKCONO':AKCONO)
     C                   callp     updhtmlvar('AKSTK':
     C                             %trim(%editc(AKSTK:'Z')))
     C                   callp     updhtmlvar('AKSTKM':
     C                             %trim(%editc(AKSTKM:'Z')))
     C                   callp     updhtmlvar('AKVAL':AKVAL)
     C                   callp     updhtmlvar('AKPR':
     C                             %trim(%editc(AKPR:'4')))
     C                   callp     updhtmlvar('AKTXT':AKTXT)
     C     GSSLGL03K     SETGT     GSSLGL03
     C     GSSLGL03K     READPE    GSSLGL03                               33
     C                   IF        *IN33
     C                   callp     updhtmlvar('LGHDT':'')
     C                   ELSE
     C                   callp     updhtmlvar('LGHDT':
     C                             %trim(%editw(LGHDT:'    .  .  ')))
     C                   END
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$endhtml
      *=====================================================================******
     C     SetEnd        begsr
      * Compute run time
     C                   time                    timedata2
     C     timedata2     subdur    timedata1     ms:*ms
     C                   eval      sec = ms / 1000000
     C                   callp     updhtmlvar('runtime':
     C                             %trim(%editw(sec:'     0 .   ')))
      *
     C                   endsr
      *=====================================================================******
      * Set html output skeleton member before its read into memory
      *=====================================================================******
     C     SetSkl        begsr
      *------------------
      * Set html output skeleton member
cccccC                   eval      IfsMultIndicators = gethtmlifsmult(
cccccC                             '/syspeddev/html/Header/eSpedTop.html -
cccccC                             /syspeddev/html/SS222.html -
cccccC                             /syspeddev/html/Footer/eSpedBot.html':
cccccC                             '/CGITAG/')
      *
     C                   endsr
      *=====================================================================******
      * HENT ORDRE
      *=====================================================================******
     C     HENTART       BEGSR
      *
     C     BWSONO        CHAIN     GSSORF                             50
     C     BWSONO        SETLL     GSSAKF
     C     BWSONO        READE     GSSAKF                                 50
      *
     C                   DOW       *IN50 = *OFF
     C                   exsr      SetTabRow
     C                   callp     wrtsection('tablerow')
     C*                  IF        ORST = 'P'
     C*                  callp     wrtsection('tabrowconf2')
     C*                  ELSE
     C                   callp     wrtsection('tabrowconf1')
     C*                  END
     C                   callp     wrtsection('tabrowconf3')
     C     BWSONO        READE     GSSAKF                                 50
     C                   ENDDO                                                  *hival
      *
     C                   ENDSR
      *
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
     C                   OPEN      BRIDF
     C     WSUSER        CHAIN(N)  BRIDF                              50
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
OddEv * hent Parametre som går igjen i mange prog:
OddEv *      Odd/Even/Rowhead-farger,Logobilde,Språk,Intern/Ekstern bruker,  mm
OddEvc                   call      'ESGETPAR'
OddEvc                   parm                    BIBRID
OddEvc                   parm                    BIFIRM
OddEvc                   parm                    BIKUNR
OddEvc                   parm                    BIKNG
OddEvc                   parm                    RowHead          10
OddEvc                   parm                    Odd              10
OddEvc                   parm                    Even             10
OddEvc                   parm                    LogoImg          50
OddEvc                   parm                    XSPRÅK            2
OddEvc                   parm                    XINTERN           1
OddEvc                   parm                    ParmDS1        1024
OddEvc                   parm                    ParmDS2        1024
OddEvc                   parm                    ParmDS3        1024
OddEv *
     C                   open      GSSORF
     C                   open      GSSAKF
     C                   open      GSSLGL03
     C                   OPEN      CUNDL01
     C                   OPEN      FIRM
     C     CUNDL01K      klist
     C                   kfld                    BIFIRM
     C                   kfld                    BIKUNR
     C     GSSLGL03K     klist
     C                   kfld                    LGFTYP
     C                   kfld                    AKONO
     C                   kfld                    AKLNO
      *
     C                   EVAL      LGFTYP = '*CONF'
      *
     C                   endsr
      *=====================================================================******
      *  AVSLUTT
      *=====================================================================******
     C     EXIT          begsr
     C                   close     BRIDF
     C                   close     GSSORF
     C                   close     GSSAKF
     C                   close     GSSLGL03
     C                   close     CUNDL01
     C                   CLOSE     FIRM
     C                   eval      *inlr = *on
      *
     C                   endsr
      *
