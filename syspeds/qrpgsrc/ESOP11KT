bhrTM * BHRTM NÂr transport¯r har bekreftet O kan han IKKE derettter avvise
bhrTM * BHRTM aktiver disse linjer hvis avisning skal  vÊre tillatt ogsÂ etter bekrevt
      *********************************************************************
      * MERKE OPPDRAG KLAR FOR HENTING
      *********************************************************************
CRTPG+*  CRTPGM SYSPED/ESOP11KT MODULE(*LIBL/ESOP11KT
CRTPGM*        *LIBL/INCGI) ACTGRP(CGI) AUT(*USE)
      *********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 JOV PTF10 store omskrivinger Ramberg tatt inn i standard
********************************************************************
      *
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
     FBRIDF     IF   E           K DISK    USROPN
     FHEADF     IF   E           K DISK    USROPN
     FTRAN      IF   E           K DISK    USROPN
     FTRACKl02  UF A E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Prototype definitions and standard system API error structure
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      * Variables common to CGI programs
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
      * VARIABLES SPECIFIC TO THIS PROGRAM
      *--------------------------------------------------------------------
      * Variables received from the input string
     D lng             s              2
     D UsrSpcName      s             10

     D WSUSER          S             10
     D MailAdr         S             70
     D*MerkTxt         S             70
     D MerkTxt         S            170
nxx  D feilTxt         S             70
     D AVD             S              4
     D OPD             S              7
     D TILDTran        S              8  0
     D WStra           S              8
     D WStran          S              8  0
     D Funk            S              1
nxx  D WSSMS           S             15
     D BEKRAVVIS       S            500
     D Lib             s             10
     D Fn              s             10
     D Mbr             s             10
     D ItemCount       s             10i 0
     D i               s             10i 0
     D                 DS
     D  WDs≈MD                 1      8  0
     D  WDs≈MD≈                1      4  0
     D  WDs≈MDM                5      6  0
     D  WDs≈MDD                7      8  0
     D                 DS
     D  WDsDM≈                 1      8  0
     D  WDsDM≈D                1      2  0
     D  WDsDM≈M                3      4  0
     D  WDsDM≈≈                5      8  0
nxx  D                 DS
nxx  D  WSETD                  1      6
nxx  D   WSETDD                1      2
nxx  D   WSETDM                3      4
nxx  D   WSETD≈                5      6
nxx  D   WSETD1_1              1      1
nxx  D   WSETD2_5              2      5
      *
      /copy syspeds/qrpgsrcpy,prolog3
      *=====================================================================
      * MAIN PROCESSING LINE
      *=====================================================================
      * Parse input string
     C                   exsr      Parse
      * Initier
     C                   EXSR      INIT
      * Oppdater/vis bekrefting
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC'
     C                   eval      Mbr = 'ESOP11KT'
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
     C                   EXSR      HOVED
     C                   callp     wrtsection('all')
      * Send output buffer and quit
     C     SLUTT         TAG
     C                   exsr      Exit
      *********************************************************************
     C     Hoved         begsr
      *********************************************************************
nxx  C                   eval      feilTxt = *blanks
     C                   eval      merkTxt = *blanks
     C     HEADFK        chain     HEADF                              33
     c     *in33         cabeq     '1'           utMerk
     C     TraKey        chain     Tran                               33
      * NÂr ble den tildelt
     C                   MOVE      'CAT'         TTACTI
     C     TT02max       setgt     TRACKL02
     C     TT02KEY       readpe(N) TRACKL02                               33
     c     *in33         ifeq      '0'
     c                   move      TTDATE        TILDDT            8 0
     c                   movel     TTTIME        TILDTI4           4 0
     c                   endif
      *  ER den alt bekreftet -  CCT Collection Confirmed Transporter
      * (kunne ha leseloop for Â se at det er rette transport¯r.... - skal bare vÊre 1)
     C                   MOVE      'CCT'         TTACTI
     C     TT02max       setgt     TRACKL02
     C     TT02KEY       readpe(N) TRACKL02                               34
     c     *in34         ifeq      '0'
     c                   move      TTDATE        BEKRDT            8 0
     c                   movel     TTTIME        BEKRTI4           4 0
     c                   eval      TILDTran = c2n2(TTDEPO)
     c                   eval      MerkTxt = 'Transport¯r '
     c                             +%trim(%editc(TILDTran:'Z'))
     c                             +' har bekreftet tildelingen.'
     c                   movel     TTTEXT        BEKRTEXT         70
     c                   else
     c                   eval      MerkTxt = 'Trykk pÂ OK-knappen '
     c                             +'for Â bekrefte at du tar oppdraget, '
     c                             +'</br>eller AVVIS for Â IKKE ta det.'
     c*                            +'for Â bekrefte at du tar henteoppdraget, '
     c*                            +'eller AVVIS for Â IKKE ta det.'
     c                   endif
      * Siste transport¯rSms
     C                   MOVE      'CMS'         TTACTI
     C     TT02max       setgt     TRACKL02
     C     TT02KEY       readpe(N) TRACKL02                               33
     c     *in33         ifeq      '0'
     c                   move      TTDATE        SMSDT             8 0
     c                   movel     TTTIME        SMSTI4            4 0
     c                   move      SMSDT         WDs≈MD
     c                   move      WDs≈MD≈       WDsDM≈≈
     c                   move      WDs≈MDM       WDsDM≈M
     c                   move      WDs≈MDD       WDsDM≈D
     c                   move      *blanks       SMSTEXT         100
     c                   eval      SMStext=%trim(%editw(WDsDM≈:'  .  .    '))
     c                             + ' / ' + %trim(%editw(SMSTI4:'  :  '))
     c                             + '  ' + TTTEXT
     c                   endif
      *
      * Bekreft tildeling
     C     FUNK          IFEQ      'B'
nxx  c                   exsr      TstBekr
nxx  c                   if        feilTxt = *blanks
     c                   exsr      Bekreft
xxx  C                   move      *blanks       WSETD
xxx  C                   move      *blanks       WSSMS
nxx  c                   endif
     c                   endif
      * Annuller tildeling
     C     FUNK          IFEQ      'A'
     c                   exsr      Annuller
     c                   endif
      *
      *
     c     UtMerk        tag
     C                   callp     updHtmlVar('USER':WSUSER)
     C                   callp     updHtmlVar('MAILADR':MAILADR)
     C                   callp     updhtmlvar('WSTRA':
     C                             %trim(%editc(WSTRAN:'Z')))
     C                   callp     updHtmlVar('HERFA':HERFA)
     C                   callp     updhtmlvar('HEAVD':
     C                             %trim(%editc(HEAVD:'Z')))
     C                   callp     updhtmlvar('HEOPD':
     C                             %trim(%editc(HEOPD:'Z')))
     C                   callp     updHtmlVar('SendNr':%subst(HXSNDN:4:17))
     C                   callp     updhtmlvar('HENT':
     C                             %trim(%editc(HENT:'Z')))
     C                   callp     updHtmlVar('HEVS1':HEVS1)
     C                   callp     updhtmlvar('HEVKT':
     C                             %trim(%editc(HEVKT:'Z')))
     C                   callp     updHtmlVar('MerkTxt':MerkTxt)
     C                   callp     updHtmlVar('feilTxt':feilTxt)
nxx   *
nxx  C*                  callp     updhtmlvar('WSETD':WSETD)
nxx  C                   callp     updhtmlvar('WSSMS':WSSMS)
      * snu dato ≈.M.dag
     c                   move      TILDDT        WDs≈MD
     c                   move      WDs≈MD≈       WDsDM≈≈
     c                   move      WDs≈MDM       WDsDM≈M
     c                   move      WDs≈MDD       WDsDM≈D
     C                   callp     updHTMLvar('TILDDT':
     C                             %trim(%editw(WDsDM≈:'  .  .    ')))
     C                   callp     updHTMLvar('TILDTI4':
     C                             %trim(%editw(TILDTI4:'  :  ')))
     c                   move      BEKRDT        WDs≈MD
     c                   move      WDs≈MD≈       WDsDM≈≈
     c                   move      WDs≈MDM       WDsDM≈M
     c                   move      WDs≈MDD       WDsDM≈D
     C                   callp     updHTMLvar('BEKRDT':
     C                             %trim(%editw(WDsDM≈:'  .  .    ')))
     C                   callp     updHTMLvar('BEKRTI4':
     C                             %trim(%editw(BEKRTI4:'  :  ')))
     C                   callp     updHtmlVar('BEKRTEXT':BEKRTEXT)
     C                   callp     updHtmlVar('SMSTEXT':SMSTEXT)
      *
     c                   move      *blanks       BEKRAVVIS
      * dersom den IKKE er bekreftet sÂ kan en bekrefte ELLER Avvise   og M≈ angi ETD-dato
     c     BEKRDT        ifeq      *zeros
     c                   move      *blanks       ETDDT           100
     c                   eval      ETDDT='<input type="text" name="WSETD"'
     c                             +' value="'+%trim(WSETD)+
     c                             '" size="6" maxlength="6">'
     c                             +'&nbsp;&nbsp;(DD el DDMM el DDMM≈≈)'
     c                   eval      BEKRAVVIS='<INPUT TYPE="button" '
     c                             +'VALUE="OK = Bekreft" '
     c                             +'NAME="Bekr" onClick="Bekreft()">'
     c                   else
     c     'ETD:'        scan      BEKRTEXT      pos               2 0
     c                   eval      ETDDT='<b>'+%subst(BEKRTEXT:pos+4:2)+'.'
     c                             +%subst(BEKRTEXT:pos+6:2)+'.20'
     c                             +%subst(BEKRTEXT:pos+8:2)+'</b>'
     c                   eval      BEKRAVVIS='<INPUT TYPE="button" '
     c                             +'VALUE="OK=Ny hente-SMS" '
     c                             +'NAME="Bekr" onClick="Bekreft()">'
     c                   end
      * kan alltid avvise (enten f¯rste gang -eller ombestemme seg etter Â ha bekreftet)
BHRTMc     BEKRDT        ifeq      *zeros
     c                   eval      BEKRAVVIS=%trim(BEKRAVVIS)+'&nbsp;&nbsp;'
     c                             +'<INPUT TYPE="button" '
     c                             +'VALUE="-- Avvis/Annuller --" '
     c                             +'NAME="Avv" onClick="Avvis()">'
BHRTMc                   end
      *
     C                   callp     updHtmlVar('ETDDT':ETDDT)                    FORV.DT INP EL OUTP.
     C                   callp     updHtmlVar('BEKRAVVIS':BEKRAVVIS)
      *
     C                   endsr
      *
      *********************************************************************
     C     TstBekr       begsr
      *********************************************************************
      *
     c                   IF        BEKRDT = *zeros
     C                   IF        WSETD = *Blanks
     C                   EVAL      feilTxt = 'Forventet hentedato mÂ angis.'
     C                   ELSE
     C                   MOVE      WSETD≈        U≈≈               2 0
     C                   MOVE      WSETDM        UMM               2 0
     C                   MOVE      WSETDD        UDD               2 0
     C                   CALL      'TESTDATO'
     C                   PARM                    UDD
     C                   PARM                    UMM
     C                   PARM                    U≈≈
     C                   PARM                    UFLAGG            1 0
     C     UFLAGG        COMP      1                                      96
     C                   IF        UFLAGG = 1
     C                   EVAL      feilTxt = 'Forventet hentedato er feil.'
     C                                     +' Oppgi pÂ format ddmmÂÂ.'
     C                   ENDIF
     C                   ENDIF
     c                   ELSE
      * Bekreft selv om den er bekreftet ....  = send nytt smsnr
     C                   IF        WSSMS = *Blanks
     C                   EVAL      feilTxt = 'Mobilnr for ny hente-SMS '
     C                                     +'mÂ oppgis'
     c                   ENDIF
     c                   ENDIF
     C                   endsr
      *
      *********************************************************************
     C     Bekreft       begsr
      *********************************************************************
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
      * DERSOM KALL MED BLANK I DATO S≈ ER DET FOR ≈ SEND NY SMS...
     C                   IF        WSETD <> *BLANKS
     C                   MOVE      HEAVD         TTAVD
     C                   MOVE      HEOPD         TTOPD
     C                   MOVE      WDT           TTDATE
     C                   MOVE      WTM           TTTIME
     C                   MOVE      WDT           TTDATL
     C                   MOVE      WTM           TTTIML
     C                   MOVE      'CCT'         TTACTI
     C                   MOVE      WSUSER        TTUSER
      * NÂr bekreftelse allerede finnes - oppdater kun SMSnr
     C                   EVAL      TTTEXT ='Coll.order conf. by '
     c                             +'transp ' +%trim(%editc(WSTRAN:'Z'))
     c                             +' '+%trim(%subst(VMNAVN:1:10))
     c                             +' ETD:'+WSETD
     c                   if        WSSMS<>*blanks
     C                   EVAL      TTTEXT = %trim(TTTEXT)+' SMS to:'+WSSMS
     c                   endif
     c                   move      *blanks       TTTEXL           70
     C                   EVAL      TTTEXL ='Henteoppd. bekr. av '
     c                             +'transp ' +%trim(%editc(WSTRAN:'Z'))
     c                             +' '+%trim(%subst(VMNAVN:1:10))
     c                             +' ETD:'+WSETD
     c                   if        WSSMS<>*blanks
     C                   EVAL      TTTEXL = %trim(TTTEXL)+' SMS til:'+WSSMS
     c                   endif
     C                   MOVEL     WSTRAN        TTDEPO
     C                   MOVE      'S'           TTMANU
     C                   write     TRACKFR
      * (ny) CCT er lagt til - opdater HTML-variable f¯r ny visning..
     c                   move      TTDATE        BEKRDT            8 0
     c                   movel     TTTIME        BEKRTI4           4 0
     c                   eval      MerkTxt = 'Transport¯r '
     c                             +%trim(%editc(TILDTran:'Z'))
     c                             +' har bekreftet tildelingen.'
     c                   movel     TTTEXT        BEKRTEXT         70
     c                   move      BEKRDT        WDs≈MD
     c                   move      WDs≈MD≈       WDsDM≈≈
     c                   move      WDs≈MDM       WDsDM≈M
     c                   move      WDs≈MDD       WDsDM≈D
     C                   callp     updHTMLvar('BEKRDT':
     C                             %trim(%editw(WDsDM≈:'  .  .    ')))
     C                   callp     updHTMLvar('BEKRTI4':
     C                             %trim(%editw(BEKRTI4:'  :  ')))
     C                   callp     updHtmlVar('BEKRTEXT':BEKRTEXT)
     C                   ENDIF
      *
     C     WSSMS         ifne      *blanks
     c                   move      'COL'         H_L
     C                   move      TTAVD         HentAvd
     C                   move      TTOPD         HentOpd
     c                   call      'ESOP11HS'
     c                   parm                    HentAvd           4
     c                   parm                    HentOpd           7
     c                   parm                    H_L               3
     c                   parm                    WSSMS
     c                   parm                    WSUSER           10
      * (ny) CMS er lagt til - opdater HTML-variable f¯r ny visning..
     C                   MOVE      'CMS'         TTACTI
     C     TT02max       setgt     TRACKL02
     C     TT02KEY       readpe(N) TRACKL02                               33
     c     *in33         ifeq      '0'
     c                   move      TTDATE        SMSDT             8 0
     c                   movel     TTTIME        SMSTI4            4 0
     c                   move      SMSDT         WDs≈MD
     c                   move      WDs≈MD≈       WDsDM≈≈
     c                   move      WDs≈MDM       WDsDM≈M
     c                   move      WDs≈MDD       WDsDM≈D
     c                   move      *blanks       SMSTEXT         100
     c                   eval      SMStext=%trim(%editw(WDsDM≈:'  .  .    '))
     c                             + ' / ' + %trim(%editw(SMSTI4:'  :  '))
     c                             + '  ' + TTTEXT
     c                   endif
     C                   callp     updHtmlVar('SMSTEXT':SMSTEXT)
     c                   endif
     C                   endsr
      *
      *********************************************************************
     C     Annuller      begsr
      *********************************************************************
      * slette/avvise tildeling som transport¯r.. - sett N i siste av TTACTI for Â avvvise
     C                   MOVE      'CAT'         TTACTI
     C     TT02max       setgt     TRACKL02
     C     TT02KEY       readpe    TRACKL02                               33
     c     *in33         ifeq      '0'
     c                   move      'N'           TTACTI
     c                   update    TRACKFR
      * legg ut egen loggpost som dikumentere DEL-en
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
      *
     C                   MOVE      HEAVD         TTAVD
     C                   MOVE      HEOPD         TTOPD
     C                   MOVE      WDT           TTDATE
     C                   MOVE      WTM           TTTIME
     C                   MOVE      WDT           TTDATL
     C                   MOVE      WTM           TTTIML
     C                   MOVE      'CAN'         TTACTI
     C                   MOVE      WSUSER        TTUSER
     C                   EVAL      TTTEXT ='Assigment canselled by '
     c                             +'transporter '
     c                             +%triml(%editc(WSTRAN:'Z'))+' '+VMNAVN
     C                   EVAL      TTTEXL ='Tildeling kansellert av '
     c                             +'transport¯r '
     c                             +%triml(%editc(WSTRAN:'Z'))+' '+VMNAVN
     C                   MOVEL     WSTRAN        TTDEPO
     C                   MOVE      'S'           TTMANU
     C                   write     TRACKFR
     c                   endif
      * reverser ogsÂ evt CCT (conformet tildeling..)
     C                   MOVE      'CCT'         TTACTI
     C     TT02max       setgt     TRACKL02
     C     TT02KEY       readpe    TRACKL02                               33
     c     *in33         ifeq      '0'
     c                   move      'N'           TTACTI
     c                   update    TRACKFR
     c                   endif
     C                   endsr
      *
      *
      *********************************************************************
     C     Exit          begsr
      *********************************************************************
     C                   callp     wrtsection('*fini')
     C                   CLOSE     BRIDF
     C                   CLOSE     HEADF
     C                   CLOSE     TRAN
     C                   CLOSE     TRACKL02
     C                   eval      *inlr = *on
     C                   return
     C                   endsr
      *=====================================================================
      * Parse input string
      *=====================================================================
     C     Parse         begsr

     C                   eval      WSUSER  = zhbgetvar('USER')
     C                   eval      AVD     = zhbgetvar('HEAVD')
     C                   eval      HEAVD   = c2n2(AVD)
     C                   eval      OPD     = zhbgetvar('HEOPD')
     C                   eval      HEOPD   = c2n2(OPD)
     C                   eval      WSTRA   = zhbgetvar('WSTRA')
     C                   eval      WSTRAN  = c2n2(WSTRA)
     C                   eval      MailAdr = zhbgetvar('MailAdr')
     C                   eval      FUNK    = zhbgetvar('FUNK')
     C                   eval      WSETD   = zhbgetvar('WSETD')
     c                   if        WSETD <> *blanks
     c                   if        WSETD2_5=*blanks
     c                   eval      WSETDD  ='0'+WSETD1_1
     c                   endif
     c                   if        WSETDM  =*blanks
     c                   move      UMONTH        WSETDM
     c                   endif
     c                   if        WSETD≈  =*blanks
     c                   move      UYEAR         WSETD≈
     c                   endif
     c                   endif
     C                   eval      WSSMS   = zhbgetvar('WSSMS')

     C                   endsr
      *=====================================================================
      *  Initier
      *=====================================================================
     C     INIT          begsr
      * Get program start time for calculating execution time
     C                   time                    timedata1
      *
     C                   OPEN      BRIDF
     C     WSUSER        CHAIN     BRIDF                              50
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   CALLB     'INCGI'
     C                   PARM                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt pÂ user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
      *
     C                   open      HEADF                                50
     C                   open      TRAN                                 50
     C                   open      TRACKL02                             50
      *
     c     HEADFK        klist
     c                   kfld                    HEAVD
     c                   kfld                    HEOPD
     c     TraKey        klist
     c                   kfld                    BIFIRM
     c                   kfld                    WStran
     C     TT02MAX       KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C                   KFLD                    TTFBNR
     C                   KFLD                    TTACTI
     C                   KFLD                    Maxdate
     C                   move      99999999      Maxdate           8 0
     C     TT02KEY       KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C                   KFLD                    TTFBNR
     C                   KFLD                    TTACTI
      *
     C                   endsr
      *
