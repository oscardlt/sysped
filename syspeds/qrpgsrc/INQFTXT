      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/INQFTXT MODULE(*LIBL/INQFTXT *LIBL/INCGI)
CRTPGM*         ACTGRP(CGI) AUT(*USE)
      *
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
ODDEV *ODDEV   PTF9 ODDEVEN-logikk
P9XXX * 02 YBC PTF9 WSNVN  : Variable navn som mottar verdi
      *             WSFORMS: Formnavn som mottar verdi
      *             WSREFR : J/Y betyr program for mottak av verdi skal refreshes
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
N01  FBRPARF    if   e           k disk    usropn
     FKODTFT    IF   E           K DISK    USROPN
     FKODTFTLF  IF   E           K DISK    USROPN
     F                                     RENAME(KODTFTR:KODTFTRL)
      *********************************************************************
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      /copy syspeds/qrpgsrcpy,variables3
      *
OddEvD OddEven         s             10
      * Client input variables
     D WSNVN           s             10
     D WSFTKD          s              2
     D SOKFTKD         s              2
     D SOKFTXT         s             51
     D SOKFTXT2        s             51
     D USER            s             10
N02  D WSFORMS         s             20
N02  D WSREFR          s              1
N02  D REFRESH         s             50
     D MaxV            s              5
     D MaxVis          s              5  0
      * Variables used to load the external html source member
     D Lib             s             10
     D Fn              s             10
     D Mbr             s             10
      * Other variables
      *
      *
      /copy syspeds/qrpgsrcpy,prolog3
      *
      * Get program start time for calculating execution time
     C                   time                    timedata1
      * Parse input / cvt to numeric
     C                   exsr      Parse
      * File open / keys
     C                   EXSR      INIT
      * Read output skeleton html member into memory
     C                   exsr      LoadHtml
      * Set section variables
     C                   exsr      SetTop
      * Send section top
     C                   callp     wrtsection('top')
      * Find/send lines
     C                   exsr      LINES
      * Send section bot
      *
     C                   callp     wrtsection('bot')
      * Quit
     C                   exsr      Exit
      *
     C                   eval      *inlr = *on
     C                   return
      *
      *
      *=====================================================================
      * Parse input / cvt to numeric
      *=====================================================================
     C     Parse         begsr
      * Parse variables from QUERY_STRING environment variable
     C                   eval      WSNVN   = zhbgetvar('WSNVN')
N02  C                   IF        WSNVN   = *BLANKS
N02  C                   EVAL      WSNVN   = 'wsftkd'
N02  C                   END
     C                   eval      WSFTKD  = zhbgetvar('WSFTKD')
     C                   IF        WSFTKD  = *BLANKS
     C                   eval      SOKFTKD = zhbgetvar('SOKFTKD')
     C                   ELSE
     C                   eval      SOKFTKD = WSFTKD
     C                   END
     C                   eval      SOKFTKD = uppify(SOKFTKD)
     C                   eval      SOKFTXT = zhbgetvar('SOKFTXT')
     C                   eval      SOKFTXT = uppify(SOKFTXT)
N02  C                   eval      WSFORMS = zhbgetvar('WSFORMS')
N02  C                   IF        WSFORMS = *BLANKS
N02  C                   EVAL      WSFORMS = 'forms[0]'
N02  C                   END
N02  C                   eval      WSREFR  = zhbgetvar('WSREFR')
N02  C                   IF        ((((WSREFR = 'J') OR
N02  C                                (WSREFR = 'j')) OR
N02  C                                (WSREFR = 'Y')) OR
N02  C                                (WSREFR = 'y'))
N02  C                   eval      REFRESH = 'self.opener.document.'
N02  C                             + %trim(WSFORMS)
N02  C                             + '.submit();'
N02  C                   END
     C                   eval      MaxV    = zhbgetvar('MaxV')
     C                   eval      MaxVis  = c2n2(MaxV)
     c     MaxVis        ifeq      *zeros
     c                   z-add     50            MaxVis
     c                   end
      * Userid.....
     C                   eval      USER = zhbgetvar('USER')
     C                   endsr
      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   CLOSE     BRIDF
N01  C                   close     BRPARF
     C                   CLOSE     KODTFT
     C                   CLOSE     KODTFTLF
     C                   return
     C                   endsr
      *=====================================================================
      * Read output skeleton html member into memory
      *=====================================================================
     C     LoadHtml      begsr
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC'
     C                   eval      Mbr = 'INQFTXT'
     C                   CAT       XSPRÅK:0      MBR
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
     C                   endsr
      *=====================================================================
      *  Set variable data for section /$top
      *=====================================================================
     C     SetTop        begsr
      *
     C* klargjøre HTMLvariable
OddEvC                   callp     updHTMLvar('ROWHEAD':RowHead)
OddEvC                   callp     updHTMLvar('LogoImg':LogoImg)
      *
BODY C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updHTMLvar('USER':USER)
     C                   callp     updHTMLvar('WSNVN':WSNVN)
N02  C                   callp     updHTMLvar('wsforms':WSFORMS)
N02  C                   callp     updHTMLvar('wsrefr':WSREFR)
N02  C                   callp     updHTMLvar('refresh':REFRESH)
      *
     C                   endsr
      *
      *=====================================================================
      * Fine lins - send to browser
      *=====================================================================
     C     Lines         begsr
     c                   eval      SOKFTXT2  = SOKFTXT
     c                   Move      *zeros        AntVist           5 0
     C     SOKFTKD       IFEQ      *BLANKS
     C     SOKFTXT       ANDEQ     *BLANKS
     C                   GOTO      UTLES
     C                   END
      *
     C     SOKFTKD       IFNE      *BLANKS
     C     SOKFTKD       SETLL     KODTFT
     C                   READ      KODTFT                                 33
     C                   ELSE
     C     SOKFTXT       SETLL     KODTFTLF
     C                   READ      KODTFTLF                               33
     C                   END
     C     *in33         DOWEQ     *OFF
     C                   exsr      exclude
     c     EXCL          IFEQ      'N'
     C                   callp     updHTMLvar('KFTTYP':KFTTYP)
     C                   callp     updHTMLvar('KFTFTX':KFTFTX)
OddEvc     OddEven       IFEQ      ODD
OddEvc                   eval      OddEven = EVEN
OddEvc                   else
OddEvc                   eval      OddEven = ODD
OddEvc                   end
OddEvC                   callp     updHTMLvar('ODDEVEN':OddEven)
OddEv *
     C                   callp     wrtsection('row')
     c                   Add       1             AntVist           5 0
     c     AntVist       cabge     MaxVis        UtLes
     C                   END
     C     SOKFTKD       IFNE      *BLANKS
     C                   READ      KODTFT                                 33
     C                   ELSE
     C                   READ      KODTFTLF                               33
     C                   END
     C                   ENDDO
      *
     C     UTLES         TAG
      *
      * Compute run time
     C                   time                    timedata2
     C     timedata2     subdur    timedata1     ms:*ms
     C                   eval      sec = ms / 1000000
     C                   callp     updhtmlvar('runtime':
     C                             %trim(%editw(sec:'     0 .   ')))
      *
     C                   endsr
      *=====================================================================******
      *  Eksluder post basert på...
      *=====================================================================******
     C     EXCLUDE       begsr
     C                   IF        KFTLN = 1
     C                   Move      'N'           EXCL              1
     C                   ELSE
     C                   Move      'J'           EXCL              1
     C                   END
      *
     C                   endsr
      *
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
     C                   OPEN      BRIDF
     C     USER          CHAIN     BRIDF                              50
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
      *
OddEv * hent Parametre som går igjen i mange prog:
OddEv *      Odd/Even/Rowhead-farger,Logobilde,Språk,Intern/Ekstern bruker,  mm
OddEvc                   call      'ESGETPAR'
OddEvc                   parm                    BIBRID
OddEvc                   parm                    BIFIRM
OddEvc                   parm                    BIKUNR
OddEvc                   parm                    BIKNG
OddEvc                   parm                    RowHead          10
OddEvc                   parm                    Odd              10
OddEvc                   parm                    Even             10
OddEvc                   parm                    LogoImg          50
OddEvc                   parm                    XSPRÅK            2
OddEvc                   parm                    XINTERN           1
OddEvc                   parm                    ParmDS1        1024
OddEvc                   parm                    ParmDS2        1024
OddEvc                   parm                    ParmDS3        1024
      *
     C                   OPEN      KODTFT
     C                   OPEN      KODTFTLF
     C                   open      BRPARF                               50
      *
     C     BRPARFK       klist
     C                   kfld                    PABRID
     C                   kfld                    PAKUNR
     C                   kfld                    PAID
      *
     C                   EVAL      PABRID = USER
     C                   EVAL      PAKUNR = 0
      *
     C                   MOVEL     'SPRÅK'       PAID
     C     BRPARFK       chain     BRPARF                             33
     C                   IF        (*IN33 OR (PAVRDA <> 'EN'))
     C                   MOVE      *BLANKS       XSPRÅK            2
     C                   ELSE
     C                   MOVE      'EN'          XSPRÅK            2
     C                   END
      *
     C                   endsr
