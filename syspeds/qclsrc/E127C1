             PGM
             DCLF       FILE(E127C) RCDFMT(QWHFDML)

 /* ALOCKERE OBJEKT E125F2 FOR Å HINDRE ANDRE BRUKER DET, */
 /* NÅR OVERFØRING PÅGÅR.                                 */
             ALCOBJ     OBJ((E125F2 *FILE *EXCL))
             MONMSG     MSGID(CPF1002) EXEC(DO)
             SNDPGMMSG  MSG('Du kan ikke overføre faktura til +
                          regnskap nå pga. at andre behandler +
                          faktura.')
             GOTO       CMDLBL(SLUTT)
             ENDDO

/* LAGE POSTER I POSTA0 OG POSTB0 FOR OVEFØRING. */
             CLRPFM     FILE(POSTA0)
             CLRPFM     FILE(POSTB0)
             OVRDBF     FILE(SYSHL01) TOFILE(IBMDAT/SYSHL01)
             OVRDBF     FILE(SYSRL01) TOFILE(IBMDAT/SYSRL01)
             OVRDBF     FILE(VAREL01) TOFILE(IBMDAT/VAREL01)
             OVRPRTF    FILE(LISTE) PAGESIZE(72 120) LPI(6) CPI(15) +
                          OVRFLW(66)
             CALL       PGM(E127R_93A)
             RGZPFM     FILE(E125F2)

 /* TEST OM DET FINNES POSTER I POSTA0. */
             DSPFD      FILE(POSTA0) TYPE(*MBRLIST) OUTPUT(*OUTFILE) +
                          FILEATR(*PF) OUTFILE(E127C)
 READ:       RCVF       RCDFMT(*FILE)
             IF         COND(&MLNAME *EQ '          ') THEN(GOTO +
                          CMDLBL(READ))
             IF         COND(&MLNRCD *EQ 0) THEN(DO)
             SNDPGMMSG  MSG('Ingen faktura/kreditnata er overført.')
             GOTO       CMDLBL(ALCFIL)
             ENDDO

/* OVERFØRE POSTER I POSTA0 OG POSTB0 TIL RENGSKAP. */
             CHGDTAARA  DTAARA(*LDA (1 1024)) VALUE('                 -
             ')
             ADDLIBLE   LIB(IBMDAT)
             MONMSG     MSGID(CPF2103)
             ADDLIBLE   LIB(IBMPGM)
             MONMSG     MSGID(CPF2103)
             DLTOVR     FILE(*ALL)
             CALL       PGM(E127C2)

/* KOPIERE POSTER TIL BACKUP-FILER. */
             CPYF       FROMFILE(POSTA0) TOFILE(BACKUPA) +
                          MBROPT(*REPLACE)
             MONMSG     MSGID(CPF2817)
             CPYF       FROMFILE(POSTB0) TOFILE(BACKUPB) +
                          MBROPT(*REPLACE)
             MONMSG     MSGID(CPF2817)
             CLRPFM     FILE(POSTB0) MBR(*FIRST)
             CLRPFM     FILE(POSTA0) MBR(*FIRST)
             CHGDTAARA  DTAARA(*LDA (1 1024)) VALUE('                 -
             ')

/* OVERFØRING ER FERDIG OG SLIPPER OBJEKT E125F2. */
 ALCFIL:     DLCOBJ     OBJ((E125F2 *FILE *EXCL))

 SLUTT:      ENDPGM
