     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPDOC('SYFA670000' 'SYFA67' 'SY400-
     A                                      LO/MAINT')
     A                                      HELP
     A                                      ALTHELP
     A          R BILDE
     A                                      TEXT('Parameters for price raise')
     A                                      CF03(03 'End program')
     A                                      CF04(04 'CUSTOMER BY ALPHA')
     A                                      CF05(05 'start flytting')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1  2'SYSPED/SYSA51'
     A                                      COLOR(BLU)
     A N88                              1 19MSGCON(040 SHU0499 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A  88                              1 19MSGCON(040 SHU0499 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 63DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  1 73TIME
     A                                      COLOR(BLU)
     A                                  7  8MSGCON(045 SHU0495 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                      DSPATR(UL)
     A                                  8  8MSGCON(023 DIV2107 *LIBL/SYMSGF)
     A            WAFRKU         8Y 0B  8 35EDTCDE(4)
     A                                      CHECK(RB)
     A  92                                  DSPATR(PC)
     A  92                                  DSPATR(RI)
     A                                  8 44MSGCON(040 SHU0482 *LIBL/SYMSGF)
     A                                  9  8MSGCON(023 DIV2101 *LIBL/SYMSGF)
     A            WAFRFR         5A  B  9 35
     A  94                                  DSPATR(PC)
     A  94                                  DSPATR(RI)
     A            WAFRFL         2A  B  9 41
     A                                  9 44MSGCON(025 SHU0483 *LIBL/SYMSGF)
     A                                 10  8MSGCON(023 DIV2103 *LIBL/SYMSGF)
     A            WAFRTI         5A  B 10 35
     A  93                                  DSPATR(PC)
     A  93                                  DSPATR(RI)
     A            WAFRTL         2A  B 10 41
     A                                 10 44MSGCON(025 SHU0483 *LIBL/SYMSGF)
     A                                 11  8MSGCON(023 SHU0480 *LIBL/SYMSGF)
     A            WAFROT         2A  B 11 35
     A                                 11 44MSGCON(010 SHU0484 *LIBL/SYMSGF)
     A                                 12  8MSGCON(023 SHU0481 *LIBL/SYMSGF)
     A            WAFRLE         3A  B 12 35
     A                                 12 44MSGCON(010 SHU0485 *LIBL/SYMSGF)
     A                                 13  8MSGCON(023 SHU0487 *LIBL/SYMSGF)
     A            WAGEDA         6Y 0B 13 35EDTCDE(4)
     A  89                                  DSPATR(RI)
     A  89                                  DSPATR(PC)
     A                                 13 44MSGCON(022 SHU0488 *LIBL/SYMSGF)
     A                                 15  8'01 02 03 04 05 06 07 08 09 10 11 1-
     A                                      2 min for hen kom'
     A            V01            1   B 16  9
     A            V02            1   B 16 12
     A            V03            1   B 16 15
     A            V04            1   B 16 18
     A            V05            1   B 16 21
     A            V06            1   B 16 24
     A            V07            1   B 16 27
     A            V08            1   B 16 30
     A            V09            1   B 16 33
     A            V10            1   B 16 36
     A            V11            1   B 16 39
     A            V12            1   B 16 42
     A            V13            1   B 16 45
     A            V14            1   B 16 49
     A            V15            1   B 16 53
     A            V16            1   B 16 57
     A                                 18  8MSGCON(023 SHU0494 *LIBL/SYMSGF)
     A            NYDATO         6Y 0B 18 35EDTCDE(4)
     A                                 19  8MSGCON(023 SHU0496 *LIBL/SYMSGF)
     A            WAFRAV         1Y 0B 19 35EDTCDE(4)
     A                                 20  8MSGCON(023 SHU0497 *LIBL/SYMSGF)
     A            WAFRPM         1   B 20 35
     A            WAFRPR         4  2B 20 38EDTWRD('  .  ')
     A                                 20 46'(+ - M)'
     A                                 22  8MSGCON(030 SHU0498 *LIBL/SYMSGF)
     A            ANTFLY         6Y 0O 22 40EDTCDE(4)
     A                                      COLOR(WHT)
     A                                 23  2MSGCON(018 SHU0490 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 42MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 62MSGCON(018 DIV0032 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                             24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
