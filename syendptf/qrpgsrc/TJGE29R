      *********************************************************************
      * Add/upfdate/delete av frisøkevei
      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/TJGE29R MODULE(*LIBL/TJGE29R *LIBL/INCGI)
CRTPGM*        ACTGRP(*NEW) AUT(*USE)
      *
      *********************************************************************
11XXX * 01 JOV PTF12 hent dft dokkoder ved ny
      *********************************************************************
      /copy SYSPEDS/qrpgsrcpy,hspecs
      /copy SYSPEDS/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FFRISOK    UF A E           K DISK    USROPN
     F                                     PREFIX(X)
     FFRISOL03  IF   E           K DISK    USROPN
     F                                     RENAME(FRISOKR:FRISOKR3)
     FKODFRI    IF   E           K DISK    USROPN
     FHEADF     IF   E           K DISK    USROPN
     FFIRM      IF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy SYSPEDS/qrpgsrcpy,prototypeb
      /copy SYSPEDS/qrpgsrcpy,usec
      /copy SYSPEDS/qrpgsrcpy,variables3
      *
      * Varible for
     D WSUSER          S             10
     D WMODE           S              2
     D JSONstring      S          32000
     D Error           S            150
     D WA              S             15
     D AVD             S              4  0
     D OPD             S              7  0

     D UPC             C                   CONST('ABCDEFGHIJKLMNOPQRST-
     D                                     UVWXYZÆØÅ')
     D LO              C                   CONST('abcdefghijklmnopqrst-
     D                                     uvwxyzæøå')

      *get input-string
      /copy syspeds/qrpgsrcpy,prolog3

      * Parse input
     C                   EXSR      Parse
      * Open files etc
     C                   EXSR      INIT
      *
     c                   callp     gethtmlifs(
     C                             '/syspeddev/html/JSON.html':
     C                             '/CGITAG/')
     C                   callp     wrtsection('top')
      * Teste input og utfør
     C                   IF        error = *blanks
     C                   EXSR      TEST
     C                   ENDIF

      * ............................................................................................
      * skriv JSON-Object start..(alltid '{' + x ant param. m/komma mellom (IKKE komma etter siste))
      * ............................................................................................
      *
      /free
        JSONstring='{'
        +'¬user¬ : ¬'+%trim(WSUSER)+'¬, '
          +'¬avd¬ : ¬'+%trim(%editc(avd:'Z'))+'¬, '
          +'¬opd¬ : ¬'+%trim(%editc(opd:'Z'))+'¬, '
          +'¬mode¬ : ¬'+%trim(WMODE)+'¬, '
          +'¬kode¬ : ¬'+%trim(FSKODE)+'¬, '
          +'¬sok¬ : ¬'+%trim(FSSOK)+'¬, '
          +'¬dokk¬ : ¬'+%trim(FSDOKK)+'¬, '
          +'¬dtop¬ : ¬'+%trim(%editw(FSDTOP:'    .  .  '))+'¬, '
          +'¬errMsg¬ : ¬'+%trim(Error)+'¬, ';
      /end-free
      * JSONfix escaper " i tekst,  for deretter å bytte ¬->"
     c                   call      'JSONFIX'
     c                   parm                    JSONstring
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      *
      * ............................................................................................
      * Skriv JSON-LISTE Start (en liste er EN parameter(fra [ til   )
      * ............................................................................................
     c                   eval      JSONstring ='"freesearchUpdate" : ['
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      * ............................................................................................
      * Skriv JSON-Liste/Array  Avslutning
      * ............................................................................................
     c                   eval      JSONstring =']'
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      * ............................................................................................
      * Skriv JSON-string Avsluttning
      * ............................................................................................
     c                   eval      JSONstring ='}'
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      *
      * Quit
     C                   exsr      Exit


      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Lukk fil
     C                   CLOSE     BRIDF
     C  N50              CLOSE     FRISOK
     C  N50              CLOSE     KODFRI
     C  N50              CLOSE     HEADF
     C  N50              CLOSE     FIRM

      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   MOVE      *ON           *INLR
     C                   return
     C                   endsr
      *
      *********************************************************
     C     Parse         Begsr
      *********************************************************
      * Get input
     C                   EVAL      WSUSER  = zhbgetvar('USER')
     C                   EVAL      WA         = zhbgetvar('AVD')
     C                   EVAL      AVD        = c2n2(WA)
     C                   EVAL      WA         = zhbgetvar('OPD')
     C                   EVAL      OPD        = c2n2(WA)
     C                   EVAL      WMODE      = zhbgetvar('MODE')
      * WMODE = A --> Lage ny post
      *         U --> Oppdater post
      *         D --> Slett post
      *         I --> Spør post
     C                   EVAL      FSKODE     = zhbgetvar('FSKODE')
     C     LO:UPC        XLATE     FSKODE        FSKODE
     C                   EVAL      FSSOK      = zhbgetvar('FSSOK')
     C     LO:UPC        XLATE     FSSOK         FSSOK
     C                   EVAL      FSDOKK     = zhbgetvar('FSDOKK')
     C     LO:UPC        XLATE     FSDOKK        FSDOKK
     C                   EVAL      O_FSKODE     = zhbgetvar('O_FSKODE')
     C     LO:UPC        XLATE     O_FSKODE      O_FSKODE
     C                   EVAL      O_FSSOK      = zhbgetvar('O_FSSOK')
     C     LO:UPC        XLATE     O_FSSOK       O_FSSOK
      *
     C                   ENDSR
      *
      *********************************************************
     C     INIT          Begsr
      *********************************************************
     C                   OPEN      BRIDF
      * Test innlogging
     C     WSUSER        CHAIN     BRIDF                              50
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C                   call      BILIBL
     C                   end
      *
     C     HEADFK        KLIST
     C                   KFLD                    AVD
     C                   KFLD                    OPD
     C     FRISOKK       KLIST
     C                   KFLD                    AVD
     C                   KFLD                    OPD
     C                   KFLD                    O_FSKODE
     C                   KFLD                    O_FSSOK
     C     FRISOKKNY     KLIST
     C                   KFLD                    AVD
     C                   KFLD                    OPD
     C                   KFLD                    FSKODE
     C                   KFLD                    FSSOK
      *
     C     *LIKE         DEFINE    FSKODE        O_FSKODE
     C     *LIKE         DEFINE    FSSOK         O_FSSOK
      *
     C   50              eval      Error = 'Invalid userID'
     C  N50              OPEN      FRISOK
     C  N50              OPEN      KODFRI
     C  N50              OPEN      HEADF
     C  N50              OPEN      FIRM
      * dummy for å få feltnavn som arbeids/inputvariable
     C     *IN50         IFEQ      *OFF
     C     *IN50         ANDEQ     *ON
     C                   OPEN      FRISOL03
     C                   READ      FRISOL03
     C                   ENDIF
      * hent info om fakt.mottaker mm
     C  N50HEADFK        CHAIN     HEADF                              33
     c  N50*loval        setll     firm
     c  N50              read      firm
      *
     c                   endsr
      *
      *********************************************************
     C     TEST          BEGSR
      *********************************************************
     C                   IF        WMODE = 'A'
      * Feil hvis kode/frisøkevei finnes fra før
     C     FRISOKKNY     CHAIN(N)  FRISOK                             33
     C                   IF        *IN33 = *OFF
     C                   eval      Error = 'Frisøkevei finnes allerede'
     c                   goto      UtTest
     C                   ENDIF
     C                   ELSE
      * Feil hvis kode/frisøkevei ikke finnes
     C     FRISOKK       CHAIN(N)  FRISOK                             33
     C                   IF        *IN33=*ON
     C                   eval      Error = 'Frisøkevei finnes ikke'
     c                   goto      UtTest
     C                   ENDIF
     C                   ENDIF
      *
     C                   IF        WMODE = 'A' OR WMODE = 'U'
      * Kode for frisøkevei
     C     FSKODE        CHAIN     KODFRI                             33
     C                   IF        *IN33 = *on
     C                   eval      Error = 'Ugyldig kode for frisøkevei'
     c                   goto      UtTest
     C                   ENDIF
      * Frisøkevei
     C                   IF        FSSOK = *BLANKS
     C                   eval      Error = 'Ugyldig tekst for frisøkevei'
     c                   goto      UtTest
     C                   ENDIF
      *
     C                   ENDIF
      *
      * MODE A=Add   U=Update   D=Delete
     C                   SELECT
     C     WMODE         WHENEQ    'A'
     C     WMODE         OREQ      'U'
     C                   EXSR      SRAU
     C     WMODE         WHENEQ    'D'
     C                   EXSR      SRD
     C     WMODE         WHENEQ    'I'
     C                   EXSR      SRI
     C                   OTHER
     C                   eval      Error = 'Wrong mode'
     C                   ENDSL
      *
     C     UtTest        ENDSR
      *
      *********************************************************
     C     SRAU          BEGSR
      *********************************************************
      * forberede visse felter
     c                   exsr      FixData
      * Add/update
     C     WMODE         ifeq      'A'
     C                   EVAL      XFSAVD = AVD
     C                   EVAL      XFSOPD = OPD
     C                   else
     C     FRISOKK       CHAIN     FRISOK                             33
     c                   endif
     C                   EVAL      XFSKODE  = FSKODE
     C                   EVAL      XFSSOK   = FSSOK
     C                   EVAL      XFSDOKK  = FSDOKK
     C                   EVAL      XFSDTOP  = HEDTOP
     C                   EVAL      FSDTOP   = HEDTOP

     C     WMODE         ifeq      'A'
N01  C                   if        XFSDOKK = *blanks
N01  C                   eval      XFSDOKK = KFSODK
N01  C                   endif
     C                   WRITE     FRISOKR
     C                   else
     C                   UPDATE    FRISOKR
     C                   end
      *

     C                   ENDSR
      *********************************************************
     C     FixData       BEGSR
      *********************************************************
      *

     C                   ENDSR
      *
      *********************************************************
     C     SRD           BEGSR
      *********************************************************
      * Delete
     C     FRISOKK       CHAIN     FRISOK                             33
     C  N33              DELETE    FRISOKR
      *
     C                   ENDSR
      *********************************************************
     C     SRI           BEGSR
      *********************************************************
      * Inquiry
     C     FRISOKK       CHAIN(N)  FRISOK                             33
     C                   IF        *IN33 = *OFF
     C                   EVAL      FSKODE  = XFSKODE
     C                   EVAL      FSSOK   = XFSSOK
     C                   EVAL      FSDOKK  = XFSDOKK
     C                   EVAL      FSDTOP  = XFSDTOP
     C                   END
      *
     C                   ENDSR
