      *
      ***********************************************************
      *** PROGRAM SKAL SKRIVE UT FLYFRAKTBREV (INNENLAND)     ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01-26                            ***
      *** LEDIGE INDIKATORER 27-32 36-71 73-81 84-99          ***
      ***********************************************************
      *
     FHEAD7     IF   E           K DISK
     FDOKUM     UF   E           K DISK
     FDOKUI     UF   E           K DISK
     FSAMLB     IF   E           K DISK
     FCUNDL01   IF   E           K DISK
     FSTED2     IF   E           K DISK
     FKODTA     IF   E           K DISK
     FKODTDIS   IF   E           K DISK
     FFIRM      IF   E           K DISK
     FFLYFRAKT  O    F   85        PRINTER
     F                                     FORMLEN(72)
     F                                     FORMOFL(72)
     FMERKL1    O    F   50        PRINTER
     F                                     FORMLEN(18)
     F                                     FORMOFL(18)
     FMERKL2    O    F   50        PRINTER
     F                                     FORMLEN(24)
     F                                     FORMOFL(24)
     D                 DS
     D  IDAG1                  1      2  0
     D  IDAG2                  3      4  0
     D  IDAG3                  5      6  0
     D  IDAG                   1      6  0
     D                 DS
     D  XIDAGÅ                 1      4  0
     D  XIDAGM                 5      6  0
     D  XIDAGD                 7      8  0
     D  XIDAG                  1      8  0
     D                 DS
     D  KOAIAT                 1      7  0
     D  XIAT1                  1      2  0
     D  XIAT2                  3      3  0
     D  XIAT3                  4      7  0
     D                 DS
     D  HESDF                  1      5
     D  XSDF1                  1      1
     D  XSDF2                  2      2
     D  XSDF3                  3      3
     D                 DS
     D  FL                     1      3
     D                                     DIM(3)
     D  XFLY1                  1      3
     D                 DS
     D* XRFA                   1     15
     D  XRFA                   1     35
     D  WSSD                   1      3
     D  WSLNR                  4      6  0
     D                 DS
     D  DIAWB                  1     11  0
     D  XAWB1                  1      1  0
     D  XAWB2                  2      2  0
     D  XAWB3                  3      3  0
     D  XAWB4                  4      4  0
     D  XAWB5                  5      5  0
     D  XAWB6                  6      6  0
     D  XAWB7                  7      7  0
     D  XAWB8                  8      8  0
     D  XAWB9                  9      9  0
     D  XAWBA                 10     10  0
     D  XAWBB                 11     11  0
     D  XAWBW                  9     11  0
     D                 DS
     D  HEHAWB                 1      9
     D  XHAWB                  4      9  0
     D  XHAWB1                 4      4  0
     D  XHAWB2                 5      5  0
     D  XHAWB3                 6      6  0
     D  XHAWB4                 7      7  0
     D  XHAWB5                 8      8  0
     D  XHAWB6                 9      9  0
     D                UDS
     D  UFIRM                  1      2
     D  UAVD                  15     18  0
     D  USG                   19     21
     D  UHOD                  47     47
     D  UBBS                  48     48
     D  USD                   49     51
     D  ULNR                  52     54  0
     C*
     C                   EXSR      INIT
     C*
     C     DOKUMK        CHAIN     DOKUM                              33
     C  N33SAMLBK        CHAIN     SAMLB                              33
     C   33              GOTO      SLUTT
     C*
     C                   EXSR      PRINT
     C*
     C*** SJEKK OM MERKELAPPER SKAL SKRIVES
     C                   EXSR      PRINT2
     C*
     C     SLUTT         TAG
     C                   MOVE      XPRANT        UPRANT
     C                   SETON                                        LR
     C                   RETURN
     C*
     C***********************************************
     C     PRINT         BEGSR
     C***********************************************
     C                   Z-ADD     KDSBL5        DMVGEB
     C*
     C* HENTER DIVERSE TEKSTER FOR FRAKTBREV
     C     DMFRA         CHAIN     STED2                              33
     C  N33              MOVEL     ST2NVN        XALUFT           17
     C     DMTIL         CHAIN     STED2                              33
     C  N33              MOVEL     ST2NVN        XBLUFT           17
     C*
     C* MOMSFRITT VED TOLLPASSFORSENDELSE
     C     DMFRAK        ADD       DMFS          XSUM              7 2
     C                   ADD       DMFPRE        XSUM
     C     XSUM          MULT      FITAX         XMVA              7 2
     C                   ADD       5             XMVA
     C                   Z-ADD(H)  XMVA          XMVAHH            7 0
     C                   Z-ADD     XMVAHH        XMVA
     C*
     C* SVALBARD ER UTEN MVA
     C     DMTIL         IFEQ      'LYR'
     C                   MOVE      *ZEROS        XMVA
     C                   END
     C                   ADD       XMVA          XSUM
     C                   ADD       DMVGEB        XSUM
     C     XMVA          COMP      *ZEROS                             72
     C     DMRUN2        COMP      *BLANKS                            8282
     C     DMRUN3        COMP      *BLANKS                            8383
     C*
     C                   EXCEPT    DET
     C                   EXCEPT    DET
     C                   MOVE      'P'           DMST
     C                   UPDATE    DOKUMR
     C                   ADD       1             XPRANT
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     PRINT2        BEGSR
     C***********************************************
     C     HEAD7K        SETLL     HEAD7
     C                   SETOFF                                       34
     C     STP2          TAG
     C     HEAD7K        READE     HEAD7                                  34
     C   34              GOTO      SLP2
     C     DOKUIK        CHAIN     DOKUI                              33
     C   33              GOTO      STP2
     C     DIMLAP        IFNE      'J'
     C     DIMLAP        ANDNE     'B'
     C                   UPDATE    DOKUIR
     C                   GOTO      STP2
     C                   END
     C     SMROA3        IFNE      *BLANKS
     C                   MOVE      SMROA3        XFLY1
     C                   MOVE      SMROA2        XFLY2             3
     C                   MOVE      SMROA1        XFLY3             3
     C                   ELSE
     C                   MOVE      *BLANKS       XFLY3
     C     SMROA2        IFNE      *BLANKS
     C                   MOVE      SMROA2        XFLY1
     C                   MOVE      SMROA1        XFLY2
     C                   ELSE
     C                   MOVE      *BLANKS       XFLY2
     C                   MOVE      SMROA1        XFLY1
     C                   END
     C                   END
     C                   Z-ADD     1             XANTL             3 0
     C     XANTL         DOWLE     DIANT
     C                   EXCEPT    MLAP
     C     DIMLAP        IFEQ      'B'
     C                   EXCEPT    MLAP2
     C                   END
     C                   ADD       1             XANTL
     C                   END
     C                   MOVE      'P'           DIMLAP
     C                   UPDATE    DOKUIR
     C                   GOTO      STP2
     C*
     C     SLP2          ENDSR
     C*
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     HEAD7K        KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    XRFA
     C     DOKUMK        KLIST
     C                   KFLD                    USD
     C                   KFLD                    ULNR
     C     DOKUIK        KLIST
     C                   KFLD                    YRI
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C                   KFLD                    YFBNR
     C     SAMLBK        KLIST
     C                   KFLD                    USD
     C                   KFLD                    ULNR
     C     KUND1K        KLIST
     C                   KFLD                    UFIRM
     C                   KFLD                    KOAKNR
     C     KODTAK        KLIST
     C                   KFLD                    YAUNI
     C                   KFLD                    UAVD
     C*
     C     *ENTRY        PLIST
     C                   PARM                    UPRANT            2
     C*
     C                   MOVE      'I'           YRI               1
     C                   MOVE      'A'           YAUNI             1
     C                   MOVE      '000'         YFBNR             3 0
     C                   MOVE      *ZEROS        XPRANT            2 0
     C*
     C                   READ      FIRM                                   35
     C                   MOVE      UDAY          XIDAGD
     C                   MOVE      UMONTH        XIDAGM
     C     UYEAR         IFGT      50
     C     UYEAR         ADD       1900          XIDAGÅ
     C                   ELSE
     C     UYEAR         ADD       2000          XIDAGÅ
     C                   END
     C*
     C     FITAXD        IFNE      *ZEROS
     C     FITAX2        ANDNE     *ZEROS
     C     XIDAG         ANDLT     FITAXD
     C                   MOVE      FITAX2        FITAX
     C                   END
     C*
     C     KODTAK        CHAIN     KODTA                              35
     C     'DISPU'       CHAIN     KODTDIS                            33
     C     KUND1K        CHAIN     CUNDL01                            33
     C*
     C                   MOVE      UDAY          UDT1              2 0
     C                   MOVE      UMONTH        UDT2              2 0
     C                   MOVE      UYEAR         UDT3              2 0
     C                   CALL      'DATOFMT'
     C                   PARM                    UDT1
     C                   PARM                    UDT2
     C                   PARM                    UDT3
     C                   MOVE      UDT1          IDAG1             2 0
     C                   MOVE      UDT2          IDAG2             2 0
     C                   MOVE      UDT3          IDAG3             2 0
     C*
     C                   ENDSR
     C*
     OFLYFRAKT  E            DET              02
     O                       DMFNAV              49
     O                       DMAWB               67 '0   &-&    &    '
     O          E            DET              05
     O                       XALUFT              38
     O                       DMRUNR              41
     O                       DMDAG1              44
     O               82      DMRUN2              51
     O               82      DMDAG2              54
     O                       XBLUFT              73
     O          E    83      DET              06
     O                       DMRUN3              41
     O                       DMDAG3              44
     O          E            DET              08
     O                       DMKNAV              55
     O          E            DET              09
     O                       DMKAD1              55
     O                       DMKTLF              72
     O          E            DET              10
     O                       DMKAD2              55
     O          E            DET              11
     O                       DMKAD3              55
     O          E            DET              14
     O                       DMSNAV              55
     O          E            DET              15
     O                       DMSAD1              55
     O                       DMSTLF              72
     O          E            DET              16
     O                       DMSAD2              55
     O          E            DET              17
     O                       DMSAD3              55
     O          E            DET              20
     O                       DMANT         Z     21
     O                       DMVEKT        Z     28
     O                       DMSLAG              49
     O                       DMFVKT        Z     54
     O                       DMPRIS        J     69
     O          E            DET              21
     O                       DMVSL2              49
     O          E            DET              27
     O                       DMMERK              69
     O          E            DET              29
     O                       DMFORS        Z     39
     O                       DMFORA              69
     O          E            DET              33
     O                                           44 'Ink.gr.pris kr100,-'
     O                       DMFRAK        Z     52
     O                                           55 ',00'
     O          E            DET              35
     O                       DMFS          Z     55
     O          E            DET              37
     O                       DMFPRE        Z     55
     O          E            DET              43
     O                       DMVGEB        Z     52
     O                                           55 ',00'
     O          E            DET              47
     O                  72   XMVA                55 '      0,  '
     O          E            DET              51
     O                       XSUM                55 '    0,  '
     O          E            DET              54
     O                       XALUFT              36
     O              N35      XIAT1               39
     O              N35                          40 '-'
     O              N35      XIAT2               41
     O              N35                          42 '-'
     O              N35      XIAT3               46
     O          E            DET              55
     O                       IDAG1               23
     O                       IDAG2               25
     O                       IDAG3               27
     O          E            DET              61
     O                       USG                 49
     O                       USD                 60
     O                       ULNR                63
     O          E            DET              64
     O                       DMAWB               67 '0   &-&    &    '
     OMERKL1    E            MLAP             06
     O                       XAWB1                5
     O                       XAWB2                7
     O                       XAWB3                9
     O                       XAWB4               12
     O                       XAWB5               14
     O                       XAWB6               16
     O                       XAWB7               18
     O                       XAWB8               21
     O                       XAWB9               23
     O                       XAWBA               25
     O                       XAWBB               27
     O                       DIANT         Z     36
     O                       DIVEKT        Z     45
     O          E            MLAP             09
     O                       FL(1)                5
     O                       FL(2)                7
     O                       FL(3)                9
     O                       XFLY2               36
     O                       XFLY3               45
     O          E            MLAP             12
     O                       XSDF1                5
     O                       XSDF2                7
     O                       XSDF3                9
     O                       XHAWB1              11
     O                       XHAWB2              13
     O                       XHAWB3              15
     O                       XHAWB4              17
     O                       XHAWB5              19
     O                       XHAWB6              21
     O                       DIANT         Z     36
     O                       DIVEKT        Z     45
     OMERKL2    E            MLAP2            04
     O                       DIKNAV              35
     O          E            MLAP2            06
     O                       DIKAD1              35
     O          E            MLAP2            08
     O                       DIKAD2              35
     O          E            MLAP2            10
     O                       DIKAD3              35
