      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/ESOP11HTW MODULE(*LIBL/ESOP11HTW *LIBL/INCGI)
CRTPGM*        ACTGRP(CGI) AUT(*USE)
      *
      *********************************************************************
      * MAIN PROGRAM FLOW
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
     FBRIDF     IF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Prototype defintions and standard system API error structure
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      *--------------------------------------------------------------------
      * Variables common to CGI programs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
      * Variables received from the input string
     D WSUSER          S             10
     D AVSLUTT         s              1
     D avd             S              4
     D avdN            S              4  0
S01  D*opd             S              4
S01  D*opdN            S              4  0
N01  D opd             S              7
N01  D opdN            S              7  0
     D TLF             S             15
     D TEXT1           S             70
     D TEXT2           S             70
     D URL             S              1
      * Number of records matching search criteria
      *
     D Lib             s             10
     D Fn              s             10
     D Mbr             s             10
      *
     D Spaces          s             12a   inz('&nbsp;&nbsp;')
      *--------------------------------------------------------------------
      * Prolog common to CGI programs
      * -Receive input from the remote browser
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prolog3
      *=====================================================================******
      * MAIN PROCESS LINE
      *=====================================================================******
      * Parse input
     C                   exsr      Parse
      *
     C                   EXSR      INIT
      **
      * Set output skeleton html member name
     C                   exsr      SetSkl
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
      * submittet med utfyllt tekst og telefon  = send / avslutt
     c                   if        TEXT1<>' ' and  TLF<>' '
     C                   MOVE      'Y'           AVSLUTT
N03  c                   move      avdN          HEAVDa            4
N03  c                   move      opdN          HEOPDa            7
N03  c                   call      'ESOP11HT'
     C                   PARM                    HEAVDa            4
     C                   PARM                    HEOPDa            7
N01  C                   PARM                    TEXT1            70
N01  C                   PARM                    TEXT2            70
     C                   PARM                    TLF              15
     C                   PARM      WSUSER        UUUSER           10
     C                   PARM                    URL               1
     c                   endif
     C                   callp     updHTMLvar('AVSLUTT':AVSLUTT)
     C                   callp     updhtmlvar('AVD':
     C                             %trim(%editc(AVDN:'Z')))
     C                   callp     updhtmlvar('OPD':
     C                             %trim(%editc(OPDN:'Z')))
     C                   callp     updHtmlVar('TLF':TLF)
     C                   callp     updHtmlVar('TEXT1':TEXT1)
     C                   callp     updHtmlVar('TEXT2':TEXT2)
     C                   callp     updHtmlVar('URL':URL)                        J=send url/mer info
     C                   callp     wrtsection('all')
      *------------------                                                   ******
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
     C*
     C                   close     BRIDF
     C                   eval      *inlr = *on
     C                   return
      *=====================================================================******
      * Parse input
      *=====================================================================******
     C     Parse         begsr
     C                   EVAL      WSUSER  = zhbgetvar('USER')
     C                   EVAL      avd     = zhbgetvar('avd')
     C                   EVAL      avdN    = c2n2(AVD)
     C                   EVAL      opd     = zhbgetvar('opd')
     C                   EVAL      opdN    = c2n2(opd)
     C                   EVAL      TLF  = zhbgetvar('TLF')
     C                   EVAL      TEXT1= zhbgetvar('TEXT1')
     C                   EVAL      TEXT2= zhbgetvar('TEXT2')
      * ikke klargjort i HTML, men f.eks checkboks ....
     C*                  EVAL      URL  = zhbgetvar('URL')
      *
      *
     C                   endsr
      *=====================================================================******
      * Set html output skeleton member before its read into memory
      *=====================================================================******
     C     SetSkl        begsr
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC'
     C                   eval      Mbr = 'ESOP11HTW'
     C                   endsr
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
     C                   OPEN      BRIDF
     C     WSUSER        CHAIN     BRIDF                              50
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   CALLB     'INCGI'
     C                   PARM                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
      *
     C                   endsr
