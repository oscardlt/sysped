********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10172 * 01 SH  PTF10 norsk text til Track and trace
10XXX * 02 YBC PTF10 EKSTRA INFORMASJONER PÅ UTSKRIFT
********************************************************************
      *
      **************************************************************
      *** PROGRAM SKAL SKRIVE UT PLUKKLISTE
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER
      *** LEDIGE INDIKATORER 01-26
      *** LEDIGE INDIKATORER 30-32 35-99
      **************************************************************
     FGSSOLL02  IF   E           K DISK
     FGSSOLL03  IF   E           K DISK
     F                                     RENAME(GSSOLFR:GSSOLFR2)
     FGSSOLL01  IF   E           K DISK
     F                                     RENAME(GSSOLFR:GSSOLFR1)
     FGSSPOF    IF   E           K DISK
     FGSSORF    IF   E           K DISK
     FGSSORL21  IF   E           K DISK
     F                                     RENAME(GSSORFR:GSSORFR21)
     FGSSTRF    IF   E           K DISK
     FGSSRFF    IF   E           K DISK
     FCUNDL01   IF   E           K DISK
     FCDBOAT    IF   E           K DISK
     FKODTFM    IF   E           K DISK
     FTRACKF    O    E             DISK
     FGSSSTK    IF   E           K DISK
     FGSS025P   O    E             PRINTER OFLIND(*IN27)
     F                                     USROPN
     D UCMD            DS          1024
     D                 DS
     D XCMD11          C                   CONST('OVRPRTF -
     D                                     FILE(GSS025P) -
     D                                     OUTQ(')
     D XCMD21          C                   CONST('PAGESIZE(72 80) -
     D                                     CPI(10) -
     D                                     OVRFLW(64)')
     D XCMD31          C                   CONST('OVRPRTF -
     D                                     FILE(GSS025P) -
     D                                     PAGESIZE(72 80) -
     D                                     CPI(10) -
     D                                     OVRFLW(64)')
     D XXTXT01         C                   CONST('Printed packinglist.')
     D XXTXT02         C                   CONST('Packinglist No.')
n01  D XXTXT01L        C                   CONST('Skrevet pakkliste.')
n91  D XXTXT02L        C                   CONST('Pakkliste Nr.')
     D                 DS
     D  TRDTLÅ                 1      4  0
     D  TRDTLM                 5      6  0
     D  TRDTLD                 7      8  0
     D  TRDTL                  1      8  0
     D                 DS
     D  XTDTLD                 1      2  0
     D  XTDTLM                 3      4  0
     D  XTDTLÅ                 5      6  0
     D  XTDTL                  1      6  0
     D                 DS
     D  OLLC                   1     20
     D  XLLC                   1      6
     D                 DS
     D  POCONO                 1     20
     D  XOCONO                 1     14
     D  XOCONO2                1     14
     D                 DS
     D  ORDSC                  1     30
     D  XORDSC                 1     15
     D                 DS
     D  ORCLI                  1      5  0
     D  XRCLI                  4      5  0
     D                 DS
     D  ORFT1                  1     30
     D  ORFT2                 31     60
     D  ORFT3                 61     90
     D  ORFT4                 91    120
     D  FT                     1    120    DIM(4)
     D                 DS
     D  TRFT1                  1     70
     D  TRFT2                 71    140
     D  TRFT3                141    210
     D  TRFT4                211    280
     D  TFT                    1    280    DIM(4)
     D                 DS
     D  OLMN                   1     35
     D  OLMNPRE                1      3
     D  OLMNTNO                4     14
     D  OLMNBOX               15     17
     D  OLMN01                 1      1
     D  OLMN02                 3      4
     D  OLMN03                 5     35
     D                UDS
     D  UFIRM                  1      2
      *
     C                   EXSR      INIT
      *
     C     UTNO          CHAIN     GSSTRF                             33
     C                   MOVE      TRDTLD        XTDTLD
     C                   MOVE      TRDTLM        XTDTLM
     C                   MOVE      TRDTLÅ        XTDTLÅ
     C     CUNDL01K      CHAIN     CUNDL01                            33
     C     TRBCD         CHAIN     CDBOAT                             33
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTD               6
     C                   MOVEL     WTD           XTID4
      *
     C     Ukopi         IFNE      'W'
     C                   Z-ADD     1024          UCMDL            15 5
     C                   MOVE      *BLANKS       UCMD
     C     XCMD11        CAT       UPRT:0        UCMD
     C                   CAT       ')':0         UCMD
     C                   CAT       XCMD21:1      UCMD
     C                   CALL      'QCMDEXC'                            33
     C                   PARM                    UCMD
     C                   PARM                    UCMDL
     c                   ELSE
     C                   Z-ADD     1024          UCMDL            15 5
     C                   EVAL      UCMD = XCMD31
     C                   CALL      'QCMDEXC'                            33
     C                   PARM                    UCMD
     C                   PARM                    UCMDL
     c                   end
      *
     C                   OPEN      GSS025P
      *
     C                   MOVE      '0'           *IN27
     C                   WRITE     GSS025PA
      *
     C                   SELECT
     C     TRBCD         WHENEQ    *BLANKS
     C     UTNO          SETLL     GSSORL21                           33
     C     UTNO          READE     GSSORL21                               33
     C     GSOL03K       SETLL     GSSOLL03                           33
     C     GSOL03K       READE     GSSOLL03                               33
     C     USORT         WHENEQ    'L'
     C     UTNO          SETLL     GSSOLL02                           33
     C     UTNO          READE     GSSOLL02                               33
     C     USORT         WHENEQ    'B'
     C     UTNO          SETLL     GSSOLL01                           33
     C     UTNO          READE     GSSOLL01                               33
     C                   OTHER
     C     UTNO          SETLL     GSSOLL03                           33
     C     UTNO          READE     GSSOLL03                               33
     C                   ENDSL
     C                   DOW       *IN33 = *OFF
     C     OLONO         CHAIN     GSSORF                             34
     C     OLONO         SETLL     GSSPOF
     C     OLONO         READE     GSSPOF                                 34
     C     *IN27         IFEQ      '1'
     C                   MOVE      '0'           *IN27
     C                   WRITE     GSS025PA
     C                   END
     C                   MOVE      *BLANKS       XWACKN
     C     OLONO         SETLL     GSSRFF
     C     OLONO         READE     GSSRFF                                 34
     C     *IN34         DOWEQ     *OFF
     C                   IF        RFKD = 'CPO'
     C                   MOVEL     RFREF         XWACKN
     C                   LEAVE
     C                   END
     C     OLONO         READE     GSSRFF                                 34
     C  N34              ENDDO
     C                   IF        ((OLBOX = 0) AND (OLMNPRE = '995'))
     C                   MOVE      OLMNBOX       OLBOX
     C                   MOVE      '*'           XXGBOX
     C                   ELSE
     C                   MOVE      ' '           XXGBOX
     C                   END
     C                   WRITE     GSS025PB
      * CONTAINER
     C                   IF        ((OLMN01 = 'C') AND (OLMN02 = ': '))
     C                   MOVEL     OLMN03        XXCONT
     C                   WRITE     GSS025PF
     C                   END
      *
     C                   EVAL      XXFT = *BLANKS
     C                   IF        ORST2 = *BLANKS
     C                   IF        (((ORKDL1 = 'PN') OR
     C                               (ORKDL2 = 'PN')) OR
     C                               (ORKDL3 = 'PN'))
     C     1             DO        4             XN                3 0
     C                   IF        FT(XN) <> *BLANKS
     C                   EVAL      XXFT = FT(XN)
     C     *IN27         IFEQ      '1'
     C                   MOVE      '0'           *IN27
     C                   WRITE     GSS025PA
     C                   END
     C                   WRITE     GSS025PC
     C                   END
     C                   ENDDO
     C                   END
     C                   END
     C                   ADD       1             XCLI
     C                   SELECT
     C     TRBCD         WHENEQ    *BLANKS
     C     GSOL03K       READE     GSSOLL03                               33
     C     *IN33         IFEQ      '1'
     C     UTNO          READE     GSSORL21                               33
     C  N33GSOL03K       SETLL     GSSOLL03                           33
     C  N33GSOL03K       READE     GSSOLL03                               33
     C                   END
     C     USORT         WHENEQ    'L'
     C     UTNO          READE     GSSOLL02                               33
     C     USORT         WHENEQ    'B'
     C     UTNO          READE     GSSOLL01                               33
     C                   OTHER
     C     UTNO          READE     GSSOLL03                               33
     C                   ENDSL
     C  N33              ENDDO
     C                   WRITE     GSS025PD
     C                   IF        (((TRKDL1 = 'PN') OR
     C                               (TRKDL2 = 'PN')) OR
     C                               (TRKDL3 = 'PN'))
     C     1             DO        4             XN                3 0
     C                   IF        TFT(XN) <> *BLANKS
     C                   EVAL      XXTFT = TFT(XN)
     C     *IN27         IFEQ      '1'
     C                   MOVE      '0'           *IN27
     C                   WRITE     GSS025PA
     C                   END
     C                   WRITE     GSS025PE
     C                   END
     C                   ENDDO
     C                   END
      *
     C                   EXSR      OPDLOGG
      *
     C                   MOVE      '1'           *INLR
     C                   RETURN
      *
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     CUNDL01K      KLIST
     C                   KFLD                    UFIRM
     C                   KFLD                    TRCNO
     C     GSOL03K       KLIST
     C                   KFLD                    TRTNO
     C                   KFLD                    ORONO
      *
     C     *ENTRY        PLIST
     C                   PARM                    UTNO2            11
     C                   PARM                    UPRT             10
     C                   PARM                    USORT             1
     C                   PARM                    UKOPI             1
     C                   PARM                    UUSER            10
      *
     C     UKOPI         COMP      'J'                                    28
     C     UKOPI         COMP      'W'                                    29
     C                   MOVE      UTNO2         UTNO             11 0
     C     Ukopi         IFNE      'W'
     C     UPRT          CHAIN     GSSSTK                             33
     C     *IN33         IFEQ      '0'
     C     STRF05        CHAIN     KODTFM                             33
      * lager stringkoder for skifte av font
     C     stresc        ifne      *blanks
      * ¤1B(¤1B(s0p10h6v0s0b0T'
     c                   move      *blanks       stri05           70
     C     stri05        CAT       stresc:0      stri05
     C     stri05        CAT       '(':0         stri05
     C     stri05        CAT       stresc:0      stri05
     C     stri05        CAT       '(s0p10h6v0':0stri05
     C     stri05        CAT       's0b0T':0     stri05
     c                   end
     C                   END
     C                   END
      *
     C                   ENDSR
      *
      *=====================================================================
      *  Oppdater loggen
      *=====================================================================
     C     OPDLOGG       begsr
      *
     C                   MOVEL     UTNO          TTAVD
     C                   MOVE      UTNO          TTOPD
     C                   MOVE      WDT           TTDATE
     C                   MOVE      WTD           TTTIME
     C                   MOVE      WDT           TTDATL
     C                   MOVE      WTD           TTTIML
     C                   MOVE      'PS1'         TTACTI
     C                   MOVE      UUSER         TTUSER
     C                   EVAL      TTTEXT = XXTXT01
     C                   CAT       XXTXT02:10    TTTEXT
     C                   CAT       UTNO2:1       TTTEXT
n01  C                   EVAL      TTTEXL = XXTXT01L
n01  C                   CAT       XXTXT02L:10   TTTEXL
n01  C                   CAT       UTNO2:1       TTTEXL
     C                   WRITE     TRACKFR
      *
     C                   endsr
