      *********************************************************************
      * SELEKSJ.BILDE ARBEIDE MED TRANSP.PLANLEGGING
      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/ss110 MODULE(*LIBL/ss110
CRTPGM*        *LIBL/CHECKUSR *LIBL/INCGI) ACTGRP(CGI) AUT(*USE)
      *
      *********************************************************************
      * MAIN PROGRAM FLOW
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
     FBRIDF     UF   E           K DISK    USROPN
     FGSSTRL01  if   e           k disk    usropn
     FBRPARF    if   e           k disk    usropn
      *--------------------------------------------------------------------
      * Prototype defintions and standard system API error structure
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      *--------------------------------------------------------------------
      * Variables common to CGI programs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
      * Variables received from the input string
     D lng             s              2
     D UsrSpcName      s             10
     D WSUSER          S             10
     D CURTUR          S             11
     D CURTURN         S             11  0
     D KUNDE           S              8
     D KUNDEN          S              8  0
     D AREA            S              5
     D KunITur         S              1
     D SLTBES1         S             50
     D STATUS          S              1
     D SLSTA           S              1
     D SLSBES          S             50
     D PGM             C                   CONST('SYSPED/CHECKUSR')
      *
     D Lib             s             10
     D Fn              s             10
     D Mbr             s             10
      *
     D Spaces          s             12a   inz('&nbsp;&nbsp;')
      *
     D                 DS
     D  PAVRDN                 1     15  5
     D  PAVRDN8                3     10
     D                 DS
     D  KU                     1    800    DIM(100)
      *
      *--------------------------------------------------------------------
      * Prolog common to CGI programs
      * -Receive input from the remote browser
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prolog3
      *=====================================================================******
      * MAIN PROCESS LINE
      *=====================================================================******
      * Get program start time for calculating execution time
     C                   time                    timedata1
      * Parse input
     C                   exsr      Parse
      *
     C                   EXSR      INIT
     C                   CALLB     PGM
     C                   PARM                    BIBRID
     C                   PARM                    UsrSpcName
     C                   PARM                    UINN              1
     C                   IF        UINN = 'N'
     C                   GOTO      SLUTT
     C                   END
     C     WSUSER        CHAIN     BRIDF                              50
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVE      WDT           BIDTF
     C                   MOVE      WTM           BITIDF
     C                   UPDATE    BRIDFR
      * Set output skeleton html member name
     C                   exsr      SetSkl
      * Read skeleton output html, etc.
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
      *
      * 1-Section /$top
     C                   exsr      SetTop
     C                   callp     wrtsection('top')
      * Kunder -pulldown
     C                   exsr      DspKund
      * 2-Turer  -pulldown
     C                   exsr      DspTrip
      * Status  -pulldown
     C                   exsr      DspStat
      *
     C     SLUTT         TAG
     C                   EXSR      EXIT
     C                   return
      *=====================================================================******
      * Parse input
      *=====================================================================******
     C     Parse         begsr
     C                   eval      lng     = zhbgetvar('lng')
     C                   eval      WSUSER  = zhbgetvar('USER')
     C                   EVAL      UsrSpcName  = zhbgetvar('UsrSpcName')
     C                   eval      CURTUR  = zhbgetvar('CURTUR')
     C                   eval      CURTURN = c2n2(CURTUR)
     C                   eval      KUNDE   = zhbgetvar('KUNDE')
     C                   eval      KUNDEN  = c2n2(KUNDE)
     C                   EVAL      AREA    = zhbgetvar('AREA')
     C                   EVAL      STATUS  = zhbgetvar('STATUS')
     C                   EVAL      KunITur = zhbgetvar('KunITur')
     c     curturn       ifeq      99999999999
     c                   eval      KunITur = 'Y'
     c                   eval      CurTur  = ' '
     c                   eval      CurTurn = 0
     c                   end
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$top
      *  (search criteria)
      *=====================================================================******
     C     SetTop        begsr
      * Repeat national language for the next invocation
     C                   callp     updhtmlvar('LNG':lng:
     C                             InitHTMLVars)
      * Color for text, background, links, visited links, active links
     C                   callp     updhtmlvar('TXTCOLOR':'black')
     C                   callp     updhtmlvar('BOLD':' ')
     C                   callp     updhtmlvar('BGCOLOR':BIFARG)
     C                   callp     updhtmlvar('LINK':'0000FF')
     C                   callp     updhtmlvar('VLINK':'FF0000')
     C                   callp     updhtmlvar('ALINK':'00FF00')
     C                   callp     updhtmlvar('WSNA':PAVRDA)
BODY C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updHtmlVar('USER':WSUSER)
     C                   callp     updHtmlVar('UsrSpcName':UsrSpcName)
     C                   callp     updHTMLvar('sdato':
     C                             %trim(%editw(BIDTV:'    .  .  ')))
     C                   callp     updHTMLvar('stid':
     C                             %trim(%editw(BITIDV:'  .  .  ')))
     C                   callp     updhtmlvar('AREA':AREA)
     C                   callp     updhtmlvar('KUNITUR':KUNITUR)
      *
     C                   endsr
      *=====================================================================******
      * Set html output skeleton member before its read into memory
      *=====================================================================******
     C     SetSkl        begsr
      *------------------
      * Set html output skeleton member
     C                   eval      lng = uppify(lng)
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC' + lng
     C                   eval      Mbr = 'SS110'
      *
     C                   endsr
      *=====================================================================******
      *  Display Kunder        to select from
      *=====================================================================******
     C     DspKund       begsr
      *
     c                   move      *zeros        LN                2 0
     c                   Clear                   KU
     C                   MOVE      'SSCUS'       PAID
     C     BrParKey      setll     BRPARF
     C     BrParKey      reade     BRPARF                                 40
     C     *in40         doweq     '0'
     C                   exsr      SetSltRow
     C                   callp     wrtsection('typsltrow')
     c                   add       1             LN
     c                   MOVE      PAVRDN8       KU(LN)
     C     BrParKey      reade     BRPARF                                 40
     C                   enddo
      *
     C                   callp     wrtsection('typsltend')
      *
     C                   endsr
      *=====================================================================******
      *  Display Turer         to select from
      *=====================================================================******
     C     DspTrip       begsr
      *
      * etter blank=All(dft) vises 999999999 = kun ordrer som IKKE er på tur
     C                   callp     updhtmlvar('SLTUR':'99999999999')
     C                   eval      SLTBES1 = 'Only orders NOT on a list.'
     C                   callp     updhtmlvar('SLTBES1':SLTBES1)
     C                   callp     updhtmlvar('SLTBES2':spaces)
     C                   callp     updhtmlvar('SLTBES3':spaces)
     C                   callp     updhtmlvar('SLTBES4':spaces)
      * Når Kun ikke Tur alt valgt (inp.param.) vis posten også i pulldown
     C     KunITur       IFEQ      'Y'
     C                   callp     updhtmlvar('TSEL':'SELECTED')
     c                   else
     C                   callp     updhtmlvar('TSEL':spaces)
     c                   end
     C                   callp     wrtsection('typsltrow2')
      *
      * Deretter alle turer med status 0/1 evt begrenset på kunde
     C     *loval        setll     GSSTRL01
     C                   read      GSSTRL01                               40
     C     *in40         doweq     '0'
      * Location From MÅ samsvare med parameter SSFAB )=ShipFrom
     c     TRLOCF        ifne      SHIPFROM
     c                   goto      NxtTrip
     c                   end
      * filter på kundenr - må stemme med denne
     c     Kunden        ifne      *zeros
     c     Kunden        andne     TRCNO
     c                   goto      NxtTrip
     c                   end
      * Ikke filter på kundenr - turen må tilhøre EN av aktuelle kunder
     c     Kunden        ifeq      *zeros
     c                   move      TRCNO         TRCNOA            8
     c     1             do        Ln            LN2               2 0
     c     KU(LN2)       CaBeq     TRCNOA        TurOK
     c                   end
     c                   goto      NxtTrip
     c                   end
      *
     c     TurOK         tag
     C                   exsr      SetSltRow2
     C                   callp     wrtsection('typsltrow2')
     c     NxtTrip       tag
     C                   read      GSSTRL01                               40
     C                   enddo
      *
     C                   callp     wrtsection('typsltend2')
      *
     C                   endsr
      *=====================================================================******
      *  Display STATUS        to select from
      *=====================================================================******
     C     DspStat       begsr
      * hardkodet valg mellom ' ' og 'R'...
      ** Opt R
     C                   eval      SLSTA  = 'R'
     C                   eval      SLSBES = 'Only RFP Released From Prod.'
     C                   callp     updhtmlvar('SLSTA':SLSTA)
     C                   callp     updhtmlvar('SLSBES':SLSBES)
     C     STATUS        IFEQ      SLSTA
     C                   callp     updhtmlvar('SSEL':'SELECTED')
     c                   else
     C                   callp     updhtmlvar('SSEL':spaces)
     c                   end
     C                   callp     wrtsection('typsltrow3')
      **
     C                   callp     wrtsection('typsltend3')
      *
     C                   endsr
      *=====================================================================******
      *  Set variable data for section /$typsltrow
      *=====================================================================******
     C     SetSltRow     begsr
     C                   Z-ADD     PAVRDN        KUNRN             8 0
     C                   callp     updhtmlvar('SLKNR':
     C                             %trim(%editc(KUNRN:'Z')))
     C                   callp     updhtmlvar('SLNAVN':PAVRDA)
      * Når kundenr alt er valgt (inp.param.) vis posten også i pulldown
     C     KUNDEN        IFNE      *ZEROS
     C     KUNDEN        ANDEQ     KUNRN
     C                   callp     updhtmlvar('KSEL':'SELECTED')
     c                   else
     C                   callp     updhtmlvar('KSEL':spaces)
     c                   end
     C                   endsr
      *=====================================================================******
      *  Set variable data for section /$typsltrow2
      *=====================================================================******
     C     SetSltRow2    begsr
     C                   callp     updhtmlvar('SLTUR':
     C                             %trim(%editc(TRTNO:'Z')))
     C                   callp     updhtmlvar('SLTBES1':
     C                             %trim(%editc(TRTNO:'Z')))
     C                   callp     updHTMLvar('SLTBES2':
     C                             %trim(%editw(TRDTS:'    .  .  ')))
     C                   callp     updhtmlvar('SLTBES3':TRLOCT)
     C                   callp     updhtmlvar('SLTBES4':TRAD1)
      * Når Tur alt er valgt (inp.param.) vis posten også i pulldown
     C     CURTURN       IFNE      *ZEROS
     C     CURTURN       ANDEQ     TRTNO
     C                   callp     updhtmlvar('TSEL':'SELECTED')
     c                   else
     C                   callp     updhtmlvar('TSEL':spaces)
     c                   end
     C                   endsr
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
     C                   OPEN      BRIDF
     C     WSUSER        CHAIN(N)  BRIDF                              50
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
     C                   OPEN      GSSTRL01
     C                   OPEN      BRPARF
      *
     C     BrParKey      klist
     C                   kfld                    PABRID
     C                   kfld                    PAKUNR
     C                   kfld                    PAID
     C                   MOVE      BIBRID        PABRID
     C                   MOVE      *zeros        PAKUNR
      * Hent fabrikk kode / Navn
     c                   Move      *blanks       SHIPFROM          5
     C                   MOVE      'SSFAB'       PAID
     C     BrParKey      Chain     BRPARF                             33
     c  n33              MoveL     PAVRDA        SHIPFROM
      *
      * hvis turnr er oppgitt MÅ param. for client følge denne
     c     CurTurN       IFNE      *zeros
     c     CurTurN       chain     GSSTRL01                           33
     c  N33              move      TRCNO         KUNDEN
     c                   end
      *
     C                   endsr
      *=====================================================================******
      *  AVSLUTT
      *=====================================================================******
     C     EXIT          begsr
     C                   callp     wrtsection('endhtml')
     C                   callp     wrtsection('*fini')
     C                   close     BRIDF
     C                   close     GSSTRL01
     C                   close     BRPARF
     C                   eval      *inlr = *on
      *
     C                   endsr
