      * SHIP START
      * ENDRINGER FOR SHIPS-SPEED OSLO ER MERKET SOM VIST HER
      * KONVERTERER OSL TIL FBU
      * SHIP SLUTT
     F*
     F*****************************************************************
     F*** PROGRAM FOR KONVERTERING AV FLYRATER ( ARILH +  ARILD )   ***
     F*****************************************************************
     F*
     FKODTPB    IF   E           K DISK
     FKODTPF    IF   E           K DISK
     FAIRLH     IF A E             DISK
     FAIRLD     IF A E             DISK
     C*
     C* DUMMY
     C  N99
     CAN 99              READ      AIRLH                                  99
     C  N99
     CAN 99              READ      AIRLD                                  99
     C* FASTE VERDIER FOR HEADING FILE
     C                   CLEAR                   AIRLHR
     C                   Z-ADD     0             AIRLIN
      * SHIP START
      *                    MOVEL'OSL'     AIRPFR
     C                   MOVEL     'FBU'         AIRPFR
      * SHIP SLUTT
     C                   MOVEL     'NOK'         AIRCUR
     C                   Z-ADD     0             AIROMR
     C                   MOVEL     'N'           AIRIAT
     C*
     C* KONVERTERER FØRST HEADING FILEN
     C* FØRST KODTPB
     C     *LOVAL        SETLL     KODTPB
     C                   READ      KODTPB                                 90
     C     *IN90         DOWEQ     '0'
     C                   MOVEL     KPBSD         AIRPTO
     C                   WRITE     AIRLHR
     C                   READ      KODTPB                                 90
     C                   ENDDO
     C* SÅ KODTPF
     C     *LOVAL        SETLL     KODTPF
     C                   READ      KODTPF                                 90
     C     *IN90         DOWEQ     '0'
     C                   Z-ADD     KPFFNR        AIRLIN
     C                   MOVEL     KPFSDF        AIRPFR
      * SHIP START
     C     AIRPFR        IFEQ      'OSL'
     C                   MOVEL     'FBU'         AIRPFR
     C                   ENDIF
      * SHIP SLUTT
     C                   MOVEL     KPFSD         AIRPTO
     C                   WRITE     AIRLHR
     C                   READ      KODTPF                                 90
     C                   ENDDO
     C*
     C* KONVERTERER SÅ DETAIL FILEN
     C* FØRST KODTPB
     C                   CLEAR                   AIRLDR
      * SHIP START
      *                    MOVEL'OSL'     AIRPFR
     C                   MOVEL     'FBU'         AIRPFR
      * SHIP SLUTT
     C                   MOVEL     'B'           AIRCCP
     C                   Z-ADD     1             AIRPRP
     C                   Z-ADD     99999         AIRWEI
     C                   Z-ADD     0             AIRHAW
     C     *LOVAL        SETLL     KODTPB
     C                   READ      KODTPB                                 90
     C     *IN90         DOWEQ     '0'
     C                   MOVEL     KPBSD         AIRPTO
      * PRIORITY RATENE --> SAS
     C                   Z-ADD     117           AIRLIN
     C                   MOVEL     'P'           AIRCOD
     C                   Z-ADD     KPBBPR        AIRGRN
     C                   Z-ADD     KPBRPR        AIRAWB
     C                   WRITE     AIRLDR
      * BASIC RATENE --> DEFAULT SELSKAP 0
     C                   Z-ADD     0             AIRLIN
     C                   MOVEL     'B'           AIRCOD
     C                   Z-ADD     KPBBAN        AIRGRN
     C                   Z-ADD     KPBRAN        AIRAWB
     C                   WRITE     AIRLDR
     C                   READ      KODTPB                                 90
     C                   ENDDO
     C* SÅ KODTPF
     C                   CLEAR                   AIRLDR
     C                   MOVEL     'B'           AIRCCP
     C                   Z-ADD     1             AIRPRP
     C     *LOVAL        SETLL     KODTPF
     C                   READ      KODTPF                                 90
     C     *IN90         DOWEQ     '0'
     C                   Z-ADD     KPFFNR        AIRLIN
     C                   MOVEL     KPFSDF        AIRPFR
      * SHIP START
     C     AIRPFR        IFEQ      'OSL'
     C                   MOVEL     'FBU'         AIRPFR
     C                   ENDIF
      * SHIP SLUTT
     C                   MOVEL     KPFSD         AIRPTO
      * LAGER UTFRA NORMALRATE ( INNTIL 45 KG )
     C                   MOVEL     'N'           AIRCOD
     C                   Z-ADD     44            AIRWEI
     C                   Z-ADD     KPFIM         AIRGRN                         GRUNN SINGEL
     C                   Z-ADD     KPFIN         AIRAWB                         PRIS SINGEL
     C                   Z-ADD     KPFSM         AIRHAM                         GRUNN SAMLAST
     C                   Z-ADD     KPFSN         AIRHAW                         PRIS SAMLAST
     C                   WRITE     AIRLDR
      * LAGER UTFRA QUANTITY RATE ( INNTIL 100 KG )
     C                   Z-ADD     0             AIRGRN
     C                   Z-ADD     0             AIRHAM
     C                   MOVEL     'Q'           AIRCOD
     C                   Z-ADD     99            AIRWEI
     C                   Z-ADD     KPFI45        AIRAWB
     C                   Z-ADD     KPFS45        AIRHAW
     C                   WRITE     AIRLDR
      * LAGER UTFRA QUANTITY RATE ( INNTIL 300 KG )
     C                   Z-ADD     299           AIRWEI
     C                   Z-ADD     KPFI10        AIRAWB
     C                   Z-ADD     KPFS10        AIRHAW
     C                   WRITE     AIRLDR
      * LAGER UTFRA QUANTITY RATE ( INNTIL 500 KG )
     C                   Z-ADD     499           AIRWEI
     C                   Z-ADD     KPFI30        AIRAWB
     C                   Z-ADD     KPFS30        AIRHAW
     C                   WRITE     AIRLDR
      * LAGER UTFRA QUANTITY RATE ( INNTIL 1000 KG )
     C                   Z-ADD     999           AIRWEI
     C                   Z-ADD     KPFI50        AIRAWB
     C                   Z-ADD     KPFS50        AIRHAW
     C                   WRITE     AIRLDR
      * LAGER UTFRA QUANTITY RATE ( INNTIL 99999 KG )
     C                   Z-ADD     99999         AIRWEI
     C                   Z-ADD     KPFI1         AIRAWB
     C                   Z-ADD     KPFS1         AIRHAW
     C                   WRITE     AIRLDR
      *
     C                   READ      KODTPF                                 90
     C                   ENDDO
      *
     C                   SETON                                        LR
