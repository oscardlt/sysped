********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 YBC PTF10 ENDGELSK BESKRIVELSE
xxxxxx*xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
11XXX * 02 YBC PTF11 KOPIERE NORSK TEKST TIL ENGELSK TEKST
11XXX * 03 JOV PTF11 INITIER TELLGE - KODE 'ESPEDPRD'- OM IKKE BRUKT FØR
********************************************************************
      *
      ***********************************************************
      *** PROGRAM SKAL VEDLIKEHOLDE PRODUKTER FOR WEB-KUNDER  ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01 02 07 09-11 13-24
      *** LEDIGE INDIKATORER 27-29 31 35-39 41-84 95-97
      ***********************************************************
      *
     FFIRM      IF   E           K DISK
     FCUNDL01   IF   E           K DISK
     FKUFAST    IF   E           K DISK
     FBRPRFL1   UF A E           K DISK
     FBRPRF     IF   E           K DISK
     F                                     RENAME(BRPRFR:BRPRFR0)
S03  F*ELLGE    UF   E           K DISK
N03  FTELLGE    UF A E           K DISK
     FSYES02D   CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
      *
     C                   EXSR      INIT
      *
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     W2KUNR        SETLL     BRPRF
     C     START1        TAG
     C                   READ      BRPRF                                  94
      *
     C     *IN94         DOWEQ     '0'
     C                   ADD       1             ZRECNO
     C                   WRITE     SUBFIL
     C                   READ      BRPRF                                  94
     C  N94              ENDDO
      *
     C                   Z-ADD     ZRECNO        YRECNO            4 0
     C     ZRECNO        IFGT      1
     C                   Z-ADD     1             ZRECNO
     C                   END
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     2             POSNBR
      *
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYVARE
     C                   ELSE
     C                   WRITE     SUBCTL
     C                   EXFMT     BUNN
     C                   END
     C                   SETOFF                                       9830
      *
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C                   Z-ADD     WSSUBN        XRECNO            4 0
      *
      *** AVSLUTT
     C   03              GOTO      SLUTT
      * FRISK OPP
     C   05              GOTO      START
      * LAGE NY
     C   06              DO                                                     1<-B
     C                   Z-ADD     4             LINNBR
     C                   Z-ADD     14            POSNBR
     C                   EXSR      MOVB2
     C                   GOTO      START5
     C                   END                                                    1<-E
      * SØK FRA
     C   08              GOTO      START6
      *
     C     ZRECNO        IFNE      0
     C                   EXSR      HENT
     C                   END
     C                   GOTO      START3
      *
     C*** CREATE A RECORD
     C     START5        TAG
     C                   EXFMT     VARELI
S01  C*                  MOVEA     '000000'      *IN(85)
N01  C                   MOVEA     '0000000'     *IN(84)
     C                   SETOFF                                       30
      *
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
      *
     C     *IN04         IFEQ      *ON
     C     *IN09         OREQ      *ON
     C                   EXSR      SØKDTA
     C                   GOTO      START5
     C                   END
      *
     C  N12              EXSR      TESTB1
     C  N12
     CAN 30              GOTO      START5
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     2             POSNBR
     C                   GOTO      START3
      *
     C     START6        TAG
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     27            POSNBR
     C                   EXFMT     VAREL2
     C   12              GOTO      START3
     C                   GOTO      START
      *
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
      *
     C***********************************************
     C     TESTB1        BEGSR
     C***********************************************
     C     CUND1K        CHAIN     CUNDL01                            90
     c     *IN90         IFEQ      *ON
     C                   MOVE      WSKUNR        XKFKOD
     C                   MOVEL     'ESPEDKUGRP'  XKFTYP
     C     KUFASK        CHAIN     KUFAST                             90
     c  N90              MOVEL     KFTXT         KNAVN
     c                   end
     C   90              DO
     C                   MOVE      'SPK0001'     MSGNR
     C                   Z-ADD     4             LINNBR
     C                   Z-ADD     14            POSNBR
     C                   SETON                                        30
     C                   END
      *
     C     WSLIN         COMP      0                                      89
     C   89
     CANN30              DO
     C                   MOVE      'SPL0005'     MSGNR
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     14            POSNBR
     C                   SETON                                        30
     C                   END
      *
     C     WSKOL         COMP      0                                      88
     C   88
     CANN30              DO
     C                   MOVE      'SPK0027'     MSGNR
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     36            POSNBR
     C                   SETON                                        30
     C                   END
      *
     C     WSTXT         COMP      *BLANKS                                87
     C   87
     CANN30              DO
     C                   MOVE      'YBC1858'     MSGNR
     C                   Z-ADD     7             LINNBR
     C                   Z-ADD     14            POSNBR
     C                   SETON                                        30
     C                   END
N01   *
N02  C                   IF        WSTXTE = *BLANKS
N02  C                   EVAL      WSTXTE = WSTXT
N02  C                   END
N01  C     WSTXTE        COMP      *BLANKS                                84
N01  C   84
N01  CANN30              DO
N01  C                   MOVE      'YBC1858'     MSGNR
N01  C                   Z-ADD     8             LINNBR
N01  C                   Z-ADD     14            POSNBR
N01  C                   SETON                                        30
N01  C                   END
      *
     C     WSAKT         COMP      ' '                                    86
     C   86
     CANN30              DO
     C                   MOVE      'YBC1856'     MSGNR
     C                   Z-ADD     9             LINNBR
     C                   Z-ADD     14            POSNBR
     C                   SETON                                        30
     C                   END
      *
     C     WSPRD         COMP      *BLANKS                                85
     C   85
     CANN30              DO
     C                   Z-ADD     11            LINNBR
     C                   Z-ADD     14            POSNBR
     C                   MOVE      'YBC1859'     MSGNR
     C                   SETON                                        30
     C                   END
      *
     C   30              GOTO      SLTB1
      *
     C     WSRECN        CHAIN     BRPRFL1                            33
     C                   EXSR      MOVWBP
     C                   MOVE      ' '           WSNR
     C   40              UPDATE    SUBFIL
     C  N40              DO
     C                   ADD       1             YRECNO
     C                   Z-ADD     YRECNO        ZRECNO
     C                   WRITE     SUBFIL
     C                   END
     C                   WRITE     SUBCTL
      *
     C  N33              UPDATE    BRPRFR
     C   33              WRITE     BRPRFR
      *
     C     SLTB1         ENDSR
      *
     C***********************************************
     C     HENT          BEGSR
     C***********************************************
     C                   Z-ADD     0             LINNBR
     C                   SETOFF                                       919299
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C   98              DO
     C   99              SETOFF                                       98
     C                   Z-ADD     1             LINNBR
     C                   GOTO      SLHENT
     C                   END
     C     ZRECNO        DIV       15            LINNBR
     C                   MVR                     XLIN              2 0
     C     XLIN          IFEQ      0
     C                   Z-ADD     15            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C     WSNR          IFEQ      '2'
     C     BPTXT         COMP      '*DLT '                                98
     C   98              GOTO      SLHENT
     C                   EXSR      CHGBP
     C   12              GOTO      SLHENT
     C                   END
     C     WSNR          IFEQ      '3'
     C     BPTXT         COMP      '*DLT '                                98
     C   98              GOTO      SLHENT
     C                   EXSR      CPYBP
     C   12              GOTO      SLHENT
     C                   END
     C     WSNR          IFEQ      '4'
     C     BPTXT         COMP      '*DLT '                                98
     C   98              GOTO      SLHENT
     C                   EXSR      DLTBP
     C   12              GOTO      SLHENT
     C                   END
     C     WSNR          IFEQ      '5'
     C     BPTXT         COMP      '*DLT '                                98
     C   98              GOTO      SLHENT
     C                   EXSR      DSPBP
     C   12              GOTO      SLHENT
     C                   END
     C                   END
      *
     C     SLHENT        TAG
     C   98              DO
     C                   MOVE      'DIV0012'     MSGNR
     C                   SETON                                            30
     C                   END
     C                   ADD       4             LINNBR
     C                   Z-ADD     2             POSNBR
     C                   SETOFF                                       99
      *
     C                   ENDSR
      *
     C***********************************************
     C     CHGBP         BEGSR
     C***********************************************
     C                   SETON                                        99
     C                   SETON                                        40
     C                   EXSR      MOVBPW
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     14            POSNBR
     C     STCHG         TAG
     C                   EXFMT     VARELI
S01  C*                  MOVEA     '000000'      *IN(85)
N01  C                   MOVEA     '0000000'     *IN(84)
     C                   MOVE      '0'           *IN30
      *
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
      *
     C     *IN04         IFEQ      *ON
     C     *IN09         OREQ      *ON
     C                   EXSR      SØKDTA
     C                   GOTO      STCHG
     C                   END
     C  N12              EXSR      TESTB1
     C  N12
     CAN 30              GOTO      STCHG
     C   12              DO
     C                   MOVE      ' '           WSNR
     C                   UPDATE    SUBFIL
     C                   EXSR      MOVB2
     C                   END
     C     XLIN          IFEQ      0
     C                   Z-ADD     15            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     CPYBP         BEGSR
     C***********************************************
     C                   SETOFF                                       40
     C                   EXSR      MOVBPW
     C                   MOVE      *ZEROS        WSKUNR
     C                   MOVE      *ZEROS        WSRECN
     C                   SETON                                        99
     C                   MOVE      ' '           WSNR
     C                   UPDATE    SUBFIL
     C                   Z-ADD     4             LINNBR
     C                   Z-ADD     14            POSNBR
      *
     C     STCPY         TAG
     C                   EXFMT     VARELI
S01  C*                  MOVEA     '000000'      *IN(85)
N01  C                   MOVEA     '0000000'     *IN(84)
     C                   MOVE      '0'           *IN30
      *
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
      *
     C     *IN04         IFEQ      *ON
     C     *IN09         OREQ      *ON
     C                   EXSR      SØKDTA
     C                   GOTO      STCPY
     C                   END
     C     *IN12         IFEQ      '1'
     C                   EXSR      MOVB2
     C                   ELSE
     C                   EXSR      TESTB1
     C   30              GOTO      STCPY
     C                   END
     C     XLIN          IFEQ      0
     C                   Z-ADD     15            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     DLTBP         BEGSR
     C***********************************************
     C                   SETON                                        99
     C                   EXFMT     VAREL3
     C  N12              DO
     C     BPRECN        CHAIN     BRPRFL1                            33
     C  N33              DELETE    BRPRFR
     C                   EXSR      MOVB1
     C                   MOVE      '0'           *IN32
     C                   END
     C     XLIN          IFEQ      0
     C                   Z-ADD     15            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C                   UPDATE    SUBFIL
      *
     C                   ENDSR
      *
     C***********************************************
     C     DSPBP         BEGSR
     C***********************************************
     C                   SETON                                        99
     C                   EXFMT     VAREL3
     C     XLIN          IFEQ      0
     C                   Z-ADD     15            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C                   UPDATE    SUBFIL
      *
     C                   ENDSR
      *
     C***********************************************
     C     MOVWBP        BEGSR
     C***********************************************
     C     *IN33         IFEQ      '1'
     C     'ESPEDPRD'    CHAIN     TELLGE                             34
N03  C                   IF        *IN34 = *ON
N03  C                   EVAL      GECO  = 'ESPEDPRD'
N03  C                   EVAL      GENO  = 1000
N03  C                   WRITE     TELLGER
N03  C     'ESPEDPRD'    CHAIN     TELLGE                             34
N03  C                   ENDIF
     C                   MOVE      GENO          WSRECN
     C                   ADD       1             GENO
     C                   UPDATE    TELLGER
     C                   END
     C                   MOVE      WSRECN        BPRECN
     C                   MOVE      WSKUNR        BPKUNR
     C                   MOVE      WSLIN         BPLIN
     C                   MOVE      WSKOL         BPKOL
     C                   MOVE      WSTXT         BPTXT
N01  C                   MOVE      WSTXTE        BPTXTE
     C                   MOVE      WSPRD         BPPRD
     C                   MOVE      WSPRD2        BPPRD2
     C                   MOVE      WSBILD        BPBILD
     C                   MOVE      WSAKT         BPAKT
      *
     C                   ENDSR
      *
     C***********************************************
     C     MOVBPW        BEGSR
     C***********************************************
     C                   MOVE      BPRECN        WSRECN
     C                   MOVE      BPKUNR        WSKUNR
     C     CUND1K        CHAIN     CUNDL01                            33
     c     *IN33         IFEQ      *ON
     C                   MOVE      WSKUNR        XKFKOD
     C                   MOVEL     'ESPEDKUGRP'  XKFTYP
     C     KUFASK        CHAIN     KUFAST                             33
     c  N33              MOVEL     KFTXT         KNAVN
     c                   end
     C   33              MOVE      *BLANKS       KNAVN
     C                   MOVE      BPLIN         WSLIN
     C                   MOVE      BPKOL         WSKOL
     C                   MOVE      BPTXT         WSTXT
N01  C                   MOVE      BPTXTE        WSTXTE
     C                   MOVE      BPPRD         WSPRD
     C                   MOVE      BPPRD2        WSPRD2
     C                   MOVE      BPBILD        WSBILD
     C                   MOVE      BPAKT         WSAKT
      *
     C                   ENDSR
      *
     C***********************************************
     C     MOVB1         BEGSR
     C***********************************************
     C                   CLEAR                   BRPRFR
     C                   MOVEL     '*DLT'        BPTXT
      *
     C                   ENDSR
      *
     C***********************************************
     C     MOVB2         BEGSR
     C***********************************************
     C                   SETOFF                                       40
     C                   MOVE      *ZEROS        WSRECN
     C                   MOVE      *ZEROS        WSKUNR
     C                   MOVE      *ZEROS        WSLIN
     C                   MOVE      *ZEROS        WSKOL
     C                   MOVE      *BLANKS       KNAVN
     C                   MOVE      *BLANKS       WSTXT
N01  C                   MOVE      *BLANKS       WSTXTE
     C                   MOVE      *BLANKS       WSPRD
     C                   MOVE      *BLANKS       WSPRD2
     C                   MOVE      *BLANKS       WSBILD
     C                   MOVE      *BLANKS       WSAKT
      *
     C                   ENDSR
      *
     C***********************************************
     C     SØKDTA        BEGSR
     C***********************************************
     C     LINNBR        IFEQ      4
     C     *IN40         andne     *ON
     C     POSNBR        ANDGE     14
     C     POSNBR        ANDLE     21
     C     *IN04         ANDeq     *ON
     C                   MOVE      *BLANKS       USØKKN
     C                   CALL      'SOEKKUN'
     C     WSKUNR        PARM      WSKUNR        UKN               8 0
     C                   PARM                    FIFIRM
     C                   PARM                    USØKKN           30
     C                   Z-ADD     5             POSNBR
     C                   Z-ADD     14            POSNBR
     C                   GOTO      SLSØK
     C                   END
      *
     C     LINNBR        IFEQ      4
     C     *IN40         andne     *ON
     C     POSNBR        ANDGE     14
     C     POSNBR        ANDLE     21
     C     *IN09         ANDEQ     '1'
     C                   MOVE      'J'           UUPDAT
     C                   MOVEL     'ESPEDKUGRP'  XKFTYP
     C                   MOVE      *BLANKS       XKFKOD
     C                   CALL      'SYKUKD'
     C                   PARM                    XKFTYP
     C                   PARM                    UUPDAT            1
     C                   PARM                    XKFKOD
     c     XKFKOD        IFGE      '00000'
     c     XKFKOD        andle     '99999'
     C                   MOVEL     '999'         KNR8A             8
     C                   MOVE      XKFKOD        KNR8A
     C                   MOVE      KNR8A         WSKUNR
     C                   Z-ADD     5             POSNBR
     C                   Z-ADD     14            POSNBR
     C                   end
     C                   GOTO      SLSØK
     C                   END
      *
     C                   MOVE      'DIV1037'     MSGNR
     C                   SETON                                        30
      *
     C     SLSØK         ENDSR
      *
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     CUND1K        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    WSKUNR
     C     KUFASK        KLIST
     C                   KFLD                    XKFTYP
     C                   KFLD                    XKFKOD
     C     *LIKE         DEFINE    KFTYP         XKFTYP
     C     *LIKE         DEFINE    KFKOD         XKFKOD
      *
     C     FIFIRM        SETLL     FIRM
     C                   READ      FIRM                                   99
      *
     C                   ENDSR
      *
