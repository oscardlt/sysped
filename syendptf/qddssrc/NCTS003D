MRK01 * !!! FELTER SOM SYSPED IKKE BRUKER
MRK02 * !!! CONTAINER/SEALS ER FLYTTET TIL ITEM
     A                                      DSPSIZ(27 132 *DS4)
     A                                      PRINT
     A                                      CA03(03 'Avslutt program')
     A                                      CF04(04 'Søking')
     A                                      CA12(12 'Gå til forrige bilde')
     A                                      HLPPNLGRP(HLPSCR TR003PNL)
     A                                      HLPTITLE('VEDLIKEHOLD AV AVDELINGSO-
     A                                      PPLYSNINGER TET')
     A                                      HELP
     A                                      ALTHELP
      *
     A          R NC003D01
     A                                  1  3'SYSPED/NCTS003R'
     A                                      COLOR(BLU)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(GRN)
     A                                  1 70TIME
     A                                      COLOR(GRN)
     A                                  1 20'Vedlikehold av standardopplysninge-
     A                                      r'
     A                                      COLOR(WHT)
     A                                  2  3MSGCON(012 DIV1451 *LIBL/SYMSGF)
     A                                      COLOR(TRQ)
     A            FIFIRM         2A  O  2 16TEXT('FIRMAKODE')
     A                                      COLOR(TRQ)
     A  27                              2 24'Forhåndsvarsling import'
     A                                      COLOR(WHT)
     A N27                              2 24'Elektronisk transittering UT'
     A                                      COLOR(WHT)
     A                                  2 58MSGCON(022 DIV0047 *LIBL/SYMSGF)
     A                                      COLOR(TRQ)
     A                                  4  2MSGCON(040 DIV2055 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  6  2MSGCON(034 SSI0030 *LIBL/SYMSGF)
     A            WSAAVD         4Y 0B  6 39DSPATR(UL)
     A  99                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                 26  2'F3 = Avslutt'
     A                                      COLOR(BLU)
     A  99                             27  2MSGCON(078 SPA0001 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
      *
     A          R NC003D02
     A                                  1  3'SYSPED/NCTS003R'
     A                                      COLOR(BLU)
     A                                  1 20'Vedlikehold av standardopplysninge-
     A                                      r'
     A                                      COLOR(WHT)
     A                                  1 76'Del-1'
     A                                      COLOR(YLW)
     A                                  2 24'NCTS avsender'
     A                                      COLOR(WHT)
     A                                  4  2'Avdeling:'
     A            WSAAVD         4Y 0O  4 15TEXT('AVDELINGSNR')
     A                                      COLOR(BLU)
     A                                      EDTCDE(4)
     A            KOANVN        33A  O  4 28TEXT('AVDELINGSNAVN')
     A                                      COLOR(BLU)
     A                                  6  2MSGCON(040 DIV2055 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  7  4'Antall eksemplar følgeseddel:'
     A            WSNTTD         1Y 0B  7 40TEXT('Antall følgeseddel')
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
     A                                  8  4'Antall eksamplar lasteliste:'
     A            WSNTLL         1Y 0B  8 40TEXT('Antall lasteliste')
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
     A                                  9  4'Trykk lastelisteformular:'
     A            WSFMLL         1A  B  9 40DSPATR(UL)
     A                                      VALUES(' ' 'J' 'N')
     A                                  9 48'(J=Ja, N=Nei)'
     A                                 10  4MSGCON(030 DIV1714 *LIBL/SYMSGF)
     A            WSTDN          7Y 0B 10 40TEXT('TELLER TIL SAD-IMPORT')
     A                                      DSPATR(UL)
     A  98                                  DSPATR(RI)
     A  98                                  DSPATR(PC)
     A                                      EDTCDE(4)
     A                                 12  2MSGCON(040 DIV2057 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 13  4'Forenklet prosedyre?'
     A            WSENKL         1A  B 13 40TEXT('FORENKLET PROSEDYRE?')
     A                                      DSPATR(UL)
     A                                      VALUES('J' 'N')
     A                                 13 48'(J=Forenklet, N=Normal)'
     A                                 14  4MSGCON(033 DIV1722 *LIBL/SYMSGF)
     A            W0004         35A  B 14 40TEXT('NODI ID AVDELINGEN')
     A                                      DSPATR(UL)
     A  95                                  DSPATR(RI)
     A  95                                  DSPATR(PC)
     A  95                                  COLOR(RED)
     A                                 15  6MSGCON(033 DIV1721 *LIBL/SYMSGF)
     A            W0010         35A  B 15 40TEXT('NODI ID TOLLVESENET')
     A                                      DSPATR(UL)
     A  94                                  DSPATR(RI)
     A  94                                  DSPATR(PC)
     A  94                                  COLOR(RED)
     A*                                16  8'Application Reference:'
     A*           W0026         14A  B 16 40
     A                                 17  4MSGCON(033 DIV1723 *LIBL/SYMSGF)
     A            W0035          1A  B 17 40TEXT('Testindikator')
     A                                      VALUES(' ' '1' '2')
     A                                 19  4'Importeres eksterne data ? '
     A            WSEKST         1   B 19 40VALUES('J' 'N' 'R')
     A                                 19 48'J/N/R  Ved R kan egen ref'
     A                                 20 48'inntastes for søking'
     A                                 26  2'F3=Avslutt uten endring'
     A                                      COLOR(BLU)
     A                                 26 28'F12=Gå til forrige uten endring'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 27  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R NC003D03
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  3'SYSPED/NCTS003R'
     A                                      COLOR(BLU)
     A                                  1 20'Default verdier av NCTS avg.'
     A                                      COLOR(WHT)
     A                                  1 52'Ref.'
     A            WSAAVD         4Y 0O  1 57COLOR(WHT)
     A                                      EDTCDE(4)
     A            WSTDN          7Y 0O  1 62COLOR(WHT)
     A                                      EDTCDE(4)
     A            THSG           3A  O  1 70COLOR(WHT)
     A                                  1 76'Del-2'
     A                                      COLOR(YLW)
     A                                  2  2'Dekl.ty'
     A            WSDK           5A  B  2 10TEXT('Deklarasjonstype')
     A                                      DSPATR(UL)
     A  99                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSDKT          1A  B  2 17TEXT('Deklarasjonstype')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  2 19'Autor:'
     A            WSATYP         4A  B  2 26TEXT('Autorisasjontype')
     A                                      DSPATR(UL)
     A  98                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSAREF        35A  B  2 31TEXT('Autorisasjonsref')
     A                                      DSPATR(UL)
     A  97                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                  2 67'Las.liste'
     A            WSLSTL         4Y 0B  2 77EDTCDE(4)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  2 82'V.post'
     A            WSVPOS         5Y 0B  2 90EDTCDE(4)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  2 96'Kolli'
     A            WSNTK          7Y 0B  2104EDTCDE(4)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  2112'B.vkt'
     A            WSVKB         13Y 3B  2118EDTCDE(4)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  3  2'Tollsted:'
     A                                  3 12'Avg'
     A            WSCATS         8A  B  3 16TEXT('AVG.TOLLSTED')
     A                                      DSPATR(UL)
     A  96                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                  3 25'Ank'
     A            WSTSB          8A  B  3 29TEXT('Bestemmels tollsted')
     A                                      DSPATR(UL)
     A  95                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                  3 38'Transit'
     A            WSTSD1         8A  B  3 46TEXT('Transitt tollsted 1')
     A                                      DSPATR(UL)
     A  94                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSTSD2         8A  B  3 55TEXT('Transitt tollsted 2')
     A                                      DSPATR(UL)
     A  93                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSTSD3         8A  B  3 64TEXT('Transitt tollsted 3')
     A                                      DSPATR(UL)
     A  92                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A*           WSTSD4         8A  B  3 73TEXT('Transitt tollsted 4')
     A*                                     DSPATR(UL)
     A* 91                                  DSPATR(RI)
     A*                                     COLOR(YLW)
     A*           WSTSD5         8A  B  3 82TEXT('Transitt tollsted 5')
     A*                                     DSPATR(UL)
     A* 90                                  DSPATR(RI)
     A*                                     COLOR(YLW)
     A*           WSTSD6         8A  B  3 91TEXT('Transitt tollsted 6')
     A*                                     DSPATR(UL)
     A* 89                                  DSPATR(RI)
     A*                                     COLOR(YLW)
     A*           WSTSD7         8A  B  3100TEXT('Transitt tollsted 7')
     A*                                     DSPATR(UL)
     A* 88                                  DSPATR(RI)
     A*                                     COLOR(YLW)
     A*           WSTSD8         8A  B  3109TEXT('Transitt tollsted 8')
     A*                                     DSPATR(UL)
     A* 87                                  DSPATR(RI)
     A*                                     COLOR(YLW)
     A                                  3 75'Aut. næringsliv:'
     A            WSCNA1        12A  B  3 92TEXT('Autorisert næringsliv')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSCNA2        10A  B  3105TEXT('Autorisert næringsliv')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSCANR1        6A  B  3116TEXT('Autorisasjonsnr')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSCANR2        6A  B  3123TEXT('Autorisasjonsnr')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  4  2'Holder:'
     A            WSKNH          8Y 0B  4 12TEXT('Kundenr holder')
     A                                      DSPATR(UL)
     A  86                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSTINH        17A  B  4 21TEXT('ID holder')
     A                                      DSPATR(UL)
     A  86                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                  4 39'TIR ID'
     A            WSTIRH        17A  B  4 46TEXT('TIR ID holder')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  4 77'Dato:'
     A            WSCDT          6Y 0B  4 84TEXT('Akseptdato')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(Y)
     A                                  4 95'Nødpros. nummerserie:'
     A            WSNPNS         1A  B  4117DSPATR(UL)
     A  91                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                  5  2'NA/AD/PN/PS/LK'
     A            WSNAH         30A  B  5 17TEXT('Navn holder')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSAD1H        30A  B  5 48TEXT('Adr1 holder')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSPNH         17A  B  5 79TEXT('Postnr holder')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSPSH         24A  B  5 97TEXT('Poststed holder')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSLKH          2A  B  5122TEXT('Landkode holder')
     A                                      DSPATR(UL)
     A  85                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                  6  2'K.person/Tlf/Epost'
     A            WSCPH         30A  B  6 21TEXT('Kontaktperson holder')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSTLH         20A  B  6 52TEXT('Telefonnr holder')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSEMH         50A  B  6 73TEXT('Epostadr holder')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
MRK01A*                                 7  2'Repre.:'
MRK01A*           WSKNA          8Y 0B  7 12TEXT('Kundenr representative')
MRK01A            WSKNA          8Y 0H
MRK01A*                                     DSPATR(UL)
MRK01A* 84                                  DSPATR(RI)
MRK01A*                                     COLOR(YLW)
MRK01A*                                     EDTCDE(4)
MRK01A*           WSTINA        17A  B  7 21TEXT('ID representative')
MRK01A            WSTINA        17A  H
MRK01A*                                     DSPATR(UL)
MRK01A* 83                                  DSPATR(RI)
MRK01A*                                     COLOR(YLW)
MRK01A*           WSSTA          1Y 0B  7 39TEXT('Status representative')
MRK01A            WSSTA          1Y 0H
MRK01A*                                     DSPATR(UL)
MRK01A*                                     COLOR(YLW)
MRK01A*                                     EDTCDE(4)
MRK01A*                                 8  2'K.person/Tlf/Epost'
MRK01A*           WSCPA         30A  B  8 21TEXT('Kontaktperson representative')
MRK01A            WSCPA         30A  H
MRK01A*                                     DSPATR(UL)
MRK01A*                                     COLOR(YLW)
MRK01A*           WSTLA         20A  B  8 52TEXT('Telefonnr representative')
MRK01A            WSTLA         20A  H
MRK01A*                                     DSPATR(UL)
MRK01A*                                     COLOR(YLW)
MRK01A*           WSEMA         50A  B  8 73TEXT('Epostadr representative')
MRK01A            WSEMA         50A  H
MRK01A*                                     DSPATR(UL)
MRK01A*                                     COLOR(YLW)
MRK01A*                                     CHECK(LC)
     A                                  9  2'Garanti'
     A            WSGKD          1A  B  9 12TEXT('Garanti kode')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSGFT1        24A  B  9 14TEXT('Garanti ref.nr')
     A                                      DSPATR(UL)
     A  82                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSGFT2        35A  B  9 39TEXT('Annen Garanti')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSGADK         4A  B  9 75TEXT('Adgangkode')
     A                                      DSPATR(UL)
     A  81                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                  9 80'Garanti %:'
     A            WSGPR          3Y 1B  9 91TEXT('PROSENT')
     A                                      DSPATR(UL)
     A  80                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  9 96'Gar. bel:'
     A            WSGBL         11Y 0B  9106TEXT('GARANTI')
     A                                      DSPATR(UL)
     A  80                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(Z)
     A                                  9118'VAL.'
     A            WSGVK          3A  B  9123TEXT('VALUTAKODE')
     A                                      DSPATR(UL)
     A  79                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 10  2'Avs.LK'
     A            WSALK          2A  B 10 12TEXT('Landk avs/utf-land')
     A                                      DSPATR(UL)
     A  78                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 10 15'Best.LK'
     A            WSBLK          2A  B 10 23TEXT('Landk avs/utf-land')
     A                                      DSPATR(UL)
     A  77                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 10 26'Containe'
     A            WSKDC          1A  B 10 35TEXT('Container 0/1')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 10 37'Tr.mode innl/grense'
     A            WSTMIN         1Y 0B 10 57TEXT('Tr.mode innland')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(9)
     A            WSTMGR         1Y 0B 10 59TEXT('Tr.mode grense')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(9)
     A                                 10 61'UCR ref'
     A            WSUCR         35A  B 10 69TEXT('Ref.nr UCR')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 11  2'Tr.portør'
     A            WSKNC          8Y 0B 11 12TEXT('Kundenr transportør')
     A                                      DSPATR(UL)
     A  76                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSTINC        17A  B 11 21TEXT('ID transportør')
     A                                      DSPATR(UL)
     A  76                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSNAC         30A  B 11 39TEXT('Navn transportør')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSTLC         20A  B 11 70TEXT('Telefonnr transportør')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSEMC         50A  H
     A            WSEMC_40      40A  B 11 91TEXT('Epostadr. transportør')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A                                 12  2'Avsender:'
     A            WSKNS          8Y 0B 12 12TEXT('Kundenr avsender')
     A                                      DSPATR(UL)
     A  75                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSTINS        17A  B 12 21TEXT('ID avsender')
     A                                      DSPATR(UL)
     A  75                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 13  2'NA/AD/PN/PS/LK'
     A            WSNAS         30A  B 13 17TEXT('Navn avsender')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSAD1S        30A  B 13 48TEXT('Adr1 avsender')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSPNS         17A  B 13 79TEXT('Postnr avsender')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSPSS         24A  B 13 97TEXT('Poststed avsender')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSLKS          2A  B 13122TEXT('Landkode avsender')
     A                                      DSPATR(UL)
     A  74                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 14  2'K.person/Tlf/Epost'
     A            WSCPS         30A  B 14 21TEXT('Kontaktperson avsender')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSTLS         20A  B 14 52TEXT('Telefonnr avsender')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSEMS         50A  B 14 73TEXT('Epostadr avsender')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A                                 15  2'Mottaker:'
     A            WSKNK          8Y 0B 15 12TEXT('Kundenr mottaker')
     A                                      DSPATR(UL)
     A  73                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSTINK        17A  B 15 21TEXT('ID mottaker')
     A                                      DSPATR(UL)
     A  73                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 16  2'NA/AD/PN/PS/LK'
     A            WSNAK         30A  B 16 17TEXT('Navn mottaker')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSAD1K        30A  B 16 48TEXT('Adr1 mottaker')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSPNK         17A  B 16 79TEXT('Postnr mottaker')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSPSK         24A  B 16 97TEXT('Poststed mottaker')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSLKK          2A  B 16122TEXT('Landkode mottaker')
     A                                      DSPATR(UL)
     A  72                                  DSPATR(RI)
     A                                      COLOR(YLW)
MRK01A*                                17  2'Actor:'
MRK01A*           WSROLE         3A  B 17 12TEXT('Role actor')
MRK01A            WSROLE         3A  H
MRK01A*                                     DSPATR(UL)
MRK01A*                                     COLOR(YLW)
MRK01A*           WSIDR         17A  B 17 16TEXT('Role ID actor')
MRK01A            WSIDR         17A  H
MRK01A*                                     DSPATR(UL)
MRK01A*                                     COLOR(YLW)
MRK02A*                                17 34'Container (Nr/Ant/Seal)'
MRK02A*           WSCNR         17A  B 17 58TEXT('Containernr')
MRK02A*                                     DSPATR(UL)
MRK02A*                                     COLOR(YLW)
MRK02A*           WSDANT         4Y 0B 17 76TEXT('Antall seal')
MRK02A*                                     DSPATR(UL)
MRK02A*                                     COLOR(YLW)
MRK02A*                                     EDTCDE(4)
MRK02A*           WSDFKD        20A  B 17 81TEXT('Seal')
MRK02A*                                     DSPATR(UL)
MRK02A*                                     COLOR(YLW)
MRK02A*                                17102'Gods ref'
MRK02A*           WSGINR         5Y 0B 17111TEXT('Seal')
MRK02A*                                     DSPATR(UL)
MRK02A*                                     COLOR(YLW)
MRK02A*                                     EDTCDE(4)
MRK01A*                                18  2'Lok:'
MRK01A*                                18  7'T'
MRK01A*           WSLGTY         1A  B 18  9TEXT('Type')
MRK01A            WSLGTY         1A  H
MRK01A*                                     DSPATR(UL)
MRK01A*                                     COLOR(YLW)
MRK01A*                                18 11'I'
MRK01A*           WSLGID         1A  B 18 13TEXT('ID qualifier')
MRK01A            WSLGID         1A  H
MRK01A*                                     DSPATR(UL)
MRK01A*                                     COLOR(YLW)
MRK01A*                                18 15'Aut'
MRK01A*           WSLGAU        35A  B 18 19TEXT('Autorisasjonsnr')
MRK01A            WSLGAU        35A  H
MRK01A*                                     DSPATR(UL)
MRK01A*                                     COLOR(YLW)
MRK01A*                                18 55'ID'
MRK01A*           WSLGAI         4A  B 18 58TEXT('Tilleggs ID')
MRK01A            WSLGAI         4A  H
MRK01A*                                     DSPATR(UL)
MRK01A*                                     COLOR(YLW)
MRK01A*                                18 63'UN'
MRK01A*           WSLGUN        17A  B 18 66TEXT('Tilleggs UN')
MRK01A            WSLGUN        17A  H
MRK01A*                                     DSPATR(UL)
MRK01A*                                     COLOR(YLW)
MRK01A*                                18 84'TS'
MRK01A*           WSLGTS         8A  B 18 87TEXT('Tollsted')
MRK01A            WSLGTS         8A  H
MRK01A*                                     DSPATR(UL)
MRK01A* 71                                  DSPATR(RI)
MRK01A*                                     COLOR(YLW)
MRK01A*           WSLGLA        17A  B 18 96TEXT('Latitude')
MRK01A            WSLGLA        17A  H
MRK01A*                                     DSPATR(UL)
MRK01A*                                     COLOR(YLW)
MRK01A*           WSLGLO        17A  B 18114TEXT('Longitude')
MRK01A            WSLGLO        17A  H
MRK01A*                                     DSPATR(UL)
MRK01A*                                     COLOR(YLW)
MRK01A*                                19  2'Økonomi:'
MRK01A*           WSKNE          8Y 0B 19 12TEXT('Kundenr økonomi operatør')
MRK01A            WSKNE          8Y 0H
MRK01A*                                     DSPATR(UL)
MRK01A* 70                                  DSPATR(RI)
MRK01A*                                     COLOR(YLW)
MRK01A*                                     EDTCDE(4)
MRK01A*           WSTINE        17A  B 19 21TEXT('ID økonomi operatør')
MRK01A            WSTINE        17A  H
MRK01A*                                     DSPATR(UL)
MRK01A*                                     COLOR(YLW)
MRK01A*                                19 39'Husnr'
MRK01A*           WSADE2        17A  B 19 45TEXT('Husnr økonomi operatør')
MRK01A            WSADE2        17A  H
MRK01A*                                     DSPATR(UL)
MRK01A*                                     COLOR(YLW)
MRK01A*           WSPNE2        17A  B 19 63TEXT('Postnr økonomi operatør')
MRK01A            WSPNE2        17A  H
MRK01A*                                     DSPATR(UL)
MRK01A*                                     COLOR(YLW)
MRK01A*           WSLKE2         2A  B 19 81TEXT('Landkode økonomi operatør')
MRK01A            WSLKE2         2A  H
MRK01A*                                     DSPATR(UL)
MRK01A* 69                                  DSPATR(RI)
MRK01A*                                     COLOR(YLW)
MRK01A*                                20  2'AD/PN/PS/LK'
MRK01A*           WSADE1        30A  B 20 48TEXT('Adr1 økonomi operatør')
MRK01A            WSADE1        30A  H
MRK01A*                                     DSPATR(UL)
MRK01A*                                     COLOR(YLW)
MRK01A*           WSPNE         17A  B 20 79TEXT('Postnr økonomi operatør')
MRK01A            WSPNE         17A  H
MRK01A*                                     DSPATR(UL)
MRK01A*                                     COLOR(YLW)
MRK01A*           WSPSE         24A  B 20 97TEXT('Poststed økonomi operatør')
MRK01A            WSPSE         24A  H
MRK01A*                                     DSPATR(UL)
MRK01A*                                     COLOR(YLW)
MRK01A*           WSLKE          2A  B 20122TEXT('Landkode økonomi operatør')
MRK01A            WSLKE          2A  H
MRK01A*                                     DSPATR(UL)
MRK01A* 68                                  DSPATR(RI)
MRK01A*                                     COLOR(YLW)
MRK01A*                                21  2'K.person/Tlf/Epost'
MRK01A*           WSCPE         30A  B 21 21TEXT('Kontaktperson økonomi')
MRK01A            WSCPE         30A  H
MRK01A*                                     DSPATR(UL)
MRK01A*                                     COLOR(YLW)
MRK01A*           WSTLE         20A  B 21 52TEXT('Telefonnr økonomi operatør')
MRK01A            WSTLE         20A  H
MRK01A*                                     DSPATR(UL)
MRK01A*                                     COLOR(YLW)
MRK01A*           WSEME         50A  B 21 73TEXT('Epostadr økonomi operatør')
MRK01A            WSEME         50A  H
MRK01A*                                     DSPATR(UL)
MRK01A*                                     COLOR(YLW)
MRK01A*                                     CHECK(LC)
     A                                 22  2'Tr.m.typ:'
     A            WSTRM          2Y 0B 22 12TEXT('Transportmiddelstype')
     A                                      DSPATR(UL)
     A  67                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(9)
     A                                 22 15'Tr.midl.ID'
     A            WSTAID        35A  B 22 26TEXT('Transportmiddel id')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 22 62'LK'
     A            WSTALK         2A  B 22 65TEXT('Transportmiddel landkode')
     A                                      DSPATR(UL)
     A  66                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 22 70'Rute'
     A            WSLKR1         2A  B 22 75TEXT('Landkode 1 i rute')
     A                                      DSPATR(UL)
     A  65                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSLKR2         2A  B 22 78TEXT('Landkode 2 i rute')
     A                                      DSPATR(UL)
     A  64                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSLKR3         2A  B 22 81TEXT('Landkode 3 i rute')
     A                                      DSPATR(UL)
     A  63                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSLKR4         2A  B 22 84TEXT('Landkode 4 i rute')
     A                                      DSPATR(UL)
     A  62                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSLKR5         2A  B 22 87TEXT('Landkode 5 i rute')
     A                                      DSPATR(UL)
     A  61                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSLKR6         2A  B 22 90TEXT('Landkode 6 i rute')
     A                                      DSPATR(UL)
     A  60                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 23  2'Activ grense:'
     A                                 23 16'Tollsted'
     A            WSTGTS         8A  B 23 25TEXT('Tollsted grense')
     A                                      DSPATR(UL)
     A  59                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 23 34'Type'
     A            WSTRMG         2Y 0B 23 39TEXT('Type ID')
     A                                      DSPATR(UL)
     A  58                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(9)
     A                                 23 42'ID'
     A            WSTGID        35A  B 23 45TEXT('Type ID')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 23 81'LK'
     A            WSTGLK         2A  B 23 84TEXT('Landkode')
     A                                      DSPATR(UL)
     A  57                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 23 87'Flight'
     A            WSTGCN        17A  B 23 94TEXT('Bilnr')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 24  2'La.st'
     A            WSLSUN        17A  B 24  8TEXT('UN lok.kode')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSLSLK         2A  B 24 26TEXT('Landkode')
     A                                      DSPATR(UL)
     A  56                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSLSNA        35A  B 24 29TEXT('Stedsnavn')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 24 65'Lo.st'
     A            WSLOUN        17A  B 24 71TEXT('UN lok.kode')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSLOLK         2A  B 24 89TEXT('Landkode')
     A                                      DSPATR(UL)
     A  55                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSLONA        35A  B 24 92TEXT('Stedsnavn')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 25  2'D.'
     A                                 25  5'Ktl.res'
     A            WSDKR          2A  B 25 13TEXT('Kontroll resultat')
     A                                      DSPATR(UL)
     A  54                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 25 16'Frist(dag)'
     A            WSDDT          3Y 0B 25 27EDTCDE(Z)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A*                                25 32'Språkkode'
     A*           WSSKFD         2A  B 25 42TEXT('Språkkode')
     A*                                     DSPATR(UL)
     A* 54                                  DSPATR(RI)
     A*                                     COLOR(YLW)
     A                                 25 46'Dekl.sted'
     A            WSDST         15A  B 25 57DSPATR(UL)
     A  53                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 25 88'Bet.måte'
     A            WSTRCH         1A  B 25 97TEXT('Betalingsmåte')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 25100'Reg.dato'
     A            THDT           8Y 0H
     A            WSDT           6Y 0O 25109TEXT('Registreringsdato')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(Y)
     A                                 26  1'F3=Avslutt u/oppdatering'
     A                                      COLOR(BLU)
     A                                 26 28'F4=Kodesøking'
     A                                      COLOR(BLU)
     A                                 26 44'F12=Gå til forrige u/oppdatering'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 27  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
