*****A**************************************************************
     A* RETTINGER I DETTE PROGRAM:
PTFnrA*Sek Sig Vers Beskrivelse...........................
"""""A*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
P8068 * 01 TH  PTF8 NY HELPFUNKSJON  KUN DDS
*****A**************************************************************
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
S01  A*                                     HLPDOC('VEDLFANR' 'HELPNU' 'HLP400')
N01  A                                      HLPPNLGRP(HLPSCR SYFA26PNL)
N01  A                                      HLPTITLE('VEDLIKEHOLD AV OPPDRAGSNU-
N01  A                                      MMER /TURNR.(PROSJEKTNR.')
     A                                      HELP
     A                                      ALTHELP
     A          R SUBFIL                    SFL
     A            WSNR           1A  B  6  3VALUES(' ' '2' '4')
     A            TEAVD          4  0O  6 11TEXT('AVDELINGSNR')
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            DLTMSG         7A  O  6 16COLOR(WHT)
     A            TEOPDN         7  0O  6 26TEXT('OPPDRAGSNR')
     A                                      EDTCDE(4)
     A            TETURN         8  0O  6 40TEXT('TURNR')
     A                                      EDTCDE(4)
     A            TETMIN         8  0O  6 57TEXT('MIN.TURNR')
     A                                      EDTCDE(4)
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A            ZRECNO         4S 0O  3 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYSPED/SYFA26'
     A                                      COLOR(BLU)
     A            MSG101        50A  O  1 19MSGID(DIV 1249 SYMSGF)
     A                                      DSPATR(HI)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                  2 70TIME
     A            MSG103        30A  O  3  2MSGID(DIV 0016 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG104        30A  O  4  2MSGID(DIV 0017 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG105        78A  O  5  2MSGID(DIV 1250 SYMSGF)
     A                                      DSPATR(HI)
     A          R BUNN
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF03(03 'EXIT')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSG201        18A  O  3  2MSGID(DIV 0020 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG202        18A  O  3 22MSGID(DIV 0021 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG203        18A  O  3 42MSGID(DIV 0022 SYMSGF)
     A                                      COLOR(BLU)
     A  98        MSG204        78A  O  4  2MSGID(DIV 0012 SYMSGF)
     A                                      COLOR(WHT)
     A N98                              4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A          R VARELI
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF12(12 'Forrige')
     A                                      CF14(14 'GENERELL TELLER')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSG301        65A  O  1  8MSGID(DIV 1251 SYMSGF)
     A                                      COLOR(WHT)
     A            WSAVD          4Y 0B  2 11TEXT('AVDELINGSNR')
     A                                      DSPATR(UL)
     A  40                                  DSPATR(HI)
     A  40                                  DSPATR(PR)
     A  90                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A            WSOPDN         7Y 0B  2 26TEXT('OPPDRAGSNR')
     A  89                                  DSPATR(RI)
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
     A            WSTURN         8Y 0B  2 40TEXT('TURNR')
     A  88                                  DSPATR(RI)
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
     A            WSTMIN         8Y 0B  2 57TEXT('MIN.TURNR')
     A  87                                  DSPATR(RI)
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
     A            MSG302        18A  O  3  2MSGID(DIV 0024 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG802        18A  O  3 22MSGID(DIV 3723 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  4  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A          R VAREL3
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            MSG401        65A  O  1  8MSGID(DIV 1251 SYMSGF)
     A                                      COLOR(WHT)
     A            TEAVD          4Y 0O  2 11TEXT('AVDELINGSNR')
     A                                      DSPATR(PC)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            TEOPDN         7Y 0O  2 26TEXT('OPPDRAGSNR')
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            TETURN         8Y 0O  2 40TEXT('TURNR')
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            TETMIN         8Y 0O  2 57TEXT('MIN.TURNR')
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            MSG402        18A  O  3  2MSGID(DIV 0024 SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A          R NYWRK
     A                                      CF03(03 'EXIT')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                  1  2'SYSPED/SYFA26'
     A                                      COLOR(BLU)
     A            MSG501        50A  O  1 19MSGID(DIV 1249 SYMSGF)
     A                                      DSPATR(HI)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                  2 70TIME
     A            MSG503        30A  O  3  2MSGID(DIV 0016 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG504        30A  O  4  2MSGID(DIV 0017 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG505        78A  O  5  2MSGID(DIV 1250 SYMSGF)
     A                                      DSPATR(HI)
     A            MSG506        18A  O 23  2MSGID(DIV 0020 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG507        18A  O 23 22MSGID(DIV 0021 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG508        18A  O 23 42MSGID(DIV 0022 SYMSGF)
     A                                      COLOR(BLU)
     A                                 24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A          R VAREL2
