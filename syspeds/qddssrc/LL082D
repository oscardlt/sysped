      *
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      REF(*LIBL/LLAGJFE LLAGJER)
     A                                      PRINT
     A                                      HELP
      * "NOERASE" MÅ HA FOR Å IKKE TØMME BILDE SOM MAN KOMME FRA
     A          R NOERASE
     A                                      ASSUME
     A                                  1  2' '
      * MAN KUN DEFINNERE VINDU PÅ REKORDNIVÅ
     A          R WINDOWSZ
     A                                      WINDOW(*DFT 18 68 *NOMSGLIN)
     A                                      OVERLAY
      *
     A          R SUBFIL                    SFL
     A            VAMN      R        H
     A            VAANR     R        H
     A            VAVKF0    R        H
     A            EVSN      R        H
     A            EVTYPE    R        H
     A            EVBOND    R        H
     A            WSNR           1A  I  6  2VALUES(' ' '5')
     A            EVOANT    R        O  6  4EDTCDE(Z)
     A            ITETXT20      20   O  6 12
     A            ITETXT    R        H
     A            VAVKT0    R        O  6 33EDTCDE(4)
     A            VAKUB0    R        O  6 42EDTCDE(4)
     A            VAOPLK    R        O  6 51
     A            VAFAR     R        H
     A            VAVAL          3   O  6 54
     A            VAVERD    R        O  6 58EDTCDE(4)
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0012)
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A                                      CA03(03 'Forrige')
     A                                      CA05(05 'Frisk')
     A                                      CA12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'LL082R '
     A                                      COLOR(BLU)
     A                                  1 11'Vis sammenslåings detaljer'
     A                                      COLOR(WHT)
     A            ZRECNO         4S 0B  1 42SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  2  2'Artikkelnr:'
     A            VAANR         15A  O  2 14COLOR(WHT)
     A                                  2 32'Dato inn:'
     A            VABDAT         8Y 0O  2 42EDTWRD('    -  -  ')
     A                                      COLOR(WHT)
     A                                  3  2'Fakturanr :'
     A            FAKTXT15      15A  O  3 14COLOR(WHT)
     A                                  3 32'Tariffnr:'
     A            TARTXT15      15A  O  3 42COLOR(WHT)
     A                                  4  2'Velg opsjon:   5=Vise'
     A                                      COLOR(BLU)
     A                                  5  2'Op Antall Item-nr                 -
     A                                       Vekt   Kubikk LK Val  Enh.pris'
     A                                      COLOR(WHT)
      *
     A          R BUNN
     A                                      WINDOW(WINDOWSZ)
     A                                      SLNO(18)
     A                                      CLRL(01)
     A  91N93N94                            ROLLUP(25)
     A                                      CA03(03 'Forrige')
     A                                      CA05(05 'Frisk')
     A                                      CA12(12 'Forrige')
     A                                      OVERLAY
     A            MSGNR          7A  P
     A                                  1  2'F3/F12=Avbryt   F5=Frisk'
     A                                      COLOR(BLU)
     A  30        MSGTXT        60A  O  1  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
      *
     A          R NYWRK
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A                                      CA03(03 'Forrige')
     A                                      CA05(05 'Frisk')
     A                                      CA12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1  2'LL082R '
     A                                      COLOR(BLU)
     A                                  1 11'Vis sammenslåings detaljer'
     A                                      COLOR(WHT)
     A                                  2  2'Artikkelnr:'
     A            VAANR         15A  O  2 14COLOR(WHT)
     A                                  2 32'Dato inn:'
     A            VABDAT         8Y 0O  2 42EDTWRD('    -  -  ')
     A                                      COLOR(WHT)
     A                                  3  2'Fakturanr :'
     A            FAKTXT15      15A  O  3 14COLOR(WHT)
     A                                  3 32'Tariffnr:'
     A            TARTXT15      15A  O  3 42COLOR(WHT)
     A                                  4  2'Velg opsjon:   5=Vise'
     A                                      COLOR(BLU)
     A                                  5  2'Op Antall Item-nr                 -
     A                                       Vekt   Kubikk LK Val  Enh.pris'
     A                                      COLOR(WHT)
     A                                 18  2'F3/F12=Avbryt   F5=Frisk'
     A  30        MSGTXT        60A  O 18  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
      *
     A          R BILDE
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A                                      CA12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'LL082R '
     A                                      COLOR(BLU)
     A                                  1 11'Vis sammenslåings detaljer'
     A                                      COLOR(WHT)
     A                                  3  2'Artikkelnr:'
     A            VAANR         15A  O  3 14COLOR(WHT)
     A                                  4  2'Item-nr   :'
     A            ITETXT        35A  O  4 14COLOR(WHT)
     A                                  6  2'Fakturanr :'
     A            FAKTXT        35A  O  6 14COLOR(WHT)
     A                                  7  2'Tariffnr  :'
     A            TARTXT        35A  O  7 14COLOR(WHT)
     A                                  9  2'Landkode  :'
     A            VAOPLK         2A  O  9 14COLOR(WHT)
     A                                  9 32'Valutakode:'
     A            VAVAL          3A  O  9 44COLOR(WHT)
     A                                 11  2'Enhetspris:'
     A            VAVERD         9Y 2O 11 14COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 12  2'Antall stk:'
     A            EVOANT         7Y 0O 12 14COLOR(WHT)
     A                                      EDTCDE(Z)
     A                                 13  2'Enhets vkt:'
     A            VAVKT0         7Y 2O 13 14COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 14  2'Fraktb.vkt:'
     A            VAVKF0         7Y 2O 14 14COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 15  2'Enhets M3 :'
     A            VAKUB0         7Y 4O 15 14COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 18  2'F12=Avbryt'
     A                                      COLOR(BLU)
