      **************************************************************
      *** PROGRAM SKAL ARBEIDE MED AMETAAVREGNING                ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER       ***
      *** LEDIGE INDIKATORER                                     ***
      **************************************************************
      *
     FFIRM      IF   E           K DISK
     FCUNDL01   IF   E           K DISK
     FHEAD11    IF   E           K DISK
     FAMWTUAG   UF A E           K DISK
     FAMHE      UF   E           K DISK
     FAMDEWRK   UF   E           K DISK
     FAMGEWRK   UF   E           K DISK
     FSYAM00FM  CF   E             WORKSTN
     F                                     SFILE(SUBFILA:ZRECNA)
     C*
     C     *ENTRY        PLIST
     C                   PARM                    UIN10             1
     C                   PARM                    UTURNR            8 0
     C                   PARM                    UAGENT            8 0
     C                   MOVE      '0'           UIN10
     C*
     C                   EXSR      INIT
     C*
     C     CHOISE        TAG
     C*
     C                   Z-ADD     04            LINNBR
     C                   Z-ADD     29            POSNBR
     C                   EXFMT     BILDE1
     C**  AVSLUTT
     C     *IN03         IFEQ      '1'
     C                   EXSR      RSETAM
     C                   Z-ADD     W1TNR         UTURNR
     C                   GOTO      SLUTT
     C                   END                                                    <*IN03
     C*
     C                   EXSR      TESTB1
     C   30              GOTO      CHOISE
     C*
     C*********** TØM TUR/AGENT-WORKFILE ************************
     C     W1TNR         SETLL     AMWTUAG
     C     W1TNR         READE     AMWTUAG                                33
     C     *IN33         DOWEQ     '0'
     C                   DELETE    AMWTUAGR
     C     W1TNR         READE     AMWTUAG                                33
     C                   END
     C***************** A G E N T E R ***************************
     C*
     C*  SUBFILE CLEAR
     C     STARTA        TAG
     C                   MOVEA     '00'          *IN(91)
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTLA
     C                   MOVEA     '00'          *IN(93)
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C                   CLEAR                   ZRECNA
     C                   CLEAR                   MRECNA
     C                   CLEAR                   HEKNA
     C* FINN ALLE OPPDRAG PÅ TUREN
     C     HEA11K        SETLL     HEAD11
     C     HELE11        TAG
     C     HEA11K        READE     HEAD11                                 34
     C     *IN34         IFEQ      *ON
     C                   GOTO      STARA2
     C                   END
     C*Finnes tur/agenten  i fila (subfila) fra før...
     C     AMWKEY        CHAIN     AMWTUAG                            33
     C*.... skal uansett legges i workfile for bruk i SYAM01
     C                   MOVE      HEPRO         WATUNR
     C                   MOVE      HEKNA         WAKNA
     C                   MOVE      HEPOS1        WAPOS1
     C                   MOVE      HEAVD         WAAVD
     C                   MOVE      HEOPD         WAOPD
     C                   WRITE     AMWTUAGR
     C*Fantes fra før.. ikke legg i subfil
     C     *IN33         IFEQ      *OFF
     C                   GOTO      HELE11
     C                   ENDIF
     C                   MOVE      '1'           *IN47
     C     STARA1        TAG
     C*
     C*  SKRIV SUBFILE
     C                   ADD       1             ZRECNA
     C                   ADD       1             MRECNA
     C                   MOVE      HEKNA         THKNA
     C**** ------> FINN AGENTOPPLYSNINGER HER
     C                   MOVE      HEKNA         ZKUNDN
     C     KUNKEY        CHAIN     CUNDL01                            35
     C     *IN35         IFEQ      '0'
     C                   MOVEL     KNAVN         THKNAV
     C                   MOVEL     ADR1          THADR1
     C                   MOVEL     ADR2          THADR2
     C                   MOVE      POSTNR        THPOST
     C                   MOVEL     ADR3          THADR3
     C                   ELSE
     C                   CLEAR                   THKNAV
     C                   MOVEL     'MANGLER '    W16              16
     C                   MOVE      'AGENT   '    W16
     C                   MOVEL     W16           THKNAV
     C                   CLEAR                   THADR1
     C                   CLEAR                   THADR2
     C                   CLEAR                   THPOST
     C                   CLEAR                   THADR3
     C                   END                                                    <*IN35
     C                   WRITE     SUBFILA
     C                   MOVEA     '100'         *IN(80)
     C                   GOTO      HELE11
     C*
     C* Kommer hit når alle oppdrag på turen er lest.....
     C*
     C     STARA2        TAG
     C                   Z-ADD     9             LINNBR
     C                   Z-ADD     2             POSNBR
     C                   Z-ADD     1             ZRECNA
     C     STARA3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     MRECNA        IFEQ      1
ERT  C*
ERT  C                   MOVE      W1TNR         ZTUNR
ERT  C                   MOVE      THKNA         ZKNA
ERT  C     AMHEKY        CHAIN(N)  AMHE                               38
ERT  C* AMETA ER FERDIGMELDT
ERT  C     *IN38         IFEQ      *OFF
ERT  C     AHST          IFEQ      '3'
ERT  C     AHST          OREQ      '4'
ERT  C     AHST          OREQ      '5'
ERT  C                   MOVE      'DIV2444'     MSGNR
ERT  C                   SETON                                        30
ERT  C                   GOTO      CHOISE
ERT  C                   ENDIF                                                  <AHST
ERT  C* AMETA BEHANDLES AV EN ANNEN BRUKER
ERT  C     AHST          IFEQ      '2'
ERT  C                   MOVE      'DIV2529'     MSGNR
ERT  C                   SETON                                        30
ERT  C                   GOTO      CHOISE
ERT  C                   ENDIF                                                  <AHST
ERT  C                   ENDIF                                                  <*IN38
     C*
     C                   MOVE      W1TNR         PTNR
     C                   MOVE      HEKNA         PKNA
     C                   CALL      'SYAM01'
     C                   PARM                    UIN10
     C                   PARM                    PTNR
     C                   PARM                    PKNA
     C                   PARM                    IX
     C                   GOTO      CHOISE
     C                   ENDIF                                                  <MRECNA
     C*
     C     MRECNA        IFEQ      0
     C                   Z-ADD     8             POSNBR
     C                   EXFMT     NYWRKA
     C                   ELSE
     C                   MOVE      '1'           *IN(94)
     C                   WRITE     SUBCTLA
     C                   MOVEA     '00'          *IN(93)
     C                   EXFMT     BUNNA
     C                   END
     C***
     C                   SETOFF                                       9899
     C*** AVSLUTT
     C     *IN03         IFEQ      '1'
     C                   GOTO      CHOISE
     C                   END                                                    <*IN03
     C*** FOREGÅENDE BILDE / NYTT SØK
     C   12              GOTO      CHOISE
     C*
     C                   SETOFF                                       94
     C*** ROLLUP
     C     *IN25         IFEQ      '1'
     C                   MOVE      '0'           *IN25
     C                   MOVE      '1'           *IN94
     C                   GOTO      STARA3
     C                   END                                                    <*IN25
     C*** ROLLDOWN
     C     *IN26         IFEQ      '1'
     C                   MOVE      '0'           *IN25
     C                   MOVE      '1'           *IN94
     C                   GOTO      STARA3
     C                   END                                                    <*IN26
     C*
     C     ZRECNA        IFNE      0
     C                   EXSR      HENTSR
     C                   END
     C*
     C                   GOTO      STARA3
     C     SLUTT         TAG
     C                   SETON                                        LR
     C*
     C***********************************************
     C     HENTSR        BEGSR
     C***********************************************
     C                   Z-ADD     0             LINNBR
     C                   SETOFF                                       919299
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFILA                                98
     C   98              DO
     C   99              SETOFF                                       98
     C                   Z-ADD     1             LINNBR
     C                   GOTO      SLHENT
     C                   ENDDO                                                  <98 DO
     C     ZRECNA        DIV       5             LINNBR
     C                   MVR                     XLIN              2 0
     C     XLIN          IFEQ      0
     C                   Z-ADD     9             LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END                                                    <XLIN
     C*    OPTION 5=ARBEIDE MED AMETA
     C     WSNRO         IFEQ      '5'                                          ENDRE
     C                   EXSR      TESTB2
     C     *IN30         IFEQ      *ON
     C                   MOVE      ' '           WSNRO
     C                   UPDATE    SUBFILA
     C                   GOTO      SLHENT
     C                   ENDIF                                                  <*IN30
     C                   EXSR      BLANKW
     C                   MOVE      ' '           WSNRO
     C                   MOVE      '1'           *IN99
     C                   Z-ADD     21            LINNBR
     C                   Z-ADD     29            POSNBR
     C*
     C                   MOVE      W1TNR         PTNR              8 0
     C                   MOVE      THKNA         PKNA              8 0
     C                   CALL      'SYAM01'
     C                   PARM                    UIN10             1
     C                   PARM                    PTNR
     C                   PARM                    PKNA
     C                   PARM                    IX
     C*
     C                   UPDATE    SUBFILA
     C                   MOVEA     '010'         *IN(80)
     C                   END                                                    <WSNRO
     C*    OPTION 7=SKRIVE UT AMETASKJEMA
     C     WSNRO         IFEQ      '7'                                          ENDRE
     C                   MOVE      ' '           WSNRO
     C                   MOVE      '1'           *IN99
     C                   Z-ADD     21            LINNBR
     C                   Z-ADD     29            POSNBR
     C*
     C                   MOVE      W1TNR         PTNR              8 0
     C                   MOVE      THKNA         PKNA              8 0
     C                   MOVE      '0'           PFRA              1
     C                   CALL      'SYAM02CA'
     C                   PARM                    PTNR
     C                   PARM                    PKNA
     C                   PARM                    PFRA
     C*
     C                   UPDATE    SUBFILA
     C                   MOVEA     '010'         *IN(80)
     C                   END                                                    <WSNRO
     C*
     C                   END                                                    <*IN98
     C*
     C     SLHENT        TAG
     C                   ADD       8             LINNBR
     C                   Z-ADD     2             POSNBR
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     TESTB1        BEGSR
     C***********************************************
     C*
     C                   MOVEA     '0000'        *IN(29)
     C                   MOVEA     '00000000'    *IN(70)
     C* FINNES OPPDRAG PÅ TUREN ??
     C     W1TNR         CHAIN     HEAD11                             70
     C   70              DO
     C                   MOVE      'DIV2422'     MSGNR
     C                   SETON                                        30
     C                   Z-ADD     3             LINNBR
     C                   Z-ADD     14            POSNBR
     C                   GOTO      SLTB1
     C                   END
      *
      *  FEIL HVIS IX >=30
      *
     C     IX            IFGE      30
     C                   MOVE      'SHU0561'     MSGNR
     C                   SETON                                        3071
     C                   Z-ADD     3             LINNBR
     C                   Z-ADD     14            POSNBR
     C                   GOTO      SLTB1
     C                   END
     C*  INPUTDATA OK
     C     SLTB1         ENDSR
     C*
     C***********************************************
     C     TESTB2        BEGSR
     C***********************************************
     C*
     C                   MOVEA     '0000'        *IN(29)
     C                   MOVEA     '00000000'    *IN(70)
     C* SJEKK OM AMETA FINNES FRA FØR
     C                   MOVE      W1TNR         ZTUNR
     C                   MOVE      THKNA         ZKNA
     C     AMHEKY        CHAIN(N)  AMHE                               39
     C* AMETA ER FERDIGMELDT
     C     *IN39         IFEQ      '0'
     C     AHST          IFEQ      '3'
     C     AHST          OREQ      '4'
     C     AHST          OREQ      '5'
     C                   MOVE      'DIV2444'     MSGNR
     C                   SETON                                        30
     C                   GOTO      SLTB2
     C                   END                                                    <AHST
     C* AMETA BEHANDLES AV EN ANNEN BRUKER
     C     AHST          IFEQ      '2'
     C                   MOVE      'DIV2529'     MSGNR
     C                   SETON                                        30
     C                   GOTO      SLTB2
     C                   END                                                    <AHST
     C                   END                                                    <*IN39
     C*  INPUTDATA OK
     C     SLTB2         ENDSR
     C*
     C***********************************************
     C     RSETAM        BEGSR
     C***********************************************
     C                   MOVE      W1TNR         ZTUNR
     C     ZTUNR         CHAIN     AMHE                               39
     C     *IN39         IFEQ      '0'
     C     UIN10         IFEQ      '0'
ERT  C     AHST          IFLT      '3'
     C                   MOVE      '1'           AHST
ERT  C                   ENDIF                                                  <AHST
     C                   END                                                    <UIN10
     C                   UPDATE    AMHER
     C                   END                                                    <*IN39
     C     SLTRS         ENDSR
     C***********************************************
     C     BLANKW        BEGSR
     C***********************************************
     C                   CLEAR                   *IN40
     C     ZNULL         SETLL     AMDEWRK                                40
     C     STRBL1        TAG
     C                   READ      AMDEWRK                                40
     C   40              GOTO      STRBL2
     C                   DELETE    AMDEWRK
     C                   GOTO      STRBL1
     C*
     C     STRBL2        TAG
     C                   CLEAR                   *IN40
     C     ZNULL         SETLL     AMGEWRK                                40
     C     STRBL3        TAG
     C                   READ      AMGEWRK                                40
     C   40              GOTO      ENDBLN
     C                   DELETE    AMGEWRK
     C                   GOTO      STRBL3
     C     ENDBLN        ENDSR
     C*
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     *LIKE         DEFINE    ZRECNA        MRECNA
     C*--------------------------------------------------------------
     C* DEFINERER NØKKEL TIL CUNDL01
     C*--------------------------------------------------------------
     C     *LIKE         DEFINE    KUNDNR        ZKUNDN
     C     KUNKEY        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    ZKUNDN
     C*--------------------------------------------------------------
     C* DEFINERER NØKKEL TIL HEADF - HEAD11
     C*--------------------------------------------------------------
     C     HEA11K        KLIST
     C                   KFLD                    W1TNR
     C*--------------------------------------------------------------
     C* DEFINERER NØKKEL TIL AMWTUAG (workfile oppdrag pr tur/agent)
     C*--------------------------------------------------------------
     C     AMWKEY        KLIST
     C                   KFLD                    W1TNR
     C                   KFLD                    HEKNA
     C*--------------------------------------------------------------
     C* DEFINERER NØKKEL TIL AMHE
     C*--------------------------------------------------------------
     C     *LIKE         DEFINE    AHTUNR        ZTUNR
     C     *LIKE         DEFINE    AHKNA         ZKNA
     C     AMHEKY        KLIST
     C                   KFLD                    ZTUNR
     C                   KFLD                    ZKNA
     C*--------------------------------------------------------------
     C* DEFINERER NØKKEL TIL AMDEWRK / AMGEWRK
     C*--------------------------------------------------------------
     C     *LIKE         DEFINE    AHTUNR        ZNULL
     C     AMDEKY        KLIST
     C                   KFLD                    ZNULL
     C*
     C                   MOVE      *ZEROS        ZNULL
     C*
     C                   READ      FIRM                                   33
     C*
     C                   MOVE      *ZEROS        IX                2 0
     C*
     C     UTI           ENDSR
     C*
