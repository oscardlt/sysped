      * BRINGTMS-prosjekt.
      *  *...+....1....+....2....+....3....+....4....+....5....+....6....+....7....+.
      *  ;;;;;;;Gybyrkoder;;;EXP er %;;;
      *  KUNDE;FraLK;FraPostnr-start;FraPostnr-slutt;TilLK;TilPostnr-start;TilPostnr-slutt;EXP;SJÅ;F
      *  20031165;NO;4000;4399;NO;5000;5499;55;0;4 823;Dusavik - Ågotnes    ;;;Dusavik - Ågotnes
      *  20031165;NO;4000;4399;NO;5954;5954;55;0;5 232;Dusavik - Mongstad  ;;;Dusavik - Mongstad
      *  20031165;NO;4000;4399;NO;6900;6999;40;4 310;7 900;Dusavik - Florø   ;;;Dusavik - Florø
      ** 20031165;NO;4000;4399;NO;6500;6599;50;7 999;13 853;Dusavik - Kristiansund ;;;Dusavik - Kris
      *  I enkelte felt kommer hex 41 (&nbsp;) i stedet for Hex40 blank - Xlate det
      **
     FBRGEBYR   IP   F 1024        DISK
     FPRIK2     UF A E           K DISK
     D A               S              1    DIM(79)
     D B               S              1    DIM(18)
      *________________________
      * DS for decimal handling
      *""""""""""""""""""""""""
     D                 DS
     D  ZDEC                   1      7  6
     D  ZDEC6                  2      7
     D  ZHEL                  11     22  0
     D                 DS
     D  data                   1   1024
     D  da                     1   1024
     D                                     DIM(1024)
     D                 DS
     D  felta                  1    800
     D  fa                     1    800
     D                                     DIM(800)
      * ARRAY MED INTIL 600 KOLONNER A 800 TEGN etter dekoding.
     D                 DS
     D  dta                    1 480000
     D                                     DIM(600)
     D LO              C                   CONST('abcdefghijklmnopqrst-
     D                                     uvwxyzæøå')
     D UP              C                   CONST('ABCDEFGHIJKLMNOPQRST-
     D                                     UVWXYZÆØÅ')
      * konv " far hex 34
     D xx              c                   x'34'
     D yy              c                   x'7F'
      * konv " far hex 35 -> |
     D ZZ              c                   x'35'
     D ÆÆ              c                   x'BB'
     D NBSP            c                   x'41'
     IBRGEBYR   NC  01
     I                                  1    3  Data1_3
     I                                  1 1024  Data
      *
      * INITIERER VARIABLE
      *
      *
     c                   z-add     1             fra               3 0
     c                   z-add     1             til               3 0
     c                   z-add     0             DN                3 0
     c                   clear                   dta
     C     DATA1_3       ifle      '000'
     C     DATA1_3       orge      '999'
     C                   GOTO      NextRcd
     C                   END
      *
      *  I enkelte felt kommer hex 41 (&nbsp;) i stedet for Hex40 blank - Xlate det
     c                   eval      data =%xlate(nbsp:' ':data)
      *
      * leser til neste skilletegn ';' (men igorer ; hvis inne i quote/unquote: ;"....;..; .."; )
      *
     c     til           dowlt     900
      * start quote/unquote
     C     da(til)       ifeq      '"'
     C     InQuote       ifne      'Y'
     C                   MOVE      'Y'           InQuote           1
     C                   else
     C                   MOVE      'N'           InQuote
     C                   endif
     C                   endif
      * mellom quotes - gjør om ';' til ':'
     C     InQuote       ifeq      'Y'
     C     da(til)       andeq     ';'
     c                   eval      da(til)=':'
     c                   endif
      * slutt quote/unquote
      *
     C     da(til)       ifne      ';'
     c                   add       1             til
     c                   else
      *
      * fra og til er funnet
      *
     c                   exsr      dekod
     c                   add       1             til
     c                   z-add     til           fra
     c                   endif
     c                   enddo
      *
      * siste post
      *
     c     fra           add       58            til
     c                   exsr      dekod
      * DETALJBEHANDLING
      *
     c                   exsr      DETSR
      *
     c     NextRcd       tag
      *
     c***************************************************************
     c     dekod         begsr
     c***************************************************************
      *
      * dekoder innlest post
      *
     c     til           sub       fra           antp              3 0
     c     antp          ifgt      0
     c                   exsr      dokoda
     c                   add       1             dn
     c                   if        %subst(felta:1:1)='"'
     c                   eval      felta=%subst(felta:2)
     c                   eval      felta=%xlate('"':' ':felta)
     c                   endif
     c                   movel     felta         dta(dn)
     C                   ELSE
     c                   add       1             dn
     c                   end
     c                   endsr
     c***************************************************************
     c     DOKODA        begsr
     c***************************************************************
     c*    dekoder  TIL FELTA
     c*
     c                   move      *blanks       felta
     c                   z-add     fra           nr                3 0
     c                   z-add     1             n2                3 0
     c                   do        antp
     c                   move      da(nr)        fa(n2)
     c                   add       1             n2
     c                   add       1             nr
     c                   enddo
     c                   endsr
     c***************************************************************
     c     DETSR         begsr
     c***************************************************************
     C     KEY           KLIST
     C                   KFLD                    GMKNR
     C                   KFLD                    GMAVTN
     C                   KFLD                    GMLNR
     C     KEYe          KLIST
     C                   KFLD                    GMKNR
     C                   KFLD                    GMAVTN
     C     *LIKE         DEFINE    PKKNR         GMKNR
     C     *LIKE         DEFINE    PKAVTN        GMAVTN
     C     *LIKE         DEFINE    PKLNR         GMLNR
      *
      * FINN HØYESTE LNR PÅ DENNE AVTALE, LEGG TIL 1
     c                   clear                   A
     c                   movea     dta(1)        A
     c                   exsr      getnum
     C                   z-add     ZNUM          KUNNUM            8 0
     C                   move      KUNNUM        GMKNR
     C                   MOVE      '0'           GMAVTN
     C                   Z-ADD     9999          GMLNR
     C     KEY           SETGT     PRIK2
     C     KEYe          READPE    PRIK2                                  34
     C  N34              Z-ADD     PKLNR         GMLNR
     C   34              MOVE      *ZEROS        GMLNR
      *
     c                   clear                   PRIKR
      *
     C                   MOVE      GMKNR         PKKNR
     C                   MOVE      GMAVTN        PKAVTN
     C                   MOVE      GMLNR         PKLNR
      *
     C                   MOVE      'J'           PKMVA
     C                   MOVEL     'NOK'         PKFVAL
     C                   MOVEL     'I'           PKFSIO
     C                   MOVEL     '*FRTI'       PKFS01
     C                   movea     dta(3)        PKFS02
     C                   movea     dta(4)        PKFS03
     C                   MOVEL     'I'           PKTSIO
     C                   MOVEL     '*FRTI'       PKTS01
     C                   movea     dta(6)        PKTS02
     C                   movea     dta(7)        PKTS03
      * PROSENT-GEBYRER
     C                   MOVE      'P'           PKAVTT
     C                   MOVE      *ZEROS        PKINT
     C                   MOVE      '1'           PKTNR
      * - ekspress
     C                   MOVE      'EXP'         PKGEBY
     C                   MOVEL     'I'           PTRAIO
     C                   MOVEL     '*TILL'       PTRA01
     C                   MOVEL     'E'           PTRA02
     c                   clear                   A
     c                   movea     dta(8)        A                              8 = EXP
     c                   exsr      KOMPRESS                                     fjern ( ) + blank
     c                   clear                   A
     c                   movea     B             A
     c                   exsr      getnum
     C                   z-add     ZNUM          PKBELØ
      *   Dersom %-sats høere enn 100 så er det Kronebeløp ...
     C                   IF        PKBELØ > 100
     C                   MOVE      'T'           PKAVTT
     C                   MOVE      99999         PKINT
     C                   MOVE      ' '           PKTNR
     C                   ENDIF
     C                   ADD       1             PKLNR
     C                   WRITE     PRIKR
      * KRONE-GEBYRER
     C                   MOVE      'T'           PKAVTT
     C                   MOVE      99999         PKINT
     C                   MOVE      ' '           PKTNR
      * Ekstra sjåfør
     c                   clear                   A
     c                   movea     dta(9)        A                              9 = SJÅ/2/3
     c                   exsr      KOMPRESS                                     fjern ( ) + blank
     c                   clear                   A
     c                   movea     B             A
     c                   exsr      getnum
     c     ZNUM          ifne      *zeros
      *  en ekstra = SJÅ
     C                   z-add     ZNUM          PKBELØ
     C                   MOVE      'SJÅ'         PKGEBY
     C                   MOVEL     'I'           PTRAIO
     C                   MOVEL     '*TILL'       PTRA01
     C                   MOVEL     '1'           PTRA02
     C                   ADD       1             PKLNR
     C                   WRITE     PRIKR
      *   2 ekstra = SJ2
     C     ZNUM          mult(H)   2             PKBELØ
     C                   MOVE      'SJ2'         PKGEBY
     C                   MOVEL     'I'           PTRAIO
     C                   MOVEL     '*TILL'       PTRA01
     C                   MOVEL     '2'           PTRA02
     C                   ADD       1             PKLNR
     C                   WRITE     PRIKR
      *   3 ekstra = SJ3
     C     ZNUM          mult(H)   3             PKBELØ
     C                   MOVE      'SJ3'         PKGEBY
     C                   MOVEL     'I'           PTRAIO
     C                   MOVEL     '*TILL'       PTRA01
     C                   MOVEL     '3'           PTRA02
     C                   ADD       1             PKLNR
     C                   WRITE     PRIKR
     C                   endif
      *
      * Følgebil
     c                   clear                   A
     c                   movea     dta(10)       A                              8 = EXP
     c                   exsr      KOMPRESS                                     fjern ( ) + blank
     c                   clear                   A
     c                   movea     B             A
     c                   exsr      getnum
     c     ZNUM          ifne      *zeros
      *  en ekstra = SJÅ
     C                   z-add     ZNUM          PKBELØ
     C                   MOVE      'FØL'         PKGEBY
     C                   MOVEL     'I'           PTRAIO
     C                   MOVEL     '*TILL'       PTRA01
     C                   MOVEL     'F'           PTRA02
     C                   ADD       1             PKLNR
     C                   WRITE     PRIKR
     C                   endif
      *
     c     NextRec       tag
     c                   endsr
     c*****************************************
     c     kompress      begsr
     c*****************************************
     C                   MOVE      *blanks       B
     C                   Z-ADD     1             AX                2 0
     C                   Z-ADD     1             BX                2 0
      * Get Integer
     C     1             do        15            AX
     C     A(AX)         IFGE      '0'
     C     A(AX)         andle     '9'
     C                   MOVE      A(AX)         B(BX)
     C                   ADD       1             BX
     C                   END
     C                   ENDDO
     c                   endsr
     c*****************************************
     c     kompressB     begsr
     c*****************************************
      *fjern blanke og (  )  + 0 (i f.eks B1000C -> B1C )
     C                   MOVE      *blanks       B
     C                   Z-ADD     1             AX                2 0
     C                   Z-ADD     1             BX                2 0
      * Fjern blanke  og .: (finn første aktive etter ledetekst )
     C     1             do        30            AX
     C     A(AX)         IFne      ' '
     C     A(AX)         andne     '('
     C     A(AX)         andne     ')'
     C     A(AX)         andne     '0'
     C                   MOVE      A(AX)         B(BX)
     C                   ADD       1             BX
     C                   END
     C                   ENDDO
     c                   endsr
     c*****************************************
     c     getnum        begsr
     c*****************************************
     C                   MOVE      *ZEROS        ZHEL
     C                   MOVE      *ZEROS        ZDEC
     C                   MOVE      *ZEROS        B
     C                   Z-ADD     1             AX                2 0
     C                   Z-ADD     1             BX                2 0
      * Get Integer
     C     A(AX)         DOWGE     '0'
     C     ZHEL          MULT      10            ZHEL
     C                   MOVE      A(AX)         ZHEL
     C                   ADD       1             AX
     C                   ENDDO
      * Decimal point ?
     C     A(AX)         IFEQ      '.'
     C     A(AX)         OREQ      ','
     C                   ADD       1             AX
     C     A(AX)         DOWGE     '0'
     C                   MOVE      A(AX)         B(BX)
     C                   ADD       1             AX
     C                   ADD       1             BX
     C                   ENDDO
     C                   MOVEA     B             ZDEC6
     C                   ENDIF
      * Concatenate
     C                   Z-ADD     ZHEL          ZNUM             18 6
     C                   ADD       ZDEC          ZNUM
     C                   ENDSR
     c
