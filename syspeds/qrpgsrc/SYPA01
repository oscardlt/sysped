      **************************************************************
      *  ARBEIDE MED  PALLEBYTTEPOSTER                             *
      **************************************************************
     FSYPA01FM  CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     F                                     INFDS(STATDS)
     FPALLD     UF A E           K DISK
     FPALLDL01  IF   E           K DISK
     F                                     RENAME(PALLDR:PALLDL1R)
     FPALLDL02  IF   E           K DISK
     F                                     RENAME(PALLDR:PALLDL2R)
     FPALLDL03  IF   E           K DISK
     F                                     RENAME(PALLDR:PALLDL3R)
     FPALLDL04  IF   E           K DISK
     F                                     RENAME(PALLDR:PALLDL4R)
     FPALLDS01  IF   E           K DISK
     F                                     RENAME(PALLDR:PALLDS1R)
     FPALLDS02  IF   E           K DISK
     F                                     RENAME(PALLDR:PALLDS2R)
     FPALLDS03  IF   E           K DISK
     F                                     RENAME(PALLDR:PALLDS3R)
     FPALLDS04  IF   E           K DISK
     F                                     RENAME(PALLDR:PALLDS4R)
     FPALLDS05  IF   E           K DISK
     F                                     RENAME(PALLDR:PALLDS5R)
     FPALFIR    UF   E             DISK
     FPAPA      UF   E           K DISK
     FFIRM      IF   E           K DISK
     FUNITL01   IF   E           K DISK
     D                 DS
     D  DTÅ                    1      4  0
     D  DTM                    5      6  0
     D  DTD                    7      8  0
     D  IDAG                   1      8  0
     D                 DS
     D  PADATO                 1      8  0
     D  WADATO                 3      8  0
     D                 DS
     D  WANA30                 1     30
     D  WANAVN                 1     10
     D                 DS
     D  WBNA30                 1     30
     D  WBNAVN                 1     10
     D                 DS
     D  PATXT                  1     20
     D  PATXT8                 1      8
     D                 DS
     D  WSLØNR                 1      7  0
     D  WSPPNR                 8     15  0
     D  WSSTAT                16     16
     D  WSDATO                19     24  0
     D  WSDAT8                17     24  0
     D  WSBUNR                25     31  0
     D  WSKVNR                32     40  0
     D  WSUSER                41     50
     D  NYSØK                  1     50
     D                 DS
     D  GMLØNR                 1      7  0
     D  GMPPNR                 8     15  0
     D  GMSTAT                16     16
     D  GMDATO                19     24  0
     D  GMDAT8                17     24  0
     D  GMBUNR                25     31  0
     D  GMKVNR                32     40  0
     D  GMUSER                41     50
     D  GMSØK                  1     50
     D STATDS          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
      *___________________________
      * Finner username
      *"""""""""""""""""""""""""""
     D                SDS
     D  ZJOB                 244    253
     D  ZUSER                254    263
     D  ZJOBNR               264    269  0
      *//////////////
      *  Hodelogikk /
      *//////////////
     C                   EXSR      INIT
     C                   EXSR      HOVED
     C                   MOVE      '1'           *INLR
      *////////////////////////////////////////////////////////////
      *  Bearbeide Formater                                   SRB /
      *////////////////////////////////////////////////////////////
     C     HOVED         BEGSR
      *______________
      * Tømme Subfile
      *""""""""""""""
      *
     C     START         TAG
     C                   MOVE      NYSØK         GMSØK
     C                   MOVE      '1'           *IN93
     C                   MOVEA     '00'          *IN(65)
     C                   EXSR      KDTFM2
     C                   WRITE     SUBCTL
     C                   EXSR      KDTFM2
     C                   MOVEA     '000'         *IN(93)
     C                   Z-ADD     *ZERO         ZRECNO
      * 50 ON = UAVHENGIG AV STATUS / 50 OFF = KUN GITT STATUS
     C     WSSTAT        COMP      ' '                                    50
      *
     C     *IN50         IFEQ      *ON
     C   51PALL1K        SETLL     PALLDL01
     C   52PALL1K        SETLL     PALLDL01
     C   53PALL2K        SETLL     PALLDL02
     C   54PALL2K        SETLL     PALLDL02
     C   55PALL3K        SETLL     PALLDL03
     C   56PALL3K        SETLL     PALLDL03
     C   57PALL4K        SETLL     PALLDL04
     C   58PALL4K        SETLL     PALLDL04
     C                   ELSE
     C   51PALS1K        SETLL     PALLDS01
     C   52PALS1K        SETLL     PALLDS01
     C   53PALS2K        SETLL     PALLDS02
     C   54PALS2K        SETLL     PALLDS02
     C   55PALS3K        SETLL     PALLDS03
     C   56PALS3K        SETLL     PALLDS03
     C   57PALS4K        SETLL     PALLDS04
     C   58PALS4K        SETLL     PALLDS04
     C                   END
      *_____________
      * Fyll Subfile (med MAX 13 POSTER (1 SIDE ER 12)
      *"""""""""""""
     C     *IN50         IFEQ      *ON
     C   51PALL1E        READE     PALLDL01                               94
     C   52              READP     PALLDL01                               94
     C   53PALL2E        READE     PALLDL02                               94
     C   54              READP     PALLDL02                               94
     C   55PALL3E        READE     PALLDL03                               94
     C   56              READP     PALLDL03                               94
     C   57PALL4E        READE     PALLDL04                               94
     C   58PALL4E        READPE    PALLDL04                               94
     C                   ELSE
     C   51PALS1E        READE     PALLDS01                               94
     C   52WSSTAT        READPE    PALLDS01                               94
     C   53PALS2E        READE     PALLDS02                               94
     C   54WSSTAT        READPE    PALLDS02                               94
     C   55PALS3E        READE     PALLDS03                               94
     C   56WSSTAT        READPE    PALLDS03                               94
     C   57PALS4E        READE     PALLDS04                               94
     C   58PALS4E        READPE    PALLDS04                               94
     C                   END
      *
     C     START2        TAG
      * HOPPER UT VED EOF, ELLER VED 13 SKREVET TIL SUBFIL
     C                   MOVE      *ZEROS        ANTSUB            2 0
      *
     C     *IN94         DOWEQ     '0'
      * FILTER SOM IKKE INNGÅR I READ-KEY
     C     WSDATO        IFNE      *ZEROS
     C     WSDAT8        ANDGT     PAKVDT
     C     *IN57         ANDEQ     '0'
     C     *IN58         ANDEQ     '0'
     C                   GOTO      NESTE
     C                   END
      *
      * LEST 1 FOR LANGT, (12 SKREVET TIL SUBFIL, 13 ER LEST FRA FILA)
      *              BEVAR KEY, HOPP UT, VIS SUBFIL
     C     ANTSUB        IFEQ      12
     C                   MOVE      PALØNR        SFLØNR            7
     C                   MOVE      PAAB          SFAB              1
     C                   GOTO      VISLIS
     C                   END
      *
     C                   MOVE      *BLANKS       PPNAVN
     C     PAPPNR        CHAIN(N)  PAPA                               33
     C                   MOVEL     PPNAVN        XXNAVN
     C     PAUSER        COMP      WSUSER                             6666
     C   66              SETON                                        65
     C                   ADD       1             ZRECNO
     C                   ADD       1             ANTSUB
     C     FIDTFM        IFNE      3
     C                   MOVE      PAKVDT        WXKVDT            6 0
     C                   MOVE      *ZEROS        DATO0             6 0
     C                   CALL      'DATFM4'
     C                   PARM                    FIDTFM
     C                   PARM                    WXKVDT
     C                   PARM                    DATO0
     C                   PARM                    DATO0
     C                   PARM                    DATO0
     C                   END
     C                   WRITE     SUBFIL
      *
     C     NESTE         TAG
      *
     C     *IN50         IFEQ      *ON
     C   51PALL1E        READE     PALLDL01                               94
     C   52              READP     PALLDL01                               94
     C   53PALL2E        READE     PALLDL02                               94
     C   54              READP     PALLDL02                               94
     C   55PALL3E        READE     PALLDL03                               94
     C   56              READP     PALLDL03                               94
     C   57PALL4E        READE     PALLDL04                               94
     C   58PALL4E        READPE    PALLDL04                               94
     C                   ELSE
     C   51PALS1E        READE     PALLDS01                               94
     C   52WSSTAT        READPE    PALLDS01                               94
     C   53PALS2E        READE     PALLDS02                               94
     C   54WSSTAT        READPE    PALLDS02                               94
     C   55PALS3E        READE     PALLDS03                               94
     C   56WSSTAT        READPE    PALLDS03                               94
     C   57PALS4E        READE     PALLDS04                               94
     C   58PALS4E        READPE    PALLDS04                               94
     C                   END
      *
     C  N94              END
      *
      *
     C     VISLIS        TAG
     C                   Z-ADD     ZRECNO        YRECNO            4 0
      *********************
      * INGEN POSTER FANTES
      *********************
     C     ZRECNO        IFEQ      0
     C                   MOVE      'JOV0593'     ZMSGNO
     C                   MOVE      '1'           *IN30
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     4             POSNBR
     C                   EXSR      KDTFM2
     C                   EXFMT     NYVARE
     C                   EXSR      KDTFM2
     C                   MOVE      '0'           *IN30
     C                   ELSE
      *********************
      * POSTER FANTES
      *********************
     C*                    Z-ADD1         ZRECNO
      *_______________
      * Behandle bilde
      *_______________
      *
     C                   MOVEA     '11'          *IN(91)
     C  N30              MOVE      'SY00000'     ZMSGNO
      * Melding om poster tilhørende annen bruker KUN EN GANG
     C     USER1         IFEQ      ' '
     C     *IN65         ANDEQ     '1'
     C     *IN30         ANDEQ     '0'
     C                   MOVE      'JOV0957'     ZMSGNO
     C                   MOVE      '1'           *IN30
     C                   MOVE      'X'           USER1             1
     C                   END
     C                   WRITE     BUNN
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     2             POSNBR
      *
     C     F6            IFEQ      'J'
     C                   EXSR      KDTFM2
     C                   WRITE     SUBCTL
     C                   EXSR      KDTFM2
     C                   MOVE      '1'           *IN06
     C                   GOTO      F6TAG
     C                   END
      *
     C                   EXSR      KDTFM2
     C                   EXFMT     SUBCTL
     C                   EXSR      KDTFM2
     C                   END
      *
     C     *IN03         IFEQ      '1'
     C                   EXSR      DFTKEY
     C     WSBUNR        IFNE      9999999
     C                   EXFMT     BUNT
     C     *IN12         CABEQ     '1'           VISLIS
     C     *IN03         CABEQ     '1'           SRB9
      * SKRIV BUNTJOURNAL
     C     WSFORM        IFNE      'L'
     C                   MOVE      'S'           WSFORM
     C                   END
     C                   CALL      'SYPA09CL'
     C                   PARM                    WSBUNR
     C                   PARM                    WSPRIN
     C                   PARM                    WSFORM
     C                   PARM                    WSARB
     C                   END
     C                   GOTO      SRB9
     C                   END
      *
      *
     C**                   MOVEA'00'      *IN,91
      *_____________
      * F5=Frisk opp
      *"""""""""""""
     C     *IN05         CABEQ     '1'           START
      *_____________
      * F6=LAG NY  (vises i subfil)
      *"""""""""""""
     C     F6TAG         TAG
     C     *IN06         IFEQ      '1'
     C                   MOVE      *ZEROS        PALØNR
     C                   EXSR      NYENDR
     C     *IN12         IFNE      '1'
     C                   MOVE      'J'           F6
     C                   MOVE      WABUNR        WSBUNR
     C                   MOVE      'B'           WSSTAT
     C                   MOVEA     '01000000'    *IN(50)
     C                   MOVE      *ZEROS        WSKVNR
     C                   MOVE      *ZEROS        WSLØNR
     C                   MOVE      *ZEROS        WSPPNR
     C                   MOVE      *ZEROS        WSDAT8
     C                   GOTO      START
     C                   ELSE
     C                   MOVE      'N'           F6                1
     C                   MOVE      '0'           *IN30
     C                   GOTO      VISLIS
     C                   END
     C                   END
      *
      *_________________
      * Standard søk/utvalg (Poster i Åpen bunt for USER)
      *"""""""""""""""""
     C     *IN16         IFEQ      *ON
     C                   EXSR      DFTKEY
     C                   MOVE      *ZEROS        WSLØNR
     C                   MOVE      *ZEROS        WSPPNR
     C                   MOVE      *ZEROS        WSDAT8
     C                   MOVE      *ZEROS        GMLØNR
     C                   MOVE      *ZEROS        GMPPNR
     C                   MOVE      *ZEROS        GMDAT8
     C                   GOTO      START
     C                   END
     C*** BLA VIDERE
     C   25              DO
     C                   Z-ADD     YRECNO        ZRECNO
      * HENT VERDIER FOR SIST LESTE (ENNÅ IKKE VISTE) POST
     C     *IN94         IFEQ      '0'
     C                   MOVE      SFLØNR        PALØNR
     C                   MOVE      SFAB          XXAB
     C     PALLK         CHAIN(N)  PALLD                              33
     C                   END
     C                   SETOFF                                       25
     C                   GOTO      START2
     C                   END
      *_________________
      * Nytt søk/utvalg
      *"""""""""""""""""
     C     WSDATO        IFEQ      0
     C                   MOVE      *ZEROS        WSDAT8
     C                   ELSE
     C     WSDATO        IFGT      501231
     C                   MOVEL     '19'          WSDAT8
     C     WSDATO        IFEQ      999999
     C                   MOVEL     '99'          WSDAT8
     C                   END
     C                   ELSE
     C                   MOVEL     '20'          WSDAT8
     C                   END
     C                   END
      *
     C     NYSØK         IFNE      GMSØK
     C                   EXSR      SETKEY
     C                   GOTO      START
     C                   END
      *_______
      * Valg ?
      *"""""""
     C                   MOVE      '0'           *IN42
     C                   MOVE      '0'           *IN30
     C                   MOVE      '0'           *IN95
     C     *IN42         DOWEQ     '0'
      *
     C                   READC     SUBFIL                                 42
     C     *IN42         IFEQ      '0'
     C                   SELECT
     C     WSNR          WHENEQ    '2'
     C                   EXSR      NYENDR
     C                   MOVE      '1'           *IN95
     C     WSNR          WHENEQ    '4'
     C                   EXSR      SLETT
     C                   MOVE      '1'           *IN95
     C     WSNR          WHENEQ    '5'
     C                   EXSR      SEPÅ
     C                   ENDSL
      *
     C                   END
      *
     C  N30
     CANN42              END
      *
     C   95              GOTO      START
     C                   GOTO      VISLIS
      *
     C     SRB9          ENDSR
     C***************************************************
     C     SETKEY        BEGSR
     C***************************************************
      *
     C                   MOVEA     '00000000'    *IN(50)
      * BUNTNR.  STIGENDE F.O.M / SISTE FØRST
     C     WSBUNR        IFNE      *ZEROS
     C     WSBUNR        IFNE      9999999
     C                   MOVE      '1'           *IN51
     C                   ELSE
     C                   MOVE      '1'           *IN52
     C                   END
     C                   GOTO      UTSETK
     C                   END
      *
      * KV.NR. STIGENDE F.O.M / SISTE FØRST
     C     WSKVNR        IFNE      *ZEROS
     C     WSKVNR        IFNE      999999999
     C                   MOVE      '1'           *IN53
     C                   ELSE
     C                   MOVE      '1'           *IN54
     C                   END
     C                   GOTO      UTSETK
     C                   END
      *
      * LØPENR. STIGENDE F.O.M / SISTE FØRST
     C     WSLØNR        IFNE      *ZEROS
     C     WSLØNR        IFNE      9999999
     C                   MOVE      '1'           *IN55
     C                   ELSE
     C                   MOVE      '1'           *IN56
     C                   END
     C                   GOTO      UTSETK
     C                   END
      *
      * PARTSNR/DATO. STIGENDE F.O.M DATO / SISTE FØRST
     C     WSPPNR        IFNE      *ZEROS
     C     WSDATO        IFNE      999999
     C                   MOVE      '1'           *IN57
     C                   ELSE
     C                   MOVE      '1'           *IN58
     C                   END
     C                   GOTO      UTSETK
     C                   END
      *
      *
      * INGEN VALG ER GJORT. VIS BUNTNR / SISTE FØRST
     C                   MOVE      9999999       WSBUNR
     C                   MOVE      '1'           *IN52
      *
      *
     C     UTSETK        ENDSR
      *
      *
     C***************************************************
     C     SLETT         BEGSR
     C***************************************************
      * BÅDE A/B MÅ FINNES OG STATUS VÆRE LIKE OG ENTEN B/D
     C                   MOVE      'N'           OK                1
     C                   MOVE      'A'           XXAB
     C     PALLK         CHAIN(N)  PALLD                              45
     C     *IN45         IFEQ      '0'
     C                   MOVE      PASTAT        TMPSTA            1
     C                   MOVE      'B'           XXAB
     C     PALLK         CHAIN(N)  PALLD                              45
     C     *IN45         IFEQ      '0'
     C     PASTAT        ANDEQ     TMPSTA
     C     PASTAT        IFEQ      'B'
     C     PASTAT        OREQ      'D'
     C                   MOVE      'J'           OK                1
     C                   END
     C                   END
     C                   END
      * OPPDATER A-SIDA
     C     OK            IFEQ      'J'
     C                   MOVE      'A'           XXAB
     C     PALLK         CHAIN     PALLD                              45
     C     *IN45         IFEQ      '0'
     C                   MOVE      *BLANKS       PATXT
     C                   MOVE      PAPPNR        XXPPNR
     C     PASTAT        IFEQ      'B'
     C                   MOVE      'D'           PASTAT
     C                   MOVEL     '*SLETTET'    PATXT
     C                   MOVE      *ZEROS        XXANT
     C                   EXSR      JUSTER
     C                   ELSE
     C                   MOVE      'B'           PASTAT
     C                   MOVE      PAANT         XXANT
     C                   MOVE      *ZEROS        PAANT
     C                   EXSR      JUSTER
     C                   MOVE      XXANT         PAANT
     C                   END
     C                   MOVE      WSUSER        PAUSER
     C     PAUSER        IFEQ      *BLANKS
     C                   MOVE      ZUSER         PAUSER
     C                   END
     C                   MOVE      IDAG          PADATO
     C                   UPDATE    PALLDR
     C                   END
      * OPPDATER B-SIDA
     C                   MOVE      'B'           XXAB
     C     PALLK         CHAIN     PALLD                              45
     C     *IN45         IFEQ      '0'
     C                   MOVE      *BLANKS       PATXT
     C                   MOVE      PAPPNR        XXPPNR
     C     PASTAT        IFEQ      'B'
     C                   MOVE      'D'           PASTAT
     C                   MOVEL     '*SLETTET'    PATXT
     C                   MOVE      *ZEROS        XXANT
     C                   EXSR      JUSTER
     C                   ELSE
     C                   MOVE      'B'           PASTAT
     C                   MOVE      PAANT         XXANT
     C                   MOVE      *ZEROS        PAANT
     C                   EXSR      JUSTER
     C                   MOVE      XXANT         PAANT
     C                   END
     C                   MOVE      WSUSER        PAUSER
     C                   MOVE      PPNAVN        PAPNVN
     C     PAUSER        IFEQ      *BLANKS
     C                   MOVE      ZUSER         PAUSER
     C                   END
     C                   MOVE      IDAG          PADATO
     C                   UPDATE    PALLDR
     C                   END
      *
     C                   MOVE      ' '           WSNR
     C     FIDTFM        IFNE      3
     C                   MOVE      PAKVDT        WXKVDT            6 0
     C                   MOVE      *ZEROS        DATO0             6 0
     C                   CALL      'DATFM4'
     C                   PARM                    FIDTFM
     C                   PARM                    WXKVDT
     C                   PARM                    DATO0
     C                   PARM                    DATO0
     C                   PARM                    DATO0
     C                   END
     C                   UPDATE    SUBFIL
     C                   END
     C                   ENDSR
     C***************************************************
     C     NYENDR        BEGSR
     C***************************************************
     C                   MOVE      '0'           *IN30
     C                   MOVEA     '0000000'     *IN(71)
     C                   MOVE      'N'           NY                1
      * VED NYREG - HOPP TIL VISNING AV SKJERMBILDE
     C     PALØNR        IFEQ      *ZEROS
     C                   MOVE      'J'           NY                1
     C                   MOVE      'A'           SUBAB
     C                   GOTO      STBILD
     C                   END
      *
      * BÅDE A/B MÅ FINNES OG BEGGE MÅ HA STATUS B
      * (MEN ANTAR AT DET ER OK - OG LEGGER UT I SKJERMVARIABLE)
      *
      * PASS PÅ AT RECORD I SUBFILA ER DEN SOM SIST OPPDATERES
     C                   MOVE      PAAB          SUBAB             1
     C                   MOVE      'N'           OK                1
     C                   MOVE      'A'           XXAB
     C     PALLK         CHAIN(N)  PALLD                              45
     C     *IN45         IFEQ      '0'
     C                   EXSR      MOVPAW
     C                   MOVE      PASTAT        TMPSTA            1
     C                   MOVE      'B'           XXAB
     C     PALLK         CHAIN(N)  PALLD                              45
     C     *IN45         IFEQ      '0'
     C     PASTAT        ANDEQ     TMPSTA
     C     PASTAT        ANDEQ     'B'
     C                   MOVE      'J'           OK                1
     C                   END
     C                   END
      *
     C     OK            IFEQ      'J'                                          B0
     C                   EXSR      MOVPAW
     C     STBILD        TAG
     C                   Z-ADD     20            LINNBR
     C     WAPPNR        IFEQ      *ZEROS
     C                   Z-ADD     22            POSNBR
     C                   ELSE
     C     WAKVDT        IFEQ      *ZEROS
     C                   Z-ADD     42            POSNBR
     C                   ELSE
     C                   Z-ADD     49            POSNBR
     C                   END
     C                   END
      *
     C     STBIL2        TAG
      *
     C  N30              MOVE      'SY00000'     ZMSGNO
      * Melding om betydning av +/- i antall kun 1. gang
     C     PLUMIN        IFEQ      ' '
     C     *IN30         ANDEQ     '0'
     C                   MOVE      'JOV0972'     ZMSGNO
     C                   MOVE      '1'           *IN30
     C                   MOVE      'X'           PLUMIN            1
     C                   END
     C                   EXSR      KDTFMT
     C                   EXFMT     VARELI
     C                   EXSR      KDTFMT
      *
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
      *
     C     *IN12         IFNE      '1'                                          B1
     C     *IN04         IFEQ      '1'                                           B2
     C                   EXSR      SØK
     C                   GOTO      STBIL2
     C                   END                                                     E2
     C                   EXSR      TESTB1
     C     *IN30         IFEQ      '1'                                           B2
     C                   GOTO      STBIL2
     C                   END                                                     E2
      * Har ikke tastet F12 - Tester = OK  -> Oppdater fila
     C     SUBAB         IFEQ      'A'                                           B2
     C                   MOVE      'B'           XXAB
     C                   ELSE
     C                   MOVE      'A'           XXAB
     C                   END                                                     E2
     C     PALØNR        IFEQ      *ZEROS                                        B2
     C     1             CHAIN     PALFIR                             33
     C                   MOVE      PFLØNR        PALØNR
     C                   ADD       1             PFLØNR
     C  N33              UPDATE    PALFIRR
     C                   END                                                     E2
     C     PALLK         CHAIN     PALLD                              45
     C                   EXSR      MOVWPA
     C     *IN45         IFEQ      '0'                                           B2
     C                   UPDATE    PALLDR
     C                   ELSE
     C                   WRITE     PALLDR
     C                   END                                                     E2
     C*
     C                   MOVE      SUBAB         XXAB
     C     PALLK         CHAIN     PALLD                              45
     C                   EXSR      MOVWPA
     C     *IN45         IFEQ      '0'                                           B2
     C                   UPDATE    PALLDR
     C                   ELSE
     C                   WRITE     PALLDR
     C                   END                                                     E2
      *
     C                   END                                                    E1
      * OPPDATER SUBFIL HVIS AKTUELT
     C     NY            IFNE      'J'                                          B1
     C                   MOVE      ' '           WSNR
     C     FIDTFM        IFNE      3
     C                   MOVE      PAKVDT        WXKVDT            6 0
     C                   MOVE      *ZEROS        DATO0             6 0
     C                   CALL      'DATFM4'
     C                   PARM                    FIDTFM
     C                   PARM                    WXKVDT
     C                   PARM                    DATO0
     C                   PARM                    DATO0
     C                   PARM                    DATO0
     C                   END
     C                   UPDATE    SUBFIL
     C                   END                                                    E1
     C                   END                                                    E0
      * BLANK W-FELT
     C                   EXSR      MOVBW
     C                   ENDSR
      *
      *
     C***************************************************
     C     SØK           BEGSR
     C***************************************************
     C     LINNBR        IFEQ      20
     C                   MOVE      WAPPNR        UPPNR             8 0
     C                   MOVEL     WANAVN        UNAVN            30
     C                   CALL      'SYPA04A'
     C                   PARM                    UPPNR
     C                   PARM                    UNAVN
     C     UPPNR         IFNE      *ZEROS
     C                   MOVE      UPPNR         WAPPNR
     C                   MOVEL     UNAVN         WANA30
     C                   END
     C                   END
     C     LINNBR        IFEQ      21
     C                   MOVE      WBPPNR        UPPNR
     C                   MOVEL     WBNAVN        UNAVN            30
     C                   CALL      'SYPA04A'
     C                   PARM                    UPPNR
     C                   PARM                    UNAVN
     C     UPPNR         IFNE      *ZEROS
     C                   MOVE      UPPNR         WBPPNR
     C                   MOVEL     UNAVN         WBNA30
     C                   END
     C                   END
     C     LINNBR        IFEQ      22
     C                   CALL      'SYTR19'
     C                   PARM                    WABILN
     C                   END
     C                   ENDSR
      *
     C***************************************************
     C     TESTB1        BEGSR
     C***************************************************
     C                   MOVE      *ZEROS        XATRNR            8 0
     C                   MOVE      *ZEROS        XBTRNR            8 0
     C                   MOVE      '0'           *IN30
     C                   MOVEA     '00000'       *IN(71)
     C     WAPPNR        IFEQ      *ZEROS
     C     WANAVN        ANDNE     *BLANKS
     C                   Z-ADD     20            LINNBR
     C                   EXSR      SØK
     C                   END
     C*** TEST PART/A
     C     WAPPNR        CHAIN(N)  PAPA                               71
     C     *IN71         IFEQ      '1'
     C                   MOVE      'JOV0622'     ZMSGNO
     C                   MOVE      '1'           *IN30
     C                   Z-ADD     20            LINNBR
     C                   Z-ADD     22            POSNBR
     C                   ELSE
     C                   MOVEL     PPNAVN        WANA30
     C                   MOVE      PPTRNR        XATRNR
     C                   END
     C*** TEST DATO  (DATO ER NÅ ALT SNUDD TIL ÅÅMMDD)
     C                   MOVE      *ZEROS        UFLAGG            1 0
     C                   MOVE      WAKVDT        XMMÅÅ             4 0
     C                   MOVE      XMMÅÅ         UDD               2 0
     C                   MOVEL     XMMÅÅ         UMM               2 0
     C                   MOVEL     WAKVDT        UÅÅ               2 0
     C                   CALL      'TESTDATO'
     C                   PARM                    UDD
     C                   PARM                    UMM
     C                   PARM                    UÅÅ
     C                   PARM                    UFLAGG            1 0
     C     UFLAGG        COMP      1                                      72
     C     *IN30         IFEQ      '0'
     C     *IN72         ANDEQ     '1'
     C                   MOVE      'SPD0001'     ZMSGNO
     C                   MOVE      '1'           *IN30
     C                   Z-ADD     20            LINNBR
     C                   Z-ADD     42            POSNBR
     C                   END
     C*** TEST ANTALL
     C     WAANT         COMP      *ZEROS                                 73
     C     *IN30         IFEQ      '0'
     C     *IN73         ANDEQ     '1'
     C                   MOVE      'JOV0623'     ZMSGNO
     C                   MOVE      '1'           *IN30
     C                   Z-ADD     20            LINNBR
     C                   Z-ADD     49            POSNBR
     C                   END
     C*** AUTOMATISK TILDELING AV BUNTNUMMER
     C     WABUNR        IFEQ      *ZEROS
     C     1             CHAIN     PALFIR                             33
     C                   MOVE      PFBUNR        WABUNR
     C                   ADD       1             PFBUNR
     C  N33              UPDATE    PALFIRR
     C                   END
     C*** TEST MANUELT TASTET PALLKVITT.NR. (HVIS FIRMAPROFIL TILSIER)
     C     PFPKKR        IFEQ      'J'
      *    .... MÅ VÆRE ULIK 0
     C     WAKVNR        COMP      *ZEROS                                 76
     C     *IN30         IFEQ      '0'
     C     *IN76         ANDEQ     '1'
     C                   MOVE      'JOV0839'     ZMSGNO
     C                   MOVE      '1'           *IN30
     C                   Z-ADD     21            LINNBR
     C                   Z-ADD     6             POSNBR
     C                   END
      *
     C                   END
     C*** TEST PART/B
     C     WBPPNR        IFEQ      *ZEROS
     C     WBNAVN        ANDNE     *BLANKS
     C                   Z-ADD     21            LINNBR
     C                   EXSR      SØK
     C                   END
     C     WBPPNR        CHAIN(N)  PAPA                               74
     C     *IN30         IFEQ      '0'
     C     *IN74         ANDEQ     '1'
     C                   MOVE      'JOV0622'     ZMSGNO
     C                   MOVE      '1'           *IN30
     C                   Z-ADD     21            LINNBR
     C                   Z-ADD     22            POSNBR
     C                   ELSE
     C     *IN74         IFEQ      '0'
     C                   MOVEL     PPNAVN        WBNAVN
     C                   MOVEL     PPTRNR        XBTRNR
     C                   END
     C                   END
      *
      * PART B MÅ VÆRE ULIK PART A
     C     *IN74         IFEQ      '0'
     C     WBPPNR        COMP      WAPPNR                                 74
     C     *IN30         IFEQ      '0'
     C     *IN74         ANDEQ     '1'
     C                   MOVE      'JOV0627'     ZMSGNO
     C                   MOVE      '1'           *IN30
     C                   Z-ADD     21            LINNBR
     C                   Z-ADD     22            POSNBR
     C                   END
     C                   END
      *
      * UTVEKSLING MELLOM 2 PARTER SOM BEGGE SKAL HA BILNR-OPPFØLGING
      * ER IKKE TILLATT (MEN KAN LØSES VED Å LAGE 2 POSTER (MED SAMME
      * KVITT.NR) DER EN FØRER VIA EN "INTERIMS-PART")
     C     *IN74         IFEQ      '0'
     C     XATRNR        ANDNE     *ZEROS
     C     XBTRNR        ANDNE     *ZEROS
     C     *IN30         ANDEQ     '0'
     C                   MOVE      'JOV0954'     ZMSGNO
     C                   MOVE      '1'           *IN74
     C                   MOVE      '1'           *IN30
     C                   Z-ADD     21            LINNBR
     C                   Z-ADD     22            POSNBR
     C                   END
      * KRAV OM BILNR PÅ SIDE A ELLER B??
     C                   MOVE      ' '           WSBIAB
     C     *IN74         IFEQ      '0'
     C     XATRNR        ANDNE     *ZEROS
     C                   MOVE      'A'           WSBIAB
     C                   END
     C     *IN74         IFEQ      '0'
     C     XBTRNR        ANDNE     *ZEROS
     C                   MOVE      'B'           WSBIAB
     C                   END
      * MINST EN AV PARTENE REGISTRERT MED TRANSP.NR.
      *   - >  KRAV OM BILNR  -  MÅ TILHØRE EN AV PARTENE
     C     WSBIAB        IFNE      ' '
     C     WABILN        CHAIN     UNITL01                            77
     C     *IN77         IFEQ      '0'
     C     WSBIAB        IFEQ      'A'
     C     XATRNR        ANDNE     UNTRAN
     C                   MOVE      '1'           *IN77
     C                   END
     C     WSBIAB        IFEQ      'B'
     C     XBTRNR        ANDNE     UNTRAN
     C                   MOVE      '1'           *IN77
     C                   END
     C                   END
     C                   END
     C     *IN30         IFEQ      '0'
     C     *IN77         ANDEQ     '1'
     C                   MOVE      'JOV0955'     ZMSGNO
     C                   MOVE      '1'           *IN30
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     31            POSNBR
     C                   END
      *
     C     WAANT         MULT      -1            WBANT
      *
     C     *IN30         IFEQ      '0'
     C     *IN23         ANDNE     '1'
     C                   MOVE      WAPPNR        YYAPNR            8 0
     C                   MOVE      WAKVDT        YYKVDT            6 0
     C                   MOVE      WAANT         YYANT             5 0
     C                   MOVE      WBPPNR        YYBPNR            8 0
     C                   MOVE      PALØNR        YYLØNR            7 0
     C                   MOVE      WABUNR        YYBUNR            7 0
     C                   MOVE      WAKVNR        YYKVNR            9 0
     C                   CALL      'SYPA05'
     C                   PARM                    YYAPNR
     C                   PARM                    YYKVDT
     C                   PARM                    YYANT
     C                   PARM                    YYBPNR
     C                   PARM                    YYBUNR
     C                   PARM                    YYKVNR
     C                   PARM                    YYLØNR
     C                   PARM                    UIN23             1
     C     UIN23         IFEQ      '0'
     C                   MOVE      'JOV0629'     ZMSGNO
     C                   MOVE      '1'           *IN30
     C                   MOVEA     '11111'       *IN(71)
     C                   Z-ADD     20            LINNBR
     C                   Z-ADD     22            POSNBR
     C                   END
     C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     KDTFMT        BEGSR
     C***********************************************
     C     WAKVDT        IFNE      *ZEROS
     C                   CALL      'DATFM5'
     C                   PARM                    FIDTFM
     C                   PARM                    WAKVDT
     C                   END
     C*
     C                   ENDSR
     C***********************************************
     C     KDTFM2        BEGSR
     C***********************************************
     C     WSDATO        IFNE      *ZEROS
     C                   Z-ADD     WSDATO        YYDATO            6 0
     C                   CALL      'DATFM5'
     C                   PARM                    FIDTFM
     C                   PARM                    YYDATO
     C                   Z-ADD     YYDATO        WSDATO
     C                   END
     C*
     C                   ENDSR
     C***************************************************
     C     SEPÅ          BEGSR
     C***************************************************
     C                   MOVE      'B'           XXAB
     C     PALLK         CHAIN(N)  PALLD                              45
     C                   MOVE      PAAB          WXAB
     C                   MOVE      PAANT         WXANT
     C                   MOVE      PAPPNR        WXPPNR
     C                   MOVE      PATXT         WXTXT
     C     PAPPNR        CHAIN(N)  PAPA                               33
     C                   MOVEL     PPNAVN        WXNAVB
     C                   MOVE      PASTAT        WXSTAT
     C                   MOVE      PAPUNR        WXPUNR
     C                   MOVE      PAAVD         WXAVD
     C                   MOVE      PAOPD         WXOPD
     C                   MOVE      PAUSER        WXUSER
     C                   MOVE      PADATO        WXDATO
     C     PABEIE        IFEQ      'X'
     C                   MOVE      XXAB          WSBIAB
     C                   END
      *
     C                   MOVE      'A'           XXAB
     C     PALLK         CHAIN(N)  PALLD                              45
     C     PAPPNR        CHAIN(N)  PAPA                               33
     C                   MOVEL     PPNAVN        WXNAVA
     C     PABEIE        IFEQ      'X'
     C                   MOVE      XXAB          WSBIAB
     C                   END
      *
     C     VISPOS        TAG
     C                   Z-ADD     2             LINNBR
     C                   Z-ADD     2             POSNBR
     C     PAPUNR        COMP      *ZEROS                             61
     C     WXPUNR        COMP      *ZEROS                             62
     C     PAOPD         COMP      *ZEROS                             63
     C     WXOPD         COMP      *ZEROS                             64
     C                   EXFMT     VAREL2
     C     *IN06         IFEQ      '1'
     C     *IN07         OREQ      '1'
     C     *IN08         OREQ      '1'
     C     *IN09         OREQ      '1'
     C                   EXSR      VISOPD
     C                   GOTO      VISPOS
     C                   END
      *
      *
     C                   MOVE      ' '           WSNR
     C                   UPDATE    SUBFIL
     C                   ENDSR
      *
      *
     C***************************************************
     C     VISOPD        BEGSR
     C***************************************************
      *
     C     *IN06         IFEQ      '1'
     C     *IN07         OREQ      '1'
     C                   MOVE      PAAVD         XXAVD             4 0
     C                   MOVE      PAOPD         XXOPD             7 0
     C                   END
     C     *IN08         IFEQ      '1'
     C     *IN09         OREQ      '1'
     C                   MOVE      WXAVD         XXAVD
     C                   MOVE      WXOPD         XXOPD
     C                   END
     C*
     C     XXOPD         IFNE      *ZEROS
     C*
     C     *IN06         IFEQ      '1'
     C     *IN08         OREQ      '1'
     C                   MOVE      *ZEROS        DUMFLA            1 0
     C                   CALL      'SYFM21A'
     C                   PARM                    FIFIRM
     C                   PARM                    XXAVD
     C                   PARM                    XXOPD
     C                   PARM                    DUMFLA
     C                   END
      *
     C     *IN07         IFEQ      '1'
     C     *IN09         OREQ      '1'
     C                   MOVE      *ZEROS        DUMFLA            1 0
     C                   CALL      'SYFM21B'
     C                   PARM                    FIFIRM
     C                   PARM                    XXAVD
     C                   PARM                    XXOPD
     C                   PARM                    DUMFLA
     C                   END
      *
     C                   END
      *
     C                   ENDSR
      *
     C***************************************************
     C     MOVPAW        BEGSR
     C***************************************************
      * KALLES EN GANG FOR HVER SIDE (A/B) I ETT BILAG (LØPENR)
     C     XXAB          IFEQ      'A'
     C                   MOVE      PAANT         WAANT
     C                   MOVE      PAPPNR        WAPPNR
     C                   MOVE      PAKVDT        WAKVDT
     C                   MOVE      PATXT         WATXT
     C     WAPPNR        CHAIN(N)  PAPA                               33
     C                   MOVEL     PPNAVN        WANA30
     C                   ELSE
     C                   MOVE      PAANT         WBANT
     C                   MOVE      PAPPNR        WBPPNR
     C                   MOVE      PATXT         WBTXT
     C     WBPPNR        CHAIN(N)  PAPA                               33
     C                   MOVEL     PPNAVN        WBNA30
     C                   END
     C                   MOVE      PAKVNR        WAKVNR
     C                   MOVE      PABUNR        WABUNR
     C                   MOVE      PABILN        WABILN
     C     PABEIE        IFEQ      'X'
     C                   MOVE      XXAB          WSBIAB
     C                   END
     C                   ENDSR
     C***************************************************
     C     MOVWPA        BEGSR
     C***************************************************
      * KALLES EN GANG FOR HVER SIDE (A/B) I ETT BILAG (LØPENR)
     C     *IN45         IFEQ      '1'
     C                   MOVE      *ZEROS        PAPPNR
     C                   MOVE      *ZEROS        PAANT
     C                   MOVE      XXAB          PAAB
     C                   END
     C     XXAB          IFEQ      'A'
     C                   MOVE      WAPPNR        XXPPNR            8 0
     C                   MOVE      WAANT         XXANT             5 0
     C                   EXSR      JUSTER
     C                   MOVE      WAANT         PAANT
     C                   MOVE      WATXT         PATXT
     C                   MOVE      WAPPNR        PAPPNR
     C                   MOVE      PPNAVN        PAPNVN
     C                   ELSE
     C                   MOVE      WBPPNR        XXPPNR
     C     WAANT         MULT      -1            WBANT
     C                   MOVE      WBANT         XXANT
     C                   EXSR      JUSTER
     C                   MOVE      WBANT         PAANT
     C                   MOVE      WBTXT         PATXT
     C                   MOVE      WBPPNR        PAPPNR
     C                   MOVE      PPNAVN        PAPNVN
     C                   END
      * DATA FELLES FOR POST A/B....
     C     WAKVDT        IFGT      501231
     C     WAKVDT        ADD       19000000      PAKVDT
     C                   ELSE
     C     WAKVDT        ADD       20000000      PAKVDT
     C                   END
     C                   MOVE      WAKVNR        PAKVNR
     C                   MOVE      WABILN        PABILN
     C     WSBIAB        IFEQ      XXAB
     C                   MOVE      'X'           PABEIE
     C                   END
     C                   MOVE      WABUNR        PABUNR
     C                   MOVE      'B'           PASTAT
     C                   MOVE      *ZEROS        PAPUNR
     C                   MOVE      *ZEROS        PAAVD
     C                   MOVE      *ZEROS        PAOPD
     C                   MOVE      WSUSER        PAUSER
     C     PAUSER        IFEQ      *BLANKS
     C                   MOVE      ZUSER         PAUSER
     C                   END
     C                   MOVE      IDAG          PADATO
     C                   ENDSR
     C***************************************************
     C     JUSTER        BEGSR
     C***************************************************
      * JUSTER PALLESALDO VED NYE POSTER/ENDRINGER
      *
      * ER DET BYTTE AV PART?? - FULLT TREKK PÅ DEN GAMLE
     C     PAPPNR        IFNE      XXPPNR
     C     PAPPNR        ANDNE     *ZEROS
     C     PAPPNR        CHAIN     PAPA                               33
     C                   SUB       XXANT         PPSALT
     C     PASTAT        IFEQ      'P'
     C                   SUB       XXANT         PPSALP
     C                   END
     C                   UPDATE    PAPAR
     C                   ELSE
      * HVIS SAMME PART ELLER NY....
      * ANTALL MINUS GML ANTALL(EVT. 0 V/NY) => SALDO-JUSTERING
     C                   SUB       PAANT         XXANT             5 0
     C                   END
      *
      *
     C     XXPPNR        CHAIN     PAPA                               33
     C                   ADD       XXANT         PPSALT
     C     PASTAT        IFEQ      'P'
     C                   ADD       XXANT         PPSALP
     C                   END
     C                   UPDATE    PAPAR
      *
     C                   ENDSR
     C***************************************************
     C     MOVBW         BEGSR
     C***************************************************
     C                   MOVE      *ZEROS        WAANT
     C                   MOVE      *BLANKS       WATXT
     C                   MOVE      *ZEROS        WBANT
     C                   MOVE      *ZEROS        WBPPNR
     C                   MOVE      *BLANKS       WBTXT
     C                   MOVE      *BLANKS       WBNA30
     C                   MOVE      *BLANKS       WSBIAB
     C     *IN09         IFNE      *ON
     C                   MOVE      *ZEROS        WAKVNR
     C                   MOVE      *ZEROS        WAKVDT
     C                   MOVE      *BLANKS       WABILN
     C                   END
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Initiering                                           SRA /
      *////////////////////////////////////////////////////////////
     C     INIT          BEGSR
      *___________________
      * Definiere WorKfelt
      *"""""""""""""""""""
     C     *LIKE         DEFINE    *IN03         WIN03
     C     *LIKE         DEFINE    *IN30         WIN30
      *__________________
      * Definiere Nøkkler
      *""""""""""""""""""
      * KEY FOR UPDATE AV PALLEPOSTER
     C     PALLK         KLIST
     C                   KFLD                    PALØNR
     C                   KFLD                    XXAB              1
      * KEY FOR SETLL  AV PALLEPOSTER
     C     PALL1K        KLIST
     C                   KFLD                    WSBUNR
     C                   KFLD                    WSKVNR
     C     PALS1K        KLIST
     C                   KFLD                    WSSTAT
     C                   KFLD                    WSBUNR
     C                   KFLD                    WSKVNR
     C     PALL2K        KLIST
     C                   KFLD                    WSKVNR
     C     PALS2K        KLIST
     C                   KFLD                    WSSTAT
     C                   KFLD                    WSKVNR
     C     PALL3K        KLIST
     C                   KFLD                    WSLØNR
     C     PALS3K        KLIST
     C                   KFLD                    WSSTAT
     C                   KFLD                    WSLØNR
     C     PALL4K        KLIST
     C                   KFLD                    WSPPNR
     C                   KFLD                    WSDAT8
     C     PALS4K        KLIST
     C                   KFLD                    WSSTAT
     C                   KFLD                    WSPPNR
     C                   KFLD                    WSDAT8
      * KEY FOR READE, REDPE...
     C     PALL1E        KLIST
     C                   KFLD                    WSBUNR
     C     PALS1E        KLIST
     C                   KFLD                    WSSTAT
     C                   KFLD                    WSBUNR
     C     PALL2E        KLIST
     C                   KFLD                    WSKVNR
     C     PALS2E        KLIST
     C                   KFLD                    WSSTAT
     C                   KFLD                    WSKVNR
     C     PALL3E        KLIST
     C                   KFLD                    WSLØNR
     C     PALS3E        KLIST
     C                   KFLD                    WSSTAT
     C                   KFLD                    WSLØNR
     C     PALL4E        KLIST
     C                   KFLD                    WSPPNR
     C     PALS4E        KLIST
     C                   KFLD                    WSSTAT
     C                   KFLD                    WSPPNR
      * KEY FOR Å FINNE ÅPENT BUNTNR FOR AKTUELL USER
     C     BUNKEY        KLIST
     C                   KFLD                    WSSTAT
     C                   KFLD                    WSUSER
      *
     C     FIFIRM        SETLL     FIRM
     C                   READ      FIRM                                   33
      * HENT DFT PRINTER FOR EVT BUNTUTSKRIFT
     C     1             CHAIN(N)  PALFIR                             33
     C                   MOVE      PFPRIN        WSPRIN
     C                   MOVE      'S'           WSFORM
      *_____________
      * Init av felt
      *"""""""""""""
     C                   MOVE      *ZEROS        WSBUNR
     C                   MOVE      *ZEROS        WSKVNR
     C                   MOVE      *ZEROS        WSLØNR
     C                   MOVE      *ZEROS        WSPPNR
     C                   MOVE      *ZEROS        WSDAT8
     C                   MOVE      *ZEROS        GMBUNR
     C                   MOVE      *ZEROS        GMKVNR
     C                   MOVE      *ZEROS        GMLØNR
     C                   MOVE      *ZEROS        GMPPNR
     C                   MOVE      *ZEROS        GMDAT8
     C                   MOVE      ZUSER         WSUSER
     C                   MOVE      *ZEROS        WAKVDT
      * DAGENS DATO
     C                   MOVE      UDAY          DTD
     C                   MOVE      UMONTH        DTM
     C     UYEAR         IFGT      50
     C     UYEAR         ADD       1900          DTÅ
     C                   ELSE
     C     UYEAR         ADD       2000          DTÅ
     C                   END
      *
     C                   EXSR      DFTKEY
      *
     C                   ENDSR
      *
      *
      *////////////////////////////////////////////////////////////
      *  STANDARD KEY ER ÅPEN BUNT FOR AKTUELL USER, ELLERS SISTE FØRST
      *////////////////////////////////////////////////////////////
     C     DFTKEY        BEGSR
      *
     C                   MOVE      'B'           WSSTAT
      * Finnes ÅPEN bunt for aktuell USER??
     C     BUNKEY        SETLL     PALLDS05
     C     BUNKEY        READE     PALLDS05                               33
     C     *IN33         IFEQ      *OFF
     C                   MOVE      PABUNR        WSBUNR
     C                   MOVE      PABUNR        WABUNR
     C                   MOVEA     '01000000'    *IN(50)
     C                   ELSE
     C                   MOVE      *ZEROS        WABUNR
     C                   MOVE      9999999       WSBUNR
     C                   MOVEA     '00100000'    *IN(50)
     C                   END
     C                   ENDSR
