*****A**************************************************************
     A* RETTINGER I DETTE PROGRAM:
PTFnrA*Sek Sig Vers Beskrivelse...........................
"""""A*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
PXXXXA* 01 YBC PTF5 FAST LAGERLOKASJON FOR VARE
PXXXXA* 02 YBC PTF5 TEST OGSÅ OM VEKT OG KUBIKK ER ENDRET
PXXXXA* 03 YBC PTF5 LOGG ENDRINGSDATO, TID OG BRUKER
%%%%%A*%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
PXXXXA* 04 YBC PTF6 KRAV PÅ SERIENR
PXXXXA* 05 YBC PTF6 FRAKT-VEKT OG FRAKT-M3.
P06XXA* 06 YBC PTF6 FELTER FOR VAREGRUPER, ANSVARLIG FOR INNKJØP,
     A*             MIN.VERDI OG LEVERANDØRNR
PXXXXA* 07 YBC PTF6 FORHOLDSTALL STYKK-EKSE FLYTTES TIL VENSTRE
PXXXXA* 08 JOV PTF6 F6 PÅ NYVARE
P6XXXA* 09 YBC PTF6 NY FIL FOR LEVERANDØRS KUNDEREF.
P6XXXA* 10 YBC PTF6 NY FELT FOR VAREEIER
P6XXXA* 11 JOV PTF6 INAKTIV ER GUL
P6XXXA* 12 YBC PTF6 VIS SALDO ISTEDFOR MINST SALGSENHET
P7XXXA* 13 YBC PTF7 TARIFFNR, FYLKEKODE OG SKRÅK
P8XXXA* 14 YBC PTF8 TEST PÅ VAREEIER /VAREGRP.
P8061A* 15 YBC PTF8 NYE FELTER
     A* 16 JOV PTF8 MERK INAKTIV MM
P8XXX * 17 YBC PTF8 FAST PLUKKLOKASJON
P8068A* 21 TH  PTF8 NY HELPFUNKSJON  KUN DDS
*****A**************************************************************
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
N21  A                                      HLPPNLGRP(HLPSCR LL001PNL)
N21  A                                      HLPTITLE('ARBEIDE MED VAREREGISTER')
     A                                      HELP
     A                                      ALTHELP
      *
     A          R BILDE01
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CA03(03 'Avslutt program')
     A                                      CF04(04 'søking')
S21  A*         H                           HLPDOC('LL00101' 'LL001' 'SY400LO/L-
S21  A*                                     OGI')
N21  A          H                           HLPPNLGRP(HLPSCR LL001PNL)
     A                                      HLPARA(01 001 24 080)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/LL001R'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(30 YBC0879 SYMSGF)
     A                                      DSPATR(HI)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                  1 70TIME
     A                                  5  2MSGCON(27 YBC0880 SYMSGF)
     A            WSGRUP         8Y 0B  5 30DSPATR(UL)
     A                                      EDTCDE(4)
     A  99                                  DSPATR(RI)
     A                                  5 41MSGCON(35 YBC0882 SYMSGF)
     A                                  7  2MSGCON(27 YBC0881 SYMSGF)
     A            WSPNR          8Y 0B  7 30DSPATR(UL)
     A                                      EDTCDE(4)
     A  98                                  DSPATR(RI)
     A                                 23  2MSGCON(18 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(18 DIV0032 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R SUBFIL                    SFL
S15  A*           WSNR           1A  I  6  2VALUES(' ' '2' '3' '4' '5')
N15  A            WSNR           1A  I  6  2VALUES(' ' '2' '3' '4' '5' 'A' 'D')
     A            LVANR         15A  O  6  5
N11  A  41                                  COLOR(YLW)
S12  A*           LVVB          35A  O  6 21
S12  A*           LVMEH          3A  O  6 77
N12  A            LVVB          35A  H
N12  A            WXVB          33A  O  6 21
E12  A            LVAN01    R        O  6 55REFFLD(LLVARER/LVAN01 *LIBL/LLVARE)
     A                                      EDTCDE(4)
E12  A            LVAN02    R        O  6 61REFFLD(LLVARER/LVAN02 *LIBL/LLVARE)
     A                                      EDTCDE(4)
E12  A            LVAN03    R        O  6 67REFFLD(LLVARER/LVAN03 *LIBL/LLVARE)
     A                                      EDTCDE(4)
N12  A            LVSALD    R        O  6 73REFFLD(LLVARER/LVSALD *LIBL/LLVARE)
N12  A                                      EDTCDE(4)
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0016)
     A                                      SFLPAG(0015)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                      CA03(03 'AVSLUTT')
     A                                      CA05(05 'Frisk')
     A                                      CA06(06 'LAGE NY')
     A                                      CA12(12 'FORRIGE')
     A                                      OVERLAY
S21  A*         H                           HLPDOC('LL00102' 'LL001' 'SY400LO/L-
S21  A*                                     OGI')
N21  A          H                           HLPPNLGRP(LL00102 LL001PNL)
     A                                      HLPARA(01 001 24 080)
     A                                  1  2'SYSPED/LL001R'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(30 YBC0879 SYMSGF)
     A                                      DSPATR(HI)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                  1 70TIME
     A                                  2  2MSGCON(16 YBC0883 SYMSGF)
     A            WSGRUP         8Y 0O  2 19DSPATR(UL)
     A                                      DSPATR(HI)
     A                                      EDTCDE(4)
     A                                  2 29MSGCON(11 YBC0884 SYMSGF)
     A            WSPNR          8Y 0O  2 41DSPATR(UL)
     A                                      DSPATR(HI)
     A                                      EDTCDE(4)
     A            KNAVN         30A  O  2 50DSPATR(UL)
     A                                      DSPATR(HI)
     A            ZRECNO         4S 0O  2 76SFLRCDNBR
     A                                      DSPATR(ND)
S15  A*                                 3  2MSGCON(70 YBC0885 SYMSGF)
N15  A                                  3  2MSGCON(78 YBC0885 SYMSGF)
     A                                      COLOR(BLU)
S12  A*                                 3 74MSGCON(6 YBC0886 SYMSGF)
S12  A*                                     COLOR(WHT)
     A                                  4  2MSGCON(78 YBC0887 SYMSGF)
     A                                      DSPATR(HI)
     A            W2NR           1A  B  5  2VALUES(' ' '1' '2' '3' '4' '5')
     A            W2ANR         15A  B  5  5DSPATR(UL)
     A            W2VB          35A  B  5 21DSPATR(UL)
      *
     A          R BUNN
N16  A  39                             22  2MSGCON(075 JOV1367 *LIBL/SYMSGF)
N16  A                                      COLOR(YLW)
     A                                 23  2MSGCON(18 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(18 DIV0021 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 42MSGCON(18 DIV0022 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 62MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                             24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R VARELI
     A                                      CSRLOC(LINNBR     POSNBR)
N06  A                                      CF04(04 'søking')
N13  A                                      CF09(09 'TARIFF')
N13  A                                      CF10(10 'SPRÅK')
     A                                      CA12(12 'Forrige')
     A                                      OVERLAY
S21  A*         H                           HLPDOC('LL00103' 'LL001' 'SY400LO/L-
S21  A*                                     OGI')
N21  A          H                           HLPPNLGRP(LL00103 LL001PNL)
     A                                      HLPARA(01 001 05 080)
S21  A*         H                           HLPDOC('LL00104' 'LL001' 'SY400LO/L-
S21  A*                                     OGI')
N21  A          H                           HLPPNLGRP(LL00104 LL001PNL)
     A                                      HLPARA(06 001 17 080)
S21  A*         H                           HLPDOC('LL00105' 'LL001' 'SY400LO/L-
S21  A*                                     OGI')
N21  A          H                           HLPPNLGRP(LL00105 LL001PNL)
     A                                      HLPARA(18 001 18 080)
S21  A*         H                           HLPDOC('LL00106' 'LL001' 'SY400LO/L-
S21  A*                                     OGI')
N21  A          H                           HLPPNLGRP(LL00106 LL001PNL)
     A                                      HLPARA(19 001 24 080)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1  2'SYSPED/LL001R'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(30 YBC0879 SYMSGF)
     A                                      DSPATR(HI)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                  1 70TIME
     A                                  2  2MSGCON(16 YBC0883 SYMSGF)
     A            WSGRUP         8Y 0O  2 19DSPATR(UL)
     A                                      DSPATR(HI)
     A                                      EDTCDE(4)
     A                                  2 29MSGCON(11 YBC0884 SYMSGF)
     A            WSPNR          8Y 0O  2 41DSPATR(UL)
     A                                      DSPATR(HI)
     A                                      EDTCDE(4)
     A            KNAVN         30A  O  2 50DSPATR(UL)
     A                                      DSPATR(HI)
N06  A                                  3  2MSGCON(8 YBC0888 SYMSGF)
N06  A            WSLNR         17A  B  3 11DSPATR(UL)
N06  A  56                                  DSPATR(RI)
S09  A*           KUALFA        10A  O  3 29DSPATR(UL)
N09  A            XADRNA        10A  O  3 29DSPATR(UL)
N06  A                                      DSPATR(HI)
N06  A                                  3 41MSGCON(8 YBC0889 SYMSGF)
N06  A            WSANS         30A  B  3 50DSPATR(UL)
N06  A  55                                  DSPATR(RI)
     A                                  4  2MSGCON(19 YBC0890 SYMSGF)
     A            WSANR         15A  B  4 22DSPATR(UL)
     A  40                                  DSPATR(HI)
     A  40                                  DSPATR(PR)
     A  90                                  DSPATR(RI)
S13  A*                                 4 42MSGCON(19 YBC0891 SYMSGF)
S13  A*           WSANRL        15A  B  4 62DSPATR(UL)
N13  A                                  4 38MSGCON(9 YBC1825 SYMSGF)
N13  A            WSANRL        15A  B  4 48DSPATR(UL)
     A  65                                  DSPATR(RI)
N13  A                                  4 64MSGCON(7 YBC1826 SYMSGF)
N13  A            WSTANR         8  0B  4 72DSPATR(UL)
N13  A  54                                  DSPATR(RI)
N13  A                                      EDTCDE(4)
     A                                  5  2MSGCON(19 YBC0892 SYMSGF)
     A            WSVB          35A  B  5 22DSPATR(UL)
     A  89                                  DSPATR(RI)
N13  A                                  5 64MSGCON(7 YBC1827 SYMSGF)
N13  A            WSFYL          2   B  5 72DSPATR(UL)
N13  A  53                                  DSPATR(RI)
S06  A*                                 6 46'Faktor i     '
S06  A*                                     COLOR(WHT)
N06  A                                  6  2MSGCON(19 YBC0893 SYMSGF)
N06  A            WSAG01         5A  B  6 22DSPATR(UL)
N14  A  47                                  DSPATR(RI)
N06  A            WSAG02         5A  B  6 30DSPATR(UL)
N14  A  48                                  DSPATR(RI)
N06  A            WSAG03         5A  B  6 38DSPATR(UL)
N14  A  49                                  DSPATR(RI)
N06  A            WSAG04         5A  B  6 46DSPATR(UL)
N14  A  50                                  DSPATR(RI)
N06  A            WSAG05         5A  B  6 54DSPATR(UL)
N14  A  51                                  DSPATR(RI)
N10  A                                  6 62MSGCON(10 YBC1714 SYMSGF)
N10  A            WSEIER         5A  B  6 73DSPATR(UL)
N14  A  52                                  DSPATR(RI)
N17  A                                  7  2'Fast plukklokasjon:'
N17  A            WSPLK1         5A  B  7 22DSPATR(UL)
N17  A            WSPLK2        20A  B  7 28DSPATR(UL)
N17  A                                  7 51'Min.:'
N17  A            WSPMIN         5Y 0B  7 57DSPATR(UL)
N17  A                                      EDTCDE(4)
N17  A                                  7 63'Supplering:'
N17  A            WSPNTS         5Y 0B  7 75DSPATR(UL)
N17  A                                      EDTCDE(4)
E17  A  31                              8  2'INAKTIV'
N15  A                                      COLOR(RED)
E17  A N31                              8  2'AKTIV'
N15  A                                      COLOR(RED)
E17  A                                  8 26MSGCON(4 YBC0894 SYMSGF)
     A                                      COLOR(WHT)
S06  A*                                 7 46'forhold til..'
E17  A                                  8 46MSGCON(13 YBC0895 SYMSGF)
     A                                      COLOR(WHT)
E17  A                                  9 26MSGCON(52 YBC0896 SYMSGF)
     A                                      COLOR(WHT)
     A                                 10  2MSGCON(19 YBC0897 SYMSGF)
     A            WSEH0          3A  B 10 26DSPATR(HI)
     A  88                                  DSPATR(RI)
     A                                      DSPATR(PR)
     A            WSEH0T        15A  B 10 30DSPATR(HI)
     A  87                                  DSPATR(RI)
     A                                      DSPATR(PR)
     A            WSVKT0         7Y 2B 10 61DSPATR(UL)
     A  86                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A            WSKUB0         7Y 4B 10 70DSPATR(UL)
     A  85                                  DSPATR(RI)
     A                                      EDTCDE(4)
N05  A            WSVKF0         7Y 2B 11 61DSPATR(UL)
N05  A  64                                  DSPATR(RI)
N05  A                                      EDTCDE(4)
N05  A            WSKUF0         7Y 4B 11 70DSPATR(UL)
N05  A  63                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                 12  2MSGCON(19 YBC0898 SYMSGF)
     A            WSEH1          3A  B 12 26DSPATR(HI)
     A  84                                  DSPATR(RI)
     A                                      DSPATR(PR)
     A            WSEH1T        15A  B 12 30DSPATR(HI)
     A  83                                  DSPATR(RI)
     A                                      DSPATR(PR)
S07  A*           WSAN01         5Y 0B 12 55DSPATR(UL)
N07  A            WSAN01         5Y 0B 12 47DSPATR(UL)
     A  82                                  DSPATR(RI)
     A                                      EDTCDE(4)
N07  A            WSAN00         5Y 0O 12 55DSPATR(UL)
N07  A                                      DSPATR(HI)
N07  A                                      EDTCDE(4)
     A            WSVKT1         7Y 2B 12 61DSPATR(UL)
     A  81                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A            WSKUB1         7Y 4B 12 70DSPATR(UL)
     A  80                                  DSPATR(RI)
     A                                      EDTCDE(4)
N05  A            WSVKF1         7Y 2B 13 61DSPATR(UL)
N05  A  62                                  DSPATR(RI)
N05  A                                      EDTCDE(4)
N05  A            WSKUF1         7Y 4B 13 70DSPATR(UL)
N05  A  61                                  DSPATR(RI)
N05  A                                      EDTCDE(4)
     A                                 14  2MSGCON(19 YBC0899 SYMSGF)
     A            WSEH2          3A  B 14 26DSPATR(HI)
     A  79                                  DSPATR(RI)
     A                                      DSPATR(PR)
     A            WSEH2T        15A  B 14 30DSPATR(HI)
     A  78                                  DSPATR(RI)
     A                                      DSPATR(PR)
     A            WSAN12         5Y 0B 14 47DSPATR(UL)
     A  77                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A            WSAN02         5Y 0B 14 55DSPATR(HI)
     A  76                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                      DSPATR(PR)
     A            WSVKT2         7Y 2B 14 61DSPATR(UL)
     A  75                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A            WSKUB2         7Y 4B 14 70DSPATR(UL)
     A  74                                  DSPATR(RI)
     A                                      EDTCDE(4)
N05  A            WSVKF2         7Y 2B 15 61DSPATR(UL)
N05  A  60                                  DSPATR(RI)
N05  A                                      EDTCDE(4)
N05  A            WSKUF2         7Y 4B 15 70DSPATR(UL)
N05  A  59                                  DSPATR(RI)
N05  A                                      EDTCDE(4)
     A                                 16  2MSGCON(19 YBC0900 SYMSGF)
     A            WSEH3          3A  B 16 26DSPATR(HI)
     A  73                                  DSPATR(RI)
     A                                      DSPATR(PR)
     A            WSEH3T        15A  B 16 30DSPATR(HI)
     A  72                                  DSPATR(RI)
     A                                      DSPATR(PR)
     A            WSAN23         5Y 0B 16 47DSPATR(UL)
     A  71                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A            WSAN03         5Y 0B 16 55DSPATR(HI)
     A  70                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                      DSPATR(PR)
     A            WSVKT3         7Y 2B 16 61DSPATR(UL)
     A  69                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A            WSKUB3         7Y 4B 16 70DSPATR(UL)
     A  68                                  DSPATR(RI)
     A                                      EDTCDE(4)
N05  A            WSVKF3         7Y 2B 17 61DSPATR(UL)
N05  A  58                                  DSPATR(RI)
N05  A                                      EDTCDE(4)
N05  A            WSKUF3         7Y 4B 17 70DSPATR(UL)
N05  A  57                                  DSPATR(RI)
N05  A                                      EDTCDE(4)
     A                                 18  2MSGCON(19 YBC0901 SYMSGF)
     A            WSMIN          1Y 0B 18 23DSPATR(UL)
     A            WSMEH          3A  B 18 26DSPATR(HI)
     A  66                                  DSPATR(RI)
     A                                      DSPATR(PR)
N15  A                                 18 30'Enh.pr:'
N15  A            WSPRI          9Y 2B 18 38DSPATR(UL)
N15  A                                      EDTCDE(4)
E15  A                                 18 49MSGCON(6 YBC0902 SYMSGF)
E15  A            WSSALD         7Y 0B 18 56DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(YLW)
E15  A                                 18 64MSGCON(10 YBC0903 SYMSGF)
N15  A            WSMSTK         5Y 0B 18 75DSPATR(UL)
N06  A                                      EDTCDE(4)
S15  A*                                19  2MSGCON(19 YBC0904 SYMSGF)
N15  A                                 19  2'Serienr(I/U)/lengd:'
N04  A            WSSN           1A  B 19 23DSPATR(UL)
N04  A                                      VALUES(' ' 'I' 'U')
N15  A            WSNTSN         2Y 0B 19 25DSPATR(UL)
N15  A                                      EDTCDE(4)
N15  A                                      COMP(LE 35)
N15  A                                 19 30'S.pris:'
N15  A            WSSPRI         9Y 2B 19 38DSPATR(UL)
N15  A                                      EDTCDE(4)
N15  A                                 19 49'Ktr innl:'
N15  A            WSKTLI         1A  B 19 59DSPATR(UL)
N15  A                                      VALUES(' ' 'J' 'N')
N03  A                                 19 64MSGCON(15 YBC0905 SYMSGF)
     A                                 20  2MSGCON(19 YBC0906 SYMSGF)
     A            WSBATC         1A  B 20 23DSPATR(UL)
     A                                      VALUES(' ' 'J' 'N')
S15  A*                                20 32MSGCON(19 YBC0907 SYMSGF)
S15  A*           WSDTU          1A  B 20 53DSPATR(UL)
N15  A                                 20 25'Utl.DT:'
N15  A            WSDTU          1A  B 20 33DSPATR(UL)
     A                                      VALUES(' ' 'J' 'N')
N15  A                                 20 35'Pro.DT:'
N15  A            WSDTP          1A  B 20 43DSPATR(UL)
N15  A                                      VALUES(' ' 'J' 'N')
N15  A                                 20 45'Pl Innl./Utl.DT:'
N15  A            WSPLS          1A  B 20 62DSPATR(UL)
N15  A                                      VALUES(' ' 'I' 'U')
N03  A                                 20 64MSGCON(6 YBC0912 SYMSGF)
N03  A            WSBUSE        10A  O 20 71DSPATR(UL)
N03  A                                      DSPATR(HI)
     A                                 21  2MSGCON(19 YBC0908 SYMSGF)
     A            WSTXT         40A  B 21 23DSPATR(UL)
N03  A                                 21 64MSGCON(6 YBC0909 SYMSGF)
N03  A            WSBDAT         8  0O 21 71DSPATR(UL)
N03  A                                      DSPATR(HI)
N03  A                                      EDTWRD('    .  .  ')
N01  A                                 22  2MSGCON(19 YBC0910 SYMSGF)
N01  A            WSLOK1         5A  B 22 23DSPATR(UL)
N01  A            WSLOK2        20A  B 22 29DSPATR(UL)
N03  A                                 22 64MSGCON(6 YBC0911 SYMSGF)
N03  A            WSBKL          6  0O 22 71DSPATR(UL)
N03  A                                      DSPATR(HI)
N03  A                                      EDTWRD('  .  .  ')
N06  A                                 23  2MSGCON(18 DIV0032 SYMSGF)
N06  A                                      COLOR(BLU)
E06  A                                 23 22MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
N13  A                                 23 42'F9=Arb. med tariff'
N13  A                                      COLOR(BLU)
N13  A                                 23 62'F10=Arb. med språk'
N13  A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                             24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
      *
     A          R VAREL3
     A                                      CA12(12 'Forrige')
N16  A                                      CF10(10 'INAKTIV')
     A                                      OVERLAY
     A                                  1  2'SYSPED/LL001R'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(30 YBC0879 SYMSGF)
     A                                      DSPATR(HI)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                  1 70TIME
     A                                  2  2MSGCON(16 YBC0883 SYMSGF)
     A            WSGRUP         8Y 0O  2 19DSPATR(UL)
     A                                      DSPATR(HI)
     A                                      EDTCDE(4)
     A                                  2 29MSGCON(11 YBC0884 SYMSGF)
     A            WSPNR          8Y 0O  2 41DSPATR(UL)
     A                                      DSPATR(HI)
     A                                      EDTCDE(4)
     A            KNAVN         30A  O  2 50DSPATR(UL)
     A                                      DSPATR(HI)
N06  A                                  3  2MSGCON(8 YBC0888 SYMSGF)
N06  A            LVLNR         17A  O  3 11DSPATR(UL)
N06  A                                      DSPATR(HI)
S09  A*           KUALFA        10A  O  3 29DSPATR(UL)
N09  A            XADRNA        10A  O  3 29DSPATR(UL)
N06  A                                      DSPATR(HI)
N06  A                                  3 41MSGCON(8 YBC0889 SYMSGF)
N06  A            LVANS         30A  O  3 50DSPATR(UL)
N06  A                                      DSPATR(HI)
     A                                  4  2MSGCON(19 YBC0890 SYMSGF)
     A            LVANR         15A  O  4 22DSPATR(UL)
     A                                      DSPATR(HI)
S13  A*                                 4 42MSGCON(19 YBC0891 SYMSGF)
S13  A*           LVANRL        15A  O  4 62DSPATR(UL)
N13  A                                  4 38MSGCON(9 YBC1825 SYMSGF)
N13  A            LVANRL        15A  O  4 48DSPATR(UL)
     A                                      DSPATR(HI)
N13  A                                  4 64MSGCON(7 YBC1826 SYMSGF)
N13  A            WSTANR         8  0O  4 72DSPATR(UL)
N13  A                                      DSPATR(HI)
N13  A                                      EDTCDE(4)
     A                                  5  2MSGCON(19 YBC0892 SYMSGF)
     A            LVVB          35A  O  5 22DSPATR(UL)
     A                                      DSPATR(HI)
N13  A                                  5 64MSGCON(7 YBC1827 SYMSGF)
N13  A            WSFYL          2   O  5 72DSPATR(UL)
N13  A                                      DSPATR(HI)
S06  A*                                 6 46'Faktor i     '
S06  A*                                     COLOR(WHT)
N06  A                                  6  2MSGCON(19 YBC0893 SYMSGF)
N06  A            LVAG01         5A  O  6 22DSPATR(UL)
N06  A                                      DSPATR(HI)
N06  A            LVAG02         5A  O  6 30DSPATR(UL)
N06  A                                      DSPATR(HI)
N06  A            LVAG03         5A  O  6 38DSPATR(UL)
N06  A                                      DSPATR(HI)
N06  A            LVAG04         5A  O  6 46DSPATR(UL)
N06  A                                      DSPATR(HI)
N06  A            LVAG05         5A  O  6 54DSPATR(UL)
N06  A                                      DSPATR(HI)
N10  A                                  6 62MSGCON(10 YBC1714 SYMSGF)
N10  A            LVEIER         5A  O  6 73DSPATR(UL)
N10  A                                      DSPATR(HI)
N17  A                                  7  2'Fast plukklokasjon:'
N17  A            LVPLK1         5A  O  7 22DSPATR(UL)
N17  A                                      DSPATR(HI)
N17  A            LVPLK2        20A  O  7 28DSPATR(UL)
N17  A                                      DSPATR(HI)
N17  A                                  7 51'Min.:'
N17  A            LVPMIN         5Y 0O  7 57DSPATR(UL)
N17  A                                      DSPATR(HI)
N17  A                                      EDTCDE(4)
N17  A                                  7 63'Supplering:'
N17  A            LVPNTS         5Y 0O  7 75DSPATR(UL)
N17  A                                      DSPATR(HI)
N17  A                                      EDTCDE(4)
E17  A  31                              8  2'INAKTIV'
N15  A                                      COLOR(RED)
E17  A N31                              8  2'AKTIV'
N15  A                                      COLOR(RED)
E17  A                                  8 26MSGCON(4 YBC0894 SYMSGF)
     A                                      COLOR(WHT)
S06  A*                                 7 46'forhold til..'
E17  A                                  8 46MSGCON(13 YBC0895 SYMSGF)
     A                                      COLOR(WHT)
E17  A                                  9 26MSGCON(52 YBC0896 SYMSGF)
     A                                      COLOR(WHT)
     A                                 10  2MSGCON(19 YBC0897 SYMSGF)
     A            LVEH0          3A  O 10 26DSPATR(UL)
     A                                      DSPATR(HI)
     A            LVEH0T        15A  O 10 30DSPATR(UL)
     A                                      DSPATR(HI)
     A            LVVKT0         7Y 2O 10 61DSPATR(UL)
     A                                      DSPATR(HI)
     A                                      EDTCDE(4)
     A            LVKUB0         7Y 4O 10 70DSPATR(UL)
     A                                      DSPATR(HI)
     A                                      EDTCDE(4)
N05  A            LVVKF0         7Y 2O 11 61DSPATR(UL)
N05  A                                      DSPATR(HI)
N05  A                                      EDTCDE(4)
N05  A            LVKUF0         7Y 4O 11 70DSPATR(UL)
N05  A                                      DSPATR(HI)
N05  A                                      EDTCDE(4)
     A                                 12  2MSGCON(19 YBC0898 SYMSGF)
     A            LVEH1          3A  O 12 26DSPATR(UL)
     A                                      DSPATR(HI)
     A            LVEH1T        15A  O 12 30DSPATR(UL)
     A                                      DSPATR(HI)
S07  A*           LVAN01         5Y 0O 12 55DSPATR(UL)
N07  A            LVAN01         5Y 0O 12 47DSPATR(UL)
     A                                      DSPATR(HI)
     A                                      EDTCDE(4)
N07  A            LVAN00         5Y 0O 12 55DSPATR(UL)
N07  A                                      DSPATR(HI)
N07  A                                      EDTCDE(4)
     A            LVVKT1         7Y 2O 12 61DSPATR(UL)
     A                                      DSPATR(HI)
     A                                      EDTCDE(4)
     A            LVKUB1         7Y 4O 12 70DSPATR(UL)
     A                                      DSPATR(HI)
     A                                      EDTCDE(4)
N05  A            LVVKF1         7Y 2O 13 61DSPATR(UL)
N05  A                                      DSPATR(HI)
N05  A                                      EDTCDE(4)
N05  A            LVKUF1         7Y 4O 13 70DSPATR(UL)
N05  A                                      DSPATR(HI)
N05  A                                      EDTCDE(4)
     A                                 14  2MSGCON(19 YBC0899 SYMSGF)
     A            LVEH2          3A  O 14 26DSPATR(UL)
     A                                      DSPATR(HI)
     A            LVEH2T        15A  O 14 30DSPATR(UL)
     A                                      DSPATR(HI)
     A            LVAN12         5Y 0O 14 47DSPATR(UL)
     A                                      DSPATR(HI)
     A                                      EDTCDE(4)
     A            LVAN02         5Y 0O 14 55DSPATR(UL)
     A                                      DSPATR(HI)
     A                                      EDTCDE(4)
     A            LVVKT2         7Y 2O 14 61DSPATR(UL)
     A                                      DSPATR(HI)
     A                                      EDTCDE(4)
     A            LVKUB2         7Y 4O 14 70DSPATR(UL)
     A                                      DSPATR(HI)
     A                                      EDTCDE(4)
N05  A            LVVKF2         7Y 2O 15 61DSPATR(UL)
N05  A                                      DSPATR(HI)
N05  A                                      EDTCDE(4)
N05  A            LVKUF2         7Y 4O 15 70DSPATR(UL)
N05  A                                      DSPATR(HI)
N05  A                                      EDTCDE(4)
     A                                 16  2MSGCON(19 YBC0900 SYMSGF)
     A            LVEH3          3A  O 16 26DSPATR(UL)
     A                                      DSPATR(HI)
     A            LVEH3T        15A  O 16 30DSPATR(UL)
     A                                      DSPATR(HI)
     A            LVAN23         5Y 0O 16 47DSPATR(UL)
     A                                      DSPATR(HI)
     A                                      EDTCDE(4)
     A            LVAN03         5Y 0O 16 55DSPATR(UL)
     A                                      DSPATR(HI)
     A                                      EDTCDE(4)
     A            LVVKT3         7Y 2O 16 61DSPATR(UL)
     A                                      DSPATR(HI)
     A                                      EDTCDE(4)
     A            LVKUB3         7Y 4O 16 70DSPATR(UL)
     A                                      DSPATR(HI)
     A                                      EDTCDE(4)
N05  A            LVVKF3         7Y 2O 17 61DSPATR(UL)
N05  A                                      DSPATR(HI)
N05  A                                      EDTCDE(4)
N05  A            LVKUF3         7Y 4O 17 70DSPATR(UL)
N05  A                                      DSPATR(HI)
N05  A                                      EDTCDE(4)
     A                                 18  2MSGCON(19 YBC0901 SYMSGF)
     A            LVMIN1         1Y 0O 18 23DSPATR(UL)
     A                                      DSPATR(HI)
     A            LVMEH          3A  O 18 26DSPATR(HI)
     A                                      DSPATR(UL)
N15  A                                 18 30'Enh.pr:'
N15  A            LVPRI          9Y 2O 18 38DSPATR(UL)
N15  A                                      DSPATR(HI)
N15  A                                      EDTCDE(4)
E15  A                                 18 49MSGCON(6 YBC0902 SYMSGF)
E15  A            LVSALD         7Y 0O 18 56DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      DSPATR(HI)
E15  A                                 18 64MSGCON(10 YBC0903 SYMSGF)
E15  A            LVMSTK         5Y 0O 18 75DSPATR(UL)
N06  A                                      DSPATR(HI)
N06  A                                      EDTCDE(4)
S15  A*                                19  2MSGCON(19 YBC0904 SYMSGF)
N15  A                                 19  2'Serienr(I/U)/lengd:'
N04  A            LVSN           1A  O 19 23DSPATR(UL)
N04  A                                      DSPATR(HI)
N15  A            LVNTSN         2Y 0O 19 25DSPATR(UL)
N15  A                                      DSPATR(HI)
N15  A                                      EDTCDE(4)
N15  A                                 19 30'S.pris:'
N15  A            LVSPRI         9Y 2O 19 38DSPATR(UL)
N15  A                                      DSPATR(HI)
N15  A                                      EDTCDE(4)
N15  A                                 19 49'Ktr innl:'
N15  A            LVKTLI         1A  O 19 59DSPATR(UL)
N15  A                                      DSPATR(HI)
N03  A                                 19 64MSGCON(15 YBC0905 SYMSGF)
     A                                 20  2MSGCON(19 YBC0906 SYMSGF)
     A            LVBATC         1A  O 20 23DSPATR(UL)
     A                                      DSPATR(HI)
S15  A*                                20 32MSGCON(19 YBC0907 SYMSGF)
S15  A*           LVDTU          1A  O 20 53DSPATR(UL)
N15  A                                 20 25'Utl.DT:'
N15  A            LVDTU          1A  O 20 33DSPATR(UL)
     A                                      DSPATR(HI)
N15  A                                 20 35'Pro.DT:'
N15  A            LVDTP          1A  O 20 43DSPATR(UL)
N15  A                                      DSPATR(HI)
N15  A                                 20 45'Pl Innl./Utl.DT:'
N15  A            LVPLS          1A  O 20 62DSPATR(UL)
N15  A                                      DSPATR(HI)
N03  A                                 20 64MSGCON(6 YBC0912 SYMSGF)
N03  A            LVBUSE        10A  O 20 71DSPATR(UL)
N03  A                                      DSPATR(HI)
     A                                 21  2MSGCON(19 YBC0908 SYMSGF)
     A            LVTXT         40A  O 21 23DSPATR(UL)
     A                                      DSPATR(HI)
N03  A                                 21 64MSGCON(6 YBC0909 SYMSGF)
N03  A            LVBDAT         8  0O 21 71DSPATR(UL)
N03  A                                      DSPATR(HI)
N03  A                                      EDTWRD('    .  .  ')
N01  A                                 22  2MSGCON(19 YBC0910 SYMSGF)
N01  A            LVLOK1         5A  O 22 23DSPATR(UL)
N01YBA                                      DSPATR(HI)
N01  A            LVLOK2        20A  O 22 29DSPATR(UL)
N01YBA                                      DSPATR(HI)
N03  A                                 22 64MSGCON(6 YBC0911 SYMSGF)
N03  A            LVBKL          6  0O 22 71DSPATR(UL)
N03  A                                      DSPATR(HI)
N03  A                                      EDTWRD('  .  .  ')
     A                                 23  2MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
N16  A  31                             23 22MSGCON(040 JOV1368 *LIBL/SYMSGF)
N16  A                                      COLOR(RED)
N16  A N31                             23 22MSGCON(040 JOV1369 *LIBL/SYMSGF)
N16  A                                      COLOR(RED)
     A                                 24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A*
     A          R NYVARE
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CA03(03 'AVSLUTT')
N08  A                                      CA06(06 'LAGE NY')
     A                                      CA12(12 'AVSLUTT')
S21  A*         H                           HLPDOC('LL00102' 'LL001' 'SY400LO/L-
S21  A*                                     OGI')
N21  A          H                           HLPPNLGRP(LL00102 LL001PNL)
     A                                      HLPARA(01 001 24 080)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/LL001R'
     A                                      COLOR(BLU)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                  1 70TIME
     A                                  1 19MSGCON(30 YBC0879 SYMSGF)
     A                                      DSPATR(HI)
     A                                  2  2MSGCON(16 YBC0883 SYMSGF)
     A            WSGRUP         8Y 0O  2 19DSPATR(UL)
     A                                      DSPATR(HI)
     A                                      EDTCDE(4)
     A                                  2 29MSGCON(11 YBC0884 SYMSGF)
     A            WSPNR          8Y 0O  2 41DSPATR(UL)
     A                                      DSPATR(HI)
     A                                      EDTCDE(4)
     A            KNAVN         30A  O  2 50DSPATR(UL)
     A                                      DSPATR(HI)
S15  A*                                 3  2MSGCON(70 YBC0885 SYMSGF)
N15  A                                  3  2MSGCON(78 YBC0885 SYMSGF)
     A                                      COLOR(BLU)
S12  A*                                 3 74MSGCON(6 YBC0886 SYMSGF)
S12  A*                                     COLOR(WHT)
     A                                  4  2MSGCON(78 YBC0887 SYMSGF)
     A                                      DSPATR(HI)
     A            W2NR           1A  B  5  2VALUES(' ' '1' '2' '3' '4' '5')
     A            W2ANR         15A  B  5  5DSPATR(UL)
     A            W2VB          35A  B  5 21DSPATR(UL)
     A                                  7  2MSGCON(40 YBC1696 SYMSGF)
     A                                      COLOR(RED)
S06  A*                                23  2'F3=Avslutt   F12=Avbryt'
N06  A                                 23  2MSGCON(18 DIV0020 SYMSGF)
N06  A                                      COLOR(BLU)
N06  A                                 23 42MSGCON(18 DIV0022 SYMSGF)
N06  A                                      COLOR(BLU)
N06  A                                 23 62MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                             24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R ALARM
     A                                      WINDOW(7 10 4 60 *NOMSGLIN)
     A                                      OVERLAY
     A                                  1  2MSGCON(58 YBC0913 SYMSGF)
     A                                      COLOR(YLW)
     A                                  2  2MSGCON(58 YBC0914 SYMSGF)
     A                                      COLOR(YLW)
     A                                  3  2MSGCON(58 YBC0915 SYMSGF)
     A                                      COLOR(RED)
     A                                  4  2MSGCON(58 YBC0916 SYMSGF)
     A                                      COLOR(RED)
