/* *********************************************************************   */
/*       * VISE PDF ON THE FLY                                             */
/* *********************************************************************   */
/*       * ÅPNE SAD I PDF-FORMAT                                           */

             PGM        PARM(&AVD &OPD &KODE &STMF &BANE +
                          &STMF2 &BANE2)
             DCL        VAR(&FILEN) TYPE(*CHAR) LEN(10)
             DCL        VAR(&FAXTYP) TYPE(*CHAR) LEN(1) VALUE('A')
             DCL        VAR(&KODE) TYPE(*CHAR) LEN(1)
             DCL        VAR(&AVD) TYPE(*CHAR) LEN(4)
             DCL        VAR(&OPD) TYPE(*CHAR) LEN(7)
             DCL        VAR(&PRT1) TYPE(*CHAR) LEN(10)
             DCL        VAR(&PRT2) TYPE(*CHAR) LEN(10)
             DCL        VAR(&PRT3) TYPE(*CHAR) LEN(10)
             DCL        VAR(&QUSER) TYPE(*CHAR) LEN(10)
             DCL        VAR(&QJOB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&QNBR) TYPE(*CHAR) LEN(6)
             DCL        VAR(&STMF) TYPE(*CHAR) LEN(152)
             DCL        VAR(&BANE) TYPE(*CHAR) LEN(50)
             DCL        VAR(&STMF2) TYPE(*CHAR) LEN(152)
             DCL        VAR(&BANE2) TYPE(*CHAR) LEN(50)
             RTVJOBA    JOB(&QJOB) USER(&QUSER) NBR(&QNBR)
/*******************************************************************/
/*      SLETTE EVENTUELL GAMMEL STREAMFILE I TEMP                 */
/*******************************************************************/
             DEL        OBJLNK(&BANE)
             MONMSG     MSGID(CPF0000)
             DEL        OBJLNK(&BANE2)
             MONMSG     MSGID(CPF0000)
/*******************************************************************/
/*      OVERRIDE IMPORT                                            */
/*******************************************************************/
             IF         COND(&KODE = 'I') THEN(DO)
             OVRPRTF    FILE(SAD61PF) DEVTYPE(*AFPDS) PAGESIZE(108 +
                          120) LPI(9) CPI(15) FRONTMGN(.24 0) +
                          OVRFLW(106) FRONTOVL(FSAD38 0 0.2) +
                          OUTQ(OUTMAIL) FORMTYPE(*STD) +
                          TOSTMF(&BANE) WSCST(*PDF)
             OVRPRTF    FILE(SAD61PFT) DEVTYPE(*AFPDS) PAGESIZE(72 +
                          120) LPI(6) CPI(15) OVRFLW(72) PAGRTT(0) +
                          OUTQ(OUTMAIL) FORMTYPE(*STD) +
                          TOSTMF(&BANE2) WSCST(*PDF)
             OVRPRTF    FILE(SAD61PFR) DEVTYPE(*SCS) PAGESIZE(72 80) +
                          LPI(6) CPI(10) OVRFLW(70) OUTQ(OUTMAIL) +
                          FORMTYPE(*STD)
             ENDDO
/*******************************************************************/
/*      OVERRIDE EKSPORT                                           */
/*******************************************************************/
             IF         COND(&KODE = 'E') THEN(DO)
             OVRPRTF    FILE(SAD52PF) DEVTYPE(*AFPDS) PAGESIZE(108 +
                          120) LPI(9) CPI(15) FRONTMGN(.24 0) +
                          OVRFLW(106) FRONTOVL(FSAD38 0 0.2) +
                          OUTQ(OUTMAIL) FORMTYPE(*STD) +
                          TOSTMF(&BANE) WSCST(*PDF)
             OVRPRTF    FILE(SAD52PFT) DEVTYPE(*AFPDS) PAGESIZE(72 +
                          120) LPI(6) CPI(15) OVRFLW(72) PAGRTT(0) +
                          OUTQ(OUTMAIL) FORMTYPE(*STD) +
                          TOSTMF(&BANE2) WSCST(*PDF)
             ENDDO
/*******************************************************************/
             OVRPRTF    FILE(TOLLDEK2) DEVTYPE(*AFPDS) PAGESIZE(72 +
                          83) LPI(6) CPI(10) OVRFLW(72) +
                          OUTQ(OUTMAIL) FORMTYPE(*STD) HOLD(*YES)
             OVRDBF     FILE(WFILEIMP) MBR(ARKIV)
             OVRDBF     FILE(WFILEEXP) MBR(ARKIV)
/*******************************************************************/
/*      IMPORT                                                     */
/*******************************************************************/
             IF         COND(&KODE = 'I') THEN(DO)
             CLRPFM     FILE(QTEMP/SADVAVG)
             MONMSG     MSGID(CPF0000) EXEC(DO)
             CRTDUPOBJ  OBJ(SADVAVG) FROMLIB(*LIBL) OBJTYPE(*FILE) +
                          TOLIB(QTEMP)
             ENDDO
             CALL       PGM(SAD61P) PARM(&FAXTYP &AVD &OPD &PRT1 +
                          &PRT2 &PRT3)
             ENDDO
/*******************************************************************/
/*      EKSPORT                                                    */
/*******************************************************************/
             IF         COND(&KODE = 'E') THEN(DO)
             CALL       PGM(SAD52P) PARM(&FAXTYP &AVD &OPD &PRT1 +
                          &PRT2 &PRT3)
             ENDDO
/*******************************************************************/
/*      SJEKK OM DATALISTE FINNES                                  */
/*******************************************************************/
             IF         COND(&KODE = 'E') THEN(DO)
             DSPLNK     OBJ(&BANE2) OUTPUT(*PRINT) OBJTYPE(*STMF) +
                          DETAIL(*NAME) DSPOPT(*USER)
             MONMSG     MSGID(CPFA0A9) EXEC(DO)
             CHGVAR     VAR(&STMF2) VALUE(' ')
             ENDDO
             ENDDO
             IF         COND(&KODE = 'I') THEN(DO)
             DSPLNK     OBJ(&BANE) OUTPUT(*PRINT) OBJTYPE(*STMF) +
                          DETAIL(*NAME) DSPOPT(*USER)
             MONMSG     MSGID(CPFA0A9) EXEC(DO)
             CHGVAR     VAR(&STMF) VALUE(' ')
             ENDDO
             ENDDO
             DLTSPLF    FILE(QSYSPRT) JOB(&QNBR/&QUSER/&QJOB) +
                          SPLNBR(*LAST)
             MONMSG     MSGID(CPF0000)
/*******************************************************************/
/*      VISE HOVED DOKUMENT                                        */
/*******************************************************************/
             CALL       PGM(PDF001D) PARM(&STMF &STMF2)
/*******************************************************************/
 SLUTT:      ENDPGM
