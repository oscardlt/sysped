********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 sh  PTF10 ikke kreditere slettede oppdrag
10XXX * 02 sh  PTF10 kan ikke kreditere kunder med epostmottatte fakturaer
10XXX * 03 sh  PTF10 må kunne kreditere ptf 02
      *----------------------------------------------------------------------------
10XXX * 04 sh  PTF11 få med parameter ikke samlefaktura
10XXX * 05 sh  PTF11 LEGG KREDIT AV FAKTURA PÅ NY I FADOCN
********************************************************************
      *
      *****************************************************************
      *** PROGRAM FOR Å KREDITERE FAKTURA/OPPDRAG - SLETTE FØR OVERF. *
      *****************************************************************
n02  FFIRM      IF   E           K DISK
n02  Farktxt    IF   E           K DISK
n02  FCUNDC03   IF   E           K DISK
     FAVSLUTF   IF   E           K DISK
     FAVSLUTL2  IF   E           K DISK
     F                                     RENAME(AVSLUTFR:AVSLUT2)
     FFAKT      UF A E           K DISK
     FFAK3      UF   E           K DISK
     F                                     RENAME(FAKTR:FAK3R)
     FXMLUFL2   UF   E           K DISK
     FHEADF     UF   E           K DISK
     FKOSBU     UF   E           K DISK
     FFREETXT1  UF A E           K DISK
     FKODTV     IF   E           K DISK
     FKODTSF    IF   E           K DISK
     FSYKR20FM  CF   E             WORKSTN
     D                 DS
     D  KRDS                   1     29
     D  KRFAST                 1     25
     D  WSSG                  27     29
     D                 DS
     D  DELTXT                 1     20
     D  DELTX1                 1      3
     D  YSG                    5      7
     D  TID                    9     20  0
     D                 DS
     D  UDATOÅ                 1      4  0
     D  UDATOM                 5      6  0
     D  UDATOD                 7      8  0
     D  UDATO                  1      8  0
s04  D*               UDS
N04  D SAVUDS         UDS
     D  UFIRM                  1      2
     D  UAVD                  15     18  0
     D  USG                   19     21
     D  UPRO                  22     29  0
     D  UDT                   30     35  0
     D  UHOD                  47     47
     D  UBBS                  48     48
     D  USMF                  63     63
     D  UGN                  981    995
     C*
N04  C     *DTAARA       DEFINE    *LDA          SAVUDS
N04  C                   IN        SAVUDS
     C                   EXSR      INIT
     C*
     C     START         TAG
     C                   EXFMT     BILDE01
     c                   setoff                                       71
     C                   MOVEA     '000000'      *IN(94)
     C                   SETOFF                                       30
     C   03              GOTO      SLUTT
     C*
     C* TESTS DATA ON PICTURE
     C*
     C     KODSFK        CHAIN     KODTSF                             99
     C   99              MOVE      'SPS0001'     MSGNR
     C   99              SETON                                        30
     C   99              GOTO      START
     C*
     C* TEST OF ORDER
     C*
     C     HEADFK        CHAIN     HEADF                              98
     C  N98              UPDATE    HEADFR
     C  N98HEST          IFEQ      'G'
     C                   SETON                                        98
     C                   END
     C   98              MOVE      'SPO0028'     MSGNR
     C   98              SETON                                        30
     C   98              GOTO      START
     C*
     C     HEADFK        CHAIN     AVSLUTF                            33
     C   33HEPRO         IFNE      *ZEROS
     C     HEPRO         CHAIN     AVSLUTL2                           33
     C                   END
     C     *IN33         IFEQ      '0'
     C                   MOVE      'SHU0579'     MSGNR
     C                   MOVE      '1'           *IN98
     C                   MOVE      '1'           *IN30
     C                   GOTO      START
     C                   ENDIF
     C* IF F4 THEN ASKED FOR DELETION (IND 04)
     C* TEST IF LINES
     C*
      *
      * tester om faktura finnes med status O T OG Z
      * ikke hvis F4 er testet
      *
     C                   MOVE      'O'           XXOPKO
      *
     c     *in04         ifeq      '0'
      *
     C     KEYFA3        CHAIN(N)  FAK3                               94
     C     *IN94         IFEQ      '1'
     C                   MOVE      'Z'           XXOPKO
     C     KEYFA3        CHAIN(N)  FAK3                               94
     C     *IN94         IFEQ      '1'
     C                   MOVE      'T'           XXOPKO
     C     KEYFA3        CHAIN(N)  FAK3                               94
     C                   end
     C                   end
     C   94              MOVE      'SPF0038'     MSGNR
     C  N94FASK          IFEQ      'X'
     C                   MOVE      '1'           *IN94
     C                   MOVE      'YBC0074'     MSGNR
     C                   END
     C*
     C* ALREADY CREDITED
     C*
     C  N94FAKDA         IFEQ      'C'
     C                   MOVE      'SPF0034'     MSGNR
     C                   SETON                                        94
     C                   END
      *
n01  C*
n01  C* slettet
n01  C*
n01  C  N94FAKDA         IFEQ      'D'
n01  C                   MOVE      'SHU1262'     MSGNR
n01  C                   SETON                                        94
n01  C                   END
      * epostmottaker
n03  c     faopko        ifne      'O'
n02  c     epokey        CHAIN     cundc03                            33
n02  C  N33              MOVE      'SHU1380'     MSGNR
n02  C  N33              SETON                                        94
n03  C                   end
      *
     C   94              SETON                                        30
     C                   end
      *
     C   04              SETON                                        94
     C*
     C* Fakturert men ikke oppdatert  status F/G
     C*
     C   94              DO
     C*
     C     KEYXX3        CHAIN(N)  FAK3                               93
     C*
     C   93YYOPKO        IFEQ      'F'
     C                   MOVE      'G'           YYOPKO            1
     C                   ELSE
     C                   MOVE      'F'           YYOPKO            1
     C                   END
     C*
     C   93KEYXX3        CHAIN(N)  FAK3                               93
     C  N93
     CANN04              MOVE      'SPF0032'     MSGNR
     C  N93
     CANN04              seton                                        71
     C   93
     CAN 04              MOVE      'SPF0033'     MSGNR
     C   93
     CAN 04              SETON                                        30
     C*
     C* samlefaktura kan ikke slettes
     C*
     C  N93FAKDA         IFEQ      'S'
     C                   MOVE      'SHU1091'     MSGNR
     C                   SETON                                        30
     c                   goto      start
     C                   END
     C*
     C* ikke kreditere 2 ganger
     C*
     C  N94FAKDA         IFEQ      'C'
     C                   MOVE      'SPF0034'     MSGNR
     C                   SETON                                        30
     c                   goto      start
     C                   END
n02  C*
n02  C* slettet
n02  C*
n02  C  N94FAKDA         IFEQ      'D'
n02  C                   MOVE      'SHU1262'     MSGNR
n02  C                   SETON                                        94
n02  C                   END
      * epostmottaker
n03  c     faopko        ifne      'O'
n02  c     epokey        CHAIN     cundc03                            33
n02  c     *in33         ifeq      '0'
n02  C                   MOVE      'SHU1380'     MSGNR
n02  C                   SETON                                        30
     c                   goto      start
n02  c                   end
n03  c                   end
     C*
     C* CALLS ROUTINE FOR DELETION
     C*
     C   04
     CANN93              DO
     C                   SETOFF                                       94
     C                   MOVE      YYOPKO        XXOPKO
     C     SLEKRE        IFEQ      'S'
     C                   EXSR      DELINV
     C                   MOVE      *ZEROS        WSKUNR
     C                   MOVE      *ZEROS        WSFANR
     C                   MOVE      *ZEROS        WSOPD
     C                   MOVE      *BLANKS       WSTXT
     C                   MOVE      *BLANKS       WSSKR
     C                   GOTO      START
     C                   END
     C                   END
     C*
     C   94              GOTO      START
     C*
     C                   END
     C*
     C     WSTXT         COMP      *BLANKS                                97
     C   97              MOVE      'SPT0027'     MSGNR
     C   97              SETON                                        30
     C   97              GOTO      START
      *
     C*
     C     START2        TAG
     C                   EXSR      KREDIT
     C                   MOVE      *ZEROS        WSKUNR
     C                   MOVE      *ZEROS        WSFANR
     C                   MOVE      *ZEROS        WSOPD
     C                   MOVE      *BLANKS       WSTXT
     C                   MOVE      *BLANKS       WSSKR
     C                   GOTO      START
     C*
     C     SLUTT         TAG
     C                   MOVE      WSSMF         USMF
     C                   SETON                                        LR
     C                   RETURN
     C*
     C***********************************************
     C     KREDIT        BEGSR
     C***********************************************
     C*
     C* UPDATES HEADINGFILE
     C*
     C     HEADFK        CHAIN     HEADF                              33
     C                   MOVE      'K'           HEST
     C*
     C                   Z-ADD     1             FRTLI
     C                   MOVE      HEAVD         FRTAVD
     C                   MOVE      HEOPD         FRTOPD
     C                   MOVE      UDATO         FRTDT
     C     ST1           TAG
     C     FRIKEY        CHAIN     FREETXT1                           33
     C  N33              ADD       1             FRTLI
     C  N33              GOTO      ST1
     C* UTFØRT AV SAKSBEHANDLER .......
     C                   MOVEL     KRDS          FRTTXT
     C                   MOVE      ' '           FRTKOD
     C                   WRITE     FREETXTR
     C* ÅRSAK    ....
     C                   ADD       1             FRTLI
     C                   MOVEL     WSTXT         FRTTXT
     C     WSSKR         IFEQ      'J'
     C                   MOVE      FASK          FRTKOD
     C                   END
     C                   WRITE     FREETXTR
     C     FASK          IFEQ      'K'
     C                   MOVE      WSFANR        HEFNGK
     C                   ELSE
     C     FASK          IFEQ      'S'
     C                   MOVE      WSFANR        HEFNGS
     C                   END
     C                   END
     C                   UPDATE    HEADFR
     C                   EXSR      LINJER
     c                   move      ' '           p1                1
     c                   move      '0'           p2                1
     c                   move      '0'           p3                1
     c                   move      '1'           p4                1
     c                   move      '0'           p5                1
     c                   move      'N'           p6                1
N04  C                   MOVE      WSSMF         USMF
N04  C                   OUT       SAVUDS
     C                   CALL      'SYFA20CLJ'
     C                   PARM                    p1
     C                   PARM                    p2
     C                   PARM                    p3
     C                   PARM                    p4
     C                   PARM                    p5
     C                   PARM                    p6
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     LINJER        BEGSR
     C***********************************************
     C*
     C* READS INVOICEFILE AND CREATES CREDITLINES
     C*
     C     KEYFA3        SETLL     FAK3
     C     STKRE         TAG
     C     KEYFA3        READE     FAK3                                   33
     C  N33              DO
     C                   MOVE      'C'           FAKDA
     C                   UPDATE    FAK3R
     C*
N05  C                   MOVEL     FAFAKT        FADOCN
N05  C                   MOVE      'K'           FADOCN
     C                   Z-SUB     FABELN        FABELN
     C                   Z-SUB     FABELV        FABELV
     C                   Z-SUB     FABELU        FABELU
     C                   MOVE      *ZEROS        FAFAKT
     C                   MOVE      ' '           FAOPKO
     C                   MOVE      *ZEROS        FAJOUR
     C                   MOVE      *ZEROS        FACC
     C                   MOVE      *ZEROS        FAÅR
     C                   MOVE      *ZEROS        FAMND
     C                   MOVE      *ZEROS        FAFRBN
     C                   MOVE      *ZEROS        FADATO
     C                   MOVE      *ZEROS        FADAFF
     C                   MOVE      'C'           FAKDA
     C                   CALL      'SYGE06'
     C                   PARM                    FAAVD
     C                   PARM                    FAOPD
     C                   PARM                    FALI
     C                   WRITE     FAKTR
     C                   GOTO      STKRE
     C                   END
     C*
     C
     C                   ENDSR
     C*
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C*--------------------------------------------------------------
     C* DEFINERER NØKKEL TIL FAKTURAREGISTER: FAK3
     C*--------------------------------------------------------------
     C     *LIKE         DEFINE    FAOPKO        XXOPKO
     C     KEYFA3        KLIST
     C                   KFLD                    XXOPKO
     C                   KFLD                    WSFANR
     C                   KFLD                    WSKUNR
     C                   KFLD                    UAVD
     C                   KFLD                    WSOPD
     C     KEYXX3        KLIST
     C                   KFLD                    YYOPKO
     C                   KFLD                    WSFANR
     C                   KFLD                    WSKUNR
     C                   KFLD                    UAVD
     C                   KFLD                    WSOPD
     C     HEADFK        KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    WSOPD
     C     FAKTK2        KLIST
     C                   KFLD                    FAAVD
     C                   KFLD                    FAOPD
     C                   KFLD                    FALI
     C     KODSFK        KLIST
     C                   KFLD                    YSFUNI
     C                   KFLD                    WSSG
     C     KODTVK        KLIST
     C                   KFLD                    YVUNI
     C                   KFLD                    UAVD
     C     FRIKEY        KLIST
     C                   KFLD                    FRTAVD
     C                   KFLD                    FRTOPD
     C                   KFLD                    FRTDT
     C                   KFLD                    FRTLI
     C                   MOVE      *ZEROS        FRTLI
N02  C     EPOKEY        KLIST
N02  C                   KFLD                    fifirm
N02  C                   KFLD                    mail30
N02  C                   KFLD                    fakunr
n02  C                   read      firm                                   33
n02  C     'fa'          chain     arktxt                             33
n02  C     *IN33         IFEQ      '0'
n02  C     arkjn         andeq     'J'
n02  C     ARKSND        andeq     'J'
n02  C                   MOVE      ARTXT         MAIL16           16
n02  C                   MOVEL     '*'           MAIL16
n02  c                   movel     MAIL16        mail30           30
N02  c                   end
N04  C                   MOVE      'X'           WSSMF
     C*
     C                   MOVE      'O'           XXOPKO
     C                   MOVE      'F'           YYOPKO            1
     C                   MOVE      'V'           YVUNI             1
     C                   MOVE      'SF'          YSFUNI            2
     C                   MOVE      'SK'          YSK               1
     C                   MOVE      '*D*'         DELTX1
     C                   MOVE      USG           YSG
     C                   MOVE      UDAY          UDATOD
     C                   MOVE      UMONTH        UDATOM
     C     UYEAR         IFGT      50
     C     UYEAR         ADD       1900          UDATOÅ
     C                   ELSE
     C     UYEAR         ADD       2000          UDATOÅ
     C                   END
     C*
     C     KODTVK        CHAIN     KODTV                              33
     C*
     C                   MOVE      'DIV1856'     XMSGNR            7            MELDING-ID
     C                   Z-ADD     3             XLEVEL            1 0          NIVÅ
     C                   Z-ADD     0             XRETUR            1 0          RETURKODE
     C                   Z-ADD     70            XMSGL             4 0          LENGDE PÅ MELDING
     C                   EXSR      GETTXT
     C                   MOVEL     XMSG          XTXT             70
     C* KREDITERT AV...... TEKST
     C                   MOVE      'JOV0015'     XMSGNR            7            MELDING-ID
     C                   Z-ADD     3             XLEVEL            1 0          NIVÅ
     C                   Z-ADD     0             XRETUR            1 0          RETURKODE
     C                   Z-ADD     25            XMSGL             4 0          LENGDE PÅ MELDING
     C                   EXSR      GETTXT
     C                   MOVEL     XMSG          KRFAST
     C*
     C                   TIME                    TID
     C                   ENDSR
     C*
     C***********************************************
     C     GETTXT        BEGSR
     C***********************************************
     C                   CALL      'SUBR23R3'
     C                   PARM                    XMSGNR
     C                   PARM                    XMSG            120
     C                   PARM                    XLEVEL
     C                   PARM                    XRETUR
     C                   PARM                    XMSGL
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     DELINV        BEGSR
     C***********************************************
     C*
     C* DELETES LINES ON INVOICE
     C*
     C                   SETOFF                                       33
     C     KEYXX3        SETLL     FAK3
     C     STDEL         TAG
     C     KEYXX3        READE(N)  FAK3                                   33
     C  N33              DO
     C     FAKTK2        CHAIN     FAKT                               34
     C  N34              MOVEL     DELTXT        FAVT
     C  N34              MOVE      'D'           FAKDA
     C  N34FAJOUR        IFNE      *ZEROS
     C     FAJOUR        CHAIN     KOSBU                              35
     C     *IN35         IFEQ      *OFF
     C                   MOVE      *ZEROS        FAJOUR
     C                   DELETE    KOSBUR
     C                   END
     C                   END
     C  N34              UPDATE    FAKTR
     C  N34              EXSR      OPDGRL
     C                   GOTO      STDEL
     C                   END
     C* HVIS SUM ANDRE=0 ÅPNE STATUS
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     OPDGRL        BEGSR
     C***********************************************
      * OPPDATER FAKTURANR PÅ GRUNNLAGFIL
     C                   MOVE      FAAVD         ULAVD             4
     C                   MOVE      FAOPD         ULOPD             7
     C                   MOVE      *ZEROS        ULLI              5
     C                   MOVE      *ZEROS        ULFDT             8
     C                   MOVE      *ZEROS        ULFN              7
     C                   MOVE      'N'           ULKOPI            1
     C                   MOVE      'K'           ULOPT             1
     C                   CALL      'LL054C'
     C                   PARM                    ULAVD
     C                   PARM                    ULOPD
     C                   PARM                    ULLI
     C                   PARM                    ULFDT
     C                   PARM                    ULFN
     C                   PARM                    ULKOPI
     C                   PARM                    ULOPT
      *
     C     FAFAKT        CHAIN     XMLUFL2                            34
     C  N34              MOVE      'S'           XFST
     C  N34              UPDATE    XMLUFFR
     C                   MOVE      *OFF          *IN34
     C
      *
     C                   ENDSR
      *
