     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPPNLGRP(HLPSCR FAX025APNL)
     A                                      HLPTITLE('SENDING AV TOLLDEKLARASJO-
     A                                      N PÅ FAX')
     A                                      HELP
     A                                      ALTHELP
      *
     A          R BILDE
     A                                      CF03(03 'Avslutt')
     A                                      CF04(04 'Avslutt')
     A                                      CF10(10 'send ønsket fax')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1  3'SYSPED/FAX025A'
     A                                      COLOR(BLU)
     A                                  1 22MSGCON(040 SHU0618 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 64DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  2 64TIME
     A                                      COLOR(BLU)
     A                                  6  3MSGCON(012 DIV0050 *LIBL/SYMSGF)
     A            UAVD           4Y 0B  6 16EDTCDE(4)
     A                                      COLOR(YLW)
     A                                  7  3MSGCON(012 DIV0148 *LIBL/SYMSGF)
     A            WSFN1          7Y 0B  7 16DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A  66                              7 26'EKSPORTDEKLARASJON'
     A                                      COLOR(WHT)
     A                                 12  3MSGCON(025 SHU0166 *LIBL/SYMSGF)
     A            KUNDNR         8Y 0B 12 30EDTCDE(4)
     A                                      DSPATR(UL)
     A            KNAVN         30A  B 12 40
     A  25                                  DSPATR(PR)
     A                                 13  3MSGCON(025 SHU0167 *LIBL/SYMSGF)
     A            FAXNR         15A  B 13 30
     A  71                                  DSPATR(PC)
     A                                      DSPATR(UL)
     A                                 13 46MSGCON(012 SHU0261 *LIBL/SYMSGF)
     A            WSLAND         2A  B 13 59
     A                                 13 62'='
     A            FAXRET         6A  O 13 64
     A                                 14  3MSGCON(006 SHU0536 *LIBL/SYMSGF)
     A            EPOST         70A  B 14 11CHECK(LC)
     A                                 15  3MSGCON(025 SHU0343 *LIBL/SYMSGF)
     A            WSCVRP         1A  B 15 30
     A                                 17  3MSGCON(025 SHU0173 *LIBL/SYMSGF)
     A            WSTIME         6Y 0B 17 30DSPATR(UL)
     A                                      EDTCDE(4)
     A  97                                  DSPATR(RI)
     A  97                                  DSPATR(PC)
     A                                 18  3MSGCON(025 SHU0174 *LIBL/SYMSGF)
     A            WSDATE         6Y 0B 18 30DSPATR(UL)
     A                                      EDTCDE(4)
     A  96                                  DSPATR(RI)
     A  96                                  DSPATR(PC)
     A                                 23  3MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 23MSGCON(018 DIV0032 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 43MSGCON(018 SHU0168 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        70A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A  30        YFN            7Y 0O 24 73COLOR(RED)
