********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 YBC PTF10 SØK PÅ BOOKINGREF
xxxxxx*xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
11XXX * 02 YBC PTF11 VIS MODUS FOR IMPORT ELLER EKSPORT
********************************************************************
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      ALTHELP
     A                                      HELP
     A          R BILDE
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CA03(03 'Avslutt')
     A                                      CF04(04 'SØKING')
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/SYSJ81R'
     A                                      COLOR(BLU)
     A                                  1 61DATE
     A                                      EDTCDE(Y)
     A                                  1 70TIME
     A                                  1 20MSGCON(40 YBC0730 SYMSGF)
     A                                      DSPATR(HI)
     A                                  3  2MSGCON(24 DIV3439 SYMSGF)
     A            WSAVD          4Y 0B  3 28DSPATR(UL)
     A  90                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                  3 45MSGCON(24 DIV0457 SYMSGF)
     A                                  5  2MSGCON(30 DIV3440 SYMSGF)
     A                                  6  2MSGCON(10 DIV3441 SYMSGF)
     A            WSDT           6Y 0B  6 28DSPATR(UL)
     A  89                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                  7  2MSGCON(24 DIV3442 SYMSGF)
     A            WSTYPE         2Y 0B  7 28DSPATR(UL)
     A  88                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                  9  2MSGCON(24 DIV3443 SYMSGF)
     A                                  9 28MSGCON(4 DIV3444 SYMSGF)
     A            WSÅR           2Y 0B  9 33DSPATR(UL)
     A                                      EDTCDE(4)
     A                                  9 37MSGCON(4 DIV3445 SYMSGF)
     A            WSUKE          2Y 0B  9 42DSPATR(UL)
     A  87                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                  9 48MSGCON(10 DIV3446 SYMSGF)
     A            WSUKET         2Y 0B  9 59DSPATR(UL)
     A  86                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                 10  2MSGCON(24 DIV3447 SYMSGF)
     A            WSKNR          8Y 0B 10 28DSPATR(UL)
     A  85                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                 11  2MSGCON(24 DIV3448 SYMSGF)
     A            WSTR           4A  B 11 28DSPATR(UL)
     A  84                                  DSPATR(RI)
     A                                 12  2MSGCON(24 DIV3449 SYMSGF)
     A            WSKNA          8Y 0B 12 28DSPATR(UL)
     A  83                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                 13  2MSGCON(24 DIV3450 SYMSGF)
     A            WSLB           5A  B 13 28DSPATR(UL)
     A  82                                  DSPATR(RI)
     A                                 14  2MSGCON(24 DIV3451 SYMSGF)
     A            WSLSH          5A  B 14 28DSPATR(UL)
     A  81                                  DSPATR(RI)
     A                                 15  2MSGCON(24 DIV3452 SYMSGF)
     A            WSKNS          8Y 0B 15 28DSPATR(UL)
     A  80                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                 16  2MSGCON(24 DIV3453 SYMSGF)
     A            WSOB           5A  B 16 28DSPATR(UL)
     A  79                                  DSPATR(RI)
     A                                 17  2MSGCON(24 DIV3454 SYMSGF)
     A            WSOPD          7Y 0B 17 28DSPATR(UL)
     A  78                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                 18  2MSGCON(24 DIV4164 SYMSGF)
     A            WSREFN        14   B 18 28
     A  77                                  DSPATR(RI)
     A                                      DSPATR(UL)
     A                                 19  2MSGCON(24 YBC0007 SYMSGF)
     A            WSLOH          5A  B 19 28DSPATR(UL)
     A  76                                  DSPATR(RI)
     A                                 20  2MSGCON(24 YBC0725 SYMSGF)
     A            WSSG           3A  B 20 28DSPATR(UL)
     A  75                                  DSPATR(RI)
     A                                 21  2MSGCON(24 YBC0726 SYMSGF)
     A            WSMN          17A  B 21 28DSPATR(UL)
N01  A  74                                  DSPATR(RI)
N01  A                                 22  2MSGCON(24 YBC1961 SYMSGF)
N01  A            WSBKNL        14A  B 22 28DSPATR(UL)
N01  A  73                                  DSPATR(RI)
     A                                 23  2MSGCON(18 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(18 DIV0032 SYMSGF)
     A                                      COLOR(BLU)
     A N30                             24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R SUBFIL                    SFL
     A            WSNR           2Y 0B  7  2EDTCDE(4)
     A            HEST           1A  H
     A            BOAVD          4  0O  7  5EDTCDE(4)
     A            BOOPD          7  0O  7 10EDTCDE(4)
     A  49                                  COLOR(WHT)
     A            XDT1           6  0O  7 18EDTCDE(4)
     A            HESG           3   H
     A            BOSG           3   O  7 25
     A  52                                  COLOR(WHT)
     A            BOUKE          2  0O  7 29EDTCDE(4)
     A  41                                  COLOR(WHT)
     A            BOKNR          8  0O  7 32EDTCDE(4)
     A  42                                  COLOR(WHT)
     A            BOTR           4   O  7 41
     A  43                                  COLOR(WHT)
     A            XNAA          20   O  7 46
     A  44                                  COLOR(WHT)
     A            BOLB           5   O  7 67
     A  45                                  COLOR(WHT)
     A            BOOB           5   O  7 73
     A  48                                  COLOR(WHT)
     A            BOST           1   O  7 79
     A            BOREFN        14   O  8  4DSPATR(UL)
S01  A* 50                                  COLOR(WHT)
N01  A  50
N01  AO 54                                  COLOR(WHT)
     A            BOPRO          8  0O  8 19DSPATR(UL)
     A                                      EDTCDE(4)
     A            XMN           17   O  8 28DSPATR(UL)
     A  53                                  COLOR(WHT)
     A            XNAS          20   O  8 46DSPATR(UL)
     A  47                                  COLOR(WHT)
     A            BOLSH          5   O  8 67DSPATR(UL)
     A  46                                  COLOR(WHT)
     A            BOLOH          5   O  8 73DSPATR(UL)
     A  51                                  COLOR(WHT)
N02  A            BOUR           1A  O  8 79DSPATR(UL)
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      CA03(03 'AVSLUTT')
     A                                      CF04(04 'SØK KODE')
     A                                      CA05(05 'Frisk')
     A                                      CA06(06 'LAGE EN NY')
     A                                      CA08(08 'SØK')
     A                                      CA12(12 'FORRIGE')
     A                                      CA13(13 'TIL OPPDRAG')
     A                                      CA14(14 'TIL BL')
     A                                      CA23(23 'MER OPSJONER')
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A                                      SFLSIZ(0008)
     A                                      SFLPAG(0007)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            ZRECNO         4S 0O  1 77SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYSPED/SYSJ81R'
     A                                      COLOR(BLU)
     A                                  1 61DATE
     A                                      EDTCDE(Y)
     A                                  1 70TIME
     A                                  1 20MSGCON(40 YBC0730 SYMSGF)
     A                                      COLOR(WHT)
     A                                  2  2MSGCON(010 YBC0065 *LIBL/SYMSGF)
     A            WSAVDA         4Y 0B  2 13EDTCDE(4)
     A  96                                  DSPATR(HI)
     A            WSSGA          3A  B  2 19DSPATR(UL)
     A  95                                  DSPATR(HI)
     A  41                              2 24MSGCON(24 DIV3443 SYMSGF)
     A  41                              2 49MSGCON(4 DIV3444 SYMSGF)
     A  41        WSÅR           2Y 0O  2 54DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(1)
     A  41                              2 58MSGCON(4 DIV3445 SYMSGF)
     A  41        WSUKE          2Y 0O  2 63DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(1)
     A  41                              2 66MSGCON(10 DIV3446 SYMSGF)
     A  41        WSUKET         2Y 0O  2 77DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(1)
     A  42                              2 24MSGCON(24 DIV3447 SYMSGF)
     A  42
     AO 44
     AO 47        XNARAS        30   O  2 49DSPATR(UL)
     A                                      COLOR(WHT)
     A  43                              2 24MSGCON(24 DIV3448 SYMSGF)
     A  43        WSTR           4   O  2 49DSPATR(UL)
     A                                      COLOR(WHT)
     A  44                              2 24MSGCON(24 DIV3449 SYMSGF)
     A  45                              2 24MSGCON(24 DIV3450 SYMSGF)
     A  45
     AO 48        XNAB          30   O  2 49DSPATR(UL)
     A                                      COLOR(WHT)
     A  46                              2 24MSGCON(24 DIV3451 SYMSGF)
     A  51                              2 24MSGCON(24 YBC0007 SYMSGF)
     A  46
     AO 51        XNASTD        30   O  2 49DSPATR(UL)
     A                                      COLOR(BLU)
     A  47                              2 24MSGCON(24 DIV3452 SYMSGF)
     A  48                              2 24MSGCON(24 DIV3453 SYMSGF)
     A  49                              2 24MSGCON(24 DIV3454 SYMSGF)
     A  49        WSOPD          7Y 0O  2 49DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A  52                              2 24MSGCON(24 YBC0725 SYMSGF)
     A  52        WSSG           3A  O  2 49DSPATR(UL)
     A  53                              2 24MSGCON(24 YBC0726 SYMSGF)
     A  53        WSMN          17A  O  2 49DSPATR(UL)
     A                                      COLOR(WHT)
     A            XOPTXT        78A  O  3  2MSGID(&XOPNR SYMSGF)
     A                                      COLOR(BLU)
     A            XOPNR          7A  P
     A                                  4  2MSGCON(78 YBC0727 SYMSGF)
     A                                      COLOR(WHT)
     A                                  5  2MSGCON(78 YBC0728 SYMSGF)
     A                                      COLOR(WHT)
     A            W2NR           2Y 0B  6  2EDTCDE(4)
     A                                      DSPATR(UL)
     A            W2AVD          4  0B  6  5EDTCDE(4)
     A                                      DSPATR(UL)
     A            W2OPD          7  0B  6 10EDTCDE(4)
     A                                      DSPATR(UL)
     A            W2DT           6  0B  6 18EDTCDE(4)
     A                                      DSPATR(UL)
     A            W2SG           3   B  6 25DSPATR(UL)
     A            W2UKE          2  0B  6 29EDTCDE(4)
     A                                      DSPATR(UL)
     A            W2KNR          8  0B  6 32EDTCDE(4)
     A                                      DSPATR(UL)
     A            W2TR           4   B  6 41DSPATR(UL)
     A            W2KNA          8  0B  6 46EDTCDE(4)
     A                                      DSPATR(UL)
     A            W2LB           5   B  6 67DSPATR(UL)
     A            W2OB           5   B  6 73DSPATR(UL)
      *
     A          R BUNN
     A                                      SLNO(22)
     A                                      CLRL(03)
     A                                      OVERLAY
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2MSGCON(78 YBC0731 SYMSGF)
     A                                      COLOR(BLU)
     A                                  2  2MSGCON(78 YBC0721 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  3  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                              3 40'(C) COPYRIGHT  SYSTEMA A/S  1999  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R NYVARE
     A                                      CA03(03 'AVSLUTT')
     A                                      CF04(04 'SØK KODE')
     A                                      CA06(06 'LAGE EN NY')
     A                                      CA08(08 'SØK')
     A                                      CA12(12 'FORRIGE')
     A                                      CA13(13 'TIL OPPDRAG')
     A                                      CA14(14 'TIL BL')
     A                                      CA23(23 'MER OPSJONER')
     A                                  1  2'SYSPED/SYSJ81R'
     A                                      COLOR(BLU)
     A                                  1 61DATE
     A                                      EDTCDE(Y)
     A                                  1 70TIME
     A                                  1 20MSGCON(40 YBC0730 SYMSGF)
     A                                      COLOR(WHT)
     A                                  2  2MSGCON(010 YBC0065 *LIBL/SYMSGF)
     A            WSAVDA         4Y 0B  2 13EDTCDE(4)
     A  96                                  DSPATR(HI)
     A            WSSGA          3A  B  2 19DSPATR(UL)
     A  95                                  DSPATR(HI)
     A  41                              2 24MSGCON(24 DIV3443 SYMSGF)
     A  41                              2 49MSGCON(4 DIV3444 SYMSGF)
     A  41        WSÅR           2Y 0O  2 54DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(1)
     A  41                              2 58MSGCON(4 DIV3445 SYMSGF)
     A  41        WSUKE          2Y 0O  2 63DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(1)
     A  41                              2 66MSGCON(10 DIV3446 SYMSGF)
     A  41        WSUKET         2Y 0O  2 77DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(1)
     A  42                              2 24MSGCON(24 DIV3447 SYMSGF)
     A  42
     AO 44
     AO 47        XNARAS        30   O  2 49DSPATR(UL)
     A                                      COLOR(WHT)
     A  43                              2 24MSGCON(24 DIV3448 SYMSGF)
     A  43        WSTR           4   O  2 49DSPATR(UL)
     A                                      COLOR(WHT)
     A  44                              2 24MSGCON(24 DIV3449 SYMSGF)
     A  45                              2 24MSGCON(24 DIV3450 SYMSGF)
     A  45
     AO 48        XNAB          30   O  2 49DSPATR(UL)
     A                                      COLOR(WHT)
     A  46                              2 24MSGCON(24 DIV3451 SYMSGF)
     A  51                              2 24MSGCON(24 YBC0007 SYMSGF)
     A  46
     AO 51        XNASTD        30   O  2 49DSPATR(UL)
     A                                      COLOR(BLU)
     A  47                              2 24MSGCON(24 DIV3452 SYMSGF)
     A  48                              2 24MSGCON(24 DIV3453 SYMSGF)
     A  49                              2 24MSGCON(24 DIV3454 SYMSGF)
     A  49        WSOPD          7Y 0O  2 49DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A  52                              2 24MSGCON(24 YBC0725 SYMSGF)
     A  52        WSSG           3A  O  2 49DSPATR(UL)
     A  53                              2 24MSGCON(24 YBC0726 SYMSGF)
     A  53        WSMN          17A  O  2 49DSPATR(UL)
     A                                      COLOR(WHT)
     A            XOPTXT        78A  O  3  2MSGID(&XOPNR SYMSGF)
     A                                      COLOR(BLU)
     A            XOPNR          7A  P
     A                                  4  2MSGCON(78 YBC0727 SYMSGF)
     A                                      COLOR(WHT)
     A                                  5  2MSGCON(78 YBC0728 SYMSGF)
     A                                      COLOR(WHT)
     A            W2NR           2Y 0B  6  2EDTCDE(4)
     A                                      DSPATR(UL)
     A            W2AVD          4  0B  6  5EDTCDE(4)
     A                                      DSPATR(UL)
     A            W2OPD          7  0B  6 10EDTCDE(4)
     A                                      DSPATR(UL)
     A            W2DT           6  0B  6 18EDTCDE(4)
     A                                      DSPATR(UL)
     A            W2SG           3   B  6 25DSPATR(UL)
     A            W2UKE          2  0B  6 29EDTCDE(4)
     A                                      DSPATR(UL)
     A            W2KNR          8  0B  6 32EDTCDE(4)
     A                                      DSPATR(UL)
     A            W2TR           4   B  6 41DSPATR(UL)
     A            W2KNA          8  0B  6 46EDTCDE(4)
     A                                      DSPATR(UL)
     A            W2LB           5   B  6 67DSPATR(UL)
     A            W2OB           5   B  6 73DSPATR(UL)
     A                                 22  2MSGCON(78 YBC0731 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23  2MSGCON(78 YBC0721 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                             24 40'(C) COPYRIGHT  SYSTEMA A/S  1999  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
