     A*%%TS  SD  20150427  134906  SVEIN       REL-V7R1M0  5770-WDS
     A*%%EC
     A                                      DSPSIZ(27 132 *DS4)
     A                                      PRINT
     A                                      HLPDOC('SYFA5201' 'SYFA52' 'SY400LO-
     A                                      /MAINT')
     A                                      HELP
     A                                      ALTHELP
     A*
     A          R SUBFIL                    SFL
     A*%%TS  SD  20150427  094909  SVEIN       REL-V7R1M0  5770-WDS
     A            WSNR           1A  B  6  5VALUES(' ' '1' '2' '4')
     A            ENKODE    R        O  6  7REFFLD(ENHETFR/ENKODE *LIBL/ENHETF)
     A            ENTEXT    R        O  6 15REFFLD(ENHETFR/ENTEXT *LIBL/ENHETF)
     A            ENLEN     R        O  6 46REFFLD(ENHETFR/ENLEN *LIBL/ENHETF)
     A            ENBRD     R        O  6 51REFFLD(ENHETFR/ENBRD *LIBL/ENHETF)
     A            ENHOY     R        O  6 56REFFLD(ENHETFR/ENHOY *LIBL/ENHETF)
     A            ENLM2     R        O  6 61REFFLD(ENHETFR/ENLM2 *LIBL/ENHETF)
     A                                      EDTCDE(4)
     A            ENLM      R        O  6 67REFFLD(ENHETFR/ENLM *LIBL/ENHETF)
     A                                      EDTCDE(4)
     A*
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      OVERLAY
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A                                      CF03(03 'EXIT')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                      CF12(12 'TILBAKE')
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/SYTR27'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(040 SHU1564 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1120DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  2  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  2120TIME
     A                                      COLOR(BLU)
     A N27                              3  2MSGCON(030 DIV0017 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  27                              3  2MSGCON(030 SHU1567 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A            ZRECNO         4S 0O  3 75SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  4  4MSGCON(128 SHU1565 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            W2KODE         7A  I  5  7
     A            W2TEXT        30A  I  5 15
      *
     A          R BUNN
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A                                  3  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A       N27                        3 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A       N27                        3 42MSGCON(018 DIV0022 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 62MSGCON(018 DIV0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 82MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  98        MSG205        78A  O  4  2MSGID(DIV 0012 SYMSGF)
     A                                      COLOR(WHT)
     A N98                              4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A*
     A          R VARELI
     A*%%TS  SD  20150427  100759  SVEIN       REL-V7R1M0  5770-WDS
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A            WSKODE         7A  B  2  6
     A  40                                  DSPATR(HI)
     A  40                                  DSPATR(PR)
     A  90                                  DSPATR(RI)
     A                                  3  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  4  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                              4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A                                  1  3MSGCON(128 SHU1565 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            WSTEXT    R        B  2 14REFFLD(ENHETFR/ENTEXT *LIBL/ENHETF)
     A            WSLEN     R        B  2 45REFFLD(ENHETFR/ENLEN *LIBL/ENHETF)
     A                                      EDTCDE(Z)
     A            WSBRD     R        B  2 50REFFLD(ENHETFR/ENBRD *LIBL/ENHETF)
     A                                      EDTCDE(Z)
     A            WSHOY     R        B  2 55REFFLD(ENHETFR/ENHOY *LIBL/ENHETF)
     A                                      EDTCDE(Z)
     A            WSLM      R        B  2 60REFFLD(ENHETFR/ENLM *LIBL/ENHETF)
     A                                      EDTCDE(4)
     A            WSLM2     R        B  2 66REFFLD(ENHETFR/ENLM2 *LIBL/ENHETF)
     A                                      EDTCDE(4)
     A*
     A          R VAREL2
     A*%%TS  SD  20150427  134906  SVEIN       REL-V7R1M0  5770-WDS
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A                                  2  2MSGCON(016 SHU1566 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            W2KODE         7A  I  2 20DSPATR(UL)
     A                                      DSPATR(PC)
     A                                  2 29MSGCON(011 SHU1568 *LIBL/SYMSGF)
     A            W2TEXT        30   I  2 42
     A                                  3  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 40'(C) COPYRIGHT  SYSTEMA A/S  2015  '
     A                                      COLOR(WHT)
     A*
     A          R VAREL3
     A*%%TS  SD  20150427  100759  SVEIN       REL-V7R1M0  5770-WDS
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A                                  1  3MSGCON(128 SHU1565 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  3  2MSGCON(024 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A            ENKODE    R        O  2  6REFFLD(ENHETFR/ENKODE *LIBL/ENHETF)
     A            ENTEXT    R        O  2 14REFFLD(ENHETFR/ENTEXT *LIBL/ENHETF)
     A            ENLEN     R        O  2 45REFFLD(ENHETFR/ENLEN *LIBL/ENHETF)
     A            ENBRD     R        O  2 50REFFLD(ENHETFR/ENBRD *LIBL/ENHETF)
     A            ENHOY     R        O  2 55REFFLD(ENHETFR/ENHOY *LIBL/ENHETF)
     A            ENLM2     R        O  2 60REFFLD(ENHETFR/ENLM2 *LIBL/ENHETF)
     A                                      EDTCDE(4)
     A            ENLM      R        O  2 66REFFLD(ENHETFR/ENLM *LIBL/ENHETF)
     A                                      EDTCDE(4)
      *
     A          R NYWRK
     A                                      CF03(03 'EXIT')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                      CF12(12 'TILBAKE')
     A                                  1  2'SYSPED/SYTR27'
     A                                      COLOR(BLU)
     A                                  1120DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  2120TIME
     A                                      COLOR(BLU)
     A                                  3  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(030 DIV0017 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(040 SHU1564 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  5  4MSGCON(128 SHU1565 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A       N27                       23 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A       N27                       23 42MSGCON(018 DIV0022 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 62MSGCON(018 DIV0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 82MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
