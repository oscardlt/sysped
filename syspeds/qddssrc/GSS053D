     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
      *
     A          R GSS053DA                  SFL
     A            OLBOX          3Y 0O  5  2EDTCDE(4)
     A            ORONO          7Y 0O  5  6EDTCDE(4)
     A            BXVKT          7Y 1O  5 14EDTCDE(4)
     A  28                                  DSPATR(ND)
     A            ORWT           7Y 1O  5 23EDTCDE(4)
     A  29                                  DSPATR(ND)
     A            ORWTNY         7Y 1B  5 32EDTCDE(4)
     A  29                                  DSPATR(ND)
     A  29                                  DSPATR(PR)
     A            G53DIFF        7Y 1O  5 45EDTCDE(L)
     A  29                                  DSPATR(ND)
     A            G53OK          1A  H
      *
     A          R GSS053DB                  SFLCTL(GSS053DA)
     A                                      SFLSIZ(0016)
     A                                      SFLPAG(0015)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                  1  2'SHIPSYS/GSS053D'
     A                                      COLOR(BLU)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                  1 70TIME
     A                                  1 21'Juster vekt'
     A                                      DSPATR(HI)
     A            ZRECNO         4S 0O  2 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  2  2'Trip No.'
     A            UTNO          11Y 0O  2 12EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  2 26'Tot. b-weight:'
     A            XTOTB          7Y 1O  2 41DSPATR(UL)
     A                                      EDTCDE(4)
     A                                  2 51'Tot. o-weight:'
     A            XTOTO          7Y 1O  2 66DSPATR(UL)
     A                                      EDTCDE(4)
     A                                  4  2'Box OrderNo Box WT   Order WT New -
     A                                      order WT Diff(box-ordre)'
     A                                      COLOR(WHT)
      *
     A          R GSS053DC
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF10(10 'ACCEPT DIFF')
     A                                      CF12(12 'Cancel')
     A                                      SLNO(23)
     A                                      CLRL(02)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'F10=Accept diff.   F12=Cancel'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  2  2MSGID(&MSGNR GSSMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
