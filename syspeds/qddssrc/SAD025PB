     A*****************************************************************
     A*           LASTELISTE                                          *
     A* ANTALL LINJE=72,  ANTALL TEGN PR. LINJE=120, OVERFLOW=72      *
     A*****************************************************************
     A          R HEADER
     A            KFMT1         75A     1  1
     A                                  6 85'Lasteliste nr.'
     A            XSIDE          3  0   6101EDTCDE(4)
     A                                 13 20'Samlast cont.'
     A            WSCNR         17A    13 35
     A                                 15 20'Oppdragsnr.'
     A  71        XOPDF          7  0  15 35EDTCDE(4)
     A  71                             15 43'--'
     A  71        XOPDT          7  0  15 46EDTCDE(4)
     A  72        XOPDF2         7  0  16 35EDTCDE(4)
     A  72                             16 43'--'
     A  72        XOPDT2         7  0  16 46EDTCDE(4)
     A  73        XOPDF3         7  0  17 35EDTCDE(4)
     A  73                             17 43'--'
     A  73        XOPDT3         7  0  17 46EDTCDE(4)
     A  74        XOPDF4         7  0  18 35EDTCDE(4)
     A  74                             18 43'--'
     A  74        XOPDT4         7  0  18 46EDTCDE(4)
     A  75        XOPDF5         7  0  19 35EDTCDE(4)
     A  75                             19 43'--'
     A  75        XOPDT5         7  0  19 46EDTCDE(4)
     A                                 25  1' '
      *
     A          R DETALJ1                   SPACEB(1)
     A            XLI            4  0      5EDTCDE(4)
     A            BVMN          17A       16
     A            XNT            7  0     38EDTCDE(4)
     A            BVVS          25A       46
     A            ST2LK          2A       83
     A            XVKT           9  0     90EDTCDE(4)
     A                                   100'Kg'
      *
     A          R DETALJ2                   SPACEB(1)
     A            BVMN          17A       16
     A            XTXT1          5A       40
     A            XTXT2         30A       46
     A            XM3            9  3     89EDTCDE(4)
     A                                   100'M3'
      *
     A          R DETALJ3                   SPACEB(1)
     A            BVMN          17A       16
     A            XTXT1          5A       40
     A            XTXT2         30A       46
      *
     A          R DETALJ4                   SPACEB(1)
     A                                    40'MOTT:'
     A            HENAK         30A       46
      *
     A          R TOMLIN                    SPACEB(1)
     A                                     1' '
      *
     A          R TOTLIN
     A                                 66  8'-----------------------------------
     A                                      ------------------------------------
     A                                      ------------------------------------
     A                                      -----'
     A                                 67 12'Tot:'
     A            XLNR           4  0  67 18EDTCDE(4)
     A                                 67 23'Pos.'
     A                                 67 39'Ant.'
     A            XTOTNT         7  0  67 47EDTCDE(4)
     A            XTOTVK        12  0  67 76EDTCDE(4)
     A                                 67 89'Kg'
     A            XTOTM3        10  3  67 96EDTCDE(4)
     A                                 67109'M3'
      *
     A          R BUNN
     A            KFMT2         75A    70  1
     A            KSINAV        16A    71 90
