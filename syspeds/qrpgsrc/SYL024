      *********************************************************************
      * ARBEID MED PLUKKLISTE (LINJE)
      * PROGRAM BEHANDLER INPUT FRA SYL024 OG VISER UTVALGT HEADLING
      *
CRTPG+*  CRTPGM SYSPED/SYL024 MODULE(*LIBL/SYL024 *LIBL/SYL023B
CRTPGM*        *LIBL/CHECKUSR *LIBL/INCGI) ACTGRP(CGI) AUT(*USE)
      *
      *
      *********************************************************************
      * MAIN PROGRAM FLOW
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
      * Boats for sale data base file (LF on BOATSALE by boat serial number)
     FLLAGJL01  IF A E           K DISK    USROPN
     FLLMRF     IF   E           K DISK    USROPN
     FLLTELL    UF   E             DISK    USROPN
     FLLVARE01  IF   E           K DISK    USROPN
     FLLGRPK05  if   e           k disk    USROPN
     FLLLSDF    IF   E           K DISK    USROPN
     FBRIDF     UF   E           K DISK    USROPN
     FBRPARF    IF   E           k disk    usropn
      *--------------------------------------------------------------------
      * Prototype definitions and standard system API error structure
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      * Variables common to CGI programs
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
      * VARIABLES SPECIFIC TO THIS PROGRAM
      *--------------------------------------------------------------------
      * Variables received from the input string
     D lng             s              2
     D UsrSpcName      s             10
     D WSUSER          s             10
      * Variables used to load the external html source member
     D Lib             s             10
     D Fn              s             10
     D Mbr             s             10
      * Boats serial numbers received in the input string
     D ItemCount       s             10i 0
     D SernoC          s            107a
     D i               s             10i 0
     D PGM             C                   CONST('SYSPED/CHECKUSR')
     D PGM2            C                   CONST('*LIBL/SYL023B')
     D FEIL01          C                   CONST('Oppgitt artikkelnr finne-
     D                                      ikke.')
     D FEILEN01        C                   CONST('Artickel No. is not valid.')
     D FEIL02          C                   CONST('Antall stykk må oppgis.')
     D FEILEN02        C                   CONST('Pieces are not given.')
     D FEIL03          C                   CONST('Lokasjon må oppgis.')
     D FEILEN03        C                   CONST('Location can not be blanks.')
     D FEIL04          C                   CONST('Oppgitt artikkelnr er -
     D                                     inaktiv.')
     D FEILEN04        C                   CONST('Artickel is inactive.')
     D FEIL05          C                   CONST('Lagersted må velges.')
     D FEILEN05        C                   CONST('Place has to -
     D                                     be selected.')
      *
     D Spaces          s             12a   inz('&nbsp;&nbsp;')
     D FEILMELDING     S             80
     D MRKJED          S              8  0
     D MRKNV           S             30
     D MRLNR           S             17
     D MRLNA           S             30
     D WSMRNR          S              5
     D WSMN            S              7
     D BWSMRNR         S              5  0
     D BWSMN           S              7  0
     D WSANR           S             15
     D WSOANT          S              7
     D BWSOANT         S              7  0
     D WSLOK1          S              5
     D WSLOK2          S             20
     D WSLLT           S              3
     D                 DS
     D VALOK                   1     25
     D VALOK1                  1      5
     D VALOK2                  5     25
     D                 DS
     D XXREF                   1    107
     D XXMN                    1      7
     D XXMRNR                  8     12
     D XXKJED                 13     20
     D XXKNV                  21     50
     D XXLNR                  51     67
     D XXLNA                  68     97
     D XXUSER                 98    107
      *
      *--------------------------------------------------------------------
      * Prolog common to CGI programs
      * -Receive input from the remote browser
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prolog3
      *=====================================================================
      * MAIN PROCESSING LINE
      *=====================================================================
      * Get program start time for calculating execution time
     C                   time                    timedata1
      * Parse input string
     C                   exsr      Parse
      *------------------
      * Retrieve number of boats to be shown
     C                   EVAL      ItemCount = ZhbGetVarCnt('serno')
      * Show the requested boats
     C                   IF        ItemCount = 0
     C                   eval      SernoC = zhbgetvar('xxref')
     C                   eval      WSUSER  = zhbgetvar('USER')
     C                   EVAL      UsrSpcName  = zhbgetvar('UsrSpcName')
     C                   ELSE
     C                   eval      i = 1
     C                   eval      SernoC = ZhbGetVar('serno':i)
     C                   eval      WSUSER  = zhbgetvar('USER':i)
     C                   EVAL      UsrSpcName  = zhbgetvar('UsrSpcName':i)
     C                   END
     C                   MOVE      SERNOC        XXREF
     C                   MOVE      XXMN          VAMN
     C                   MOVE      XXMN          BWSMN
     C                   MOVE      XXKJED        MRKJED
     C                   MOVE      XXKNV         MRKNV
     C                   MOVE      XXLNR         MRLNR
     C                   MOVE      XXLNA         MRLNA
      * Initier
     C                   EXSR      INIT
     C                   CALLB     PGM
     C                   PARM                    BIBRID
     C                   PARM                    UsrSpcName
     C                   PARM                    UINN              1
     C                   IF        UINN = 'N'
     C                   GOTO      SLUTT
     C                   END
     C     WSUSER        CHAIN     BRIDF                              50
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVE      WDT           BIDTF
     C                   MOVE      WTM           BITIDF
     C                   UPDATE    BRIDFR
      *
     C     LLMRFK        CHAIN     LLMRF                              33
     C                   IF        VAMN = 0
     C                   EXSR      NYLINJE
      *
     C                   IF        FEILMELDING = *BLANKS
     C                   MOVE      *BLANKS       WSANR
     C                   MOVE      *BLANKS       WSLOK2
     C                   MOVE      *BLANKS       LVVB
     C                   MOVE      *ZEROS        LVSALD
     C                   MOVE      *BLANKS       WSOANT
     C                   END
     C                   MOVE      LVSALD        WSSALD            7
     C                   CALLB     PGM2
     C                   PARM                    BWSMRNR
     C                   PARM                    GKGRP             8 0
     C                   PARM                    BIKUNR
     C                   PARM                    WSUSER
     C                   PARM                    UsrSpcName
     C                   PARM                    BIFIRM
     C                   PARM                    BIFARG
     C                   PARM                    WSANR
     C                   PARM                    LVVB
     C                   PARM                    WSSALD
     C                   PARM                    WSOANT
     C                   PARM                    WSLOK1
     C                   PARM                    WSLOK2
     C                   PARM                    WSLLT
     C                   PARM                    FMELDNR
     C                   PARM                    FEILMELDING
     C                   ELSE
     C                   EXSR      ENDRING
     C                   END
      *
      * Send output buffer and quit
     C     SLUTT         TAG
     C                   exsr      Exit
      *=====================================================================
      * Send output buffer and quit
      *=====================================================================
     C     Exit          begsr
      * Compute and display run time
     C                   IF        UINN = 'N'
     C                   time                    timedata2
     C     timedata2     subdur    timedata1     ms:*ms
     C                   eval      sec = ms / 1000000
     C                   callp     updhtmlvar('runtime':
     C                             %trim(%editw(sec:'     0 .   ')))
     C                   callp     wrtsection('endhtml')
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
     C                   END
      *
     C                   close     LLAGJL01
     C                   close     LLMRF
     C                   close     LLTELL
     C                   close     LLVARE01
     C                   close     LLGRPK05
     C                   close     LLLSDF
     C                   close     BRIDF
     C                   close     BRPARF
     C                   eval      *inlr = *on
      *
     C                   return
     C                   endsr
      *=====================================================================
      * Parse input string
      *=====================================================================
     C     Parse         begsr
     C                   eval      lng     = zhbgetvar('lng')
     C                   eval      WSMRNR  = zhbgetvar('MRMRNR')
     C                   EVAL      BWSMRNR = c2n2(WSMRNR)
     C                   eval      WSOANT  = zhbgetvar('WSOANT')
     C                   EVAL      BWSOANT = c2n2(WSOANT)
     C                   eval      WSANR   = zhbgetvar('WSANR')
     C                   eval      WSANR   = uppify(WSANR)
     C                   eval      WSLOK1  = zhbgetvar('WSLOK1')
     C                   eval      WSLOK1  = uppify(WSLOK1)
     C                   eval      WSLOK2  = zhbgetvar('WSLOK2')
     C                   eval      WSLOK2  = uppify(WSLOK2)
     C                   eval      WSLLT   = zhbgetvar('WSLLT')
     C                   eval      WSLLT   = uppify(WSLLT)

     C                   endsr
      *=====================================================================
      *  Initier
      *=====================================================================
     C     INIT          begsr
     C                   OPEN      BRIDF
     C     WSUSER        CHAIN(N)  BRIDF                              50
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   CALLB     'INCGI'
     C                   PARM                    BISTDL
     C                   else
      * Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
     C                   open      LLAGJL01                             50
     C                   open      LLMRF                                50
     C                   open      LLTELL                               50
     C                   open      LLVARE01                             50
     C                   open      LLGRPK05                             50
     C                   open      LLLSDF                               50
     C                   open      BRPARF                               50
     C     LLMRFK        klist
     C                   kfld                    GKGRP
     C                   kfld                    BIKUNR
     C                   kfld                    BWSMRNR
     C     LLAGJL01K     klist
     C                   kfld                    GKGRP
     C                   kfld                    BIKUNR
     C                   kfld                    BWSMN
     C     LV01K         klist
     C                   kfld                    GKGRP
     C                   kfld                    BIKUNR
     C                   kfld                    WSANR
     C     BRPARFK       klist
     C                   kfld                    PABRID
     C                   kfld                    PAKUNR
     C                   kfld                    PAID
      *
     C                   MOVE      BIBRID        PABRID
     C                   MOVEL     'SPRÅK'       PAID
     C     BRPARFK       chain     BRPARF                             33
     C                   IF        (*IN33 OR (PAVRDA <> 'EN'))
     C                   MOVE      *BLANKS       XSPRÅK            2
     C                   ELSE
     C                   MOVE      'EN'          XSPRÅK            2
     C                   END
     C     BIKUNR        chain     LLGrpK05                           33
     C                   MOVE      *BLANKS       FMELDNR           1
     C                   MOVE      *BLANKS       FEILMELDING
      *
     C                   endsr
      *=====================================================================
      *  NY LINJE
      *=====================================================================
     C     NYLINJE       BEGSR
      * TEST INNGÅENDE DATA
     C                   EXSR      TESTB
      *
     C                   IF        FEILMELDING = *BLANKS
     C                   EXSR      OPDLINJE
     C                   END
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
     C     TESTB         BEGSR
      *////////////////////////////////////////////////////////////
     C                   MOVE      *BLANKS       FEILMELDING
     C                   MOVE      *BLANKS       FMELDNR
      * TEST ARTIKKELNR
     C     LV01K         CHAIN     LLVARE01                           33
     C  N33LVVKT0        COMP      0                                      33
     C  N33              IF        LVAKT = 'I'
     C                   IF        XSPRÅK = 'EN'
     C                   MOVEL     FEILEN04      FEILMELDING
     C                   ELSE
     C                   MOVEL     FEIL04        FEILMELDING
     C                   END
     C                   MOVE      *ON           *IN33
     C                   END
      *
     C     STTESTB12     TAG
     C     *IN33         IFEQ      '1'
     C                   IF        FEILMELDING = *BLANKS
     C                   IF        XSPRÅK = 'EN'
     C                   MOVEL     FEILEN01      FEILMELDING
     C                   ELSE
     C                   MOVEL     FEIL01        FEILMELDING
     C                   END
     C                   MOVE      *BLANKS       LVVB
     C                   END
     C                   MOVE      '1'           FMELDNR
     C                   GOTO      SLTESTB
     C                   END
      * TEST ANTALL STYKK
     C     BWSOANT       IFEQ      0
     C                   IF        XSPRÅK = 'EN'
     C                   MOVEL     FEILEN02      FEILMELDING
     C                   ELSE
     C                   MOVEL     FEIL02        FEILMELDING
     C                   END
     C                   MOVE      '2'           FMELDNR
     C                   GOTO      SLTESTB
     C                   END
      * TEST LOKASJON
     C     WSLOK2        IFEQ      *BLANKS
     C                   IF        XSPRÅK = 'EN'
     C                   MOVEL     FEILEN03      FEILMELDING
     C                   ELSE
     C                   MOVEL     FEIL03        FEILMELDING
     C                   END
     C                   MOVE      '3'           FMELDNR
     C                   GOTO      SLTESTB
     C                   END
      * TEST LAGERSTED
     C     WSLOK1        IFEQ      *BLANKS
     C                   IF        XSPRÅK = 'EN'
     C                   MOVEL     FEILEN05      FEILMELDING
     C                   ELSE
     C                   MOVEL     FEIL05        FEILMELDING
     C                   END
     C                   MOVE      '5'           FMELDNR
     C                   GOTO      SLTESTB
     C                   END
      *
     C     SLTESTB       ENDSR
      *
      *=====================================================================
      *  Oppdater linje
      *=====================================================================
     C     OPDLINJE      begsr
      *
     C                   CLEAR                   LLAGJR
     C                   MOVE      GKGRP         VAGRUP
     C                   MOVE      GKPNR         VAKJED
     C     3             CHAIN     LLTELL                             33
     C                   ADD       1             LLREC1
     C                   UPDATE    LLTELLR
     C                   MOVE      LLREC1        VAMN

     C                   EXSR      MOVWSVA
     C                   WRITE     LLAGJR
      *
     C                   endsr
      *=====================================================================
      *  OPPDATER FELTER I FIL
      *=====================================================================
     C     MOVWSVA       begsr
      *
     C                   MOVE      MRMRNR        VAMRNR
     C                   MOVE      WSANR         VAANR
     C                   MOVE      LVVB          VAVARE
     C                   MOVE      BWSOANT       VAOANT
     C                   MOVE      BWSOANT       VASAE0
     C                   MOVE      BWSOANT       VASALD
     C                   MOVEL     WSLOK1        VALLOK
     C                   MOVE      WSLOK2        VALLOK
     C     WSLOK1        CHAIN     LLLSDF                             33
     C                   IF        (*IN33 OR (LSLLT = *BLANKS))
     C                   MOVE      WSLLT         VALLT
     C                   ELSE
     C                   MOVE      LSLLT         VALLT
     C                   END
     C                   MOVE      99999999      VADATO
     C                   MOVE      LVVKT0        VAVKT0
     C                   MOVE      LVKUB0        VAKUB0
     C                   MOVE      LVVKF0        VAVKF0
     C                   MOVE      LVKUF0        VAKUF0
     C                   MOVE      LVVKT1        VAVKT1
     C                   MOVE      LVKUB1        VAKUB1
     C                   MOVE      LVVKF1        VAVKF1
     C                   MOVE      LVKUF1        VAKUF1
     C                   MOVE      LVAN01        VAAN01
     C                   MOVE      LVVKT2        VAVKT2
     C                   MOVE      LVKUB2        VAKUB2
     C                   MOVE      LVVKF2        VAVKF2
     C                   MOVE      LVKUF2        VAKUF2
     C                   MOVE      LVAN02        VAAN02
     C                   MOVE      LVAN12        VAAN12
     C                   MOVE      LVVKT3        VAVKT3
     C                   MOVE      LVKUB3        VAKUB3
     C                   MOVE      LVVKF3        VAVKF3
     C                   MOVE      LVKUF3        VAKUF3
     C                   MOVE      LVAN03        VAAN03
     C                   MOVE      LVAN23        VAAN23
     C                   MOVE      LVMIN         VAMIN
     C     BWSOANT       MULT(H)   LVVKT0        VAVKTS
     C     BWSOANT       MULT(H)   LVKUB0        VAKUBS
     C                   MOVE      XXUSER        VABUSE
     C                   MOVE      WDT           VABDAT
     C                   MOVE      WTM           VABKL
      *
     C                   ENDSR
      *
      *=====================================================================
      *  ENDRE LINJE
      *=====================================================================
     C     ENDRING       BEGSR
      * Set output skeleton html member name
     C                   exsr      SetSkl
      * Read skeleton output html into core
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
      * Retrieve environment variables
     C                   exsr      RtvEnvVar
      *
      * Issue html section /$top
     C                   exsr      SetTop
     C                   callp     wrtsection('top')

     C     LLAGJL01K     CHAIN     LLAGJL01                           60
     C   60              CLEAR                   LLAGJR
     C                   exsr      SetFound
     C                   callp     wrtsection('found')
      *
     C                   ENDSR
      *
      *=====================================================================
      * Set html output variables for section /$top
      *=====================================================================
     C     SetTop        begsr
      * Repeat national language for the next invocation
     C                   callp     updhtmlvar('LNG':lng:
     C                             InitHTMLVars)
      * Repeat background color for the next invocation
     C                   callp     updhtmlvar('TXTCOLOR':'black')
     C                   callp     updhtmlvar('BOLD':' ')
     C                   callp     updhtmlvar('BGCOLOR':BIFARG)
     C                   callp     updhtmlvar('LINK':'0000FF')
     C                   callp     updhtmlvar('VLINK':'FF0000')
     C                   callp     updhtmlvar('ALINK':'00FF00')
      * Protocol
     C                   callp     updHtmlVar('PROTOCOL':S_Protocol)
      *
BODY C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updhtmlvar('USER':XXUSER)
     C                   EVAL      UsrSpcName  = zhbgetvar('UsrSpcName')
     C                   callp     updhtmlvar('WSGRP':
     C                             %trim(%editC(MRGRUP:'Z')))
     C                   callp     updhtmlvar('WSPNR':
     C                             %trim(%editC(MRKJED:'Z')))
      *
     C                   endsr
      *=====================================================================
      * Set html output variables for section /$found (display a boat)
      *=====================================================================
     C     SetFound      begsr
      *
      * Sett inn i HTMLvariabler
     C                   callp     updhtmlvar('MRMRNR':
     C                             %trim(%editc(BWSMRNR:'Z')))
     C                   callp     updhtmlvar('VAMN':
     C                             %trim(%editc(VAMN:'Z')))
     C                   callp     updhtmlvar('MRKJED':
     C                             %trim(%editc(MRKJED:'Z')))
     C                   callp     updHtmlVar('MRKNV':MRKNV)
     C                   callp     updHtmlVar('MRLNR':MRLNR)
     C                   callp     updHtmlVar('MRLNA':MRLNA)
     C                   callp     updHtmlVar('WSANR':VAANR)
     C                   callp     updhtmlvar('WSOANT':
     C                             %trim(%editC(VAOANT:'Z')))
     C                   callp     updhtmlvar('WSVB':VAVARE)
     C                   callp     updhtmlvar('FEILMELDING':spaces)
     C                   callp     updhtmlvar('FMELDNR':FMELDNR)
     C                   callp     updhtmlvar('USER':XXUSER)
     C                   callp     updHtmlVar('UsrSpcName':UsrSpcName)
      *
     C                   endsr
      *=====================================================================
      * Set html output skeleton member before its read into memory
      *=====================================================================
     C     SetSkl        begsr
     C                   eval      lng = uppify(lng)
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC' + lng
     C                   eval      Mbr = 'SYL025'
     C                   CAT       XSPRÅK:0      MBR
     C                   endsr
      *=====================================================================
      *  Retrieve environment variables
      *=====================================================================
     C     RtvEnvVar     begsr
      * Use getenvp to get this server's protocol
     C                   eval      S_Protocol =getenv('SERVER_PROTOCOL':
     C                             qusec)
      *
     C                   endsr
