********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
P11094* 01 CB  PTF11  Nya berækningsenheter ingen mærkning
********************************************************************
     FSVEVA     UF A E           K DISK
     FSVE004D   CF   E             WORKSTN
     F                                     INFDS(FBAREA)

     D FBAREA          DS
     D  WSCLOC               382    383B 0

     D                 DS
     D  Å01                    1     27
     D                                     DIM(9)
     D  Å01A                   1      3
     D  Å01B                   4      6
     D  Å01C                   7      9
     D  Å01D                  10     12
     D  Å01E                  13     15
     D  Å01F                  16     18
     D  Å01G                  19     21
     D  Å01H                  22     24
     D  Å01I                  25     27
     D                 DS
     D  Å02                    1     99  3
     D                                     DIM(9)
     D  Å02A                   1     11S 3
     D  Å02B                  12     22S 3
     D  Å02C                  23     33S 3
     D  Å02D                  34     44S 3
     D  Å02E                  45     55S 3
     D  Å02F                  56     66S 3
     D  Å02G                  67     77S 3
     D  Å02H                  78     88S 3
     D  Å02I                  89     99S 3
     D                 DS
     D  Å03                    1     81  4
     D                                     DIM(9)
     D  Å03A                   1      9S 4
     D  Å03B                  10     18S 4
     D  Å03C                  19     27S 4
     D  Å03D                  28     36S 4
     D  Å03E                  37     45S 4
     D  Å03F                  46     54S 4
     D  Å03G                  55     63S 4
     D  Å03H                  64     72S 4
     D  Å03I                  73     81S 4
     D                 DS
     D  Å04                    1     36
     D                                     DIM(9)
     D  Å04A                   1      4
     D  Å04B                   5      8
     D  Å04C                   9     12
     D  Å04D                  13     16
     D  Å04E                  17     20
     D  Å04F                  21     24
     D  Å04G                  25     28
     D  Å04H                  29     32
     D  Å04I                  33     36
     D                 DS
     D  Å05                    1     81  0
     D                                     DIM(9)
     D  Å05A                   1      9S 0
     D  Å05B                  10     18S 0
     D  Å05C                  19     27S 0
     D  Å05D                  28     36S 0
     D  Å05E                  37     45S 0
     D  Å05F                  46     54S 0
     D  Å05G                  55     63S 0
     D  Å05H                  64     72S 0
     D  Å05I                  73     81S 0

      *//////////////
      *  Hodelogikk /
      *//////////////
     C                   EXSR      SRA
     C                   EXSR      SRB
     C     *IN88         IFEQ      *OFF
     C                   EXSR      SRC
     C                   ENDIF
     C                   MOVE      '1'           *INLR
      *////////////////////////////////////////////////////////////
      *  Initiering                                           SRA /
      *////////////////////////////////////////////////////////////
     C     SRA           BEGSR
      *____________________
      * Definiere variabler
      *""""""""""""""""""""
     C     *LIKE         DEFINE    SVEVA_SAV     ZVEVA_SAV
     C     *LIKE         DEFINE    SVEVA_SOP     ZVEVA_SOP
     C     *LIKE         DEFINE    SVEVA_SLI     ZVEVA_SLI
     C                   Z-ADD     1             X                 3 0
     C                   Z-ADD     0             WX                3 0
     C                   Z-ADD     0             PFLAG             1 0
      *_________________
      * Input parametere
      *"""""""""""""""""
     C     *ENTRY        PLIST
     C                   PARM                    ZVEVA_SAV
     C                   PARM                    ZVEVA_SOP
     C                   PARM                    ZVEVA_SLI
     C                   PARM                    PFLAG             1 0
      *__________________
      * Definiere Nøkkler
      *""""""""""""""""""
     C     SVEVA_KEY     KLIST
     C                   KFLD                    ZVEVA_SAV
     C                   KFLD                    ZVEVA_SOP
     C                   KFLD                    ZVEVA_SLI
     C                   MOVEA     *ZEROS        Å02
     C                   MOVEA     *ZEROS        Å03
     C                   MOVEA     *ZEROS        Å05
     C     PFLAG         IFEQ      1
     C                   MOVE      *ON           *IN88
     C                   ENDIF
      *_________________________________
      * Hæmta dagens datum och klockslag
      *"""""""""""""""""""""""""""""""""
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Bearbeide Formater                                   SRB /
      *////////////////////////////////////////////////////////////
     C     SRB           BEGSR
      * Sjekker først om det finnes noen verdier å vise
     C     SVEVA_KEY     SETLL     SVEVA
     C     SVEVA_KEY     READE(N)  SVEVA                                  90
     C     *IN90         DOWEQ     *OFF
     C                   MOVE      SVEVA_ABK     Å01(X)
     C                   MOVE      SVEVA_ABG     Å02(X)
     C                   MOVE      SVEVA_ABS     Å03(X)
     C                   MOVE      SVEVA_ABX     Å04(X)
     C                   MOVE      SVEVA_ABB     Å05(X)
     C                   ADD       SVEVA_ABB     Å05S
     C                   ADD       1             X
     C     X             IFGT      9
     C                   LEAVE
     C                   ENDIF
     C     SVEVA_KEY     READE(N)  SVEVA                                  90
     C                   ENDDO
      * Viser bilde
     C     TAG01         TAG
     C                   EXFMT     SVE004DA
      * F4=Søk kode
     C     *IN04         IFEQ      *ON
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C                   EVAL      X=LINNBR-3
      * Avgiftsslag
     C     POSNBR        IFEQ      1

     C                   CALL      'SVT003R'
     C                   PARM      'CHN'         UTYP              3
     C                   PARM      'A'           UIE               1
     C                   PARM      WDT           UDATO             8
     C                   PARM                    UKD              10
     C     UKD           IFNE      *BLANKS
     C                   MOVEA     UKD           Å01(X)
     C                   ENDIF

     C                   GOTO      TAG01
     C                   ENDIF
      * Extra mængdenhet
     C     POSNBR        IFEQ      29

     C                   CALL      'SVT003R'
     C                   PARM      'MEM'         UTYP              3
     C                   PARM      'A'           UIE               1
     C                   PARM      WDT           UDATO             8
     C                   PARM                    UKD              10
     C     UKD           IFNE      *BLANKS
     C                   MOVEA     UKD           Å04(X)
     C                   ENDIF

     C                   GOTO      TAG01
     C                   ENDIF
     C                   ENDIF
      * andre funksjonstaster
     C     *IN03         CABEQ     *ON           SRB9
     C     *IN12         CABEQ     *ON           SRB9
     C     *IN88         CABEQ     *ON           SRB9
      * Enter utan funktionstangenter, rækna ut summor
     C     Å04A          IFEQ      *BLANKS
     C                   EVAL      Å05A = (Å02A * Å03A) / 100
     C                   ELSE
     C                   EVAL      Å05A = Å02A * Å03A
     C                   ENDIF
     C     Å04B          IFEQ      *BLANKS
     C                   EVAL      Å05B = (Å02B * Å03B) / 100
     C                   ELSE
     C                   EVAL      Å05B = Å02B * Å03B
     C                   ENDIF
     C     Å04C          IFEQ      *BLANKS
     C                   EVAL      Å05C = (Å02C * Å03C) / 100
     C                   ELSE
     C                   EVAL      Å05C = Å02C * Å03C
     C                   ENDIF
     C     Å04D          IFEQ      *BLANKS
     C                   EVAL      Å05D = (Å02D * Å03D) / 100
     C                   ELSE
     C                   EVAL      Å05D = Å02D * Å03D
     C                   ENDIF
     C     Å04E          IFEQ      *BLANKS
     C                   EVAL      Å05E = (Å02E * Å03E) / 100
     C                   ELSE
     C                   EVAL      Å05E = Å02E * Å03E
     C                   ENDIF
     C     Å04F          IFEQ      *BLANKS
     C                   EVAL      Å05F = (Å02F * Å03F) / 100
     C                   ELSE
     C                   EVAL      Å05F = Å02F * Å03F
     C                   ENDIF
     C     Å04G          IFEQ      *BLANKS
     C                   EVAL      Å05G = (Å02G * Å03G) / 100
     C                   ELSE
     C                   EVAL      Å05G = Å02G * Å03G
     C                   ENDIF
     C     Å04H          IFEQ      *BLANKS
     C                   EVAL      Å05H = (Å02H * Å03H) / 100
     C                   ELSE
     C                   EVAL      Å05H = Å02H * Å03H
     C                   ENDIF
     C     Å04I          IFEQ      *BLANKS
     C                   EVAL      Å05I = (Å02I * Å03I) / 100
     C                   ELSE
     C                   EVAL      Å05I = Å02I * Å03I
     C                   ENDIF
     C                   EVAL      Å05S = Å05A + Å05B + Å05C + Å05D + Å05E +Å05F
     C                                    + Å05G + Å05H + Å05I

     C                   GOTO      TAG01

     C     SRB9          TAG
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Summering                                            SRC /
      *////////////////////////////////////////////////////////////
     C     SRC           BEGSR
      * Enter medfører oppdatering, først sletting og deretter WRITE
     C     SVEVA_KEY     SETLL     SVEVA
     C     SVEVA_KEY     READE     SVEVA                                  90
     C     *IN90         DOWEQ     *OFF
     C                   DELETE    SVEVAR
     C     SVEVA_KEY     READE     SVEVA                                  90
     C                   ENDDO
      * Skriver så alle poster til fil.
      * Skriver også blanke linjer, men ikke når det er bare blanke
     C                   EVAL      X=1
     C                   EVAL      WX=0
     C                   DO        9
     C     Å01(X)        IFNE      *BLANKS
     C                   EVAL      WX=X
     C                   ENDIF
     C                   ADD       1             X
     C                   ENDDO
     C                   EVAL      X=1
     C     WX            IFGT      0
     C                   DO        9
     C                   CLEAR                   SVEVAR
     C                   EVAL      SVEVA_SAV=ZVEVA_SAV
     C                   EVAL      SVEVA_SOP=ZVEVA_SOP
     C                   EVAL      SVEVA_SLI=ZVEVA_SLI
     C                   MOVEA     Å01(X)        SVEVA_ABK
     C                   MOVEA     Å02(X)        SVEVA_ABG
     C                   MOVEA     Å03(X)        SVEVA_ABS
     C                   MOVEA     Å04(X)        SVEVA_ABX
     C                   MOVEA     Å05(X)        SVEVA_ABB
     C     Å01(X)        IFNE      *BLANKS
     C                   WRITE     SVEVAR
     C                   ENDIF
     C     X             IFGE      WX
     C                   LEAVE
     C                   ENDIF
     C                   ADD       1             X
     C                   ENDDO
     C                   ENDIF

     C     SRC9          TAG
     C                   ENDSR
