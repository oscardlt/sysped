      *
      ***********************************************************
      *** PROGRAM SKAL ARBEIDE MED KODE FOR EL. TRANSIT       ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01 02 04 07-11 13-24             ***
      *** LEDIGE INDIKATORER 29 31 32 34-39 55-59 61-84 88
      ***                    90 95-97                         ***
      ***********************************************************
      *
     FTRKODLUP  UF A E           K DISK
     FTRKODL01  IF   E           K DISK
     F                                     RENAME(TRKODFR:TRKODFR1)
     FTRKODL02  IF   E           K DISK
     F                                     RENAME(TRKODFR:TRKODFR2)
     FTR001D    CF   E             WORKSTN
     F                                     SFILE(TR001D11:ZRECNO)
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
      *
     C                   EXSR      INIT
     C     UUNIK         CABNE     *ZEROS        START2
      *
     C     START         TAG
     C                   EXFMT     TR001D01
     C                   MOVE      '0'           *IN30
     C                   MOVE      '0'           *IN99
     C     *IN03         CABEQ     '1'           SLUTT
      *
     C     WSUNIK        IFNE      12
     C     WSUNIK        ANDNE     13
     C     WSUNIK        ANDNE     14
     C     WSUNIK        ANDNE     17
     C     WSUNIK        ANDNE     31
     C     WSUNIK        ANDNE     39
     C     WSUNIK        ANDNE     47
     C     WSUNIK        ANDNE     64
     C     WSUNIK        ANDNE     96
     C     WSUNIK        ANDNE     105
     C     WSUNIK        ANDNE     106
     C     WSUNIK        ANDNE     116
     C     WSUNIK        ANDNE     23
     C     WSUNIK        ANDNE     41
     C     WSUNIK        ANDNE     42
     C     WSUNIK        ANDNE     49
     C     WSUNIK        ANDNE     149
     C     WSUNIK        ANDNE     209
     C                   MOVE      'YBC0808'     MSGNR
     C                   MOVE      '1'           *IN30
     C                   MOVE      '1'           *IN99
     C                   GOTO      START
     C                   END
      *
     C     START2        TAG
     C     WSUNIK        IFEQ      64
     C                   CALL      'TR004R'
     C                   ELSE
     C                   EXSR      ARBKOD
     C     UUNIK         CABNE     *ZEROS        SLUTT
     C                   END
     C     *IN03         CABEQ     '0'           START
      *
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
      *
     C***********************************************
     C     ARBKOD        BEGSR
     C***********************************************
     C     WSUNIK        COMP      106                                    28
     C                   MOVE      WSUNIK        YKUNIK
     C                   SELECT
     C     WSUNIK        WHENEQ    12
     C                   MOVE      'YBC0795'     MSGNRW
     C     WSUNIK        WHENEQ    13
     C                   MOVE      'YBC0796'     MSGNRW
     C     WSUNIK        WHENEQ    14
     C                   MOVE      'YBC0797'     MSGNRW
     C     WSUNIK        WHENEQ    17
     C                   MOVE      'YBC0798'     MSGNRW
     C     WSUNIK        WHENEQ    31
     C                   MOVE      'YBC0799'     MSGNRW
     C     WSUNIK        WHENEQ    39
     C                   MOVE      'YBC0800'     MSGNRW
     C     WSUNIK        WHENEQ    47
     C                   MOVE      'YBC0801'     MSGNRW
     C     WSUNIK        WHENEQ    96
     C                   MOVE      'YBC1941'     MSGNRW
     C     WSUNIK        WHENEQ    105
     C                   MOVE      'YBC0802'     MSGNRW
     C     WSUNIK        WHENEQ    106
     C                   MOVE      'YBC0803'     MSGNRW
     C     WSUNIK        WHENEQ    116
     C                   MOVE      'YBC1942'     MSGNRW
     C     WSUNIK        WHENEQ    23
     C                   MOVE      'YBC0831'     MSGNRW
     C     WSUNIK        WHENEQ    41
     C                   MOVE      'YBC0829'     MSGNRW
     C     WSUNIK        WHENEQ    42
     C                   MOVE      'YBC0830'     MSGNRW
     C     WSUNIK        WHENEQ    49
     C                   MOVE      'YBC0832'     MSGNRW
     C     WSUNIK        WHENEQ    149
     C                   MOVE      'YBC0853'     MSGNRW
     C     WSUNIK        WHENEQ    209
     C                   MOVE      'YBC1910'     MSGNRW
     C                   ENDSL
     C                   MOVE      '0'           *IN27
     C                   MOVE      *BLANKS       W2KODE
     C                   MOVE      *BLANKS       W2TXTN
      *
     C     STAK1         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     TR001D12
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
     C                   MOVE      W2KODE        Y2KODE           10
     C                   MOVE      W2TXTN        Y2TXTN           35
     C                   MOVE      *BLANKS       XKKODE           10
     C                   MOVE      *BLANKS       XKTXTN           35
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C  N27TRK1K         SETLL     TRKODL01
     C   27TRK2K         SETLL     TRKODL02
     C  N27YKUNIK        READE     TRKODL01                               94
     C   27YKUNIK        READE     TRKODL02                               94
      *
     C     STAK2         TAG
      *
     C  N94              DO
     C                   DO        14
     C                   ADD       1             ZRECNO
     C                   WRITE     TR001D11
     C  N27YKUNIK        READE     TRKODL01                               94
     C   27YKUNIK        READE     TRKODL02                               94
     C  N94              MOVE      TKKODE        XKKODE
     C  N94              MOVE      TKTXTN        XKTXTN
     C  N94              END
     C                   END
      *
     C                   Z-ADD     ZRECNO        YRECNO            5 0
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     2             POSNBR
     C     STAK3         TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     TR001D17
     C                   ELSE
     C                   WRITE     TR001D13
     C                   EXFMT     TR001D12
     C                   END
     C                   SETOFF                                       999830
      *
      *** AVSLUTT
     C   03              GOTO      SLAK
      *
      *** FRISK OPP
     C   05              GOTO      STAK1
      *
      *** LAGE NY KODE
     C   06              DO
     C                   Z-ADD     21            LINNBR
     C                   Z-ADD     5             POSNBR
     C                   GOTO      STAK5
     C                   END
      *
      *** SØK
     C   08              GOTO      STAK6
      *** GO TIL VALGBILDE
     C   12              GOTO      SLAK
      *
      *** BLA VIDERE
     C   25              DO
     C                   SETOFF                                       25
     C                   Z-ADD     YRECNO        ZRECNO
     C  N94              DO
     C  N27TRK1K2        SETLL     TRKODL01
     C   27TRK2K2        SETLL     TRKODL02
     C     STAK4         TAG
     C  N27YKUNIK        READE     TRKODL01                               94
     C   27YKUNIK        READE     TRKODL02                               94
     C   27
     CANN94XKKODE        CABNE     TKKODE        STAK4
     C                   END
     C                   GOTO      STAK2
     C                   END
      *
     C     ZRECNO        IFNE      0
     C                   EXSR      HENT
     C                   END
     C                   GOTO      STAK3
      *
      *** LAGE NY KODE
     C     STAK5         TAG
     C                   EXFMT     TR001D14
     C                   MOVEA     '000'         *IN(88)
     C                   MOVE      '0'           *IN30
     C   03              GOTO      SLAK
     C   12              GOTO      STAK3
     C                   EXSR      TESTB2
     C   30              GOTO      STAK5
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     2             POSNBR
     C                   GOTO      STAK3
      *
      *** SØK
     C     STAK6         TAG
     C                   EXFMT     TR001D16
     C   12              GOTO      STAK3
     C     W2TXTN        COMP      *BLANKS                            2727
     C                   GOTO      STAK1
      *
     C     SLAK          ENDSR
      *
     C***********************************************
     C     TESTB2        BEGSR
     C***********************************************
     C  N40              DO
     C     WSKODE        COMP      *BLANKS                                90
     C   90              MOVE      'YBC0804'     MSGNR
     C   90              SETON                                        30
     C     TRK1K3        CHAIN(N)  TRKODLUP                           33
     C  N33              DO
     C                   MOVE      'YBC0805'     MSGNR
     C                   SETON                                        3090
     C                   END
     C   90              Z-ADD     21            LINNBR
     C   90              Z-ADD     5             POSNBR
     C                   END
      *
     C     WSTXTN        IFEQ      *BLANKS
     C  N30              MOVE      'YBC0806'     MSGNR
     C  N30              Z-ADD     21            LINNBR
     C  N30              Z-ADD     16            POSNBR
     C                   SETON                                        8930
     C                   END
      *
     C     WSTXTE        IFEQ      *BLANKS
     C  N30              MOVE      'YBC0807'     MSGNR
     C  N30              Z-ADD     22            LINNBR
     C  N30              Z-ADD     16            POSNBR
     C                   SETON                                        8830
     C                   END
      *
     C   30              GOTO      SLTB1
      *
      *** OPPDATERE PÅ REGISTER OG SUBFIL
     C     TRK1K3        CHAIN     TRKODLUP                           33
     C                   EXSR      MOVWST
     C  N33              UPDATE    TRKODFR
     C   33              WRITE     TRKODFR
     C   40              UPDATE    TR001D11
     C  N40              ADD       1             YRECNO
     C  N40              Z-ADD     YRECNO        ZRECNO
     C  N40              WRITE     TR001D11
     C                   WRITE     TR001D12
     C                   EXSR      MOVB2
      *
     C     SLTB1         ENDSR
      *
     C***********************************************
     C     HENT          BEGSR
     C***********************************************
     C                   MOVEA     '00'          *IN(91)
     C     *IN98         DOWEQ     '0'
     C                   READC     TR001D11                               98
     C   98              DO
     C   99              MOVE      '0'           *IN98
     C                   MOVE      '0'           *IN99
     C                   GOTO      SLHENT
     C                   END
      *
     C     ZRECNO        DIV       14            XLIN              5 0
     C                   MVR                     XLIN
     C     XLIN          IFEQ      0
     C                   Z-ADD     21            XLIN
     C                   ELSE
     C                   ADD       5             XLIN
     C                   END
      *
     C                   SELECT
     C     WSNR          WHENEQ    '2'
     C     TKKODE        COMP      '*DELETE'                              98
     C   98              GOTO      SLHENT
     C                   EXSR      CHGTRK
     C     WSNR          WHENEQ    '4'
     C     TKKODE        COMP      '*DELETE'                              98
     C   98              GOTO      SLHENT
     C                   EXSR      DLTTRK
     C                   ENDSL
     C                   END
      *
     C     SLHENT        TAG
     C     *IN98         IFEQ      '1'
     C                   MOVE      'DIV0012'     MSGNR
     C                   SETON                                        30
     C                   ELSE
     C                   Z-ADD     5             LINNBR
     C                   END
     C                   Z-ADD     2             POSNBR
      *
     C                   ENDSR
      *
     C***********************************************
     C     CHGTRK        BEGSR
     C***********************************************
     C                   EXSR      MOVTWS
     C     STCHG         TAG
     C                   EXFMT     TR001D14
     C                   MOVEA     '000'         *IN(88)
     C                   SETOFF                                         30
     C   12              DO
     C                   UPDATE    TR001D11
     C                   EXSR      MOVB2
     C                   END
     C  N12              EXSR      TESTB2
     C  N12
     CAN 30              GOTO      STCHG
     C     XLIN          IFEQ      0
     C                   Z-ADD     6             LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     DLTTRK        BEGSR
     C***********************************************
     C                   SETON                                        99
     C                   EXFMT     TR001D15
     C  N12              DO
     C     TRK1K4        CHAIN     TRKODLUP                           33
     C  N33              DELETE    TRKODFR
     C                   EXSR      MOVB1
     C                   END
     C     XLIN          IFEQ      0
     C                   Z-ADD     6             LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C                   UPDATE    TR001D11
      *
     C                   ENDSR
      *
     C***********************************************
     C     MOVWST        BEGSR
     C***********************************************
     C                   MOVE      YKUNIK        TKUNIK
     C                   MOVE      WSKODE        TKKODE
     C                   MOVE      WSTXTN        TKTXTN
     C                   MOVE      WSTXTE        TKTXTE
     C                   IF        *IN28
     C                   MOVE      WSAVG         TKAVG
     C                   MOVE      WSANK         TKANK
     C                   MOVE      WSTRS         TKTRS
     C                   ELSE
     C                   MOVE      ' '           TKAVG
     C                   MOVE      ' '           TKANK
     C                   MOVE      ' '           TKTRS
     C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     MOVTWS        BEGSR
     C***********************************************
     C                   MOVE      TKKODE        WSKODE
     C                   MOVE      TKTXTN        WSTXTN
     C                   MOVE      TKTXTE        WSTXTE
     C                   IF        *IN28
     C                   MOVE      TKAVG         WSAVG
     C                   MOVE      TKANK         WSANK
     C                   MOVE      TKTRS         WSTRS
     C                   ELSE
     C                   MOVE      ' '           WSAVG
     C                   MOVE      ' '           WSANK
     C                   MOVE      ' '           WSTRS
     C                   END
     C                   SETON                                        40
     C                   SETON                                        99
     C                   Z-ADD     21            LINNBR
     C                   Z-ADD     16            POSNBR
      *
     C                   ENDSR
      *
     C***********************************************
     C     MOVB1         BEGSR
     C***********************************************
     C                   MOVE      *BLANKS       TKKODE
     C                   MOVEL     '*DELETE'     TKKODE
     C                   MOVE      *BLANKS       TKTXTN
     C                   MOVE      *BLANKS       TKTXTE
     C                   MOVE      ' '           TKAVG
     C                   MOVE      ' '           TKANK
     C                   MOVE      ' '           TKTRS
      *
     C                   ENDSR
      *
     C***********************************************
     C     MOVB2         BEGSR
     C***********************************************
     C                   SETOFF                                       40
     C                   MOVE      *BLANKS       WSKODE
     C                   MOVE      *BLANKS       WSTXTN
     C                   MOVE      *BLANKS       WSTXTE
     C                   MOVE      ' '           WSAVG
     C                   MOVE      ' '           WSANK
     C                   MOVE      ' '           WSTRS
      *
     C                   ENDSR
      *
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     TRK1K         KLIST
     C                   KFLD                    YKUNIK            3
     C                   KFLD                    W2KODE
     C     TRK1K2        KLIST
     C                   KFLD                    YKUNIK
     C                   KFLD                    XKKODE
     C     TRK1K3        KLIST
     C                   KFLD                    YKUNIK
     C                   KFLD                    WSKODE
     C     TRK1K4        KLIST
     C                   KFLD                    YKUNIK
     C                   KFLD                    TKKODE
     C     TRK2K         KLIST
     C                   KFLD                    YKUNIK
     C                   KFLD                    W2TXTN
     C     TRK2K2        KLIST
     C                   KFLD                    YKUNIK
     C                   KFLD                    XKTXTN
      *
     C     *ENTRY        PLIST
     C                   PARM                    UUNIK             3
      *
     C                   MOVE      UUNIK         WSUNIK
      *
     C                   ENDSR
      *
