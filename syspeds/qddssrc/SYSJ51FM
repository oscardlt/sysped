     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HELP
     A          R BILDE01
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF03(03 'AVSLUTT')
     A                                      CF04(04 'SØK BÅTKODE')
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/SYSJ51'
     A                                      COLOR(BLU)
     A                                  1 61DATE
     A                                      EDTCDE(Y)
     A                                  1 70TIME
     A                                  1 20MSGCON(40 DIV3853 SYMSGF)
     A                                      DSPATR(HI)
     A                                  5  2MSGCON(12 DIV3854 SYMSGF)
     A            WSBK           5   B  5 29DSPATR(UL)
     A  99                                  DSPATR(RI)
     A                                  7  2MSGCON(12 DIV3855 SYMSGF)
     A            WSREIS        14A  B  7 29DSPATR(UL)
     A  98                                  DSPATR(RI)
     A                                 10  2MSGCON(12 DIV3856 SYMSGF)
     A            WSDT           6  0B 10 29DSPATR(UL)
     A  97                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                 13  2MSGCON(18 DIV3894 SYMSGF)
     A            WSOPD          7Y 0B 13 29TEXT('Oppdragsnummer')
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
     A                                 23  2MSGCON(18 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(18 DIV0032 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                             24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R BILDE02
     A                                      CF12(12 'FORRIGE')
     A                                      CF05(05 'BEKREFTE ENDRING')
     A                                  1  2'SYSPED/SYSJ51'
     A                                      COLOR(BLU)
     A                                  1 61DATE
     A                                      EDTCDE(Y)
     A                                  1 70TIME
     A                                  1 20MSGCON(40 DIV3857 SYMSGF)
     A                                      DSPATR(HI)
     A                                  5  2MSGCON(12 DIV3854 SYMSGF)
     A            WSBK           5   O  5 29DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  7  2MSGCON(12 DIV3855 SYMSGF)
     A            WSREIS        14A  O  7 29DSPATR(UL)
     A                                      DSPATR(HI)
     A                                 10  2MSGCON(12 DIV3856 SYMSGF)
     A            WSDT           6  0O 10 29DSPATR(UL)
     A                                      COLOR(RED)
     A                                      EDTCDE(4)
     A                                 12  2MSGCON(12 DIV3859 SYMSGF)
     A            WSDTG          6  0O 12 29DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 23  2MSGCON(18 DIV3858 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
