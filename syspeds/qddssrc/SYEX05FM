     A*%%TS  SD  20130806  161236  SVEIN       REL-V7R1M0  5770-WDS
*****A**************************************************************
     A* RETTINGER I DETTE PROGRAM:
PTFnrA*Sek Sig Vers Beskrivelse...........................
"""""A*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
P8068A* 01 TH  PTF8 NY HELPFUNKSJON  KUN DDS
*****A**************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3                  -
     A                                             27 132 *DS4)
     A                                      REF(*LIBL/KODTLK)
     A                                      CF03(03 'Avslutt')
     A                                      PRINT
     A                                      HLPPNLGRP(HLPSCR SYEX05PNL)
     A                                      HLPTITLE('VEDLIKEHOLD AV FLYFRAKTRA-
     A                                      TER')
     A                                      HELP
     A                                      ALTHELP
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  6  3VALUES(' ' '1' '2' '4')
     A  98                                  DSPATR(RI)
     A  98                                  DSPATR(PC)
     A            KLKNVE        30   H
     A            KLKNVT        30   H
     A            KLKLK     R    2   O  6  8
     A            KLKLK2    R    2   O  6 16
     A            KLKNVN    R   30   O  6 21
     A            KLKALF    R    2   O  6 53
     A            KLKPRO    R    3Y 0O  6 59EDTCDE(4)
     A            KLKMIN    R    4Y 0O  6 66EDTCDE(4)
     A            KLKDIS    R    1   O  6 74
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A  91N93                               CF05(05 'Frisk')
     A  91N93                               CF06(06 'LAGE NY')
     A  91N93                               CF08(08 'SØK')
     A            ZRECNO         4S 0O  2 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYSPED/SYEX05'
     A                                      COLOR(BLU)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                  1 69TIME
     A                                  1 19MSGCON(40 DIV1580 SYMSGF)
     A                                      DSPATR(HI)
     A                                  3  2MSGCON(30 DIV0016 SYMSGF)
     A                                      COLOR(BLU)
     A N27                              4  2MSGCON(30 DIV0017 SYMSGF)
     A                                      COLOR(BLU)
     A  27                              4  2MSGCON(15 DIV3895 SYMSGF)
     A                                      COLOR(BLU)
     A  27                              4 19MSGCON(30 DIV0017 SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  2MSGCON(79 DIV1581 SYMSGF)
     A                                      DSPATR(HI)
     A          R BUNN
     A                                 23  2MSGCON(18 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 21MSGCON(18 DIV0021 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 40MSGCON(18 DIV0022 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 60MSGCON(18 DIV0023 SYMSGF)
     A                                      COLOR(BLU)
     A  98                             24  2'INVALID OPTION'
     A                                      COLOR(WHT)
     A N98                             24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A          R VARELI
     A*%%TS  SD  20130806  161236  SVEIN       REL-V7R1M0  5770-WDS
     A                                      CF12(12 'Forrige')
     A                                      SLNO(20)
     A                                      CLRL(05)
     A                                  1  2MSGCON(079 DIV1581 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            WSLK           2A  B  2  8DSPATR(UL)
     A  40                                  DSPATR(HI)
     A  40                                  DSPATR(PR)
     A N40                                  DSPATR(PC)
     A  70                                  DSPATR(RI)
     A            WSLK2          2A  B  2 16DSPATR(UL)
     A  40                                  DSPATR(PC)
     A  68                                  DSPATR(RI)
     A            WSNVN         30A  B  2 21DSPATR(UL)
     A  69                                  DSPATR(RI)
     A            WSALF          2A  B  2 53DSPATR(UL)
     A            WSPRO          3Y 0B  2 59DSPATR(UL)
     A                                      EDTCDE(4)
     A            WSMIN          4Y 0B  2 66DSPATR(UL)
     A                                      EDTCDE(4)
     A            WSDIS          1A  B  2 74DSPATR(UL)
     A            WSNVNE        30A  B  3 15DSPATR(UL)
     A            WSNVNT        30A  B  3 47DSPATR(UL)
     A                                  4  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 21MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  5  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                              5 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A                                  3  1MSGCON(013 SHU1394 *LIBL/SYMSGF)
     A          R VAREL2
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                  2  2MSGCON(12 DIV0026 SYMSGF)
     A            W2LK           2A  I  2 16DSPATR(UL)
     A                                      DSPATR(PC)
     A                                  2  2MSGCON(18 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A          R VAREL3
     A                                      CF12(12 'Forrige')
     A                                      SLNO(20)
     A                                      CLRL(05)
     A                                  1  2MSGCON(79 DIV1581 SYMSGF)
     A                                      COLOR(WHT)
     A            KLKLK     R    2   O  2  8DSPATR(UL)
     A                                      DSPATR(PC)
     A                                      DSPATR(HI)
     A            KLKLK2    R    2   O  2 16DSPATR(UL)
     A                                      DSPATR(HI)
     A            KLKNVN    R   30   O  2 21DSPATR(UL)
     A                                      DSPATR(HI)
     A            KLKALF    R    2   O  2 53DSPATR(UL)
     A                                      DSPATR(HI)
     A            KLKPRO    R    3Y00O  2 59DSPATR(UL)
     A                                      DSPATR(HI)
     A                                      EDTCDE(4)
     A            KLKMIN    R    4Y00O  2 66DSPATR(UL)
     A                                      DSPATR(HI)
     A                                      EDTCDE(4)
     A            KLKDIS    R    1   O  2 74DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  3  2MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A          R NYWRK
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                  1  2'SYSPED/SYEX05'
     A                                      COLOR(BLU)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                  1 70TIME
     A                                  1 19MSGCON(40 DIV1580 SYMSGF)
     A                                      DSPATR(HI)
     A                                  3  2MSGCON(30 DIV0016 SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(30 DIV0017 SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  2MSGCON(79 DIV1581 SYMSGF)
     A                                      DSPATR(HI)
     A                                 23  2MSGCON(18 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 21MSGCON(18 DIV0021 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 40MSGCON(18 DIV0022 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 60MSGCON(18 DIV0023 SYMSGF)
     A                                      COLOR(BLU)
     A                                 24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
