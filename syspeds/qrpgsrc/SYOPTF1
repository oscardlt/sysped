      *********************************************************************
      *
      *
CRTPG+*  CRTPGM SYSPED/SYOPTF1 MODULE(*LIBL/SYOPTF1 *LIBL/INCGI)
CRTPGM*         ACTGRP(CGI) AUT(*USE)
      *
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 JOV PTF10 ved csv må HTMLvar HXSNDN finnes/ HTML SYOPTF1S fikset
10XXX * 02 JOV PTF10 HESDL=Lossested lagt til
10XXX * 03 JOV PTF10 Søke på flere Felt,Avs navn/ref lagt til/ søk å ALLE FELT tilatt
********************************************************************
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FCUNDL01   IF   E           K DISK    USROPN
     FDOKUFT1   IF   E           K DISK    usropn
     FDOKUF     IF   E           K DISK    usropn
     FheadF     IF   E           K DISK    usropn
     FTURER14   IF   E           K DISK    usropn
     FTRACKl02  IF   E           K DISK    usropn
      *********************************************************************
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      /copy syspeds/qrpgsrcpy,variables3
      *
     D OddEven         s             10
     0* Client input variables
     D user            s             10
     D DATO            s             10
     D DATON           s              8  0
     D TERM            s              5
     D AGENT           S              8
     D AGENTN          S              8  0
N03  D KUNKU           S              8
N03  D KUNKUN          S              8  0
     D TUR             S              8
     D TURN            S              8  0
     D AVD             S              4
     D AVDN            S              4  0
     D TRANS           S              8
     D TRANSN          S              8  0
     D MottPost        s             20    VARYING
     D VisKl           s              1
N03  D VisKunLis       s              1
      *
     D ANTSND          s              5  0
     D MaxSN           s              5
     D MaxSND          s              5  0
     D ANTRCD          s              5  0
     D MaxRC           s              5
     D MaxRCD          s              5  0
     D TSTSCAN15       s             15
     D TSTSCAN30       s             30
     D HXSNDNTXT       s            240
     D CSV             s              1
     D CSVFILE         s             50
     D Spaces          s             12a   inz('&nbsp;&nbsp;')
     D ItemCount       s             10i 0
     D i               s             10i 0
     D                 DS
     D FTTIME                  1      6  0
     D  FTTIME4                1      4  0
     D                 DS
     D TEIDATKL                1     12  0
     D  TEIDAT                 1      8  0
     D  TEITIM                 9     14  0
     D                 DS
     D TEUDATKL                1     12  0
     D  TEUDAT                 1      8  0
     D  TEUTIM                 9     14  0
     D                 DS
     D PODDATKL                1     12  0
     D  PODDAT                 1      8  0
     D  PODTIM                 9     14  0
     D                 DS
     D  DsAvdOpd               1     12
     D  DsAVD                  1      4
     D  DsSlash                5      5
     D  DsOPD                  6     12
     D                 DS
     D  FTAvOpFb               1     14
     D    DsTAvd               1      4
     D    DsTOpd               5     11
     D    DsTFbnr             12     14
     D                 DS
     D  HEADK3                 1     30
     D    HEADK3A              1      4
     D    HEADK3B              6     30
     D UPC             C                   CONST('ABCDEFGHIJKLMNOPQRST-
     D                                     UVWXYZÆØÅ')
     D LO              C                   CONST('abcdefghijklmnopqrst-
     D                                     uvwxyzæøå')
      *
      /copy syspeds/qrpgsrcpy,prolog3
      *
      * Get program start time for calculating execution time
     C                   time                    timedata1
      * Parse input / cvt to numeric
     C                   exsr      Parse
      * File open / keys
     C                   EXSR      INIT
      * Read output skeleton html member into memory
     C                   exsr      LoadHtml
      * Set section variables
     C                   exsr      Settop
     C                   callp     wrtsection('top')
      *
      * HOVEDRUTINE
      *
      * Vis liste over registrete leveringer
      *
     C     MaxSnd        IFEQ      *ZEROS
     C                   Z-add     500           MaxSnd
     C                   END
     C     MaxRcd        IFEQ      *ZEROS
     C                   Z-add     2000          MaxRcd
     C                   END
      *
     C                   Move      *zeros        AntSnd
     C                   Move      *zeros        AntRcd
      * FØRST alle kjente (som har avd/oppd) - ukjente/overtallige deretter
     C*                  z-add     1             XXAvd
N03  C                   MOVE      'J'           Kjente            1
     c     LesCLID       tag
N03  c     TERM          ifne      *blanks
     c     DOKUFT1Key    setll     DOKUFT1
S03  c*    DOKUFT1KeyE   Reade     DOKUFT1                                35
N03  c     DOKUFT1Key    Reade     DOKUFT1                                35
N03  c                   else
N03  c     DATON         setll     DOKUFT1
N03  c     DATON         Reade     DOKUFT1                                35
N03  c                   endif
      *
     c     *IN35         doweq     '0'
     C                   Add       1             AntRcd
     C     AntRcd        CabGe     MaxRcd        Utles
S03  c*    XXAvd         ifeq      *zeros
s03  C*    FTavd         Cabgt     0             UtLes
S03  c*                  endif
n03  c     Kjente        ifeq      'J'
N03  C     FTavd         Cabeq     0             NextRec
n03  c                   else
N03  C     FTavd         Cabgt     0             NextRec
n03  c                   endif
      * ved 0 i avd/opd, el samme som forrige -  hopp over head/dokuf-sjekk
     c                   exsr      ClearHeDf
     C     FTOPD         Cabeq     0             NoOpd                          Ukjent/overtallig
     c                   move      FTavd         DsTAvd
     c                   move      FTOpd         DsTOpd
     c                   move      FTfbnr        DsTFbnr
     c     FtAvOpFB      Cabeq     GmAvOpFB      NoOpd                          Samme som forrige
      *
     c     DOKUFKey      chain     DOKUF                              33
     c     HEADFKey      chain     HEADF                              33
      * div filter
     c     AVDN          Ifne      *zeros
     C     AVDN          CabNe     HEAVD         NextRec
     c                   end
      *
     c     TURN          Ifne      *zeros
     C     TURN          CabNe     HEPRO         NextRec
     c                   end
      *
     c     AgentN        Ifne      *zeros
     C     AgentN        CabNe     HEKNA         NextRec
     c                   end
N03  c     KunKuN        Ifne      *zeros
N03  C     KunKuN        CabNe     TRKNFA        NextRec
N03  c                   end
      *
S03  c     hepro         chain     TURER14                            33
     c     TRANSN        Ifne      *zeros
     C     hepro         Cabeq     *zeros        NextRec
S03  c*    hepro         chain     TURER14                            33
     C     *in33         Cabeq     '1'           NextRec
     C                   move      TUKNT2        HEKNT
     c     DFTRAN        ifne      *zeros
     C                   move      DFTRAN        HEKNT
     c                   endif
     C     TRANSN        CabNe     HEKNT         NextRec
     c                   end
N03   * Kun avgåtte= Listet - TUTST1-(tur-konv-status) 1,2,3
N03  c     VisKunLis     ifeq      'Y'
N03  c                   if        TUTST1<'0' or TUTST1>'9'
N03  c                   goto      NextRec
N03  c                   endif
N03  c                   endif
      *
     c     MottPost      ifne      *BLANKS
     C     LO:UPC        XLATE     HEADK3        HEADK3
     C                   eval      TSTSCAN30  = uppify(HEADK3)
     C     MottPost      SCAN      TSTSCAN30                              33
     c     *IN33         CaBEQ     *OFF          NextRec
     C                   end
     c     NOopd         tag
      * Ok - skriv
     C                   exsr      Setrow
     c     AntUkj        ifeq      1
     c     CSV           andne     'Y'
     C                   callp     wrtsection('UkjHead')
     c                   endif
     C                   callp     wrtsection('row')
     C                   Add       1             AntSnd
     C     AntSnd        CabGe     MaxSnd        Utles
      *
     c     NextRec       Tag
S03  c*    DOKUFT1KeyE   Reade     DOKUFT1                                35
N03  c     TERM          ifne      *blanks
N03  c     DOKUFT1Key    Reade     DOKUFT1                                35
N03  c                   else
N03  c     DATON         Reade     DOKUFT1                                35
N03  c                   endif
     c                   enddo
      *
      * kommet gjennom leseloop uten max.. - hvis første runde , vis også Ukjente
s03  c*    XXAvd         ifeq      1
s03  C*                  move      *zeros        XXAvd
n03  c     Kjente        ifeq      'J'
n03  C                   move      'N'           Kjente
     c                   goto      LesCLID
     c                   endif
      *
     C     UTLES         TAG
      * Quit
      * Compute run time
     C                   time                    timedata2
     C     timedata2     subdur    timedata1     ms:*ms
     C                   eval      sec = ms / 1000000
     C                   callp     updhtmlvar('runtime':
     C                             %trim(%editw(sec:'     0 .   ')))
      *
     C                   callp     wrtsection('bot')
      *
     C                   exsr      Exit
      *
     C                   eval      *inlr = *on
     C                   return
      *
      ************************************************
     C     ClearHeDf     Begsr
      ************************************************
     c                   clear                   HXsndn
     c                   clear                   Heavd
     c                   clear                   Heopd
     c                   clear                   Hepro
     c                   clear                   Hekna
     c                   clear                   TRknfa
N03  c                   clear                   Henas
N03  c                   clear                   Herfa
     c                   clear                   Henak
     c                   clear                   Headk3
     c                   clear                   Hent
     c                   clear                   Hevkt
N02  c                   clear                   Hesdl
     c                   clear                   DFavd
     c                   clear                   DFopd
     c                   clear                   DFfbnr
     c                   clear                   DFNt
     c                   clear                   DFvkt
     c                   clear                   DFtran
     c                   endsr
      *
      *=====================================================================
      * Parse input / cvt to numeric
      *=====================================================================
     C     Parse         begsr
      * Parse variables from QUERY_STRING environment variable
     C                   eval      DATO     = zhbgetvar('DATO')
     C                   eval      DATON    = c2n2(DATO)
     C                   eval      MAXSN    = zhbgetvar('MaxSn')
     C                   eval      MaxSnd   = c2n2(MAXSN)
     C                   eval      MAXRC    = zhbgetvar('MaxRc')
     C                   eval      MaxRcd   = c2n2(MAXRC)
     C                   eval      MottPost = zhbgetvar('MottPost')
     C     LO:UPC        XLATE     MottPost      MottPost
     C                   eval      MottPost = uppify(MottPost)
      * Userid.....
     C                   eval      user = zhbgetvar('user')
     C                   eval      TERM = zhbgetvar('TERM')
N03  c     TERM          ifeq      'T_   '
N03  c     TERM          oreq      '*ALL '
N03  c                   move      *blanks       TERM
N03  c                   endif
     C                   eval      AGENT  = zhbgetvar('AGENT')
     C                   eval      AGENTN = c2n2(AGENT)
N03  C                   eval      KUNKU  = zhbgetvar('KUNKU')
N03  C                   eval      KUNKUN = c2n2(KUNKU)
     C                   eval      TUR    = zhbgetvar('TUR')
     C                   eval      TURN   = c2n2(TUR)
     C                   eval      AVD  = zhbgetvar('AVD')
     C                   eval      AVDN  = c2n2(AVD)
     C                   eval      TRANS  = zhbgetvar('TRANS')
     C                   eval      TRANSN = c2n2(TRANS)
     C                   eval      VisKl = zhbgetvar('VisKl')
N03  C                   eval      VisKunLis = zhbgetvar('VisKunLis')
     C                   eval      CSV  = zhbgetvar('CSV')
     C                   endsr
      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   CLOSE     BRIDF
     C                   CLOSE     CUNDL01
     C                   CLOSE     DOKUFT1
     C                   CLOSE     DOKUF
     C                   CLOSE     headF
     C                   CLOSE     TURER14
     C                   CLOSE     TRACKl02
     C                   endsr
      *=====================================================================
      * Read output skeleton html member into memory
      *=====================================================================
     C     LoadHtml      begsr
N02  c     CSV           ifeq      'Y'
N02  C                   callp     gethtml('HTMLSRC':
N02  C                             '*LIBL':
N02  C                             'SYOPTF1S':'/CGITAG/')
N02  c                   else
     C                   callp     gethtml('HTMLSRC':
     C                             '*LIBL':
     C                             'SYOPTF1':'/CGITAG/')
N02  c                   end
     C                   endsr
      *=====================================================================
      *  Set variable data for section /$top
      *=====================================================================
     C     SetTop        begsr
      *
     C                   callp     updHTMLvar('ROWHEAD':RowHead)
     C                   callp     updHTMLvar('LogoImg':LogoImg)
     C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updHTMLvar('USER':USER)
     C                   callp     updHTMLvar('BESK':BIBESK)
     C                   callp     updHtmlVar('KUNDE':
     C                             %trim(%editc(BIKUNR:'Z')))
     C                   callp     updHTMLvar('KNAVN':KNAVN)
      * Søkeparam
     C                   callp     updHTMLvar('DATO':
     C                             %trim(%editw(DATON:'    .  .  ')))
     C                   callp     updHTMLvar('DATU':DATO)                      UNEDITED
N03  c     Term          ifne      *blanks
     C                   callp     updHTMLvar('TERM':TERM)
N03  c                   else
N03  C                   callp     updHTMLvar('TERM':'*ALL')
N03  c                   endif
     c                   callp     updHtmlVar('AVD':
     C                             %trim(%editc(AVDN:'Z')))
     c                   callp     updHtmlVar('TRANS':
     C                             %trim(%editc(TRANSN:'Z')))
     C                   callp     updHtmlVar('AGENT':
     C                             %trim(%editc(AGENTN:'Z')))
N03  C                   callp     updHtmlVar('KUNKU':
N03  C                             %trim(%editc(KUNKUN:'Z')))
     C                   callp     updHtmlVar('TUR':
     C                             %trim(%editc(TURN:'Z')))
     C                   callp     updHTMLvar('MOTTPOST':MOTTPOST)
     C                   callp     updHTMLvar('VISKL':VISKL)
N03  C                   callp     updHTMLvar('VISKunLis':VISKunLis)
     C                   movel     timedata1     time26           26
     C                   eval      csvfile = %trim(USER)+'_'+Time26
     C                   callp     updHTMLvar('CSVFILE':CSVFILE)
      *
     C                   endsr
      *=====================================================================
      *  Set variable data for section /$row
      *=====================================================================
     C     Setrow        begsr
     c     FtAvOpFb      IFne      GmAvOpFb
     c     OddEven       IFEQ      ODD
     c                   eval      OddEven = EVEN
     c                   else
     c                   eval      OddEven = ODD
     c                   end
     c                   end
     C                   callp     updHTMLvar('ODDEVEN':OddEven)
      *
     c                   move      FtAvOpFb      GmAvOpFb         14
      *
     c     FTOPD         ifeq      *zeros
     c                   add       1             AntUkj            9 0
     c                   eval      HXSNDNTXT='<b>** Ukjent/Overtallig **</b>'
     c                   else
     c     HXSNDN        Ifeq      *blanks
     c     HEOPD         andne     *zeros
     c                   MOVE      HEAVD         DsAvd
     c                   MOVE      '/'           DsSlash
     c                   MOVE      HEOPD         DsOpd
     c                   MOVEL     DsAvdOpd      HXSNDN
     c                   endif
     c     HXSNDN        ifeq      *blanks
     c                   eval      HXSNDNTXT =''                                Som forrige
     c                   else
     c                   eval      HXSNDNTXT ='<a href="/sycgip/SYOPI1.pgm?'
     c                             +'user='+%trim(BIBRID)+'&sndnr='
     c                             +%trim(HXSNDN)+'">'+%trim(HXSNDN)+'</a>'
     c                   endif
     c                   endif
N03  C                   callp     updHTMLvar('FELT':FTTERM)
N03  C                   callp     updHTMLvar('HXSNDN':HXSNDN)
     C                   callp     updHTMLvar('HXSNDNTXT':HXSNDNTXT)
     C                   callp     updHTMLvar('HEAVD':
     C                             %trim(%editc(HEAVD:'Z')))
     C                   callp     updHTMLvar('TRKNFA':
     C                             %trim(%editc(TRKNFA:'Z')))
     C                   callp     updHTMLvar('HEPRO':
     C                             %trim(%editc(HEPRO:'Z')))
     C                   callp     updHTMLvar('HEKNA':
     C                             %trim(%editc(HEKNA:'Z')))
N03  C                   callp     updHTMLvar('HENAS':HENAS)
N03  C                   callp     updHTMLvar('HERFA':HERFA)
     C                   callp     updHTMLvar('HENAK':HENAK)
     C                   callp     updHTMLvar('HEADK3A':HEADK3A)
     C                   callp     updHTMLvar('HEADK3B':HEADK3B)
      *
     C                   callp     updHTMLvar('HENT':
     C                             %trim(%editc(HENT:'Z')))
     C                   callp     updHTMLvar('HEVKT':
     C                             %trim(%editc(HEVKT:'Z')))
      *
     C                   callp     updHTMLvar('FTCLID':FTCLID)
     C                   callp     updHTMLvar('KL':
     C                             %trim(%editw(FTTIME4:'  :  ')))
     C                   callp     updHTMLvar('FTUSER':FTUSER)
N02  C                   callp     updHTMLvar('HESDL':HESDL)
     c                   exsr      GetDates
     c     VisKl         ifne      'Y'
     C                   callp     updHTMLvar('TEIDAT':
     C                             %trim(%editw(TEIDAT:'    .  .  ')))
     C                   callp     updHTMLvar('TEUDAT':
     C                             %trim(%editw(TEUDAT:'    .  .  ')))
     C                   callp     updHTMLvar('PODDAT':
     C                             %trim(%editw(PODDAT:'    .  .  ')))
     C                   else
     C                   callp     updHTMLvar('TEIDAT':
     C                             %trim(%editw(TEIDATKL:'    .  .  _  :  ')))
     C                   callp     updHTMLvar('TEUDAT':
     C                             %trim(%editw(TEUDATKL:'    .  .  _  :  ')))
     C                   callp     updHTMLvar('PODDAT':
     C                             %trim(%editw(PODDATKL:'    .  .  _  :  ')))
     C                   end
      *
     C                   endsr
      *
      *
      ***************************************************************************
     C     GetDates      begsr
      ***************************************************************************
     c                   move      *zeros        TEIDAT            8 0
     c                   move      *zeros        TEUDAT            8 0
     c                   move      *zeros        PODDAT            8 0
     c                   move      *zeros        TEITIM            6 0
     c                   move      *zeros        TEUTIM            6 0
     c                   move      *zeros        PODTIM            6 0
      *
     c     HEOPD         cabeq     0             UtDates
      * TEI
     C                   move      'TEI'         TTACTI
     c     TrackKey      Chain     TrackL02                           33
     c     *in33         ifeq      *off
     c                   move      TTDATE        TEIDAT
     c                   move      TTTIME        TEITIM
     c                   end
      *
      *
      * TEU
     C                   move      'TEU'         TTACTI
     c     TrackKey      Chain     TrackL02                           33
     c     *in33         ifeq      *off
     c                   move      TTDATE        TEUDAT
     c                   move      TTTIME        TEUTIM
     c                   end
      *
      *
      * POD
     C                   move      'POD'         TTACTI
     c     TrackKey      Chain     TrackL02                           33
     c     *in33         ifeq      *off
     c                   move      TTDATE        PODDAT
     c                   move      TTTIME        PODTIM
     c                   end
      *
     c     UtDates       endsr
      *
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
     C                   OPEN      BRIDF
     C     USER          CHAIN     BRIDF                              50
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
      *
      * hent Parametre som går igjen i mange prog:
      *      Odd/Even/Rowhead-farger,Logobilde,Språk,Intern/Ekstern bruker,  mm
     c                   call      'ESGETPAR'
     c                   parm                    BIBRID
     c                   parm                    BIFIRM
     c                   parm                    BIKUNR
     c                   parm                    BIKNG
     c                   parm                    RowHead          10
     c                   parm                    Odd              10
     c                   parm                    Even             10
     c                   parm                    LogoImg          50
     c                   parm                    XSPRÅK            2
     c                   parm                    XINTERN           1
     c                   parm                    ParmDS1        1024
     c                   parm                    ParmDS2        1024
     c                   parm                    ParmDS3        1024
      *
     C                   OPEN      CUNDL01
     C                   OPEN      DOKUFT1
     C                   OPEN      DOKUF
     C                   OPEN      headF
     C                   OPEN      TURER14
     C                   OPEN      TRACKl02
     C     DOKUFT1Key    KLIST
     C                   KFLD                    DATON
     C                   KFLD                    Term
S03   * Lesing over flere terminaler -UFtest i stedet for avd med i KEY
S03  C*                  KFLD                    XXavd             4 0
S03  C*    DOKUFT1KeyE   KLIST
S03  C*                  KFLD                    DATON
S03  C*                  KFLD                    Term
     c                   eval      GmAvOpFb='99999999999999'                    Trigge ULIK
      *
     C     DOKUFKey      KLIST
     C                   KFLD                    DFRI
     C                   KFLD                    FTAVD
     C                   KFLD                    FTOPD
     C                   KFLD                    FTFBNR
     C                   move      'F'           DFRI
     C     HEADFKey      KLIST
     C                   KFLD                    FTAVD
     C                   KFLD                    FTOPD
     C     KunKey        KLIST
     C                   KFLD                    BIFIRM
     C                   KFLD                    BIKUNR
     C     TrackKey      KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C                   KFLD                    TTFBNR
     C                   KFLD                    TTACTI
      *
     C     KunKey        CHAIN     Cundl01                            33
      *
     C                   endsr
      *
