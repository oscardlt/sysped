     H DFTACTGRP(*NO)
      *
      ***********************************************************
      *** PROGRAM SKAL KONVERTER LANDKODER FRA TVINN          ***
      ***********************************************************
      *
     FXMLRCAF   IF   E           K DISK
     FXMLRCVF   IF   E           K DISK
     FKODTS2    UF A E           K DISK
      *
     D                sds
     D ZUSER                 254    263
      *  Prototype for syti04r
     d syti04r         pr
     d usn_                                like(usn)
      *  *ENTRY Interface for Main Procedure
     d syti04r         pi
     d usn                            9
      *----------------------------------------------------------------------
      * Stand Alone Fields - TOP
      *----------------------------------------------------------------------
     d xan08           s              8A
     d x1_xrlev1       s                   like(xrlev1)
     d x1_xrlev2       s                   like(xrlev2)
     d x2_xrlev1       s                   like(xrlev1)
     d x2_xrlev2       s                   like(xrlev2)
     d x2_xrlev3       s                   like(xrlev3)
     d x_ks2lk         s                   like(ks2lk)
     d x_ks2ftx        s                   like(ks2ftx)
     d x_ks2nas        s                   like(ks2nas)
     d x_ks2pre        s                   like(ks2pre)
     d x_ks2mo         s                   like(ks2mo)
     D UP              C                   CONST('ABCDEFGHIJKLMNOPQRST-
     D                                     UVWXYZÆØÅ')
     D LO              C                   CONST('abcdefghijklmnopqrst-
     D                                     uvwxyzæøå')

      *----------------------------------------------------------------------
      /free

       exsr init;

       exsr srxml1;

       *inlr = *on;
       return;

       //**********************************************                ------
       begsr srxml1;
       //**********************************************

         X1_XRLEV1 = 1;
         X1_XRLEV2 = 0;

         chain (xrsn:x1_xrlev1:x1_xrlev2) xmlrcvf;
         dow %found;
           select;
           when xrnv = 'versjon';
           when xrnv = 'medlem';
             exsr srxml2;
           endsl;
           x1_xrlev1 = x1_xrlev1 + 1;
           chain (xrsn:x1_xrlev1:x1_xrlev2) xmlrcvf;
         enddo;

         endsr;

       //**********************************************
       begsr srxml2;
       //**********************************************

         x2_xrlev1 = xrlev1;
         x2_xrlev2 = 1;
         x2_xrlev3 = 0;

         chain (xrsn:x2_xrlev1:x2_xrlev2:x2_xrlev3) xmlrcvf;
         dow %found;
           select;
           when xrnv = 'landkode';
             x_ks2lk = xrverd;
           when xrnv = 'landnavn';
             x_ks2ftx = xrverd;
             x_ks2ftx = %xlate(lo:up:x_ks2ftx);
           when xrnv = 'landgrupper';
           endsl;
           x2_xrlev2 = x2_xrlev2 + 1;
           chain (xrsn:x2_xrlev1:x2_xrlev2:x2_xrlev3) xmlrcvf;
         enddo;

         exsr opdland;

       endsr;

       //**********************************************
       begsr opdland;
       //**********************************************

         chain ('S2 ': x_ks2lk) kodts2;
         ks2ftx = x_ks2ftx;
         if %found;
           update kos2r;
         else;
           ks2lk  = x_ks2lk;
           ks2nas = *blanks;
           ks2pre = *blanks;
           ks2mo  = *blanks;
           write kos2r;
         endif;

       endsr;

       //**********************************************
       begsr init;
       //**********************************************

         xrsn = %dec(usn:9:0);

       endsr;

      /end-free
