      *********************************************************************
      *  After compiling this RPG MODULE,
      *  create the related program with the following command:
      *
CRTPG+*  CRTPGM SYSPED/STSL13 MODULE(*LIBL/STSL13
CRTPGM*        *LIBL/INCGI) ACTGRP(*NEW) AUT(*USE)
      *
      *********************************************************************
      *
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
      *--------------------------------------------------------------------
      * Data base files
      *--------------------------------------------------------------------
     FBRIDF     if   e           k disk    usropn
     FLLGRPK05  IF   E           K DISK    USROPN
     FLLAGJL01  IF   E           K DISK    USROPN
     FLLAGJL98  IF   E           K DISK    usropn
     F                                     RENAME(LLAGJR:LLAGJR98)
     F                                     PREFIX(X9_)
     FLLMRF     IF   E           K DISK    USROPN
     FLLSERL01  UF   E           K DISK    usropn
     FLLSERL05  IF   E           K DISK    usropn
     F                                     RENAME(LLSERR:LLSERR05)
      *--------------------------------------------------------------------
      * Prototype defintions and standard system API error structure
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      *
      *--------------------------------------------------------------------
      * Variables common to CGI programs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
      * Variables received from the input string
     Dwssninn          s             35
     DWSLENGDE         s              2
     DWSUSER           s             10
     DWSANR            s             15
     DWSMN             s              7
     DXMN              s              7  0
     D Fn              s             10
     D Lib             s             10
     D Mbr             s             10
     D MsgId           s              7
     D State           ds                  based(StateP)
     D  XMR                 1001   1200  0 DIM(40)
     D UsrSpcName      s             10
     D UsrSpcLib       c                   'SYCGIUSP'
      *--------------------------------------------------------------------
      * Prolog common to CGI programs
      * -Receive input from the remote browser
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prolog3
      *=====================================================================******
      * MAIN PROCESS LINE
      *=====================================================================******
      * Parse input string
     C                   EXSR      PARSE
      ***
      * Set output skeleton html member name
     C                   exsr      SetSkl
      * Read skeleton output html, etc.
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
      * User changed?? - Set libl ..
     C                   exsr      Init
      *
     C                   IF        WSSNINN <> *BLANKS
     C     LLSERL05K     CHAIN     LLSERL05                           33
     C     LLSERKUP      CHAIN     LLSERL01                           33
     C  N33              MOVE      'I'           SNST
     C  N33              UPDATE    LLSERR
     C                   END
      *------------------
      * Write sections of HTML.
      *
      * Her er HOVED logikk-rutine
     C     LLAGJL98K     CHAIN     LLAGJL98                           33
     C     LLSERL01K     SETLL     LLSERL01
     C     LLSERL01K     READE(N)  LLSERL01                               50
     C                   DOW       *IN50 = '0'
     C                   IF        SNMNI = XMN
     C     LLAGJL01K     CHAIN     LLAGJL01                           50
     C     LLMRFK        CHAIN     LLMRF                              50
     C                   LEAVE
     C                   END
     C     LLSERL01K     READE(N)  LLSERL01                               50
     C  N50              ENDDO                                                  *hival
     C                   exsr      SetTop
     C                   callp     wrtsection('top')
     C                   exsr      SetTabStr
     C                   callp     wrtsection('tablestr')
      *
     C                   DOW       *IN50 = '0'
     C                   IF        SNMNI = XMN
     C                   exsr      SetTabRow
     C                   callp     wrtsection('tablerow')
     C                   IF        SNST = ' '
     C                   callp     wrtsection('tablerow1')
     C                   ELSE
     C                   callp     wrtsection('tablerow2')
     C                   END
     C                   callp     wrtsection('tablerow3')
     C                   END
     C     LLSERL01K     READE(N)  LLSERL01                               50
     C  N50              ENDDO                                                  *hival
      *
     C                   callp     wrtsection('tableend')
      *
     C                   callp     wrtsection('endhtml')
     C                   callp     wrtsection('*fini')
      *
      *
     C                   close     BRIDF
     C                   CLOSE     LLAGJL01
     C                   CLOSE     LLAGJL98
     C                   CLOSE     LLMRF
     C                   CLOSE     LLGRPK05
     C                   CLOSE     LLSERL01
     C                   CLOSE     LLSERL05
     C                   eval      *inlr = *on
     C                   RETURN
      *
      *=====================================================================******
      * Parse input
      *=====================================================================******
     C     Parse         begsr
     C                   eval      WSUSER   = zhbgetvar('USER')
     C                   eval      WSLENGDE = zhbgetvar('XLENGDE')
     C                   eval      WSANR    = zhbgetvar('VAANR')
     C                   eval      WSMN     = zhbgetvar('VAMN')
     C                   eval      XMN      = c2n2(WSMN)
     C                   eval      WSSNINN  = zhbgetvar('WSSNINN')
      *
     C                   EVAL      UsrSpcName  = zhbgetvar('UsrSpcName')
     C                   eval      StateP = RtvUsrSpcPtr(UsrSpcName:
     C                             UsrSpcLib)
      *
     C                   endsr
      *=====================================================================
      * EXECUTE LOGIC / Set html output variables for section /$top
      *=====================================================================
     C     SetTop        begsr
      *
      * Repeat UserId for the next invocation
BODY C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updhtmlvar('USER':WSUSER)
     C                   callp     updhtmlvar('XLENGDE':WSLENGDE)
     C                   callp     updhtmlvar('UsrSpcName':UsrSpcName)
     C                   callp     updhtmlvar('VAANR':WSANR)
     C                   callp     updhtmlvar('VAMN':WSMN)
     C                   callp     updhtmlvar('MRLREF':MRLREF)
      *
     C                   endsr
      *
      *
      *=====================================================================******
      *  Different User than last invocation
      *=====================================================================******
     C     Init          begsr
     C                   open      BRIDF
     C     WSUSER        CHAIN     BRIDF                              30
      *
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C* (bedre ressursmessig). MODUL må da være med comp. av dette pgm!!
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
      *
     C                   open      LLAGJL01
     C                   open      LLAGJL98
     C                   open      LLMRF
     C                   open      LLGRPK05
     C                   open      LLSERL01
     C                   open      LLSERL05
      *
     C     LLSERL01K     KLIST
     C                   KFLD                    X9_VAGRUP
     C                   KFLD                    X9_VAKJED
     C                   KFLD                    WSANR
     C     LLSERL05K     KLIST
     C                   KFLD                    WSSNINN
     C     LLSERKUP      KLIST
     C                   KFLD                    SNGRP
     C                   KFLD                    SNPNR
     C                   KFLD                    WSANR
     C                   KFLD                    WSSNINN
     C     LLAGJL01K     KLIST
     C                   KFLD                    X9_VAGRUP
     C                   KFLD                    X9_VAKJED
     C                   KFLD                    SNMNI
     C     LLAGJL98K     KLIST
     C                   KFLD                    XMN
     C     LLMRFK        KLIST
     C                   KFLD                    X9_VAGRUP
     C                   KFLD                    X9_VAKJED
     C                   KFLD                    VAMRNR
      *
     C     BIKUNR        chain     LLGRPK05                           33
      *
     C                   endsr
      *
      *=====================================================================******
      * Set html output skeleton member before its read into memory
      *=====================================================================******
     C     SetSkl        begsr
      *------------------
      * Set html output skeleton member
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC'
     C                   eval      Mbr = 'STSL13'
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$tablestr (table start)
      *=====================================================================******
     C     SetTabStr     begsr
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$tablerow (table row)
      *=====================================================================******
     C     SetTabRow     begsr
      *
     C                   callp     updhtmlvar('USER':WSUSER)
     C                   callp     updhtmlvar('VAANR':WSANR)
     C                   callp     updhtmlvar('VAMN':
     C                             %trim(%editc(XMN:'Z')))
     C                   callp     updhtmlvar('SNSN':SNSN)
      *
     C                   endsr
