             PGM        PARM(&DEVD &TYPE &MODEL)
        /*___________________*/
        /* Beskriv variabler */
        /*"""""""""""""""""""*/
             DCL        VAR(&DEVDESC) TYPE(*CHAR) LEN(132)
             DCL        VAR(&DEVD) TYPE(*CHAR) LEN(10)
             DCL        VAR(&TYPE) TYPE(*CHAR) LEN(5)
             DCL        VAR(&MODEL) TYPE(*CHAR) LEN(5)
             DCL        VAR(&FIELD) TYPE(*CHAR) LEN(5)
             DCL        VAR(&SPLF) TYPE(*CHAR) LEN(10) +
                          VALUE(QPDCDEV) /* ny, 25/7 */
             DCLF       FILE(QTEMP/DEVDESC) RCDFMT(DEVDESC)
        /*___________________*/
        /* Clear av workfile */
        /*"""""""""""""""""""*/
             CRTPF      FILE(QTEMP/DEVDESC) RCDLEN(132) AUT(*ALL)
             MONMSG     MSGID(CPF0000)
        /*______________________*/
        /* Skriv ut beskrivelse */
        /*""""""""""""""""""""""*/
             /**/
      /*     CHKOBJ     OBJ(QSYS/Q5728SS1) OBJTYPE(*DTAARA)   */
      /*     MONMSG     MSGID(CPF9801) EXEC(DO)               */
      /*     CHGVAR     VAR(&SPLF) VALUE(QSYSPRT)             */
      /*     ENDDO                                            */
             /**/
             OVRPRTF    FILE(&SPLF) HOLD(*YES)
             DSPDEVD    DEVD(&DEVD) OUTPUT(*PRINT)
             /**/

        /*___________________*/
        /* Kopiere spoolfile */
        /*"""""""""""""""""""*/
 COPY:       CPYSPLF    FILE(&SPLF) TOFILE(QTEMP/DEVDESC) SPLNBR(*LAST)
             /**/
             MONMSG     MSGID(CPF3303) EXEC(DO)
             CHGVAR     VAR(&SPLF) VALUE(QSYSPRT)
             OVRPRTF    FILE(&SPLF) HOLD(*YES)
             DSPDEVD    DEVD(&DEVD) OUTPUT(*PRINT)
             CPYSPLF    FILE(&SPLF) TOFILE(QTEMP/DEVDESC) SPLNBR(*LAST)
             ENDDO
             /**/
        /*_________*/
        /* Les fil */
        /*"""""""""*/
 READ:       RCVF       RCDFMT(*FILE)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(EXIT))
             CHGVAR     VAR(&FIELD) VALUE(%SUBSTRING(&DEVDESC 41 5))
        /*______*/
        /* Type */
        /*""""""*/
             IF         COND(&FIELD *EQ TYPE) THEN(DO)
             CHGVAR     VAR(&TYPE) VALUE(%SST(&DEVDESC 53 5))
             GOTO       CMDLBL(READ)
             ENDDO
        /*_______*/
        /* Model */
        /*"""""""*/
             IF         COND(&FIELD *EQ MODEL) THEN(DO)
             CHGVAR     VAR(&MODEL) VALUE(%SST(&DEVDESC 53 6))
             GOTO       CMDLBL(EXIT)
             ENDDO
        /*__________________________________________*/
        /* Ikke TYPE eller MODELL, les neste record */
        /*""""""""""""""""""""""""""""""""""""""""""*/
             GOTO       CMDLBL(READ)
        /*______________________*/
        /* Delete av spoolfiler */
        /*""""""""""""""""""""""*/
 EXIT:       DLTSPLF    FILE(&SPLF) SPLNBR(*LAST)
             MONMSG     MSGID(CPF0000)
             ENDPGM
