     FEDI79F1K  IF   E             DISK
     D XSPARTY         C                   CONST('<PartyStructure>')
     D  XSPARTY1       C                   CONST('<PartySet>')
     D  XSPARTY1END    C                   CONST('</PartySet>')
     D  XSPARTY2       C                   CONST('<Party')
     D  XSPARTY3       C                   CONST('<ReferenceNo>')
     D  XSPARTY4       C                   CONST('partyId="')
     D  XSPARTY5       C                   CONST('subClass="')
     D                 DS
     D  RDATA1                 1   1024
     D  RDATA1X              600   1024
     D  RDATA2              1025   2048
     D  RDATA2X             1625   2048
     D  RDATA3                 1   2048
     D  RDATAXX              900   1024
     D  IN                     1   2048    DIM(2048)
     D                 DS
     D  RDATA                  1   1024
     D  RDATAX               700   1024
     D                 DS
     D  RDATA10                1   2048
     D  RDATA10X            1700   2048
     D                 DS
     D  XTXT                   1   1024
     D  XT80                   1   1024    DIM(1024)
     D                 DS
     D  XSTEGN                 1     10
     D  XST10                  1     10    DIM(10)
      *
     C     *ENTRY        PLIST
     C                   PARM      WTYPE         WTYPE             4
      *
     C                   MOVE      *BLANKS       X0004            35
     C                   MOVE      *BLANKS       X0010            35
     C                   BITON     '123457'      XAPOS             1
     C                   BITOFF    '06'          XAPOS
      *
     C                   READ      EDI79F1K                               51
     C                   MOVE      RDATA         RDATA2
     C                   EXSR      LESREC
     C                   EXSR      SRSPARTY
     C     XSLUTTLES     CABEQ     'J'           SLUTT
     C                   EXSR      SRSPARTY
     C                   SELECT
     C                   WHEN      X0010 = '21096601'
     C                   EVAL      WTYPE = '*NDK'
     C                   WHEN      X0010 = 'XXXXXXXX'
     C                   EVAL      WTYPE = '*NSE'
     C                   ENDSL
      *
     C     SLUTT         TAG
     C                   MOVE      '1'           *INLR
     C                   RETURN
      *
      *////////////////////////////////////////////////////////////
      * SR for PartyStructure                            SRSPARTY /
      *////////////////////////////////////////////////////////////
     C     SRSPARTY      BEGSR
      *
     C*                  EXSR      LESREC
      * LES <PartySet>
     C     XSPARTY1      SCAN      RDATA3:WPOS   WPOS              4 0    51
     C                   DOW       *IN51 = *OFF
     C                   EXSR      LESREC
     C     XSLUTTLES     CABEQ     'J'           SLSRSPARTY
     C     XSPARTY1      SCAN      RDATA3:WPOS   WPOS              4 0    51
     C  N51              ENDDO
      *
     C                   IF        WPOS > 1024
     C                   SUB       1024          WPOS
     C                   EXSR      LESREC
     C                   END
      * LES <Party
     C                   ADD       1             WPOS
     C     XSPARTY2      SCAN      RDATA3:WPOS   WPOS              4 0    51
     C                   DOW       *IN51 = *OFF
     C                   EXSR      LESREC
     C     XSLUTTLES     CABEQ     'J'           SLSRSPARTY
     C     XSPARTY2      SCAN      RDATA3:WPOS   WPOS              4 0    51
     C  N51              ENDDO
      *
     C                   IF        WPOS > 1024
     C                   SUB       1024          WPOS
     C                   EXSR      LESREC
     C                   END
      * LES MessageSender
     C                   MOVE      WPOS          XPOS              4 0
     C     XSPARTY5      SCAN      RDATA3:WPOS   WPOS              4 0    51
     C                   ADD       10            WPOS
     C                   EXSR      GETTXT
     C                   MOVEL     XTXT          XAVSMOT          30
      * LES partyId="
     C                   MOVE      XPOS          WPOS
     C     XSPARTY4      SCAN      RDATA3:WPOS   WPOS              4 0    51
     C                   DOW       *IN51 = *OFF
     C                   EXSR      LESREC
     C     XSLUTTLES     CABEQ     'J'           SLSRSPARTY
     C     XSPARTY4      SCAN      RDATA3:WPOS   WPOS              4 0    51
     C  N51              ENDDO
      * LES avsender/mottakers-id
     C                   ADD       9             WPOS
     C                   EXSR      GETTXT
      *
     C                   SELECT
     C                   WHEN      XAVSMOT = 'MessageSender'
     C                   EVAL      X0004 = XTXT
     C                   WHEN      XAVSMOT = 'MessageRecipient'
     C                   EVAL      X0010 = XTXT
     C                   ENDSL
      *
     C     SLSRSPARTY    ENDSR
      *
      *////////////////////////////////////////////////////////////
      * SR for HENTING AV POSTER                           LESREC /
      *////////////////////////////////////////////////////////////
     C     LESREC        BEGSR
      *
     C                   IF        ((RDATA2 = *BLANKS) AND
     C                              (XSLUTTLES <> *BLANKS))
     C                   MOVE      'J'           XSLUTTLES         1
     C                   ELSE
     C                   READ      EDI79F1K                               50
     C  N50              DOW       RDATA = *BLANKS
     C                   READ      EDI79F1K                               50
     C  N50              ENDDO
     C                   MOVE      RDATA2        RDATA1
     C     STLESREC1     TAG
     C                   IF        *IN50 = *OFF
     C                   MOVE      RDATA         RDATA2
      *
     C  N50              IF        RDATAX = *BLANKS
     C                   EVAL      RDATA2  = *BLANKS
     C                   END
     C  N50              DOW       RDATAX = *BLANKS
     C                   EVAL      RDATA10 = RDATA3
     C                   CAT       RDATA:1       RDATA10
     C                   EVAL      RDATA3  = RDATA10
     C                   IF        RDATA10X <> *BLANKS
     C                   LEAVE
     C                   END
     C                   READ      EDI79F1K                               50
     C  N50              DOW       RDATA = *BLANKS
     C                   READ      EDI79F1K                               50
     C  N50              ENDDO
     C   50              MOVE      *BLANKS       RDATA
     C  N50              ENDDO
      *
     C                   ELSE
     C                   MOVE      *BLANKS       RDATA2
     C                   MOVE      'S'           XSLUTTLES         1
     C                   END
     C                   END
     C                   IF        WPOS = 0
     C                   Z-ADD     1             WPOS
     C                   END
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      * SR for Hent tekst                                  GETTXT /
      *////////////////////////////////////////////////////////////
     C     GETTXT        BEGSR
      *
     C                   MOVE      *BLANKS       XTXT
     C                   MOVE      *ZEROS        XN                4 0
     C                   DOW       ((IN(WPOS) <> '<') AND (IN(WPOS) <> '"'))
     C                   IF        IN(WPOS) = '&'
     C                   EXSR      KVTTEGN
     C                   END
     C                   ADD       1             XN
     C                   MOVE      IN(WPOS)      XT80(XN)
     C                   ADD       1             WPOS
     C                   ENDDO
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      * SR for Konvertering av spesiale tegn              KVTTEGN /
      *////////////////////////////////////////////////////////////
     C     KVTTEGN       BEGSR
      *
     C                   MOVE      *BLANKS       XSTEGN
     C                   MOVE      *ZEROS        XN2               3 0
     C                   DOW       IN(WPOS) <> ';'
     C                   ADD       1             XN2
     C                   MOVE      IN(WPOS)      XST10(XN2)
     C                   ADD       1             WPOS
     C                   ENDDO
      *
     C                   SELECT
     C                   WHEN      XSTEGN = '&amp'
     C                   MOVE      '&'           IN(WPOS)
     C                   WHEN      XSTEGN = '&quot'
     C                   MOVE      '"'           IN(WPOS)
     C                   WHEN      XSTEGN = '&apos'
     C                   MOVE      XAPOS         IN(WPOS)
     C                   WHEN      XSTEGN = '&gt'
     C                   MOVE      '>'           IN(WPOS)
     C                   WHEN      XSTEGN = '&lt'
     C                   MOVE      '>'           IN(WPOS)
     C                   ENDSL
      *
     C                   ENDSR
      *
