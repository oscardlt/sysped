      *********************************************************************
      *  Henter inntil 2 bilder knyttet til kolli-ID  fra Cargoscan bildearkiv
      *  Ren Rambergspesial (eller rettere sagt benytte kun av Ramberg - KUN DE har cargoscan)
      *********************************************************************
CRTPG+*  CRTPGM SYSPED/WSBILNK MODULE(*LIBL/WSBILNK
CRTPGM*         *LIBL/INCGI) ACTGRP(*NEW) AUT(*USE)
      *
      *********************************************************************
      * MAIN PROGRAM FLOW
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
      *--------------------------------------------------------------------
     FBRIDF     IF   E           K DISK    USROPN
     FHEADF     IF   E           K DISK    usropn
     FDOKUFM1   IF   E           K DISK    usropn
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
     D WSUSER          S             10
     D XMLstring       S          32000
     D KLLID           s             20A
     D GetFrmCS        S              1
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prolog3
      *--------------------------------------------------------------------
      *
      * Parse input
     C                   exsr      Parse
      * Initiering
     C                   EXSR      INIT
      *
      * Hent XML output-dok/ skriv top + linjer
      *
     c                   callp     gethtmlifs(
     C                             '/syspeddev/html/WS_XML.html':
     C                             '/CGITAG/')
      *
     C                   callp     wrtsection('top')
      *
      /free
        XMLstring='<?xml version="1.0" encoding="UTF-8" standalone="yes"?>';
        updhtmlvar2('XMLstring':%addr(XMLstring):%len(XMLstring));
        wrtsection('XMLlin');
        XMLstring='<getBiLinks>';
        updhtmlvar2('XMLstring':%addr(XMLstring):%len(XMLstring));
        wrtsection('XMLlin');
       //
        exsr CrtBody;
       //
        XMLstring='</getBiLinks>';
        updhtmlvar2('XMLstring':%addr(XMLstring):%len(XMLstring));
        wrtsection('XMLlin');
      /end-free
      *
     C     SLUTT         TAG
     C                   EXSR      EXIT
     C                   return
      ******************************************************************************
     C     Parse         begsr
      ******************************************************************************
      * Parse input   Bruker er fast NN + sndnr
      *
     C                   eval      WSUSER  = zhbgetvar('USER')
     c     WSUSER        ifeq      *blanks
     C                   eval      WSUSER  = 'NN'
     c                   endif
     C                   eval      KLLID   = zhbgetvar('KLLID')
     C                   eval      GetFrmCS= zhbgetvar('GetFrmCS')
     C                   endsr
      ******************************************************************************
     C     INIT          begsr
      ******************************************************************************
      *
      *  O hente fram bildenen fra Cargoscan server er nødvendig kun ved første kall innen ett oppdr
     C                   IF        GetFrmCS = 'Y'
     C                   OPEN      BRIDF
     C     WSUSER        CHAIN     BRIDF                              50
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
      *
     C                   OPEN      DOKUFM1
     C                   OPEN      HEADF
      * HENT OVER EVT BILDER FRA CARGOSCAN BILDESERVER
     c                   eval      FMMRK1 = %subst(KLLID:3:18)
     c     FMMRK1        chain     DOKUFM1                            33
     c                   if        *in33 = *off
     C                   move      FMAVD         AVD               4
     C                   move      FMOPD         OPD               7
     c                   call      'CARGOIMGC'
     C                   PARM                    AVD
     C                   PARM                    OPD
     C                   endif
     C                   CLOSE     DOKUFM1
     C                   CLOSE     HEADF
     C                   endif
     C                   endsr
      *******************************************************************************
     C     EXIT          begsr
      *******************************************************************************
      *  AVSLUTT
     C                   callp     wrtsection('*fini')
     C                   eval      *inlr = *on
      *
     C                   endsr
      * **************************************************
     C     CrtBody       begsr
      * **************************************************
     c                   move      *blanks       IP               50
     c                   eval      IP='http://ftp.ramberg.no:65432'
     c                   move      *blanks       BILINKA         512
     c                   move      *blanks       BILINK1         512
     c                   move      *blanks       BILINK2         512
      * hent bane til bilde
      * finnes Bilde 1
     C                   eval      ubane='/syspeddev/clid/'+KLLID
     c                             +'-0.jpg'
     c                   CALL      'BHRURLCL'
     c                   Parm                    ubane           256
     c                   Parm                    Bilde1            1
     c                   if        Bilde1 = 'J'
     c                   eval      BiLink1= %trim(IP)+ubane
     c                   eval      BiLinkA='<a href="' + %trim(IP)
     c                             + %trim(ubane) + '" '
     c                             + 'Target="ImgWindow">Bilde1</a>'
      * finnes Bilde 2  (sjekkes kun dersom bilde 1 finnes )
     C                   eval      ubane='/syspeddev/clid/'+KLLID
     c                             +'-1.jpg'
     c                   CALL      'BHRURLCL'
     c                   Parm                    ubane           256
     c                   Parm                    Bilde2            1
     c                   if        Bilde2 = 'J'
     c                   eval      BiLink2= %trim(IP)+ubane
     c                   eval      BiLinkA= %trim(BiLinkA) + ' '
     c                             +'<a href="' + %trim(IP)
     c                             + %trim(ubane) + '" '
     c                             + 'Target="ImgWindow">Bilde2</a>'
     c                   endif
     c                   endif
      *
      /free
        XMLstring='  <biLinka>'+%trim(BILINKA)+'</biLinka>';
        updhtmlvar2('XMLstring':%addr(XMLstring):%len(XMLstring));
        wrtsection('XMLlin');
        XMLstring='  <biLink1>'+%trim(BILINK1)+'</biLink1>';
        updhtmlvar2('XMLstring':%addr(XMLstring):%len(XMLstring));
        wrtsection('XMLlin');
        XMLstring='  <biLink2>'+%trim(BILINK2)+'</biLink2>';
        updhtmlvar2('XMLstring':%addr(XMLstring):%len(XMLstring));
        wrtsection('XMLlin');
      /end-free
     C                   endsr
      *
