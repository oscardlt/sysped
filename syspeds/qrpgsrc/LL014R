     FKODTFM    IF   E           K DISK
     FCUNDL01   IF   E           K DISK
     FLLHEAD02  UF   E           K DISK
     FLLLINE    IF   E           K DISK
     FLLAGJL01  IF   E           K DISK
     FLLAGJD02  IF   E           K DISK
     FLLSERL02  IF   E           K DISK
     FLL014P    O    E             PRINTER OFLIND(*IN01)
     F                                     USROPN
     D                 DS
     D  XTIME                  1     14  0
     D  XTID                   1      4  0
     D  XDD                    7      8  0
     D  XMM                    9     10  0
     D  XÅÅ                   11     14  0
     D  XIDAG                  7     14  0
     D                 DS
     D  XXSENR                 1     25
     D  XXSEN2                 1     21
      *
     C                   EXSR      INIT
     C                   OPEN      LL014P
     C                   EXSR      HOVED
     C                   CLOSE     LL014P
     C                   IF        UEPOST <> *BLANKS
     C                   EXSR      SNDEPOST
     C                   END
     C                   MOVE      '1'           *INLR
      *
      *
      *////////////////////////////////////////////////////////////
     C     INIT          BEGSR
      *////////////////////////////////////////////////////////////
     C     *ENTRY        PLIST
     C                   PARM                    UFIRM             2
     C                   PARM                    UALN              7
     C                   PARM                    UKOPI             1
     C                   PARM                    UNR2              1
     C                   PARM                    UTOT2             1
     C     LASER         PARM      LASER         LASER             1
     C     STRING        PARM      STRING        STRING            5
     C                   PARM                    UEPOST           70
     C                   PARM                    UQUSER           10
     C                   PARM                    UQJOB            10
     C                   PARM                    UQNBR             6
     C                   MOVE      UNR2          UNR
     C                   MOVE      UTOT2         UTOT
     C     UTOT          COMP      1                                  45
     C     STRING        IFNE      *BLANKS
     C     STRING        CHAIN     KODTFM                             33
     C                   END
     C     LASER         IFEQ      'A'
     C                   SETON                                        88
     C                   END
      *__________________
      * Definiere Nøkkler
      *""""""""""""""""""
     C     CUND1K        KLIST
     C                   KFLD                    UFIRM
     C                   KFLD                    LHPNR
     C     LLAGDK        KLIST
     C                   KFLD                    LHGRP
     C                   KFLD                    LHPNR
     C                   KFLD                    LLMN
     C                   KFLD                    LLVN
     C     LLAGLK        KLIST
     C                   KFLD                    LHGRP
     C                   KFLD                    LHPNR
     C                   KFLD                    VDMN
     C     LLSERK        KLIST
     C                   KFLD                    LLMN
     C                   KFLD                    LLVN
      *
     C                   TIME                    XTIME
     C                   MOVE      UALN          LHALN
     C     UKOPI         COMP      'J'                                    46
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
     C     HOVED         BEGSR
      *////////////////////////////////////////////////////////////
     C     LHALN         SETLL     LLHEAD02
     C     LHALN         READE     LLHEAD02                               33
     C     *IN33         CABEQ     '1'           SLHOV
     C                   MOVE      'P'           LHST3
     C                   UPDATE    LLHEADR
     C                   MOVE      *BLANKS       XHBA3
     C     LHBPU         IFEQ      *BLANKS
     C                   MOVEL     LHBPN         XHBA3
     C                   ELSE
     C                   MOVEL     LHBPU         XHBA3
     C                   END
     C                   CAT       LHBA3:1       XHBA3
      *
     C     LHMN          SETLL     LLLINE
     C     LHMN          READE     LLLINE                                 33
     C     *IN33         CABEQ     '1'           SLHOV
      *
     C                   MOVE      '1'           *IN01
     C     CUND1K        CHAIN     CUNDL01                            34
      *
     C     *IN33         DOWEQ     '0'
     C     LLANT0        IFEQ      0
     C                   MOVE      LLWNT0        LLANT0
     C                   END
     C     LLANR         COMP      XANR                                   41
     C                   MOVE      *ZEROS        XXLANT
     C                   MOVE      *ZEROS        XXSAE0
     C                   MOVE      *ZEROS        XXSAE1
     C                   MOVE      *ZEROS        XXSAE2
     C                   MOVE      *ZEROS        XXSAE3
     C                   MOVE      *ZEROS        XXSENR
     C     LLWNT0        IFEQ      0
     C                   MOVE      VASENR        XXSENR
     C                   ELSE
     C     LLAGDK        SETLL     LLAGJD02
     C     LLAGDK        READE     LLAGJD02                               34
     C     *IN34         DOWEQ     '0'
      *
     C                   ADD       VDLANT        TLWNT0
     C                   ADD       VDSAE0        TLWNTL
     C                   ADD       VDSAE1        TLWNT1
     C                   ADD       VDSAE2        TLWNT2
     C                   ADD       VDSAE3        TLWNT3
      *
     C     LLAGLK        CHAIN     LLAGJL01                           35
     C   35              MOVE      *BLANKS       VASENR
     C     XXSENR        IFEQ      *ZEROS
     C                   MOVE      VASENR        XXSENR
     C                   END
     C     VASENR        IFEQ      XXSENR
     C                   ADD       VDLANT        XXLANT
     C                   ADD       VDSAE0        XXSAE0
     C                   ADD       VDSAE1        XXSAE1
     C                   ADD       VDSAE2        XXSAE2
     C                   ADD       VDSAE3        XXSAE3
     C                   ELSE
     C   01              WRITE     LL014P1
     C   01              MOVE      '0'           *IN01
     C                   WRITE     LL014P2
     C                   Z-ADD     VDLANT        XXLANT
     C                   Z-ADD     VDSAE0        XXSAE0
     C                   Z-ADD     VDSAE1        XXSAE1
     C                   Z-ADD     VDSAE2        XXSAE2
     C                   Z-ADD     VDSAE3        XXSAE3
     C                   MOVE      VASENR        XXSENR
     C                   END
      *
     C     LLAGDK        READE     LLAGJD02                               34
     C  N34              ENDDO
     C                   END
      *
     C     XXSENR        IFNE      *ZEROS
     C   01              WRITE     LL014P1
     C   01              MOVE      '0'           *IN01
     C                   WRITE     LL014P2
     C                   END
      *
     C                   MOVE      '0'           *IN42
     C     LLSERK        SETLL     LLSERL02
     C     LLSERK        READE     LLSERL02                               34
     C     *IN34         DOWEQ     '0'
     C   01              WRITE     LL014P1
     C   01              MOVE      '0'           *IN01
     C                   WRITE     LL014P4
     C                   MOVE      '1'           *IN42
     C     LLSERK        READE     LLSERL02                               34
     C  N34              ENDDO
      *
     C                   MOVE      LLANR         XANR             15
     C     LHMN          READE     LLLINE                                 33
     C  N33              ENDDO
      *
     C   01              WRITE     LL014P1
     C   01              MOVE      '0'           *IN01
     C                   WRITE     LL014P3
      *
     C     SLHOV         ENDSR
      *
      ***********************************************
     C     SNDEPOST      BEGSR
      ***********************************************
      *
     C                   MOVE      '*CURRENT'    PTIME             8
     C                   MOVE      '*CURRENT'    PDATE             8
     C                   MOVE      'L'           UUKODE            1
     C                   MOVEL     'PAKKSEDDEL'  DOKNAV           12
     C                   MOVE      *ZEROS        FAXAVD            4
     C                   MOVE      *ZEROS        FAXOPD            7
     C                   MOVE      *ZEROS        FAXTUR            8
     C                   MOVEL     'LL014P  '    FILEN            10
     C                   MOVE      'N'           COVP              1
      *
     C                   EVAL      UEMNE = 'PAKKSEDDEL:'
     C                   CAT       UALN:1        UEMNE
     C                   CALL      'LL014C22'
     C                   PARM                    COVP
     C                   PARM                    UQUSER
     C                   PARM                    UQJOB
     C                   PARM                    UQNBR
     C                   PARM                    FILEN
     C                   PARM                    PTIME
     C                   PARM                    PDATE
     C                   PARM                    COV01            40
     C                   PARM                    COV02            40
     C                   PARM                    COV03            40
     C                   PARM                    COV04            40
     C                   PARM                    COV05            40
     C                   PARM                    COV06            40
     C                   PARM                    COV07            40
     C                   PARM                    COV08            40
     C                   PARM                    UEPOST           70
     C                   PARM                    UEMNE            40
      *
     C                   MOVEL     UQUSER        MAPPE            20
     C                   CALL      'FAX013'
     C                   PARM                    UQUSER
     C                   PARM                    UQNBR
     C                   PARM                    UQJOB
     C                   PARM                    FAXNRP           21
     C                   PARM                    DOKNAV
     C                   PARM                    MAPPE
     C                   PARM                    PTIME
     C                   PARM                    PDATE
     C                   PARM                    LHBNA
     C                   PARM                    FAXAVD
     C                   PARM                    FAXOPD
     C                   PARM                    FAXTUR
     C                   PARM                    UUKODE
      *
     C                   ENDSR
      *
