********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 YBC PTF10 KALL FRA SHIP-SYSTEM
10XXX * 02 SH  PTF10 endrer til kode 'NY'
10XXX * 03 JOV PTF10 Nye parametre SYIN10B
********************************************************************
      *
      *   SPØRRING PRISER
      *
N01  FGSS052F2  IF   E           K DISK
     FHEDUMMY   IF   E           K DISK    USROPN
     F                                     RENAME(HEADFR:HEADum)
     FKOFAST    IF   E           K DISK
     FKUFAST    IF   E           K DISK
     F                                     RENAME(KOFASTR:KUFASTR)
     FSI01D     CF   E             WORKSTN
     F                                     INFDS(FBAREA)
     FSIF       IF   E           K DISK
     FKODTA     IF   E           K DISK
     FKODTV     IF   E           K DISK
     FKODTFR    IF   E           K DISK
     FKODTOT2   IF   E           K DISK
     FSTED2D    IF A E           K DISK
     FSTED2     IF   E           K DISK
     F                                     RENAME(STED2R:STED2R2)
     FSTEDIL01  IF   E           K DISK
     FHEADF     UF A E           K DISK
     FFAK2      UF   E           K DISK
     FFIRM      IF   E           K DISK
     FTELL      UF A E           K DISK
     FCUNDL01   IF   E           K DISK
     D HØ              S              3  2 DIM(12)
     D LE              S              3  2 DIM(12)
     D BR              S              3  2 DIM(12)
     D AN              S              3  0 DIM(12)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D                 DS
     D  HEDTRÅ                 1      4  0
     D  HEDTRM                 5      6  0
     D  HEDTRD                 7      8  0
     D  HEDTR                  1      8  0
     D                 DS
     D  HEDTÅ                  1      4  0
     D  HEDTM                  5      6  0
     D  HEDTD                  7      8  0
     D  HEDTOP                 1      8  0
     D                UDS
     D  UFIRM                  1      2
     D  UAVD                  15     18
     D  USG                   19     21
     D  UOPD                  22     28
N01  D                SDS
N01  D  UUSER                254    263
      *//////////////
      *  Hodelogikk /
      *//////////////
     C     *LIKE         DEFINE    ZEAVD         AVDZER
     C                   EXSR      SRA
     C                   EXSR      SRB
     C                   MOVE      '1'           *INLR
      *////////////////////////////////////////////////////////////
      *  F4=Søk for bilde-A                                  PMTA /
      *////////////////////////////////////////////////////////////
     C     PMTA          BEGSR
      *__________
      * Søk Kunde
      *""""""""""
     C     ZLIN          IFEQ      6
     C                   MOVE      *BLANKS       USØKKN           30
     C                   CALL      'SOEKKUN'     PLISTE
     C                   END
     C     ZLIN          IFEQ      7
     C                   MOVEL     'HEUR'        UTYP
     C                   CALL      'SYFAKDA'
     C                   PARM                    UTYP             10
     C                   PARM                    VEDL              1
     C     RETURK        PARM      RETURK        RETURK            5
     C                   END
     C                   MOVEL     RETURK        ZEUR
     C     UYEAR         IFGT      50
     C                   MOVE      '19'          UCENT             2 0
     C                   ELSE
     C                   MOVE      '20'          UCENT
     C                   END
     C                   MOVE      *ZEROS        HEDTR
     C                   MOVE      *ZEROS        HEDTOP
      *
     C     PMTA9         ENDSR
      *////////////////////////////////////////////////////////////
      *  F4=Søk for bilde-B                                  PMTB /
      *////////////////////////////////////////////////////////////
     C     PMTB          BEGSR
      *_________________
      * Søk Tjenestetype
      *"""""""""""""""""
      *_____________
      * Søk Fra sted
      *"""""""""""""
     C     ZLIN          IFEQ      8
     C     KOAPOS        IFNE      'J'
     C                   CALL      'SYFI10F'
     C     ZESDF         PARM      ZESDF         ZESDF
     C                   PARM                    ZEPNFR           30
     C                   PARM                    ZELKS             2
     C                   ELSE
     C                   CALL      'SYFI10F'
     C     ZESDFF        PARM      ZESDFF        ZESDFF
     C                   PARM                    ZEPNFR           30
     C                   PARM                    ZELKS             2
     C                   END
     C                   GOTO      PMTB9
     C                   END
      *_____________
      * Søk Til sted
      *"""""""""""""
     C     ZLIN          IFEQ      9
     C     KOAPOS        IFNE      'J'
     C                   CALL      'SYFI10F'
     C     ZESDT         PARM      ZESDT         ZESDT
     C                   PARM                    ZEPNTI           30
     C                   PARM                    ZELKK             2
     C                   ELSE
     C                   CALL      'SYFI10F'
     C     ZESDVT        PARM      ZESDVT        ZESDVT
     C                   PARM                    ZEPNTI           30
     C                   PARM                    ZELKK             2
     C                   END
     C                   GOTO      PMTB9
     C                   END
      *_________________
      * Søk Oppdragstype
      *"""""""""""""""""
     C     ZLIN          IFEQ      11
     C                   MOVE      '*S'          ZEOT
     C                   CALL      'SYFI10C'
     C     ZEOT          PARM      ZEOT          ZEOT
     C                   GOTO      PMTB9
     C                   END
      *______________
      * Søk Frankatur
      *""""""""""""""
     C     ZLIN          IFEQ      12
     C                   MOVE      '*SE'         ZEFR
     C                   MOVE      'J'           USPED             1
     C                   CALL      'SYFI10G'
     C     ZEFR          PARM      ZEFR          ZEFR
     C                   PARM                    USPED
     C                   GOTO      PMTB9
     C                   END
      *______________________
      * Søk Produktkode
      *""""""""""""""""""""""
     C     ZLIN          IFEQ      14
     C                   MOVEL     'PRODTYPE'    XUTYP
     C                   MOVEL     ZEKDPL        XUKOD
     C                   CALL      'SYKUKD'
     C                   PARM                    XUTYP
     C                   PARM                    UUPDAT
     C                   PARM                    XUKOD
     C                   MOVEL     XUKOD         ZEKDPL
     C                   END
      *______________________
      * Søk Forfrakt fra sted
      *""""""""""""""""""""""
     C     KOAPOS        IFNE      'J'
     C     ZLIN          IFEQ      20
     C                   CALL      'SYFI10F'
     C     ZESDFF        PARM      ZESDFF        ZESDFF
     C                   PARM                    ZEPNTI           30
     C                   PARM                    ZELKS             2
     C                   GOTO      PMTB9
     C                   END
     C                   END
      *________________________
      * Søk Etterfrakt til sted
      *""""""""""""""""""""""""
     C     KOAPOS        IFNE      'J'
     C     ZLIN          IFEQ      21
     C                   CALL      'SYFI10F'
     C     ZESDVT        PARM      ZESDVT        ZESDVT
     C                   PARM                    ZEPNTI           30
     C                   PARM                    ZELKK             2
     C                   GOTO      PMTB9
     C                   END
     C                   END
     C     PMTB9         ENDSR
      *////////////////////////////////////////////////////////////
      *  Initiering                                           SRA /
      *////////////////////////////////////////////////////////////
     C     SRA           BEGSR
      *___________________
      * Definiere Workfelt
      *"""""""""""""""""""
     C                   MOVE      'A'           WAUNI             1
     C                   MOVE      'V'           WVUNI             1
     C                   MOVE      'O2Y'         WO2UNI            3
     C     *LIKE         DEFINE    FASK          WASK
     C                   Z-ADD     0             WSOPD             7 0
     C                   MOVE      'NY'          WNYGML            2
     C                   MOVE      *BLANKS       WSMELD            1
     C                   Z-ADD     0             UFLAGG            1 0
     C                   Z-ADD     0             UKFRA             7 0
     C                   MOVE      'FR'          WFRUNI            2
     C     *LIKE         DEFINE    HELKA         WELKAF                         LANDK FRA STED
     C     *LIKE         DEFINE    HELKA         WELKAT                         LANDK TIL STED
      *_________________
      * Output paramtrer
      *"""""""""""""""""
     C     PLISTC        PLIST
     C                   PARM                    FIFIRM                         FIRMAKODE
     C                   PARM                    ZEAVD                          AVDELING
     C                   PARM                    WSOPD                          OPDNR
     C                   PARM                    WNYGML                         NY=N GML=G
     C                   PARM                    KO2UTL            1            UTL PROV
     C                   PARM                    XKUTL             4 2          UTLP SATS
     C                   PARM                    XSUTL             4 2          UTLP SATS
     C                   PARM                    XKUMIN            2 0          UTLP MIN
     C                   PARM                    XSUMIN            2 0          UTLP MIN
     C                   PARM                    KO2MVK            1            MVA KJØPER
     C                   PARM                    KO2MVS            1            MVA SELGER
     C                   PARM                    UKFRA                          =0
     C                   PARM                    KOVAVR            1            OVERST.AVRUN
     C                   PARM                    WSMSG            62            DS
     C                   PARM                    WSMELD                         =*BLANK
     C                   PARM                    UFLAGG                         =0
     C                   PARM                    UKODE             1
     C     USECUR        PARM      USECUR        USECUR            6            =*SECUR
      *
     C     PLISTE        PLIST                                                  KUNDF
     C                   PARM                    ZEKNKF
     C                   PARM                    FIFIRM
     C                   PARM                    USØKKN           30
      *________________
      * Input paramtrer
      *""""""""""""""""
     C     *ENTRY        PLIST
     C                   PARM                    WUSR             10
      *__________________
      * Definiere Nøkkler
      *""""""""""""""""""
     C     KEYA          KLIST                                                  SIF
     C                   KFLD                    WUSR                           SIF
      *
     C     KEYB          KLIST                                                  CUNDL01
     C                   KFLD                    FIFIRM                         CUNDL01
     C                   KFLD                    ZEKNKF                         CUNDL01
      *
     C     KEYC          KLIST                                                  KODTA
     C                   KFLD                    WAUNI                          KODTA
     C                   KFLD                    ZEAVD                          KODTA
      *
     C     KEYD          KLIST                                                  HEADF
     C                   KFLD                    ZEAVD                          HEADF
     C                   KFLD                    WSOPD                          HEADF
      *
     C     KEYE          KLIST                                                  KODTV
     C                   KFLD                    WVUNI                          KODTV
     C                   KFLD                    ZEAVD                          KODTV
      *
     C     KEYF          KLIST                                                  FAK2
     C                   KFLD                    ZEAVD                          FAK2
     C                   KFLD                    WSOPD                          FAK2
     C                   KFLD                    WASK                           FAK2
      *
     C     KEYG          KLIST                                                  KODTOT2
     C                   KFLD                    WO2UNI                         KODTOT2
     C                   KFLD                    ZEOT                           KODTOT2
      *
     C     KEYH          KLIST
     C                   KFLD                    WFRUNI
     C                   KFLD                    ZEFR
     C* STED 1
     C     KEYS1         KLIST
     C                   KFLD                    ZELKS
     C                   KFLD                    ZESDF
     C     KEYS1G        KLIST
     C                   KFLD                    ZESDF
     C* STED 2
     C     KEYS2         KLIST
     C                   KFLD                    ZELKK
     C                   KFLD                    ZESDT
     C     KEYS2G        KLIST
     C                   KFLD                    ZESDT
     C* STED 3
     C     KEYS3         KLIST
     C                   KFLD                    ZELKS
     C                   KFLD                    ZESDFF
     C     KEYS3G        KLIST
     C                   KFLD                    ZESDFF
     C* STED 4
     C     KEYS4         KLIST
     C                   KFLD                    ZELKK
     C                   KFLD                    ZESDVT
     C     KEYS4G        KLIST
     C                   KFLD                    ZESDVT
      *
     C     KEYDUM        KLIST                                                  HEADF
     C                   KFLD                    ZEAVD                          HEADF
     C                   KFLD                    WSOPD0            7 0          HEADF
      *
      *_____________
      * Init av felt
      *"""""""""""""
      *________________
      * Hente Firmakode
      *""""""""""""""""
     C                   MOVE      *BLANKS       FIFIRM
     C     FIFIRM        SETLL     FIRM
     C                   SETOFF                                       51
     C                   READ      FIRM                                   51
     C                   MOVE      FIFIRM        UFIRM
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Spørring.                                            SRB /
      *////////////////////////////////////////////////////////////
     C     SRB           BEGSR
      *_________________________
      * Intern /Ekstern bruker ?
      *"""""""""""""""""""""""""
     C     KEYA          CHAIN     SIF                                51
      * Ekstern bruker
     C     *IN51         IFEQ      '0'
     C                   MOVE      IAVD          ZEAVD
     C                   MOVE      IKN           ZEKNKF
     C                   MOVE      IEUR          ZEUR
     C                   END
     C                   Z-ADD     5             ZLIN
     C                   Z-ADD     41            ZPOS
N01   *
N01  C     UUSER         CHAIN     GSS052F2                           33
N01  C                   IF        *IN33 = *OFF
N01  C                   EVAL      ZEKNKF = G52KN
N01  C                   EVAL      ZELKS  = G52LK
N01  C                   EVAL      ZESDF  = G52LOK
N01  C                   EVAL      ZENT   = WSCLI
N01  C                   EVAL      ZEVKT  = WSWT
N01  C                   EVAL      ZEM3   = WSVOL
N01  C                   END
      *_______________________________________
      * Bilde-A, vises kun for interne brukere
      *"""""""""""""""""""""""""""""""""""""""
     C     SRB1          TAG
     C     *IN51         IFEQ      '1'
     C  N30              MOVE      'SY00000'     ZMSGNO
     C   12              Z-ADD     5             ZLIN
     C   12              Z-ADD     41            ZPOS
     C                   WRITE     SI01DØ
     C                   EXFMT     SI01DA
     C                   MOVEA     '000'         *IN(30)
      * Funksjonstaster
     C     *IN03         CABEQ     '1'           SRB9
     C     *IN12         CABEQ     '1'           SRB9
      * Konvertere Cursorlocation
     C     WSCLOC        DIV       256           ZLIN
     C                   MVR                     ZPOS
     C                   Z-ADD     WSSUBN        XRECNO            4 0
      * Søk
     C     *IN04         IFEQ      '1'
     C                   EXSR      PMTA
     C                   GOTO      SRB1
     C                   END
      *
     C                   END
      * Hente avdeling
     C     KEYC          CHAIN     KODTA                              30
     C     *IN30         IFEQ      '1'
     C     *IN51         ANDEQ     '1'
     C                   MOVE      '1'           *IN31
     C                   MOVE      'SPA0001'     ZMSGNO
     C                   Z-ADD     5             ZLIN
     C                   Z-ADD     41            ZPOS
     C                   GOTO      SRB1
     C                   END
      * Hente avdeling
     C     KEYE          CHAIN     KODTV                              30
      * Hente kunde
     C     KEYB          CHAIN     CUNDL01                            30
     C     *IN30         IFEQ      '1'
     C     *IN51         ANDEQ     '1'
     C                   MOVE      '1'           *IN32
     C                   MOVE      'SPK0001'     ZMSGNO
     C                   Z-ADD     6             ZLIN
     C                   Z-ADD     41            ZPOS
     C                   GOTO      SRB1
     C                   END
      * Teste Tjenestetype(Ursprungskode)
     C*
     C     KEYDEL        KLIST
     C                   KFLD                    UTYP
     C                   KFLD                    FDELSY            5
     C                   MOVEL     ZEUR          FDELSY
     C                   MOVEL     'HEUR  '      UTYP
     C     KEYDEL        CHAIN     KOFAST                             31
     C     *IN31         IFEQ      '1'
     C     *IN51         ANDEQ     '1'
     C                   MOVE      '1'           *IN(30)
     C                   MOVE      'SPU0009'     ZMSGNO
     C                   Z-ADD     7             ZLIN
     C                   Z-ADD     41            ZPOS
     C                   GOTO      SRB1
     C                   END
     C*
     C  N30SYUTLP        IFEQ      0
     C                   MOVE      FIUPS         XKUTL
     C                   MOVE      FIUPS         XSUTL
     C                   MOVE      FIUPM         XKUMIN
     C                   MOVE      FIUPM         XSUMIN
     C                   ELSE
     C                   Z-ADD     SYUTLP        XKUTL
     C                   Z-ADD     SYUTLP        XSUTL
     C                   Z-ADD     SYMINU        XKUMIN
     C                   Z-ADD     SYMINU        XSUMIN
     C                   END
     C  N30VALKOD        IFEQ      *BLANKS
     C                   MOVE      FICURR        WSVALK            3
     C                   ELSE
     C                   MOVE      VALKOD        WSVALK
     C                   END
      *__________________________   _______________________
      * Bilde-B, vises for både interne og eksterne brukere
      *""""""""""""""""""""""""""""""""""""""""""""""""""""
     C                   MOVE      '1'           *IN61
     C                   Z-ADD     8             ZLIN
     C                   Z-ADD     41            ZPOS
      *
     C     SRB2          TAG
      *
      * hent dummy
      *
     c     zeur          ifeq      'A'
     c     zeur          oreq      'B'
     c                   exsr      dumsr
     c                   end
      *
     C  N30              MOVE      'SY00000'     ZMSGNO
     C                   WRITE     SI01DØ
     C* POSTALSYSTEM???
     C     KOAPOS        IFNE      'J'
     C                   EXFMT     SI01DB
     C                   ELSE
     C                   EXFMT     SI01DBP
     C                   END
     C                   MOVEA     '00000000'    *IN(30)
      *________________
      * Funksjonstaster
      *""""""""""""""""
     C* BEREGN KUBIKK
     C   08              DO
     C                   CALL      'SYIN10B'
n03  C                   PARM                    UUavd             4 0
n03  C                   PARM                    UUopd             7 0
n03  C                   PARM                    UUfbn             3 0
     C                   PARM                    XFLYTM            1
     C     WSM3          PARM      WSM3          WSM3              7 3
     C                   PARM                    HØ
     C                   PARM                    LE
     C                   PARM                    BR
     C                   PARM                    AN
     C                   Z-ADD     WSM3          ZEM3
     C                   GOTO      SRB2
     C                   END
      * Avslutt
     C     *IN03         CABEQ     '1'           SRB9
      * Forrige
     C   51*IN12         CABEQ     '1'           SRB1
     C  N51*IN12         CABEQ     '1'           SRB9
      * Flere parametere
     C     *IN10         IFEQ      '1'
     C                   MOVE      '0'           *IN61
     C                   GOTO      SRB2
     C                   END
      * Konvertere Cursorlocation
     C     WSCLOC        DIV       256           ZLIN
     C                   MVR                     ZPOS
     C                   Z-ADD     WSSUBN        XRECNO            4 0
      * Søk
     C     *IN04         IFEQ      '1'
     C                   EXSR      PMTB
     C                   GOTO      SRB2
     C                   END
      * Teste Input
      * POSTAL
     C     KOAPOS        IFEQ      'J'
     C     ZESDFF        IFEQ      *BLANKS
     C     ZEPNFR        ANDNE     *BLANKS
     C                   CALL      'SYFI10F'
     C     ZESDFF        PARM      ZESDFF        ZESDFF
     C                   PARM                    ZEPNFR           30
     C                   PARM                    ZELKS             2
     C                   END
     C     ZESDVT        IFEQ      *BLANKS
     C     ZEPNTI        ANDNE     *BLANKS
     C                   CALL      'SYFI10F'
     C     ZESDVT        PARM      ZESDVT        ZESDVT
     C                   PARM                    ZEPNTI           30
     C                   PARM                    ZELKK             2
     C                   END
     C                   EXSR      POSTAL
     C   30              GOTO      SRB2
     C                   END
      *
     C                   EXSR      SRBB
     C   30              GOTO      SRB2
      * Faktura
     C                   EXSR      SRBC
     C                   GOTO      SRB2
      *
     C     SRB9          ENDSR
      *////////////////////////////////////////////////////////////
      *  Teste input fra bilde-B                             SRBB /
      *////////////////////////////////////////////////////////////
     C     SRBB          BEGSR
      * Teste Import eller Eksport
     C                   MOVE      '0'           *IN71                          0=Import
     C     ZEUR          IFEQ      'B'
     C     ZEUR          OREQ      'D'
     C                   MOVE      '1'           *IN71                          1=Eksport
     C                   END
      * Teste Frasted
     C     ZESDF         COMP      *BLANKS                                30
     C  N30KOAPOS        IFEQ      'J'
     C     KEYS1         CHAIN     STED2D                             30
     C                   ELSE
     C     KEYS1G        CHAIN     STED2                              30
     C                   END
     C     *IN30         IFEQ      '1'
     C                   MOVE      '1'           *IN32
     C                   MOVE      'SPS0012'     ZMSGNO
     C                   Z-ADD     8             ZLIN
     C                   Z-ADD     41            ZPOS
     C                   GOTO      SRBB9
     C                   END
     C                   MOVE      ST2LK         WELKAF
      * Teste tilsted
     C     ZESDT         COMP      *BLANKS                                30
     C  N30KOAPOS        IFEQ      'J'
     C     KEYS2         CHAIN     STED2D                             30
     C                   ELSE
     C     KEYS2G        CHAIN     STED2                              30
     C                   END
     C     *IN30         IFEQ      '1'
     C                   MOVE      '1'           *IN33
     C                   MOVE      'SPS0013'     ZMSGNO
     C                   Z-ADD     9             ZLIN
     C                   Z-ADD     41            ZPOS
     C                   GOTO      SRBB9
     C                   END
     C                   MOVE      ST2LK         WELKAT
      * Teste oppdragstype
     C     ZEOT          COMP      *BLANKS                                30
     C  N30KEYG          CHAIN     KODTOT2                            30
     C     *IN30         IFEQ      '1'
     C                   MOVE      '1'           *IN34
     C                   MOVE      'SPO0007'     ZMSGNO
     C                   Z-ADD     11            ZLIN
     C                   Z-ADD     41            ZPOS
     C                   GOTO      SRBB9
     C                   END
      * Teste Frankatur
     C     ZEFR          COMP      *BLANKS                                30
     C  N30KEYH          CHAIN     KODTFR                             30
     C     *IN30         IFEQ      '1'
     C                   MOVE      '1'           *IN35
     C                   MOVE      'SPF0006'     ZMSGNO
     C                   Z-ADD     12            ZLIN
     C                   Z-ADD     41            ZPOS
     C                   GOTO      SRBB9
     C                   END
     C     KUFASK        KLIST
     C                   KFLD                    XUTYP
     C                   KFLD                    XUKOD
     C                   MOVE      '0'           *in39
     C     ZEKDPL        IFNE      *blanks
     C                   MOVEL     'PRODTYPE'    XUTYP
     C     *LIKE         DEFINE    KFTYP         XUTYP
     C     *LIKE         DEFINE    KFKOD         XUKOD
     C                   MOVE      'N'           UUPDAT            1
     C                   MOVEL     ZEKDPL        XUKOD
     C     KUFASK        CHAIN     KUFAST                             39
     C  N30*IN39         IFEQ      '1'
     C                   Z-ADD     14            ZLIN
     C                   Z-ADD     41            ZPOS
     C                   MOVE      'YBC0360'     ZMSGNO
     C                   SETON                                        30
     C                   GOTO      SRBB9
     C                   END
     C                   END
      * Teste forfrakt fra
     C     *IN61         IFEQ      '0'
      *
     C     ZESDFF        IFNE      *BLANKS
     C     KOAPOS        IFEQ      'J'
     C     KEYS3         CHAIN     STED2D                             30
     C                   ELSE
     C     KEYS3G        CHAIN     STED2                              30
     C                   END
     C     *IN30         IFEQ      '1'
     C                   MOVE      '1'           *IN37
     C                   MOVE      'SPS0016'     ZMSGNO
     C                   Z-ADD     20            ZLIN
     C                   Z-ADD     41            ZPOS
     C                   GOTO      SRBB9
     C                   END
     C                   END
      * Teste viderefrakt til
     C     ZESDVT        IFNE      *BLANKS
     C     KOAPOS        IFEQ      'J'
     C     KEYS4         CHAIN     STED2D                             30
     C                   ELSE
     C     KEYS4G        CHAIN     STED2                              30
     C                   END
     C     *IN30         IFEQ      '1'
     C                   MOVE      '1'           *IN38
     C                   MOVE      'SPS0017'     ZMSGNO
     C                   Z-ADD     21            ZLIN
     C                   Z-ADD     41            ZPOS
     C                   GOTO      SRBB9
     C                   END
     C                   END
      *
     C                   END
      *
     C     SRBB9         ENDSR
      *////////////////////////////////////////////////////////////
      *  Beregn Faktura                                      SRBC /
      *////////////////////////////////////////////////////////////
     C     SRBC          BEGSR
      * Add av record til HEADF.
     C     ZEAVD         CHAIN     TELL                               52
     C   52AVDZER        CHAIN     TELL                               52
     C     *IN52         IFEQ      '0'
     C                   MOVE      TEOPDN        WSOPD
     C                   ADD       1             TEOPDN
     C                   UPDATE    TELLR
     C                   ELSE
     C                   Z-ADD     ZEAVD         TEAVD
     C                   Z-ADD     1             WSOPD
     C                   Z-ADD     1             TEOPDN
     C                   WRITE     TELLR
     C                   END
     C     KEYD          CHAIN     HEADF                              52
     C  N71              MOVE      WELKAF        HELKA
     C   71              MOVE      WELKAT        HELKA
     C     ZSK           IFNE      ' '
     C     ZSK           IFEQ      'S'
     C                   MOVE      '1'           *IN71
     C                   ELSE
     C                   MOVE      '0'           *IN71
     C                   END
     C                   END
     C                   MOVE      ZEAVD         HEAVD
     C                   MOVE      WSOPD         HEOPD
     C                   MOVE      ZESDFF        HESDFF
     C                   MOVE      ZELKS         HELKS
     C                   MOVE      ZELKK         HELKK
     C                   MOVE      ZEUR          HEUR
     C                   MOVE      ZEKDPL        HEKDPL
     C                   MOVE      WUSR          HESG
     C                   MOVE      *ZEROS        HESTD
     C                   MOVE      *ZEROS        HENTVS
     C                   MOVE      *ZEROS        HEPRO
     C                   MOVE      ZESDVT        HESDVT
     C                   MOVE      UDAY          HEDTD
     C                   MOVE      UMONTH        HEDTM
     C     UCENT         MULT      100           HEDTÅ
     C                   MOVE      UYEAR         HEDTÅ
     C                   MOVE      ZESDF         HESDF
     C                   MOVE      ZESDT         HESDT
     C                   MOVE      *ZEROS        HEKNA
      *_________Selger________*                   N71=Kjøper  71=Selger
     C  N71              MOVE      *ZEROS        HEKNS
     C   71              MOVE      ZEKNKF        HEKNS
     C  N71              MOVE      'X'           HEKDFS
     C   71              MOVE      ' '           HEKDFS
     C  N71              MOVE      '   '         HEVALS
     C   71              MOVE      WSVALK        HEVALS
     C  N71              MOVE      *ZEROS        HEKNSF
     C   71              MOVE      ZEKNKF        HEKNSF
     C  N71              MOVE      *ZEROS        HEANS
     C   71              MOVE      ZEANK         HEANS
     C  N71              MOVE      *BLANKS       HENASF
     C   71              MOVEL     KNAVN         HENASF
      *_________Selger________*
      *_________Kjøper________*
     C  N71              MOVE      ZEKNKF        HEKNK
     C   71              MOVE      *ZEROS        HEKNK
     C  N71              MOVE      ' '           HEKDFK
     C   71              MOVE      'X'           HEKDFK
     C  N71              MOVE      WSVALK        HEVALK
     C   71              MOVE      '   '         HEVALK
     C  N71              MOVE      ZEKNKF        HEKNKF
     C   71              MOVE      *ZEROS        HEKNKF
     C  N71              MOVE      ZEANK         HEANK
     C   71              MOVE      *ZEROS        HEANK
     C  N71              MOVEL     KNAVN         HENAKF
     C   71              MOVE      *BLANKS       HENAKF
      *_________Kjøper________*
     C                   MOVE      ZEOT          HEOT
     C                   MOVE      *BLANKS       HEPOS1
     C                   MOVE      ZEFR          HEFR
     C                   MOVE      ZENT          HENT
     C                   MOVE      ZEVKT         HEVKT
     C                   MOVE      ZELM          HELM
     C                   MOVE      ZEM3          HEM3
     C                   MOVE      *ZEROS        HEDTMP
     C                   MOVE      *ZEROS        HEDTÅP
     C                   MOVE      *ZEROS        HEDTCP
     C                   MOVE      *ZEROS        HEKNT
     C                   MOVE      *ZEROS        HEBELT
     C                   MOVE      *ZEROS        HEBELM
     C                   MOVE      *ZEROS        HEVN
     C                   MOVE      *ZEROS        HETRM
     C                   MOVE      *ZEROS        HETRC
     C*
     C     HELM          MULT      KOVLKG        XFBV1             5 0
     C     HEM3          MULT      KOVKKG        XFBV2             5 0
     C     HEVKT         IFGE      XFBV1
     C                   Z-ADD     HEVKT         HEFBV
     C                   ELSE
     C                   Z-ADD     XFBV1         HEFBV
     C                   END
     C     HEFBV         IFLT      XFBV2
     C                   Z-ADD     XFBV2         HEFBV
     C                   END
     C*
     C     UCENT         MULT      100           HEDTRÅ
     C                   MOVE      UYEAR         HEDTRÅ
     C                   MOVE      UMONTH        HEDTRM
     C                   MOVE      UDAY          HEDTRD
     C                   MOVE      *ZEROS        HEFBK
     C                   MOVE      *ZEROS        HEFNGK
     C                   MOVE      *ZEROS        HEFNGS
     C                   MOVE      *BLANKS       HEPOS1
     C     HEVALT        IFEQ      *BLANKS
     C     ZEUR          IFEQ      'A'
     C     ZEUR          OREQ      'C'
     C     ZEUR          OREQ      'F'
     C     HEKNKF        IFNE      *ZEROS
     C     HEKDFK        ANDNE     'X'                                          VED IMPORT..
     C                   MOVE      HEKNKF        UKNOP                          PRIM. KJØPER
     C                   ELSE
     C                   MOVE      HEKNSF        UKNOP                          SEK. SELGER
     C                   END
     C                   ELSE
     C     HEKNSF        IFNE      *ZEROS
     C     HEKDFS        ANDNE     'X'                                          VED EKSPORT
     C                   MOVE      HEKNSF        UKNOP                          PRIM. SELGER
     C                   ELSE
     C                   MOVE      HEKNKF        UKNOP                          SEK. KJØPER
     C                   END
     C                   END
     C                   MOVE      *ZEROS        UVALP
     C                   CALL      'SYFA24B'
     C                   PARM      ZEUR          UVUR              1
     C                   PARM      ZEAVD         UAVDOP            4 0
     C                   PARM                    UKNOP             8 0
     C                   PARM                    HEVALT
     C                   PARM                    UVALP             3 1
     C                   MOVE      UVALP         HEVALP
     C                   END
     C                   MOVE      ZENTF         HENTF
     C                   WRITE     HEADFR
     C                   MOVE      FIFIRM        UFIRM
     C                   MOVE      ZEAVD         UAVD
     C                   MOVE      ISG           USG
     C                   MOVE      WSOPD         UOPD
     C  N71              MOVE      'K'           UKODE
     C   71              MOVE      'S'           UKODE
      * Kall på eksternt program.
     C                   Z-ADD     0             UFLAGG
     C                   MOVE      '*SECUR'      USECUR
N02  C                   MOVE      'NY'          WNYGML            2
     C                   CALL      'SYFI10X'     PLISTC
      * Slette Oppdrag
     C     KEYD          CHAIN     HEADF                              52
     C  N52              DELETE    HEADFR
      *
     C  N71              MOVE      'K'           WASK
     C   71              MOVE      'S'           WASK
     C                   MOVE      '0'           *IN52
     C     KEYF          CHAIN     FAK2                               52
     C     *IN52         DOWEQ     '0'
     C                   DELETE    FAKTR
     C     KEYF          READE     FAK2                                   52
     C  N52              END
      *
     C                   ENDSR
     C*
     C*****************************************************
     C     POSTAL        BEGSR
     C*****************************************************
     C*
     C*
     C*
     C*** TEST FORFRAKT FRA
     C     STFF          TAG
     C     KEYS3         CHAIN     STED2D                             37
     C  N37              MOVE      ST2NVN        ZEPNFR
     C*AUTOMATISK OPPDATERING
     C   37              DO
     C     KEYS3         SETLL     STEDIL01
     C     ZELKS         READE     STEDIL01                               37
     C   37
     CANN30              MOVE      'SHU0120'     ZMSGNO
     C*
     C  N37STINPF        IFLE      ZESDFF
     C*
     C     ZEPNFR        IFNE      *BLANKS
     C                   CLEAR                   STED2R
     C                   MOVE      ZELKS         ST2LK
     C                   MOVE      ZEPNFR        ST2NVN
     C                   MOVE      ZESDFF        ST2KOD
     C                   MOVE      STINBA        ST2KO2
     C                   WRITE     STED2R
     C                   GOTO      STFF
     C                   ELSE
     C                   SETON                                        37
     C   37
     CANN30              MOVE      'SHU0118'     ZMSGNO
     C                   END
     C                   ELSE
     C                   SETON                                        37
     C   37
     CANN30              MOVE      'SHU0120'     ZMSGNO
     C                   END
     C*
     C                   END
      *
     C   37              SETON                                        37
      *
     C  N37              MOVE      ST2KO2        ZESDF
     C  N37              MOVE      ST2LK         WELKAF
      *
      *** TEST VIDEREFRAKT TIL
      *
     C     STVT          TAG
     C     KEYS4         CHAIN     STED2D                             38
     C  N38              MOVE      ST2NVN        ZEPNTI
      *AUTOMATISK OPPDATERING
     C   38              DO
     C     KEYS4         SETLL     STEDIL01
     C     ZELKK         READE     STEDIL01                               38
     C   38
     CANN30              MOVE      'SHU0120'     ZMSGNO
     C  N38STINPF        IFLE      ZESDVT
     C     ZEPNTI        IFNE      *BLANKS
     C                   CLEAR                   STED2R
     C                   MOVE      ZELKK         ST2LK
     C                   MOVE      ZEPNTI        ST2NVN
     C                   MOVE      ZESDVT        ST2KOD
     C                   MOVE      STINBA        ST2KO2
     C                   WRITE     STED2R
     C                   GOTO      STVT
     C                   ELSE
     C                   SETON                                        38
     C   38
     CANN30              MOVE      'SHU0119'     ZMSGNO
     C                   END
     C                   ELSE
     C                   SETON                                        38
     C   38
     CANN30              MOVE      'SHU0120'     ZMSGNO
     C                   END
     C                   END
      *
     C   38              SETON                                        30
     C  N38              MOVE      ST2KO2        ZESDT
     C  N38              MOVE      ST2LK         WELKAT
      *
     C                   ENDSR
     C*
     C*
     C*****************************************************
     C     DUMSR         BEGSR
     C*****************************************************
     C*
     C                   MOVEL     'UR'          komp              3
     C                   Move      ZEUR          komp
     C                   CALL      'SI01DCL'
     c                   parm                    komp
     c                   open      hedummy
     c     keydum        chain     hedummy                            33
     C                   close     hedummy
     c                   endsr
