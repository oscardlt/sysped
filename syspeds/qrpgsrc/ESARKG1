      *********************************************************************
      *
      *
CRTPG+*  CRTPGM SYSPED/ESARKG1 MODULE(*LIBL/ESARKG1 *LIBL/INCGI)
CRTPGM*         ACTGRP(CGI) AUT(*USE)
      *
ODDEV *ODDEV   PTF9 ODDEVEN-logikk
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 JOV PTF10 skipp poster som er loggført på oppdrag
xxxxxx*xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
11XXX * 02 YBC PTF11 VIS SISTE PDF
********************************************************************
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FCUNDL01   IF   E           K DISK    USROPN
     Farkivl04  IF   E           K DISK    usropn
     Farkivl05  IF   E           K DISK    usropn
     F                                     RENAME(ARKIVR:ARKI05R)
     FFIRMARC   IF   E           K DISK    usropn
     Fturer14   IF   E           K DISK    usropn
     FWRKCGI    UF A E           K DISK    usropn
N02  FWRKCGISL  UF   E           K DISK    usropn
N02  F                                     RENAME(WRKCGIR:WRKCGIRSL)
N02  F                                     PREFIX(SL_)
      *********************************************************************
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      /copy syspeds/qrpgsrcpy,variables3
      *
OddEvD OddEven         s             10
      * Client input variables
     D user            s             10
     D DATO            s             10
      *
     D FraDT8          s              8  0
     D ANTSND          s              5  0
     D MaxSN           s              5
     D MaxSND          s              5  0
     D ANTRCD          s              5  0
     D MaxRC           s              5
     D MaxRCD          s              5  0
     D TURNR           s              8
     D TURNRN          s              8  0
     D TURNRA          s              8
     D TraN            s              8
     D TraNN           s              8  0
     D ArGod           s             40
     D Spaces          s             12a   inz('&nbsp;&nbsp;')
     D ItemCount       s             10i 0
     D i               s             10i 0
     D                 DS
     D  TXDATO                 1      8  0
     D  TXDATOD                1      2  0
     D  TXDATOM                3      4  0
     D  TXDATOÅ                5      8  0
     D                 DS
     D  XXDATO                 1      8  0
     D  XXDATOD                1      2  0
     D  XXDATOM                3      4  0
     D  XXDATOÅ                5      8  0
     D                 DS
     D  DATON                  1      6  0
     D  DATOND                 1      2  0
     D  DATONM                 3      4  0
     D  DATONÅ                 5      6  0
     D                 DS
     D  Wdata                  1    220
     D  ARSTAT                 1      1
     D  ARTYPE                 2      3
     D  ARFIRM                 4      5
     D  ARAVD                  6      9  0
     D  AROPD                 10     16  0
     D  ARUNDE                17     26
     D  ARTUR                 17     24
     D  ARVERSJ               25     25
     D  ARUSER                27     36
     D  ARDATE                37     44  0
     D  ARdtÅ                 37     40  0
     D  ARdtM                 41     42  0
     D  ARdtD                 43     44  0
     D  ARTIME                45     50  0
     D  ARKUND                51     58  0
     D  ARLINK                59     98
      * Data fra turer for å slippe å chaine på ny...
     D  TUKNT2               181    188  0
     D  TUDT                 191    198  0
     D  TUdtÅ                191    194  0
     D  TUdtM                195    196  0
     D  TUdtD                197    198  0
     D  TUSONF               201    205
     D  TUSTEF               206    210
     D  TUSONT               211    215
     D  TUSTET               216    220
      *
      /copy syspeds/qrpgsrcpy,prolog3
      *
      * Get program start time for calculating execution time
     C                   time                    timedata1
      * Parse input / cvt to numeric
     C                   exsr      Parse
      * File open / keys
     C                   EXSR      INIT
      * Read output skeleton html member into memory
     C                   exsr      LoadHtml
      * Set section variables
     C                   exsr      Settop
     C                   callp     wrtsection('top')
      *
      * HOVEDRUTINE
      *
      * Vis liste over arkivposter leveringer
     C                   Move      *zeros        AntSnd
     C                   Move      *zeros        AntRcd
      * Først vanlige single fakturaer
     c                   Move      'go'          ARTYPE
     c     StrLoop1      tag
     c                   Move      FraDt8        ARDATE
     c                   Move      *blanks       ARUNDE
     C     TURNRN        Ifne      *zeros
     c                   MoveL     TURNRN        ARUNDE
     c                   end
      *
     C                   select
     C     TURNRN        Whenne    *zeros
     c                   Move      TURNRN        TURNRA

     C     FakKey        SETLL     ARKIVL04
     c     FakKeyE       Reade     ARKIVL04                               35
     C  N35ARTUR         COMP      TURNRA                             3535
     c                   other
     c     FraKey        setLL     ARKIVL05
     c     FrakeyE       ReadE     ARKIVL05                               35
     c                   endsl
      *
     c     *IN35         doweq     '0'
     C                   Add       1             AntRcd
     C     AntRcd        CabGe     MaxRcd        Utles
      * diverse filter legges her..
N01   * skipp poster som er spredd på oppdrag
N01  c     AROPD         cabne     *zeros        NextRec
     C                   move      artur         ArturN            8 0
     C     ArturN        Chain     TURER14                            33
     C     *in33         IFEQ      '0'
     C     TRANN         ANDNE     *ZEROS
     C     TRANN         CABne     TUKNT2        NextRec
     c                   end
      *
      * Ok - skriv  TIL WRKFIL
     C                   exsr      SetWrk
      *
     C                   Add       1             AntSnd
     C     AntSnd        CabGe     MaxSnd        Utles
      *
     c     NextRec       Tag
     C                   select
     C     TURNRN        Whenne    *zeros
S02  C*    FakKey        SETGT     ARKIVL04
     c     FakKeyE       Reade     ARKIVL04                               35
     C  N35ARTUR         COMP      TURNRA                             3535
     c                   other
S02  c*    FraKey        setGT     ARKIVL05
     c     FrakeyE       ReadE     ARKIVL05                               35
     c                   endsl
     c                   end
      *
      *
     C     UTLES         TAG
      * Vis liste over basert på Wfil
     c     WINTEG        setLL     WRKCGI
     c     WINTEG        ReadE     WRKCGI                                 35
     c     *IN35         doweq     '0'
     C                   exsr      Setrow
     C                   callp     wrtsection('row')
     c                   delete    wrkcgir
     c     WINTEG        ReadE     WRKCGI                                 35
     c                   end
      *
     C                   callp     updHTMLvar('ANTSND':
     C                             %trim(%editc(ANTSND:'3')))
     C                   callp     updHTMLvar('MaxSnd':
     C                             %trim(%editc(MaxSnd:'3')))
     C                   callp     updHTMLvar('ANTRCD':
     C                             %trim(%editc(ANTRCD:'3')))
     C                   callp     updHTMLvar('MaxRcd':
     C                             %trim(%editc(MaxRcd:'3')))
      * Quit
      * Compute run time
     C                   time                    timedata2
     C     timedata2     subdur    timedata1     ms:*ms
     C                   eval      sec = ms / 1000000
     C                   callp     updhtmlvar('runtime':
     C                             %trim(%editw(sec:'     0 .   ')))
      *
     C                   callp     wrtsection('bot')
      *
     C                   exsr      Exit
      *
     C                   eval      *inlr = *on
     C                   return
      *
      *
      *=====================================================================
      * Parse input / cvt to numeric
      *=====================================================================
     C     Parse         begsr
      * Parse variables from QUERY_STRING environment variable
     C                   eval      DATO     = zhbgetvar('DATO')
     C                   eval      DATON    = c2n2(DATO)
     C                   eval      TURNR    = zhbgetvar('TURNR')
     C                   eval      TURNRN   = c2n2(TURNR)
     C                   eval      TRAN     = zhbgetvar('TRAN')
     C                   eval      TRANN    = c2n2(TRAN)
     C                   eval      MAXSN    = zhbgetvar('MaxSn')
     C                   eval      MaxSnd   = c2n2(MAXSN)
     C                   eval      MAXRC    = zhbgetvar('MaxRc')
     C                   eval      MaxRcd   = c2n2(MAXRC)
     c     MaxSnd        ifeq      *zeros
     c                   z-add     100           MaxSnd
     c                   end
     c     MaxRcd        ifeq      *zeros
     c                   z-add     1000          MaxRcd
     c                   end
      * Userid.....
     C                   eval      user = zhbgetvar('user')
      * LÅNER ARDTOP for datokonvertering
     C     2000          Add       DATONÅ        ARDtÅ
     C                   move      DATONM        ARDtM
     C                   move      DATOND        ARDtD
     C                   move      ARDate        FraDt8
     C                   endsr
      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
      * Fjern poster i WFil
     c     WINTEG        setLL     WRKCGI
     c     WINTEG        ReadE     WRKCGI                                 35
     c     *IN35         doweq     '0'
     C                   delete    WRKCGIR
     c     WINTEG        ReadE     WRKCGI                                 35
     c                   end
      *
     C                   CLOSE     BRIDF
     C                   CLOSE     cundl01
     C                   CLOSE     arkivl04
     C                   CLOSE     arkivl05
     C                   CLOSE     firmarc
     C                   CLOSE     turer14
     C                   CLOSE     WRKCGI
N02  C                   CLOSE     WRKCGISL
     C                   endsr
      *=====================================================================
      * Read output skeleton html member into memory
      *=====================================================================
     C     LoadHtml      begsr
     C                   callp     gethtml('HTMLSRC':
     C                             '*LIBL':
     C                             'ESARKG1':'/CGITAG/')
     C                   endsr
      *=====================================================================
      *  Set variable data for section /$top
      *=====================================================================
     C     SetTop        begsr
      *
OddEvC                   callp     updHTMLvar('ROWHEAD':RowHead)
OddEvC                   callp     updHTMLvar('LogoImg':LogoImg)
BODY C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updHTMLvar('USER':USER)
     C                   callp     updHTMLvar('BESK':BIBESK)
     C                   callp     updHtmlVar('KUNDE':
     C                             %trim(%editc(BIKUNR:'Z')))
     C                   callp     updHTMLvar('KNAVN':KNAVN)
      * Søkeparam
     C                   callp     updHTMLvar('DATO':
     C                             %trim(%editw(DATON:'  .  .  ')))
     C                   callp     updHTMLvar('TURNR':TURNR)
     C                   callp     updHtmlVar('TRAN':
     C                             %trim(%editc(TRANN:'Z')))
      *
     C                   endsr
      *=====================================================================
      *  Set variable data for Wfile WRKCGI
      *=====================================================================
     C     SetWrk        begsr
     c                   eval      wkey2   = ARUNDE
N02  C     WRKCGISLK     CHAIN     WRKCGISL                           33
N02  C  N33              DELETE    WRKCGIRSL
N02  C  N33              SUB       1             AntRcd
     c                   write     WRKCGIR
      *
     C                   endsr
      *=====================================================================
      *  Set variable data for section /$row
      *=====================================================================
     C     Setrow        begsr
OddEvc     OddEven       IFEQ      ODD
OddEvc                   eval      OddEven = EVEN
OddEvc                   else
OddEvc                   eval      OddEven = ODD
OddEvc                   end
OddEvC                   callp     updHTMLvar('ODDEVEN':OddEven)
OddEv *
N01  c                   if        ARVersj>='0' and ARVersj<='9'
     c                   eval      ARGod = ARTUR+' Versj:'+ARVersj
N01  c                   else
N01  c                   eval      ARGod = ARTUR
N01  c                   endif
     C                   callp     updHTMLvar('ARGOD':ARGOD)
     C                   callp     updHtmlVar('TUTRAN':
     C                             %trim(%editc(TUKNT2:'Z')))
     C                   move      TUdtÅ         TXDATOÅ
     C                   move      TUdtM         TXDATOM
     C                   move      TUdtD         TXDATOD
     C                   callp     updHTMLvar('TXDATO':
     C                             %trim(%editw(TXDATO:'  .  .    ')))
     c                   MOVEL     TUSONF        FRA               8
     c                   MOVE      '      '      FRA
     c                   MOVE      TUSTEF        FRA
     c                   MOVEL     TUSONT        TIL               8
     c                   MOVE      '      '      TIL
     c                   MOVE      TUSTET        TIL
     c                   MOVE      *BLANKS       FRATIL           30
     c                   eval      FRATIL = FRA + ' - ' +TIL
     C                   callp     updHTMLvar('FRATIL':FRATIL)
     C                   move      ARdtÅ         XXDATOÅ
     C                   move      ARdtM         XXDATOM
     C                   move      ARdtD         XXDATOD
     C                   callp     updHTMLvar('XXDATO':
     C                             %trim(%editw(XXDATO:'  .  .    ')))
     C                   exsr      LagPDFURL
     C                   callp     updHTMLvar('UPDF':UPDF)
      *
     C                   endsr
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
     C                   OPEN      BRIDF
     C     USER          CHAIN     BRIDF                              50
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
      *
OddEv * hent Parametre som går igjen i mange prog:
OddEv *      Odd/Even/Rowhead-farger,Logobilde,Språk,Intern/Ekstern bruker,  mm
OddEvc                   call      'ESGETPAR'
OddEvc                   parm                    BIBRID
OddEvc                   parm                    BIFIRM
OddEvc                   parm                    BIKUNR
OddEvc                   parm                    BIKNG
OddEvc                   parm                    RowHead          10
OddEvc                   parm                    Odd              10
OddEvc                   parm                    Even             10
OddEvc                   parm                    LogoImg          50
OddEvc                   parm                    XSPRÅK            2
OddEvc                   parm                    XINTERN           1
OddEvc                   parm                    ParmDS1        1024
OddEvc                   parm                    ParmDS2        1024
OddEvc                   parm                    ParmDS3        1024
      *
     C                   OPEN      Cundl01
     C                   OPEN      arkivl04
     C                   OPEN      arkivl05
     C                   OPEN      firmarc
     C                   OPEN      turer14
     C                   OPEN      WRKCGI
N02  C                   MOVE      *BLANKS       UCMD           1024
N02  C                   Z-ADD     1024          UCMDL            15 5
N02  C                   EVAL      UCMD = 'OVRDBF FILE(WRKCGISL) TOFILE(WRKCGI)'
N02  C                   CALL      'QCMDEXC'                            33
N02  C                   PARM                    UCMD
N02  C                   PARM                    UCMDL
N02  C                   OPEN      WRKCGISL
      *
N02  C     WRKCGISLK     KLIST
N02  C                   KFLD                    WINTEG
N02  C                   KFLD                    WKEY2
     C     KunKey        KLIST
     C                   KFLD                    BIFIRM
     C                   KFLD                    BIKUNR
     C     FakKey        KLIST
     C                   KFLD                    BIKUNR
     C                   KFLD                    artype
     C                   KFLD                    arunde
     C     FakKeyE       KLIST
     C                   KFLD                    BIKUNR
     C                   KFLD                    artype
     C     FraKey        KLIST
     C                   KFLD                    BIKUNR
     C                   KFLD                    artype
     C                   KFLD                    ardate
     C                   KFLD                    arunde
     C     FraKeyE       KLIST
     C                   KFLD                    BIKUNR
     C                   KFLD                    artype
      *
     C     KunKey        CHAIN     Cundl01                            33
      * Spesialvri for å kunne deme uten å avsløre kundenavn
     c                   MOVEL     USER          tstjov            4
     c     tstjov        ifeq      'JOVO'
     c                   eval      KNAVN  = 'Jan Ottar Valderhaug'
     c                   end
     c                   read      firmarc                                41
      *
      * Tildel random 6-sifret nr som key i Workfil.
     c                   eval      WINTEG   = random(1:999999)
      * Just to be sure - slett evt gamle poster
     c     WINTEG        setLL     WRKCGI
     c     WINTEG        ReadE     WRKCGI                                 35
     c     *IN35         doweq     '0'
     C                   delete    WRKCGIR
     c     WINTEG        ReadE     WRKCGI                                 35
     c                   end
      *
     C                   endsr
      *
     C***********************************************
     C     LagPDFURL     BEGSR
     C***********************************************
     C* genererer bane for lagring (0 i avd/opd ved samlefakt..)
     C                   MOVE      *ZEROS        AAVD              4
     C                   MOVE      *ZEROS        AOPD              7
     C                   move      *blanks       UPDF
     c                   movel     '<a href="'   UPDF            200
     C     UPDF          CAT       '/':0         UPDF
     C     UPDF          CAT       arcane:0      UPDF
     C     UPDF          CAT       '/':0         UPDF
     C     ARLINK        IFNE      *Blanks
     C     UPDF          CAT       ArLink:0      UPDF
     c                   else
     C     UPDF          CAT       aavd:0        UPDF
     C     UPDF          CAT       aopd:0        UPDF
     C     UPDF          CAT       artype:0      UPDF
     C     UPDF          CAT       arunde:0      UPDF
     c                   end
     C     '.pdf'        SCAN      updf                                   33
     C  N33'.PDF'        SCAN      updf                                   33
     C     *in33         ifeq      *off
     C     UPDF          CAT       '.pdf" ':0    UPDF
     C                   else
     C     UPDF          CAT       '" ':0        UPDF
     C                   end
     C                   CAT       'target="Sy':0UPDF
     C                   CAT       'PDFWind">':0 UPDF
     C     UPDF          CAT       '<img src="':0UPDF
     C     UPDF          CAT       '/syspeddev':0UPDF
     C     UPDF          CAT       '/PDF.gif">':0UPDF
     c                   endsr
