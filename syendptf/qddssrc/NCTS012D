     A*%%TS  SD  20240618  081607  CB          REL-V7R4M0  5770-WDS
*****A**************************************************************
     A* RETTINGER I DETTE PROGRAM:
PTFnrA*Sek Sig Vers  Beskrivelse...........................
"""""A*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
12305A* 01 CB  PTF12 FEIL VARIABELNAVN I BILDE: VARSEL
12362A* 02 CB  PTF12 NYTT BILDE: VARSLING2
*****A**************************************************************
     A*%%EC
     A                                      DSPSIZ(27 132 *DS4)
     A                                      PRINT
     A                                      HLPPNLGRP(HLPSCR TR012PNL)
     A                                      HLPTITLE('REGISTRERING AV VAREPOSTE-
     A                                      R TET EKSPORT')
     A                                      HELP
     A                                      ALTHELP
      *
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  4  2VALUES(' ' '2' '4' '5' '8')
     A            X_LINNR        5Y 0O  4  5EDTCDE(Z)
     A            TCLI           5Y 0H
     A            TCLN           5Y 0H
     A            TCVKTB        13Y 3O  4 11EDTCDE(4)
     A            TCDTY          5A  H
     A            TCALK          2A  H
     A            TCBLK          2A  H
     A            TCUCR         35A  O  4 26
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0019)
     A                                      SFLPAG(0018)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A            ZRECNO         4S 0O  1 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYSPED/NCTS012R'
     A                                      COLOR(BLU)
     A  39                              1 21'Forh.varsel House consignment'
     A                                      COLOR(WHT)
     A N39                              1 21'NCTS avgang House consignment'
     A                                      COLOR(WHT)
     A                                  1 95'Tdeknr.'
     A            UAVD           4Y 0O  1103COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  1108'-'
     A                                      COLOR(WHT)
     A            UTDN           7Y 0O  1110COLOR(WHT)
     A                                      EDTCDE(4)
     A N27                              2  2'Velg option:   2=Endre   4=Slett  -
     A                                       5=Vis   8=Item'
     A                                      COLOR(BLU)
     A  27                              2  2'Velg option:   5=Vis   8=Item'
     A                                      COLOR(BLU)
     A N32                              3  2'Op Linje Bruttovekt     UCR-ref'
     A                                      COLOR(WHT)
     A  32                              3  2'Op Linje Bruttovekt     UCR-ref'
     A                                      COLOR(WHT)
      *
     A          R BUNN
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF03(03 'Be om utskrift')
     A                                      CF05(05 'FRISK')
     A N27                                  CF06(06 'LAGE NY')
     A N36                                  CF07(07 'SØKE OPPDRAG')
     A                                      CF08(08 'SØK')
     A N27                                  CF10(10 'PLukk fra tur')
     A N27                                  CF11(11 'Plukk fra SAD Eksport')
     A                                      CF12(12 'Gå til bilde02')
     A N27                                  CF17(17 'Be om utskrift/behold en d-
     A                                      el oppl. for neste SAD')
     A                                      SLNO(22)
     A                                      CLRL(06)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A N27                              4  2'F3=Neste/avslutt   F5=Forny   F6=L-
     A                                      ag ny   F8=Hopp til'
     A                                      COLOR(BLU)
     A  27                              4  2'F3=Avslutt   F5=Forny   F8=Hopp ti-
     A                                      l   F12=Forrige'
     A                                      COLOR(BLU)
     A N36                              4 58'F7=Se oppdrag'
     A                                      COLOR(BLU)
     A N27                              5  2'F10=Hent fra oppdrag   F12=Forrige-
     A                                         F17=Ny NCTS kopier fra denne   F-
     A                                      11=Hent fra SAD Eksport'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  6  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A*
     A          R VARELI
     A*%%TS  SD  20240618  081607  CB          REL-V7R4M0  5770-WDS
     A                                      WINDOW(*DFT 21 120 *NOMSGLIN)
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF04(04 'Søk')
     A N36                                  CF07(07 'SØKE OPPDRAG')
     A                                      CF15(15 'ARBEIDE MED TILLEGG OPPL')
     A                                      CF12(12 'Forrige')
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            W2AITX       120A  H
     A            MSGNR          7A  P
     A                                  1  2'SYSPED/NCTS012R'
     A                                      COLOR(BLU)
     A  39                              1 21'Forh.varsel'
     A                                      COLOR(WHT)
     A N39                              1 21'NCTS avgang'
     A                                      COLOR(WHT)
     A                                  1 45'Tdeknr.'
     A            UAVD           4Y 0O  1 53COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  1 58'-'
     A                                      COLOR(WHT)
     A            UTDN           7Y 0O  1 60COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  1 68'-'
     A                                      COLOR(WHT)
     A            W2LI           5Y 0O  1 70COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  2  2'Oppdrag'
     A            W2AVD2         4Y 0B  2 10TEXT('Avd. til hentet oppdrag')
     A                                      DSPATR(UL)
     A  90                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            W2TDN2         7Y 0B  2 17TEXT('Opd. til hentet oppdrag')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(M)
     A                                  2 32'EXTERN REF:'
     A            W2XEXT        35A  B  2 44TEXT('ORDREREF.')
     A                                      DSPATR(UL)
     A  90                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                  3  2'2.Avsender'
     A            W2KNS          8Y 0B  3 13TEXT('Kundenr selger')
     A                                      DSPATR(UL)
     A  89                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            W2TINS        17A  B  3 22TEXT('TIN kode selger')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  3 62'8.Mottaker'
     A            W2KNK          8Y 0B  3 73TEXT('Kundenr selger')
     A                                      DSPATR(UL)
     A  88                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            W2TINK        17A  B  3 82TEXT('TIN kode selger')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            W2NAS         30A  B  4  2TEXT('Navn selger')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            W2NAK         30A  B  4 62TEXT('Navn selger')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            W2AD1S        30A  B  5  2TEXT('Adresse-1 selger')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            W2AD1K        30A  B  5 62TEXT('Adresse-1 selger')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            W2PNS          9A  B  6  2TEXT('Postnr selger')
     A                                      DSPATR(UL)
     A  87                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            W2PSS         24A  B  6 12TEXT('Poststed selger')
     A                                      DSPATR(UL)
     A  86                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            W2LKS          2A  B  6 37TEXT('Landkode selger')
     A                                      DSPATR(UL)
     A  85                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            W2PNK          9A  B  6 62TEXT('Postnr selger')
     A                                      DSPATR(UL)
     A  84                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            W2PSK         24A  B  6 72TEXT('Poststed selger')
     A                                      DSPATR(UL)
     A  83                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            W2LKK          2A  B  6 97TEXT('Landkode selger')
     A                                      DSPATR(UL)
     A  82                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                  7  2'Kontakt'
     A            W2CPS         30A  B  7 12TEXT('Kontaktperson selger')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            W2TLS         20A  B  7 43TEXT('Selgers telefonnr')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            W2EMS         50A  B  8 12TEXT('Email kontaktperson selger')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 10  2'Dekl.ty'
     A            W2DK           5A  B 10 10TEXT('DEKL.TYPE')
     A                                      DSPATR(UL)
     A  81                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 10 22'Av.land'
     A            W2ALK          2A  B 10 30TEXT('Avsendersland')
     A                                      DSPATR(UL)
     A  80                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 10 42'Be.land'
     A            W2BLK          2A  B 10 50TEXT('Bestemmelsesland')
     A                                      DSPATR(UL)
     A  79                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 11  2'Br.vekt'
     A            W2VKTB        13Y 3B 11 10TEXT('Bruttovekt')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 11 32'UCR-ref'
     A            W2UCR         35A  B 11 40TEXT('UCR-ref')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 12  2'Lev.rol'
     A            W2ROLE         3A  B 12 10TEXT('Leverandørs rolle')
     A                                      DSPATR(UL)
     A  78                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 12 22'Lev.id'
     A            W2IDR         17A  B 12 30TEXT('Leverandørs id')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 12 52'Tr.midl'
     A            W2TATY         2Y 0B 12 60TEXT('Type identifikasjon')
     A                                      DSPATR(UL)
     A  77                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            W2TAID        35A  B 12 63TEXT('Tr.middel referanse')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            W2TALK         2A  B 12 99TEXT('Tr.middel landkode')
     A                                      DSPATR(UL)
     A  76                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 13  2'T.dok'
     A            W2PDTY         4A  B 13 10TEXT('Tidligere dok.type')
     A                                      DSPATR(UL)
     A  75                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            W2PDRF        70A  B 13 15TEXT('Tidligere dok.ref')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            W2PDIN        35A  B 13 86TEXT('Informasjon')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 14  2'St.dok'
     A            W2SDTY         4A  B 14 10TEXT('Støtte dok.type')
     A                                      DSPATR(UL)
     A  74                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            W2SDRF        70A  B 14 15TEXT('Støtte dok.ref')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            W2SDIN        35A  B 14 86TEXT('Informasjon')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 15  2'Tr.dok'
     A            W2TDTY         4A  B 15 10TEXT('Støtte dok.type')
     A                                      DSPATR(UL)
     A  73                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            W2TDRF        70A  B 15 15TEXT('Støtte dok.ref')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 16  2'Til.ref'
     A            W2ADTY         4A  B 16 10TEXT('Tilleggs ref.type')
     A                                      DSPATR(UL)
     A  72                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            W2ADRF        70A  B 16 15TEXT('Tilleggs ref.')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 17  2'Til.inf'
     A            W2AICD         5A  B 17 10TEXT('Tilleggs inf. kode')
     A                                      DSPATR(UL)
     A  71                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            W2AITX110    110A  B 18 10TEXT('Tilleggs inf.')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 19  2'Bet.måt'
     A            W2TRCH         1A  B 19 10TEXT('Betalingsmåte')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 20  2'F4=Søk   F12=Avbryt   F15=Tillegg -
     A                                      opplysning'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 21  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
      *
     A          R VAREL3
     A                                      WINDOW(*DFT 21 120 *NOMSGLIN)
     A                                      CF12(12 'Forrige')
     A                                      CF15(15 'ARBEIDE MED TILLEGG OPPL')
     A                                  1  2'SYSPED/NCTS012R'
     A                                      COLOR(BLU)
     A  39                              1 21'Forh.varsel'
     A                                      COLOR(WHT)
     A N39                              1 21'NCTS avgang'
     A                                      COLOR(WHT)
     A                                  1 45'Tdeknr.'
     A            UAVD           4Y 0O  1 53COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  1 58'-'
     A                                      COLOR(WHT)
     A            UTDN           7Y 0O  1 60COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  1 68'-'
     A                                      COLOR(WHT)
     A            TCLI           5Y 0O  1 70COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  2  2'Oppdrag'
     A            TCAVD2         4Y 0O  2 10TEXT('Avd. til hentet oppdrag')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            TCTDN2         7Y 0O  2 17TEXT('Opd. til hentet oppdrag')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(M)
     A                                  2 32'EXTERN REF:'
     A            TCXEXT        35A  O  2 44TEXT('ORDREREF.')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  3  2'2.Avsender'
     A            TCKNS          8Y 0O  3 13TEXT('Kundenr selger')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            TCTINS        17A  O  3 22TEXT('TIN kode selger')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  3 62'8.Mottaker'
     A            TCKNK          8Y 0O  3 73TEXT('Kundenr selger')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            TCTINK        17A  O  3 82TEXT('TIN kode selger')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            TCNAS         30A  O  4  2TEXT('Navn selger')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            TCNAK         30A  O  4 62TEXT('Navn selger')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            TCAD1S        30A  O  5  2TEXT('Adresse-1 selger')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            TCAD1K        30A  O  5 62TEXT('Adresse-1 selger')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            TCPNS          9A  O  6  2TEXT('Postnr selger')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            TCPSS         24A  O  6 12TEXT('Poststed selger')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            TCLKS          2A  O  6 37TEXT('Landkode selger')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            TCPNK          9A  O  6 62TEXT('Postnr selger')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            TCPSK         24A  O  6 72TEXT('Poststed selger')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            TCLKK          2A  O  6 97TEXT('Landkode selger')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  7  2'Kontakt'
     A            TCCPS         30A  O  7 12TEXT('Kontaktperson selger')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            TCTLS         20A  O  7 43TEXT('Selgers telefonnr')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            TCEMS         50A  O  8 12TEXT('Email kontaktperson selger')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 10  2'Dekl.ty'
     A            TCDK           5A  O 10 10TEXT('DEKL.TYPE')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 10 22'Av.land'
     A            TCALK          2A  O 10 30TEXT('Avsendersland')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 10 42'Be.land'
     A            TCBLK          2A  O 10 50TEXT('Bestemmelsesland')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 11  2'Br.vekt'
     A            TCVKTB        13Y 3O 11 10TEXT('Bruttovekt')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 11 32'UCR-ref'
     A            TCUCR         35A  O 11 40TEXT('UCR-ref')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 12  2'Lev.rol'
     A            TCROLE         3A  O 12 10TEXT('Leverandørs rolle')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 12 22'Lev.id'
     A            TCIDR         17A  O 12 30TEXT('Leverandørs id')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 12 52'Tr.midl'
     A            TCTATY         2Y 0O 12 60TEXT('Transport middel')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            TCTAID        35A  O 12 63TEXT('Tr.middel referanse')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            TCTALK         2A  O 12 99TEXT('Tr.middel landkode')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 13  2'Tid.dok'
     A            TCPDTY         4A  O 13 10TEXT('Tidligere dok.type')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            TCPDRF        70A  O 13 15TEXT('Tidligere dok.ref')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            TCPDIN        35A  O 13 86TEXT('Informasjon')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 14  2'St.dok'
     A            TCSDTY         4A  O 14 10TEXT('Støtte dok.type')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            TCSDRF        70A  O 14 15TEXT('Støtte dok.ref')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            TCSDIN        35A  O 14 86TEXT('Informasjon')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 15  2'Tr.dok'
     A            TCTDTY         4A  O 15 10TEXT('Støtte dok.type')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            TCTDRF        70A  O 15 15TEXT('Støtte dok.ref')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 16  2'Til.ref'
     A            TCADTY         4A  O 16 10TEXT('Tilleggs ref.type')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            TCADRF        70A  O 16 15TEXT('Tilleggs ref.')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 17  2'Til.inf'
     A            TCAICD         5A  O 17 10TEXT('Tilleggs inf. kode')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            TCAITX       120A  H
     A            TCAITX110    110A  O 18 10TEXT('Tilleggs inf.')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 19  2'Bet.måt'
     A            TCTRCH         1A  O 19 10TEXT('Betalingsmåte')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 20  2'F12=Forrige   F15=Tillegg opplysni-
     A                                      nger'
     A                                      COLOR(BLU)
      *
     A          R NYWRK
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF03(03 'Be om utskrift')
     A                                      CF05(05 'FRISK')
     A N27                                  CF06(06 'LAGE NY')
     A N36                                  CF07(07 'SØKE OPPDRAG')
     A                                      CF08(08 'SØK')
     A N27                                  CF10(10 'PLukk fra tur')
     A N27                                  CF11(11 'Plukk fra SAD Eksport')
     A                                      CF12(12 'Gå til bilde02')
     A N27                                  CF17(17 'Be om utskrift/behold en d-
     A                                      el oppl. for neste SAD')
     A                                  1  2'SYSPED/NCTS012R'
     A                                      COLOR(BLU)
     A  39                              1 21'Forh.varsel House consignment'
     A                                      COLOR(WHT)
     A N39                              1 21'NCTS avgang House consignment'
     A                                      COLOR(WHT)
     A                                  1 45'Tdeknr.'
     A            UAVD           4Y 0O  1 53COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  1 58'-'
     A                                      COLOR(WHT)
     A            UTDN           7Y 0O  1 60COLOR(WHT)
     A                                      EDTCDE(4)
     A N27                              2  2'Velg option:   2=Endre   4=Slett  -
     A                                       5=Vis   8=Item'
     A                                      COLOR(BLU)
     A  27                              2  2'Velg option:   5=Vis   8=Item'
     A                                      COLOR(BLU)
     A N32                              3  2'Op Linje Bruttovekt     UCR-ref'
     A                                      COLOR(WHT)
     A  32                              3  2'Op Linje Bruttovekt     UCR-ref'
     A                                      COLOR(WHT)
     A N27                             25  2'F3=Neste/avslutt   F5=Forny   F6=L-
     A                                      ag ny   F8=Hopp til'
     A                                      COLOR(BLU)
     A  27                             25  2'F3=Avslutt   F5=Forny   F8=Hopp ti-
     A                                      l   F12=Forrige'
     A                                      COLOR(BLU)
     A N36                             25 58'F7=Se oppdrag'
     A                                      COLOR(BLU)
     A N27                             26  2'F10=Hent fra oppdrag   F12=Forrige-
     A                                         F17=Ny NCTS kopier fra denne   F-
     A                                      11=Hent fra SAD Eksport'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 27  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R VAREL2
     A                                      CF12(12 'Forrige')
     A                                      SLNO(19)
     A                                      CLRL(06)
     A                                  3  2'Hopp til utg.lin.-->'
     A            W3LN           5S 0B  3 24DSPATR(UL)
     A                                      DSPATR(PC)
     A                                  4  2'Hopp til postnr. -->'
     A            W3LI           5S 0I  4 24DSPATR(UL)
     A                                  5  2'F12=Forrige'
     A                                      COLOR(BLU)
      *
     A          R SUBFIL2                   SFL
     A            WSNR           1A  O  5  2
     A            X_LINNR        5Y 0O  5  5EDTCDE(Z)
     A            TCLI           5Y 0H
     A            TCLN           5Y 0H
     A            TCVKTB        13Y 3O  5 11EDTCDE(4)
     A            TCALK          2A  H
     A            TCBLK          2A  H
     A            TCUCR         35A  O  5 26
      *
     A          R SUBCTL2                   SFLCTL(SUBFIL2)
     A                                      SFLSIZ(0021)
     A                                      SFLPAG(0020)
     A  41                                  SFLDSP
     A  42                                  SFLDSPCTL
     A  43                                  SFLCLR
     A  44                                  SFLEND
     A  41N93N94                            ROLLUP(25)
     A            ZRECN2         4S 0O  1 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYSPED/NCTS012R'
     A                                      COLOR(BLU)
     A  39                              1 21'Forh.varsel House consignment'
     A                                      COLOR(WHT)
     A N39                              1 21'NCTS avgang House consignment'
     A                                      COLOR(WHT)
     A                                  1 45'Tdeknr.'
     A            UAVD           4Y 0O  1 53COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  1 58'-'
     A                                      COLOR(WHT)
     A            UTDN           7Y 0O  1 60COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  2  2'Trykk Enter for å bekrefte at du v-
     A                                      algte 4=Slette.'
     A                                      COLOR(BLU)
     A                                  3  2'Trykk F12 for å gå tilbake og endr-
     A                                      e valget.'
     A                                      COLOR(BLU)
     A N32                              3  2'Op Linje Bruttovekt     UCR-ref'
     A                                      COLOR(WHT)
     A  32                              4  2'Op Linje Bruttovekt     UCR-ref'
     A                                      COLOR(WHT)
      *
     A          R BUNN2
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF12(12 'Gå tilbake')
     A                                      SLNO(26)
     A                                      CLRL(02)
     A                                      OVERLAY
     A                                  1  2'F12=Avbryt'
     A                                      COLOR(BLU)
      *
     A          R HENTPRO
     A                                      WINDOW(*DFT 4 30 *NOMSGLIN)
     A                                      CF12(12 'Gå tilbake')
     A                                      OVERLAY
     A                                  2  2'Tast inn turnr.'
     A            WSPRO          8Y 0B  2 18DSPATR(UL)
     A  30                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                  3  2'F12=Avbryt'
     A                                      COLOR(BLU)
     A  30                              4  2'Tastet turnr finnes ikke.'
     A                                      COLOR(WHT)
      *
     A          R TSTGRBL
     A                                      WINDOW(*DFT 6 35 *NOMSGLIN)
     A  99                                  CF10(10 'KANKULATOR')
     A                                      CF12(12 'Gå tilbake')
     A                                      OVERLAY
     A                                  1  2'Total vareverdi   :'
     A            WSTBL         11Y 0O  1 22TEXT('Vareverdi')
     A                                      EDTCDE(4)
     A  99                              2  2'Minst en linje har ikke vareverdi.'
     A                                      COLOR(YLW)
     A                                  3  2'Oppgi garantibeløp:'
     A            WSGBL         11Y 0B  3 22TEXT('Garantibeløp')
     A  30                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                  4  2'Eller % av v.verdi:'
     A            WSGPR          5Y 2B  4 22TEXT('% av v.verdi')
     A                                      EDTCDE(4)
     A                                  5  2'F12=Avbryt'
     A                                      COLOR(BLU)
     A  99                              5 15'F10=Kalkulator'
     A                                      COLOR(BLU)
     A  30                              6  2MSGCON(33 YBC1901 SYMSGF)
     A                                      COLOR(WHT)
      *
     A          R VARSLING
     A                                      WINDOW(*DFT 5 45 *NOMSGLIN)
     A                                      OVERLAY
     A                                  1  2'Brukt garantibeløp er over'
     A                                  2  2'varslingsgrense på'
     A            TGPRM          3Y 0O  2 21EDTCDE(4)
     A                                      COLOR(YLW)
     A                                  2 25'%'
     A                                  3  2'Totalt garantibeløp er'
     A            TGGBL         11Y 0O  3 25EDTCDE(2)
     A                                      COLOR(BLU)
     A                                  4  2'Totalt brukt garantibeløp'
     A                                  5  2'inkludert denne NCTS er'
S01  A*           XBEL1         11Y 0O  5 28EDTCDE(2)
N01  A            TGGBLB        11Y 0O  5 28EDTCDE(2)
     A                                      COLOR(YLW)
N02  A          R VARSLING2
N02  A                                      WINDOW(*DFT 5 45 *NOMSGLIN)
N02  A                                      OVERLAY
N02  A                                  2  2'Brukt garantibeløp er over'
N02  A                                  3  2'totalt garantibeløp:'
N02  A            TGGBL         11Y 0O  3 28EDTCDE(2)
N02  A                                      COLOR(BLU)
N02  A                                  4  2'Totalt brukt garantibeløp'
N02  A                                  5  2'eksklusive denne NCTS er'
N02  A            TGGBLB        11Y 0O  5 28EDTCDE(2)
N02  A                                      COLOR(YLW)
