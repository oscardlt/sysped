********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
12XXX * 01 JOV PTF12 FRATN er endret / les FRATNNY er gammel key :=)
********************************************************************
     FCUNDF     IP   E             DISK
     FKUNDL01   IF   E           K DISK
     F                                     RENAME(KUNDR:KUIBMR)
     FPRIK      IF   E           K DISK
     FFIRKU     IF   E           K DISK
S01  F*RATN     IF   E           K DISK
N01  FFRATNNY   IF   E           K DISK
     FAVFRH     IF   E           K DISK
     FFRATXT    IF   E           K DISK
     FSADL      IF   E           K DISK
     FKODTFFR   IF   E           K DISK
     FLISTE     O    F  132        PRINTER OFLIND(*INOF)
     IKUNDR
     I              BANKG                       SYBAGI
     I              POSTG                       SYPOGI
     C     KUND1K        KLIST
     C                   KFLD                    FIRMA
     C                   KFLD                    KUNDNR
     C  N99              EXCEPT    OBSHEA
      * SKAL SENERE HENTES FRA FIRM..
     C**
     C  N99              SETON                                        99
     C**
     C     KUND1K        CHAIN     KUNDL01                            33
     C     FIRMA         CHAIN     FIRKU                              34
      * IKKE SLETT HVIS "ADRESSE-KUNDE"
     C     *IN33         IFEQ      '1'
     C     KUNDNR        ANDGE     FIKUFR
     C     KUNDNR        ANDLE     FIKUTI
     C                   MOVE      '0'           *IN33
     C                   END
     C* Finnes ikke i KUNDL01 - Vil bli slettet også i CUNDF
     C     *IN33         IFEQ      '1'
     C                   MOVEA     '00000000'    *IN(50)
     C* Gebyravtaler?
     C                   MOVE      KUNDNR        KUNDNA            8
     C     KUNDNA        SETLL     PRIK
     C     KUNDNA        READE     PRIK                                   33
     C  N33              SETON                                        5051
     C* Fraktavtaler?
s01  C*    KUNDNR        SETLL     FRATN
s01  C*    KUNDNR        READE     FRATN                                  33
n01  C     KUNDNR        SETLL     FRATNNY
n01  C     KUNDNR        READE     FRATNNY                                33
     C  N33              SETON                                        5052
      *... POSTAL
     C     KUNDNR        SETLL     AVFRH
     C     KUNDNR        READE     AVFRH                                  33
     C  N33              SETON                                        5056
     C* Fritekster  ?
     C     KUNDNR        SETLL     FRATXT
     C     KUNDNR        READE     FRATXT                                 33
     C  N33              SETON                                        5053
     C* Kunders varekatalog SAD ?
     C     KUNDNR        SETLL     SADL
     C     KUNDNR        READE     SADL                                   33
     C  N33              SETON                                        5054
     C* Avtale flyfraktrater    ?
     C     KUNDNR        SETLL     KODTFFR
     C     KUNDNR        READE     KODTFFR                                33
     C  N33              SETON                                        5055
     C*
     C* 50 on = Minst en underpost fantes.. skriv på obsliste.
     C   50              EXCEPT    OBSDET
     C                   END
     OLISTE     E            OBSHEA           02
     O                                           25 '***** LISTE OVER KUNDER '
     O                                           49 ' SOM VIL BLI SLETTET HVO'
     O                                           73 'R DET FINNES "UNDERPOSTE'
     O                                           90 'R"          *****'
     O          E            OBSHEA           04
     O                                           25 'Kundenr  Navn           '
     O                                           49 '              Gebyravt. '
     O                                           73 'Fraktavt. Fritekst  Vare'
     O                                           90 'kat-SAD Flyrater '
     O          E            OBSHEA           05
     O                                           25 '________________________'
     O                                           49 '________________________'
     O                                           73 '________________________'
     O                                           90 '_________________'
     O          E            OBSDET      1
     O                       KUNDNR        Z      9
     O                       KNAVN               40
     O               51                          45 'X'
     O               52                          53 'X'
     O               56                          55 'X'
     O               53                          65 'X'
     O               54                          75 'X'
     O               55                          85 'X'
