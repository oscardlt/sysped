********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 YBC PTF10 FELT FOR KRAV PÅ ANDREKOSTNAD I SAD-EKSPORT
********************************************************************
      *
      **************************************************************
      *** PROGRAM SKAL VEDLIKEHOLDE LEVERINGSBETINGELSER (SAD)   ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER       ***
      *** LEDIGE INDIKATORER 01 02 04 07 09-11 13-24             ***
      *** LEDIGE INDIKATORER 27-29 31 32 34-39 41-85 90 95-97    ***
      **************************************************************
      *
     Fsysthlp   IF   E             DISK
     FKODTLB    UF A E           K DISK
     FSAD012FM  CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D                 DS
     D  KLBXXX                 1     35
     D*** VED CIF OM ADRESSE SKAL HENTES, EVT. FRA AVSENDER/MOTTAGER
     D  KLBHAD                 1      1
     D  KLBHAE                 2      2
N01  D  KLBANK                 3      3
     C*
     C                   EXSR      INIT
     C*
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
     C                   MOVE      W2KOD         YKOD              3
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     KODL2K        SETLL     KODTLB
     C     START1        TAG
     C     YLBUNI        READE     KODTLB                                 94
     C   94              MOVE      *BLANKS       KLBKOD
     C   94              MOVE      *BLANKS       WSNR
     C  N94              UPDATE    KOLBR
     C  N94              MOVE      KLBKOD        YKOD
      *
     C     START2        TAG
      *
     C  N94              DO
     C                   DO        16
     C                   ADD       1             ZRECNO
N01  C                   IF        KLBANK <> 'J'
N01  C                   MOVE      ' '           KLBANK
N01  C                   END
     C                   WRITE     SUBFIL
     C     YLBUNI        READE     KODTLB                                 94
     C   94              MOVE      *BLANKS       KLBKOD
     C  N94              UPDATE    KOLBR
     C  N94              MOVE      KLBKOD        YKOD
     C  N94              END
     C                   END
     C*
     C                   Z-ADD     ZRECNO        YRECNO            5 0
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     2             POSNBR
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYWRK
     C                   ELSE
     C                   WRITE     SUBCTL
     C                   EXFMT     BUNN
     c   01              do
     c                   movel     'sad012'      hlpna             8
     c                   exsr      helpsr
     C                   GOTO      START3
     c                   end
     C                   END
     C*
     C***
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C                   Z-ADD     WSSUBN        XRECNO            4 0
     C     XRECNO        IFEQ      0
     C                   Z-ADD     ZRECNO        XRECNO            4 0
     C                   END
     C***
     C*
     C                   SETOFF                                       989930
     C*
     C   03              GOTO      SLUTT
     C*** FRISK OPP
     C   05              GOTO      START
     C*** LAGE NY
     C   06              DO
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     7             POSNBR
     C                   MOVE      *BLANKS       WLBKOD
     C                   GOTO      START5
     C                   END
     C*** SOEK FRA
     C   08              GOTO      START6
     C*
     C*** BLA VIDERE
     C   25              DO
     C                   Z-ADD     YRECNO        ZRECNO
     C                   SETOFF                                       25
     C  N94KODL3K        SETLL     KODTLB
     C  N94YLBUNI        READE     KODTLB                                 94
     C  N94              UPDATE    KOLBR
     C                   GOTO      START2
     C                   END
     C*
     C     ZRECNO        IFNE      0
     C                   EXSR      HENT
     C                   END
     C   98XRECNO        IFNE      0
     C                   Z-ADD     XRECNO        ZRECNO
     C                   END
     C                   GOTO      START3
     C*
     C*** LAGE NY
     C     START5        TAG
     C                   EXFMT     VARELI
     c   01              do
     c                   movel     'sad012'      hlpna
     c                   exsr      helpsr
     C                   GOTO      START5
     c                   end
     C                   MOVEA     '0000'        *IN(86)
     C                   SETOFF                                       30
     C   12              EXSR      MOVB2
     C  N12              EXSR      TESTB1
     C  N12
     CAN 30              GOTO      START5
     C  N12              EXSR      OPDLB
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     2             POSNBR
     C                   GOTO      START3
     C*
     C*** SOEK
     C     START6        TAG
     C                   EXFMT     SOKLB
     c   01              do
     c                   movel     'sad012'      hlpna             8
     c                   exsr      helpsr
     C                   GOTO      START
     c                   end
     C   12              GOTO      START3
     C                   GOTO      START
     C*
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
     C*
     C***********************************************
     C     TESTB1        BEGSR
     C***********************************************
     C*
     C* TEST OM BRUKER HAR TASTET LEVERINGSBETINGELSEKODE
     C     WLBKOD        COMP      *BLANKS                                89
     C   89              MOVE      'SPL0007'     MSGNR
     C  N40
     CANN89              DO
     C     KODLBK        CHAIN     KODTLB                             33
     C  N33              UPDATE    KOLBR
     C  N33              SETON                                        89
     C  N33              MOVE      'SPL0008'     MSGNR
     C                   END
     C   89              SETON                                        30
     C*
     C* TEST OM KODETEKST = BLANK
     C     WLBKT         COMP      *BLANKS                                88
     C   88
     CANN30              MOVE      'SPT0018'     MSGNR
     C   88              SETON                                        30
     C*
     C* TEST OM FORSIKRINGSKODE ER RIKTIG
     C     WLBFOK        COMP      ' '                                    87
     C   87
     CANN30              MOVE      'SPF0021'     MSGNR
     C   87              SETON                                        30
     C*
     C* TEST OM FRAKTKODE ER RIKTIG
     C     WLBFRK        COMP      ' '                                    86
     C   86
     CANN30              MOVE      'SPF0021'     MSGNR
     C   86              SETON                                        30
     C*
     C   30              Z-ADD     22            LINNBR
S01  C*  86              Z-ADD     66            POSNBR
N01  C   86              Z-ADD     60            POSNBR
S01  C*  87              Z-ADD     45            POSNBR
N01  C   87              Z-ADD     42            POSNBR
     C   88              Z-ADD     16            POSNBR
     C   89              Z-ADD     7             POSNBR
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     HENT          BEGSR
     C***********************************************
     C                   SETOFF                                       919299
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C   98              DO
     C   99              SETOFF                                       98
     C   98              MOVE      'DIV0012'     MSGNR
     C   98              SETON                                            30
     C                   Z-ADD     1             LINNBR
     C                   GOTO      SLHENT
     C                   END
     C     ZRECNO        DIV       16            LINNBR
     C                   MVR                     LINNBR
     C                   Z-ADD     LINNBR        XLIN              2 0
     C     XLIN          IFEQ      0
     C                   Z-ADD     16            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C     WSNR          IFEQ      '2'
     C                   MOVE      ' '           WSNR
     C     KLBKOD        COMP      '*DL'                                  98
     C   98              MOVE      'SPO0020'     MSGNR
     C   98              SETON                                            30
     C   98              GOTO      SLHENT
     C                   EXSR      CHGLB
     C   12              GOTO      SLHENT
     C                   END
     C     WSNR          IFEQ      '4'
     C                   MOVE      ' '           WSNR
     C     KLBKOD        COMP      '*DL'                                  98
     C   98              MOVE      'SPO0020'     MSGNR
     C   98              SETON                                            30
     C   98              GOTO      SLHENT
     C                   EXSR      DLTLB
     C   12              GOTO      SLHENT
     C                   END
     C                   END
     C*
     C     SLHENT        TAG
     C                   ADD       4             LINNBR
     C                   Z-ADD     2             POSNBR
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     DLTLB         BEGSR
     C***********************************************
     C     DLTLBTAG      TAG
     C                   SETON                                        99
     C                   MOVE      KLBKOD        WLBKOD
     C                   EXFMT     VAREL3
     c   01              do
     c                   movel     'sad012'      hlpna             8
     c                   exsr      helpsr
     C                   GOTO      DLTLBTAG
     c                   end
     C  N12              DO
     C     KODLBK        CHAIN     KODTLB                             33
     C  N33              DELETE    KOLBR
     C                   EXSR      MOVB1
     C                   END
     C     XLIN          IFEQ      0
     C                   Z-ADD     16            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C                   UPDATE    SUBFIL
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     CHGLB         BEGSR
     C***********************************************
     C                   EXSR      MOVLBW
     C                   SETON                                        99
     C     STCHG         TAG
     C                   EXFMT     VARELI
     c   01              do
     c                   movel     'sad012'      hlpna             8
     c                   exsr      helpsr
     C                   GOTO      STCHG
     c                   end
     C                   MOVEA     '0000'        *IN(86)
     C                   SETOFF                                       30
     C   12              DO
     C                   UPDATE    SUBFIL
     C                   EXSR      MOVB2
     C                   END
     C  N12              EXSR      TESTB1
     C  N12
     CAN 30              GOTO      STCHG
     C  N12              EXSR      OPDLB
     C     XLIN          IFEQ      0
     C                   Z-ADD     16            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     OPDLB         BEGSR
     C***********************************************
     C     KODLBK        CHAIN     KODTLB                             33
     C                   EXSR      MOVWLB
     C  N33              UPDATE    KOLBR
     C   33              WRITE     KOLBR
     C*
     C   40              UPDATE    SUBFIL
     C  N40              ADD       1             YRECNO
     C  N40              Z-ADD     YRECNO        ZRECNO
     C  N40              WRITE     SUBFIL
     C                   EXSR      MOVB2
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVLBW        BEGSR
     C***********************************************
     C                   MOVE      KLBKOD        WLBKOD
     C                   MOVE      KLBNAV        WLBNAV
     C                   MOVE      KLBKT         WLBKT
     C                   MOVE      KLBFOK        WLBFOK
     C                   MOVE      KLBPRM        WLBPRM
     C                   MOVE      KLBFRK        WLBFRK
N01  C                   MOVE      KLBANK        WLBANK
     C                   MOVE      KLBHAD        WLBHAD
     C                   MOVE      KLBHAE        WLBHAE
     C                   SETON                                        40
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     16            POSNBR
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVWLB        BEGSR
     C***********************************************
     C                   MOVE      YLBUNI        KLBUNI
     C                   MOVE      WLBKOD        KLBKOD
     C                   MOVE      WLBNAV        KLBNAV
     C                   MOVE      WLBKT         KLBKT
     C                   MOVE      WLBFOK        KLBFOK
     C                   MOVE      WLBFRK        KLBFRK
N01  C                   MOVE      WLBANK        KLBANK
     C                   MOVE      WLBPRM        KLBPRM
     C                   MOVE      WLBHAD        KLBHAD
     C                   MOVE      WLBHAE        KLBHAE
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVB1         BEGSR
     C***********************************************
     C                   MOVE      '*DL'         KLBKOD
     C                   MOVE      *BLANKS       KLBKT
     C                   MOVE      *BLANKS       KLBNAV
     C                   MOVE      *BLANKS       KLBFOK
     C                   MOVE      *ZEROS        KLBPRM
     C                   MOVE      *BLANKS       KLBFRK
N01  C                   MOVE      *BLANKS       KLBANK
     C                   MOVE      *BLANKS       KLBHAD
     C                   MOVE      *BLANKS       KLBHAE
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVB2         BEGSR
     C***********************************************
     C                   SETOFF                                       40
     C                   MOVE      *BLANKS       WLBKOD
     C                   MOVE      *BLANKS       WLBKT
     C                   MOVE      *BLANKS       WLBNAV
     C                   MOVE      *BLANKS       WLBFOK
     C                   MOVE      *ZEROS        WLBPRM
     C                   MOVE      *BLANKS       WLBFRK
N01  C                   MOVE      *BLANKS       WLBANK
     C                   MOVE      *BLANKS       WLBHAD
     C                   MOVE      *BLANKS       WLBHAE
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     KODLBK        KLIST
     C                   KFLD                    YLBUNI
     C                   KFLD                    WLBKOD
     C     KODL2K        KLIST
     C                   KFLD                    YLBUNI
     C                   KFLD                    W2KOD
     C     KODL3K        KLIST
     C                   KFLD                    YLBUNI
     C                   KFLD                    YKOD
     C*
     C                   MOVE      'LB'          YLBUNI            2
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     HELPSR        BEGSR
     C***********************************************
     c
     c                   move      *blanks       pcocmd           70
     c     1             chain     systhlp                            33
     c     *in(33)       ifeq      '0'
      *
      * intern
     c                   movel     'http://'     pcocmd
     C     pcocmd        CAT       systip:0      pcocmd
     C     pcocmd        CAT       '/':0         pcocmd
     C     pcocmd        CAT       systba:0      pcocmd
     C     pcocmd        CAT       '/':0         pcocmd
     C     pcocmd        CAT       hlpna:0       pcocmd
     C     pcocmd        CAT       '.htm':0      pcocmd
      *
     c                   call      'SYGE34CL'
     c                   parm                    pcocmd
     c                   end
     C                   ENDSR
