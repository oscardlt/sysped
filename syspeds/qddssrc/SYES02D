*****A**************************************************************
     A* RETTINGER I DETTE PROGRAM:
PTFnrA*Sek Sig Vers  Beskrivelse...........................
"""""A*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXXA* 01 YBC PTF10 ENDGELSK BESKRIVELSE
*****A**************************************************************
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
      *
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  5  2VALUES(' ' '2' '3' '4' '5')
     A            BPAKT          1A  O  5  5
     A            BPKUNR         8Y 0O  5  8EDTCDE(4)
     A            BPLIN          3Y 0O  5 17EDTCDE(4)
     A            BPKOL          1Y 0O  5 23EDTCDE(4)
     A            BPTXT         50A  O  5 25
N01  A            BPTXTE        50A  H
     A            BPPRD        128A  H
     A            BPPRD2       128A  H
     A            BPBILD        50A  H
     A            BPRECN         7Y 0H
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0016)
     A                                      SFLPAG(0015)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                  1  2'SYSPED/SYES02R'
     A                                      COLOR(BLU)
     A            ZRECNO         4S 0O  3 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1 20MSGCON(040 DIV2638 *LIBL/SYMSGF)
     A                                  2  2MSGCON(077 DIV2639 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  3  2MSGCON(051 DIV2630 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(034 DIV2640 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
      *
     A          R BUNN
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF03(03 'Avslutt program')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A  30        MSGTXT        78A  O  4  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A                                  3  2MSGCON(042 DIV2600 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
      *
     A          R VARELI
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF04(04 'SØKING')
     A                                      CF09(09 'SØKING')
     A                                      CF12(12 'Forrige')
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1  2'SYSPED/SYES02R'
     A                                      COLOR(BLU)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                  2 70TIME
     A            WSRECN         7S 0H
     A            WSKUNR         8Y 0B  4 14DSPATR(UL)
     A  40                                  DSPATR(PR)
     A  90                                  DSPATR(RI)
     A  40                                  COLOR(WHT)
     A N40                                  COLOR(YLW)
     A                                      EDTCDE(4)
     A            KNAVN         30A  O  4 24DSPATR(UL)
     A                                      COLOR(WHT)
     A            WSLIN          3Y 0B  5 14DSPATR(UL)
     A  89                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSKOL          3Y 0B  5 36DSPATR(UL)
     A  88                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSTXT         50A  B  7 14DSPATR(UL)
     A  87                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A            WSTXTE        50A  B  8 14DSPATR(UL)
     A  84                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A            WSAKT          1A  B  9 14DSPATR(UL)
     A  86                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSPRD        128A  B 11 14DSPATR(UL)
     A  85                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A            WSPRD2       128A  B 14 14DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A            WSBILD        50A  B 17 14DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 20MSGCON(040 DIV2638 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  2  2MSGCON(053 DIV2639 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  2 57MSGCON(007 DIV2641 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  4  2MSGCON(009 DIV2642 *LIBL/SYMSGF)
     A                                  5  2MSGCON(009 DIV2643 *LIBL/SYMSGF)
     A                                  5 24MSGCON(009 DIV2644 *LIBL/SYMSGF)
     A                                  7  2MSGCON(011 DIV2645 *LIBL/SYMSGF)
     A                                  8  2MSGCON(011 DIV2646 *LIBL/SYMSGF)
     A                                  9  2MSGCON(010 DIV2647 *LIBL/SYMSGF)
     A                                 11  2MSGCON(011 DIV2648 *LIBL/SYMSGF)
     A                                 14  2MSGCON(011 DIV2649 *LIBL/SYMSGF)
     A                                 17  2MSGCON(009 DIV2650 *LIBL/SYMSGF)
     A                                 23  2MSGCON(020 DIV2635 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 23MSGCON(020 DIV2622 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
      *
     A          R VAREL3
     A                                      CF12(12 'Forrige')
     A                                  1  2'SYSPED/SYES02R'
     A                                      COLOR(BLU)
     A                                  1 20'Arbeid med produkter for web-kunde-
     A                                      r'
     A                                      COLOR(WHT)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                  2 70TIME
     A            BPRECN         7S 0H
     A            BPKUNR         8Y 0O  3 14DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            KNAVN         30A  O  3 24DSPATR(UL)
     A                                      COLOR(WHT)
     A            BPLIN          3Y 0O  5 14DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            BPKOL          1Y 0O  5 36DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            BPTXT         50A  O  7 14DSPATR(UL)
     A                                      COLOR(WHT)
     A            BPTXTE        50A  O  8 14DSPATR(UL)
     A                                      COLOR(WHT)
     A            BPAKT          1A  O  9 14DSPATR(UL)
     A                                      COLOR(WHT)
     A            BPPRD        128A  O 11 14DSPATR(UL)
     A                                      COLOR(WHT)
     A            BPPRD2       128A  O 14 14DSPATR(UL)
     A                                      COLOR(WHT)
     A            BPBILD        50A  O 17 14DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 23  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3  2MSGCON(011 DIV2609 *LIBL/SYMSGF)
     A                                  5  2MSGCON(009 DIV2643 *LIBL/SYMSGF)
     A                                  5 24MSGCON(009 DIV2644 *LIBL/SYMSGF)
     A                                  7  2MSGCON(011 DIV2645 *LIBL/SYMSGF)
     A                                  8  2MSGCON(011 DIV2646 *LIBL/SYMSGF)
     A                                  9  2MSGCON(010 DIV2647 *LIBL/SYMSGF)
     A                                 11  2MSGCON(011 DIV2648 *LIBL/SYMSGF)
     A                                 14  2MSGCON(011 DIV2649 *LIBL/SYMSGF)
     A                                 17  2MSGCON(009 DIV2650 *LIBL/SYMSGF)
      *
     A          R VAREL2
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            W2KUNR         8Y 0B  2 27DSPATR(UL)
     A                                      EDTCDE(4)
     A                                  2  2MSGCON(022 DIV2651 *LIBL/SYMSGF)
     A                                  3  2MSGCON(011 DIV2626 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
      *
     A          R NYVARE
     A                                      CF03(03 'Avslutt program')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                  1  2'SYSPED/SYES02R'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A                                  1 20MSGCON(040 DIV2638 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  3  2MSGCON(051 DIV2630 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(067 DIV2631 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                 23  2MSGCON(031 DIV2652 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
