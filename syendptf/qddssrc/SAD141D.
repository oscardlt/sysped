     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A                                      HELP
     A                                      ALTHELP
      *
     A          R WINDOWSZ
     A                                      WINDOW(*DFT 12 70 *NOMSGLIN)
     A                                      OVERLAY
      *
     A          R BILDE01
     A                                      WINDOW(WINDOWSZ)
     A                                      CF04(04 'Søking')
     A                                      CF09(09 'Hent UUID')
     A                                      CA12(12 'Avbryt')
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1  2'SYSPED/SAD141R'
     A                                      COLOR(BLU)
     A                                  1 20'Arbeid med oppdrag'
     A                                      COLOR(WHT)
     A                                  2  2'Avd.:'
     A            EFAVD          4Y 0O  2  8TEXT('AVDELINGSNR')
     A                                      COLOR(BLU)
     A                                      EDTCDE(4)
     A                                  2 13'Turnr:'
     A            EFPRO          8Y 0O  2 20TEXT('Turnr')
     A                                      COLOR(BLU)
     A                                      EDTCDE(4)
     A                                  2 29'UUID:'
     A            EFUUID        36A  O  2 35TEXT('Turnr')
     A                                      COLOR(BLU)
      *
     A                                  4  2'Avdeling:'
     A            WSAVD          4Y 0O  4 15TEXT('Deklarantnr')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  4 21'Oppdrag:'
     A            WSTDN          7Y 0O  4 30TEXT('Oppdragsnr')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  4 41'Status:'
     A            CLST           1A  O  4 49TEXT('Status')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
      *
     A                                  5  2'Send.org:'
     A            WSRG          11A  O  5 15TEXT('Senders org.nr')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  5 27'Dato:'
     A            WS0068A        6Y 0O  5 33TEXT('Sendingsdato')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTWRD('  .  .  ')
     A                                  5 42'Sekvens:'
     A            WS0068B        6Y 0O  5 51TEXT('Sendingsdato')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
      *
     A                                  6  2'Fra sted:'
     A            WSSDF          5A  B  6 15TEXT('Fra sted')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N99                                  COLOR(YLW)
     A  99                                  COLOR(WHT)
     A            WSSDFT        30A  O  6 21COLOR(WHT)
     A                                  7  2'Til sted:'
     A            WSSDT          5A  B  7 15TEXT('Fra sted')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N98                                  COLOR(YLW)
     A  98                                  COLOR(WHT)
     A            WSSDTT        30A  O  7 21COLOR(WHT)
      *
     A                                  8  2'Prosedyre:'
     A            WSPR           2A  B  8 15TEXT('Type')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N97                                  COLOR(YLW)
     A  97                                  COLOR(WHT)
     A            WSPRT         45A  O  8 21COLOR(WHT)
     A                                  9  2'Eksp.type:'
     A            WSETYP         2A  B  9 15TEXT('Type')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N96                                  COLOR(YLW)
     A  96                                  COLOR(WHT)
     A            WSETYPT       45A  O  9 21COLOR(WHT)
     A                                 10  2'Eksport Id:'
     A            WSEID         18A  B 10 15TEXT('Eksport Id')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N95                                  COLOR(YLW)
     A  95                                  COLOR(WHT)
     A                                 10 35'Sertifisert:'
     A            WSESER         1A  B 10 48TEXT('Adr-1 Deklarant')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'J' 'N')
     A                                 10 50'(J/N)'
     A                                 11  2'F4=Søk   F12=Avbryt'
     A                                      COLOR(BLU)
     A  30        MSGTXT        68A  O 12  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
