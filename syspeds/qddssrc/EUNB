********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 YBC PTF10 SENDINGSNR ER ENDRET FRA 7.0 TIL 9.0
********************************************************************
     A          R UNBR
      *____
      * KEY
      *""""
S01  A*           UIN            7S 0       COLHDG('Interchg.no')
N01  A            UIN            9S 0       COLHDG('Interchg.no')
      *____
      * UNA
      *""""
     A            UNA1           1          COLHDG('Cmp.data elm sep')
     A            UNA2           1          COLHDG('Data elm sep')
     A            UNA3           1          COLHDG('Dec not')
     A            UNA4           1          COLHDG('Rel ind')
     A            UNA5           1          COLHDG('Future use')
     A            UNA6           1          COLHDG('Segment terminator')
      *____
      * UNB
      *""""
      * S001 Syntax identifier
     A            U0001          4          COLHDG('Syntax id')
     A            U0002          1          COLHDG('Syntax v.no')
      * S002 Interchange sender
     A            U0004         35          COLHDG('Sender id')
     A            U0007S         4          COLHDG('Part.id.code Q')
     A            U0008         14          COLHDG('Adr.for rev.rout')
      * S003 Interchange recipient
     A            U0010         35          COLHDG('Recip. id')
     A            U0007R         4          COLHDG('Part.id.code Q')
     A            U0014         14          COLHDG('Routing adr')
      * S004 Date/Time of preparation
     A            U0017          6          COLHDG('Date of prep')
     A            U0019          4          COLHDG('Time of prep')
      *
     A            U0020A        14          COLHDG('Interchange ctl ref')
      * S005 Recipients Reference Password
     A            U0022         14          COLHDG('Rec ref/pwd')
     A            U0025          2          COLHDG('Rec ref/pwd Q')
      *
     A            U0026         14          COLHDG('Application ref')
     A            U0029          1          COLHDG('Proc prior code')
     A            U0031          1          COLHDG('Ack req')
     A            U0032         35          COLHDG('Cmn agree id')
     A            U0035          1          COLHDG('Test ind')
      *____
      * UNZ
      *""""
     A            U0036          6          COLHDG('Interchg ctl count')
     A            U0020B        14          COLHDG('Interchange ctl ref')
      *____
      * KEY
      *""""
     A          K UIN
