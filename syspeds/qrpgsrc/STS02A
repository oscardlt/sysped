      *********************************************************************
      * Arbeid med inn terminal
      * PROGRAM BEHANDLER INPUT FRA STS02
      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/STS02A MODULE(*LIBL/STS02A *LIBL/INCGI)
CRTPGM*        ACTGRP(*NEW) AUT(*USE)
      *
      *********************************************************************
      * MAIN PROGRAM FLOW
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
     FBRIDF     IF   E           K DISK    USROPN
     FHENOKW1   IF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Prototype defintions and standard system API error structure
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      *--------------------------------------------------------------------
      * Variables common to CGI programs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
      * Variables received from the input string
     D lng             s              2
     D BckGnd          s             10
     D WSUSER          s             10
     D WSCLID          s             20
     D WSPALLV         s              1
     D WSFRA12         s              1
     D WSKOLL          s              5  0
     D WSOVRT          s              5  0
     D WSPALL          s              5  0
     D WSLEST          s              5  0
     D WSTOT           s              5  0
      * Number of records matching search criteria
     DRecordCnt        s              5S 0
      *
     D Lib             s             10
     D Fn              s             10
     D Mbr             s             10
      *
     D Spaces          s             12a   inz('&nbsp;&nbsp;')
      *--------------------------------------------------------------------
      * Prolog common to CGI programs
      * -Receive input from the remote browser
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prolog3
      *=====================================================================******
      * MAIN PROCESS LINE
      *=====================================================================******
      * Get program start time for calculating execution time
     C                   time                    timedata1
      * Parse input
     C                   exsr      Parse
     C                   EXSR      INIT
      *
     C                   SELECT
     C                   WHEN      *IN60
     C                   exsr      SetSkl01B
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
     C                   exsr      SetTop
     C                   callp     wrtsection('all')
     C                   WHEN      BIINN = ' '
     C                   exsr      SetSkl01B
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
     C                   exsr      SetTop
     C                   callp     wrtsection('all')
      *
     C                   OTHER
     c                   EXSR      TstFinnes
     C     FINNES        IFEQ      'N'
     C                   exsr      SetSkl11
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
     C                   exsr      SetTop
     C                   callp     wrtsection('top')
     C                   ELSE
     C                   EXSR      SetSkl12
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
     C                   exsr      SetTop12
     C                   callp     wrtsection('top')
     C                   callp     wrtsection('endhtml')
     C                   END
      *
     C                   ENDSL
      *
      *
     C                   callp     wrtsection('*fini')
      *
     C                   EXSR      EXIT
     C                   return
      *=====================================================================******
      * Parse input
      *=====================================================================******
     C     Parse         begsr
     C                   eval      WSUSER  = zhbgetvar('USER')
     C                   eval      WSFRA12 = zhbgetvar('FRA12')
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$top
      *  (search criteria)
      *=====================================================================******
     C     SetTop        begsr
      * Repeat national language for the next invocation
     C                   callp     updhtmlvar('LNG':lng:
     C                             InitHTMLVars)
      * Repeat background color for the next invocation
     C                   callp     updhtmlvar('BCKGND':bckgnd)
      * Color for text, background, links, visited links, active links
     C                   exsr      setcolor
BODY C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updhtmlvar('USER':WSUSER)
     C                   callp     updhtmlvar('WSLEST':
     C                             %trim(%editc(WSLEST:'3')))
     C                   callp     updhtmlvar('WSTOT':
     C                             %trim(%editc(WSTOT:'3')))
     C                   callp     updhtmlvar('CLID':WSCLID)
     C                   callp     updhtmlvar('PALLV':WSPALLV)
     C                   callp     updHTMLvar('FEILKODE':spaces)
     C                   callp     updHTMLvar('FEILTXT':spaces)
      *
     C                   endsr
      *=====================================================================
      * Set html output variables for section /$top
      *=====================================================================
     C     SetTop12      begsr
      * Repeat national language for the next invocation
     C                   callp     updhtmlvar('LNG':lng:
     C                             InitHTMLVars)
      * Repeat background color for the next invocation
     C                   callp     updhtmlvar('BCKGND':bckgnd)
      * Color for text, background, links, visited links, active links
     C                   exsr      SetColor
      * Protocol
     C                   callp     updHtmlVar('PROTOCOL':S_Protocol)
      *
     C                   callp     updHtmlVar('USER':WSUSER)
     C                   callp     updhtmlvar('WSLEST':
     C                             %trim(%editc(WSLEST:'3')))
     C                   callp     updhtmlvar('WSTOT':
     C                             %trim(%editc(WSTOT:'3')))
     C                   callp     updhtmlvar('WSKOLL':
     C                             %trim(%editc(WSKOLL:'3')))
     C                   callp     updhtmlvar('WSOVRT':
     C                             %trim(%editc(WSOVRT:'3')))
     C                   callp     updhtmlvar('WSPALL':
     C                             %trim(%editc(WSPALL:'3')))
     C                   callp     updHtmlVar('WSBLANK':spaces)
     C                   endsr
      *=====================================================================******
      * Color for font, background, links, visited links, active links
      *=====================================================================******
     C     SetColor      begsr
     C                   callp     updhtmlvar('TXTCOLOR':'black')
     C                   callp     updhtmlvar('BOLD':' ')
      * BGCOLOR white, LINK blue, VLINK red, ALINK green
     C     bckgnd        ifeq      'WHITE'
     C     bckgnd        oreq      *blank
     C                   callp     updhtmlvar('BGCOLOR':'FFFFFF')
     C                   callp     updhtmlvar('LINK':'0000FF')
     C                   callp     updhtmlvar('VLINK':'FF0000')
     C                   callp     updhtmlvar('ALINK':'00FF00')
     C                   endif
      * BGCOLOR gray, LINK blue, VLINK red, ALINK green
     C     bckgnd        ifeq      'GRAY'
     C                   callp     updhtmlvar('BGCOLOR':'CCCCCC')
     C                   callp     updhtmlvar('LINK':'0000FF')
     C                   callp     updhtmlvar('VLINK':'FF0000')
     C                   callp     updhtmlvar('ALINK':'00FF00')
     C                   endif
      * BGCOLOR light blue, LINK blue, VLINK red, ALINK green
     C     bckgnd        ifeq      'LBLUE'
     C                   callp     updhtmlvar('BGCOLOR':'5BE6F3')
     C                   callp     updhtmlvar('LINK':'0000FF')
     C                   callp     updhtmlvar('VLINK':'FF0000')
     C                   callp     updhtmlvar('ALINK':'00FF00')
     C                   endif
      * BGCOLOR black, LINK green, VLINK red, ALINK white
     C     bckgnd        ifeq      'BLACK'
     C                   callp     updhtmlvar('TXTCOLOR':'white')
     C                   callp     updhtmlvar('BGCOLOR':'000000')
     C                   callp     updhtmlvar('LINK':'00FF00')
     C                   callp     updhtmlvar('VLINK':'FF0000')
     C                   callp     updhtmlvar('ALINK':'FFFFFF')
     C                   callp     updhtmlvar('BOLD':'<b>')
     C                   endif
     C                   endsr
      *=====================================================================******
      * Set html output skeleton member before its read into memory
      *=====================================================================******
     C     TstFinnes     begsr
      * Sjekk manko, overtallige og antall pall med gods
     C                   MOVE      *ZEROS        WSLEST
     C                   MOVE      *ZEROS        WSTOT
     C                   MOVE      *ZEROS        WSKOLL
     C                   MOVE      *ZEROS        WSOVRT
     C                   MOVE      *ZEROS        WSPALL
     C                   MOVE      'N'           FINNES            1
     C     WSUSER        SETLL     HENOKW1
     C     WSUSER        READE     HENOKW1                                50
     C     *in50         doweq     '0'
     C                   MOVE      'J'           FINNES
     C     HKSTIN        IFNE      ' '
     C                   ADD       1             WSLEST
     C                   end
     C                   ADD       1             WSTOT
     C     HKLNR         IFEQ      0
     C                   ADD       1             WSOVRT
     C                   ELSE
     C     HKSTIN        IFEQ      ' '
     C                   ADD       1             WSKOLL
     C                   ENDIF
     C                   ENDIF
     C     HKPALL        IFNE      ' '
     C                   ADD       1             WSPALL
     C                   ENDIF
     C     WSUSER        READE     HENOKW1                                50
     C  N50              ENDDO                                                  *hival
      *
      * HVIS EN KOMMER FRA STS12 (STATUS-BILDE) GÅ ALLTID TIL SKYTING
     C     WSFRA12       IFEQ      'Y'
     C                   MOVE      'N'           FINNES            1
     C                   END
      *
      *
     C                   endsr
      *=====================================================================******
      * Set html output skeleton member before its read into memory
      *=====================================================================******
     C     SetSkl01B     begsr
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC' + lng
     C                   eval      Mbr = 'STS01B'
      *
     C                   endsr
      *=====================================================================******
      * Set html output skeleton member before its read into memory
      *=====================================================================******
     C     SetSkl11      begsr
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC' + lng
     C                   eval      Mbr = 'STS11'
     C                   endsr
      *=====================================================================******
      * Set html output skeleton member before its read into memory
      *=====================================================================******
     C     SetSkl12      begsr
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC' + lng
     C                   eval      Mbr = 'STS12'
      *
     C                   endsr
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
     C                   open      BRIDF                                50
     C     WSUSER        CHAIN     BRIDF                              60
     C                   IF        *IN60 = *OFF
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C* (bedre ressursmessig). MODUL må da være med comp. av dette pgm!!
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
     C                   open      HENOKW1                              50
     C                   END
     C                   eval      WSCLID = *BLANKS
     C                   eval      WSPALLV = *BLANKS
      *
     C                   endsr
      *=====================================================================******
      *  AVSLUTT
      *=====================================================================******
     C     EXIT          begsr
     C                   close     BRIDF
     C                   close     HENOKW1
     C                   eval      *inlr = *on
      *
     C                   endsr
