     A*%%TS  SD  20160803  090212  SVEIN       REL-V7R1M0  5770-WDS
*****A**************************************************************
     A* RETTINGER I DETTE PROGRAM:
PTFNRA*SEK SIG VERS  BESKRIVELSE...........................
"""""A*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
11XXXA* 01 YBC PTF11 OPSJON 4 = SLETT OPPLASTET DOKUMENT
*****A**************************************************************
     A*%%EC
     A                                      DSPSIZ(27 132 *DS4)
     A                                      HELP
     A*
     A          R SUBFIL                    SFL
     A*%%TS  SD  20160803  090212  SVEIN       REL-V7R1M0  5770-WDS
     A            ARTYPE         2A  H
     A            ARAVD          4S 0H
     A            AROPD          7S 0H
     A            ARUNDE        10A  H
     A            ARUSER        10A  H
     A            ARDATE         8S 0H
     A            ARKUND         8S 0H
     A            ARRFK         35A  H
     A            ARSTAT         1A  H
     A            ARLINK        40A  H
     A            WBANE        140A  H      TEXT('BANE')
     A            WSLINE       152A  H      TEXT('EKSTERN')
     A            WSLINK       152A  H      TEXT('EKSTERN')
S04  A*           WSNR           1A  B  6  2VALUES(' ' 'L' 'V' 'W')
S18  A*           WSNR           1A  B  6  2VALUES(' ' 'L' 'V' 'W' '2')
S01  A*           WSNR           1A  B  6  2VALUES(' ' 'L' 'V' 'W' '2' 'R')
S20  A*           WSNR           1A  B  6  2VALUES(' ' 'L' 'V' 'W' '2' 'R' '4')
S23  A*           WSNR           1A  B  6  2VALUES(' ' 'L' 'V' 'W' '2' 'R' '4' -
S23  A*                                     'H' 'I' 'J' 'K')
     A            WSNR           1A  B  6  2VALUES(' ' 'L' 'V' 'W' '2' 'R' '4' -
     A                                      'H' 'I' 'J' 'K' '3')
     A            WSTEX1        15A  O  6  4
     A            WSTEX2        90   O  6 20
     A            WSDATO         6Y 0O  6114EDTWRD('  .  .  ')
     A            ARTIME         6Y 0O  6123EDTWRD('  :  :  ')
     A            WSLINW       152A  O  7  4
     A*
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A*%%TS  SD  20160802  101221  SVEIN       REL-V7R1M0  5770-WDS
     A                                      SFLSIZ(0006)
     A                                      SFLPAG(0005)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                  1  2'SYSPED/ARC003'
     A                                      COLOR(BLU)
     A                                  1 16MSGCON(037 SHU0856 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            WSAVD          4S 0O  1 54
     A            WSOPD          7S 0O  1 59
     A                                  1125DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A  80                              1 68MSGCON(012 JOV1429 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A  80        URUNDE        10A  O  1 82
     A                                  2125TIME
     A                                      COLOR(BLU)
     A                                  3  2MSGCON(018 SHU0860 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  2  2MSGCON(016 SHU1036 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            WSANT          3Y 0O  2 19COLOR(RED)
     A                                      EDTCDE(4)
     A            ZRECNO         4S 0O  3126SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  4  2MSGCON(018 SHU1024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 22MSGCON(016 SHU1119 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  2MSGCON(130 SHU0858 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                  4 40MSGCON(026 SHU1616 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
      *
     A          R BUNN
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF03(03 'EXIT')
     A                                      CF12(12 'EXIT')
     A                                      CF05(05 'Frisk')
     A                                      CF10(10 'EPOST')
     A                                      CF08(08 'SØK')
     A                                      CF09(09 'TUR')
     A                                      CF11(11 'TØM')
     A                                      CF23(23 'SKILLEARK')
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            MSGNR          7A  P
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  3  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 41MSGCON(018 DIV0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 60MSGCON(018 SHU0847 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  4  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                              4  2MSGCON(018 SHU1033 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N30                              4 22MSGCON(018 SHU1291 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
      *
     A          R VAREL2
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF12(12 'Forrige')
     A                                      CF08(08 'SØK')
     A                                      CF03(12)
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  2  2MSGCON(031 SHU1405 *LIBL/SYMSGF)
     A            WSDATI         6  0I  2 36
     A            WSTIME         6S 0I  2 44
     A                                  3  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
      *
     A          R NYWRK
     A                                      CF03(03 'EXIT')
     A                                      CF08(08 'EXIT')
     A                                      CF05(05 'Frisk')
     A                                      CF09(09 'TUR')
     A                                      CF10(10 'EPOST')
     A                                      CF11(11 'TØM')
     A                                      CF12(03)
     A                                  1  2'SYSPED/ARC003'
     A                                      COLOR(BLU)
     A                                  1 16MSGCON(037 SHU0856 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            WSAVD          4S 0O  1 54
     A            WSOPD          7S 0O  1 59
     A                                  1 67DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A  80                              2 41MSGCON(012 JOV1429 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A  80        URUNDE        10A  O  2 54
     A                                  2 67TIME
     A                                      COLOR(BLU)
     A                                  3  2MSGCON(018 SHU0860 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 22MSGCON(016 SHU1036 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            WSANT          3Y 0O  3 40COLOR(RED)
     A                                      EDTCDE(4)
     A                                  4  2MSGCON(018 SHU1024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  2MSGCON(070 SHU0857 *LIBL/SYMSGF)
     A                                      COLOR(RED)
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(018 SHU1033 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 42MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 62MSGCON(018 SHU0847 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A                                 24  4MSGCON(018 SHU1291 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 22  2MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
      *
     A          R VARELI
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF12(12 'Forrige')
     A                                      SLNO(20)
     A                                      CLRL(05)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1  2MSGCON(077 SHU1017 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  2  2MSGCON(060 SHU1018 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            WSTYPE         2A  B  3  7
     A  71                                  DSPATR(RI)
     A            WSTEX1        15   O  3 10
     A            WSAVD          4Y 0B  3 26EDTCDE(4)
     A  70                                  DSPATR(RI)
     A            WSOPD          7Y 0B  3 31EDTCDE(4)
     A  70                                  DSPATR(RI)
     A            WSUNDE        10A  B  3 39
     A            ARUSER        10A  O  3 50
     A            ARDATE         8S 0O  3 61
     A            ARTIME         6S 0O  3 70
     A            WSKUND         8Y 0B  4  2EDTCDE(4)
     A  72                                  DSPATR(RI)
     A            KNAVN         30A  O  4 11
     A            WSRFK         35A  B  4 42CHECK(LC)
     A  30        MSGTXT        78A  O  5  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                              5  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
N01   *
N01  A          R BEKREFT
N01  A                                      WINDOW(*DFT 4 35 *NOMSGLIN)
N01  A                                  2  2'Vil du slette dette dokumentet?'
N01  A                                      COLOR(WHT)
N01  A            WSJN           1A  B  3  2DSPATR(UL)
N01  A                                  3  5'(J/N)'
