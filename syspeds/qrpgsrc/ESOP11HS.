      ****************************************************************************************
      * Hente-oppdrag,  Send SMS med URL                                                     *
      ****************************************************************************************
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
11XXX * 01 JOV PTF11 Probl (fra f.eks Toten) når mot.navn er tlfnr@esms.nu (nå TellusTalk)
11XXX * 02 sh  PTF11 variabel internbruker
11XXX * 03 JOV PTF11 SMS via SYGE99/webservice - trenger ikke ifsfil - STORE endringer/ikke merket
********************************************************************
     FHeadf     IF   E           K DISK
     FFIRM      IF   E           K DISK
     FFIRMARC   IF   E           K DISK
     FSYPARL3   IF   E           K DISK
     FTRACKF    O  A E           K DISK
      *__________
      * Variabler
      *""""""""""
     d usrid           S            128
     d pw              S             32
     d smsnr           S             16
     d body            S           1280
     d smsLen          S              5  0
     d smsAnt          S              1  0
     D LF              c                   x'25'
     D UP              C                   CONST('ABCDEFGHIJKLMNOPQRST-
     D                                     UVWXYZÆØÅ')
     D LO              C                   CONST('abcdefghijklmnopqrst-
     D                                     uvwxyzæøå')
     D                SDS
     D  ZUSER                254    263
      *
      *
     C                   EXSR      INIT
      *
     C     HEAKEY        CHAIN     HEADF                              33
     C     *IN33         ifeq      *OFF
     C                   EXSR      LAGSMS
     C                   EXSR      TTSR
     C                   endif
      *
     C                   MOVE      *ON           *INLR
     C                   RETURN
      *
      *
      *
      ******************************************************************************
     C     LAGSMS        BEGSR
      ******************************************************************************
      /free
        if UUH_L = *blanks;
          body='Lenke til sporings-App for '+ %trim(FIFT);
        else;
          body='Henting hos '+ %trim(HENAS) + ' for ' + %trim(FIFT);
        endif;
          body=%trim(body)+' http://'+ %trim(ARCPAE) + '/sycgip/estk550.pgm'
          +'?user='+%trim(SYVRDA)
          +'%26t='+%trim(UUSMS)
          +'%26snd='+%trim(SNDEVT);

        smsLen=%len(%trim(body));
        smsAnt=(smsLen/160)+0.9999;
        usrId='roger@systema.no';
        pw='straffe12';
        smsnr= uusms;
      /end-free
     C                   CALL      'SYGE99'
     C                   PARM                    USRID
     c                   PARM                    PW
     c                   PARM                    SMSNR
     c                   PARM                    BODY
      *
     C                   ENDSR
      *
      ******************************************************************************
     C     TTSR          BEGSR
      ******************************************************************************
     C                   MOVE      HEAVD         TTAVD
     C                   MOVE      HEOPD         TTOPD
     C                   MOVE      WDT           TTDATE
     C                   MOVE      WTM           TTTIME
     C                   MOVE      WDT           TTDATL
     C                   MOVE      WTM           TTTIML
     C                   MOVE      'CMS'         TTACTI
     C                   MOVE      ZUSER         TTUSER
     c                   if        UUUSER <> *blanks
     C                   MOVE      UUUSER        TTUSER
     c                   endif
     c                   if        UUH_L = *blanks
     C                   EVAL      TTTEXT ='Link to trackingApp SMS sent to '
     c                                     + UUSMS
     C                   EVAL      TTTEXL ='Lenke til sporingsApp SMS sent til '
     c                                     + UUSMS
     c                   else
     C                   EVAL      TTTEXT ='Coll. SMS sent to '
     c                                     + UUSMS
     C                   EVAL      TTTEXL ='HenteSMS sendt til '
     c                                     + UUSMS
     c                   endif
     c                   eval      TTEDRE = %trim(%editc(smsAnt:'Z'))
     c                   eval      TTDEPO = 'Len:'+%trimr(%editc(smsLen:'Z'))
     C                   MOVE      'S'           TTMANU
     C                   write     TRACKFR
      *
     C                   ENDSR
      *
      ******************************************************************************
     C     INIT          BEGSR
      ******************************************************************************
     C     *ENTRY        PLIST
     C                   PARM                    HEAVDa            4
     C                   PARM                    HEOPDa            7
     C                   PARM                    UUH_L             3
     C                   PARM                    UUSMS            15
     C                   PARM                    UUUSER           10
      *
     C                   MOVE      HEAVDa        HEAVD
     C                   MOVE      HEOPDa        HEOPD
     C                   Movel     HEAVD         AvdOpd           11
     C                   Move      HEOPD         AvdOpd
     c     HeaKey        klist
     c                   kfld                    HEAVD
     c                   kfld                    HEOPD
     c     sypak         klist
     c                   kfld                    sykd              5
     c                   move      'ESUSR'       SYKD
     c     sypak         chain     SYPARL3                            33
     C   33              EVAL      SYVRDA = 'NN'
     c                   move      *blanks       SNDEVT           50
     c                   if        UUH_L<>*blanks
     c                   eval      SNDEVT = %trim(%editc(HEAVD:'Z'))
     c                             +'*'+%trim(%editc(HEOPD:'Z'))
     c                             +'%26event='+UUH_L
     c                   endif
      *
     C     *LOVAL        SETLL     FIRM
     C                   READ      FIRM                                   33
      *
     C     FIFIRM        CHAIN     FIRMARC                            33
     C     UP:LO         XLATE     ARCPAE        ARCPAE
      *
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   ENDSR
      *
      *
