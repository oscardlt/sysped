      *********************************************************************
      *  After compiling this RPG MODULE,
      *  create the related program with the following command:
      *
CRTPG+*  CRTPGM SYSPED/STSW02Rm  MODULE(*LIBL/STSW02Rm *LIBL/INCGI)
CRTPGM*         ACTGRP(*NEW) AUT(*USE)
      *
      *
      *********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
11XXX * 01 JOV PTF11 Kan merke manuelt via avd_opd dersom ikke innland (og ikke kollimerket)
********************************************************************
      *
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
      *--------------------------------------------------------------------
      * Data base files
      *--------------------------------------------------------------------
     FBRIDF     if   e           k disk    usropn
     FHeadf     if   e           k disk    usropn
     FSTSCLI    if   E           K DISK    usropn
     FSTSOPD    if   E           K DISK    usropn
      *--------------------------------------------------------------------
      * Prototype defintions and standard system API error structure
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      *--------------------------------------------------------------------
      * Variables common to CGI programs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
      * Variables received from the input string
     D TurNr           s              8
     D avd             s              4
     D opd             s              7
     D WSPRO           s              8  0
     DTA1              s              4
     DTN1              s              8
     DwsPro1           s              8  0
     DTA2              s              4
     DTN2              s              8
     DwsPro2           s              8  0
     DTA3              s              4
     DTN3              s              8
     DwsPro3           s              8  0
     DBILN             s             10
     D UserId          s             10
     D User            s             10
     D VisAlle         s              1
     D SEMI            s              1
      * andre variabler
     D AntCli          s              5  0
     D Antmank         s              5  0
     D Fn              s             10
     D Lib             s             10
     D Mbr             s             10
      *
     D Spaces          s             12a   inz('&nbsp;&nbsp;')
     D                 DS
     D  HXSNDN                 1     20
     D  Sndnr                  1     20
      *--------------------------------------------------------------------
      * Prolog common to CGI programs
      * -Receive input from the remote browser
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prolog3
      *=====================================================================******
      * MAIN PROCESS LINE
      *=====================================================================******
      * Parse input string
     C                   eval      TURNR  = zhbgetvar('TURNR')
     C                   eval      WSPRO    = c2n2(TURNR)
     C                   eval      AVD    = zhbgetvar('AVD')
     C                   eval      scavd  = c2n2(AVD)
     C                   eval      OPD    = zhbgetvar('OPD')
     C                   eval      scopd  = c2n2(OPD)
     C                   eval      TN1      = zhbgetvar('TN1   ')
     C                   eval      TN2      = zhbgetvar('TN2   ')
     C                   eval      TN3      = zhbgetvar('TN3   ')
     C                   eval      TA1      = zhbgetvar('TA1   ')
     C                   eval      TA2      = zhbgetvar('TA2   ')
     C                   eval      TA3      = zhbgetvar('TA3   ')
     C                   eval      BILN   = zhbgetvar('BILN')
     C                   eval      VisAlle = zhbgetvar('Alle')
     C                   eval      userid = zhbgetvar('user')
     C                   eval      SEMI    = zhbgetvar('SEMI')
      * Set libl/open files
     C                   exsr      Init
      * Set output skeleton html member name
     C                   exsr      SetSkl
      * Read skeleton output html, etc.
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
      *------------------
      * Write sections of HTML.
      *
      * Hent/vis kolli-id
     C                   exsr      HCLI
      *
      *
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      *
     C                   close     BRIDF
     C                   close     headf
     C                   close     STSCLI
     C                   close     STSOPD
     C                   eval      *inlr = *on
     C                   RETURN
      *
     C***********************************************
     C     HCLI          BEGSR
     C***********************************************
      * Repeat UserId for the next invocation
     C                   callp     updhtmlvar('TURNR':turnr)
BODY C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updhtmlvar('USER':userid)
     C                   callp     updhtmlvar('ALLE':VisAlle)
     C                   callp     updhtmlvar('TN1   ':TN1   )
     C                   callp     updhtmlvar('TN2   ':TN2   )
     C                   callp     updhtmlvar('TN3   ':TN3   )
     C                   callp     updhtmlvar('TA1   ':TA1   )
     C                   callp     updhtmlvar('TA2   ':TA2   )
     C                   callp     updhtmlvar('TA3   ':TA3   )
     C                   callp     updhtmlvar('BILN':biln)
     C     CliKey        chain     StsOpd
     C     HEAKey        chain     headf
N01  c     SNDNR         ifeq      *blanks
N01  c                   move      HEAVD         HEAVDA            4
N01  c                   move      HEOPD         HEOPDA            7
N01  c                   eval      SNDNR = HEAVDA+'_'+HEOPDA
N01  c                   endif
     C                   callp     updhtmlvar('SNDNR':sndnr)
     C                   callp     updhtmlvar('AVD':
     C                             %trim(%editc(SCAVD:'3')))
     C                   callp     updhtmlvar('OPD':
     C                             %trim(%editc(SCOPD:'3')))
     C                   callp     updhtmlvar('AVS':HENAS)
     C                   callp     updhtmlvar('MOT':HENAK)
     C                   callp     updhtmlvar('MOTPNR':HEADK3)
     C                   callp     updhtmlvar('ANT':
     C                             %trim(%editc(HENT:'3')))
      *
     C                   callp     wrtsection('top')
      *
     c                   move      *zeros        antcli
     c                   move      *zeros        antmank
     C     cliKey        SETLL     STSCLI
     C     cliKey        READE     STSCLI                                 94
     C     *IN94         DOWEQ     '0'
     C                   add       1             antcli
     C     VisAlle       Ifne      'J'
     C     SCSTAT        Andne     ' '
     C                   goto      NEXT
     C                   end
     C     SCSTAT        IFEQ      ' '
     C                   add       1             antmank
     C                   end
     C                   callp     updhtmlvar('ANTN':
     C                             %trim(%editc(Antcli:'3')))
     C                   callp     updhtmlvar('CLID':SCCLID)
     C     scstat        ifeq      *blanks
     C     SEMI          andne     'Y'
     C                   callp     updhtmlvar('STAT':spaces)
     C                   else
     C                   callp     updhtmlvar('STAT':SCSTAT)
     C                   end
      * Ikke som klikkbar url dersom det er anonym bruker (feks NN)
     C     BIPO          ifeq      *blanks
     C     SEMI          andne     'Y'
     C                   callp     wrtsection('tablerowA')
     C                   else
     C                   callp     wrtsection('tablerow')
     c                   end
     C     Next          tag
     C     cliKey        READE     STSCLI                                 94
     C  N94              ENDDO
      * Hvis IKKE kollimerket gods - tilby Button for å Kvittere manuelt
     C     Antcli        IFEQ      *ZEROS
     C     SEMI          andne     'Y'
     C     sostat        ifeq      *blanks
     C                   callp     updhtmlvar('OSTAT':spaces)
     C                   callp     wrtsection('ManStat')
     C                   callp     wrtsection('ManButt')
     C                   else
     C                   callp     updhtmlvar('OSTAT':SOSTAT)
     C                   callp     wrtsection('ManStat')
     C                   end
     c                   end
      *
     C                   callp     updhtmlvar('ANTM':
     C                             %trim(%editc(Antmank:'3')))
     C                   callp     wrtsection('tablebot')
      *
     C                   ENDSR
      *
      *
      *=====================================================================******
     C     init          begsr
      *=====================================================================******
      *
     C                   open      BRIDF
     C     UserID        CHAIN     BRIDF                              30
      *
      * --------------------------------------
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C* (bedre ressursmessig). MODUL må da være med comp. av dette pgm!!
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
      *
     C                   open      STSCLI
     C                   open      STSOPD
     C                   open      headf
      *
      *  keys mm
     C     CLIKEY        KLIST
     C                   KFLD                    sctype
     C                   KFLD                    wspro
     C                   KFLD                    scavd
     C                   KFLD                    scopd
     C     HEAKEY        KLIST
     C                   KFLD                    scavd
     C                   KFLD                    scopd
     C                   endsr
      *
      *=====================================================================******
      * Set html output skeleton member before its read into memory
      *=====================================================================******
     C     SetSkl        begsr
      *------------------
      * Set html output skeleton member
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC'
     C     SEMI          IFNE      'Y'
     C                   eval      Mbr = 'STSW02M'
     C                   else
     C                   eval      Mbr = 'STSW02T'
     C                   end
      *------------------
     C                   endsr
