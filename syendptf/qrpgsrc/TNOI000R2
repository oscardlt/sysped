      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/TNOI000R2 MODULE(*LIBL/TNOI000R2 *LIBL/INCGI)
CRTPGM*        ACTGRP(*NEW) AUT(*USE)
      *
      *********************************************************************
      /copy SYSPEDS/qrpgsrcpy,hspecs
      /copy SYSPEDS/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FBRPARF    IF   E           K DISK    USROPN
     FFRISOK    IF   E           K DISK    USROPN
     FSADH      IF   E           K DISK    USROPN
     FSADHO     IF   E           K DISK    USROPN
     F                                     PREFIX(O2_)
     F                                     RENAME(SADHR:SADHRO2)
     FWRKINV    UF A E           K DISK    USROPN
     FARKLOGH4  IF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy SYSPEDS/qrpgsrcpy,prototypeb
      /copy SYSPEDS/qrpgsrcpy,usec
      /copy SYSPEDS/qrpgsrcpy,variables3
     ‚*  Prototype for 'JSONFIX'
     d jsonfix         pr                  extpgm('JSONFIX')
     d jsonstring_                         like(jsonstring)
     ‚*  Prototype for 'INCGI'
     d incgi           pr                  extproc('INCGI')
     d bistdl_                             like(bistdl)
     ‚*  Prototype for variabel program
     D Varpgm          Pr                  ExtPgm(BILIBL)

     d qcmdexc         Pr                  extpgm('QCMDEXC')
     d cmdline_                            like(cmdline)
     d cmdlen_                             like(cmdlen)

     d TNOI000R2A      Pr                  extpgm('TNOI000R2A')
     d UsrSpcname_                         like(UsrSpcname)
     d WinvNo_                             like(WinvNo)

     d TNOI000R2B      Pr                  extpgm('TNOI000R2B')
     d UsrSpcname_                         like(UsrSpcname)
     d WinvNo_                             like(WinvNo)

     D WSUSER          S             10
     D UsrSpcName      s             10
     D WinvNo          s             17
     D CmdLine         s            512
     D CmdLen          s             15  5
     D JSONstring      s          32000
     D Error           S            150
     D AntLest         S              9  0
     D Jn043           S              1
     d X               S             10  0
     d wspll           S              1
     d EPJN            S              1
     d pos             S              5U 0
     d wpabrid         s                   like(pabrid)
     d wpakunr         s                   like(pakunr)
     d wpasort         s                   like(pasort)
     d wsitdn          s                   like(o2_sitdn)
      *get input-string
      /copy syspeds/qrpgsrcpy,prolog3

      /free
         exsr ParseSr;
         exsr init;

       gethtmlifs(
       '/syspeddev/html/JSON.html':
       '/CGITAG/');
       wrtsection('top');

       //...........................................................................................
       //skriv JSON-Object start (alltid '{' + x ant param. m/komma mellom (IKKE komma etter siste))
       //...........................................................................................

        JSONstring='{'
        +'¬user¬ : ¬'+%trim(WSUSER)+'¬, '
        +'¬usrspcname¬ : ¬'+%trim(USRSPCNAME)+'¬, '
        +'¬inv¬ : ¬'+%trim(WINVNO)+'¬, '
        +'¬errMsg¬ : ¬'+%trim(Error)+'¬, ';

       //JSONfix escaper " i tekst,  for deretter å bytte ¬->"

       jsonfix ( jsonstring );

       updhtmlvar2('JSONstring'
                  :%addr(jsonstring)
                  :%len(jsonstring));
       wrtsection('JSONlin');

       //...........................................................................................
       // Skriv JSON-LISTE Start (en liste er EN parameter(fra [ til   )
       //..........................................................................................
       jsonstring ='"orderList" : [';

       updhtmlvar2('JSONstring'
                  :%addr(jsonstring)
                  :%len(jsonstring));
       wrtsection('JSONlin');

       if error = *blanks;
         exsr SelSr;
           exsr LesSr;
       endif;
       // ..........................................................................................
       // Skriv JSON-Liste/Array  Avslutning
       // ..........................................................................................
       jsonstring =']';
       updhtmlvar2('JSONstring'
                  :%addr(jsonstring)
                  :%len(jsonstring));
       wrtsection('JSONlin');
       // ..........................................................................................
       // Skriv JSON-string Avsluttning
       // ..........................................................................................
       jsonstring ='}';
       updhtmlvar2('JSONstring'
                  :%addr(jsonstring)
                  :%len(jsonstring));
       wrtsection('JSONlin');
         exsr Exit;

       //-----------------------------------------------------------------
       // Select fra filer
       //-----------------------------------------------------------------
       begsr     SelSr;
           Setll usrSpcName Wrkinv;
           reade usrSpcName Wrkinv;
             dow not %eof(Wrkinv);
                 delete wrkinvr;
               reade usrSpcName Wrkinv;
             enddo;

           tnoi000r2a ( UsrSpcName : WinvNo );
           tnoi000r2b ( UsrSpcName : WinvNo );
       endsr;
       //-----------------------------------------------------------------
       // hent input
       //-----------------------------------------------------------------
       begsr     ParseSr;
         wsuser  = zhbgetvar('USER');
         usrspcname = zhbgetvar('USRSPCNAME');
         winvno  = zhbgetvar('inv');

        endsr;
        // __________________________________________________
        // Læs array                                    LESSR
        // """"""""""""""""""""""""""""""""""""""""""""""""""
           begsr LesSr;
           Setll usrSpcName Wrkinv;
           reade usrSpcName Wrkinv;
           dow not %eof(Wrkinv);

           chain (wrkavd: wrktdn) SADH;

           chain (wrkavd: wrktdn: '*CR') FRISOK;
           if not %found;
              fssok = *blanks;
           endif;

        // Finnes omberegning ?

           wsitdn = wrktdn * -1;
           chain (wrkavd: wsitdn) SADHO;
           if not %found;
              O2_SIST = *BLANKS;
              O2_SIMF = *BLANKS;
              O2_SITLL = *BLANKS;
              O2_SIDT = 0;
           else;
              if O2_SITLL = sitll;
                O2_SITLL = *BLANKS;
              endif;
           endif;

        // Er e-post sendt ?
           chain (wrkavd: wrktdn) ARKLOGH4;
           if %found;
              epjn = 'E';
           else;
              epjn = *BLANKS;
           endif;

        // Videre hvis ikke noen feil !

           if error = *blanks;

        // Lægg ut Json stræng
           AntLest = AntLest + 1;

        // Skriv en JSON-Array-linje pr menypunkt - komma før hvert nytt element

       if AntLest=1;
          JSONstring=*blanks;
          else;
          JSONstring=', ';
          endif;
       JSONstring=%trim(JSONstring)+'{'
          +'¬avd¬ : ¬'+%trim(%editc(SIAVD:'Z'))+'¬, '
          +'¬opd¬ : ¬'+%trim(%editc(SITDN:'Z'))+'¬, '
          +'¬tst¬ : ¬'+%trim(SI0035)+'¬, '
          +'¬sitll¬ : ¬'+%trim(SITLL)+'¬, '
          +'¬sitle¬ : ¬'+%trim(SITLE)+'¬, '
          +'¬aart¬ : ¬'+%trim(SIDTY)+'¬, '
          +'¬sg¬ : ¬'+%trim(SISG)+'¬, '
          +'¬datum¬ : ¬'+%trim(%editc(SIDT:'Z'))+'¬, '
          +'¬sivkb¬ : ¬'+%trim(%editc(SIVKB:'4'))+'¬, '
          +'¬status¬ : ¬'+%trim(SIST)+'¬, '
          +'¬avsNavn¬ : ¬'+%trim(SINAS)+'¬, '
          +'¬motNavn¬ : ¬'+%trim(SINAK)+'¬, '
          +'¬sign¬ : ¬'+%trim(SIGN)+'¬, '
          +'¬simi¬ : ¬'+%trim(SIMI)+'¬, '
          +'¬h_xref¬ : ¬'+%trim(FSSOK)+'¬, '
          +'¬invno¬ : ¬'+%trim(wrksif)+'¬, '
          +'¬epjn¬ : ¬'+%trim(epjn)+'¬, '
          +'¬o2_sist¬ : ¬'+%trim(o2_sist)+'¬, '
          +'¬o2_simf¬ : ¬'+%trim(o2_simf)+'¬, '
          +'¬o2_sitll¬ : ¬'+%trim(o2_sitll)+'¬, '
          +'¬o2_sidt¬ : ¬'+%trim(%editc(o2_sidt:'Z'))+'¬}';
         jsonfix (JSONstring);
       updhtmlvar2('JSONstring'
                  :%addr(jsonstring)
                  :%len(jsonstring));
         wrtsection ('JSONlin');

        endif;
           reade usrSpcName Wrkinv;
        enddo;
       endsr;
       //********************************************************
       begsr init;
       //********************************************************
         open bridf;
         // Test innlogging
         chain wsuser bridf;

         if %found;
           if bilibl = *blanks;
             incgi ( bistdl );
           else;

             Monitor;
                   Varpgm();
                 On-Error;
                   //
                 EndMon;

           endif;
         else;

           error = 'Invalid userID';
         endif;
         open brparf;
         open frisok;
         open wrkinv;
         open sadh;
           cmdline = 'OVRDBF FILE(SADHO) TOFILE(SADH)';
           cmdlen = %len(%trim(cmdline));
           qcmdexc ( cmdline : cmdlen );
         open sadho;
         open arklogh4;

       endsr;
       //=====================================================================
       // Close output html and quit
       //=====================================================================
       begsr exit;
         // Lukk fil
         close bridf;
         close brparf;
         close frisok;
         close wrkinv;
         close sadh;
         close sadho;
         close arklogh4;

         wrtsection('*fini');
         *inlr = *on;
         return;
       endsr;
      /end-free
