********************************************************************
      * Sjekk om unik ID tilsendt i URL stememr med oppdraget/turen
      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/TJGETUNID MODULE(*LIBL/TJGETUNID *LIBL/INCGI)
CRTPGM*        ACTGRP(*NEW) AUT(*USE)
      *
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
12XXX * 17 jov PTF12 'SGAKX' arkiv-koder ekspressfortolling
********************************************************************
      *********************************************************************
      /copy SYSPEDS/qrpgsrcpy,hspecs
      /copy SYSPEDS/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy SYSPEDS/qrpgsrcpy,prototypeb
      /copy SYSPEDS/qrpgsrcpy,usec
      /copy SYSPEDS/qrpgsrcpy,variables3
     D user            S             10
     D avd             S              4
     D opd             S              7
     D tur             S              8
     D unId            S             10
     D Error           S            150
     D JSONstring      s          32000

      *get input-string
      /copy syspeds/qrpgsrcpy,prolog3
      *
     C                   EXSR      PARSE
     C                   EXSR      INIT
     c                   if        error = *blanks
     c                   exsr      MAIN
     c                   endif

     C                   CALLP     gethtmlifs(
     C                             '/syspeddev/html/JSON.html':
     C                             '/CGITAG/')
     C                   CALLP     wrtsection('top')
      /free
        JSONstring='{'
        +'¬user¬ : ¬'+%trim(USER)+'¬, '
        +'¬errMsg¬ : ¬'+%trim(Error)+'¬,'
        +'¬unId¬ : ¬'+%trim(unId)+'¬}';
      /end-free
     c                   call      'JSONFIX'
     c                   parm                    JSONstring
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')

     C                   EXSR      EXIT
      *
     C                   EVAL      *INLR=*ON
     C                   RETURN
      *
      **************************************************
     C     MAIN          BEGSR
      **************************************************
      *
     c                   call      'GETUNID'
     C                   parm                    UUAVD             4 0
     C                   parm                    UUOPD             7 0
     C                   parm                    UUTUR             8 0
     C                   parm                    USER
     C                   parm                    unId             10
      *
     C                   ENDSR
      *
      ************************************************************
     C     Parse         BEGSR
      ************************************************************
     C                   EVAL      user = zhbgetvar('user')
     C                   EVAL      avd  = zhbgetvar('avd')
N02  C                   EVAL      UUAVD= c2n2(avd)
     C                   EVAL      opd  = zhbgetvar('opd')
N02  C                   EVAL      UUOPD= c2n2(opd)
     C                   EVAL      tur  = zhbgetvar('tur')
N02  C                   EVAL      UUTUR= c2n2(tur)
     C                   EVAL      unId = zhbgetvar('unId')
     C                   ENDSR
      ************************************************************
     C     INIT          BEGSR
      ************************************************************
     C                   OPEN      BRIDF
     C     USER          CHAIN     BRIDF                              50
      * En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        IFEQ      *blanks
     C                   CALLB     'INCGI'
     C                   PARM                    BISTDL
     C                   ELSE
      * Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   CALL      BILIBL
     C                   ENDIF
     C   50              eval      Error = 'Invalid userID'

     C                   ENDSR
      *
      ************************************************************
     C     Exit          BEGSR
      ************************************************************
     C                   callp     wrtsection('*fini')
     C                   CLOSE     BRIDF
     C                   ENDSR
