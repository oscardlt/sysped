     A                                      REF(HEADF)
     A* BRINGTMS-PROSJEKT
      *_____________________________________________
      * Format for ORDRESPESIFIKASJON
      *"""""""""""""""""""""""""""""""""""""""""""""
     A          R TKJØP
E03  A N80        KFMT1         75      1  1
E03  A N80        KFMT2         75      1  1
     A  47        HOHT1         80A     1  1HIGHLIGHT
     A  47        HOHT2         80A     2  1HIGHLIGHT
     A  47        HOHT3         80A     3  1HIGHLIGHT
     A  47        HOHT4         80A     4  1HIGHLIGHT
     A  47        HOHT5         80A     5  1HIGHLIGHT
     A  47        HOHT6         80A     6  1HIGHLIGHT
     A  47        HOHT7         80A     7  1HIGHLIGHT
     A            KNAVN     R          11 11REFFLD(KNAVN CUNDF)
     A                                 11 48'******************************'
     A                                      HIGHLIGHT
     A            ADR1      R          12 11REFFLD(ADR1 CUNDF)
     A N26N70N79                       12 48MSGCON(30 JOV0553 SYMSGF)
     A                                      HIGHLIGHT
     A N26 70                          12 48'*    INVOICE SPECIFICATION   *'
     A                                      HIGHLIGHT
     A N26 79                          12 48'*    RECHNUNGSSPESIFIKATION  *'
     A  26                             12 48'*'
     A                                      HIGHLIGHT
     A  26        XKOHE         28     12 49
     A                                      HIGHLIGHT
     A  26                             12 77'*'
     A                                      HIGHLIGHT
     A            ADR2      R          13 11REFFLD(ADR2 CUNDF)
     A                                 13 48'******************************'
     A                                      HIGHLIGHT
     A N37        POSTNR    R          14 11REFFLD(POSTNR CUNDF)
     A                                      DLTEDT
     A N37        ADR3      R          14 17REFFLD(ADR3 CUNDF)
S03  A*N38        SYPOGE         9     14 11
S03  A*N38        ADR3B     R          14 22REFFLD(ADR3 CUNDF)
N03  A N38        SYPOGN        33     14 11
     A  37 38     ADR3C     R          14 11REFFLD(ADR3 CUNDF)
      * PRØVEKJØRING  (KOPI BLIR IKKE SPES.MERKET PÅ SPES.DELEN...)
     A  28                             14 48MSGCON(24 JOV0541 SYMSGF)
     A                                      HIGHLIGHT
      * PÅ OPPDRAGSTIDSPKT - PRESSISERE AT FAKT. KOMMER SENERE
     A  29N70N79                       14 45MSGCON(35 JOV0554 SYMSGF)
     A                                      HIGHLIGHT
     A  29 70                          14 45'NOT PAYABLE! INV. WILL BE SENT! '
     A                                      HIGHLIGHT
     A  29 79                          14 45'KEINE RECNUNG! RECN. WIRT GESANDT!'
     A                                      HIGHLIGHT
      * hvis ikke prøvekj. eller på oppdragstispunkt - skriv fakt.nr.
     A N28N29
     AAN70N79                          14 48MSGCON(12 DIV0287 SYMSGF)
     A N28N29 70                       14 48'Invoice no....:'
     A N28N29 79                       14 48'Rechnungsnr...:'
     A N28N29     XFAKT          7  0  14 71EDTCDE(3) HIGHLIGHT
     A            KLKNVN    R          15 11REFFLD(KLKNVN KODTLK)
     A N70N79                          15 48MSGCON(15 DIV1668 SYMSGF)
     A  70                             15 48'Customer no...:'
     A  79                             15 48'Kunden Nr.....:'
     A            KUNDNR    R          15 70REFFLD(KUNDNR CUNDF)
     A                                      EDTCDE(Z)
      * Når fra oppdrag kall dato utskr.dato/ikke faktdato
     A N70N79N29                       16 48MSGCON(15 DIV1666 SYMSGF)
     A  70N29                          16 48'Date of invoice'
     A  79N29                          16 48'Rechn.datum...:'
     A N70N79 29                       16 48MSGCON(15 JOV0555 SYMSGF)
     A  70 29                          16 48'Printed date..:'
     A  79 29                          16 48'Datum geschrib:'
     A            IDAGX          6S 0  16 70EDTCDE(Y)
      * Når fra oppdrag er det ikke snakk om forfallsdato
     A N70N79N29                       17 48MSGCON(15 DIV1665 SYMSGF)
     A  70N29                          17 48'Date due......:'
     A  79N29                          17 48'Verfallstag...:'
     A N39N29     XFFDT          6S 0  17 70EDTCDE(Y)
     A  39N70                          17 71'Kontant'
     A  39 70                          17 71'Cash'
     A N70N79                          18 48MSGCON(15 DIV1669 SYMSGF)
     A  70                             18 48'Our reference.:'
     A  79                             18 48'Unsere Ref....:'
     A            HEAVD     R          18 64
     A                                 18 68'/'
     A            HEOPD     R          18 69
     A                                 18 76'/'
     A            HESG      R          18 77
     A N70N79                          19 48MSGCON(15 DIV1670 SYMSGF)
     A  70                             19 48'Date of disp..:'
     A  79                             19 48'Auftrag vom...:'
     A            XDT            6S 0  19 70EDTCDE(Y)
     A N70N79N53                       20  2MSGCON(13 DIV1659 SYMSGF)
     A  70N53                          20  2'Agentref....:'
     A  79N53                          20  2'Agentref....:'
     A            HERFA     R          20 16
     A            HERFA15       15     20 16
     A N70N79                          20 48MSGCON(15 DIV1671 SYMSGF)
     A  70                             20 48'Tripno........:'
     A  79                             20 48'Tour Nr.......:'
     A            HEPRO     R          20 70
     A N70N79N52                       21  2MSGCON(13 DIV1663 SYMSGF)
     A  70N52                          21  2'Shipped pr..:'
     A  79N52                          21  2'Mit.........:'
     A            HEHAWB         9     21 16
     A N70N79N54                       21 48MSGCON(15 DIV5021 SYMSGF)
     A  70N54                          21 48'Roundtrip no..:'
     A  79N54                          21 48'Rundtour Nr...:'
     A N54        TRRUND         8  0  21 70
     A N70N79                          22  2MSGCON(13 DIV1661 SYMSGF)
     A  70                             22  2'Terms of del:'
     A  79                             22  2'Frankatur...:'
     A N70        KFRNTX    R          22 16REFFLD(KFRNTX KODTFR)
     A  70        KFRTTX    R          22 16REFFLD(KFRTTX KODTFR)
     A    N70N79
     A  70                             22 48'Billno........:'
     A  79                             22 48'Reg.nr........:'
     A            HEGN      R          22 64
     A N79N56N70                       23  2MSGCON(13 DIV1662 SYMSGF)
     A N52N56 70                       23  2'Container...:'
     A N52N56 79                       23  2'Container...:'
     A N52N56     HETRC     R          23 16
     A N52N56     HETRCN    R          23 20
     A                                 24  2' '
      * FriE SØKEVEIER
     A          R TKJØFS                    SPACEB(1)
     A            KFSOTX        30         2
     A                                    32':'
     A            FSSOK         35        35
      * Fritekstlinjer mellom heading og Avs/mottaker del.
     A          R TFRIEL                    SPACEB(1)
     A            FRTTXT        70         2
      * Avs/mottaker del.
     A          R THEAT
     A                                     1' _______________________'
     A                                      SPACEB(1)
     A                                    25'________________________'
     A                                    49'________________________'
     A                                    73'________'
     A N70N79                              2MSGCON(13 DIV1651 SYMSGF)
     A                                      SPACEB(1)
     A  70                                 2'Shipper.....:'
     A                                      SPACEB(1)
     A  79                                 2'Absender....:'
     A                                      SPACEB(1)
     A N70N79                             42MSGCON(13 DIV1652 SYMSGF)
     A  70                                42'Consignee...:'
     A  79                                42'Empfanger...:'
     A                                     1' _______________________'
     A*                                     SPACEB(1)
     A                                    25'________________________'
     A                                    49'________________________'
     A                                    73'________'
     A*                                    2'!'
     A*                                     SPACEB(1)
     A*                                   41'!'
     A*                                   78'!'
     A*                                    2'!'
     A*                                     SPACEB(1)
     A            HENAS         30         8
     A                                      SPACEB(2)
     A*                                   41'!'
     A            HENAK         30        48
     A*                                   78'!'
     A*                                    2'!'
     A*                                     SPACEB(1)
     A            HEADS1        30         8
     A                                      SPACEB(1)
     A*                                   41'!'
     A            HEADK1        30        48
     A*                                   78'!'
     A*                                    2'!'
     A*                                     SPACEB(1)
     A            HEADS2        30         8
     A                                      SPACEB(1)
     A*                                   41'!'
     A            HEADK2        30        48
     A*                                   78'!'
     A*                                    2'!'
     A*                                     SPACEB(1)
     A            HEADS3        30         8
     A                                      SPACEB(1)
     A*                                   41'!'
     A            HEADK3        30        48
     A*                                   78'!'
     A*                                    2'!'
     A*                                     SPACEB(1)
     A            SLAND         30         8
     A                                      SPACEB(1)
     A*                                   41'!'
     A            KLAND         30        48
     A*                                   78'!'
     A                                     1' _______________________'
     A                                      SPACEB(1)
     A                                    25'________________________'
     A                                    49'________________________'
     A                                    73'________'
     A          R TEAD22                    SPACEB(2)
     A N70N79                              1MSGCON(77 DIV1679 SYMSGF)
     A  70                                 1' No.psc Desc.of goods             +
     A                                             Weight    M3/LM Ch.Weight  M+
     A                                      arks'
     A  79                                 1' Anzahl Warenart                  +
     A                                            Gewicht    M3/LM   Fr.gewi  Z+
     A                                      eichen'
     A                                     1' _______________________'
     A                                      SPACEB(1)
     A                                    25'________________________'
     A                                    49'________________________'
     A                                    73'________'
     A            HENT      R              1EDTCDE(Z)
     A                                      SPACEB(1)
     A            HEVS1     R              9
     A            HEVKT     R             36EDTCDE(Z)
     A            HEM3      R             46EDTCDE(J)
     A            HEFBV     R             55EDTCDE(Z)
     A            HEGM1     R             66
     A                                     2' '
     A                                      SPACEB(1)
     A            HEVS2     R              9
     A            HELM      R             49EDTCDE(J)
     A            HEGM2     R             66
N51  A          R TEAD22B                   SPACEB(1)
     A                                     2' '
     A                                      SPACEB(2)
     A N70N79                             17MSGCON(30 DIV1680 SYMSGF)
     A N70N79                             69MSGCON(12 DIV1681 SYMSGF)
     A  70                                32'D E S C R I P T I O N'
     A  70                                71'AMOUNT'
     A  79                                22'K O S T E N B E R E C H N U N G'
     A  79                                71'KOSTEN'
     A                                     1' '
     A                                      SPACEB(1)
N05  A          R VARELI0                   SPACEB(1)
N05  A N70N79                              2MSGCON(77 SHU1529 SYMSGF)
N05  A  70                                 2'Marks/No            NO PSC'
N05  A  79                                 2'Marks               Anzahl'
N05  A  70                                28' Goods type/description'
N05  A  79                                28' Typ Warenart'
N05  A  70                                55'  Weight      M3   Loadm'
N05  A  79                                55'  Gewicht     M3   Loadm'
N05  A          R VARELI1
N05  A N60N63                              1' _______________________'
N05  A N60N63                             25'________________________'
N05  A N60N63                             49'________________________'
N05  A N60N63                             73'________'
N05  A          R VARELI                    SPACEB(1)
N05  A            R33MRK        18         2
N05  A            R34ANT         7  0     21EDTCDE(Z)
N05  A            R35VSL        23        29
N05  A            R36VKT         9  0     54EDTCDE(Z)
N05  A            FVVOL          7  3     64EDTCDE(4)
N05  A            R37LM          4  2     73EDTCDE(4)
N05   *
N05  A          R VARELIT                   SPACEB(1)
N05  A            HENT      R             21EDTCDE(Z)
N05  A            HEVKT     R             54EDTCDE(Z)
N05  A            HEM3      R             64EDTCDE(4)
N05  A            HELM      R             73EDTCDE(4)
      *_______________________________
      * Format for faktura-varelinjer.
      *"""""""""""""""""""""""""""""""
     A          R TDET                      SPACEB(1)
     A            FAVT      R             17REFFLD(FAVT FAKT)
     A N70N79     KGENOT    R             41REFFLD(KGENOT KODTGE)
     A  70        KGEENT    R             41REFFLD(KGEENT KODTGE)
     A  79        KG2TYT    R             41REFFLD(KG2TYT KODTG2)
     A  36        XVAL           3        59
     A  36        XPFNOK        13  2     60EDTCDE(J)
     A     36     XMVAK          1        78
      *_________________________________________________
      * Format for faktura-total. without Norwegian giro.
      *""""""""""""""""""""""""""""""""""""""""""""""""""
      *
     A          R TTOTCM                    SPACEB(1)
     A N70N79                              2MSGCON(24 DIV0296 SYMSGF)
     A  70                                 2'Basis for VAT (sum of  )'
     A  79                                 2'MWS-basis (summe von   )'
     A            XMVAK          1        24
     A            XMVAGR         9  2     28EDTCDE(J)
     A N70N79                             41MSGCON(4 JOV0089 SYMSGF)
     A  70                                41'VAT '
     A  79                                41'MWS '
     A            MVAPRO         4  2     46EDTCDE(4)
     A                                    52'%'
     A            XVAL           3A       59
     A            XPMOMS         8  2     67EDTCDE(J)
     A          R TTOTC                     SPACEB(1)
     A N70N79                              2MSGCON(17 SHU1120 SYMSGF)
     A  79                                 2'Taxfree total:  '
     A  70                                 2'Taxfree total:  '
     A            XMVAFR         9  2     28EDTCDE(J)
     A                                      SPACEA(1)
     A N70N79                             45MSGCON(12 DIV1685 SYMSGF)
     A  70                                45'Total:'
     A  79                                45'Gesamtbetrag'
     A            XVAL1          3        57
     A            XBRUTT        13  2     60EDTCDE(J)
     A                                     2' '
     A                                      SPACEB(1)
     A                                    45'================================'
     A  44        HOBT1         80         1SPACEB(1)
     A  44        HOBT2         80         1SPACEB(1)
     A  44        HOBT3         80         1SPACEB(1)
