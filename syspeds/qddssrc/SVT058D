     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      ALTHELP
     A                                      HELP
      *
     A          R SVT058DA                  SFL
     A            ZOP            1A  I  8  2VALUES(' ' '2' '4' '5')
     A            SVLV_KD        3A  O  8  4
     A            SVLV_TR        1A  O  8  9
     A            SVLV_TR2       1A  O  8 14
     A            SVLV_FS        1A  O  8 21
     A            SVLV_FSP       5Y 3O  8 23EDTCDE(2)
     A            SVLV_FS2       1A  O  8 32
     A            SVLV_FS2P      5Y 3O  8 34EDTCDE(2)
     A            SVLV_OK        1A  O  8 51
     A            SVLV_KR        1A  O  8 61
     A            SVLV_AR        1A  O  8 71
      *
     A          R SVT058DB                  SFLCTL(SVT058DA)
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0012)
     A  91N93N94                            ROLLUP(25 'Rollup')
     A  91N93N94                            ROLLDOWN(26 'Rolldown')
     A                                      CF03(03 'Gå ut')
     A                                      CF04(04 'Søk')
     A                                      CF05(05 'Refresh')
     A                                      CF06(06 'NY')
     A                                      CF12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/SVT058'
     A                                      COLOR(BLU)
     A                                  1 16'DA'
     A                                      COLOR(BLU)
     A                                  1 30'Underhåll av TDS Import'
     A                                      COLOR(WHT)
     A                                  1 64DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  1 73TIME
     A                                      COLOR(BLU)
     A                                  2 34'Leveransvillkår'
     A                                      COLOR(WHT)
     A            ZRECNO         4S 0O  2 68SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  4  2'Vælj option eller tryck Enter.'
     A                                      COLOR(BLU)
     A                                  5  4'2=Ændra   4=Radera'
     A                                      COLOR(BLU)
     A                                  6  4'Kod'
     A                                      COLOR(WHT)
     A                                  6  8'Transport'
     A                                      COLOR(WHT)
     A                                  6 20'Försäkring'
     A                                      COLOR(WHT)
     A                                  6 47'Övr_kost'
     A                                      COLOR(WHT)
     A                                  6 57'Kassarab'
     A                                      COLOR(WHT)
     A                                  6 67'Annan_rab'
     A                                      COLOR(WHT)
     A                                  7  8'Stat'
     A                                      COLOR(WHT)
     A                                  7 13'Tull'
     A                                      COLOR(WHT)
     A                                  7 20'Stat'
     A                                      COLOR(WHT)
     A                                  7 26'Tull'
     A                                      COLOR(WHT)
      *
     A          R SVT058DC
     A                                      OVERLAY
     A                                 23  2'F3=Avsluta'
     A                                      COLOR(BLU)
     A                                 23 15'F4=Søk'
     A                                      COLOR(BLU)
     A                                 23 24'F6=Skapa'
     A                                      COLOR(BLU)
     A                                 23 35'F12=Avbryt'
     A                                      COLOR(BLU)
      *
     A          R SVT058DD
     A                                      OVERLAY
     A                                      CF03(03 'Gå ut')
     A                                      CF04(04 'Søk')
     A                                      CF12(12 'Forrige')
     A            ZVLV_KD        3A  B 21  4
     A  31                                  DSPATR(RI)
     A  31                                  DSPATR(PC)
     A            ZVLV_TR        1A  B 21  9VALUES(' ' 'J' 'N')
     A                                      DSPATR(PC)
     A            ZVLV_TR2       1A  B 21 14VALUES(' ' 'J' 'N')
     A            ZVLV_FS        1A  B 21 21VALUES(' ' 'J' 'N' 'P')
     A            ZVLV_FSP       5Y 3B 21 23EDTCDE(2)
     A            ZVLV_FS2       1A  B 21 32VALUES(' ' 'J' 'N' 'P')
     A            ZVLV_FS2P      5Y 3B 21 34EDTCDE(2)
     A            ZVLV_OK        1A  B 21 51VALUES(' ' 'J' 'N')
     A            ZVLV_KR        1A  B 21 61VALUES(' ' 'J' 'N')
     A            ZVLV_AR        1A  B 21 71VALUES(' ' 'J' 'N')
     A                                 23  2'F3=Avsluta'
     A                                      COLOR(BLU)
     A                                 23 15'F4=Søk'
     A                                      COLOR(BLU)
     A                                 23 24'F12=Avbryt'
     A                                      COLOR(BLU)
      *
     A          R SVT058DE
     A                                 20  2' '
     A                                 21  2' '
      *
     A          R SVT058DØ
     A                                      OVERLAY
     A            ZMSGNO         7A  P
     A            ZMSGTX        79   O 24  2MSGID(&ZMSGNO SYMSGF)
     A                                      COLOR(WHT)
