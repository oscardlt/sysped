     FEUR11D    CF   E             WORKSTN
     F                                     SFILE(EUR11DB:ZRECNO)
     FEUROOHL1  IF   E           K DISK
     F                                     RENAME(EUROOHR:EUROOHR1)
     FEUROOH    UF   E           K DISK
     FEUROOL    UF   E           K DISK
      *//////////////
      *  Hodelogikk /
      *//////////////
     C                   EXSR      SRA
     C                   EXSR      SRB
     C                   MOVE      '1'           *INLR
      *////////////////////////////////////////////////////////////
      *  Initiering                                           SRA /
      *////////////////////////////////////////////////////////////
     C     SRA           BEGSR
      *___________________
      * Definiere Workfelt
      *"""""""""""""""""""
      *__________________
      * Definiere Nøkkler
      *""""""""""""""""""
     C     KEYA          KLIST
     C                   KFLD                    EUROOH6
     C                   KFLD                    EUROOH5
      *_____________
      * Init av felt
      *"""""""""""""
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Bearbeide Formater                                   SRB /
      *////////////////////////////////////////////////////////////
     C     SRB           BEGSR
     C     SRB1          TAG
      *______________
      * Tømme Subfile
      *""""""""""""""
      *
     C                   MOVE      '1'           *IN93
     C                   WRITE     EUR11DC
     C                   MOVEA     '000'         *IN(93)
     C                   Z-ADD     *ZERO         ZRECNO
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     *LOVAL        SETLL     EUROOHL1
     C                   READ      EUROOHL1                               94
     C     *IN94         DOWEQ     '0'

     C                   ADD       1             ZRECNO
     C                   WRITE     EUR11DB

     C                   READ      EUROOHL1                               94
     C                   ENDDO
      *_______________
      * Behandle bilde
      *"""""""""""""""
     C     SRB5          TAG
      *
     C                   MOVEA     '11'          *IN(91)
     C  N30              MOVE      'CCC0000'     ZMSGNO
     C                   WRITE     EUR11DØ
     C                   WRITE     EUR11DD
     C                   EXFMT     EUR11DC
      *
     C     *IN12         CABEQ     '1'           SRB1
     C     *IN03         IFEQ      '0'
      *
     C                   MOVEA     '00'          *IN(91)
      *_____________
      * F5=Frisk opp
      *"""""""""""""
     C     *IN05         CABEQ     '1'           SRB1
      *_______
      * Valg ?
      *"""""""
     C                   MOVE      '0'           *IN52
     C                   MOVE      '0'           *IN30
     C                   MOVE      '0'           *IN95
     C     *IN52         DOWEQ     '0'
      *
     C                   READC     EUR11DB                                52
     C     *IN52         IFEQ      '0'
     C     ZOP           CASEQ     '4'           SRBD
     C     ZOP           CASEQ     '5'           SRBE
     C                   ENDCS
     C                   ENDIF
      *
     C  N30
     CANN52              END
      *
     C     *IN03         CABEQ     '1'           SRB9
     C                   END
     C  N03              GOTO      SRB1
      *
     C     SRB9          ENDSR
      *////////////////////////////////////////////////////////////
      *  Slett ordre                                         SRBD /
      *////////////////////////////////////////////////////////////
     C     SRBD          BEGSR
     C     KEYA          CHAIN     EUROOH                             51
     C                   IF        *IN51=*OFF
     C                   DELETE    EUROOHR
     C                   EVAL      *IN53=*OFF
     C     KEYA          SETLL     EUROOL
     C     KEYA          READE     EUROOL                                 53
     C     *IN53         DOWEQ     *OFF
     C                   DELETE    EUROOLR
     C     KEYA          READE     EUROOL                                 53
     C                   ENDDO
     C                   ENDIF
      *_________________
      * Oppdatere SUBFIL
      *"""""""""""""""""
     C                   MOVE      *BLANKS       ZOP
     C                   UPDATE    EUR11DB
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Vise hele ordren.                                   SRBE /
      *////////////////////////////////////////////////////////////
     C     SRBE          BEGSR
     C     SRBE1         TAG
     C                   MOVE      'CCC0000'     ZMSGNO
     C                   EXFMT     EUR11DE
      * F7=Vis varelinjer
     C                   IF        *IN07
     C                   CALL      'EUR12R'
     C                   PARM                    EUROOH6
     C                   PARM                    EUROOH5
     C                   ENDIF
      * F8=Arbeide med svar
     C                   IF        *IN08
     C                   CALL      'EUR13R'
     C                   PARM                    EUROOH6
     C                   PARM                    EUROOH5
     C                   CALL      'EUR14R'
     C                   PARM                    EUROOH6
     C                   PARM                    EUROOH5
     C                   GOTO      SRBE1
     C                   ENDIF
      * F9=Send svar
     C                   IF        *IN09
     C                   IF        EUROOH5B=*BLANKS
     C                   CALL      'EUR16R'
     C                   PARM                    EUROOH6
     C                   PARM                    EUROOH5
     C                   ENDIF
     C                   ENDIF
      *_________________
      * Oppdatere SUBFIL
      *"""""""""""""""""
     C                   MOVE      *BLANKS       ZOP
     C                   UPDATE    EUR11DB
     C                   ENDSR
