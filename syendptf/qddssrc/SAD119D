     A                                      DSPSIZ(27 132 *DS4)
     A                                      PRINT
     A                                      HELP
     A                                      ALTHELP
      *
     A          R WINDOWSZ
     A                                      WINDOW(*DFT 17 122 *NOMSGLIN)
     A                                      OVERLAY
      *
     A          R BILDE01
     A                                      WINDOW(WINDOWSZ)
     A                                      CF03(03 'Avbryt')
     A                                      CF04(04 'Søking')
     A N47                                  CF09(09 'Hent UUID')
     A                                      CA12(12 'Avbryt')
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1  2'SYSPED/SAD119R'
     A                                      COLOR(BLU)
     A                                  1 20'Arbeid med kurermanifest default'
     A                                      COLOR(WHT)
     A                                  2  2'Avd:'
     A            WSAVD          4Y 0B  2  7DSPATR(UL)
     A  49
     AO 48                                  DSPATR(PR)
     A N99
     AAN48                                  COLOR(YLW)
     A  99
     AO 48                                  COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  2 13'Neste turnr:'
     A            KMPRO          8Y 0O  2 26COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  2 36'R.Dato:'
     A            WSDTR          6Y 0O  2 44COLOR(WHT)
     A                                      EDTWRD('  .  .  ')
     A            KMUUID        36A  H
      *
     A                                  4  2'Deklarant:'
     A            WSKND          8Y 0B  4 15TEXT('Deklarantnr')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N98                                  COLOR(YLW)
     A  98                                  COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  4 24'Org.nr.:'
     A            WSRGD         11A  B  4 33TEXT('Org.nr')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N97                                  COLOR(YLW)
     A  97                                  COLOR(WHT)
     A            WSNAD         30A  B  5 15TEXT('Navn Deklarant')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N96                                  COLOR(YLW)
     A  96                                  COLOR(WHT)
     A            WSADD1        30A  B  5 46TEXT('Adr-1 Deklarant')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N95                                  COLOR(YLW)
     A  95                                  COLOR(WHT)
     A            WSADD2        30A  B  6 15TEXT('Adr-2 Deklarant')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A            WSPND          9A  B  6 46TEXT('Postnr Deklarant')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A            WSADD3        30A  B  6 56TEXT('Adr-3 Deklarant')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N94                                  COLOR(YLW)
     A  94                                  COLOR(WHT)
     A                                  6 87'LK:'
     A            WSLKD          2A  B  6 91TEXT('Landkode Deklarant')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N93                                  COLOR(YLW)
     A  93                                  COLOR(WHT)
     A                                  7  2'Tlf/epost:'
     A            WSCMD         50A  B  7 15TEXT('Tlf/epostadresse')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A                                  7 66'Type:'
     A            WSCMDT         3A  B  7 72TEXT('Type')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N92                                  COLOR(YLW)
     A  92                                  COLOR(WHT)
     A                                      VALUES(' ' 'TE' 'EM')
     A                                  7 77'(TE eller EM)'
      *
     A                                  9  2'Tr. måte:'
     A            WSTM           2Y 0B  9 15TEXT('Transportmåte')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N91                                  COLOR(YLW)
     A  91                                  COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  9 19'Tr.middel:'
     A            WSTRID        35A  B  9 30TEXT('Transportmiddel')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                  9 67'ATD:'
     A            WSATDD         6Y 0B  9 72TEXT('ATD')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N90                                  COLOR(YLW)
     A  90                                  COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  9 80'Tid:'
     A            WSATDT         6Y 0B  9 85TEXT('ATD tid')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N89                                  COLOR(YLW)
     A  89                                  COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  9 93'Reisen ref:'
     A            WSTRRF        17A  B  9105TEXT('Reisen ref.nr')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                 10  2'1. ank.TSD:'
     A            WSTD1          8A  B 10 15TEXT('1. ankomst tollsted')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N88                                  COLOR(YLW)
     A  88                                  COLOR(WHT)
     A                                 10 25'Akt.ank.TSD:'
     A            WSTDA          8A  B 10 38TEXT('Akt. ankomst tollsted')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N87                                  COLOR(YLW)
     A  87                                  COLOR(WHT)
      *
     A                                 12  2'Transportør:'
     A            WSKNC          8Y 0B 12 15TEXT('Transportør')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N86                                  COLOR(YLW)
     A  86                                  COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 12 24'Org.nr.:'
     A            WSRGC         11A  B 12 33TEXT('Org.nr')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N85                                  COLOR(YLW)
     A  85                                  COLOR(WHT)
     A            WSNAC         30A  B 13 15TEXT('Navn Transportør')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N84                                  COLOR(YLW)
     A  84                                  COLOR(WHT)
     A            WSADC1        30A  B 13 46TEXT('Adr-1 Transportør')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N83                                  COLOR(YLW)
     A  83                                  COLOR(WHT)
     A            WSADC2        30A  B 14 15TEXT('Adr-2 Transportør')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A            WSPNC          9A  B 14 46TEXT('Postnr Tramsportør')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A            WSADC3        30A  B 14 56TEXT('Adr-3 Transportør')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N82                                  COLOR(YLW)
     A  82                                  COLOR(WHT)
     A                                 14 87'LK:'
     A            WSLKC          2A  B 14 91TEXT('Landkode Transportør')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N81                                  COLOR(YLW)
     A  81                                  COLOR(WHT)
     A                                 15  2'Tlf/epost:'
     A            WSCMC         50A  B 15 15TEXT('Tlf/epostadresse')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                 15 66'Type:'
     A            WSCMCT         3A  B 15 72TEXT('Type')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N80                                  COLOR(YLW)
     A  80                                  COLOR(WHT)
     A                                      VALUES(' ' 'TE' 'EM')
     A                                 15 77'(TE eller EM)'
     A                                 16  2'F3=Avslutt   F4=Søk   F12=Avbryt'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 17  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
