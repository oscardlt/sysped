     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A          R BILDE1
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF03(03 'EXIT')
     A                                      CF12(03)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1  2'SYSPED/SYAM00'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(045 DIV2417 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 67DATE
     A                                      EDTCDE(Y)
     A                                  2 67TIME
     A                                  4 18MSGCON(010 TRO0103 *LIBL/SYMSGF)
     A            W1TNR          8Y 0B  4 29CHECK(FE)
     A                                      CHECK(RB)
     A  70                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                 19  2MSGCON(030 SHU0530 *LIBL/SYMSGF)
     A            IX             2  0O 19 34
     A  71                                  DSPATR(RI)
     A                                      COLOR(WHT)
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A          R SUBFILA                   SFL
     A            THDTD          2S 0H
     A            WSNRO          1A  B  9  2VALUES(' ' '5' '7')
     A            THKNA          8Y 0O  9  6DSPATR(HI)
     A                                      EDTCDE(4)
     A            THKNAV        30A  O  9 15
     A            THADR1        30A  O  9 47
     A            THADR2        30A  O 10 15
     A            THPOST         4S 0O 10 47
     A            THADR3        24A  O 10 52
     A          R SUBCTLA                   SFLCTL(SUBFILA)
     A                                      SFLSIZ(0100)
     A                                      SFLPAG(0005)
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A                                  1  2'SYSPED/SYAM00'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(045 DIV2417 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            ZRECNA         4S 0O  1 65SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  3  2MSGCON(009 TRO0103 *LIBL/SYMSGF)
     A            W1TNR          8Y 0O  3 12COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  2 72TIME
     A                                  5  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  6  2MSGCON(020 TRO0060 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  8  2MSGCON(009 TRO0061 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                  6 25MSGCON(013 TRO0488 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A          R BUNNA
     A                                      CF03(03 'EXIT')
     A                                      CF05(05 'Frisk')
     A                                      CF12(12 'Forrige bilde')
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      SLNO(19)
     A                                      CLRL(06)
     A                                      CSRLOC(LINNBR POSNBR)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  5  2MSGCON(010 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5 24MSGCON(010 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5 69MSGCON(011 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  98                              6  2MSGCON(078 DIV0012 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A  30        MSGTXT        78A  O  6  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A          R NYWRKA
     A                                      CF03(03 'EXIT')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'Ny linje')
     A                                      CF12(12 'FORIGE BILDE')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/SYAM00'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(045 DIV2417 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            ZRECNO         4S 0O  1 65DSPATR(ND)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  3  2MSGCON(009 TRO0103 *LIBL/SYMSGF)
     A            W1TNR          8Y 0O  3 12COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  2 72TIME
     A                                  5  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  6  2MSGCON(020 TRO0060 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  8  2MSGCON(009 TRO0061 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                 11 15'(Ingen oppdrag på denne turen)'
     A                                 23  2MSGCON(010 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 24MSGCON(010 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 69MSGCON(011 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
