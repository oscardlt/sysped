********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
11016 * 01 YBC PTF11 OMBEREGNING
********************************************************************
     FEDIMA     IF   E           K DISK
     FEDIC      UF   E             DISK
     FSTANDI    IF   E           K DISK
     FEDII      IF   E           K DISK
     FEDIS      O  A E             DISK
     D                 DS
     D  WMBR                   1     14
     D  WMBRA                  1      1
     D  WMBRB                  2     10  0
      *//////////////
      *  Hodelogikk /
      *//////////////
     C                   EXSR      SRA
     C                   EXSR      SRB
     C                   MOVE      '1'           *INLR
      *////////////////////////////////////////////////////////////
      *  Initiering                                           SRA /
      *////////////////////////////////////////////////////////////
     C     SRA           BEGSR
      *___________________
      * Definiere Workfelt
      *"""""""""""""""""""
     C     *LIKE         DEFINE    MSR           WSR
     C     *LIKE         DEFINE    MST           WST
     C     *LIKE         DEFINE    *IN51         WIN51
      *__________________
      * Definiere Nøkkler
      *""""""""""""""""""
     C     KEYA          KLIST
     C                   KFLD                    WSR
     C                   KFLD                    WST
      *_________________
      * Input parametrer
      *"""""""""""""""""
     C     *ENTRY        PLIST
     C                   PARM      *IN51         WIN51
     C                   PARM      SMBR          SMBR
     C                   PARM      SSN           SSN
     C                   PARM                    INNETW
      *_____________
      * Init av felt
      *"""""""""""""
     C                   MOVE      'S'           WSR
     C                   MOVE      '+'           WST
     C                   MOVE      'I'           WMBRA                          INNFØRSEL,
      *                                                    SENDNING
     C* Hente dagens dato og klokkeslett.
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Bearbeide Formater                                   SRB /
      *////////////////////////////////////////////////////////////
     C     SRB           BEGSR
      *________________________________
      * Oppdatere status i meldingslogg
      *""""""""""""""""""""""""""""""""
     C                   MOVE      '0'           *IN20
     C     KEYA          CHAIN     EDIMA                              51
     C     *IN51         DOWEQ     '0'                                          1<-B
     C     MVEN          IFEQ      *BLANKS                                       2<-B
     C     M0065         IFEQ      'CUSDEC'                                       3<-B
     C     M1001         ANDEQ     '929'
     C     M1N07         IFEQ      'DFU'                                           4<-B
     C     M1N07         OREQ      'DMA'
     C     M1N07         OREQ      'DFO'
     C     M1N07         OREQ      'DEN'
     C     M1N07         OREQ      'DKO'
S01  C*    M1N07         OREQ      'DOM'
N01  C     M1N07         OREQ      'DEB'
N01  C     M1N07         OREQ      'DRE'
N01  C     M1N07         OREQ      'DSO'
     C     M0010         CHAIN     EDII                               52
     C     *IN52         IFEQ      '0'
     C                   MOVE      '1'           *IN20
     C                   ELSE
     C                   MOVE      '0'           *IN20
     C                   ENDIF
     C                   END                                                       4<-E
     C                   END                                                      3<-E
     C                   END                                                     2<-E
     C  N20KEYA          READE     EDIMA                                  51
     C  N51
     CANN20              END                                                    1<-E
     C     *IN51         CABEQ     '1'           SRB9
      *_____________________________
      * Hente data fra avdelingsreg.
      *"""""""""""""""""""""""""""""
     C     MAVD          CHAIN     STANDI                             20
      *________________________________________
      * Hente og oppdater neste sendningsnummer
      *""""""""""""""""""""""""""""""""""""""""
     C     1             CHAIN     EDIC                               20
     C                   ADD       1             CSN
     C                   UPDATE    EDICR
      *_________________
      * Logge sendningen
      *"""""""""""""""""
     C                   MOVE      M0004         S0004
     C                   MOVE      M0010         S0010
     C                   Z-ADD     CSN           WMBRB
     C                   MOVE      WMBR          S0020
     C                   MOVE      M0035         S0035
     C                   Z-ADD     0             S0036
     C                   Z-ADD     CSN           SSN
     C                   MOVE      'S'           SSR
     C                   MOVE      ' '           SST
     C                   MOVEL     WDT           SDT
     C                   MOVEL     WTM           STM
     C                   MOVEL     '*LIBL   '    SLIB
     C                   MOVE      '  '          SLIB
     C                   MOVEL     'EDISE'       SFILE
     C                   MOVEL     WMBR          SMBR
     C                   WRITE     EDISR
     C     SRB9          ENDSR
