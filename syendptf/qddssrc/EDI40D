     A*%%TS  SD  20211027  082529  JOVO        REL-V7R4M0  5770-WDS
*****A**************************************************************
     A* RETTINGER I DETTE PROGRAM:
PTFnrA*Sek Sig Vers  Beskrivelse...........................
"""""A*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXXA* 01 YBC PTF10 BEVARE SIFS I SUBFIL
10XXXA* 02 CB  PTF10 27X132, IKKE MERKET
10XXXA* 03 SH  PTF12 VISER S0026
*****A**************************************************************
     A*%%EC
     A                                      DSPSIZ(27 132 *DS4)
     A                                      REF(*LIBL/EDIS)
     A                                      PRINT
     A                                      CF03(03 'Avslutt program')
     A                                      CF02(02 'BYTT PROGRAM')
     A                                      HLPPNLGRP(HLPSCR EDI40PNL)
     A                                      HLPTITLE('EDI UTVEKSLINGER')
     A                                      HELP
     A                                      ALTHELP
     A*
     A          R EDI40DA
     A*%%TS  SD  20190527  075614  SVEIN       REL-V7R1M0  5770-WDS
     A                                      CF04(04 'Query')
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'EDI'
     A                                      COLOR(BLU)
     A                                  1 60MSGCON(012 EDI0032 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1125'EDI40D A'
     A                                      COLOR(BLU)
     A                                  3  2MSGCON(025 EDI0602 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  2MSGCON(038 EDI0016 *LIBL/SYMSGF)
     A            ZDTF      R     D  B  5 42REFFLD(SDT)
     A                                  5 53MSGCON(008 EDI0605 *LIBL/SYMSGF)
     A                                  6  4MSGCON(036 EDI0017 *LIBL/SYMSGF)
     A            ZTMF      R     D  B  6 42REFFLD(STM)
     A  30 32                               DSPATR(PC)
     A                                  6 53MSGCON(006 EDI0606 *LIBL/SYMSGF)
     A                                  8  2MSGCON(038 EDI0019 *LIBL/SYMSGF)
     A            ZDTT      R     D  B  8 42REFFLD(SDT)
     A  30 33                               DSPATR(PC)
     A                                  8 53MSGCON(008 EDI0605 *LIBL/SYMSGF)
     A                                  9  4MSGCON(036 EDI0020 *LIBL/SYMSGF)
     A            ZTMT      R     D  B  9 42REFFLD(STM)
     A  30 34                               DSPATR(PC)
     A                                  9 53MSGCON(006 EDI0606 *LIBL/SYMSGF)
     A                                 11  2MSGCON(038 EDI0022 *LIBL/SYMSGF)
     A            ZJNS           1A  B 11 42
     A  30 35                               DSPATR(PC)
     A                                      VALUES('J' 'N' 'Y')
     A                                 11 53'J,  N'
     A                                 12  2MSGCON(038 EDI0024 *LIBL/SYMSGF)
     A            ZJNR           1A  B 12 42
     A  30 36                               DSPATR(PC)
     A                                      VALUES('J' 'N' 'Y')
     A                                 12 53'J,  N'
     A                                 14  2MSGCON(038 EDI0026 *LIBL/SYMSGF)
     A            Z0020     R        B 14 42REFFLD(S0020)
     A  30 37                               DSPATR(PC)
     A                                      CHECK(LC)
     A                                 14 59MSGCON(021 EDI0027 *LIBL/SYMSGF)
     A                                 16  2MSGCON(038 EDI0028 *LIBL/SYMSGF)
     A            Z0004     R        B 16 42REFFLD(S0004)
     A  30 38                               DSPATR(PC)
     A                                      CHECK(LC)
     A                                 18  2MSGCON(030 EDI0817 *LIBL/SYMSGF)
N17  A            ZMELDT         6   B 18 42
N17  A            ZMELD2         4   B 18 49
     A                                 26  2MSGCON(010 EDI0803 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 26 15MSGCON(014 EDI0804 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 26 32MSGCON(015 EDI0807 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A*
     A          R EDI40DB                   SFL
     A*%%TS  SD  20190527  072637  SVEIN       REL-V7R1M0  5770-WDS
S03  A*           S0026     R        H
     A            SSN       R        H
     A            SLIB      R        H
     A            SFILE     R        H
     A            SMBR      R        H
     A            SMBRS     R        H
     A            SIFS      R        H
     A            S0004     R        H
     A            S0010     R        H
     A            ZOP            1A  I  7  3VALUES(' ' '1' '5' '6' '7' '8' '9' -
     A                                      'B' 'L' 'I' 'W' 'J' '4' 'R' 'E' 'F')
     A            WSMOTP        20A  O  7  6
     A            S0020     R        O  7 27
     A            S0035     R        O  7 43
     A            S0036     R        O  7 49EDTCDE(4)
     A            SSR       R        O  7 58
     A            SST       R        O  7 61
     A            SDT       R        O  7 64
     A*           STM       R        O  7 73EDTWRD('  :  :  ')
     A            STM       R        O  7 73EDTWRD('0  :  :  ')
     A            WSEGEP        20A  O  7 83
N17  A            M0065          6   O  7105
N17  A            MELDN          6   O  7112
N03  A            S0026     R        O  7119
     A*
     A          R EDI40DC                   SFLCTL(EDI40DB)
     A*%%TS  SD  20210917  113519  SVEIN       REL-V7R4M0  5770-WDS
     A                                      SFLSIZ(0019)
     A                                      SFLPAG(0018)
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                      CF05(05 'Forny')
     A                                      CF12(12 'Annuller')
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A          H                           HLPPNLGRP(EDI40R20 EDI40PNL)
     A                                      HLPARA(01 001 27 132)
     A                                  1 61MSGCON(012 EDI0032 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1125'EDI40D B'
     A                                      COLOR(BLU)
     A            ZRECNO         4S 0O  2 67SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  3  2'Velg og trykk Enter.'
     A                                      COLOR(BLU)
     A                                  4  2'1=Omkjøring   4=Slett   5=Vise   6-
     A                                      =Meldinger   7=Se fil   8=Hent OCR/-
     A                                      UTB   9=EDIfact med LF   B=Bankavst-
     A                                      emming til PC-fil   I=*IFS'
     A                                      COLOR(BLU)
     A                                  5  2'J=Backup   R=Resend OCR/UTB   W=*I-
     A                                      FS via Browser  E=EPOST  F=EPOST ED-
     A                                      Ifactformat'
     A                                      COLOR(BLU)
     A*                                 6  2MSGCON(078 EDI0037 *LIBL/SYMSGF)
     A                                  6  2MSGCON(130 EDI0037 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1  3'EDI40R'
     A                                      COLOR(BLU)
     A*
     A          R EDI40DD
     A*%%TS  SD  20131112  185850  CB          REL-V7R1M0  5770-WDS
     A                                      OVERLAY
     A                                 26  2MSGCON(010 EDI0803 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 26 15MSGCON(010 EDI0805 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 26 26MSGCON(010 EDI0812 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A*
     A          R EDI40DE
     A*%%TS  SD  20211027  082529  JOVO        REL-V7R4M0  5770-WDS
     A                                      CF05(05 'Forny')
     A                                      CF12(12 'Annuller')
     A                                  1  2'EDI'
     A                                      COLOR(BLU)
     A                                  1 61MSGCON(012 EDI0032 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1125'EDI40D E'
     A                                      COLOR(BLU)
     A                                  2 57MSGCON(021 EDI0039 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  4  2MSGCON(028 EDI0603 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  6  2MSGCON(038 EDI0041 *LIBL/SYMSGF)
     A            S0004     R        O  6 43
     A                                  7  2MSGCON(038 EDI0042 *LIBL/SYMSGF)
     A            S0010     R        O  7 43
     A                                  8  2MSGCON(038 EDI0026 *LIBL/SYMSGF)
     A            S0020     R        O  8 43
     A                                  9  2MSGCON(038 EDI0044 *LIBL/SYMSGF)
     A            S0026     R        O  9 43REFFLD(S0026)
     A                                 10  2MSGCON(038 EDI0045 *LIBL/SYMSGF)
     A            S0035     R        O 10 43
     A                                 11  2MSGCON(038 EDI0046 *LIBL/SYMSGF)
     A            S0036     R        O 11 43EDTCDE(4)
     A                                 13  2MSGCON(038 EDI0047 *LIBL/SYMSGF)
     A            SSN       R        O 13 43EDTCDE(4)
     A                                 14  2MSGCON(038 EDI0048 *LIBL/SYMSGF)
     A            SSR       R        O 14 43
     A                                 16  2'Status . . . . . . . . . . . . . .-
     A                                       . :'
     A            SST       R        O 16 43
     A                                 17  4MSGCON(036 EDI0050 *LIBL/SYMSGF)
     A            SDT       R        O 17 43
     A                                 18  6MSGCON(034 EDI0051 *LIBL/SYMSGF)
     A            STM       R        O 18 43EDTWRD('  :  :  ')
     A                                 19  2MSGCON(038 EDI0052 *LIBL/SYMSGF)
     A            SLIB      R        O 19 43
     A                                 20  4MSGCON(036 EDI0053 *LIBL/SYMSGF)
     A            SFILE     R        O 20 43
     A                                 21  6MSGCON(034 EDI0054 *LIBL/SYMSGF)
     A            SMBR      R        O 21 43
     A                                 22  8'Størrelse i byte . . . . . . . :'
     A            SMBRS     R        O 22 43EDTCDE(K)
     A                                 26  2MSGCON(010 EDI0803 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 26 15MSGCON(010 EDI0812 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A            SIFS      R        O 23  6REFFLD(EDISR/SIFS *LIBL/EDIS)
     A                                 23  1'IFS:'
     A            SNDNAME       30   O  6 79COLOR(WHT)
     A            RECNAME       30   O  7 79COLOR(WHT)
      *
     A          R EDI40DØ
     A                                      OVERLAY
     A            ZMSGNO         7A  P
     A            ZMSGTX        79   O 27  2MSGID(&ZMSGNO SYMSGF)
     A                                      COLOR(WHT)
