      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/TJINQSDL MODULE(*LIBL/TJINQSDL *LIBL/INCGI)
CRTPGM*        ACTGRP(*NEW) AUT(*USE)
      *
      *********************************************************************
      /copy SYSPEDS/qrpgsrcpy,hspecs
      /copy SYSPEDS/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FKODTERM   IF   E           K DISK    USROPN
     FKODTERLF  IF   E           K DISK    USROPN
     F                                     RENAME(KOTMR:KOTMRLF)
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy SYSPEDS/qrpgsrcpy,prototypeb
      /copy SYSPEDS/qrpgsrcpy,usec
      /copy SYSPEDS/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
     D SOKKOD          S              1
     D SOKNVN          S             10    varying
     D SOKNVN2         S             20
     D user            S             10
     D MaxV            S              5
     D MaxVis          S              5  0
     D UPC             C                   CONST('ABCDEFGHIJKLMNOPQRST-
     D                                     UVWXYZÆØÅ')
     D LO              C                   CONST('abcdefghijklmnopqrst-
     D                                     uvwxyzæøå')
     D Error           S            150
     D JSONstring      s          32000

      *get input-string
      /copy syspeds/qrpgsrcpy,prolog3

     C                   EXSR      PARSE
     C                   EXSR      INIT

     C                   CALLP     gethtmlifs(
     C                             '/syspeddev/html/JSON.html':
     C                             '/CGITAG/')
     C                   CALLP     wrtsection('top')
      *
      * ............................................................................................
      * skriv JSON-Object start..(alltid '{' + x ant param. m/komma mellom (IKKE komma etter siste))
      * ............................................................................................
      *
      /free
        JSONstring='{'
        +'¬user¬ : ¬'+%trim(USER)+'¬, '
        +'¬sokkod¬ : ¬'+%trim(SOKKOD)+'¬, '
        +'¬soknvn¬ : ¬'+%trim(SOKNVN)+'¬, '
        +'¬errMsg¬ : ¬'+%trim(Error)+'¬, ';
      /end-free
      * JSONfix escaper " i tekst,  for deretter å bytte ¬->"
     c                   call      'JSONFIX'
     c                   parm                    JSONstring
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      *
      * ............................................................................................
      * Skriv JSON-LISTE Start (en liste er EN parameter(fra [ til   )
      * ............................................................................................
     c                   eval      JSONstring ='"inqlosslass" : ['
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      *
     C     ERROR         IFEQ      *blanks
     C                   EXSR      LINES
     C                   ENDIF
      * Skriv JSON-Liste/Array  Avslutning
     C                   EVAL      JSONstring =']'
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%ADDr(JSONstring)
     C                                         :%len(JSONstring))
     C                   CALLP     wrtsection('JSONlin')
      * Skriv JSON-string Avsluttning
     C                   EVAL      JSONstring ='}'
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%ADDr(JSONstring)
     C                                         :%len(JSONstring))
     C                   CALLP     wrtsection('JSONlin')

     C                   EXSR      EXIT
      *=====================================================================
      * Parse input / cvt to numeric
      *=====================================================================
     C     Parse         BEGSR
     C                   EVAL      user = zhbgetvar('user')
     C                   EVAL      SOKKOD = zhbgetvar('SOKKOD')
     C                   EVAL      SOKNVN  = zhbgetvar('SOKNVN')
     C                   EVAL      MaxV    = zhbgetvar('MaxV')
     C                   EVAL      MaxVis  = c2n2(MaxV)
     C     MaxVis        IFEQ      *zeros
     C                   EVAL      MAXVIS=50
     C                   ENDIF
     C     SOKKOD        IFEQ      *BLANKS
     C     SOKNVN        ANDEQ     *BLANKS
     C                   EVAL      SOKNVN='A'
     C                   ENDIF
     C                   ENDSR
      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          BEGSR
     C                   callp     wrtsection('*fini')
     C                   CLOSE     BRIDF
     C  N50              CLOSE     KODTERM
     C  N50              CLOSE     KODTERLF
     C                   EVAL      *INLR=*ON
     C                   RETURN
     C                   ENDSR
      *=====================================================================
      * Find lins
      *=====================================================================
     C     LINES         BEGSR
     C                   EVAL      SOKNVN2   = SOKNVN
     C                   IF        soknvn = '*ALL'
     C                             or soknvn = '*All'
     C                             or soknvn = '*all'
     C                   EVAL      SOKNVN2   = *blanks
     C                   ENDIF
     C                   MOVE      *zeros        AntVist           5 0
     C     SOKKOD        IFEQ      *BLANKS
     C     SOKNVN        ANDEQ     *BLANKS
     C                   GOTO      UTLES
     C                   END

     C     SOKKOD        IFNE      *BLANKS
     C     NRKEY         SETLL     KODTERM
     C                   READ      KODTERM                                33
     C                   ELSE
     C     NVNKEY        SETLL     KODTERLF
     C                   READ      KODTERLF                               33
     C                   END
     C     *in33         DOWEQ     *OFF

     C                   ADD       1             AntVist           5 0
      /free
       if AntVist=1;
          JSONstring=*blanks;
          else;
          JSONstring=', ';
          endif;
       JSONstring=%trim(JSONstring)+'{'
          +'¬kotmko¬ : ¬'+%trim(KOTMKO)+'¬, '
          +'¬kotmnv¬ : ¬'+%trim(KOTMNV)+'¬}';
      /end-free
     C                   call      'JSONFIX'
     C                   parm                    JSONstring
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')


     C     AntVist       CABGE     MaxVis        UtLes
     C     SOKKOD        IFNE      *BLANKS
     C                   READ      KODTERM                                33
     C                   ELSE
     C                   READ      KODTERLF                               33
     C                   END
     C                   ENDDO

     C     UTLES         TAG
     C                   ENDSR
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          BEGSR
     C                   OPEN      BRIDF
     C     USER          CHAIN     BRIDF                              50
      * En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        IFEQ      *blanks
     C                   CALLB     'INCGI'
     C                   PARM                    BISTDL
     C                   ELSE
      * Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   CALL      BILIBL
     C                   ENDIF
     C   50              eval      Error = 'Invalid userID'
     C  N50              OPEN      KODTERM
     C  N50              OPEN      KODTERLF
      * Key for KODTERM
     C     NRkey         KLIST
     C                   KFLD                    KOTMUN
     C                   KFLD                    SOKKOD
     C                   MOVEL     'TERM'        KOTMUN
      * Key for KODTERM
     C     NVNKEY        KLIST
     C                   KFLD                    SOKNVN2

     C                   ENDSR
