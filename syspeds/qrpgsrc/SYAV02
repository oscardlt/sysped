INT   * 'N' I MOMS HVIS KHENV = 11
      **************************************************************
      *** PROGRAM SKAL ARBEIDE MED TRANSPORTØRAVREGNING          ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER       ***
      *** LEDIGE INDIKATORER                                     ***
      **************************************************************
      *
     FWAVREGN   UF   E           K DISK
     FFRISOK    IF   E           K DISK
     FFORDEL6   IF   E           K DISK
     FTURER14   UF   E           K DISK
     FHEAD21    IF   E           K DISK
     FFAKT      UF   E           K DISK
     FKOSTT     UF   E           K DISK
     FFIRM      IF   E           K DISK
     FTRAN      IF   E           K DISK
     FLEVEL01   IF   E           K DISK
     FKUNDL01   IF   E           K DISK
     FDOKUF     IF   E           K DISK
     FHODE      IF   E           K DISK
     FSYAV02PF  O    E             PRINTER
     FSYAV02PA  O    E             PRINTER
     D                 DS
     D  DSFDAT                 1      8  0
     D  DSFAA                  1      4  0
     D  DSFMM                  5      6  0
     D  DSFDD                  7      8  0
     D                 DS
     D  FORFAL                 1      6  0
     D  FORFDD                 1      2  0
     D  FORFMM                 3      4  0
     D  FORFAA                 5      6  0
     D                 DS
     D  FORF8                  1      8  0
     D  FORF8A                 1      4  0
     D  FORF8M                 5      6  0
     D  FORF8D                 7      8  0
     D                 DS
     D  TUDT                   1      8  0
     D  TUDTÅ                  1      4  0
     D  TUDTM                  5      6  0
     D  TUDTD                  7      8  0
     D                 DS
     D  H1DATO                 1      6  0
     D  H1DTD                  1      2  0
     D  H1DTM                  3      4  0
     D  H1DTÅ                  5      6  0
     D                 DS
     D  ATDATO                 1      6  0
     D  ATDTD                  1      2  0
     D  ATDTM                  3      4  0
     D  ATDTÅ                  5      6  0
     D                 DS
     D  PPARM                  1     20
     D  PXUNIK                 1      6  0
     D  PXKL                   7     12  0
     D  PXFAKT                13     19  0
     D  PXUTSK                20     20
      *
     C     *ENTRY        PLIST
     C                   PARM                    APARM            20
     C                   PARM                    FFDATO            6
     C                   MOVEL     APARM         PPARM
     C                   MOVE      FFDATO        FORFAL            6 0
     C                   EXSR      INIT
      *
     C                   CALL      'SYAV03CL'
     C                   PARM                    BRUKER           10
      * HENT HEADING
     C     HODEK         KLIST
     C                   KFLD                    YNET
     C                   KFLD                    TUAVD
      *
     C                   MOVE      ' '           YNET              1
     C     HODEK         CHAIN     HODE                               33
     C*
     C                   Z-ADD     0             XPRO              8 0
     C                   MOVE      *BLANKS       XBILN            10
      * SKRIV HEADING
     C                   Z-ADD     0             AVPOS
     C                   Z-ADD     0             ATPOS
     C                   Z-ADD     44            ANTDET            2 0
     C                   Z-ADD     44            ANTTUR            2 0
     C                   Z-ADD     0             SIDET
     C                   Z-ADD     0             SIDEA
     C                   Z-ADD     UDAY          XDAY
     C                   Z-ADD     UMONTH        XMONTH
     C                   Z-ADD     UYEAR         XYEAR
     C                   Z-ADD     0             AMBELN
     C                   Z-ADD     0             ANBELN
     C                   Z-ADD     0             ABBELN
      * LES WORKFILE
     C     WAVRKY        SETLL     WAVREGN                            77
     C     NYAVR         TAG
     C     WAVRKY        READE     WAVREGN                                77
     C     *IN77         IFEQ      *OFF
     C     WXPRO         CHAIN     TURER14                            79
     C     *IN08         IFNE      '1'
     C                   MOVE      'X'           TUAVRE
     C                   Z-ADD     HTAVRN        TUAVNR
     C                   ENDIF                                                  <*IN08
     C                   UPDATE    TURERR
      * BRUDD TUR
     C     TUPRO         IFNE      XPRO
     C     XPRO          IFNE      0
     C                   ADD       1             ATPOS
     C                   Z-ADD     AMBELN        ATBELN
     C                   ADD       AMBELN        ANBELN
     C                   ADD       AMBELN        ABBELN
     C                   WRITE     OPDSUM                               66
     C     PXUTSK        IFNE      'G'
     C                   WRITE     TURDET                               66
     C                   END
     C                   Z-ADD     0             AMBELN
     C                   ENDIF                                                  <XPRO
     C                   Z-ADD     TUPRO         H1PRO
     C                   Z-ADD     TUPRO         ATPRO
     C                   MOVE      TUDTD         H1DTD
     C                   MOVE      TUDTM         H1DTM
     C                   MOVE      TUDTÅ         H1DTÅ
     C                   Z-ADD     H1DATO        ATDATO
     C                   MOVEL     TUBILN        H1BILN
     C                   MOVEL     TUBILN        ATBILN
     C                   Z-ADD     TUPRO         XPRO
     C                   Z-ADD     44            ANTDET
     C                   Z-ADD     0             SIDET
     C                   Z-ADD     0             AVPOS
     C                   ENDIF                                                  <TUPRO
      * BRUDD BILNR
     C     TUBILN        IFNE      XBILN
     C     XBILN         IFNE      *BLANKS
     C     PXUTSK        IFNE      'G'
     C                   WRITE     BILSUM                               66
     C                   END
     C                   Z-ADD     0             ABBELN
     C                   ENDIF                                                  <XBILN
     C                   MOVE      TUBILN        XBILN
     C                   ENDIF                                                  <TUBILN
     C     ANTDET        IFGE      43
     C                   ADD       1             SIDET
      * HENT NAVN OG ADRESSE
     C     TRANK         CHAIN     TRAN                               33
     C                   Z-ADD     VMTRLE        LEVNR
     C     LEVKY         CHAIN     LEVEL01                            33
     C     KUNKY         CHAIN     KUNDL01                            33
     C                   WRITE     OPDH1                                66
     C                   WRITE     OPDH2                                66
     C                   Z-ADD     0             ANTDET
     C                   ENDIF                                                  <ANTDET
      *
     C     PXUTSK        IFNE      'G'
     C     ANTTUR        IFGE      43
     C                   ADD       1             SIDEA
     C                   WRITE     TURH1                                66
     C                   WRITE     TURH2                                66
     C                   Z-ADD     0             ANTTUR
     C                   ENDIF                                                  <ANTTUR
     C                   ENDIF
      *
     C     WXPRO         SETLL     HEAD21                             75
     C     NYOPD         TAG
     C     WXPRO         READE     HEAD21                                 73
     C     *IN73         IFEQ      *OFF
     C                   Z-ADD     HEAVD         AVAVD
     C                   Z-ADD     HEOPD         AVOPD
     C                   MOVEL     HENAK         AVNAK
     C                   MOVEL     HEADK3        AVADK3
     C                   Z-ADD     HENT          AVANT
     C                   Z-ADD     HEVKT         AVVKT
     C                   Z-ADD     HEM3          AVM3
      * LES FAKTURALINJER - 'AVR'
     C     FAKTK         SETLL     FAKT                               74
     C     NYFAKT        TAG
     C     FAKTK         READE     FAKT                                   74
     C     *IN74         IFEQ      *OFF
     C     FAVK          IFNE      'AVR'
     C                   UPDATE    FAKTR
     C                   GOTO      NYFAKT
     C                   ELSE
     C     *IN08         IFNE      '1'
     C                   Z-ADD     XFN           FAFAKT
     C                   Z-ADD     DSFDAT        FADATO
     C                   MOVE      FORF8         FADAFF
     C                   MOVE      'F'           FAOPKO
     C                   Z-ADD     VMTRKU        FAKUNR
     C                   Z-ADD     VMTRLE        FALEVN
     C                   MOVEL     TUDTÅ         FACC
     C                   MOVE      TUDTÅ         FAÅR
     C                   Z-ADD     TUDTM         FAMND
     C                   MOVE      'J'           FAKDM
INT  C     KHENV         IFEQ      '11 '
INT  C                   MOVE      'N'           FAKDM
INT  C                   END
     C                   ENDIF                                                  <*IN08
     C                   UPDATE    FAKTR
     C                   Z-SUB     FABELN        AVBELN
     C                   ADD       AVBELN        AMBELN
     C                   ENDIF                                                  <FAVK
     C                   ENDIF                                                  <*IN74
      * LES FRAKTBREV
     C     FORD6K        KLIST
     C                   KFLD                    AVAVD
     C                   KFLD                    AVOPD
     C     *LIKE         DEFINE    DFAVD         YAVD
     C     *LIKE         DEFINE    DFOPD         YOPD
     C     *LIKE         DEFINE    DFFBNR        YFBN
     C     FORD6K        CHAIN     FORDEL6                            62
     C     *IN62         IFEQ      '1'
     C                   MOVE      *BLANKS       AV1004
     C                   MOVE      '1'           *IN39
     C                   ELSE
     C     FOAVDI        IFEQ      9999
     C     FOOPDI        ANDEQ     9999999
     C                   MOVE      FOAVD         YAVD
     C                   MOVE      FOOPD         YOPD
     C                   MOVE      FOLOP         YFBN
     C                   ELSE
     C                   MOVE      FOAVDI        YAVD
     C                   MOVE      FOOPDI        YOPD
     C                   Z-ADD     1             YFBN
     C                   END
      * TEST OM LINJE ER BLITT SLÅTT SAMME
     C     FRISKK        KLIST
     C                   KFLD                    AVAVD
     C                   KFLD                    AVOPD
     C                   KFLD                    FSKODE
     C                   MOVE      'SAA'         FSKODE
     C     FRISKK        CHAIN     FRISOK                             39
     C*
     C     DOKUKY        KLIST
     C                   KFLD                    YKODE1
     C                   KFLD                    YAVD
     C                   KFLD                    YOPD
     C                   KFLD                    YFBN
     C                   MOVE      'F'           YKODE1            1
     C     DOKUKY        CHAIN     DOKUF                              37
     C     *IN37         IFEQ      *OFF
     C                   MOVEL     DF1004        AV1004
     C                   ENDIF                                                  <*IN37
     C                   ENDIF                                                  <*IN62
      **  SKRIV DETALJELINJE
     C                   ADD       1             AVPOS
     C                   WRITE     OPDDET                               66
     C                   ADD       1             ANTDET
     C                   GOTO      NYOPD
     C                   ENDIF                                                  <*IN73
     C                   DELETE    WAVREGNR
     C                   GOTO      NYAVR
     C                   ENDIF                                                  <*IN77
      *
     C                   WRITE     OPDSUM                               66
     C                   ADD       1             ATPOS
     C                   Z-ADD     TUPRO         ATPRO
     C                   MOVE      TUDTD         ATDTD
     C                   MOVE      TUDTM         ATDTM
     C                   MOVE      TUDTÅ         ATDTÅ
     C                   MOVEL     TUBILN        ATBILN
     C                   Z-ADD     AMBELN        ATBELN
     C                   ADD       AMBELN        ANBELN
     C                   ADD       AMBELN        ABBELN
     C     PXUTSK        IFNE      'G'
     C                   WRITE     TURDET                               66
     C                   WRITE     BILSUM                               66
     C                   END
INT  C     KUNKY         CHAIN     KUNDL01                            33
INT  C     KHENV         IFNE      '11 '
     C     ANBELN        MULT(H)   FITAX         ANMVA
     C                   MOVE      *ON           *IN71
INT  C                   ELSE
INT  C                   Z-ADD     0             ANMVA
INT  C                   MOVE      *OFF          *IN71
INT  C                   ENDIF                                                  <KHENV
     C     ANBELN        ADD       ANMVA         ANTOT
     C     PXUTSK        IFNE      'G'
     C                   WRITE     TURSUM                               66
     C                   END
      *
     C                   MOVE      *ON           *INLR
     C*
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C*--------------------------------------------------------------
     C     TRANK         KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    TUKNT
     C     LEVKY         KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    LEVNR
     C     KUNKY         KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    VMTRKU
     C*--------------------------------------------------------------
     C* DEFINERER NØKKEL TIL FAKT
     C*--------------------------------------------------------------
     C     FAKTK         KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C*--------------------------------------------------------------
     C* DEFINERER NØKKEL TIL WAVREGN
     C*--------------------------------------------------------------
     C     WAVRKY        KLIST
     C                   KFLD                    PXUNIK
     C                   KFLD                    PXKL
     C                   KFLD                    PXFAKT
     C*
     C                   MOVE      *ZEROS        TUDT
     C                   MOVE      *ZEROS        ATDATO
     C*
     C                   READ      FIRM                                   32
     C     PXUTSK        IFNE      'A'
      * ULIK A: P=PRØVE (F08)
      *         G=FRA GODSLISTE (SKRIV KUN SPESIF. SYAV01PF..)
     C                   MOVE      *ON           *IN08
     C                   ELSE
      * A=AVREGNING (F16)
     C                   MOVE      *OFF          *IN08
     C                   MOVE      'L'           KTTYP
     C     KTTYP         CHAIN     KOSTT                              31
     C     *IN31         IFEQ      *OFF
     C                   Z-ADD     KTNR          XFN               6 0
     C                   Z-ADD     KTNR          HTAVRN
     C                   ADD       1             KTNR
     C                   UPDATE    KOSTTR
     C                   ENDIF                                                  <*IN31
     C                   ENDIF                                                  <PXUTSK
     C                   MOVE      *ZEROS        DSFDAT
     C                   Z-ADD     UYEAR         DSFAA
     C                   Z-ADD     UMONTH        DSFMM
     C                   Z-ADD     UDAY          DSFDD
     C     UYEAR         IFGT      50
     C                   MOVEL     '19'          DSFAA
     C                   ELSE
     C                   MOVEL     '20'          DSFAA
     C                   END
      *
     C                   MOVE      DSFDAT        FORF8
     C     FORFAL        IFEQ      *ZEROS
     C                   MOVE      DSFDD         FORFDD
     C                   MOVE      DSFMM         FORFMM
     C                   MOVE      DSFAA         FORFAA
     C                   ELSE
     C                   MOVE      FORFDD        FORF8D
     C                   MOVE      FORFMM        FORF8M
     C                   MOVE      FORFAA        FORF8A
     C     FORFAA        IFGT      50
     C                   MOVEL     '19'          FORF8A
     C                   ELSE
     C                   MOVEL     '20'          FORF8A
     C                   END
     C                   END
      *
     C     FITAXD        IFNE      *ZEROS
     C     FITAX2        ANDNE     *ZEROS
     C     DSFDAT        ANDLT     FITAXD
     C                   MOVE      FITAX2        FITAX
     C                   MOVE      FITAX2        TAX
     C                   END
      *
     C     UTI           ENDSR
     C*
