      *********************************************************************
CRTPG+*  CRTPGM SYSPED/TSYFI10X MODULE(*LIBL/TSYFI10X *LIBL/INCGI)
CRTPGM*        ACTGRP(*NEW) AUT(*USE)
      *********************************************************************
      /copy SYSPEDS/qrpgsrcpy,hspecs
      /copy SYSPEDS/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FFIRM      IF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy SYSPEDS/qrpgsrcpy,prototypeb
      /copy SYSPEDS/qrpgsrcpy,usec
      /copy SYSPEDS/qrpgsrcpy,variables3
      *
      * Varible for
     D WSUSER          S             10
     D WSAVDA          S              4
     D WSAVD           S              4  0
     D WSOPDA          S              7
     D WSOPD           S              7  0
     D JSONstring      s          32000
     D Error           S            150
      *get input-string
      /copy syspeds/qrpgsrcpy,prolog3

      * Parse input
     C                   EXSR      Parse
      * Open files etc
     C                   EXSR      INIT

     c                   callp     gethtmlifs(
     C                             '/syspeddev/html/JSON.html':
     C                             '/CGITAG/')
     C                   callp     wrtsection('top')
      * Teste input og utfør
     C  N50              EXSR      UpdFaktura
      * ............................................................................................
      * skriv JSON-Object start..(alltid '{' + x ant param. m/komma mellom (IKKE komma etter siste))
      * ............................................................................................
      *
      /free
        JSONstring='{'
        +'¬user¬ : ¬'+%trim(WSUSER)+'¬, '
        +'¬avd¬ : ¬'+%trim(WSAVDA)+'¬, '
        +'¬opd¬ : ¬'+%trim(WSOPDA)+'¬, '
        +'¬errMsg¬ : ¬'+%trim(Error)+'¬, ';
      /end-free
      * JSONfix escaper " i tekst,  for deretter å bytte ¬->"
     c                   call      'JSONFIX'
     c                   parm                    JSONstring
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      *
      * ............................................................................................
      * Skriv JSON-LISTE Start (en liste er EN parameter(fra [ til   )
      * ............................................................................................
     C                   eval      JSONstring ='"recalcinv" : ['
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')

     c                   eval      JSONstring = *blanks
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      * ............................................................................................
      * Skriv JSON-Liste/Array  Avslutning
      * ............................................................................................
     c                   eval      JSONstring =']'
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      * ............................................................................................
      * Skriv JSON-string Avsluttning
      * ............................................................................................
     c                   eval      JSONstring ='}'
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      * Quit
     C                   exsr      Exit
      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Lukk fil
     C                   CLOSE     BRIDF
     C  N50              CLOSE     FIRM
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   MOVE      *ON           *INLR
     C                   endsr
      *********************************************************
     C     Parse         Begsr
      *********************************************************
      * Get input
     C                   EVAL      WSUSER  = zhbgetvar('USER')
     C                   EVAL      WSAVDA = zhbgetvar('AVD')
     C                   EVAL      WSAVD  = c2n2(WSAVDA)
     C                   EVAL      WSOPDA = zhbgetvar('OPD')
     C                   EVAL      WSOPD  = c2n2(WSOPDA)
     C                   ENDSR
      *********************************************************
     C     INIT          Begsr
      *********************************************************
     C                   OPEN      BRIDF
      * Test innlogging
     C     WSUSER        CHAIN     BRIDF                              50
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C                   call      BILIBL
     C                   end
     C   50              EVAL      Error = 'Invalid userID'
     c     *IN50         cabeq     '1'           UtInit
      * hent firmaverdier
     C                   OPEN      FIRM
     C     *loval        setll     FIRM
     C                   read      FIRM

     c     UtInit        ENDSR
      *************************************************
     C     UpdFaktura    BEGSR
      *************************************************
     C                   CALL      'SYFI10X'
     C                   PARM                    FIFIRM
     C                   PARM                    WSAVD
     C                   PARM                    WSOPD
     C                   PARM      'NY'          XNYGML            2
     C                   PARM      'J'           KO2UTL            1
     C                   PARM      *ZEROS        XKUTL             4 2
     C                   PARM      *ZEROS        XSUTL             4 2
     C                   PARM      *ZEROS        XKUMIN            2 0
     C                   PARM      *ZEROS        XSUMIN            2 0
     C                   PARM                    KO2MVK            1
     C                   PARM                    KO2MVS            1
     C                   PARM      *ZEROS        UKFRA             7 0
     C                   PARM                    KOVAVR            1
     C                   PARM                    WSMSG            62
     C                   PARM                    WSMELD            1
     C                   PARM      3             UFLAGG            1 0
     C                   PARM                    UKODE             1
     C                   PARM                    USECUR            6
     C                   ENDSR
