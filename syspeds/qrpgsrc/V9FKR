     FV9FNF     UF   E           K DISK
     FV9FKD     CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
      *
     C                   EXSR      INIT
      *
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     *LOVAL        SETLL     V9FNF
     C     START1        TAG
     C                   READ(N)   V9FNF                                  94
     C  N94UHIST         IFEQ      'J'
     C     FNHIST        CABNE     'J'           START1
     C                   ELSE
     C     FNDRIF        CABNE     'J'           START1
     C                   END
     C  N94USAD          IFEQ      'J'
     C     FNSAD         CABNE     'J'           START1
     C                   END
      *
     C  N94              DO
     C                   DO        5000
     C                   ADD       1             ZRECNO
     C     UHIST         IFEQ      'J'
     C                   MOVE      FNHST         WSST
     C                   MOVE      FNHKNV        WSKNV
     C                   ELSE
     C                   MOVE      FNST          WSST
     C                   MOVE      FNKNV         WSKNV
     C                   END
     C     WSST          COMP      'J'                                    50
     C                   WRITE     SUBFIL
     C     START2        TAG
     C                   READ(N)   V9FNF                                  94
     C  N94UHIST         IFEQ      'J'
     C     FNHIST        CABNE     'J'           START2
     C                   ELSE
     C     FNDRIF        CABNE     'J'           START2
     C                   END
     C  N94USAD          IFEQ      'J'
     C     FNSAD         CABNE     'J'           START2
     C                   END
     C  N94              END
     C                   END
     C                   Z-ADD     ZRECNO        YRECNO            4 0
     C     ZRECNO        IFGT      0
     C                   Z-ADD     1             ZRECNO
     C                   END
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     2             POSNBR
      *
     C     START3        TAG
     C                   SETOFF                                       9998
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYVARE
     C                   ELSE
     C                   WRITE     SUBCTL
     C                   EXFMT     BUNN
     C                   END
      *
     C                   SETOFF                                       9830
     C   03              MOVE      '1'           UFLAGG
     C   03              GOTO      SLUTT
     C   05              GOTO      START
     C   19              DO
     C                   EXSR      MERKE
     C                   Z-ADD     1             ZRECNO
     C                   GOTO      START
     C                   END
     C   21              GOTO      SLUTT
     C   25              DO
     C                   ADD       16            ZRECNO
     C     ZRECNO        IFGT      YRECNO
     C                   MOVE      YRECNO        ZRECNO
     C                   END
     C                   GOTO      START3
     C                   END
      *
     C     ZRECNO        IFNE      0
     C                   EXSR      HENT
     C                   END
     C                   GOTO      START3
      *
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
      *
     C***********************************************
     C     HENT          BEGSR
     C***********************************************
     C                   SETOFF                                       919299
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C   98              DO
     C   99              SETOFF                                       98
     C                   GOTO      SLHENT
     C                   END
     C     FNFN          CHAIN     V9FNF                              33
     C     UHIST         IFEQ      'J'
     C     FNHST         IFEQ      ' '
     C                   MOVE      WSKNV         FNHKNV
     C                   END
     C                   ELSE
     C     FNST          IFEQ      ' '
     C                   MOVE      WSKNV         FNKNV
     C                   END
     C                   END
     C                   UPDATE    V9FNFR
     C                   UPDATE    SUBFIL
     C                   MOVE      '1'           *IN99
     C                   END
      *
     C     SLHENT        TAG
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     2             POSNBR
     C                   SETOFF                                       99
     C   98              DO
     C                   MOVE      'DIV0012'     MSGNR
     C                   SETON                                            30
     C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     MERKE         BEGSR
     C***********************************************
     C     1             DO        YRECNO        ZRECNO
     C     ZRECNO        CHAIN     SUBFIL                             33
     C     FNFN          CHAIN     V9FNF                              33
     C     UHIST         IFEQ      'J'
     C     FNHST         IFEQ      ' '
     C                   MOVE      'J'           FNHKNV
     C                   MOVE      'J'           WSKNV
     C                   END
     C                   ELSE
     C     FNST          IFEQ      ' '
     C                   MOVE      'J'           FNKNV
     C                   MOVE      'J'           WSKNV
     C                   END
     C                   END
     C                   UPDATE    V9FNFR
     C                   UPDATE    SUBFIL
     C                   ENDDO
      *
     C                   ENDSR
      *
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     *ENTRY        PLIST
     C                   PARM                    UHIST             1
     C                   PARM                    USAD              1
     C                   PARM                    UFLAGG            1
     C                   MOVE      ' '           UFLAGG
      *
     C                   ENDSR
      *
