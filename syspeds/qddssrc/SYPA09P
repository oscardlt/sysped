     A*****************************************************************
     A*   BUNTJOURNAL  PALLESYSTEM
     A*****************************************************************
     A          R LASER1
     A            KFMT1         75A     1  1
     A          R LASER2
     A            KFMT2         75A     1  1
     A          R HEAD
     A                                  2  2'******************************'
     A                                      HIGHLIGHT
     A                                  2 32'******************************'
     A                                      HIGHLIGHT
     A                                  2 68MSGCON(10 JOV0912 SYMSGF)
     A            ZUSER         10      2 85
     A N50                              3  2MSGCON(60 JOV0911 SYMSGF)
     A                                      HIGHLIGHT
     A  50                              3  2MSGCON(60 JOV0926 SYMSGF)
     A                                      HIGHLIGHT
     A            BUNTNR         7  0   3 51EDTCDE(3) HIGHLIGHT
     A                                  3 68MSGCON(6 JOV0914 SYMSGF)
     A                                  3 75DATE EDTCDE(Y)
     A                                  3 85MSGCON(6 JOV0915 SYMSGF)
     A                                  3 92TIME
     A                                  3106MSGCON(6 JOV0916 SYMSGF)
     A            SIDE           3  0   3113EDTCDE(3) HIGHLIGHT
     A                                  4  2'******************************'
     A                                      HIGHLIGHT
     A                                  4 32'******************************'
     A                                      HIGHLIGHT
     A                                  7  1MSGCON(119 JOV0913 SYMSGF)
     A                                  8  1' ------------------------'
     A                                  8 26'-------------------------'
     A                                  8 51'-------------------------'
     A                                  8 76'-------------------------'
     A                                  8101'-------------------'
     A
     A          R SPACE1                    SPACEB(1)
     A                                     2' '
     A          R DET                       SPACEB(1)
     A            PAKVNR         9  0      1EDTCDE(Z)
     A            PALØNR         7  0     11EDTCDE(Z)
     A            PAKVDT         8  0     19EDTCDE(Z)
     A            PAPPNR         8  0     27EDTCDE(Z)
     A            PAPNVN        30A       36
     A            PABILN        10A       68
     A            XXMANT         5  0     81EDTCDE(Z)
     A            XXLANT         5  0     90EDTCDE(Z)
     A            PATXT         20A       98
     A          R TOT                       SPACEB(2) HIGHLIGHT
     A            TOTKVN         5  0      4EDTCDE(Z)
     A            TOTLØN         5  0     13EDTCDE(Z)
     A                                    36MSGCON(15 JOV0910 SYMSGF)
     A            TOTMAN         5  0     81EDTCDE(Z)
     A            TOTLAN         5  0     90EDTCDE(Z)
