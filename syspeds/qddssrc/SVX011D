********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10003 * 01 YBC PTF10 KLARGJØR TIL EKSTERN REFERANSE
xxxxxx*xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
11133 * 02 YBC PTF11 VEKT ER ENDRET FRA 9.1 TIL 11.3
********************************************************************
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPPNLGRP(HLPSCR SVX011PNL)
     A                                      HLPTITLE('REGISTRERING AV TET EKSPO-
     A                                      RT')
     A                                      HELP
     A                                      ALTHELP
      *
     A          R SVX011D01
     A                                      CSRLOC(LINNBR     POSNBR)
N01  A                                      CF04(04 'HENTE REFERANSE FRA Z-FIL')
     A N36                                  CF07(07 'Söke oppdrag')
     A                                      CF10(10 'Slette ferdigmeldt TVINN')
     A                                      CA12(12 'Forrige')
     A          H                           HLPPNLGRP(SVX011000 SVX011PNL)
     A                                      HLPARA(01 001 08 080)
     A          H                           HLPPNLGRP(SVX011000 SVX011PNL)
     A                                      HLPARA(09 001 09 080)
     A          H                           HLPPNLGRP(SVX01100A SVX011PNL)
     A                                      HLPARA(10 001 10 080)
     A          H                           HLPPNLGRP(SVX01100B SVX011PNL)
     A                                      HLPARA(11 001 11 080)
     A          H                           HLPPNLGRP(SVX001100C SVX011PNL)
     A                                      HLPARA(13 001 13 080)
     A          H                           HLPPNLGRP(SVX01100D SVX011PNL)
     A                                      HLPARA(14 001 14 080)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1  3'SYSPED/SVX011R'
     A                                      COLOR(BLU)
     A                                  1 24'Underhåll av NCTS Export'
     A                                      COLOR(WHT)
     A                                  1 62DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(GRN)
     A                                  1 71TIME
     A                                      COLOR(GRN)
     A            FIFIRM         2A  O  2 16TEXT('FIRMAKODE')
     A                                      COLOR(TRQ)
     A                                  2 58MSGCON(022 DIV0047 *LIBL/SYMSGF)
     A                                      COLOR(TRQ)
     A                                  5 18'Avdelning:'
     A            UAVD           4Y 0B  5 34EDTCDE(4)
     A  95                                  DSPATR(PC)
     A  31                                  DSPATR(PR)
     A                                      COLOR(WHT)
N01  A                                  8 18'Ekstern ref:'
N01  A            WSEXTR        35A  B  8 34DSPATR(UL)
N01  A                                      DSPATR(PC)
N01  A                                      COLOR(YLW)
     A                                  9 18'Signatur:'
     A            WSSG           3A  B  9 34DSPATR(UL)
     A  99                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 10 18'Ärende.nr.:'
     A            WSTDN          7Y 0B 10 34DSPATR(UL)
     A  98                                  DSPATR(RI)
     A  32                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 10 42'Tast oppdragsnr.'
     A                                      COLOR(BLU)
     A                                 11 18'Förenklad ?:'
     A            WSENKL         1A  B 11 34DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      VALUES('J' 'N')
     A                                 11 42'(J=Föränklad, N=Normal)'
     A                                      COLOR(BLU)
     A N36                             23  2'F7=Sök ärende  '
     A                                      COLOR(BLU)
     A                                 23 20'F12=Avbryt'
     A                                      COLOR(BLU)
N01  A                                 23 38'F4=Sök ekstern ref.'
N01  A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A                                  2  3'Firmakod:'
      *
     A          R SVX011D02
     A N27                                  CF04(04 'Söking')
     A                                      CF06(06 'SIKKERHET')
     A N36                                  CF07(07 'Söke oppdrag')
     A                                      CF10(10 'Se TVINN logg')
     A                                      CA12(12 'Forrige')
     A                                      CF14(14 'KUNDEINFO')
     A                                      CF15(15 'ARBEIDE MED TILLEGG OPPL')
     A                                      CSRLOC(LINNBR     POSNBR)
     A          H                           HLPPNLGRP(SVX01101A SVX011PNL)
     A                                      HLPARA(01 072 01 080)
     A          H                           HLPPNLGRP(SVX01101B SVX011PNL)
     A                                      HLPARA(02 001 02 015)
     A          H                           HLPPNLGRP(SVX001101C SVX011PNL)
     A                                      HLPARA(02 016 02 030)
     A          H                           HLPPNLGRP(SVX01101D SVX011PNL)
     A                                      HLPARA(02 031 02 044)
     A          H                           HLPPNLGRP(SVX01101E SVX011PNL)
     A                                      HLPARA(02 045 02 060)
     A          H                           HLPPNLGRP(SVX01101F SVX011PNL)
     A                                      HLPARA(02 061 02 080)
     A          H                           HLPPNLGRP(SVX011002 SVX011PNL)
     A                                      HLPARA(03 001 03 038)
     A          H                           HLPPNLGRP(SVX011004 SVX011PNL)
     A                                      HLPARA(03 041 03 080)
     A          H                           HLPPNLGRP(SVX011002 SVX011PNL)
     A                                      HLPARA(04 001 04 038)
     A          H                           HLPPNLGRP(SVX011004 SVX011PNL)
     A                                      HLPARA(04 041 04 080)
     A          H                           HLPPNLGRP(SVX011002 SVX011PNL)
     A                                      HLPARA(05 001 05 038)
     A          H                           HLPPNLGRP(SVX011004 SVX011PNL)
     A                                      HLPARA(05 040 05 080)
     A          H                           HLPPNLGRP(SVX011002 SVX011PNL)
     A                                      HLPARA(06 001 06 038)
     A          H                           HLPPNLGRP(SVX011005 SVX011PNL)
     A                                      HLPARA(06 040 06 080)
     A          H                           HLPPNLGRP(SVX011002 SVX011PNL)
     A                                      HLPARA(07 002 07 035)
     A          H                           HLPPNLGRP(SVX011006 SVX011PNL)
     A                                      HLPARA(07 040 07 080)
     A          H                           HLPPNLGRP(SVX011007 SVX011PNL)
     A                                      HLPARA(08 001 08 030)
     A          H                           HLPPNLGRP(SVX011006 SVX011PNL)
     A                                      HLPARA(08 040 08 080)
     A          H                           HLPPNLGRP(SVX011007 SVX011PNL)
     A                                      HLPARA(09 001 09 040)
     A          H                           HLPPNLGRP(SVX011008 SVX011PNL)
     A                                      HLPARA(09 040 09 080)
     A          H                           HLPPNLGRP(SVX011007 SVX011PNL)
     A                                      HLPARA(10 001 10 040)
     A          H                           HLPPNLGRP(SVX011006B SVX011PNL)
     A                                      HLPARA(10 040 10 080)
     A          H                           HLPPNLGRP(SVX011007 SVX011PNL)
     A                                      HLPARA(11 001 11 040)
     A          H                           HLPPNLGRP(SVX011006B SVX011PNL)
     A                                      HLPARA(11 040 11 080)
     A          H                           HLPPNLGRP(SVX011007 SVX011PNL)
     A                                      HLPARA(12 001 12 038)
     A          H                           HLPPNLGRP(SVX011009 SVX011PNL)
     A                                      HLPARA(12 040 12 080)
     A          H                           HLPPNLGRP(SVX011010 SVX011PNL)
     A                                      HLPARA(13 001 13 038)
     A          H                           HLPPNLGRP(SVX011011 SVX011PNL)
     A                                      HLPARA(13 040 13 080)
     A          H                           HLPPNLGRP(SVX011010 SVX011PNL)
     A                                      HLPARA(14 001 14 038)
     A          H                           HLPPNLGRP(SVX011012 SVX011PNL)
     A                                      HLPARA(14 040 14 080)
     A          H                           HLPPNLGRP(SVX011010 SVX011PNL)
     A                                      HLPARA(15 001 15 038)
     A          H                           HLPPNLGRP(SVX011013 SVX011PNL)
     A                                      HLPARA(15 040 15 080)
     A          H                           HLPPNLGRP(SVX011010 SVX011PNL)
     A                                      HLPARA(16 001 16 038)
     A          H                           HLPPNLGRP(SVX011014 SVX011PNL)
     A                                      HLPARA(16 040 16 080)
     A          H                           HLPPNLGRP(SVX011010 SVX011PNL)
     A                                      HLPARA(17 001 17 038)
     A          H                           HLPPNLGRP(SVX011015 SVX011PNL)
     A                                      HLPARA(17 058 17 077)
     A          H                           HLPPNLGRP(SVX011016 SVX011PNL)
     A                                      HLPARA(18 001 18 080)
     A          H                           HLPPNLGRP(SVX011017A SVX011PNL)
     A                                      HLPARA(19 001 19 031)
     A          H                           HLPPNLGRP(SVX011017B SVX011PNL)
     A                                      HLPARA(19 033 19 067)
     A          H                           HLPPNLGRP(SVX011017C SVX011PNL)
     A                                      HLPARA(19 069 19 072)
     A          H                           HLPPNLGRP(SVX011017D SVX011PNL)
     A                                      HLPARA(19 074 19 074)
     A          H                           HLPPNLGRP(SVX011017E SVX011PNL)
     A                                      HLPARA(19 076 19 077)
     A          H                           HLPPNLGRP(SVX011017F SVX011PNL)
     A                                      HLPARA(20 001 20 037)
     A          H                           HLPPNLGRP(SVX011018 SVX011PNL)
     A                                      HLPARA(20 052 20 080)
     A          H                           HLPPNLGRP(SVX011021 SVX011PNL)
     A                                      HLPARA(21 001 21 029)
     A          H                           HLPPNLGRP(SVX011022 SVX011PNL)
     A                                      HLPARA(21 031 21 080)
     A          H                           HLPPNLGRP(SVX011023 SVX011PNL)
     A                                      HLPARA(23 001 23 080)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1  3'SYSPED/SVX011R'
     A                                      COLOR(BLU)
     A N27                              1 20'Underhåll NCTS Export'
     A                                      COLOR(WHT)
     A  27                              1 20'Fråga NCTS Export'
     A                                      COLOR(WHT)
     A                                  1 46'Ref.'
     A            UAVD           4Y 0O  1 51COLOR(WHT)
     A                                      EDTCDE(4)
     A            WSTDN          7Y 0O  1 56COLOR(WHT)
     A                                      EDTCDE(4)
     A            WSSG           3A  O  1 64COLOR(WHT)
     A                                  1 68'Tur'
     A            WSPRO          8Y 0B  1 72DSPATR(UL)
     A  99                                  DSPATR(RI)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  2  2'1.Dekl.'
     A            WSDK           5A  B  2 10TEXT('Deklarasjonstype')
     A                                      DSPATR(UL)
     A  98                                  DSPATR(RI)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                  2 16'4.L.lista'
     A            WSLSTL         4Y 0B  2 26EDTCDE(4)
     A                                      DSPATR(UL)
     A  55                                  DSPATR(RI)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                  2 31'5.V.pos'
     A            WSVPOS         5Y 0O  2 39EDTCDE(4)
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  2 45'6.Kolli'
     A            WSNTK          7Y 0O  2 53EDTCDE(4)
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  2 61'B.vikt'
S02  A*           WSVKB          9Y 1O  2 68EDTCDE(4)
N02  A            WSVKB         11Y 3O  2 68EDTCDE(4)
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  3  2'2.Avsänd. '
     A            WSKNS          8Y 0B  3 13TEXT('Kundenr selger')
     A                                      DSPATR(UL)
     A  97                                  DSPATR(RI)
     A  27                                  DSPATR(PR)
     A  37                                  DSPATR(BL)
     A  37                                  COLOR(RED)
     A N37                                  COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSTINS        17A  B  3 22TEXT('TIN kode selger')
     A                                      DSPATR(UL)
     A  96                                  DSPATR(RI)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                  3 40'9.Skicka'
     A            WSTSN1        30A  B  3 50DSPATR(UL)
     A                                      DSPATR(PR)
     A                                      COLOR(YLW)
     A            WSNAS         30A  B  4  2TEXT('Navn selger')
     A                                      DSPATR(UL)
     A  95                                  DSPATR(RI)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                  4 41'tillbaka'
     A            WSTSN2        30A  B  4 50DSPATR(UL)
     A                                      DSPATR(PR)
     A                                      COLOR(YLW)
     A            WSADS1        30A  B  5  2TEXT('Adresse-1 selger')
     A                                      DSPATR(UL)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                  5 40'15.Avs/utf.land'
     A            WSALK          2A  B  5 56TEXT('Landk avs/utf-land')
     A                                      DSPATR(UL)
     A  94                                  DSPATR(RI)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A  27                              5 60'Reg.dat '
     A  27        THDT           8Y 0O  5 69TEXT('Registreringsdato')
     A                                      DSPATR(UL)
     A                                      EDTWRD('    .  .  ')
     A                                      COLOR(WHT)
     A            WSPNS          9A  B  6  2TEXT('Postnr selger')
     A                                      DSPATR(UL)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A            WSPSS         24A  B  6 12TEXT('Poststed selger')
     A                                      DSPATR(UL)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                  6 40'17.Be.land'
     A            WSBLK          2A  B  6 56TEXT('Landk avs/utf-land')
     A                                      DSPATR(UL)
     A  93                                  DSPATR(RI)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                  6 60'Tar'
     A            WSTARF        16A  O  6 64TEXT('Tariffer')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  7  2'Landkod '
     A            WSLKS          2A  B  7 12TEXT('Landkode selger')
     A                                      DSPATR(UL)
     A  92                                  DSPATR(RI)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                  7 22'Språkkod '
     A            WSSKS          2A  B  7 32TEXT('Språkkode selger')
     A                                      DSPATR(UL)
     A  91                                  DSPATR(RI)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                  7 40'18.Tr.medel vid avgång ID/LK/SK '
     A                                  7 78'TM'
     A                                  8  2'8.Mottag. '
     A            WSKNK          8Y 0B  8 13TEXT('Kundenr kjöper')
     A                                      DSPATR(UL)
     A  90                                  DSPATR(RI)
     A  27                                  DSPATR(PR)
     A  38                                  DSPATR(BL)
     A  38                                  COLOR(RED)
     A N38                                  COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSTINK        17A  B  8 22TEXT('TIN kode kjöper')
     A                                      DSPATR(UL)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A            WSTAID        27A  B  8 43TEXT('Identitet')
     A                                      DSPATR(UL)
     A  56                                  DSPATR(RI)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A            WSTALK         2A  B  8 71TEXT('Landkode')
     A                                      DSPATR(UL)
     A  89                                  DSPATR(RI)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A            WSTASK         2A  B  8 74TEXT('Språkkode')
     A                                      DSPATR(UL)
     A  88                                  DSPATR(RI)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A            WSTRMI         2Y 0B  8 78TEXT('Transportmåte')
     A                                      DSPATR(UL)
     A  82                                  DSPATR(RI)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                      EDTCDE(9)
     A            WSNAK         30A  B  9  2TEXT('Navn kjöper')
     A                                      DSPATR(UL)
     A  87                                  DSPATR(RI)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                  9 40'19.Cont.'
     A            WSKDC          1A  B  9 49TEXT('Container 0/1')
     A                                      DSPATR(UL)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                      VALUES('0' '1')
     A            WSADK1        30A  B 10  2TEXT('Adresse-1 kjöper')
     A                                      DSPATR(UL)
     A  53                                  DSPATR(RI)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                 10 40'21.Tr.medel vid gränsen ID/LK/SK'
     A                                 10 78'TM'
     A            WSPNK          9A  B 11  2TEXT('Postnr kjöper')
     A                                      DSPATR(UL)
     A  52                                  DSPATR(RI)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A            WSPSK         24A  B 11 12TEXT('Poststed kjöper')
     A                                      DSPATR(UL)
     A  51                                  DSPATR(RI)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A            WSTGID        27A  B 11 43TEXT('Identitet')
     A                                      DSPATR(UL)
     A  54                                  DSPATR(RI)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A            WSTGLK         2A  B 11 71TEXT('Landkode')
     A                                      DSPATR(UL)
     A  86                                  DSPATR(RI)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A            WSTGSK         2A  B 11 74TEXT('Språkkode')
     A                                      DSPATR(UL)
     A  85                                  DSPATR(RI)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A            WSTRM          2Y 0B 11 78TEXT('Transportmåte')
     A                                      DSPATR(UL)
     A  45                                  DSPATR(RI)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                      EDTCDE(9)
     A                                 12  2'Landkod '
     A            WSLKK          2A  B 12 12TEXT('Landkode kjöper')
     A                                      DSPATR(UL)
     A  84                                  DSPATR(RI)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                 12 22'Språkkod '
     A            WSSKK          2A  B 12 32TEXT('Språkkode kjöper')
     A                                      DSPATR(UL)
     A  83                                  DSPATR(RI)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                 12 40'L.st:'
     A            WSLASD        17A  B 12 46TEXT('Laststed')
     A                                      DSPATR(UL)
     A  46                                  DSPATR(RI)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A            WSLAS2        17A  B 12 64TEXT('SUB Laststed')
     A                                      DSPATR(UL)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                 13  2'50. Ansvar'
     A            WSKNA          8Y 0B 13 13TEXT('Kundenr H.ansvalig')
     A                                      DSPATR(UL)
     A  81                                  DSPATR(RI)
     A  27                                  DSPATR(PR)
     A  39                                  DSPATR(BL)
     A  39                                  COLOR(RED)
     A N39                                  COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSTINA        17A  B 13 22TEXT('Tin kode H.ansvalig')
     A                                      DSPATR(UL)
     A  80                                  DSPATR(RI)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                 13 40'Auto.lagringsplats:'
     A            WSLSD         17A  B 13 60TEXT('Auto.lagringssted')
     A                                      DSPATR(UL)
     A  47                                  DSPATR(RI)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A            WSNAA         30A  B 14  2TEXT('Navn H.ansvalig')
     A                                      DSPATR(UL)
     A  79                                  DSPATR(RI)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                 14 40'C. Avg. tullkont'
     A            WSCATS         8A  B 14 57TEXT('AVG.TOLLSTED')
     A                                      DSPATR(UL)
     A  78                                  DSPATR(RI)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A            WSADA1        30A  B 15  2TEXT('Adresse-1 H.ansvalig')
     A                                      DSPATR(UL)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                 15 40'Språkkod på följedok.'
     A            WSSKFD         2A  B 15 63TEXT('Språkkode')
     A                                      DSPATR(UL)
     A  77                                  DSPATR(RI)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A            WSPNA          9A  B 16  2TEXT('Postnr H.ansvalig')
     A                                      DSPATR(UL)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A            WSPSA         24A  B 16 12TEXT('Poststed H.ansvalig')
     A                                      DSPATR(UL)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                 16 40'Trans.nr/dt'
     A            WSTRNR        18A  B 16 53TEXT('Transittteringsnr')
     A                                      DSPATR(UL)
     A                                      DSPATR(PR)
     A                                      COLOR(YLW)
     A            WSTRDT         8Y 0B 16 72TEXT('Transittteringsnr')
     A                                      DSPATR(UL)
     A  57                                  DSPATR(RI)
     A                                      DSPATR(PR)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 17  2'Landkod '
     A            WSLKA          2A  B 17 12TEXT('Landkode H.ansvalig')
     A                                      DSPATR(UL)
     A  76                                  DSPATR(RI)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                 17 22'Språkkod '
     A            WSSKA          2A  B 17 32TEXT('Språkkode H.ansvalig')
     A                                      DSPATR(UL)
     A  75                                  DSPATR(RI)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                 17 40'Dekl.plats'
     A            WSDST         15A  B 17 51DSPATR(UL)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                 17 67'Dekl.språk'
     A            WSDSK          2A  B 17 78DSPATR(UL)
     A  62                                  DSPATR(RI)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                 18  2'51.'
     A            WSTSD1         8A  B 18  6TEXT('Transitt tollsted 1')
     A                                      DSPATR(UL)
     A  74                                  DSPATR(RI)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A            WSTSD2         8A  B 18 15TEXT('Transitt tollsted 2')
     A                                      DSPATR(UL)
     A  73                                  DSPATR(RI)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A            WSTSD3         8A  B 18 24TEXT('Transitt tollsted 3')
     A                                      DSPATR(UL)
     A  72                                  DSPATR(RI)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A            WSTSD4         8A  B 18 33TEXT('Transitt tollsted 4')
     A                                      DSPATR(UL)
     A  71                                  DSPATR(RI)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A            WSTSD5         8A  B 18 42TEXT('Transitt tollsted 5')
     A                                      DSPATR(UL)
     A  70                                  DSPATR(RI)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A            WSTSD6         8A  B 18 51TEXT('Transitt tollsted 6')
     A                                      DSPATR(UL)
     A  69                                  DSPATR(RI)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A            WSTSD7         8A  B 18 60TEXT('Transitt tollsted 7')
     A                                      DSPATR(UL)
     A  68                                  DSPATR(RI)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A            WSTSD8         8A  B 18 69TEXT('Transitt tollsted 8')
     A                                      DSPATR(UL)
     A  67                                  DSPATR(RI)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                 19  2'52.'
     A            WSGKD          1Y 0B 19  6TEXT('Garanti kode')
     A                                      DSPATR(UL)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A            WSGFT1        24A  B 19  8TEXT('Garanti')
     A                                      DSPATR(UL)
     A  50                                  DSPATR(RI)
     A  27                                  DSPATR(PR)
     A N35                                  COLOR(YLW)
     A  35                                  COLOR(RED)
     A            WSGFT2        35A  B 19 33TEXT('Annen garanti')
     A                                      DSPATR(UL)
     A  66                                  DSPATR(RI)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A            WSGADK         4A  B 19 69TEXT('Adgangkode')
     A                                      DSPATR(UL)
     A  65                                  DSPATR(RI)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A            WSGBGI         1Y 0B 19 74TEXT('Begrensing innen EF')
     A                                      DSPATR(UL)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                      VALUES(0 1)
     A            WSGBGU         2A  B 19 76TEXT('Begrensing utenfor')
     A                                      DSPATR(UL)
     A  64                                  DSPATR(RI)
     A  27                                  DSPATR(PR)
     A N28                                  COLOR(YLW)
     A  28                                  COLOR(RED)
     A                                 20  2'Gar.belopp  '
     A            WSGBL         13Y 2B 20 15TEXT('Garantibelöp')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(RI)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 20 31'Valuta'
     A            WSGVK          3A  B 20 38TEXT('Valutakode')
     A                                      DSPATR(UL)
     A  48                                  DSPATR(RI)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                 20 52'53. B.tullkont'
     A            WSTSB          8A  B 20 67TEXT('Bestemmelses tollsted')
     A                                      DSPATR(UL)
     A  63                                  DSPATR(RI)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                 21  2'D.'
     A                                 21  5'Ktl.res'
     A            WSDKR          2A  B 21 13TEXT('Kontroll resultat')
     A                                      DSPATR(UL)
     A  61                                  DSPATR(RI)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                 21 16'Frist'
     A            WSDDT          8Y 0B 21 22TEXT('Tidsfrist')
     A                                      DSPATR(UL)
     A  60                                  DSPATR(RI)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 21 31'Plombering Ant/KD/SK'
     A            WSDANT         4Y 0B 21 52TEXT('Antall forsegling')
     A                                      DSPATR(UL)
     A  27                                  DSPATR(PR)
     A N29                                  COLOR(YLW)
     A  29                                  COLOR(RED)
     A                                      EDTCDE(4)
     A            WSDFKD        20A  B 21 57TEXT('FORSEGLING-ID KODE')
     A                                      DSPATR(UL)
     A  59                                  DSPATR(RI)
     A  27                                  DSPATR(PR)
     A N29                                  COLOR(YLW)
     A  29                                  COLOR(RED)
     A            WSDFSK         2A  B 21 78TEXT('FORSEGLING-ID Språk')
     A                                      DSPATR(UL)
     A  58                                  DSPATR(RI)
     A  27                                  DSPATR(PR)
     A                                      COLOR(YLW)
S01  A* 27                             22  5'Ktl.Tsd/DT/av/SK'
S01  A* 27        WSDATS         8A  O 22 22COLOR(WHT)
S01  A* 27        WSDDTK         8Y 0O 22 31COLOR(WHT)
S01  A*                                     EDTCDE(4)
S01  A* 27        WSDKAV        35A  O 22 40COLOR(WHT)
S01  A* 27        WSDKSK         2A  O 22 76COLOR(WHT)
N01  A  27                             22  5'Ktl.Tsd/DT'
N01  A  27        WSDATS         8   O 22 16COLOR(WHT)
N01  A  27        WSDDTK         6Y 0O 22 25COLOR(WHT)
N01  A                                 22 34'Ext.ref.'
N01  A            WSSOK         35A  B 22 43TEXT('Frisökevei')
N01  A                                      DSPATR(UL)
N01  A  27
N01  AO 44                                  DSPATR(PR)
N01  A  44                                  COLOR(WHT)
N01  A N44                                  COLOR(YLW)
     A N27                             23  1'F4=Sök   '
     A                                      COLOR(BLU)
     A N36                             23 11'F7=Se ärende '
     A                                      COLOR(BLU)
     A                                 23 25'F10=NCT'
     A                                      COLOR(BLU)
     A                                 23 33'F6=Säkerhet '
     A                                      COLOR(BLU)
     A                                 23 47'F14=Se kunduppl'
     A                                      COLOR(BLU)
     A                                 23 64'F15=Tilägg kod '
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
      *
     A          R SVX011D03
     A                                      WINDOW(*DFT 5 45 *NOMSGLIN)
     A                                      OVERLAY
     A                                  1  2'Använt garantibelopp över'
     A                                  2  2'alarmgräns på'
     A            TGPRM          3Y 0O  2 16EDTCDE(4)
     A                                      COLOR(YLW)
     A                                  2 20'%'
     A                                  3  2'Totalt garantibelopp är'
     A            TGGBL         11Y 0O  3 26EDTCDE(2)
     A                                      COLOR(BLU)
     A                                  4  2'Totalt använt garantibelopp'
     A                                  5  2'inklusive detta ärende är'
     A            TGGBLB        11Y 0O  5 28EDTCDE(2)
     A                                      COLOR(YLW)
      *
     A          R SVX011D04
     A                                      WINDOW(*DFT 3 45 *NOMSGLIN)
     A                                      OVERLAY
     A                                  1  2'Angiven garantityp kräver '
     A N99                              2  2'normal procedur. '
     A  99                              2  2'forenklet prosedyre.'
     A                                  3  2'Vill du ändra procedur?'
     A            WSJN           1A  B  3 26DSPATR(UL)
     A                                      VALUES('J' 'N')
