     FEDIM      IF   E           K DISK
     FERFF96B   IF   E           K DISK
     FEGIS96B   IF   E           K DISK
     FEDTM96B   IF   E           K DISK
     FEFTX96B   IF   E           K DISK
     FECST96B   IF   E           K DISK
     FETAX96B   IF   E           K DISK
     FEMOA96B   IF   E           K DISK
     FEDOC96B   IF   E           K DISK
     FELOC96B   IF   E           K DISK
     FDKTKD     IF   E           K DISK
     FDKIH      UF   E           K DISK
     FDKIVA     UF A E           K DISK
     FDKEH      UF   E           K DISK
     FDKIHH     O  A E             DISK
     FDKEHH     O  A E             DISK
     FTVINF     O  A E             DISK
     D A               S              1    DIM(79)
     D B               S              1    DIM(18)
      *________________________
      * DS for decimal handling
      *""""""""""""""""""""""""
     D                 DS
     D  ZDEC                   1      7  6
     D  ZDEC6                  2      7
     D  ZHEL                  11     22  0
     D                 DS
     D  WMN2                   1      9  0
     D                 DS
     D  WTX256                 1    256
     D  WTX70A                 1     65
     D  WTX70B                66    135
     D  WTX70C               136    205
     D  WTX70D               206    256
      */////////////
      * MAIN LOGIC /
      */////////////
      *___________
      * Initiering
      *"""""""""""
     C                   EXSR      INIT
      *________
      * CONVERT
      *""""""""
     C  N51              EXSR      SRA

     C                   MOVE      '1'           *INLR
      *////////////////////////////////////////////////////////////
      *                                                      INIT /
      *////////////////////////////////////////////////////////////
     C     INIT          BEGSR
     C     KEYMS0        KLIST
     C                   KFLD                    ZMN
     C                   KFLD                    ZGN0
     C     KEYMS1        KLIST
     C                   KFLD                    ZMN
     C                   KFLD                    ZGN1
     C                   KFLD                    ZREPB0
     C     KEYMS2        KLIST
     C                   KFLD                    ZMN
     C                   KFLD                    ZGN2
     C                   KFLD                    ZREPC0
     C                   KFLD                    ZREPC1
     C     KEYMS3        KLIST
     C                   KFLD                    ZMN
     C                   KFLD                    ZGN3
     C                   KFLD                    ZREPD0
     C                   KFLD                    ZREPD1
     C                   KFLD                    ZREPD2
     C     KODKEY        KLIST
     C                   KFLD                    WDKKD_TYP
     C                   KFLD                    WDKKD_KD

     C     *LIKE         DEFINE    RMN           ZMN
     C     *LIKE         DEFINE    RGN           ZGN0
     C     *LIKE         DEFINE    RGN           ZGN1
     C     *LIKE         DEFINE    RGN           ZGN2
     C     *LIKE         DEFINE    RGN           ZGN3
     C     *LIKE         DEFINE    RREP0         ZREPB0
     C     *LIKE         DEFINE    RREP0         ZREPC0
     C     *LIKE         DEFINE    RREP1         ZREPC1
     C     *LIKE         DEFINE    RREP0         ZREPD0
     C     *LIKE         DEFINE    RREP1         ZREPD1
     C     *LIKE         DEFINE    RREP1         ZREPD2
     C     *LIKE         DEFINE    DKIH_SYAV     WVIH_SYAV
     C     *LIKE         DEFINE    DKIH_SYOP     WVIH_SYOP
     C     *LIKE         DEFINE    DKKD_TYP      WDKKD_TYP
     C     *LIKE         DEFINE    DKKD_KD       WDKKD_KD
     C                   Z-ADD     0             W1496             2 0
     C                   Z-ADD     0             W5278            11 2
     C                   Z-ADD     0             W1154             1 0
     C                   Z-ADD     0             W5004            17 2
      * INIT WORKFIELDS
     C                   EVAL      WDKKD_TYP='016'

     C     *ENTRY        PLIST
     C                   PARM                    WMN1              9
     C                   MOVEL     WMN1          WMN2
     C                   Z-ADD     WMN2          ZMN
      * Definiere Nøkkler
     C     DKIH_KEY      KLIST
     C                   KFLD                    WVIH_SYAV
     C                   KFLD                    WVIH_SYOP
      * Get msg and interchange values
     C     ZMN           CHAIN     EDIM                               51
     C     ZMN           CHAIN     ERFF96B                            54

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * Vilken typ av svar ?                                  SRA /
      *////////////////////////////////////////////////////////////
     C     SRA           BEGSR
     C     ZMN           CHAIN     ERFF96B                            54
     C                   SELECT
     C     R1153         WHENEQ    'ABE'
     C                   MOVEL     R1154         WR1154E          20
     C     R1153         WHENEQ    'ABT'
     C                   MOVEL     R1154         WR1154T          18
     C                   ENDSL
     C     ZMN           READE     ERFF96B                                54
     C                   IF        *IN54=*OFF
     C                   SELECT
     C     R1153         WHENEQ    'ABE'
     C                   MOVEL     R1154         WR1154E
     C     R1153         WHENEQ    'ABT'
     C                   MOVEL     R1154         WR1154T
     C                   ENDSL
     C                   ENDIF
      *_________________
      * Finnes ærendet ?
      *"""""""""""""""""
     C                   MOVEL     WR1154E       WVIH_SYAV
     C     7             SUBST     WR1154E:5     M0062B            7
     C                   MOVE      M0062B        WVIH_SYOP
     C     DKIH_KEY      CHAIN(N)  DKIH                               51
     C                   IF        *IN51 = *OFF
     C                   EXSR      SRAI
     C                   EVAL      WATYPE='DI'
     C                   EVAL      WUSER='SY400USR'
     C                   ELSE
     C     DKIH_KEY      CHAIN(N)  DKEH                               51
     C                   IF        *IN51 = *OFF
     C                   EXSR      SRAE
     C                   EVAL      WATYPE='DE'
     C                   EVAL      WUSER='SY400USR'
     C                   ENDIF
     C                   ENDIF
      *_________________________
      * Aktivera arkivfunktionen
      *"""""""""""""""""""""""""
     C                   CALL      'ARC002CLB'
     C                   PARM                    WATYPE            2
     C                   PARM                    WUSER            10

     C                   EVAL      WATYPE1='D1'
     C                   CALL      'ARC002CLB'
     C                   PARM                    WATYPE1           2
     C                   PARM                    WUSER            10

     C                   EVAL      WATYPE2='D2'
     C                   CALL      'ARC002CLB'
     C                   PARM                    WATYPE2           2
     C                   PARM                    WUSER            10

     C                   EVAL      WATYPE3='D3'
     C                   CALL      'ARC002CLB'
     C                   PARM                    WATYPE3           2
     C                   PARM                    WUSER            10

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * Import                                               SRAI /
      *////////////////////////////////////////////////////////////
     C     SRAI          BEGSR
      *____
      * GIS
      *""""
      * Første GIS+1
     C     ZMN           CHAIN     EGIS96B                            55
     C  N55              MOVEL     G7365         WG7365A           2
      * Ev. andre GIS+XX
     C     ZMN           READE     EGIS96B                                56
     C  N56              MOVEL     G7365         WG7365            2
     C                   IF        WG7365=*BLANKS
     C                   EVAL      WG7365 = WG7365A
     C                   ENDIF
      *____
      * DTM
      *""""
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     0             ZGN0
     C     KEYMS0        SETLL     EDTM96B
     C     KEYMS0        READE     EDTM96B                                51
     C     *IN51         DOWEQ     '0'
     C                   SELECT
     C     D2005         WHENEQ    '131'
     C                   MOVEL     D2380         WSADT             8
     C     D2005         WHENEQ    '154'
     C                   MOVEL     D2380         WGODT             8
     C     D2005         WHENEQ    '266'
     C                   MOVEL     D2380         WVADT             8
     C     D2005         WHENEQ    '178'
     C                   MOVEL     D2380         WANDT            12
     C                   ENDSL
     C     KEYMS0        READE     EDTM96B                                51
     C  N51              ENDDO
      *____
      * FTX
      *""""
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     10            ZGN0
     C                   MOVE      *BLANKS       W2                2
     C     KEYMS0        SETLL     EFTX96B
     C     KEYMS0        READE     EFTX96B                                51
     C     *IN51         DOWEQ     '0'
      *
      * Skriv til historikfil
     C                   CLEAR                   DKIHHR
     C                   EVAL      DKIHH_SYA = DKIH_SYAV
     C                   EVAL      DKIHH_SYO = DKIH_SYOP
     C                   MOVEL     MDT           DKIHH_DTH
     C                   MOVEL     MTM           DKIHH_TMH
     C                   EVAL      DKIHH_SR  = 'R'
     C                   EVAL      DKIHH_TXH  = 'CUSRES OK'
     C                   MOVEL     F4441         WDKKD_KD
     C     KODKEY        CHAIN     DKTKD                              53
     C  N53              EVAL      WTX256=DKKD_TXT
     C  N53F4441         CAT       WTX70A:1      DKIHH_TX1
     C  N53              EVAL      DKIHH_TX2=WTX70B
     C  N53              EVAL      DKIHH_TX3=WTX70C
     C  N53              EVAL      DKIHH_TX4=WTX70D
     C                   WRITE     DKIHHR
N01   * Skriv til TVINF
N01  C                   CLEAR                   TVINFF
N01  C                   EVAL      FMN = ZMN
N01  C                   EVAL      FLN = 1
N01  C                   EVAL      F0078A = DKIHH_TXH
N01  C                   EVAL      F0078B = DKIHH_TX1
     C                   EVAL      F0078C = DKIHH_TX2
     C                   EVAL      F0078D = DKIHH_TX3
     C                   EVAL      F0078E = DKIHH_TX4
N01  C                   WRITE     TVINFF
      * Øversætt 4-stællig kod till 2-stællig
     C                   SELECT
     C     WDKKD_KD      WHENEQ    '8003'
     C                   EVAL      W2='10'
     C     WDKKD_KD      WHENEQ    '8123'
     C                   EVAL      W2='20'
     C                   IF        WANDT <> *BLANKS
     C                   EVAL      W2='40'
     C                   ENDIF
     C     WDKKD_KD      WHENEQ    '8141'
     C                   EVAL      W2='20'
     C     WDKKD_KD      WHENEQ    '8149'
     C                   EVAL      W2='22'
     C     WDKKD_KD      WHENEQ    '8163'
     C                   EVAL      W2='20'
     C     WDKKD_KD      WHENEQ    '8168'
     C                   EVAL      W2='20'
     C     WDKKD_KD      WHENEQ    '8512'
     C                   EVAL      W2='20'
     C     WDKKD_KD      WHENEQ    '8530'
     C                   EVAL      W2='20'
     C     WDKKD_KD      WHENEQ    '8119'
     C                   EVAL      W2='40'
     C     WDKKD_KD      WHENEQ    '8120'
     C                   EVAL      W2='40'
     C     WDKKD_KD      WHENEQ    '8161'
     C                   EVAL      W2='40'
     C     WDKKD_KD      WHENEQ    '8162'
     C                   EVAL      W2='40'
     C     WDKKD_KD      WHENEQ    '8513'
     C                   EVAL      W2='40'
     C     WDKKD_KD      WHENEQ    '8529'
     C                   EVAL      W2='40'
     C     WDKKD_KD      WHENEQ    '8661'
     C                   EVAL      W2='40'
     C                   ENDSL

     C     KEYMS0        READE     EFTX96B                                51
     C  N51              ENDDO
      *________________________________
      * CST med tillhørande TAX/MOA/RFF
      *""""""""""""""""""""""""""""""""
     C                   EXSR      CST10
      *__________________
      * Uppdatera ærendet
      *""""""""""""""""""
     C     DKIH_KEY      CHAIN     DKIH                               61
     C                   IF        *IN61 = *OFF
     C     W2            IFNE      *BLANKS
     C                   MOVEL     W2            DKIH_SYST
     C                   ELSE
     C                   EVAL      DKIH_SYST=WG7365
     C                   ENDIF
     C                   EVAL      DKIH_07=M1004
     C                   EVAL      DKIH_07C=M1004
     C                   MOVEL     WGODT         DKIH_GODT
     C                   MOVEL     WVADT         DKIH_VADT
     C                   MOVEL     WSADT         DKIH_SADT
     C                   MOVEL     WANDT         DKIH_ANDT
     C                   UPDATE    DKIHR
     C                   IF        DKIH_SYST='40'
     C                   MOVE      DKIH_SYAV     DKIHASYAV
     C                   MOVE      DKIH_SYOP     DKIHASYOP
     C                   CALL      'DKI502C'
     C                   PARM                    DKIHASYAV
     C                   PARM                    DKIHASYOP
     C                   ENDIF
     C                   ENDIF
      *_________________
      * Skriv ut ærendet
      *"""""""""""""""""
     C                   IF        W2='40'
     C                   MOVE      'C'           calltyp
     C                   MOVE      DKIH_SYAV     DKIHASYAV         4
     C                   MOVE      DKIH_SYOP     DKIHASYOP         7
     C                   CALL      'DKI006R'
     C                   PARM                    DKIHASYAV
     C                   PARM                    DKIHASYOP
     C                   PARM                    calltyp           1
     C                   CALL      'DKI046R'
     C                   PARM                    DKIHASYAV
     C                   PARM                    DKIHASYOP
     C                   PARM                    calltyp           1
     C                   ENDIF

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * Export                                               SRAE /
      *////////////////////////////////////////////////////////////
     C     SRAE          BEGSR
      *____
      * GIS
      *""""
      * Første GIS+1
     C     ZMN           CHAIN     EGIS96B                            55
     C  N55              MOVEL     G7365         WG7365A           2
      * Ev. andre GIS+XX
     C     ZMN           READE     EGIS96B                                56
     C  N56              MOVEL     G7365         WG7365            2
     C                   IF        WG7365=*BLANKS
     C                   EVAL      WG7365 = WG7365A
     C                   ENDIF
      *____
      * LOC
      *""""
      * Faktisk udpassagested
     C     ZMN           CHAIN     ELOC96B                            57
     C  N57              MOVEL     L3225         WL3225           25
      *____
      * DTM
      *""""
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     0             ZGN0
     C     KEYMS0        SETLL     EDTM96B
     C     KEYMS0        READE     EDTM96B                                51
     C     *IN51         DOWEQ     '0'
     C                   SELECT
     C     D2005         WHENEQ    '154'
     C                   MOVEL     D2380         WDT154           12
     C     D2005         WHENEQ    '204'
     C                   MOVEL     D2380         WDT204           12
     C     D2005         WHENEQ    '266'
     C                   MOVEL     D2380         WDT266           12
     C     D2005         WHENEQ    '186'
     C                   MOVEL     D2380         WDT186           12
     C     D2005         WHENEQ    '129'
     C                   MOVEL     D2380         WDT129            8
     C     D2005         WHENEQ    '286'
     C                   MOVEL     D2380         WDT286            8
     C     D2005         WHENEQ    '351'
     C                   MOVEL     D2380         WDT351            8
     C     D2005         WHENEQ    '58'
     C                   MOVEL     D2380         WDT58             8
     C     D2005         WHENEQ    '178'
     C                   MOVEL     D2380         WDT178            8
     C                   ENDSL
     C     KEYMS0        READE     EDTM96B                                51
     C  N51              ENDDO
      *____
      * FTX
      *""""
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     10            ZGN0
     C     KEYMS0        SETLL     EFTX96B
     C     KEYMS0        READE     EFTX96B                                51
     C     *IN51         DOWEQ     '0'
      *
      * Skriv til historikfil
     C                   CLEAR                   DKEHHR
     C                   EVAL      DKEHH_SYA = DKEH_SYAV
     C                   EVAL      DKEHH_SYO = DKEH_SYOP
     C                   MOVEL     MDT           DKEHH_DTH
     C                   MOVEL     MTM           DKEHH_TMH
     C                   EVAL      DKEHH_SR  = 'R'
     C                   EVAL      DKEHH_TXH  = 'CUSRES OK'
     C                   MOVEL     F4440A        WDKKD_KD
     C                   EVAL      WDKKD_TYP='101'
     C     KODKEY        CHAIN     DKTKD                              53
     C  N53F4440A        CAT       DKKD_TXT:1    DKEHH_TX1
     C  N5370            SUBST     DKKD_TXT:66   DKEHH_TX2
     C  N5370            SUBST     DKKD_TXT:136  DKEHH_TX3
     C  N5350            SUBST     DKKD_TXT:206  DKEHH_TX4
     C                   WRITE     DKEHHR
N01   * Skriv til TVINF
N01  C                   CLEAR                   TVINFF
N01  C                   EVAL      FMN = ZMN
N01  C                   EVAL      FLN = 1
N01  C                   EVAL      F0078A = DKEHH_TXH
N01  C                   EVAL      F0078B = DKEHH_TX1
N01  C                   EVAL      F0078C = DKEHH_TX2
N01  C                   EVAL      F0078D = DKEHH_TX3
N01  C                   EVAL      F0078E = DKEHH_TX4
N01  C                   WRITE     TVINFF

     C     KEYMS0        READE     EFTX96B                                51
     C  N51              ENDDO
      *__________________
      * Uppdatera ærendet
      *""""""""""""""""""
     C     DKIH_KEY      CHAIN     DKEH                               61
     C                   IF        *IN61 = *OFF
     C     *LIKE         DEFINE    DKEH_SYST     WDKEH_SYST
     C                   EVAL      WDKEH_SYST=DKEH_SYST
     C                   SELECT
     C     WDKKD_KD      WHENEQ    '0017'
     C                   EVAL      DKEH_SYST='11'
     C     WDKKD_KD      WHENEQ    '0067'
     C                   EVAL      DKEH_SYST='15'
     C     WDKKD_KD      WHENEQ    '0001'
     C     DKEH_SYST     IFLE      '20'
     C                   EVAL      DKEH_SYST='20'
     C                   ENDIF
     C     WDKKD_KD      WHENEQ    '0005'
     C                   EVAL      DKEH_SYST='22'
     C     WDKKD_KD      WHENEQ    '0038'
     C                   EVAL      DKEH_SYST='23'
     C     WDKKD_KD      WHENEQ    '0007'
     C                   EVAL      DKEH_SYST='30'
     C     WDKKD_KD      WHENEQ    '0066'
     C                   EVAL      DKEH_SYST='39'
     C     WDKKD_KD      WHENEQ    '0004'
     C                   EVAL      DKEH_SYST='40'
     C     WDKKD_KD      WHENEQ    '0015'
     C                   EVAL      DKEH_SYST='44'
     C     WDKKD_KD      WHENEQ    '0016'
     C                   EVAL      DKEH_SYST='44'
     C     WDKKD_KD      WHENEQ    '0Z46'
     C                   EVAL      DKEH_SYST='46'
     C     WDKKD_KD      WHENEQ    '0050'
     C                   EVAL      DKEH_SYST='50'
     C     WDKKD_KD      WHENEQ    '0051'
     C                   EVAL      DKEH_SYST='51'
     C     WDKKD_KD      WHENEQ    '0052'
     C                   EVAL      DKEH_SYST='52'
     C     WDKKD_KD      WHENEQ    '0053'
     C                   EVAL      DKEH_SYST='53'
     C     WDKKD_KD      WHENEQ    '0054'
     C                   EVAL      DKEH_SYST='54'
     C     WDKKD_KD      WHENEQ    '0070'
     C                   EVAL      DKEH_SYST='70'
     C     WDKKD_KD      WHENEQ    '0063'
     C                   EVAL      DKEH_SYST='63'
     C     WDKKD_KD      WHENEQ    '0Z97'
     C                   EVAL      DKEH_SYST='97'
     C     WDKKD_KD      WHENEQ    '0Z98'
     C                   EVAL      DKEH_SYST='98'
     C                   ENDSL
     C                   IF        WDKEH_SYST='70'
     C                   IF        DKEH_SYST < '70'
     C                   EVAL      DKEH_SYST='70'
     C                   ENDIF
     C                   ENDIF
     C                   EVAL      DKEH_07=M1004
     C                   EVAL      DKEH_A2=M1004
     C                   MOVEL     WDT154        DKEH_GODT
     C                   MOVEL     WDT204        DKEH_FRDT
     C                   MOVEL     WDT266        DKEH_VADT
     C                   MOVEL     WDT129        DKEH_FUDT
     C                   MOVEL     WDT186        DKEH_FEDT
     C                   MOVEL     WDT286        DKEH_FVDT
     C                   MOVEL     WDT351        DKEH_CTDT
     C                   MOVEL     WDT58         DKEH_CFDT
     C                   MOVEL     WDT178        DKEH_FADT
     C                   MOVEL     WL3225        DKEH_FAST
     C     DKEH_MRN      IFEQ      *BLANKS
     C                   MOVEL     WR1154T       DKEH_MRN
     C                   ENDIF
     C                   UPDATE    DKEHR
     C                   IF        DKEH_SYST='40'
     C                   MOVE      DKEH_SYAV     DKIHASYAV
     C                   MOVE      DKEH_SYOP     DKIHASYOP
     C                   CALL      'DKI502C'
     C                   PARM                    DKIHASYAV
     C                   PARM                    DKIHASYOP
     C                   ENDIF
     C                   ENDIF
      *_________________
      * Skriv ut ærendet
      *"""""""""""""""""
     C                   IF        DKEH_SYST='40'
     C     WDKEH_SYST    IFLT      '40'
     C                   IF        DKEH_MRN <> *BLANKS
     C                   IF        WR1154T <> *BLANKS

     C                   MOVE      'C'           calltyp
     C                   MOVE      DKEH_SYAV     DKIHASYAV         4
     C                   MOVE      DKEH_SYOP     DKIHASYOP         7
     C                   CALL      'DKE006R'
     C                   PARM                    DKIHASYAV
     C                   PARM                    DKIHASYOP
     C                   PARM                    calltyp           1

     C*                  CALL      'DKZ002R'
     C*                  PARM                    DKIHASYAV
     C*                  PARM                    DKIHASYOP
     C*                  PARM                    calltyp           1

     C                   ENDIF
     C                   ENDIF
     C                   ENDIF
     C                   ENDIF

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * Import CST med tillhørande TAX/MOA/RFF              CST10 /
      *////////////////////////////////////////////////////////////
     C     CST10         BEGSR
     C                   Z-ADD     6             ZGN0
     C     KEYMS0        CHAIN     EDOC96B                            51
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     10            ZGN1
     C                   Z-ADD     DREP0         ZREPB0
     C     KEYMS1        SETLL     ECST96B
     C     KEYMS1        READE     ECST96B                                51
     C     *IN51         DOWEQ     '0'

      * Varupostnummer
     C                   MOVEA     C1496         A
     C                   EXSR      GETNUM
     C                   EVAL      W1496=ZNUM
      * TAX11
     C                   EXSR      TAX11

     C     KEYMS1        READE     ECST96B                                51
     C  N51              ENDDO
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * TAX11                                               TAX11 /
      *////////////////////////////////////////////////////////////
     C     TAX11         BEGSR
     C                   MOVE      '0'           *IN52
     C                   Z-ADD     11            ZGN2
     C                   Z-ADD     CREP0         ZREPC0
     C                   Z-ADD     CREP1         ZREPC1
     C     KEYMS2        SETLL     ETAX96B
     C     KEYMS2        READE     ETAX96B                                52
     C     *IN52         DOWEQ     '0'

      * Om detta ær førsta varuposten så radera gamla avgifter
     C                   IF        *IN91=*OFF
     C                   EXSR      DLTAVG
     C                   EVAL      *IN91=*ON
     C                   ENDIF
      * Sats_nr
     C                   MOVEA     T5278         A
     C                   EXSR      GETNUM
     C                   EVAL      W5278=ZNUM / 100
      * RFF11
     C     KEYMS2        CHAIN     ERFF96B                            53
     C                   MOVEA     R1154         A
     C                   EXSR      GETNUM
     C                   EVAL      W1154=ZNUM
      * MOA11
     C                   EXSR      MOA11

     C     KEYMS2        READE     ETAX96B                                52
     C  N52              ENDDO
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * Radera gamla avgifter                              DLTAVG /
      *////////////////////////////////////////////////////////////
     C     DLTAVG        BEGSR
     C                   MOVE      '0'           *IN54
     C     DKIH_KEY      SETLL     DKIVA
     C     DKIH_KEY      READE     DKIVA                                  54
     C     *IN54         DOWEQ     '0'

     C                   DELETE    DKIVAR

     C     DKIH_KEY      READE     DKIVA                                  54
     C  N54              ENDDO
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * MOA11                                               MOA11 /
      *////////////////////////////////////////////////////////////
     C     MOA11         BEGSR

     C                   CLEAR                   DKIVAR
     C                   EVAL       DKIVA_SAV=WVIH_SYAV
     C                   EVAL       DKIVA_SOP=WVIH_SYOP
     C                   EVAL       DKIVA_SLI=W1496
     C                   EVAL       DKIVA_ABA=T5289
     C                   EVAL       DKIVA_ABK=T5305
     C                   EVAL       DKIVA_ABS=W5278

     C                   MOVE      '0'           *IN55
     C                   Z-ADD     11            ZGN3
     C                   Z-ADD     TREP0         ZREPD0
     C                   Z-ADD     TREP1         ZREPD1
     C                   Z-ADD     TREP2         ZREPD2
     C     KEYMS3        SETLL     EMOA96B
     C     KEYMS3        READE     EMOA96B                                55
     C     *IN55         DOWEQ     '0'

      * Num
     C                   MOVEA     M5004         A
     C                   EXSR      GETNUM
     C                   SELECT
     C     M5025         WHENEQ    '161'
     C                   EVAL      DKIVA_ABB=ZNUM/100
     C     M5025         WHENEQ    '56'
     C                   EVAL      DKIVA_ABG=ZNUM
     C                   ENDSL

     C     KEYMS3        READE     EMOA96B                                55
     C  N55              ENDDO

     C                   WRITE     DKIVAR
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Get numerical value                               GETNUM /
      *////////////////////////////////////////////////////////////
     C     GETNUM        BEGSR
     C                   MOVE      *ZEROS        ZHEL
     C                   MOVE      *ZEROS        ZDEC
     C                   MOVE      *ZEROS        B
N01  C                   IF        A(1) = '-'
N01  C                   Z-SUB     1             XFORTEGN
N01  C                   Z-ADD     2             AX
N01  C                   ELSE
N01  C                   Z-ADD     1             XFORTEGN          1 0
     C                   Z-ADD     1             AX                2 0
N01  C                   END
     C                   Z-ADD     1             BX                2 0
      * Get Integer
     C     A(AX)         DOWGE     '0'
     C     ZHEL          MULT      10            ZHEL
     C                   MOVE      A(AX)         ZHEL
     C                   ADD       1             AX
     C                   ENDDO
      * Decimal point ?
     C     A(AX)         IFEQ      '.'
     C     A(AX)         OREQ      ','
     C                   ADD       1             AX
     C     A(AX)         DOWGE     '0'
     C                   MOVE      A(AX)         B(BX)
     C                   ADD       1             AX
     C                   ADD       1             BX
     C                   ENDDO
     C                   MOVEA     B             ZDEC6
     C                   ENDIF
      * Concatenate
     C                   Z-ADD     ZHEL          ZNUM             18 6
     C                   ADD       ZDEC          ZNUM
N01  C                   EVAL      ZNUM = ZNUM * XFORTEGN
      *
     C                   ENDSR
