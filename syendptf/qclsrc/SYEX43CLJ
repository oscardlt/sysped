/*  04 ENDRET LITT PÅ FRONTMARGIN TIL 1 TIL VENSTRE       */
/*  03 OVERLAY LIGGER NÅ INNE I DOKUMENTET                */
/*  BYTTET GETPR2 MED GETPRN FOR DRAWER OG ANTAL.L KOPIER */
/*N01 OVERSTYRER  OGSÅ SYFA12SAP (STREKKODE PÅ AFP/LASER) */
/*N05 OVERSTYRING PÅ MERKELAPP                            */
/*N06 OVERSTYRING PÅ SYEX43P UTEN LOGO             */
/*N07 4 KOPIER                                     */
             PGM        PARM(&IN03 &IN04)
             DCL        VAR(&DRAWER) TYPE(*CHAR) LEN(7)
             DCL        VAR(&DRAWUT) TYPE(*CHAR) LEN(11)
             DCL        VAR(&COPIES)  TYPE(*CHAR) LEN(6)
             DCL        VAR(&PNAVN)  TYPE(*CHAR) LEN(10)
             DCL        VAR(&FORM)  TYPE(*CHAR) LEN(10)
             DCL        VAR(&LOGO)  TYPE(*CHAR) LEN(9)
             DCL        VAR(&FINS) TYPE(*CHAR) LEN(1) VALUE('N')
             DCL        VAR(&ATYPE) TYPE(*CHAR) LEN(2) VALUE('cm')
/* N07 */    DCL        VAR(&ATYPE4) TYPE(*CHAR) LEN(2) VALUE('c4')
             DCL        VAR(&AUSER) TYPE(*CHAR) LEN(10)
             DCL        VAR(&IN03) TYPE(*LGL) LEN(1) /* Hvis &in03 +
                          på, skal GODSLISTE   være stående. */
             DCL        VAR(&IN04) TYPE(*LGL) LEN(1) /* Hvis &in04 +
                          på, skal printetype være 4224. */
             DCL        VAR(&PRT1) TYPE(*CHAR) LEN(10) /* Printernavn +
                          til stående losseliste. */
             DCL        VAR(&PT1) TYPE(*CHAR) LEN(10) /* Papirtype +
                          til stående losseliste. */
             DCL        VAR(&PRT2) TYPE(*CHAR) LEN(10) /* Printernavn +
                          til liggende losseliste. */
             DCL        VAR(&PT2) TYPE(*CHAR) LEN(10) /* Papirtype +
                          til liggende losseliste. */
             DCL        VAR(&PRT3) TYPE(*CHAR) LEN(10) /* Printernavn +
                          til CMR-FRAKTBREV        */
             DCL        VAR(&PT3) TYPE(*CHAR) LEN(10) /* Papirtype +
                          til CMR-FRAKTBREV        */
             DCL        VAR(&CPI) TYPE(*DEC) LEN(2 0) VALUE(10) /* +
                          Antall tegn pr. tomm. */
             DCL        VAR(&PRANT) TYPE(*CHAR) LEN(2) /* Antall +
                          utskrifter. */
             DCL        VAR(&CMR) TYPE(*DEC) LEN(3 0) VALUE(024)
             DCL        VAR(&LPI2) TYPE(*DEC) LEN(2 0)
             DCL        VAR(&LASER) TYPE(*CHAR) LEN(1)
             DCL        VAR(&AVD) TYPE(*CHAR) LEN(4)
             DCL        VAR(&LPP) TYPE(*DEC) LEN(3 0) VALUE(72)
             DCL        VAR(&NR67) TYPE(*DEC) LEN(3 0) VALUE(067)
             DCL        VAR(&PRT67) TYPE(*CHAR) LEN(10) /* Printernavn+
                          til ZEBRA/ELTRON-strekkodemerkelapper */
             DCL        VAR(&PT67) TYPE(*CHAR) LEN(10) /* Papirtype +
                          til ZEBRA/ELTRON strekkodemerkelapper */
             DCL        VAR(&KOPI) TYPE(*CHAR) LEN(1)

 RTVTIME:    RTVJOBA    USER(&AUSER)
             RTVDTAARA  DTAARA(*LDA (100 1)) RTNVAR(&KOPI)
    /* */
    /* SJEKKER OM LASER */
    /* */
             RTVDTAARA  DTAARA(*LDA (15 4)) RTNVAR(&AVD)
             CALL       PGM(GETPRN) PARM(&AVD &CMR &PNAVN &FORM +
                          &LASER &LPI2 &DRAWER &COPIES &LOGO &DRAWUT)
             IF         COND(&LASER = 'J') THEN(DO)
             CHGVAR     VAR(&LPP) VALUE('066')
             ENDDO
    /* */
             RTVDTAARA  DTAARA(*LDA (561 10)) RTNVAR(&PRT3)
             RTVDTAARA  DTAARA(*LDA (571 10)) RTNVAR(&PT3)
    /* */
    /* SKRIVER UT LOSSELISTE */
    /* ----------------------------------------------------------- */
    /* ORDNER PRINTER FOR FRAKTBREV */
    /**************************************/
    /* START FOR IKKE PDF DOKUMENTER      */
    /**************************************/
             IF         COND(&LASER *NE 'P') THEN(DO)
             OVRPRTF    FILE(SYEX43P) PAGESIZE(&LPP 80) CPI(10) +
                          OVRFLW(&LPP) DRAWER(&DRAWER) +
                          OUTBIN(&DRAWUT) PAGRTT(0) +
                          OUTQ(&PRT3) FORMTYPE(&PT3) COPIES(&COPIES)
             OVRPRTF    FILE(CMRBREV) PAGESIZE(&LPP 80) CPI(10) +
                          OVRFLW(&LPP) DRAWER(&DRAWER) +
                          OUTBIN(&DRAWUT) PAGRTT(0) FRONTOVL(CM1) +
                          OUTQ(&PRT3) FORMTYPE(&PT3) COPIES(&COPIES)
             ENDDO
    /**************************************/
    /* START FOR PDF DOKUMENTER           */
    /**************************************/
             IF         COND(&LASER *EQ 'P') THEN(DO)
             OVRPRTF    FILE(SYEX43P) DEVTYPE(*AFPDS) PAGESIZE(&LPP +
                          85) CPI(10)  OVRFLW(&LPP) +
                          DRAWER(&DRAWER) OUTBIN(&DRAWUT) PAGRTT(0) +
                          OUTQ(&PRT3) FORMTYPE(&PT3) +
                          COPIES(&COPIES) /*N02 */
             OVRPRTF    FILE(CMRBREV) DEVTYPE(*AFPDS) PAGESIZE(&LPP +
                          80) CPI(10) OVRFLW(&LPP) DRAWER(&DRAWER) +
                          OUTBIN(&DRAWUT) PAGRTT(0) FRONTOVL(CM1) +
                          OUTQ(&PRT3) FORMTYPE(&PT3)
             ENDDO
 /* EGEN OVERSTYRING FOR AFP-DOKUMENT, ANNEN PLASSERING AV FRONTOVL */
    /*************************************************************/
    /* START FOR AFP DOKUMENTER. FORSKYVING HØYRE VIA FRONTMGN.. */
    /*************************************************************/
             IF         COND(&LASER *EQ 'F') THEN(DO)
             OVRPRTF    FILE(SYEX43P) DEVTYPE(*AFPDS) PAGESIZE(&LPP +
                          85) CPI(10) FRONTMGN(0.2 0.2) +
                          OVRFLW(&LPP) DRAWER(&DRAWER) +
                          OUTBIN(&DRAWUT) PAGRTT(0) OUTQ(&PRT3) +
                          FORMTYPE(&PT3) COPIES(&COPIES)
             OVRPRTF    FILE(CMRBREV) DEVTYPE(*AFPDS) PAGESIZE(&LPP +
                          85) CPI(10) OVRFLW(&LPP) DRAWER(&DRAWER) +
                          OUTBIN(&DRAWUT) PAGRTT(0) FRONTOVL(CM1 +
                          0.2 0.1) OUTQ(&PRT3) FORMTYPE(&PT3) +
                          COPIES(&COPIES)
             ENDDO
/*N05*/      RTVDTAARA  DTAARA(*LDA (921 10)) RTNVAR(&PRT67)
/*N05*/      RTVDTAARA  DTAARA(*LDA (931 10)) RTNVAR(&PT67)
/*N05*/      IF         COND(&PRT67 = ' ') THEN(DO)
             CALL       PGM(GETPRT) PARM(&AVD &NR67 &PRT67 &PT67)
/*N05*/      ENDDO
             OVRPRTF    FILE(ZBARC) OUTQ(&PRT67) FORMTYPE(&PT67)
             OVRPRTF    FILE(SYLA03P) OUTQ(&PRT67) FORMTYPE(&PT67)
/*N01*/      OVRPRTF    FILE(SYFA12SAP) DEVTYPE(*AFPDS) PAGESIZE(70 +
                          80) LPI(8) CPI(10) OVRFLW(70) OUTQ(&PRT67) +
                          FORMTYPE(*STD)
             OVRMSGF    MSGF(QUSERMSG) TOMSGF(SYMSGF)
    /* ----------------------------------------------------------- */
    /* */
    /* ----------------------------------------------------------- */
    /* SKRIVER UT LASTELISTE */
             CALL       PGM(SYEX43)
    /* ----------------------------------------------------------- */
    /* */
             IF         COND(&KOPI *EQ ' ') THEN(DO)
             SBMJOB     CMD(CALL PGM(ARC002CL) PARM(&ATYPE &AUSER +
                          &FINS)) JOB(ARKIV) JOBD(*USRPRF) +
                          JOBQ(SYJOBQARK)
             ENDDO
/*N07*/
             IF         COND(&KOPI *EQ ' ') THEN(DO)
             SBMJOB     CMD(CALL PGM(ARC002CL) PARM(&ATYPE4 &AUSER +
                          &FINS)) JOB(ARKIV) JOBD(*USRPRF) +
                          JOBQ(SYJOBQARK)
             ENDDO
             ENDPGM
