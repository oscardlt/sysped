     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPPNLGRP(HLPSCR SYFA28PNL)
     A                                      HLPTITLE('VEDLIKEHOLD AVDELINGERS F-
     A                                      ASTE DATA')
     A                                      HELP
     A                                      ALTHELP
      *
     A          R SUBFIL                    SFL
     A            HOHT1         80   H
     A            HOHT2         80   H
     A            HOHT3         80   H
     A            HOHT4         80   H
     A            HOHT5         80   H
     A            HOHT6         80   H
     A            HOHT7         80   H
     A            HOBT1         80   H
     A            HOBT2         80   H
     A            HOBT3         80   H
     A            WSNR           1A  I  6  2VALUES(' ' '2' '4' '3')
     A  98                                  DSPATR(RI)
     A  98                                  DSPATR(PC)
     A            HOAVD          4Y 0O  6  4EDTCDE(4)
     A                                      COLOR(WHT)
     A            KOANVN        33A  O  6  9
     A  80                                  COLOR(RED)
     A  81                                  COLOR(BLU)
     A            HONET          1A  O  6 45
     A            HOSTFR         2A  O  6 54
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0016)
     A                                      SFLPAG(0015)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A          H                           HLPPNLGRP(SYFA280020 SYFA28PNL)
     A                                      HLPARA(01 001 23 080)
     A            ZRECNO         4S 0O  2 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYSPED/SYFA63I'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(055 DIV1472 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  3  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 34MSGCON(030 DIV0034 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  2MSGCON(070 SHU1208 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
      *
     A          R BUNN
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF03(03 'AVSLUTT')
     A                                      CF05(05 'Frisk')
     A                                      CF08(08 'SØK')
     A                                      SLNO(23)
     A                                      CLRL(02)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  1 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  1 42MSGCON(018 DIV0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  2  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                              2 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R VAREL2
     A                                      CF12(12 'Forrige')
     A                                      SLNO(22)
     A                                      CLRL(03)
     A                                      OVERLAY
     A                                  1  2MSGCON(012 DIV0026 *LIBL/SYMSGF)
     A            W2AAVD         4Y 0I  1 16DSPATR(UL)
     A                                      DSPATR(PC)
     A                                  3  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
      *
     A          R VAREL3
     A                                      CF12(12 'Forrige')
     A                                      CF23(23 'Bekreft sletting')
     A                                      SLNO(22)
     A                                      CLRL(03)
     A                                      OVERLAY
     A            SIAVD          4Y 0O  1  4EDTCDE(4)
     A                                      COLOR(WHT)
     A            KOANVN        33A  O  1  9
     A                                  3  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A                                  2  2MSGCON(075 SHU0983 *LIBL/SYMSGF)
     A                                      COLOR(RED)
      *
     A          R VAREL4
     A                                      CF12(12 'Forrige')
     A                                      SLNO(19)
     A                                      CLRL(06)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  2  2MSGCON(075 SHU1209 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            HONET          1A  O  3 19COLOR(WHT)
     A            HOAVD          4Y 0O  3  7COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  3 24MSGCON(020 SHU1204 *LIBL/SYMSGF)
     A            WSFRA          4Y 0B  3 45EDTCDE(4)
     A            WSTIL          4Y 0B  3 50EDTCDE(4)
     A                                  5  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  6 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A                                  3 12MSGCON(006 SHU1210 *LIBL/SYMSGF)
     A                                  3  2MSGCON(004 SHU1211 *LIBL/SYMSGF)
      *
     A          R NYVARE
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF03(03 'AVSLUTT')
     A                                      CF05(05 'Frisk')
     A                                      CF08(08 'SØK')
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/SYFA63I'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(055 DIV1472 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                  1 69TIME
     A                                  3  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 34MSGCON(030 DIV0017 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(002 DIV0025 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                  4  5MSGCON(075 DIV0584 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 42MSGCON(018 DIV0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                             24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
