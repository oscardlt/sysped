********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
12XXX * 01 SH  PTF12 skriv til logg    ......
********************************************************************
     H DATEDIT(*DMY)
N01  FKOSTL     O  A E             DISK
N01  FLEVEL01   IF   E           K DISK
     FXMLRCVF   IF   E           K DISK
     FKOSTA     O  A E           K DISK
     FKOSTA2    IF   E           K DISK
     F                                     RENAME(KOSTAR:KOSTAR2)
     FKOSTB     O  A E           K DISK
     FKOSTT     UF   E           K DISK
nxx  FKOSBU9    uf   e           k DISK
     FFIRM      IF   E           K DISK
nxx  Ffirmna    if   e           k DISK
     FFRISOL01  IF   E           K DISK
     FHEADF     IF   E           K DISK
     FHEAD39    IF   E           K DISK
     F                                     RENAME(HEADFR:HEADFR39)
nxx  FDOKUFM1   IF   E           K DISK
     FLISTE     O    F  132        PRINTER OFLIND(*INOF)
     D                SDS
     D  ZJOB                 244    253
     D  ZUSER                254    263
     D  ZJOBNR               264    269  0
     D                 DS
     D  XULTID                 1     14  0
     D  XULTM                  1      6  0
     D  XULDD                  7      8  0
     D  XULMM                  9     10  0
     D  XULÅÅ                 11     14  0
     D                 DS
     D  ULÅÅ                   1      4  0
     D  ULMM                   5      6  0
     D  ULDD                   7      8  0
     D  ULDT                   1      8  0
      * workvariabler
     D xxCustId        S             15A
     D xxCustNa        S             35A
     D xxInvNo         S             15A
     D xxRows          S              9S 0
     D xxSndn          S             17A
     D xxKllID         S             18A
xx   D xxKll35         S             35A
     D xxSndn20        S             20A
     D xxDesc          S             35A
     D xxSREF          S             20A
     D xxFvkt          S              6S 1
     D xxPris          S             11S 2
      *
     C                   EXSR      INIT
      *
      * Les EN OG EN TAG ("Nøstede tager" kommer nå i "rett rekkefølge" )
     C     XRSN          setll     XMLRCVF
     C     XRSN          reade     XMLRCVF                                94
     C     *in94         doweq     '0'
     C                   EXSR      SRXML2
     C     XRSN          reade     XMLRCVF                                94
     C                   enddo
      * write KOSTA
     c     KABNR         ifne      *zeros
     C                   WRITE     KOSTAR
N01  c                   EXSR      LOGGSR
     c                   endif
      *
     C                   SETON                                        LR
     C                   RETURN
      *
      *
      ***********************************************
     C     SRXML2        BEGSR
      ***********************************************
      * Handling utfra tag-navn
      *    ( ekstra tester dersom samme tagnavn fins på flere nivå                     )
      *    ( les i filene XMLRCAF (attributt-verdier)+ XMLRCLF ("Extras") om nødvendig )
     C                   SELECT
      * CustomerId
     C                   WHEN      XRNV = 'CustomerId'
     c                   eval      xxCustId = XRVERD
      * CustomerName
     C                   WHEN      XRNV = 'CustomerName'
     c                   eval      xxCustNa = XRVERD
      * InvoiceNumber
     C                   WHEN      XRNV = 'InvoiceNumber'
     c                   eval      xxInvNo  = XRVERD
     C* NumberOfRows    = Siste tag i header  - INITIER KOSTA-records(skrives til sist)
     C                   WHEN      XRNV = 'NumberOfRows'
     c                   eval      xxRows = %float(XRVERD)
     C                   EXSR      SRKAI
      *
      *
      *
      * ShipmentNumber/sendingsnr 17 lang
     C                   WHEN      XRNV = 'ShipmentNumber'
     c                   eval      xxSndn = XRVERD
      * PackageNumber/kolli-ID 18 lang
     C                   WHEN      XRNV = 'PackageNumber'
     c                   eval      xxKllID= XRVERD
     c                   eval      xxKll35= XRVERD
      * Description
     C                   WHEN      XRNV = 'Description'
     c                   eval      xxDesc = XRVERD
      * SenderReference
     C                   WHEN      XRNV = 'SenderReference'
     c                   eval      xxSref = XRVERD
      * CalculatedShipp  = fraktvekt
     C                   WHEN      XRNV = 'CalculatedShipp'
     c                   eval      xxFvkt = %float(XRVERD)
     C* 'AgreementPrice' = Siste tag i linje - behandle linje
     C                   WHEN      XRNV = 'AgreementPrice'
     c                   eval(H)   xxPris = %float(XRVERD)
nxx  c                   if        xxPris <> 0
     C                   EXSR      SRKB
nxx  c                   endif
     C                   ENDSL
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
     C     SRKB          BEGSR
      *////////////////////////////////////////////////////////////
      *
N03  c                   move      *blanks       KBBILT
     C                   MOVE      *ZEROS        XXMVA             9 2
      * FAST MVA PÅ DISSE TJENESTER...
     C                   MOVE      'J'           XXMVAJN           1
     C     XXMVAJN       IFEQ      'J'
     C     xxPris        MULT(H)   FITAXN        XXMVA
     C                   END
      *
     C     xxPris        ADD       XXMVA         KBBLHB
     C                   SUB       KBBLHB        KABL                           Headervariabel
     C                   SUB       XXMVA         KABLM                          Headervariabel
     C     XXMVA         IFNE      *ZEROS
     C                   Z-ADD     1             KBKDM
     C                   MOVE      'P'           KBKDPF
     C                   ELSE
     C                   Z-ADD     0             KBKDM
     C                   MOVE      'F'           KBKDPF
     C                   END
      *
     C                   MOVE      *ZEROS        KBAVD
     C                   MOVE      *ZEROS        KBOPD
     C                   MOVE      *ZEROS        KBKN
     C                   MOVE      xxSndn        KBBILT
      *
     C                   MOVE      *ZEROS        HEAVD
     C                   MOVE      *ZEROS        HEOPD
      *
      * 1. SENDINGSNR - INNLAND??
     c                   eval      xxSndn20='401'+XXSndn
     C     XXSndn20      CHAIN     HEAD39                             33
     C     *IN33         IFEQ      '0'
     C                   GOTO      UTOPD
     C                   END
xx    * 1.B  Kolli-nummer
xxx  C     XXKll35       CHAIN     DOKUFM1                            33
xx   C     *IN33         IFEQ      '0'
xx   C                   MOVE      FMAVD         HEAVD
xx   C                   MOVE      FMOPD         HEOPD
xxx  C     OPDKEY        CHAIN     HEADF                              33
xx   C  N33              GOTO      UTOPD
xx   C                   MOVE      *ZEROS        HEAVD
xxx  C                   MOVE      *ZEROS        HEOPD
xx   C                   END
xx   C*
      * 2. Fri Søkevei
     C                   eval      FSSOK    =    xxSndn
     C     FSKEY         CHAIN     FRISOL01                           33
     C     *IN33         IFEQ      '0'
     C                   MOVE      FSAVD         HEAVD
     C                   MOVE      FSOPD         HEOPD
     C     OPDKEY        CHAIN     HEADF                              33
     C  N33              GOTO      UTOPD
     C                   MOVE      *ZEROS        HEAVD
     C                   MOVE      *ZEROS        HEOPD
     C                   END
      *
      * 3. Oppgitt i Senders Ref
     C                   IF        %subst(xxSref:5:1)='/' and
     C                             %subst(xxSref:13:1)='/'
     C                   eval      HEAVD = %float(%subst(xxSref:1:4))
     C                   eval      HEOPD = %float(%subst(xxSref:6:7))
     C     OPDKEY        CHAIN     HEADF                              33
     C  N33              GOTO      UTOPD
     C                   MOVE      *ZEROS        HEAVD
     C                   MOVE      *ZEROS        HEOPD
     C                   END
      *
      *
     C     UTOPD         TAG
      * Legg ut posten i KOSTB
      *
     C                   EXSR      SRKBU
      *
      *
     C                   ENDSR
      *
      *
      *
      *////////////////////////////////////////////////////////////
     C     SRKBU         BEGSR
      *////////////////////////////////////////////////////////////
      *
      * MANGLENDE MATCH -  Skriv til liste...
     C                   MOVE      *ZEROS        KBKAVD
     C                   MOVE      *BLANKS       KBKKEY
n03  C                   MOVE      *BLANKS       MATCHTXT         20
      * HEAVD/HEOPD lik 0 = ref ikke funnet - åpne ferdig-status m.m.
     C* 7/8 av KBTUNR: 7:GEBYR,1=GEB.0=Vkt 8:NØKKEL,1=etter opd 9=IKKE
     C     HEOPD         IFEQ      *ZEROS                                       B1
     C                   MOVE      ' '           KAST
n03  c                   eval      MATCHTXT= 'Ingen match !!!'
     C                   MOVE      1             KBGEBY
     C                   MOVE      9             KBNØKK
     C                   ELSE                                                   X1
     C                   MOVE      1             KBGEBY
     C                   MOVE      1             KBNØKK
     C                   Z-ADD     HEAVD         KBKAVD
     C                   MOVEL     HEOPD         KBKKEY
     C                   MOVE      HEAVD         KBAVD
     C                   MOVE      HEOPD         KBOPD
     C                   SELECT
     C     HEUR          WHENEQ    'H'
     C                   MOVE      HEKNSF        KBKN
     C     KBKN          IFEQ      *ZEROS
     C                   MOVE      HEKNKF        KBKN
     C                   END
     C     HEUR          WHENEQ    'A'
     C     HEUR          OREQ      'C'                                          IMPORT
     C     HEUR          OREQ      'F'
     C                   MOVE      HEKNKF        KBKN
     C     KBKN          IFEQ      *ZEROS
     C                   MOVE      HEKNSF        KBKN
     C                   END
     C     HEUR          WHENEQ    'B'                                          EKSPORT
     C     HEUR          OREQ      'D'
     C     HEUR          OREQ      'G'
     C                   MOVE      HEKNSF        KBKN
     C     KBKN          IFEQ      *ZEROS
     C                   MOVE      HEKNKF        KBKN
     C                   END
     C                   ENDSL
      * Ikke funnet kunde - åpne bilaget m.m.
     C     KBKN          IFEQ      *ZEROS                                        B2
     c                   eval      MATCHTXT= 'Knr mangler(oppd)!!!'
     C                   MOVE      ' '           KAST
     C                   MOVE      9             KBNØKK
     C                   END                                                     E2
      *
     C                   END                                                    E1
      * GEBYRKODE
     c                   select
      * Bediftspakke / Bedriftspakke ekspress over natt / Klimanøytral Servicepakke ..
     C                   when      %subst(xxDesc:1:13)='Bedriftspakke' or
     C                             %subst(xxDesc:1:13)='Klimanøytral '
     C                   MOVE      'FRA'         KBVK
     c
     C                   when      %subst(xxDesc:1:9)='Bomavgift'
     C                   MOVE      'SBT'         KBVK
     c
     C                   other
     C                   if        xxCustID='00100420884'
     C                   MOVE      'DRM'         KBVK                           DIVERSE DRAMMEN
     C                   else
     C                   MOVE      'OSL'         KBVK                           DIVERSE OSLO
     c                   endif
     c
     c                   endsl
tmp  c                   if        KBAVD = 8197 and KBOPD= 1029
tmp  c                   move      'x'           dbgDummy          1
tmp  c                   endif
nxx   * sjekker budsjett
nxx  c                   move      '   '         kbsgg
nxx  c                   move      *zeros        kbbuds
nxx  c                   if        KBNØKK  <>  9
nxx  c                   if        KBVK = 'FRA'  or KBVK = 'SBT'
nxx  c                   exsr      budsro
nxx  c                   endif
nxx  c                   endif
      * Info i referansefelter
     c                   eval      KBRefA = xxSndn
     c                   eval      KBRefB = xxKllID
     c                   eval      KBRefC = xxDesc
      *
     C                   WRITE     KOSTBR
      *
      *
      * MANGLENDE MATCH -  Skriv til liste...
     C     KBNØKK        IFEQ      9
     C   OF              EXCEPT    PHEAD
     C   OF              SETOFF                                       OF
     C                   EXCEPT    DET
     C                   END
      *
     C                   ENDSR
      *
      *
      *////////////////////////////////////////////////////////////
     C     SRKAI         BEGSR
      *////////////////////////////////////////////////////////////
N03   * SAMME FAKT-NR MOTTATT FØR.. (FRA GITT LEVERANDØR??)  (men ignorere slettede)
N03  C                   eval      KALNR = 1316
N03  C                   eval      KAFNR = xxInvNo
N03  C     KOSA2K        SETLL     KOSTA2                             33
N03  C     KOSA2K        READE     KOSTA2                                 33
N03  C     *IN33         DOWEQ     '0'
N03  C     kast          IFNE      'D'
N03  C* Finnes fra før ?? - meld og hopp ut 'J'
     C                   EXCEPT    PHEAD
     C                   EXCEPT    DETFIN
     C                   SETON                                        LR
     C                   RETURN
N03  C                   END
N03  C     KOSA2K        READE     KOSTA2                                 33
N03  C                   END
N03   *
N03   * bilaget SKAL behandles
      * INITIERING AV KOSTA-FELT
     C                   CLEAR                   KOSTAR
N03  C                   eval      KALNR = 1316
N03  C                   eval      KAFNR = xxInvNo
     C                   MOVE      'EDI'         KASG
     C                   MOVE      'NOK'         KAVAL
     C                   Z-ADD     100           KAVKU
     C                   Z-ADD     100           KAOMRF
     C                   MOVE      ZUSER         KAUSER
     C                   TIME                    XULTID
     C                   MOVE      XULDD         ULDD
     C                   MOVE      XULMM         ULMM
     C                   MOVE      XULÅÅ         ULÅÅ
     C                   MOVE      ULDT          KADTE
     C                   MOVE      XULTM         KATME
     C                   MOVE      ULDT          KADTR
     C                   MOVE      XULTM         KATDR
      *
     C                   MOVE      'N'           UKJENT            1
      *
     C* TILDEL PRE.REG.NR (LØPENR FRA FAST TELLER)...
     C     'Ø'           CHAIN     KOSTT                              33
     C                   MOVE      KTNR          KBBNR
     C                   ADD       1             KTNR
     C  N33              UPDATE    KOSTTR
      * Prereg løpenr
     C                   MOVE      KBBNR         KABNR
     c                   move      ' '           BILSERIE          1
     C     BILSERIE      IFNE      *BLANKS
     C* Bilagsnr til økonomi
     C     BILSERIE      CHAIN     KOSTT                              33
     C  N33              MOVE      KTNR          KABNR2
     C  N33              ADD       1             KTNR
     C  N33              UPDATE    KOSTTR
     C                   END
      * Bilagsdato / Lev.s fakt.dato/forfallsdato/KID  - mangler dessverre i fila
     C*                  Z-ADD     INDATO        KABDT
     C*                  Z-ADD     INDATO        KAFDT
     C*                  MOVE      INDAFF        KAFFDT
     C*                  MOVEL     INKID         KALKID
      *
     C*                  MOVE      20            KAPCC
     C*                  MOVE      INAAR         KAPÅR
     C*                  MOVE      INMND         KAPMN
      * MOMSSATS
      * MOTTAR DESSVERRE IKKE BILAGSDATO - BENYTT DAGENS FOR Å HENTE MOMSSATS
N01  c                   move      KADTE         MVADT             8 0
N01  C     MVADT         IFEQ      *zeros
N01  c                   move      ULDT          MVADT
N01  c                   endif
     C                   Z-ADD     FITAX         FITAXN            5 4          0,2500
     C     FITAXD        IFNE      *ZEROS
     C     FITAX2        ANDNE     *ZEROS
N01  C     MVADT         ANDLT     FITAXD
     C                   Z-ADD     FITAX2        FITAXN
     C                   END
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
     C     INIT          BEGSR
      *////////////////////////////////////////////////////////////
     C     *ENTRY        PLIST
     C                   PARM                    USN               9
     C                   MOVE      USN           XRSN
      *
     C     FSKEY         KLIST
     C                   KFLD                    FSKODE
     C                   KFLD                    FSSOK
     C                   MOVEL     'IFB'         FSKODE
      *
     C     OPDKEY        KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
      *
     C     KOSA2K        KLIST
     C                   KFLD                    KALNR
     C                   KFLD                    KAFNR
      *
     C     *LOVAL        SETLL     FIRM
     C                   READ      FIRM                                   33
nxx   * Navitro for øvre/nedre grense budsjett
nxx  c     fifirm        chain     firmna                             34
Nxx  c     nagrpl        div       100           nagrplu           7 3
Nxx  c     nagrmi        div       100           nagrmin           7 3
      * Trigge print av heading (hvis manglende match)
     C                   SETON                                        OF
      *
     C                   ENDSR
      *
nxx  c***************************************************************
nxx  c     budsro        begsr
Nxx  c***************************************************************
nxx   * henter eventuelle budsjetter (alltid på sendingsnivå fra Bring ...)
nxx   * VAREKODE MÅ MATCHE (sjekker FRA og SBT/Bomtillegg separat)
tmp  c                   if        KBAVD = 8197 and KBOPD= 1029
tmp  c                   move      'x'           dbgDummy          1
tmp  c                   endif
Nxx  c                   move      '   '         kbsgg
Nxx  c                   setoff                                       33
nxx  c                   move      *zeros        totopd           13 2
nxx  c     keybudo       klist
Nxx  c                   kfld                    blank0            1
Nxx  c                   kfld                    kbavd
Nxx  c                   kfld                    kbopd
nxx  c     kbavd         ifne      0
nxx  c     kbopd         andne     0
Nxx  c     keybudo       setll     kosbu9
nxx  c     *in33         doweq     '0'
nxx  c     keybudo       reade     kosbu9                                 33
Nxx  c     *in33         ifeq      '0'
Nxx  c     kalnr         ifeq      bulnr
Nxx  c     kbvk          andeq     buvk
Nxx  c                   add       bubl          totopd
nxx  c                   move      'F'           bust
nxx  c                   endif
nxx  c                   update    kosbur
nxx  c                   endif
nxx  c                   enddo
Nxx  c                   end
Nxx   *
nxx   *
nxx   *  Lagrer budsjettpost uten MVA
Nxx  c                   z-add     totopd        kbbuds
nxx   *  Legg på evt MVA  før sammenligning mot linjenum
     c                   if        KBKDM = 1
Nxx  c                   eval(h)   totopd = totopd + totopd * FITAXN
     c                   endif
Nxx   *
Nxx  c     totopd        ifne      0
nxx  c     totopd        div       kbblhb        diffbu           13 3
nxx  c                   sub       1             diffbu
Nxx  c     diffbu        iflt      nagrplu
nxx  c     diffbu        andge     0
nxx  c                   move      'AUT'         kbsgg
nxx  c                   end
Nxx  c     diffbu        ifgt      nagrmin
Nxx  c     diffbu        andle     0
Nxx  c                   move      'AUT'         kbsgg
nxx  c                   end
nxx  c                   end
Nxx  c                   endsr
Nxx   *
N01  C***********************************************
N01  C     LOGGSR        BEGSR
N01  C***********************************************
N01  c     levek         klist
N01  c                   kfld                    fifirm
N01  c                   kfld                    kalnr
N01  c     levek         chain     level01                            44
N01  c     *in44         ifeq      '0'
N01  C                   CALL      'SYGE15C'
N01  C                   PARM                    WDT               8
N01  C                   PARM                    WTM               6
N01  c                   move      wdt           LODATO
N01  c                   move      wtm           loklok
N01  c                   eval      louser='NAVITRO'
N01  c                   move      kabnr         lounik
N01  c                   move      kabnr2        lobila
n01  c                   move      'E'           lofoan
n01  c                   move      *zeros        lojour
n01  c                   move      kasg          losg
n01  c
N01  C                   eval      lotxt='Mottat B2B '
N01  c                             +'Faktura fra:'
N01  c                             +%trim(LNAVN)
n01  c                   write     kostlr
n01  c                   end
n01  c                   endsr
      *
     OLISTE     E            PHEAD            01
     O                       FIFT                45
     O                                           71 'Fakt.nr:'
     O                       XXINVNO             88
     O                                           98 'Kn:'
     O                       xxCustId           115
     OLISTE     E            PHEAD            02
     O                                           24 '** BRING/POSTEN FAKTURA '
     O                                           48 '. M/REF.  SOM IKKE MATCH'
     O                                           72 'ER SYSPED OPPDRAG **    '
     O                                          100 'DATO:'
     O                       UDATE         Y    110
     O                                          120 'SIDE:'
     O                       PAGE          Z    126
N03
N03  OLISTE     E            PHEAD            03
N03  O                                           24 '-----------       ****  '
N03  O                                           48 'Mottatte referanser for '
N03  O                                           72 'matching ****      -----'
N03  O                                           96 '------------------------'
N03  O                                          120 '------------------------'
N03  O                                          132 '------------'
     O          E            PHEAD            04
N03  O                                           10 'PREREG.NR'
N03  O                                           35 'SENDINGSNUMMER  '
N03  O                                           65 'ANNEN REFERANSE     '
N03  O                                           88 'FEILTYPE            '
N03  O                                          104 'NETTO BELØP'
N03  O                                          120 'MOMS'
     OLISTE     E            PHEAD          1 05
     O                                           24 '------------------------'
     O                       *PLACE              48
     O                       *PLACE              72
     O                       *PLACE              96
     O                       *PLACE             120
     O                                          132 '------------'
      *
     OLISTE     E            DET            1
     O                       KABNR         Z     10
     O                       XXSNDN              35
     O                       XXSREF              65
     O                       MATCHTXT            88
     O                       XXPRIS        J    105
     O                       XXMVA         J    121
     OLISTE     E            DETFIN         1
     O                                           28 '*FAKTURA FINNES FRA FØR*'
     O                                           33 'Lev:'
     O                       KALNR         Z     41
     O                                           47 'F.nr:'
     O                       KAFNR               61
     O                                           75 '(M/Prereg.nr:'
     O                       KABNR         Z     84
     O                                           90 'B.nr:'
     O                       KABNR2        Z     97
     O                                          105 'B.dt:'
     O                       KABDT              116 '    .  .  '
     O                                          118 ')'
