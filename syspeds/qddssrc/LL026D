********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr *Sek Sig Vers Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
PXXXX * 01 YBC PTF6 BESKRIVELSE OM PALLELAGER
P7113 * 02 YBC PTF7 REGISTRERE PALL-ID
********************************************************************
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HELP
     A                                      ALTHELP(CA01)
      * "NOERASE" MÅ HA FOR Å IKKE TØMME BILDE SOM MAN KOMME FRA
     A          R NOERASE
     A                                      ASSUME
     A                                  1  2' '
      * MAN KUN DEFINNERE VINDU PÅ REKORDNIVÅ
     A          R WINDOWSZ
E02  A                                      WINDOW(*DFT 14 57 *NOMSGLIN)
     A                                      OVERLAY
      *
     A          R SUBFIL                    SFL
E01  A            WSNR           1A  I  6  2VALUES(' ' '2' '4')
     A            XLLOK         25A  H
E01  A            XLOK1          5A  O  6  5
E01  A            XLOK2         20A  O  6 11
N02  A            XPLID         20A  O  6 32
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      WINDOW(WINDOWSZ)
     A                                      SFLSIZ(0007)
     A                                      SFLPAG(0006)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                      CA03(03 'AVSLUTT')
     A                                      CA06(06 'LAGE NY')
     A                                      CA12(12 'Forrige')
     A                                      OVERLAY
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/LL026R'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(30 YBC1364 SYMSGF)
     A                                      DSPATR(HI)
     A            ZRECNO         4S 0O  1 50SFLRCDNBR
     A                                      DSPATR(ND)
N01  A                                  2  2MSGCON(55 YBC1365 SYMSGF)
N01  A                                      COLOR(BLU)
E01  A                                  3  2MSGCON(12 YBC1366 SYMSGF)
E01  A            UANT           5  0O  3 15DSPATR(UL)
     A                                      DSPATR(HI)
     A                                      EDTCDE(4)
E01  A                                  3 21MSGCON(11 YBC1367 SYMSGF)
E01  A            XANTM          5  0O  3 33DSPATR(UL)
     A                                      DSPATR(HI)
     A                                      EDTCDE(4)
E01  A                                  3 39MSGCON(11 YBC1368 SYMSGF)
E01  A            XANTU          5  0O  3 51DSPATR(UL)
     A                                      DSPATR(HI)
     A                                      EDTCDE(4)
E01  A                                  4  2MSGCON(40 YBC1369 SYMSGF)
     A                                      COLOR(BLU)
E02  A                                  5  2MSGCON(50 YBC1370 SYMSGF)
     A                                      DSPATR(HI)
      *
     A          R BUNN
     A                                      WINDOW(WINDOWSZ)
E02  A                                 13  2MSGCON(40 YBC1371 SYMSGF)
     A                                      COLOR(BLU)
E02  A  30        MSGTXT        50A  O 14  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R VARELI
     A                                      WINDOW(WINDOWSZ)
     A                                      CF04(04 'Søk')
     A                                      CA12(12 'Forrige')
E01  A                                      SLNO(12)
E02  A                                      CLRL(03)
     A                                      OVERLAY
     A            MSGNR          7A  P
E02  A                                  1  5MSGCON(50 YBC1372 SYMSGF)
     A                                      DSPATR(HI)
E02  A            WSLOK1         5A  B  2  5DSPATR(UL)
     A  90                                  DSPATR(PC)
     A  90                                  DSPATR(RI)
E02  A            WSLOK2        20A  B  2 11DSPATR(UL)
     A N30
     AO 89                                  DSPATR(PC)
N02  A            WSPLID        20A  B  2 32DSPATR(UL)
E02  A N30                              3  2MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
E02  A  30        MSGTXT        50A  O  3  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
      *
     A          R VAREL3
     A                                      WINDOW(WINDOWSZ)
     A                                      CA12(12 'Forrige')
E01  A                                      SLNO(12)
E02  A                                      CLRL(03)
     A                                      OVERLAY
E02  A                                  1  5MSGCON(50 YBC1372 SYMSGF)
     A                                      DSPATR(HI)
E02  A            XLOK1          5A  O  2  5DSPATR(UL)
     A                                      DSPATR(PC)
E02  A            XLOK2         20A  O  2 11DSPATR(UL)
N02  A            XPLID         20A  O  2 32DSPATR(UL)
E02  A                                  3  2MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
