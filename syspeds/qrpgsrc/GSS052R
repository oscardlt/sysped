      *
     FGSS052F   UF   E           K DISK
     FGSS052F2  O  A E             DISK
     FGSSORF    IF   E           K DISK
     FGSSOVF    IF   E           K DISK
     FGSSPOF    IF   E           K DISK
     FGSSPAF    IF   E           K DISK
     FFIRM      IF   E           K DISK
     FCUNDL01   IF   E           K DISK
     FKODTDIN   IF   E           K DISK
     FSTED2     IF   E           K DISK
     FVALUL01   IF   E           K DISK
     FGSS052D   CF   E             WORKSTN SFILE(GSS052DA:ZRECNO)
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D                 DS
     D  ORDTLÅ                 1      4  0
     D  ORDTLM                 5      6  0
     D  ORDTLD                 7      8  0
     D  ORDTL                  1      8  0
     D                 DS
     D  WXDTLD                 1      2  0
     D  WXDTLM                 3      4  0
     D  WXDTLÅ                 5      6  0
     D  WXDTL                  1      6  0
     D                 DS
     D  POCONO                 1     20
     D  XPOCONO                1     17
     D                SDS
     D  UUSER                254    263
      *
     C                   EXSR      INIT
      *
     C     START         TAG
     C                   MOVE      '1'           *IN(93)
     C                   WRITE     GSS052DB
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
     C                   MOVE      *BLANKS       WSNR
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     UUSER         SETLL     GSS052F
     C     UUSER         READE(N)  GSS052F                                94
     C                   DOW       *IN94 = *OFF
     C     G52ONO        CHAIN     GSSORF                             33
     C                   ADD       1             ZRECNO
     C                   MOVE      ORDTLD        WXDTLD
     C                   MOVE      ORDTLM        WXDTLM
     C                   MOVE      ORDTLÅ        WXDTLÅ
     C                   ADD       ORCLI         WSCLI
     C                   ADD       ORWT          WSWT
     C                   ADD       ORVOL         WSVOL
     C     ORONO         CHAIN     GSSPOF                             33
     C   33              MOVE      *BLANKS       POCONO
     C     ORONO         CHAIN     GSSPAF                             33
     C  N33              MOVE      ' '           PAPKD
     C   33              MOVE      'C'           PAPKD
     C                   WRITE     GSS052DA
     C     UUSER         READE(N)  GSS052F                                94
     C                   ENDDO
      *
     C     START3        TAG
     C  N30              Z-ADD     5             LINNBR
     C  N30              Z-ADD     2             POSNBR
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     GSS052DG
     C                   ELSE
     C                   WRITE     GSS052DC
     C                   EXFMT     GSS052DB
     C                   END
      *
      *** AVSLUTT
     C                   MOVE      '0'           *IN30
     C                   MOVEA     '00'          *IN(98)
      *
     C   03              GOTO      SLUTT
     C   05              GOTO      START
     C   12              GOTO      SLUTT
     C   10              DO
     C                   MOVE      'J'           UTILPR
     C                   EXSR      SUMBL
     C                   GOTO      SLUTT
     C                   END
      *
      *
     C     ZRECNO        IFNE      0
     C                   EXSR      HHENT
     C                   END
     C                   GOTO      START3
      *
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
      *
     C***********************************************
     C     HHENT         BEGSR
     C***********************************************
     C                   MOVE      *BLANKS       MSGNR
     C                   MOVEA     '000'         *IN(90)
     C                   MOVE      '0'           *IN99
     C     *IN98         DOWEQ     '0'
     C                   READC     GSS052DA                               98
     C   98              DO
     C   99              MOVE      '0'           *IN98
     C                   GOTO      SLHENT
     C                   END
      *
     C     ZRECNO        DIV       17            LINNBR
     C                   MVR                     XLIN              2 0
     C     XLIN          IFEQ      0
     C                   Z-ADD     21            LINNBR
     C                   ELSE
     C     XLIN          MULT      2             LINNBR
     C                   ADD       3             LINNBR
     C                   END
      *
     C                   SELECT
     C     WSNR          WHENEQ    '4'
     C     GSS052FK      CHAIN     GSS052F                            33
     C                   IF        *IN33 = *OFF
     C                   DELETE    GSS052FR
     C                   EVAL      ORRTNG = ' '
     C                   EVAL      ORCNO  = 0
     C                   EVAL      ORSNO  = 0
     C                   EVAL      ORBCD  = '*DLT'
     C                   EVAL      WXDTL  = 0
     C                   EVAL      ORLC   = *BLANKS
     C                   EVAL      POCONO = *BLANKS
     C                   EVAL      WSCLI  = WSCLI - ORCLI
     C                   EVAL      WSWT   = WSWT - ORWT
     C                   EVAL      WSVOL  = WSVOL - ORVOL
     C                   EVAL      PAPKD  = *BLANKS
     C                   END
     C                   MOVE      *BLANKS       WSNR
     C                   UPDATE    GSS052DA
     C                   OTHER
     C                   MOVE      '1'           *IN98
     C                   MOVE      *BLANKS       WSNR
     C                   UPDATE    GSS052DA
     C                   GOTO      SLHENT
     C                   ENDSL
     C                   MOVE      '1'           *IN99
      *
     C                   END
      *
     C     SLHENT        TAG
     C                   MOVE      '0'           *IN99
     C   98MSGNR         IFEQ      *BLANKS
     C                   MOVE      'GSS0009'     MSGNR
     C                   END
     C   98              MOVE      '1'           *IN30
     C  N30              Z-ADD     5             LINNBR
     C                   Z-ADD     2             POSNBR
      *
     C                   ENDSR
      *
      ***********************************************
     C     SUMBL         BEGSR
      ***********************************************
     C                   EVAL      G52USE = UUSER
     C                   EVAL      G52LN  = 1
     C                   EVAL      G52VK  = '*SH'
     C                   EVAL      G52VT  = 'Outlay from ship-sys'
     C                   EVAL      G52KN  = ORCNO
     C                   EVAL      G52KDA = 'S'
     C                   MOVE      *ZEROS        XBELV1           13 2
     C                   MOVE      *ZEROS        XBELV2           13 2
     C                   MOVE      *ZEROS        XBELN1           13 2
     C                   MOVE      *ZEROS        XBELN2           13 2
      *
     C     UUSER         SETLL     GSS052F
     C     UUSER         READE(N)  GSS052F                                33
      *
      * FINN VALUTAKODE
     C  N33G52ONO        CHAIN     GSSORF                             33
     C  N33CUNDL01K      CHAIN     CUNDL01                            33
     C  N33              IF        VALKOD = *BLANKS
     C                   EVAL      G52VAL = FICURR
     C                   ELSE
     C                   EVAL      G52VAL = VALKOD
     C                   END
      * FINN VALUTAKODE
     C  N33              DO
     C     G52ONO        CHAIN     GSSORF                             33
     C     ORLC          CHAIN     STED2                              33
     C                   EVAL      G52LK  = ST2LK
     C                   EVAL      G52LOK = ORLC
     C                   END
      *
     C                   DOW       *IN33 = *OFF
     C     G52ONO        SETLL     GSSOVF
     C     G52ONO        READE     GSSOVF                                 33
     C                   DOW       *IN33 = *OFF
     C                   IF        OVST = ' '
      * VALUTA
     C     OVCURC        IFEQ      FICURR                                        2<-B
     C                   Z-ADD     OVAMT         XBELN            13 2
     C                   MOVE      'N'           XTILB             1
     C                   ELSE                                                    2
      * HENT TILLEGG BANKKURS
     C                   MOVE      'J'           XTILB
     C     KODDIK        CHAIN     KODTDINR                           34
     C  N34KDIPRO        DIV       100           XTILBA            5 4
     C  N34              ADD       1             XTILBA
     C     VALUL01K      CHAIN     VALUL01                            34
     C     VALKU1        IFEQ      0                                              3<-B
     C     OVAMT         MULT      VALKU1        XBL              15 5
     C                   ELSE                                                     3
     C     OVAMT         MULT      VALKU2        XBL
     C                   END                                                      3<-E
     C     XBL           DIV(H)    OMRFAK        XBELN
     C                   MULT(H)   XTILBA        XBELN
     C                   END                                                     2<-E
     C                   SELECT                                                  2<-B
     C     OVCURC        WHENEQ    G52VAL
      * VALUTAKODE PÅ LINJE = VALUTAKODE TIL FAKTURAMOTTAKER
     C                   Z-ADD     OVAMT         XBELV            13 2
     C     FICURR        WHENEQ    G52VAL
      * VALUTAKODE TIL FIRMA = VALUTAKODE TIL FAKTURAMOTTAKER
     C                   Z-ADD     XBELN         XBELV
     C                   OTHER
      * HENT TILLEGG BANKKURS
     C                   IF        XTILB = 'N'
     C     KODDIK        CHAIN     KODTDINR                           34
     C  N34KDIPRO        DIV       100           XTILBA            5 4
     C  N34              ADD       1             XTILBA
     C                   END
      * OMREGN BELØP TIL VALUTAKODE TIL FAKTURAMOTTAKER
     C     VALUL01K2     CHAIN     VALUL01                            34
     C     XBELN         MULT      OMRFAK        XBELV
     C     VALKU1        IFEQ      0                                              3<-B
     C                   DIV(H)    VALKU2        XBELV
     C                   ELSE                                                     3
     C                   DIV(H)    VALKU1        XBELV
     C                   END                                                      3<-E
     C                   IF        XTILB = 'N'
     C                   MULT(H)   XTILBA        XBELV
     C                   END
     C                   ENDSL                                                   2<-E
      *
     C                   IF        OVVAT = 'J'
     C                   EVAL      XBELV1 = XBELV1 + XBELV
     C                   EVAL      XBELN1 = XBELN1 + XBELN
     C                   ELSE
     C                   EVAL      XBELV2 = XBELV2 + XBELV
     C                   EVAL      XBELN2 = XBELN2 + XBELN
     C                   END
     C                   END
     C     G52ONO        READE     GSSOVF                                 33
     C                   ENDDO
     C     UUSER         READE(N)  GSS052F                                33
     C                   ENDDO
      *
     C                   IF        XBELN1 <> 0
     C                   EVAL      G52KDM = 'J'
     C                   EVAL      G52BLV = XBELV1
     C                   EVAL      G52BLN = XBELN2
     C                   WRITE     GSS0522R
     C                   END
     C                   IF        XBELN2 <> 0
     C                   EVAL      G52KDM = 'N'
     C                   EVAL      G52BLV = XBELV2
     C                   EVAL      G52BLN = XBELN2
     C                   WRITE     GSS0522R
     C                   END
     C                   IF        ((XBELN1 = 0) AND (XBELN2 = 0))
     C                   WRITE     GSS0522R
     C                   END
      *
     C                   ENDSR
      *
      ***********************************************
     C     INIT          BEGSR
      ***********************************************
     C     GSS052FK      KLIST
     C                   KFLD                    UUSER
     C                   KFLD                    ORONO
     C     KODDIK        KLIST
     C                   KFLD                    KDIUNI
     C                   MOVE      'DINNF'       KDIUNI
     C     CUNDL01K      KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    ORCNO
     C     VALUL01K      KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    OVCURC
     C     VALUL01K2     KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    G52VAL
      *
     C     *ENTRY        PLIST
     C                   PARM                    UTILPR            1
      *
     C                   MOVE      'N'           UTILPR
     C     *LOVAL        SETLL     FIRM
     C                   READ      FIRM                                   33
      *
     C                   ENDSR
      *
