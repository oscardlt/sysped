     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      REF(*LIBL/EDIM)
     A                                      CF03(03 'Avslutt program')
     A                                      CF05(05 'Forny')
     A                                      CF12(12 'Annuller')
     A                                      PRINT
     A                                      HLPPNLGRP(HLPSCR SVX006PNL)
     A                                      HLPTITLE('SAD IMPORT')
     A                                      HELP
     A                                      ALTHELP
      *
     A          R SVX006DA                  SFL
     A            MSN       R        H
     A            MMN       R        H
     A            MSR       R        H
     A            M0004     R        H
     A            M0010     R        H
     A            MVEN      R        H
     A            M0068     R        H
     A            ZOP            1A  I  9  3VALUES(' ' '5' '6' '7' '9' 'P')
     A            W0004         12A  O  9  6
     A  27                                  COLOR(PNK)
     A            W0010         12A  O  9 19
     A  27                                  COLOR(PNK)
     A            M0035     R        O  9 32
     A  27                                  COLOR(PNK)
     A            M0065     R        O  9 34
     A  27                                  COLOR(PNK)
     A            M1N07     R        O  9 41
     A  27                                  COLOR(PNK)
     A            W0068         14A  O  9 46
     A  27                                  COLOR(PNK)
     A            MST       R        O  9 61
     A  27                                  COLOR(PNK)
     A            MDT       R        O  9 63
     A  27                                  COLOR(PNK)
     A            MTM       R        O  9 72
     A  27                                  COLOR(PNK)
      *
     A          R SVX006DB                  SFLCTL(SVX006DA)
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0012)
     A                                      TEXT('EDI MESSAGE-LOG, Heading')
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A                                  1  2'NCTS '
     A                                      COLOR(BLU)
     A                                  1 34'Historik'
     A                                      COLOR(WHT)
     A                                  1 69'SVX006D A'
     A                                      COLOR(BLU)
     A                                  3  2'Avdelning:'
     A            WAVD      R        O  3 13REFFLD(MAVD)
     A                                      EDTCDE(4)
     A                                  3 19'Ærende:'
     A            WTDN      R        O  3 27REFFLD(MTDN)
     A                                      EDTCDE(4)
     A                                  5  2'Vælj och tryck Enter:'
     A                                      COLOR(BLU)
     A                                  6  2'5=Visa meddelande   6=Visa fel   7-
     A                                      =Visa komponent   9=Visa edifact   -
     A                                      P=Skriv'
     A                                      COLOR(BLU)
     A            ZRECNO         4S 0O  7 68SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  8  2'Alt'
     A                                      COLOR(WHT)
     A                                  8  6'Avsændare'
     A                                      COLOR(WHT)
     A                                  8 19'Mottagare'
     A                                      COLOR(WHT)
     A                                  8 32'T'
     A                                      COLOR(WHT)
     A                                  8 34'MSG ID'
     A                                      COLOR(WHT)
     A                                  8 41'Funk'
     A                                      COLOR(WHT)
     A                                  8 46'REF.'
     A                                      COLOR(WHT)
     A                                  8 61'S'
     A                                      COLOR(WHT)
     A                                  8 63'Datum'
     A                                      COLOR(WHT)
     A                                  8 72'Tid'
     A                                      COLOR(WHT)
      *
     A          R SVX006DC
     A                                      OVERLAY
     A                                 23  2'F3=Avsluta   F5=Uppdatera   F12=Av-
     A                                      bryt'
     A                                      COLOR(BLU)
      *
     A          R SVX006DD
     A                                      OVERLAY
     A                                      ROLLUP(25)
     A                                  1  2'NCTS'
     A                                      COLOR(BLU)
     A                                  1 34'Historik'
     A                                      COLOR(WHT)
     A                                  1 69'SVX006D D'
     A                                      COLOR(BLU)
     A                                  3  2'Tryck Enter før att fortsætta:'
     A                                      COLOR(BLU)
     A                                  5  2'Avdelning. . . . . . . . . . . . .-
     A                                       . :'
     A            MAVD      R        O  5 43EDTCDE(4)
     A                                  6  4'Ærende . . . . . . . . . . . . . .-
     A                                       :'
     A            MTDN      R        O  6 43EDTCDE(4)
     A                                  8  6'UNB ID Avsændare . . . . . . . . :'
     A            M0004     R        O  8 43
     A                                  9  6'UNB ID Mottagare . . . . . . . . :'
     A            M0010     R        O  9 43
     A                                 10  6'Testindikator  . . . . . . . . . :'
     A            M0035     R        O 10 43
     A                                 11  6'Meldings Referanse . . . . . . . :'
     A            M0062     R        O 11 43
     A                                 12  6'Meldings Identitet . . . . . . . :'
     A            M0065     R        O 12 43
     A                                 13  6'Deklarasjonens dato  . . . . . . :'
     A            M0068A    R        O 13 43EDTWRD('    .  .  ')
     A                                 14  8'Sekvensnummer  . . . . . . . . :'
     A            M0068B    R        O 14 43EDTCDE(4)
     A                                 15 10'Versjonsnummer . . . . . . . :'
     A            M0068C    R        O 15 43EDTCDE(4)
     A                                 16  6'Omberegning, Deklarasjonens dato :'
     A            M0068D    R        O 16 43EDTWRD('    .  .  ')
     A                                 17  8'Sekvensnummer  . . . . . . . . :'
     A            M0068E    R        O 17 43EDTCDE(4)
     A                                 18 10'Versjonsnummer . . . . . . . :'
     A            M0068F    R        O 18 43EDTCDE(4)
     A                                 19  6'Ønsket Behandlingsdato . . . . . :'
     A            M2005B    R        O 19 43
     A                                 20  6'Deklarantens Referanse . . . . . :'
     A            M3039D    R        O 20 43EDTCDE(4)
     A                                 21  6'Ekspidisjons Enhet . . . . . . . :'
     A            M3039E    R        O 21 43EDTCDE(4)
     A                                 21 72'Mer...'
     A                                      COLOR(WHT)
     A                                 23  2'F3=Gå ut   F12=Annuller'
     A                                      COLOR(BLU)
      *
     A          R SVX006DI                  OVERLAY
     A                                      ROLLDOWN(26)
     A                                  1  2'NCTS'
     A                                      COLOR(BLU)
     A                                  1 34'Meldingslogg'
     A                                      COLOR(WHT)
     A                                  1 69'SVX006D I'
     A                                      COLOR(BLU)
     A                                  3  2'Trykk Enter for å fortsette:'
     A                                      COLOR(BLU)
     A                                  5  2'Avdeling . . . . . . . . . . . . .-
     A                                       . :'
     A            MAVD      R        O  5 43EDTCDE(4)
     A                                  6  4'Oppdragsnummer . . . . . . . . . .-
     A                                       :'
     A            MTDN      R        O  6 43EDTCDE(4)
     A                                  8  7'Depositum Beløp . . . . . . . . :'
     A            M5004D    R        O  8 43EDTCDE(K)
     A                                  9  7'Meldingsfunksjon  . . . . . . . :'
     A            M1N07     R        O  9 43
     A                                 10  7'Meldingsfunksjon Korrigert  . . :'
     A            M1N08     R        O 10 43
     A                                 11  7'Ekspidisjons Prioritet  . . . . :'
     A            M9N01     R        O 11 43
     A                                 12  7'Sendningsnummer . . . . . . . . :'
     A            MSN       R        O 12 43EDTCDE(4)
     A                                 13  9'Meldingsnummer  . . . . . . . :'
     A            MMN       R        O 13 43EDTCDE(4)
     A                                 14  7'Send/Recive . . . . . . . . . . :'
     A            MSR       R        O 14 43
     A                                 15  7'Status  . . . . . . . . . . . . :'
     A            MST       R        O 15 43
     A                                 16  9'Status Dato . . . . . . . . . :'
     A            MDT       R        O 16 43EDTWRD('    .  .  ')
     A                                 17 11'Status Tid  . . . . . . . . :'
     A            MTM       R        O 17 43EDTWRD('  :  :  ')
     A                                 18  7'Ventekode . . . . . . . . . . . :'
     A            MVEN      R        O 18 43
     A                                 21 72'Slutt '
     A                                      COLOR(WHT)
     A                                 23  2'F3=Gå ut   F12=Annuller'
     A                                      COLOR(BLU)
      *
     A          R SVX006DE                  SFL
     A            F0077     R        O  8  2REFFLD(TVINFF/F0077 *LIBL/TVINF)
     A            F4815     R        O  8  6REFFLD(TVINFF/F4815 *LIBL/TVINF)
     A            F0078A    R        O  8 11REFFLD(TVINFF/F0078A *LIBL/TVINF)
     A            F0078B    R        O  9 11REFFLD(TVINFF/F0078B *LIBL/TVINF)
     A            F0078C    R        O 10 11REFFLD(TVINFF/F0078C *LIBL/TVINF)
     A            F0078D    R        O 11 11REFFLD(TVINFF/F0078D *LIBL/TVINF)
     A            F0078E    R        O 12 11REFFLD(TVINFF/F0078E *LIBL/TVINF)
      *
     A          R SVX006DF                  SFLCTL(SVX006DE)
     A                                      SFLSIZ(0004)
     A                                      SFLPAG(0003)
     A  81N83N84                            ROLLUP(25)
     A  81N83N84                            ROLLDOWN(26)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  84                                  SFLEND
     A                                  1  2'NCTS '
     A                                      COLOR(BLU)
     A                                  1 21'Historik - Felmeddelanden - Text'
     A                                      COLOR(WHT)
     A                                  1 69'SVX006D E'
     A                                      COLOR(BLU)
     A                                  3  2'Avdelning:'
     A            MAVD      R        O  3 13EDTCDE(4)
     A                                  3 23'Ærende:'
     A            MTDN      R        O  3 31EDTCDE(4)
     A                                  4  2'Datum...:'
     A            M0068A    R        O  4 12EDTWRD('    .  .  ')
     A                                  4 25'Sekvensnummer :'
     A            M0068B    R        O  4 41EDTCDE(4)
     A                                  4 50'Versjonsnummer:'
     A            M0068C    R        O  4 66EDTCDE(4)
     A                                  5  2'Trykk Enter for å fortsette:'
     A                                      COLOR(BLU)
     A            ÆRECNO         4S 0O  5 68SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  7  2'Ref'
     A                                      COLOR(WHT)
     A                                  7  6'Funk'
     A                                      COLOR(WHT)
     A                                  7 11'Text '
     A                                      COLOR(WHT)
      *
     A          R SVX006DØ
     A                                      OVERLAY
     A            ZMSGTX        79A  O 24  2MSGID(&ZMSGNO SYMSGF)
     A N30                                  COLOR(WHT)
     A  30                                  COLOR(RED)
     A            ZMSGNO         7A  P
