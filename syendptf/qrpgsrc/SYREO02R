********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
12XXX * 01 YBC PTF12 SADHMO
********************************************************************
     H DFTACTGRP(*NO)
     FEDIM      UF   E           K DISK
     FEDIS      UF   E           K DISK
     FEDISE     UF   E           K DISK
     FEDIRE     UF   E           K DISK
     FEDISE1K   UF   E           K DISK    RENAME(EDISER:EDISER1K)
     F                                     PREFIX(K_)
     FEDIRE1K   UF   E           K DISK    RENAME(EDIRER:EDIRER1K)
     F                                     PREFIX(K_)
     FEDISX     UF   E           K DISK
     FXMLRCVF   UF   E           K DISK
     FXMLRCAF   UF   E           K DISK
     FXMLRCLF   UF   E           K DISK
     FSADF      UF   E           K DISK
     FSADH      UF   E           K DISK
N01  FSADHMO    UF   E           K DISK
     FSADHMD    UF   E           K DISK
     FSADHIN    UF   E           K DISK
     FSADHAN    UF   E           K DISK
     FSADIUF    UF   E           K DISK
     FSADV      UF   E           K DISK
     FSADVCN    UF   E           K DISK
     FSADW      UF   E           K DISK
     FSADVJ     UF   E           K DISK
     FSADVEXT   UF   E           K DISK
     FSADVAVG   UF   E           K DISK
     FSADHOM    UF   E           K DISK    RENAME(SADHR:SADHOMR)
     FSADOMT    UF   E           K DISK
     FSADOMTF   UF   E           K DISK
     FSADOMTH   UF   E           K DISK
     FSADOMTV   UF   E           K DISK
     FSADOMTVCN UF   E           K DISK
     FSADOMTVJ  UF   E           K DISK
     FSADOMTW   UF   E           K DISK
     FSADHKO    UF   E           K DISK
     FSADVKO    UF   E           K DISK
     FSADVCNKO  UF   E           K DISK
     FSADVJKO   UF   E           K DISK
     FSADFKO    UF   E           K DISK
     FSADWKO    UF   E           K DISK
     FFPSTD     UF   E           K DISK
     FFPXREF    UF   E           K DISK
     FGODSJF    UF   E           K DISK
     FTVINF     UF   E           K DISK
     FFRISOK    UF   E           K DISK
     FTRACKF    UF   E           K DISK
      *
     FARKTXT    IF   E           K DISK
     FARKEXT    IF   E           K DISK
     FFIRMARC   IF   E           K DISK
     FFIRM      IF   E           K DISK
     FARKIVL17  UF   E           K DISK

     d                 ds
     d  usn_n                  1      9  0
     d  usn                    1      9
     d                 ds
     d  smbr                   1     10A
     d  smbr_1                 1      1A
     d  smbr_2                 2      8A
     d  smbr_3                 9     10A
     d  smbr_4                 4     10A
     d                 ds
     d  uavd                   1      4A
     d  xavd                   1      4  0
     d                 ds
     d  utdn                   1      7A
     d  xtdn                   1      7  0

     d WhereC          s            512a
     d FirstW          s              1A
     d cQuote          C                   const('''')
     d SqlStm          s            512a
     d myrows0         s             10i 0
     d myrows          s             10i 0
     d myrows2         s             10i 0
     D XDTF            S              8  0
     D XDTP            S              8  0
     D XPOS            S              3  0
     d XAPS            C                   const(X'7D')
     D XMBR            S             10A
     D Error           S            150
     d X               S             10  0
     d X2              S             10  0
     D ufil            S            256A
     D utyp            S              5A
     D uok             S              1A
     D XSLUTT          S              1
     D XSLUTT2         S              1
     D XSLT            S              1
      *
      *  Prototype for 'SYREO01C3'
     d SYREO01C3       pr                  extpgm('SYREO01C3')
     d ufil_                               like(ufil)
      *  Prototype for 'SYREO01R3'
     d SYREO01R3       pr                  extpgm('SYREO01R3')
     d uavd_                               like(uavd)
     d utdn_                               like(utdn)
     d utyp_                               like(utyp)
     d uok_                                like(uok)
      *  Prototype for 'SYREO02R2'
     d syreo02r2       pr                  extpgm('SYREO02R2')
     d usn_                                like(usn)
     d udtp_                               like(udtp)
     d uok_                                like(uok)
      *  Prototype for syreo02r
     d syreo02r        pr
     d udtf_                               like(udtf)
     d udtp_                               like(udtp)
      *  *ENTRY Interface for Main Procedure
     d syreo02r        pi
     d udtf                           8
     d udtp                           8
      *
     d MyPoDS0         ds                  Qualified  dim(1)
     d  FIFIRM                        2A
     d MyPoDS          ds                  Qualified  dim(10000)
     d  SIAVD                         4S 0
     d  SITDN                         7S 0
     d  SISG                          3A
     d  SIDT                          8S 0
     d  SIRG                         11A
     d  SIGN                         15A
     d MyPoDS2         ds                  Qualified  dim(1000)
     d  MAVD                          4S 0
     d  MTDN                          7S 0
     d  MSR                           1A
     d  MMN                           9S 0
     d  MSN                           9S 0
      /free

         exsr init;

         dow xslutt <> 'J';
           exsr srhoved;
         enddo;

       *inlr = *on;
       return;

       //=====================================================================
       // Hoved logikk
       //=====================================================================
       begsr srhoved;

       clear MyPoDS;
       SqlStm = 'Select SIAVD, SITDN, SISG, SIDT, SIRG, SIGN ' +
                'from SADH ' +
                'Where SITDN > 0 AND SIDT <= ' + %trim(udtf);
       Exec SQL PREPARE DynSel from :SqlStm;
       EXEC SQL DECLARE  A33  cursor for DynSel;
       Exec SQL OPEN A33;
       Myrows = %elem(MyPoDS);
       exec sql FETCH A33   for :MyRows rows
                  INTO :MyPoDs;
       Exec SQL CLOSE A33;

       if myrows > 0;
         for x = 1 to myrows;
           siavd = mypods(x).siavd;
           sisg  = mypods(x).sisg;
           if sisg = *blanks and siavd = 0;
             xslutt = 'J';
             leave;
           endif;

           if %subst(mypods(x).sirg:10:2) = *blanks or                          //Firma 5 år
              %subst(mypods(x).sirg:10:2) <> *blanks and mypods(x).sidt < xdtp; //Personkunde 10 år

             sitdn = mypods(x).sitdn;
             sign  = mypods(x).sign;

             exsr sra;
             exsr srb;
             xslutt2 = 'N';
             dow xslutt2 <> 'J';
               exsr src;
             enddo;
             exsr sre;
             exsr srf;
             exsr srg;

N01          exec sql
N01          delete from SADHMO
N01          where siavd = :siavd and sitdn = :sitdn
N01          with NC;
             exec sql
             delete from SADHMD
             where siavd = :siavd and sitdn = :sitdn
             with NC;
             exec sql
             delete from SADHIN
             where insiavd = :siavd and insitdn = :sitdn
             with NC;
             exec sql
             delete from SADHAN
             where siavd = :siavd and sitdn = :sitdn
             with NC;
             exec sql
             delete from SADIUF
             where heavd = :siavd and heavd = :sitdn
             with NC;

             if sitdn > 0;
            // OMBEREGNING
               sitdn = sitdn * -1;
               chain (siavd: sitdn) SADH;
               if %found;

                 exsr sra;
                 exsr srb;
                 xslutt2 = 'N';
                 dow xslutt2 <> 'J';
                   exsr src;
                 enddo;
                 exsr srd;
                 exsr sre;
                 exsr srf;

N01              exec sql
N01              delete from SADHMO
N01              where siavd = :siavd and sitdn = :sitdn
N01              with NC;
                 exec sql
                 delete from SADHMD
                 where siavd = :siavd and sitdn = :sitdn
                 with NC;
                 exec sql
                 delete from SADHIN
                 where insiavd = :siavd and insitdn = :sitdn
                 with NC;
                 exec sql
                 delete from SADHAN
                 where siavd = :siavd and sitdn = :sitdn
                 with NC;
                 exec sql
                 delete from SADIUF
                 where heavd = :siavd and heavd = :sitdn
                 with NC;

                 delete sadhr;
               endif;  // if %found;

               sitdn = sitdn * -1;
             endif;   // if sitdn > 0;

           // slett hovedpost
             exec sql
             delete from SADHKO
             where siavd = :siavd and sitdn = :sitdn
             with NC;
             exec sql
             delete from SADH
             where siavd = :siavd and sitdn = :sitdn
             with NC;
           endif;  // End of "if %subst(mypods(x).sirg:10:2) = *blanks or"

         endfor;
       endif;

       endsr;
      //*_________________________________________
      //* SETLL, READ AND DELETE RECORDS FROM SADV
      //*"""""""""""""""""""""""""""""""""""""""""
       begsr sra;

       exec sql
       delete from sadv
       where svavd=:siavd and svtdn=:sitdn
       with NC;

       exec sql
       delete from sadvcn
       where svavd=:siavd and svtdn=:sitdn
       with NC;

       exec sql
       delete from sadvj
       where sjavd=:siavd and sjtdn=:sitdn
       with NC;

       exec sql
       delete from sadvext
       where svavd=:siavd and svtdn=:sitdn
       with NC;

       exec sql
       delete from sadvavg
       where wfavd=:siavd and wftdn=:sitdn
       with NC;

       exec sql
       delete from sadw
       where swavd=:siavd and swtdn=:sitdn
       with NC;

       if sitdn > 0;
         exec sql
         delete from sadvko
         where svavd=:siavd and svtdn=:sitdn
         with NC;

         exec sql
         delete from sadvcnko
         where svavd=:siavd and svtdn=:sitdn
         with NC;

         exec sql
         delete from sadvjko
         where svavd=:siavd and svtdn=:sitdn
         with NC;

         exec sql
         delete from sadwko
         where svavd=:siavd and svtdn=:sitdn
         with NC;
       endif;

       endsr;

      //*_________________________________________
      //* SETLL, READ AND DELETE RECORDS FROM SADF
      //*"""""""""""""""""""""""""""""""""""""""""
       begsr srb;

       exec sql
       delete from sadf
       where sfavd=:siavd and sfopdn=:sitdn
       with NC;

       if sitdn > 0;
         exec sql
         delete from sadfko
         where sfavd=:siavd and sfopdn=:sitdn
         with NC;
       endif;

       endsr;

      //*_____________________________________________________
      //* SETLL, READ AND DELETE RECORDS FROM EDIM/EDIS/TVINF
      //*"""""""""""""""""""""""""""""""""""""""""""""""""""""
       begsr src;

       clear MyPoDS2;
       SqlStm = 'Select MAVD, MTDN, MSR, MMN, MSN ' +
                'from EDIM ' +
                'Where MAVD = ' + %trim(%editc(siavd:'Z')) +
                ' and MTDN = ' + %trim(%editc(sitdn:'Z')) +
                ' and M1001 <> ' + xaps + '830' + xaps +
                ' and M1001 <> ' + xaps + 'TEI' + xaps +
                ' and M1001 <> ' + xaps + 'TET' + xaps +
                ' and M1001 <> ' + xaps + 'SVI' + xaps +
                ' and M1001 <> ' + xaps + 'SVE' + xaps +
                ' and M1001 <> ' + xaps + 'STI' + xaps +
                ' and M1001 <> ' + xaps + 'STE' + xaps +
                ' and M1001 <> ' + xaps + 'DKI' + xaps +
                ' and M1001 <> ' + xaps + 'DKE' + xaps;
       Exec SQL PREPARE DynSel2 from :SqlStm;
       EXEC SQL DECLARE  A34  cursor for DynSel2;
       Exec SQL OPEN A34;
       Myrows2 = %elem(MyPoDS2);
       exec sql FETCH A34 for :MyRows2 rows
                  INTO :MyPoDs2;
       Exec SQL CLOSE A34;

       if myrows2 > 0;
         for x2 = 1 to myrows2;
           if mypods2(x2).msr = *blanks;
             xslutt2 = 'J';
             leave;
           endif;

           mmn = mypods2(x2).mmn;
           msn = mypods2(x2).msn;

           exec sql
           delete from TVINF
           where fmn = :mmn
           with NC;
           exec sql
           delete from XMLRCVF
           where xrsn = :msn
           with NC;
           exec sql
           delete from XMLRCAF
           where xrsn = :msn
           with NC;
           exec sql
           delete from XMLRCLF
           where xrsn = :msn
           with NC;

           usn_n = msn;
           syreo02r2 (usn: udtp: uok); //Test om samme fil inneholder oppdrag til privatkunde
           if uok ='J';
             chain (msn) edis;
             if %found;
              // slett komponent
               exsr src2;
              // slett fil i IFS
               if sifs <> *blanks;
                 if %subst(sifs:1:1) <> '/';
                   sifs = '/' + %trim(sifs);
                 endif;
                 ufil = sifs;
                 syreo01c3 (ufil);
               endif;
               delete edisr;
             endif;
           endif;

         // slett hovedpost
           exec sql
           delete from EDIM
           where mmn = :mmn
           with NC;

         endfor;
       endif;

       endsr;

      //*_____________________________________________________
      //* SETLL, READ AND DELETE RECORDS FROM EDIRE/EDISE/EDIRE1K/EDISE1K/XMLRCVF/AF/LF/EDISX
      //*"""""""""""""""""""""""""""""""""""""""""""""""""""""
       begsr src2;

       exec sql
       delete from edire
       where rembr = :smbr
       with NC;

       exec sql
       delete from edise
       where sembr = :smbr
       with NC;

       exec sql
       delete from edire1k
       where rembr = :smbr
       with NC;

       exec sql
       delete from edise1k
       where sembr = :smbr
       with NC;

       chain smbr edisx;
       if %found;
         if sxifsobj <> *blanks and %subst(sxifsobj:1:1) <> '/';
           sxifsobj = '/' + %trim(sxifsobj);
         endif;
         syreo01c3 (sxifsobj);
         delete edisxr;
       else;
         if smbr_3 = ' ';
         // MBR = R1234567 -> PRØV R001234567
           xmbr = smbr_1 + '00' + smbr_2;
         else;
         // MBR = R001234567 -> PRØV R1234567
           xmbr = smbr_1 + smbr_4 + '  ';
         endif;
         chain xmbr edisx;
         if %found;
           if sxifsobj <> *blanks and %subst(sxifsobj:1:1) <> '/';
             sxifsobj = '/' + %trim(sxifsobj);
           endif;
           syreo01c3 (sxifsobj);
           delete edisxr;
         endif;
       endif;

       endsr;

      //*___________________________________________
      //* SETLL, READ AND DELETE RECORDS FROM OMBEREGNING
      //*"""""""""""""""""""""""""""""""""""""""""""
       begsr srd;

       exec sql
       delete from sadhom
       where siavd = :siavd and sitdn = :sitdn
       with NC;

       exec sql
       delete from sadomt
       where siavd = :siavd and sitdn = :sitdn
       with NC;

       exec sql
       delete from sadomtf
       where sfavd = :siavd and sfopdn = :sitdn
       with NC;

       exec sql
       delete from sadomth
       where siavd = :siavd and sitdn = :sitdn
       with NC;

       exec sql
       delete from sadomtv
       where svavd = :siavd and svtdn = :sitdn
       with NC;

       exec sql
       delete from sadomtvcn
       where svavd = :siavd and svtdn = :sitdn
       with NC;

       exec sql
       delete from sadomtvj
       where sjavd = :siavd and sjtdn = :sitdn
       with NC;

       exec sql
       delete from sadomtw
       where swavd = :siavd and swtdn = :sitdn
       with NC;

       endsr;

      //*_________________________________________
      //* SETLL, READ AND DELETE RECORDS FROM GODSJF
      //*"""""""""""""""""""""""""""""""""""""""""
       begsr sre;

       if %subst(sign:14:2) <> *blanks;
         exec sql
         delete from godsjf
         where gogn = :sign
         with NC;
       endif;

       endsr;

      //*_________________________________________
      //* SETLL, READ AND DELETE RECORDS FROM FRISOK/TRACKF
      //*"""""""""""""""""""""""""""""""""""""""""
       begsr srf;

       exec sql
       delete from frisok
       where fsavd = :siavd and fsopd = :sitdn
       with NC;

       exec sql
       delete from trackf
       where ttavd = :siavd and ttopd = :sitdn
       with NC;

       // Sjekk om oppdrag ligger i andre modul. Ved N sletter KUN SAD-import.
       xavd = siavd;
       xtdn = sitdn;
       syreo01r3 (uavd: utdn: utyp: uok);
       setll (siavd:sitdn) arkivl17;
       reade (siavd:sitdn) arkivl17;
       dow not %eof;
         if artype = 'si' or artype = 'sk' or artype = 'sj' or uok = 'J';
           chain (fifirm) firmarc;
           chain (artype) arktxt;
           if %found and arklag <> *blanks;
             chain (fifirm:arklag) arkext;
           endif;
           if arbhis <> *blanks;
           // Bane for historikk
             arcane = arbhis;
           endif;
           xpos = %scan ('.' : arlink : 1);
           if xpos = 0;
             ufil = '/' + %TRIM(ARCANE) + '/' + %TRIM(ARLINK) + '.pdf';
           else;
             ufil = '/' + %TRIM(ARCANE) + '/' + %TRIM(ARLINK);
           endif;
           syreo01c3 (ufil);

           delete arkivr;
         else;
           update arkivr;
         endif;
         reade (siavd:sitdn) arkivl17;
       enddo;

       endsr;

      //*_________________________________________
      //* SETLL, READ AND DELETE RECORDS FROM FPSTD/FPXREF
      //*"""""""""""""""""""""""""""""""""""""""""
       begsr srg;

       exec sql
       delete from fpstd
       where fpstda = :siavd and fpstdo = :sitdn
       with NC;

       exec sql
       delete from fpxref
       where fpavd = :siavd and fpopd = :sitdn
       with NC;

       endsr;

      //***********************************************
       begsr init;
      //***********************************************
       xdtf = %float(udtf);
       xdtp = %float(udtp);
       utyp = 'SAD-I';

       SqlStm = 'Select * ' +
                'from FIRM ' +
                'FOR READ ONLY ';
       Exec SQL PREPARE DynSel0 from :SqlStm;
       EXEC SQL DECLARE  A30  cursor for DynSel0;
       Exec SQL OPEN A30;
       Myrows0 = %elem(MyPoDS0);
       exec sql FETCH A30   for :MyRows0 rows
                  INTO :MyPoDs0;
       Exec SQL CLOSE A30;
       FIFIRM = mypods0(1).fifirm;

       endsr;

      /end-free
