      *  PDF/AFP (*AFPDS) BARCODE DIREKTE I DDS
      *  PRINT HUMAN READABLE DOBBELT OPP VED AFP - ENKELTE PRINTERE TAKLER IKKE
      * HTTP://PUBLIB.BOULDER.IBM.COM/ISERIES/V5R2/IC2924/BOOKS/C415713531.HTM
     A          R LABEL
     A                                  1  5'Fra/from'
     A                                      FNTCHRSET(C0D0GC15 T1D0BASE)
     A                                  1 20'Print date'
     A                                      FNTCHRSET(C0D0GC15 T1D0BASE)
     A            WDATO          8  0   1 27EDTWRD('  /  /    ')
     A                                      FNTCHRSET(C0D0GC15 T1D0BASE)
     A            WTID4          4  0   1 35EDTWRD('  :  ')
     A                                      FNTCHRSET(C0D0GC15 T1D0BASE)
     A            EHLNAV        30A     2  9FNTCHRSET(C0S0OB10 T1D0BASE)
     A            EHLAD1        30A     3  9FNTCHRSET(C0S0OB10 T1D0BASE)
     A            EHLAD2        30A     4  9FNTCHRSET(C0S0OB10 T1D0BASE)
     A            EHLAD3        30A     5  9FNTCHRSET(C0S0OB10 T1D0BASE)
     A            EHLAD4        30A     6  9FNTCHRSET(C0S0OB10 T1D0BASE)
      *
     A                                  7  5'__________________________________'
     A                                      FNTCHRSET(C0S0OB10 T1D0BASE)
      *
     A                                  8  5'Til/to'
     A                                      FNTCHRSET(C0D0GC15 T1D0BASE)
     A            VADRNA        30A    10  9FNTCHRSET(C0S0OB10 T1D0BASE)
     A            VADRN1        30A    11  9FNTCHRSET(C0S0OB10 T1D0BASE)
     A            VADRN2        30A    12  9FNTCHRSET(C0S0OB10 T1D0BASE)
     A            VADRN3        30A    13  9FNTCHRSET(C0S0OB10 T1D0BASE)
      *
     A                                 14  5'__________________________________'
     A                                      FNTCHRSET(C0S0OB10 T1D0BASE)
      *
     A                                 15  5'PO-nummer:'
     A                                      FNTCHRSET(C0D0GC15 T1D0BASE)
     A            EHBES         10A    16  7FNTCHRSET(C0S0OB10 T1D0BASE)
     A                                 17  5'Supl.ref:'
     A                                      FNTCHRSET(C0D0GC15 T1D0BASE)
     A            EHLREF        30A    18  7FNTCHRSET(C0D0GB10 T1D0BASE)
     A                                 19  5'C.name:'
     A                                      FNTCHRSET(C0D0GC15 T1D0BASE)
     A            ELTRNV        30A    20  7FNTCHRSET(C0D0GB10 T1D0BASE)
     A                                 22  5'Art.No.:'
     A                                      FNTCHRSET(C0D0GC15 T1D0BASE)
     A            ELART         35A    23  7FNTCHRSET(C0S0OB10 T1D0BASE)
     A                                 24  5'Supl.A.No.:'
     A                                      FNTCHRSET(C0D0GC15 T1D0BASE)
     A            ELARTL        35A    25  7FNTCHRSET(C0S0OB10 T1D0BASE)
     A                                 26  5'Description:'
     A                                      FNTCHRSET(C0D0GC15 T1D0BASE)
     A            ETBES1_01     32A    27  7FNTCHRSET(C0D0GB10 T1D0BASE)
     A            ETBES1_02     32A    28  7FNTCHRSET(C0D0GB10 T1D0BASE)
     A            ETBES1_03     32A    29  7FNTCHRSET(C0D0GB10 T1D0BASE)
     A            ETBES1_04     32A    30  7FNTCHRSET(C0D0GB10 T1D0BASE)
     A            ETBES1_05     32A    31  7FNTCHRSET(C0D0GB10 T1D0BASE)
     A            ETBES1_06     32A    32  7FNTCHRSET(C0D0GB10 T1D0BASE)
     A            ETBES1_07     32A    33  7FNTCHRSET(C0D0GB10 T1D0BASE)
     A            ETBES1_08     32A    34  7FNTCHRSET(C0D0GB10 T1D0BASE)
     A                                 36  5'Reference:'
     A                                      FNTCHRSET(C0D0GC15 T1D0BASE)
     A            ETREF1        35A    37  7FNTCHRSET(C0D0GB10 T1D0BASE)
     A            ETREF2        35A    38  7FNTCHRSET(C0D0GB10 T1D0BASE)
     A                                 40  5'Brt.weight:'
     A                                      FNTCHRSET(C0D0GC15 T1D0BASE)
     A            XVEKT         11  3  40 10EDTCDE(3)
     A                                      FNTCHRSET(C0D0GB10 T1D0BASE)
     A                                 40 23'Net weight:'
     A                                      FNTCHRSET(C0D0GC15 T1D0BASE)
     A            XVEKTN        11  3  40 28EDTCDE(3)
     A                                      FNTCHRSET(C0D0GB10 T1D0BASE)
     A                                 42  5'Volumn:'
     A                                      FNTCHRSET(C0D0GC15 T1D0BASE)
     A            XM3           11  3  42 10EDTCDE(3)
     A                                      FNTCHRSET(C0D0GB10 T1D0BASE)
     A                                 44  5'Stykk:'
     A                                      FNTCHRSET(C0D0GC15 T1D0BASE)
     A            KLLTXT        11A    44 14FNTCHRSET(C0S0OB10 T1D0BASE)
     A                                 45  5'__________________________________'
     A                                      FNTCHRSET(C0S0OB10 T1D0BASE)
      *
     A*           VADRNA2       30A    47  5FNTCHRSET(C0C075H0 T1D0BASE)
     A*           VADRNA2       30A    47  5FNTCHRSET(C0A077H0 T1D0BASE)
     A            VADRNA2       30A    47  5FNTCHRSET(C0H400H0 T1D0BASE)
     A            VADRN32       30A    50  5FNTCHRSET(C0H400H0 T1D0BASE)
      *
     A                                 52  5'Kolli-ID:'
     A                                      FNTCHRSET(C0D0GC15 T1D0BASE)
     A            KOID21        21     53 10BARCODE(CODE128 5 *NOHRI +
     A                                      (*WIDTH 0.018) (*RATIO 3) X'02')
     A            HRI_KOID      25A    59 12
      *
     A  28                             61  5'EAN:'
     A                                      FNTCHRSET(C0D0GC15 T1D0BASE)
     A  28        KOID14        14     62 10BARCODE(CODE128 5 *NOHRI +
     A                                      (*WIDTH 0.018) (*RATIO 3) X'02')
     A  28        ELARTE        14A    68 12
      *
      *
