********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
P6051 * 01 JOV PTF5 FILTER PÅ POSTNR
********************************************************************
      *
      ************************************************************
      *** PROGRAM SKAL VEDLIKEHOLDE LEVERANDØRS ADRESSER       ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER     ***
      *** LEDIGE INDIKATORER 01 02 07 09-11 13-24              ***
      *** LEDIGE INDIKATORER 28 29 32 34-39 41-87 95-97        ***
      ************************************************************
      *
     FFIRM      IF   E           K DISK
     FCUNDL01   IF   E           K DISK
     FLLVADR    UF A E           K DISK
     FLLVADR1   IF   E           K DISK
     F                                     RENAME(LLVADRR:LLVADRR1)
     FLL009D    CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D                 DS
     D  VADRNA                 1     30
     D  XDLT                   1      7
     D  VAADNX                 1     25
     D                 DS
     D  VADRN1                 1     30
     D  VAAD1X                 1     25
     D                 DS
     D  VADRN3                 1     30
     D  VAAD3X                 1     18
N01  D  VAPNR1                 1      1
N01  D  VAPNR2                 2      2
N01  D  VAPNR3                 3      3
N01  D  VAPNR4                 4      4
N01  D                 DS
N01  D  YPNR                   1      4
N01  D  W2PNR1                 1      1
N01  D  W2PNR2                 2      2
N01  D  W2PNR3                 3      3
N01  D  W2PNR4                 4      4
      *
     C                   EXSR      INIT
      *
     C     WSKUNR        IFNE      *ZEROS
     C                   GOTO      STARTX
     C                   END
      *
     C     START0        TAG
     C                   EXFMT     BILDE01
     C                   SETOFF                                       9930
     C   03              GOTO      SLUTT
     C   04              DO
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C     LINNBR        IFEQ      11
     C     POSNBR        ANDGE     17
     C     POSNBR        ANDLE     24
     C                   EXSR      SØKUNR
     C     KUND1K        CHAIN     CUNDL01                            99
     C  N99              GOTO      START
     C                   MOVE      *ZEROS        WSKUNR
     C                   SETOFF                                       99
     C                   END
     C                   GOTO      START0
     C                   END
     C     STARTX        TAG
     C     KUND1K        CHAIN     CUNDL01                            99
     C   99              MOVE      'SPK0001'     MSGNR
     C   99              SETON                                        30
     C   99              GOTO      START0
     C                   MOVE      '0'           *IN27
      *
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
     C                   MOVE      W2ADNR        YADNR             5 0
     C                   MOVE      W2ADNA        YADNA            30
N01  C                   MOVE      W2PNR         YPNR              4
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     *IN27         IFEQ      '0'
     C     VADRK1        SETLL     LLVADR
     C                   ELSE
     C     VAD1K1        SETLL     LLVADR1
     C                   END
     C     START1        TAG
     C     *IN27         IFEQ      '0'
     C     FIFIRM        READE(N)  LLVADR                                 94
     C                   ELSE
     C     FIFIRM        READE     LLVADR1                                94
     C                   END
     C  N94WSKUNR        COMP      KUNDNR                             9494
     C   94              MOVE      *BLANKS       VADRNR
     C   94              MOVE      *BLANKS       WSNR
      *
     C     START2        TAG
      *
     C  N94              DO
     C                   DO        7
N01  C     YPNR          IFNE      *BLANKS
N01  C                   EXSR      TSTPNR
N01  C     PNROK         CABEQ     'N'           NEXT
N01  C                   END
     C                   ADD       1             ZRECNO
     C                   WRITE     SUBFIL
N01  C     NEXT          TAG
     C     *IN27         IFEQ      '0'
     C     FIFIRM        READE(N)  LLVADR                                 94
     C                   ELSE
     C     FIFIRM        READE     LLVADR1                                94
     C                   END
N01  C  N94YPNR          IFNE      *BLANKS
N01  C                   EXSR      TSTPNR
N01  C     PNROK         CABEQ     'N'           NEXT
N01  C                   END
     C  N94WSKUNR        COMP      KUNDNR                             9494
     C  N94              MOVE      VADRNR        YADNR
     C  N94              END
     C                   END
      *
     C                   Z-ADD     ZRECNO        YRECNO            5 0
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYVARE
     C                   ELSE
     C                   WRITE     SUBCTL
     C                   EXFMT     BUNN
     C                   END
     C                   SETOFF                                       9830
      *
      *** AVSLUTT
     C   03              GOTO      SLUTT
      *** FRISK OPP
     C   05              GOTO      START
      *** LAGE NY VARE
     C   06              GOTO      START5
      *** SØK FRA VARE
     C   08              GOTO      START6
      *** TIL BILDE SOM HENTER KUNDENR
     C   12              GOTO      START0
      *
      *** BLA VIDERE
     C   25              DO
     C                   Z-ADD     YRECNO        ZRECNO
     C                   SETOFF                                       25
     C     *IN27         IFEQ      '0'
     C  N94VADRK3        SETLL     LLVADR
     C  N94FIFIRM        READE(N)  LLVADR                                 94
     C                   ELSE
     C  N94VAD1K3        SETLL     LLVADR1
     C  N94FIFIRM        READE     LLVADR1                                94
     C                   END
     C  N94WSKUNR        COMP      KUNDNR                             9494
     C                   GOTO      START2
     C                   END
      *
     C     ZRECNO        IFEQ      0
     C                   GOTO      START3
     C                   ELSE
     C                   EXSR      HENT
     C   98              GOTO      START3
     C                   END
      *
      *** SLETT VARE
     C     WSNR          IFEQ      '4'
     C                   MOVE      ' '           WSNR
     C                   EXFMT     VAREL3
     C  N12              DO
     C     VADRK2        CHAIN     LLVADR                             33
     C  N33              DELETE    LLVADRR
     C                   EXSR      MOVB1
     C                   MOVE      '*DELETE'     XDLT
     C                   END
     C                   UPDATE    SUBFIL
     C                   GOTO      START3
     C                   END
      *
      *** LAGE/ENDRE VARE
     C     START5        TAG
     C                   EXFMT     VARELI
     C                   SETOFF                                       908988
     C                   SETOFF                                       30
     C   12              DO
     C   40              UPDATE    SUBFIL
     C                   SETOFF                                       40
     C                   EXSR      MOVB2
     C                   GOTO      START3
     C                   END
     C   092             DO        99998         XN                5 0
     C     VASØKK        CHAIN(N)  LLVADR                             33
     C     *IN33         IFEQ      '1'
     C                   MOVE      XN            WSADNR
     C                   LEAVE
     C                   END
     C                   ENDDO
     C                   EXSR      TESTB1
     C   30              GOTO      START5
     C                   GOTO      START3
      *
      *** SØK FRA
     C     START6        TAG
     C                   EXFMT     VAREL2
     C   03              GOTO      SLUTT
     C     W2ADNA        COMP      *BLANKS                            2727
     C                   GOTO      START
      *
      *
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
      *
N01  C***********************************************
N01  C     TSTPNR        BEGSR
N01  C***********************************************
N01  C* TEST KUN NÅR W2PNR/YPNR ULIK BALNK
N01  C                   MOVE      'N'           PNROK             1
N01  C     W2PNR1        IFNE      ' '
N01  C     W2PNR1        ANDNE     VAPNR1
N01  C                   GOTO      UTPNR
N01  C                   END
N01  C     W2PNR2        IFNE      ' '
N01  C     W2PNR2        ANDNE     VAPNR2
N01  C                   GOTO      UTPNR
N01  C                   END
N01  C     W2PNR3        IFNE      ' '
N01  C     W2PNR3        ANDNE     VAPNR3
N01  C                   GOTO      UTPNR
N01  C                   END
N01  C     W2PNR4        IFNE      ' '
N01  C     W2PNR4        ANDNE     VAPNR4
N01  C                   GOTO      UTPNR
N01  C                   END
N01   * ALLE TESTER BESTÅTT..
N01  C                   MOVE      'J'           PNROK
N01  C*
N01  C     UTPNR         ENDSR
     C*
     C***********************************************
     C     TESTB1        BEGSR
     C***********************************************
     C  N40              DO
     C     WSADNR        COMP      0                                    9090
     C  N90VADRK         CHAIN(N)  LLVADR                             33
     C  N90
     CANN33              SETON                                        90
     C   90              MOVE      'SPA0018'     MSGNR
     C   90              SETON                                        30
     C                   END
      *
     C     WSADNA        COMP      *BLANKS                                89
     C   89
     CANN30              MOVE      'SPN0014'     MSGNR
     C   89              SETON                                        30
      *
     C     WSAD3         COMP      *BLANKS                                88
     C   88
     CANN30              MOVE      'SPA0019'     MSGNR
     C   88              SETON                                        30
     C   30              GOTO      SLTB1
      *
     C     VADRK         CHAIN     LLVADR                             33
     C  N33
     CANN40              GOTO      SLTB1
     C                   EXSR      MOVWVA
     C  N33              UPDATE    LLVADRR
     C   33              WRITE     LLVADRR
     C   40              UPDATE    SUBFIL
     C  N40              ADD       1             YRECNO
     C  N40              Z-ADD     YRECNO        ZRECNO
     C  N40              WRITE     SUBFIL
     C                   WRITE     SUBCTL
     C                   SETOFF                                       40
     C                   EXSR      MOVB2
      *
     C     SLTB1         ENDSR
      *
     C***********************************************
     C     HENT          BEGSR
     C***********************************************
     C                   MOVEA     '00'          *IN(91)
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C  N98WSNR          IFEQ      '2'
     C     XDLT          COMP      '*DELETE'                              98
     C   98              GOTO      SLHENT
     C                   EXSR      MOVVAW
     C                   MOVE      ' '           WSNR
     C                   SETOFF                                       908988
     C                   SETON                                        40
     C                   GOTO      SLHENT
     C                   END
     C  N98WSNR          IFEQ      '4'
     C     XDLT          COMP      '*DELETE'                              98
     C                   GOTO      SLHENT
     C                   END
     C                   END
      *
     C     SLHENT        TAG
     C   98              DO
     C                   MOVE      'DIV0012'     MSGNR
     C                   SETON                                            30
     C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     MOVWVA        BEGSR
     C***********************************************
     C                   MOVE      FIFIRM        FIRMA
     C                   MOVE      WSKUNR        KUNDNR
     C                   MOVE      WSADNR        VADRNR
     C                   MOVE      WSADNA        VADRNA
     C                   MOVE      WSAD1         VADRN1
     C                   MOVE      WSLAND        VALAND
     C                   MOVE      WSAD2         VADRN2
     C                   MOVE      WSAD3         VADRN3
     C                   MOVE      WSKURE        VAKURE
     C                   MOVE      WSTLF         VATLF
     C                   MOVE      WSFAX         VAFAX
     C                   MOVE      WSMAIL        VAMAIL
      *
     C                   ENDSR
      *
     C***********************************************
     C     MOVVAW        BEGSR
     C***********************************************
     C                   MOVE      VADRNR        WSADNR
     C                   MOVE      VADRNA        WSADNA
     C                   MOVE      VADRN1        WSAD1
     C                   MOVE      VALAND        WSLAND
     C                   MOVE      VADRN2        WSAD2
     C                   MOVE      VADRN3        WSAD3
     C                   MOVE      VAKURE        WSKURE
     C                   MOVE      VATLF         WSTLF
     C                   MOVE      VAFAX         WSFAX
     C                   MOVE      VAMAIL        WSMAIL
      *
     C                   ENDSR
      *
     C***********************************************
     C     MOVB1         BEGSR
     C***********************************************
     C                   MOVE      *ZEROS        VADRNR
     C                   MOVE      *BLANKS       VADRNA
     C                   MOVE      *BLANKS       VADRN1
     C                   MOVE      *BLANKS       VALAND
     C                   MOVE      *BLANKS       VADRN2
     C                   MOVE      *BLANKS       VADRN3
     C                   MOVE      *BLANKS       VAKURE
     C                   MOVE      *BLANKS       VATLF
     C                   MOVE      *BLANKS       VAFAX
     C                   MOVE      *BLANKS       VAMAIL
      *
     C                   ENDSR
      *
     C***********************************************
     C     MOVB2         BEGSR
     C***********************************************
     C                   MOVE      *ZEROS        WSADNR
     C                   MOVE      *BLANKS       WSADNA
     C                   MOVE      *BLANKS       WSAD1
     C                   MOVE      *BLANKS       WSLAND
     C                   MOVE      *BLANKS       WSAD2
     C                   MOVE      *BLANKS       WSAD3
     C                   MOVE      *BLANKS       WSKURE
     C                   MOVE      *BLANKS       WSTLF
     C                   MOVE      *BLANKS       WSFAX
     C                   MOVE      *BLANKS       WSMAIL
      *
     C                   ENDSR
      *
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
      *
     C     *ENTRY        PLIST
     C                   PARM                    WSKUNR
      *
     C     VASØKK        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    WSKUNR
     C                   KFLD                    XN
     C     VADRK         KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    WSKUNR
     C                   KFLD                    WSADNR
     C     KUDRK         KLIST
     C                   KFLD                    WSKUNR
     C                   KFLD                    WSKUNR
     C                   KFLD                    WSADNR
     C     VADRK1        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    WSKUNR
     C                   KFLD                    W2ADNR
     C     VADRK2        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    WSKUNR
     C                   KFLD                    VADRNR
     C     KUDRK2        KLIST
     C                   KFLD                    WSKUNR
     C                   KFLD                    WSKUNR
     C                   KFLD                    VADRNR
     C     VADRK3        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    WSKUNR
     C                   KFLD                    YADNR
     C     VAD1K1        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    WSKUNR
     C                   KFLD                    W2ADNA
     C     VAD1K3        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    WSKUNR
     C                   KFLD                    YADNA
     C     KUND1K        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    WSKUNR
      *
      *
     C     FIFIRM        SETLL     FIRM
     C                   READ      FIRM                                   33
      *
     C                   ENDSR
      *
     C***********************************************
     C     SØKUNR        BEGSR
     C***********************************************
     C                   MOVE      *BLANKS       USØKKN           30
     C                   CALL      'SOEKKUN'
     C                   PARM                    WSKUNR
     C                   PARM                    FIFIRM
     C                   PARM                    USØKKN           30
      *
     C                   ENDSR
      *
