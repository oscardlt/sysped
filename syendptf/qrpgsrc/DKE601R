********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
12100 * 01 CB  PTF12 MRN-nr
12100 * 02 SH  PTF12 UTVIDET SYGE111 TIL 180 FELT
12XXX * 03 YBC PTF12 Nummer i nummerteller er neste nummer
12284 * 04 CB  PTF12 Legg automatisk ut Z380 og fakturanummer
********************************************************************
     FFIRM      IF   E           K DISK
     FEDIRE1K   IF   E           K DISK
     FZTELL     UF   E           K DISK
     FDKEFZ     O  A E             DISK
     FDKEVZ     O  A E             DISK
     D                 DS
N02  D  DTAFLD                 1  54000
N02  D                                     DIM(180)
S02  D* DTAFLD                 1  18000
S02  D*                                    DIM(60)
     D                 DS
     D  RDATA                  1   1024
     D  RTYP                   1      1
     D                 DS
     D  ZDEC                   1      7  6
     D  ZDEC6                  2      7
     D  ZHEL                  11     22  0
     D                 DS
     D  A                      1     79
     D                                     DIM(79)
     D                 DS
     D  B                      1     18
     D                                     DIM(18)
      */////////////
      * MAIN LOGIC /
      */////////////
     C                   EXSR      INIT

     C     UMBR          SETLL     EDIRE1K
     C     UMBR          READE     EDIRE1K                                91
     C     *IN91         DOWEQ     *OFF

     C                   SELECT
     C     RTYP          WHENEQ    'S'
     C                   EXSR      SRHEAD
     C     RTYP          WHENEQ    'I'
     C                   EXSR      SRFAK
     C     RTYP          WHENEQ    'L'
     C                   EXSR      SRVAR
     C                   OTHER
     C                   ENDSL

     C     UMBR          READE     EDIRE1K                                91
     C                   ENDDO


     C     LR            TAG
     C                   EVAL      *INLR=*ON
      *____________________________________________
      * Init                                   INIT
      *""""""""""""""""""""""""""""""""""""""""""""
     C     INIT          BEGSR
     C     *LIKE         DEFINE    DKEV_SYLI     WVLI

     C     *ENTRY        PLIST
     C                   PARM                    UMBR             10
      * Hent unikt nummer for Z-filer
     c                   READ      FIRM                                   33
     C     FIFIRM        CHAIN     ZTELL
     C                   ADD       1             ZUNIK
     C                   UPDATE    ZTELLR
N03  C                   SUB       1             ZUNIK
     C     ENDINI        ENDSR
      *____________________________________________
      * HEADING                              SRHEAD
      *""""""""""""""""""""""""""""""""""""""""""""
     C     SRHEAD        BEGSR
      * Kaller eksternt program og legger felt i array
S02  C*                  MOVEL     RDATA         UDATA          1024
S02  C*                  MOVEL     *BLANKS       UARRA         18000
N02  C                   MOVEL     RDATA         UDATA          4096
N02  C                   MOVEL     *BLANKS       UARRA         54000
     C                   MOVE      ';'           SKILLET           1
     C                   CALL      'SYGE111'
     C                   PARM                    SKILLET
     C                   PARM                    UDATA
     C                   PARM                    UARRA
     c                   MOVEA     UARRA         DTAFLD

      * 2. Avsender
     C                   MOVE      *BLANKS       WAVS             35
     C                   MOVE      *BLANKS       WAVS1             1
     C                   EVAL      WAVS = %TRIM(DTAFLD(2))
     C                   MOVEL     WAVS          WAVS1
     C                   ENDSR
      *____________________________________________
      * Fakturalinje                          SRFAK
      *""""""""""""""""""""""""""""""""""""""""""""
     C     SRFAK         BEGSR
      * Kaller eksternt program og legger felt i array
S02  C*                  MOVEL     RDATA         UDATA          1024
S02  C*                  MOVEL     *BLANKS       UARRA         18000
N02  C                   MOVEL     RDATA         UDATA          4096
N02  C                   MOVEL     *BLANKS       UARRA         54000
     C                   MOVE      ';'           SKILLET           1
     C                   CALL      'SYGE111'
     C                   PARM                    SKILLET
     C                   PARM                    UDATA
     C                   PARM                    UARRA
     c                   MOVEA     UARRA         DTAFLD

     C                   CLEAR                   DKEFR
      * 2. Fakturanummer
     C                   EVAL      DKEF_FATX = %TRIM(DTAFLD(2))
      * 3. Valutakode
     C                   EVAL      DKEF_VAKD = %TRIM(DTAFLD(3))
      * 4. valutabeløp
     C                   MOVEA     DTAFLD(4)     A
     C                   EXSR      GETNUM
     C                   Z-ADD     ZNUM          DKEF_FABL
      * 5. MRN
     C                   EVAL      DKEF_MRN = %TRIM(DTAFLD(5))

     C                   EVAL      DKEF_UNIK=ZUNIK
     C                   EVAL      DKEF_REFF=DKEF_FATX

     C                   WRITE     DKEFR
     C                   ENDSR
      *____________________________________________
      * Vareposter                            SRVAR
      *""""""""""""""""""""""""""""""""""""""""""""
     C     SRVAR         BEGSR
      * Kaller eksternt program og legger felt i array
     C                   MOVEL     RDATA         UDATA
     C                   MOVEL     *BLANKS       UARRA
     C                   MOVE      ';'           SKILLET
     C                   CALL      'SYGE111'
     C                   PARM                    SKILLET
     C                   PARM                    UDATA
     C                   PARM                    UARRA
     c                   MOVEA     UARRA         DTAFLD

     C                   EVAL      DKEV_UNIK=ZUNIK
     C                   EVAL      DKEV_REFF=DKEF_FATX

      * 2. Artikkelnummer
     C                   EVAL      DKEV_X02 = %TRIM(DTAFLD(2))
      * 3. Kollimærke
     C                   EVAL      DKEV_311 = %TRIM(DTAFLD(3))
      * 4. Containernummer
     C                   EVAL      DKEV_312A = %TRIM(DTAFLD(4))
      * 5. Kolliantal
     C                   MOVEA     DTAFLD(5)     A
     C                   EXSR      GETNUM
     C                   Z-ADD     ZNUM          DKEV_313
      * 6. Kolliart
     C                   EVAL      DKEV_314 = %TRIM(DTAFLD(6))
      * 7. Varebeskrivelse
     C                   EVAL      DKEV_315 = %TRIM(DTAFLD(7))
      * 8. Varekode
     C                   EVAL      DKEV_331 = %TRIM(DTAFLD(8))
      * 9. Oprindelsesland
     C                   EVAL      DKEV_34A = %TRIM(DTAFLD(9))
      * 10. Bruttovægt
     C                   MOVEA     DTAFLD(10)    A
     C                   EXSR      GETNUM
     C                   Z-ADD     ZNUM          DKEV_35
      * 12. Prosedyre
     C                   EVAL      DKEV_37 = %TRIM(DTAFLD(12))
      * 13. Nettovægt
     C                   MOVEA     DTAFLD(13)    A
     C                   EXSR      GETNUM
     C                   Z-ADD     ZNUM          DKEV_38
      * 14. Varens pris
     C                   MOVEA     DTAFLD(14)    A
     C                   EXSR      GETNUM
     C                   Z-ADD     ZNUM          DKEV_42
      * 15. Suppl.verdi
     C                   MOVEA     DTAFLD(15)    A
     C                   EXSR      GETNUM
     C                   Z-ADD     ZNUM          DKEV_412

     C                   ADD       1             WVLI
     C                   EVAL      DKEV_SYLI=WVLI
     C                   EVAL      DKEV_32=WVLI
N04   * Legg ut Z380 + faktura
N04  C                   EVAL      DKEV_401A='Z'
N04  C                   EVAL      DKEV_402A='380'
N04  C                   MOVEL     DKEF_FATX     DKEV_403A
      * Skriv post til Arbetsfil
     C                   WRITE     DKEVR
     C                   ENDSR
      *____________________________________________
      * Konverter alpha til num              GETNUM
      *""""""""""""""""""""""""""""""""""""""""""""
     C     GETNUM        BEGSR

     C                   MOVE      *ZEROS        ZHEL
     C                   MOVE      *ZEROS        ZDEC
     C                   MOVE      *ZEROS        B
     C                   Z-ADD     1             AX                2 0
     C                   Z-ADD     1             BX                2 0
      * Get Integer
     C     A(AX)         DOWGE     '0'
     C     ZHEL          MULT      10            ZHEL
     C                   MOVE      A(AX)         ZHEL
     C                   ADD       1             AX
     C                   ENDDO
      * Decimal point ?
     C     A(AX)         IFEQ      '.'
     C     A(AX)         OREQ      ','
     C                   ADD       1             AX
     C     A(AX)         DOWGE     '0'
     C                   MOVE      A(AX)         B(BX)
     C                   ADD       1             AX
     C                   ADD       1             BX
     C                   ENDDO
     C                   MOVEA     B             ZDEC6
     C                   ENDIF
      * Concatenate
     C                   Z-ADD     ZHEL          ZNUM             18 6
     C                   ADD       ZDEC          ZNUM
     C                   ENDSR
