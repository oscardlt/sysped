********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFNR:*SEK SIG VERS  BESKRIVELSE...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
11XXX * 01 YBC PTF11 PRINT SERIENR
********************************************************************
     A          R LL027P1
     A            KFMT1         75      1  1
     A  77                              1 90'¤&l'
     A  77        COPY           2  0   1 93
     A  77                              1 95'X'
     A  77                              1 96'¤&a-3R'
     A            KFMT2         75      2  1
     A  77                              2 76'¤&l0O'
     A  77                              2 81'¤(12U¤(s0p15h0s3b4099T'
     A                                  3  2MSGCON(40 YBC1386 SYMSGF)
     A  46N48                           3  2MSGCON(40 YBC1387 SYMSGF)
     A N46 47                           3  2MSGCON(40 YBC1388 SYMSGF)
     A                                  3 85MSGCON(05 YBC1389 SYMSGF)
     A            XIDAG          8  0   3 91EDTWRD('  .  .    ')
     A                                  3103MSGCON(04 YBC1390 SYMSGF)
     A            XTID           4  0   3108EDTWRD('  :  ')
     A                                  4 85MSGCON(08 YBC1391 SYMSGF)
     A            MRMRNR         5  0   4 94EDTCDE(Z)
     A                                  4103MSGCON(05 YBC1392 SYMSGF)
     A                                  4110PAGNBR
     A                                  5  2MSGCON(11 YBC1393 SYMSGF)
     A            LHPNR          8  0   5 14EDTCDE(Z)
     A            KNAVN         30A     5 23
     A N47                              5 85MSGCON(08 YBC1760 SYMSGF)
     A N47        HEAVD          4  0   5 94
     A N47                              5 98'/'
     A N47        HEOPD          7  0   5 99
     A N47                              5106'/'
     A N47        HESG           3A     5107
     A                                  6  2MSGCON(11 YBC1394 SYMSGF)
     A            MRLNA         30A     6 14
     A            MRLAD1        30A     6 45
     A            MRLAD2        30A     7 14
     A            MRLPN          4  0   7 45
     A            MRLAD3        24A     7 51
     A                                  8  2MSGCON(11 YBC1395 SYMSGF)
     A            MRLREF        25A     8 14
     A                                  8 51MSGCON(11 YBC1396 SYMSGF)
     A            MRKREF        25A     8 63
     A                                  8 90MSGCON(11 YBC1397 SYMSGF)
     A            XMRDT          8  0   8102EDTWRD('  .  .    ')
     A                                  9  2MSGCON(11 YBC1398 SYMSGF)
     A            MRFT1         30A     9 14
     A            MRFT2         30A     9 45
     A                                  9 90MSGCON(11 YBC1399 SYMSGF)
     A            MRTM           4  0   9102EDTWRD('  :  ')
     A                                 10  2'Godsnr:'
     A            MRGN          15A    10 10
     A                                 10 30'Mottaker:'
     A            MRMOTT        15A    10 40
     A                                 10 60'MRN-nr:'
     A            MRTRNR        18A    10 68
     A                                 11  2'-------------------------'
     A                                 11 27'-------------------------'
     A                                 11 52'-------------------------'
     A                                 11 77'-------------------------'
     A                                 11102'------------------'
     A                                 12  2MSGCON(15 YBC1400 SYMSGF)
     A                                 12 18MSGCON(34 YBC1401 SYMSGF)
     A                                 12 54MSGCON(07 YBC1402 SYMSGF)
     A                                 12 62MSGCON(05 YBC1403 SYMSGF)
     A                                 12 68MSGCON(05 YBC1404 SYMSGF)
     A                                 12 74MSGCON(05 YBC1405 SYMSGF)
     A                                 12 80MSGCON(07 YBC1406 SYMSGF)
     A                                 12 88MSGCON(08 YBC1407 SYMSGF)
     A                                 12 97MSGCON(08 YBC1408 SYMSGF)
     A                                 12106MSGCON(14 YBC1409 SYMSGF)
     A                                 13  2'-------------------------'
     A                                 13 27'-------------------------'
     A                                 13 52'-------------------------'
     A                                 13 77'-------------------------'
     A                                 13102'------------------'
     A                                 14  2' '
      *
     A          R LL027P2                   SPACEB(1)
     A N41        VAANR         15A        2
     A N41        LVVB          35A       18
     A  41 42                             18MSGCON(08 YBC1410 SYMSGF)
     A  41 42     VASENR        25A       27
     A            VAOANT         7  0     54EDTCDE(Z)
     A            VASAE3         5  0     62EDTCDE(Z)
     A            VASAE2         5  0     68EDTCDE(Z)
     A            VASAE1         5  0     74EDTCDE(Z)
     A            VASAE0         7  0     80EDTCDE(Z)
     A            VAVKTS         7  2     88EDTCDE(3)
     A            VAKUBS         7  4     97EDTCDE(3)
     A*           VALLOK        25A       88
     A            XXLLOK        14A      106
     A                                      SPACEA(1)
      *
     A          R LL027P3                   SPACEB(2)
     A                                     2'-------------------------'
     A                                    27'-------------------------'
     A                                    52'-------------------------'
     A                                    77'-------------------------'
     A                                   102'------------------'
     A                                      SPACEA(1)
     A                                     2MSGCON(10 YBC1411 SYMSGF)
     A            TLWNT0         7  0     54EDTCDE(Z)
     A            TLWNT3         5  0     62EDTCDE(Z)
     A            TLWNT2         5  0     68EDTCDE(Z)
     A            TLWNT1         5  0     74EDTCDE(Z)
     A            TLWNTL         7  0     80EDTCDE(Z)
     A            TLVKTS         7  2     88EDTCDE(3)
     A            TLKUBS         7  4     97EDTCDE(3)
     A                                      SPACEA(2)
      *
     A          R LL027P4
     A N41 42                             18MSGCON(08 YBC1410 SYMSGF)
     A N41 42     VASENR        25A       27
     A  43                                55MSGCON(11 YBC1412 SYMSGF)
     A  43        VAULDT         8  0     67EDTWRD('    .  .  ')
     A                                    78' '
     A                                      SPACEA(1)
N01   *
N01  A          R LL027P5
N01  A            SNSN          35A       18
N01  A            SNST           1A       55
N01  A                                      SPACEA(1)
