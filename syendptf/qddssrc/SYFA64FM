     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      REF(*LIBL/KODTLK)
     A                                      PRINT
     A                                      HLPDOC('STD' 'HELPNU' 'HLP400')
     A                                      HELP
     A                                      ALTHELP(CA01)
      * "NOERASE" MÅ HA FOR Å IKKE TØMME BILDE SOM MAN KOMME FRA
     A          R NOERASE
     A                                      ASSUME
     A                                  1  2' '
      * MAN KUN DEFINNERE VINDU PÅ REKORDNIVÅ
     A          R WINDOWSZ
     A                                      WINDOW(*DFT 16 70 *NOMSGLIN)
     A                                      OVERLAY
     A          R SUBFIL                    SFL
     A            OTIKNR         8S 0H
     A            FSAVD          4S 0H
     A            FSOPD          7S 0H
     A            FSSOK         35A  H
     A            KFSTTU         5A  H
     A            WSNR           1A  I  6  2VALUES(' ' '2' '4' '9' 'W')
     A            FSKODE    R        O  6  5REFFLD(FRISOKR/FSKODE FRISOK)
     A            KFSOTY        14A  O  6  9
     A            WSSOK     R        B  6 24REFFLD(FRISOKR/FSSOK FRISOK)
     A  80                                  DSPATR(RI)
     A  81                                  DSPATR(HI)
     A            WSDOKK    R        B  6 60REFFLD(FRISOKR/FSDOKK FRISOK)
     A            KRAV           1   O  6 57
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A                                      CA03(03 'Avslutt')
     A                                      CF04(04 'SØKING')
     A                                      CA05(05 'Frisk')
     A                                      CA12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYFA64'
     A                                      COLOR(BLU)
     A                                  1 13MSGCON(050 JOV0865 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            ZRECNO         4S 0O  1 65SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  2  2MSGCON(010 YBC0223 *LIBL/SYMSGF)
     A            UAVD           4Y 0O  2 13EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  2 18'/'
     A                                      COLOR(WHT)
     A            UOPD           7Y 0O  2 20EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  2 33MSGCON(035 JOV0863 *LIBL/SYMSGF)
     A                                  3  2MSGCON(040 JOV0866 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 44MSGCON(025 JOV0864 *LIBL/SYMSGF)
     A                                  4  2MSGCON(068 JOV0862 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            W2NR           1A  B  5  2VALUES(' ' '1' '9')
     A                                      DSPATR(UL)
     A            W2KODE         3A  B  5  5DSPATR(UL)
     A  90                                  DSPATR(HI)
     A            W2SOK         35A  B  5 24DSPATR(UL)
     A  89                                  DSPATR(HI)
     A            W2DOKK        10A  B  5 60DSPATR(UL)
     A          R BUNN
     A                                      WINDOW(WINDOWSZ)
     A                                      SLNO(16)
     A                                      CLRL(01)
     A                                      OVERLAY
     A N30                              1  2MSGCON(29 YBC0237 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        68A  O  1  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A          R NYWRK
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A                                      CA03(03 'Forrige')
     A                                      CF04(04 'SØKING')
     A                                      CA05(05 'Frisk')
     A                                      CA12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1  2'SYFA64'
     A                                      COLOR(BLU)
     A                                  1 13MSGCON(050 JOV0865 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  2  2MSGCON(010 YBC0223 *LIBL/SYMSGF)
     A            UAVD           4Y 0O  2 13EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  2 18'/'
     A                                      COLOR(WHT)
     A            UOPD           7Y 0O  2 20EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  2 33MSGCON(035 JOV0863 *LIBL/SYMSGF)
     A                                  3  2MSGCON(035 JOV0866 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 43MSGCON(025 JOV0864 *LIBL/SYMSGF)
     A                                  4  2MSGCON(068 JOV0862 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            W2NR           1A  B  5  2VALUES(' ' '1' '9')
     A                                      DSPATR(UL)
     A            W2KODE         3A  B  5  5DSPATR(UL)
     A  90                                  DSPATR(HI)
     A            W2SOK         35A  B  5 24DSPATR(UL)
     A  89                                  DSPATR(HI)
     A            W2DOKK        10A  B  5 60DSPATR(UL)
     A N30                             16  2MSGCON(029 YBC0237 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        68A  O 16  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A          R WEBSOK
     A                                      WINDOW(*DFT  2 75 *NOMSGLIN)
     A                                      OVERLAY
     A                                      CA03(03 'Avslutt program')
     A                                      CA12(03 'Avslutt program')
     A                                  1  1'Tast på hyperkobling for å søke på-
     A                                      WEB.'
     A                                  1 61'F3=avslutt'
     A                                      COLOR(BLU)
     A            LINK1         70A  O  2  1COLOR(BLU)
     A                                      DSPATR(UL)
