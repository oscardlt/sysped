     A*%%TS  SD  20240923  125425  CB          REL-V7R4M0  5770-WDS
*****A**************************************************************
     A* RETTINGER I DETTE PROGRAM:
PTFnrA*Sek Sig Vers  Beskrivelse...........................
"""""A*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
12237A* 01 YBC PTF12 UTVIDE KURS FRA 6,3 TIL 7,3
12318A* 02 CB  PTF12 FAKTURANUMMER ØKES FRA 17 TIL 21 TEGN
12318A* 03 CB  PTF12 VIS/IKKE VIS 4 SISTE TEGN AV FAKTURANUMMER
*****A**************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3                  -
     A                                             27 132 *DS4)
     A                                      PRINT
     A                                      HELP
     A                                      ALTHELP(CA01)
      * "NOERASE" MÅ HA FOR Å IKKE TØMME BILDE SOM MAN KOMME FRA
     A          R NOERASE
     A                                      ASSUME
     A                                  1  2' '
      * MAN KUN DEFINNERE VINDU PÅ REKORDNIVÅ
     A          R WINDOWSZ
S02  A*                                     WINDOW(5 5 15 62 *NOMSGLIN)
N02  A                                      WINDOW(5 5 15 67 *NOMSGLIN)
     A                                      OVERLAY
      *
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  6  2VALUES(' ' '2' '4')
     A            SFTXT         17A  O  6  5
N02  A            SFAFTXT21      4A  O  6 23
     A            SFDT           8Y 0H
S02  A*           WSDT           6Y 0O  6 23EDTCDE(Y)
S02  A*           SFBL28        12Y 2O  6 32EDTCDE(L)
S02  A*           SFVK28         3A  O  6 47
S01  A*           SFKR28         6Y 3O  6 51EDTCDE(4)
S02  A*           SFKR28         7Y 3O  6 51EDTCDE(4)
N02  A            WSDT           6Y 0O  6 28EDTCDE(Y)
N02  A            SFBL28        12Y 2O  6 37EDTCDE(L)
N02  A            SFVK28         3A  O  6 52
N02  A            SFKR28         7Y 3O  6 56EDTCDE(4)
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A                                      SFLSIZ(0007)
     A                                      SFLPAG(0006)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A* 28                                  CA02(02 'Plukk fra EDI')
     A                                      CA02(02 'Plukk fra EDI')
     A                                      CA05(05 'Frisk')
     A                                      CA06(06 'LAGE NY')
     A  27                                  CA07(07 'HENT PÅ NYTT')
     A                                      CA12(12 'Forrige')
     A            ZRECNO         4S 0O  2 56SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'RUBR28'
     A                                      COLOR(BLU)
     A                                  1 10MSGCON(40 DIV1831 SYMSGF)
     A                                      COLOR(WHT)
     A                                  3  2'Velg opsjon;   1=Lage ny  2=Endre -
     A                                       4=Slett'
     A                                      COLOR(BLU)
S02  A*                                 4  2MSGCON(56 DIV1830 SYMSGF)
N02  A                                  4  2MSGCON(61 DIV1830 SYMSGF)
     A                                      COLOR(WHT)
     A            W2NR           1A  I  5  2VALUES(' ' '1')
     A            W2TXT         17A  I  5  5
S02  A*           W2DT           6D 0I  5 23DSPATR(UL)
N02  A            W2DT           6D 0I  5 28DSPATR(UL)
     A          R BUNN
     A                                      WINDOW(WINDOWSZ)
     A                                 14  2'F5=Frisk F6=Lage F12=Forrige'
     A                                      COLOR(BLU)
     A  27                             14 31'F7=Ny henting'
     A                                      COLOR(BLU)
     A  27                             14 45'F2=Plukk fra EDI'
     A                                      COLOR(BLU)
     A  30        MSGTXT        58A  O 15  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A*
     A          R VARELI
     A*%%TS  SD  20240923  125425  CB          REL-V7R4M0  5770-WDS
     A  *DS3                                WINDOW(WINDOWSZ)
     A  *DS4                                WINDOW(WINDOWSZ)
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CA02(02 'Plukk fra EDI')
     A                                      CF04(04 'SØK')
     A                                      CA08(08 '21 LANG FNR')
     A                                      CA12(12 'Forrige')
     A                                      SLNO(12)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  5MSGCON(058 DIV1832 *LIBL/SYMSGF)
     A            W28TXT        17A  B  2  5TEXT('TXT AV FINANS. OPPL.')
     A                                      DSPATR(UL)
     A  40
     AO 90                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            W28TXT21       4A  B  2 23
N03  A  32                                  DSPATR(UL)
N03  A N32                                  DSPATR(ND)
N03  A N32                                  DSPATR(PR)
N03  A  32                                  COLOR(YLW)
     A            W28DT          6D 0B  2 28TEXT('DATO AV RUB. 28')
     A                                      DSPATR(UL)
     A  40
     AO 89                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSBL28        12Y 2B  2 37TEXT('BELØP FOR RUBR.28')
     A                                      DSPATR(UL)
     A  87                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(M)
     A            WSVK28         3A  B  2 52TEXT('VALUTAKODE RUBR.28')
     A                                      DSPATR(UL)
     A  88                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSKR28         7Y 3B  2 56TEXT('KURS PÅ RUBR.28')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            MSGNR          7A  P
N03  A                                  3  2'F2=Plukk EDI'
N03  A                                      COLOR(BLU)
N03  A                                  3 17'F4=Kalk.'
N03  A                                      COLOR(BLU)
N03  A                                  3 28'F8=av/på 21 tegn faktura'
N03  A                                      COLOR(BLU)
N03  A                                  3 55'F12=Forrige'
N03  A                                      COLOR(BLU)
     A  30        MSGTXT        58A  O  4  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
      *
     A          R VAREL3
     A                                      WINDOW(WINDOWSZ)
     A                                      CA12(12 'Forrige')
     A                                      SLNO(12)
     A                                      CLRL(04)
     A                                      OVERLAY
S02  A*                                 1  5MSGCON(53 DIV1832 SYMSGF)
N02  A                                  1  5MSGCON(58 DIV1832 SYMSGF)
     A            SFTXT         17A  O  2  5TEXT('TXT AV FINANS. OPPL.')
     A                                      DSPATR(UL)
     A                                      DSPATR(PC)
     A                                      COLOR(WHT)
N02  A            SFAFTXT21      4A  B  2 23
N02  A                                      DSPATR(UL)
S02  A*           WSDT           6Y 0O  2 23TEXT('DATO AV RUB. 28')
N02  A            WSDT           6Y 0O  2 28TEXT('DATO AV RUB. 28')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(Y)
S02  A*           SFBL28        12Y 2O  2 32TEXT('BELØP FOR RUBR.28')
N02  A            SFBL28        12Y 2O  2 37TEXT('BELØP FOR RUBR.28')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
S02  A*           SFVK28         3A  O  2 47TEXT('VALUTAKODE RUBR.28')
N02  A            SFVK28         3A  O  2 52TEXT('VALUTAKODE RUBR.28')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
S01  A*           SFKR28         6Y 3   2 51TEXT('KURS PÅ RUBR.28')
S02  A*           SFKR28         7Y 3   2 51TEXT('KURS PÅ RUBR.28')
N02  A            SFKR28         7Y 3   2 56TEXT('KURS PÅ RUBR.28')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  3  2MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
      *
     A          R NYWRK
     A                                      WINDOW(WINDOWSZ)
     A* 28                                  CA02(02 'Plukk fra EDI')
     A                                      CA02(02 'Plukk fra EDI')
     A                                      CA05(05 'Frisk')
     A                                      CA06(06 'LAGE NY')
     A  27                                  CA07(07 'HENT PÅ NYTT')
     A                                      CA12(12 'Forrige')
     A                                  1  2'RUBR28'
     A                                      COLOR(BLU)
     A                                  1 10MSGCON(40 DIV1831 SYMSGF)
     A                                      COLOR(WHT)
     A                                  3  2'Velg opsjon;   1=Lage ny  2=Endre -
     A                                       4=Slett'
     A                                      COLOR(BLU)
S02  A*                                 4  2MSGCON(56 DIV1830 SYMSGF)
N02  A                                  4  2MSGCON(61 DIV1830 SYMSGF)
     A                                      COLOR(WHT)
     A            W2NR           1A  I  5  2VALUES(' ' '1')
     A            W2TXT         17A  I  5  5
S02  A*           W2DT           6D 0I  5 23DSPATR(UL)
N02  A            W2DT           6D 0I  5 28DSPATR(UL)
     A                                 14  2'F5=Frisk F6=Lage F12=Forrige'
     A                                      COLOR(BLU)
     A  27                             14 31'F7=Ny henting'
     A                                      COLOR(BLU)
     A* 28                             14 45'F2=Plukk fra EDI'
     A                                 14 45'F2=Plukk fra EDI'
     A                                      COLOR(BLU)
     A  30        MSGTXT        58A  O 15  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R BEKREFT
     A                                      WINDOW(*DFT 6 40 *NOMSGLIN)
     A                                      CA12(12 'Avbryt')
     A                                      OVERLAY
     A                                  1  2'Hent varelinjer fra Z-filer på nyt-
     A                                      t'
     A                                      COLOR(WHT)
     A                                  3  2'ENTER = Slette alle eksistrerende '
     A                                      COLOR(BLU)
     A                                  4  2'        linjer og hente linjer fra'
     A                                      COLOR(BLU)
     A                                  5  2'        Z-filer'
     A                                      COLOR(BLU)
     A                                  6  2'F12   = Avbryt'
     A                                      COLOR(BLU)
