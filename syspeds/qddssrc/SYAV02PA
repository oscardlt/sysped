     A*****************************************************************
     A* TRANSPORTØRAVREGNING                                          *
     A* ANTALL LINJE=72,  ANTALL TEGN PR. LINJE=96,  OVERFLOW=70      *
     A*****************************************************************
     A          R TURH1
     A
     A* HEADING
     A            HOHT1         80      1  1
     A            HOHT2         80      2  1
     A            HOHT3         80      3  1
     A            HOHT4         80      4  1
     A            HOHT5         80      5  1
     A            HOHT6         80      6  1
     A            HOHT7         80      7  1
     A  08                              7 75'** PRØVEUTSKRIFT **'
     A            LNAVN     R          11 11REFFLD(KNAVN CUNDF)
     A            ADR1      R          12 11REFFLD(ADR1 CUNDF)
     A            ADR2      R          13 11REFFLD(ADR2 CUNDF)
     A            POSTNR    R          14 11REFFLD(POSTNR CUNDF)
     A            ADR3      R          14 17REFFLD(ADR3 CUNDF)
     A N08                             17  2'AVREGNING NR:'
     A N08        HTAVRN         6S 0  17 22EDTCDE(Z)
     A                                 17 65'Side     :'
     A            SIDEA          2S 0  17 77EDTCDE(Z)
     A                                 18  2'Kundenr.    :'
     A            VMTRKU         8S 0  18 20EDTCDE(Z)
     A                                 18 65'Dato/sign:'
     A            UDAY           2S 0  18 77
     A                                 18 79'.'
     A            UMONTH         2S 0  18 80
     A                                 18 82'.'
     A            UYEAR          2S 0  18 83
     A                                 18 85'/'
     A            BRUKER        10A    18 86
     A                                 19  2'Lev. nr.    :'
     A            VMTRLE         8S 0  19 20EDTCDE(Z)
     A N08                             19 65'Forf.dato:'
     A N08        FORFAL         6S 0  19 77EDTWRD('  .  .  ')
     A                                 20  2'Transportør :'
     A            TUKNT          8S 0  20 20EDTCDE(Z)
     A                                 21  2'Dato        :'
     A            XDAY           2S 0  21 20
     A                                 21 22'.'
     A            XMONTH         2S 0  21 23
     A                                 21 25'.'
     A            XYEAR          2S 0  21 26
     A*DETALJ HEADINGER
     A          R TURH2                     SPACEB(2)
     A                                     2'Pos.'
     A                                    10'Bilnr'
     A                                    21'Dato'
     A                                    32'Turnr'
     A                                    65'Beløp'
     A          R TURDET                    SPACEB(1)
     A            ATPOS          4S 0      2EDTCDE(Z)
     A*N79        ATBILN        10A       10
     A            ATBILN        10A       10
     A            ATDATO         6S 0     21EDTCDE(Y)
     A            ATPRO          8S 0     32EDTCDE(Z)
     A            ATBELN         7S 2     64EDTCDE(M)
     A* SUM
     A          R BILSUM                    SPACEB(1)
     A                                    63'_________'
     A                                    40'Sum bil'
     A                                      SPACEB(2)
     A            ABBELN        10S 2     61EDTCDE(M)
     A                                    63'_________'
     A* SUM
     A          R TURSUM                    SPACEB(2)
     A                                    40'Sum beløp'
     A            ANBELN        10S 2     61EDTCDE(M)
     A                                    63'_________'
     A* 71                                40'24% MVA'
     A  71        TAX            4S 2     36EDTWRD('  ,  ')
     A  71                                42'% MVA'
     A  71                                  SPACEB(2)
     A  71        ANMVA         10S 2     61EDTCDE(M)
     A  71                                63'_________'
     A                                    40'KREDITERT DERES KONTO'
     A                                      SPACEB(2)
     A            ANTOT         10S 2     61EDTCDE(M)
     A                                    63'_________'
