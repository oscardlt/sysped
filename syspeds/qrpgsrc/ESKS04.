      *********************************************************************
      *
      *
CRTPG+*  CRTPGM SYSPED/ESKS04 MODULE(*LIBL/ESKS04 *LIBL/INCGI)
CRTPGM*         ACTGRP(CGI) AUT(*USE)
      *
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FKOSTA     UF   E           K DISK    USROPN
     FKOSTB     UF   E           K DISK    USROPN
     F                                     INFDS(KOSTBDS)
     FKOSTT     UF   E           K DISK    USROPN
     FHEADF     IF   E           K DISK    USROPN
     FVALUL01   IF   E           K DISK    USROPN
     FKODTGE    IF   E           K DISK    USROPN
     FKODTÅ     IF   E           K DISK    USROPN
     FFIRM      IF   E           K DISK    USROPN
      *********************************************************************
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      /copy syspeds/qrpgsrcpy,variables3
      *
      * Client input variables
     D user            s             10
     D WSKABNR         s              7
     D WBKABNR         s              7  0
     D WSSORT          s             10
     D WSINTEG         s              6
     D WBINTEG         s              6  0
     D WSFRAMES        s              1
     D WSSTART         s              1
     D WSFERDIGM       s              1
     D WSEMOD2         s             10
     D WSBS            s              1
     D WSBS1           s              1
     D WSBS2           s             30
     D WSPMN           s              2
     D WBPMN           s              2  0
     D WSPAR           s              2
     D WBPAR           s              2  0
     D WSSG            s              3
     D WSGEB           s              3
     D WSBL            s             15
     D WBBL            s             13  2
     D XBBLHB          s             13  2
     D WSMVA           s              1
     D WSBLM           s             15
     D WBBLM           s             13  2
     D WSVAL           s              3
     D WSVKU           s              8
     D WBVKU           s              7  4
     D OddEven         s             10
      *
     D KOSTBDS         DS
     D  RECNOB               397    400B 0
      *
     D WSBLDIFF        s             15
     D WBBLDIFF        s             13  2
     D WSRECNO         s              7
     D WBRECNO         s              7  0
      *
     D Spaces          s             12a   inz('&nbsp;&nbsp;')
     D FMELDNR         S              2
     D FEILMELDING     S             80
      *
     D FEIL01          C                   CONST('Du må velge bilagserie.')
     D FEIL02          C                   CONST('Måned i periode er ugyldig.')
     D*FEIL03          C                   CONST('År i periode er ugyldig.')
     D FEIL03          C                   CONST('Periode er ugyldig.')
     D FEIL04          C                   CONST('Attestasjonskode må oppgis.')
     D FEIL05          C                   CONST('Gebyrkode er ugyldig.')
     D FEIL06          C                   CONST('Beløp må oppgis.')
     D FEIL07          C                   CONST('P for plikt, F for momsfri.')
     D FEIL08          C                   CONST('Valutakode må oppgis.')
      *
     D                 DS
     D  UPARM1                 1    512
     D  W3FM                 300    300
      * For xlate mellom Upper Case og Lower Case
     D UC              C                   CONST('ABCDEFGHIJKLMNOPQRST-
     D                                     UVWXYZÆØÅ')
     D LC              C                   CONST('abcdefghijklmnopqrst-
     D                                     uvwxyzæøå')
      *
      /copy syspeds/qrpgsrcpy,prolog3
      *
      * Get program start time for calculating execution time
     C                   time                    timedata1
      * Parse input / cvt to numeric
     C                   exsr      Parse
      * File open / keys
     C                   EXSR      INIT
      *
     C     WBKABNR       CHAIN(N)  KOSTA                              33
     C                   IF        WSSTART = 'N'
     C                   EXSR      TESTB
     C                   IF        FEILMELDING = *BLANKS
     C                   GOTO      SLUTT
     C                   END
     C                   ELSE
     C                   MOVE      KAPMN         WBPMN
     C                   MOVE      KAPÅR         WBPAR
     C                   MOVE      KASG          WSSG
     C                   MOVE      KAVK          WSGEB
     C                   MOVE      KABL          WBBL
     C                   MOVE      KAMVA         WSMVA
     C                   MOVE      KABLM         WBBLM
     C                   MOVE      KAVAL         WSVAL
     C                   MOVE      KAVKU         WBVKU
     C                   END
      *
     C                   callp     gethtml('HTMLSRC':
     C                             '*LIBL':
     C                             'ESKS04':'/CGITAG/')
BODY C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updHTMLvar('USER':USER)
     C                   callp     updHTMLvar('INRNR':WSKABNR)
     C                   callp     updHTMLvar('SORT':WSSORT)
     C                   callp     updHTMLvar('INTEG':WSINTEG)
     C                   callp     updHTMLvar('FRAMES':WSFRAMES)
     C                   callp     updHTMLvar('FERDIGM':WSFERDIGM)
     C                   callp     updHTMLvar('EModuls2':WSEMOD2)
     C                   callp     updhtmlvar('FEILMELDING':
     C                             FEILMELDING)
     C                   callp     updhtmlvar('FMELDNR':FMELDNR)
     C                   callp     updHTMLvar('KABNR2':
     C                             %trim(%editc(KABNR2:'3')))
     C                   callp     wrtsection('top')
      *
     C                   IF        KABNR2 = 0
     C                   EXSR      DSPBILS
     C                   END
      *
     C                   callp     updHTMLvar('WSPMN':
     C                             %trim(%editc(WBPMN:'Z')))
     C                   callp     updHTMLvar('WSPAR':
     C                             %trim(%editc(WBPAR:'Z')))
     C                   callp     updHTMLvar('WSSG':WSSG)
     C                   callp     updHTMLvar('WSGEB':WSGEB)
     C                   callp     updHTMLvar('WSBL':
     C                             %trim(%editc(WBBL:'L')))
     C                   callp     updHTMLvar('WSMVA':WSMVA)
     C                   callp     updHTMLvar('WSBLM':
     C                             %trim(%editc(WBBLM:'L')))
     C                   callp     updHTMLvar('WSVKU':
     C                             %trim(%editc(WBVKU:'3')))
     C                   callp     wrtsection('Andre')
      *
     C                   EXSR      DSPVAL
      *
     C     SLUTT         TAG
     C                   exsr      Exit
      *
     C                   eval      *inlr = *on
     C                   return
      *
      *
      *=====================================================================
      * Parse input / cvt to numeric
      *=====================================================================
     C     Parse         begsr
      * Parse variables from QUERY_STRING environment variable
     C                   eval      user     = zhbgetvar('user')
     C                   eval      WSKABNR  = zhbgetvar('INRNR')
     C                   EVAL      WBKABNR  = c2n2(WSKABNR)
     C                   eval      WSSORT   = zhbgetvar('SORT')
     C                   eval      WSINTEG  = zhbgetvar('INTEG')
     C                   EVAL      WBINTEG  = c2n2(WSINTEG)
     C                   eval      WSFRAMES = zhbgetvar('FRAMES')
     C                   eval      WSSTART  = zhbgetvar('WSSTART')
     C                   eval      WSFERDIGM= zhbgetvar('FERDIGM')
     C                   eval      WSEMOD2  = zhbgetvar('EMODULS2')
     C                   eval      WSBS     = zhbgetvar('WSBS')
      *
     C                   eval      WSPMN    = zhbgetvar('WSPMN')
     C                   EVAL      WBPMN    = c2n2(WSPMN)
     C                   eval      WSPAR    = zhbgetvar('WSPAR')
     C                   EVAL      WBPAR    = c2n2(WSPAR)
     C                   eval      WSSG     = zhbgetvar('WSSG')
     C     LC:UC         XLATE     WSSG          WSSG
     C                   eval      WSGEB    = zhbgetvar('WSGEB')
     C     LC:UC         XLATE     WSGEB         WSGEB
     C                   eval      WSBL     = zhbgetvar('WSBL')
     C                   EVAL      WBBL     = c2n2(WSBL)
     C                   eval      WSBLM    = zhbgetvar('WSBLM')
     C                   EVAL      WBBLM    = c2n2(WSBLM)
     C                   eval      WSMVA    = zhbgetvar('WSMVA')
     C     LC:UC         XLATE     WSMVA         WSMVA
     C                   eval      WSVAL    = zhbgetvar('WSVAL')
     C                   eval      WSVKU    = zhbgetvar('WSVKU')
     C                   EVAL      WBVKU    = c2n2(WSVKU)
      *
     C                   endsr
      *
      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   CLOSE     BRIDF
     C                   CLOSE     KOSTA
     C                   CLOSE     KOSTB
     C                   CLOSE     KOSTT
     C                   CLOSE     HEADF
     C                   CLOSE     VALUL01
     C                   CLOSE     KODTGE
     C                   CLOSE     KODTÅ
     C                   CLOSE     FIRM
      *
     C                   endsr
      *
      *=====================================================================******
      *  DSPBILS
      *=====================================================================******
     C     DSPBILS       begsr
     C                   callp     wrtsection('BSStr')
     C                   callp     updHTMLvar('WSBS1':'')
     C                   callp     updHTMLvar('WSBS2':'-- Velg bilagserie --')
     C                   callp     wrtsection('BSrow')
     C     *LOVAL        SETLL     KOSTT
     C                   READ(N)   KOSTT                                  33
     C                   DOW       *IN33 = *OFF
     C                   callp     updHTMLvar('WSBS1':KTTYP)
     C                   callp     updHTMLvar('WSBS2':KTNA)
     C                   callp     wrtsection('BSrow')
     C                   READ(N)   KOSTT                                  33
     C                   ENDDO
     C                   callp     wrtsection('BSbot')
      *
     C                   endsr
      *
      *=====================================================================******
      *  DSPVAL
      *=====================================================================******
     C     DSPVAL        begsr
     C                   IF        KAVAL = *BLANKS
     C                   callp     updHTMLvar('WSVAL1':'')
     C                   callp     updHTMLvar('WSVAL2':'-- Velg valuta --')
     C                   callp     wrtsection('VALrow')
     C                   END
     C     BIFIRM        SETLL     VALUL01
     C     BIFIRM        READE     VALUL01                                33
     C                   DOW       *IN33 = *OFF
     C                   IF        KAVAL = VALKOD
     C                   callp     updhtmlvar('VALSEL':'SELECTED')
     C                   else
     C                   callp     updhtmlvar('VALSEL':spaces)
     C                   end
     C                   callp     updHTMLvar('WSVAL1':VALKOD)
     C                   callp     updHTMLvar('WSVAL2':VALTEK)
     C                   callp     wrtsection('VALrow')
     C     BIFIRM        READE     VALUL01                                33
     C                   ENDDO
     C                   callp     wrtsection('VALbot')
      *
     C                   endsr
      *
      *=====================================================================******
      *  TESTB
      *=====================================================================******
     C     TESTB         begsr
      *
     C                   IF        ((WSFERDIGM = 'J') AND (KABNR2 = 0))
     C                   IF        WSBS = *BLANKS
     C                   MOVEL     FEIL01        FEILMELDING
     C                   MOVE      '01'          FMELDNR
     C                   GOTO      SLTB1
     C                   END
     C                   END
      *
     C                   IF        ((WBPMN < 1) OR (WBPMN > 12))
     C                   MOVEL     FEIL02        FEILMELDING
     C                   MOVE      '02'          FMELDNR
     C                   GOTO      SLTB1
     C                   END
      *
     C*    UYEAR         ADD       1             XXPAR1            2 0
     C*    UYEAR         SUB       1             XXPAR2            2 0
     C*                  IF        ((WBPAR < XXPAR2) OR (WBPAR > XXPAR1))
     C*                  MOVEL     FEIL03        FEILMELDING
     C*                  MOVE      '03'          FMELDNR
     C*                  GOTO      SLTB1
     C*                  END
      *
     C     KODTÅK        CHAIN     KODTÅ                              33
     C                   IF        *IN33
     C                   MOVEL     FEIL03        FEILMELDING
     C                   MOVE      '03'          FMELDNR
     C                   GOTO      SLTB1
     C                   END
      *
     C                   IF        WSSG = *BLANKS
     C                   MOVEL     FEIL04        FEILMELDING
     C                   MOVE      '04'          FMELDNR
     C                   GOTO      SLTB1
     C                   END
      *
     C                   IF        ((KALNR = 0) OR (WSGEB <> *BLANKS))
     C     KODTGEK       CHAIN     KODTGE                             33
     C                   IF        *IN33
     C                   MOVEL     FEIL05        FEILMELDING
     C                   MOVE      '05'          FMELDNR
     C                   GOTO      SLTB1
     C                   END
     C                   END
      *
     C                   IF        WBBL = 0
     C                   MOVEL     FEIL06        FEILMELDING
     C                   MOVE      '06'          FMELDNR
     C                   GOTO      SLTB1
     C                   END
      *
     C                   IF        ((WSMVA <> 'P') AND (WSMVA <> 'F'))
     C                   MOVEL     FEIL07        FEILMELDING
     C                   MOVE      '07'          FMELDNR
     C                   GOTO      SLTB1
     C                   END
      *
     C                   IF        WSVAL = *BLANKS
     C                   MOVEL     FEIL08        FEILMELDING
     C                   MOVE      '08'          FMELDNR
     C                   GOTO      SLTB1
     C                   END
      *
     C     WBKABNR       CHAIN     KOSTA                              33
     C                   IF        KASG = WSSG
     C                   MOVE      'N'           XANDRE            1
     C                   ELSE
     C                   MOVE      'J'           XANDRE
     C                   END
      *
     C                   IF        KABNR2 = 0
     C     WSBS          CHAIN     KOSTT                              33
     C                   IF        *IN33 = *OFF
     C                   MOVE      KTNR          KABNR2
     C                   ADD       1             KTNR
     C                   UPDATE    KOSTTR
     C                   END
     C                   END
      *
     C                   IF        ((WSVAL <> *BLANKS) AND (KAVAL <> *BLANKS))
     C                   IF        ((WBVKU <> KAVKU) OR (WSVAL <> KAVAL))
     C                   EXSR      OPDKOSTB
     C                   END
     C                   END
      *
     C                   IF        WSFERDIGM = 'J'
     C     *LOVAL        SETLL     FIRM
     C                   READ      FIRM                                   33
     C     FITAX         ADD       1             FITAXN            5 4
     C                   Z-ADD     9             UFLAGG
     C                   CALL      'SYKS05'
     C                   PARM                    WBKABNR
     C                   PARM                    KABLM
     C                   PARM                    FITAXN
     C                   PARM                    UFLAGG
      *
     C                   IF        UFLAGG > 0
     C                   IF        UFLAGG = 1
     C                   EVAL      FEILMELDING = 'Samlet MVA på linjer stemmer '
     C                             + 'ikke med MVA på bil ag. Sjekk MVA på '
     C                             + 'linje.'
     C                   ELSE
     C                   EVAL      FEILMELDING = 'EDI-motatt bilag inneholder '
     C                             + 'minst en linje som mangler gyldig '
     C                             + 'henvisning.'
     C                   END
     C                   MOVE      '09'          FMELDNR
     C                   GOTO      SLTB1
     C                   END
     C                   END
      *
     C                   MOVE      WBPMN         KAPMN
     C                   MOVE      WBPAR         KAPÅR
     C                   MOVE      20            KAPCC
     C                   MOVE      WSSG          KASG
     C                   MOVE      WSGEB         KAVK
     C                   MOVE      WBBL          KABL
     C                   MOVE      WSMVA         KAMVA
     C                   MOVE      WBBLM         KABLM
     C                   MOVE      WSVAL         KAVAL
     C                   IF        WBVKU = 0
     C     VALUL01K      CHAIN     VALUL01                            33
     C                   Z-ADD     VALKU2        WBVKU
     C                   END
     C                   MOVE      WBVKU         KAVKU
     C                   IF        ((WSFERDIGM = 'J') AND (KABNR2 <> 0))
     C                   MOVE      'G'           KAST
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVEL     USER          KAUSER
     C                   MOVE      WTM           KATME
     C                   MOVE      WDT           KADTE
     C                   END
     C                   UPDATE    KOSTAR
      *
     C                   SELECT
     C                   WHEN      WSFERDIGM = 'J'
     C                   EXSR      SRFM
     C                   WHEN      XANDRE = 'J'
      * SEND OPPDRAG TIL EN ANNEN BRUKER
     C                   callp     gethtml('HTMLSRC':
     C                             '*LIBL':
     C                             'ESKS02IC':'/CGITAG/')
     C                   callp     wrtsection('top')
     C                   exsr      Exit
     C                   eval      *inlr = *on
     C                   return
     C                   OTHER
     C                   EXSR      SRVISLIN
     C                   ENDSL
      *
     C     SLTB1         endsr
      *
      *=====================================================================******
      *  SRFM
      *=====================================================================******
     C     SRFM          begsr
     C                   MOVE      *ZEROS        UFLAGG            1 0
     C                   MOVE      'N'           W2ENDRE           1
     C                   MOVE      UPARM1        UPARM2
      *
     C                   CALL      'SYKS03'
     C                   PARM                    BIFIRM
     C                   PARM                    WBKABNR
     C                   PARM                    UBDT              6 0
     C                   PARM                    KAPÅR
     C                   PARM                    KAPMN
     C                   PARM                    KAVK
     C                   PARM                    KAVAL
     C                   PARM                    UVKU             10 7
     C                   PARM                    KABL
     C                   PARM                    KABLM
     C                   PARM                    ULNR              8 0
     C                   PARM                    UBL              13 2
     C                   PARM                    KAMVA
     C                   PARM                    KABNR2
     C                   PARM                    UFLAGG            1 0
     C                   PARM                    UPARM2          512
      *
     C                   callp     gethtml('HTMLSRC':
     C                             '*LIBL':
     C                             'ESKS02IC':'/CGITAG/')
     C                   callp     wrtsection('top')
     C                   exsr      Exit
     C                   eval      *inlr = *on
     C                   return
      *
     C                   endsr
      *
      *=====================================================================******
      *  SRVISLIN
      *=====================================================================******
     C     SRVISLIN      begsr
      *
     C                   callp     gethtml('HTMLSRC':
     C                             '*LIBL':
     C                             'ESKS02':'/CGITAG/')
      *
     C                   EXSR      SUMDELT
      *
OddEvC                   callp     updHTMLvar('ROWHEAD':RowHead)
OddEvC                   callp     updHTMLvar('LogoImg':LogoImg)
BODY C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updHTMLvar('USER':USER)
     C                   callp     updHTMLvar('INRNR':WSKABNR)
     C                   callp     updHTMLvar('SORT':WSSORT)
     C                   callp     updHTMLvar('INTEG':WSINTEG)
     C                   callp     updHTMLvar('FRAMES':WSFRAMES)
     C                   EVAL      WSEMOD2 = 'KOMPLETT'
     C                   callp     updHTMLvar('EModuls2':WSEMOD2)
      *
     C                   callp     updHTMLvar('KAVAL':KAVAL)
     C                   callp     updHtmlVar('KABL':
     C                             %trim(%editc(KABL:'L')))
     C                   callp     updHtmlVar('WSBLDELT':
     C                             %trim(%editc(WSBLDELT:'L')))
     C                   callp     updHtmlVar('WSBLDIFF':
     C                             %trim(%editc(WBBLDIFF:'L')))
      *
     C                   callp     updHtmlVar('WSBL':
     C                             %trim(%editc(0:'L')))
     C                   callp     updHTMLvar('WSVK':'')
     C                   callp     updHTMLvar('WSKDPF':'')
     C                   callp     updHTMLvar('WSKDM':'')
     C                   callp     updHtmlVar('WSPROS':
     C                             %trim(%editc(0:'3')))
     C                   callp     updHTMLvar('WSKDMV':'')
      *
     C                   callp     updHtmlVar('WSAVD':
     C                             %trim(%editc(0:'Z')))
     C                   callp     updHtmlVar('WSOPD':
     C                             %trim(%editc(0:'Z')))
     C                   callp     updHtmlVar('WSPRO':
     C                             %trim(%editc(0:'Z')))
     C                   callp     updHTMLvar('WSGN17':'')
      *
     C                   callp     updHTMLvar('WSBILT':'')
     C                   callp     updHTMLvar('WSFRIO':'')
     C                   callp     updHTMLvar('WSFR01':'')
     C                   callp     updHTMLvar('WSFR02':'')
     C                   callp     updHTMLvar('WSFR03':'')
     C                   callp     updHTMLvar('WSOTIO':'')
     C                   callp     updHTMLvar('WSOT01':'')
     C                   callp     updHTMLvar('WSOT02':'')
     C                   callp     updHTMLvar('WSOT03':'')
     C                   callp     updHTMLvar('WSSKIO':'')
     C                   callp     updHTMLvar('WSSK01':'')
     C                   callp     updHTMLvar('WSSK02':'')
      *
     C                   callp     updhtmlvar('FEILMELDING':'')
     C                   callp     updhtmlvar('FMELDNR':'')
     C                   callp     wrtsection('top')
      *
     C                   callp     wrtsection('Lage')
     C                   callp     wrtsection('LageMod2')
     C                   callp     wrtsection('LageMod4')
     C                   callp     wrtsection('Felles')
      *
     C     WBKABNR       SETGT     KOSTB
     C     WBKABNR       READPE(N) KOSTB                                  33
     C                   DOW       *IN33 = *OFF
     C                   exsr      Setrow
     C                   callp     wrtsection('row')
     C     WBKABNR       READPE(N) KOSTB                                  33
     C                   ENDDO
      * Quit
      * Compute run time
     C                   time                    timedata2
     C     timedata2     subdur    timedata1     ms:*ms
     C                   eval      sec = ms / 1000000
     C                   callp     updhtmlvar('runtime':
     C                             %trim(%editw(sec:'     0 .   ')))
      *
     C                   callp     wrtsection('bot')
      *
     C                   exsr      Exit
     C                   eval      *inlr = *on
     C                   return
      *
     C                   endsr
      *
      *=====================================================================******
      *  SUM FORDELT
      *=====================================================================******
     C     SUMDELT       begsr
     C                   MOVE      *ZEROS        WSBLDELT         13 2
     C     WBKABNR       SETLL     KOSTB
     C     WBKABNR       READE(N)  KOSTB                                  33
     C                   DOW       *IN33 = *OFF
     C                   ADD       KBBLHB        WSBLDELT
     C     WBKABNR       READE(N)  KOSTB                                  33
     C                   ENDDO
      *
     C     VALUL01K      CHAIN     VALUL01                            33
     C                   EVAL(H)   WSBLDELT = WSBLDELT / KAVKU * OMRFAK
     C     KABL          ADD       WSBLDELT      WBBLDIFF
      *
     C                   endsr
      *
      *=====================================================================
      *  Set variable data for section /$row
      *=====================================================================
     C     Setrow        begsr
     c     OddEven       IFEQ      ODD
     c                   eval      OddEven = EVEN
     c                   else
     c                   eval      OddEven = ODD
     c                   end
     C                   callp     updHTMLvar('ODDEVEN':OddEven)
      *
     C     HEADFK        CHAIN     HEADF                              33
      *
     C                   callp     updHtmlVar('KBAVD':
     C                             %trim(%editc(KBAVD:'Z')))
     C                   callp     updHtmlVar('KBOPD':
     C                             %trim(%editc(KBOPD:'Z')))
     C                   callp     updHTMLvar('KBOT':HEOT)
     C                   callp     updHTMLvar('KBFR':HEFR)
     C                   callp     updHtmlVar('KBNOKK':
     C                             %trim(%editc(KBNØKK:'3')))
     C                   callp     updHtmlVar('KBKN':
     C                             %trim(%editc(KBKN:'Z')))
     C                   callp     updHTMLvar('KBVK':KBVK)
     C                   callp     updHTMLvar('KAVAL':KAVAL)
     C                   callp     updHtmlVar('KBKDM':
     C                             %trim(%editc(KBKDM:'3')))
     C                   EVAL(H)   XBBLHB = KBBLHB / KAVKU * OMRFAK
     C                   callp     updHtmlVar('KBBLHB':
     C                             %trim(%editc(XBBLHB:'L')))
     C                   callp     updHTMLvar('KBKDMV':KBKDMV)
     C                   callp     updHtmlVar('KBBLF':
     C                             %trim(%editc(KBBLF:'L')))
     C                   callp     updHtmlVar('RECNO':
     C                             %trim(%editc(RECNOB:'Z')))
      *
     C                   endsr
      *
      *=====================================================================******
      *  OPPDATER BELØP I KOSTB
      *=====================================================================******
     C     OPDKOSTB      begsr
      *
     C     VALUL01KG     CHAIN     VALUL01                            33
     C                   MOVE      KAVKU         XVKUGM            7 4
     C                   MOVE      OMRFAK        XOMRFG            3 0
     C     VALUL01K      CHAIN     VALUL01                            33
     C                   IF        WBVKU = 0
     C                   MOVE      VALKU1        XVKUNY            7 4
     C                   ELSE
     C                   MOVE      WBVKU         XVKUNY            7 4
     C                   END
     C                   MOVE      OMRFAK        XOMRFN            3 0
     C     WBKABNR       SETGT     KOSTB
     C     WBKABNR       READPE    KOSTB                                  33
     C                   DOW       *IN33 = *OFF
     C                   EVAL(H)   KBBLHB = KBBLHB * XOMRFG / XVKUGM
     C                   EVAL(H)   KBBLHB = KBBLHB * XVKUNY / XOMRFN
     C                   UPDATE    KOSTBR
     C     WBKABNR       READPE    KOSTB                                  33
     C                   ENDDO
      *
     C                   endsr
      *
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
     C                   OPEN      BRIDF
     C     USER          CHAIN     BRIDF                              50
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
      *
OddEv * hent Parametre som går igjen i mange prog:
OddEv *      Odd/Even/Rowhead-farger,Logobilde,Språk,Intern/Ekstern bruker,  mm
OddEvc                   call      'ESGETPAR'
OddEvc                   parm                    BIBRID
OddEvc                   parm                    BIFIRM
OddEvc                   parm                    BIKUNR
OddEvc                   parm                    BIKNG
OddEvc                   parm                    RowHead          10
OddEvc                   parm                    Odd              10
OddEvc                   parm                    Even             10
OddEvc                   parm                    LogoImg          50
OddEvc                   parm                    XSPRÅK            2
OddEvc                   parm                    XINTERN           1
OddEvc                   parm                    ParmDS1        1024
OddEvc                   parm                    ParmDS2        1024
OddEvc                   parm                    ParmDS3        1024
      *
     C                   OPEN      KOSTA
     C                   OPEN      KOSTB
     C                   OPEN      KOSTT
     C                   OPEN      HEADF
     C                   OPEN      VALUL01
     C                   OPEN      KODTGE
     C                   OPEN      KODTÅ
     C                   OPEN      FIRM
      *
     C     HEADFK        KLIST
     C                   KFLD                    KBAVD
     C                   KFLD                    KBOPD
     C     VALUL01K      klist
     C                   kfld                    BIFIRM
     C                   kfld                    WSVAL
     C     VALUL01KG     klist
     C                   kfld                    BIFIRM
     C                   kfld                    KAVAL
     C     KODTGEK       klist
     C                   kfld                    KGEUNI
     C                   kfld                    WSGEB
     C                   MOVE      'GE'          KGEUNI
     C     KODTÅK        klist
     C                   KFLD                    KOÅUNI
     C                   kfld                    KOÅCC
     C                   kfld                    WBPAR
     C                   kfld                    WBPMN
     C                   Z-ADD     20            KOÅCC
     C                   MOVE      'W'           KOÅUNI
      *
     C                   endsr
      *
