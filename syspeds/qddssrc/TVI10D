     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      REF(*LIBL/TVINF)
     A                                      CF03(03 'Avslutt program')
     A                                      CF12(12 'Annuller')
     A                                      PRINT
     A                                      HLPDOC('STD' 'HELPNU' 'HLP400')
     A                                      HELP
     A          R TVI10DA
     A                                      OVERLAY
     A                                  1  2'TVINN'
     A                                      COLOR(BLU)
     A                                  1 29'Besvare melding internt'
     A                                      COLOR(WHT)
     A                                  1 73'TVI10D A'
     A                                      COLOR(BLU)
     A N50                              2 29'I M P O R T'
     A                                      COLOR(WHT)
     A  50                              2 29'E K S P O R T'
     A                                      COLOR(WHT)
     A                                  3  2'Avd:'
     A            MAVD      R        O  3  7REFFLD(MAVD EDIM)
     A                                      EDTCDE(4)
     A                                  3 14'Tdek:'
     A            MTDN      R        O  3 20REFFLD(MTDN EDIM)
     A                                      EDTCDE(4)
     A                                  3 28'Dekl.dato:'
     A            M0068A    R        O  3 39REFFLD(M0068A EDIM)
EV5  A                                      EDTWRD('    .  .  ')
     A                                  3 50'Sekv:'
SV5  A*           M0068B    R        O  3 56REFFLD(M0068A EDIM)
NV5  A            M0068B    R        O  3 56REFFLD(M0068B EDIM)
     A                                      EDTCDE(4)
     A                                  3 65'Ver:'
     A            M0068C    R        O  3 70REFFLD(M0068C EDIM)
     A                                      EDTCDE(4)
     A                                  5  2'Velg og trykk Enter:'
     A                                      COLOR(BLU)
     A                                  7  4'Tekst Referanse Kode . . . . . . .-
     A                                       :'
     A            F0077     R        O  7 43
     A                                  8  6'Funksjonskode  . . . . . . . . . :'
     A            F4815     R        O  8 43
     A                                  9  8'Fritekst . . . . . . . . . . . :'
     A            F0078A    R        B 10  8CHECK(LC)
     A                                      DFTVAL('Internt besvart melding.   -
     A                                                           **  Systemamel-
     A                                      ding  **')
     A                                      DSPATR(PR)
     A            F0078B    R        B 11  8
     A  31                                  COLOR(RED)
     A                                      CHECK(LC)
     A                                      COMP(GT ' ')
     A            F0078C    R        B 12  8CHECK(LC)
     A            F0078D    R        B 13  8CHECK(LC)
     A            F0078E    R        B 14  8CHECK(LC)
     A                                 23  2'F3=Gå ut   F12=Annuller'
     A                                      COLOR(BLU)
     A          R TVI10DØ
     A                                      OVERLAY
     A            ZMSGTX        79A  O 24  2MSGID(&ZMSGNO SYMSGF)
     A N30                                  COLOR(WHT)
     A  30                                  COLOR(RED)
     A            ZMSGNO         7A  P
