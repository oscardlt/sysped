      *
      ***********************************************************
      *** PROGRAM SKAL VEDLIKEHOLDE E.F. MANIFEST - LISTE     ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01 02 07 09-14 17-24             ***
      *** LEDIGE INDIKATORER 29 31 32       52-75 78-89       ***
      ***********************************************************
      *
     H DFTACTGRP(*NO)
     FSADEFCL1  UF   E           K DISK
     FSADEFCSL1 IF   E           K DISK
     FTURER14   IF   E           K DISK
     FSADH      IF   E           K DISK
     FTRACKF    O  A E             DISK
     FSAD132D   CF   E             WORKSTN SFILE(SUBFIL:ZRECNO)
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D                SDS
     D  UUSR                 254    263
     D                 DS
     D  SCASTR                 1     20
     D  SCASTR4                1      4  0
     D  SCASTR7                5     11  0
      *  Prototype for 'SYGE15C'
     d SYGE15C         pr                  extpgm('SYGE15C')
     d wdt_                                like(wdt)
     d wtm_                                like(wtm)
      *  Prototype for 'ARCA000CL'
     d ARCA000CL       pr                  extpgm('ARCA000CL')
     d scatyp_                             like(scatyp)
     d scastr_                             like(scastr)
      *  Prototype for 'ARC003'
     d ARC003          pr                  extpgm('ARC003')
     d u_avd_                              like(u_avd)
     d u_tdn_                              like(u_tdn)
     d u_runde_                            like(u_runde)
      *  Prototype for 'TR011C'
     d TR011C          pr                  extpgm('TR011C')
     d u_avd_                              like(u_avd)
     d u_tdn_                              like(u_tdn)
     d u_pro_                              like(u_pro)
     d u_sg_                               like(u_sg)
     d u_type_                             like(u_type)
     d u_ie_                               like(u_ie)
     d uflagg_                             like(uflagg)
      *  Prototype for 'SAD005A'
     d SAD005A         pr                  extpgm('SAD005A')
     d u_avd_                              like(u_avd)
     d u_tdn_                              like(u_tdn)
     d uflagg_                             like(uflagg)
      *  Prototype for 'SAD134R'
     d SAD134R         pr                  extpgm('SAD134R')
     d upro_                               like(upro)
     d u_avd_                              like(u_avd)
     d u_tdn_                              like(u_tdn)
     d utype_                              like(utype)
     d uflagg_                             like(uflagg)
      *  Prototype for 'SAD133R'
     d SAD133R         pr                  extpgm('SAD133R')
     d uavd_                               like(uavd)
     d upro_                               like(upro)
     d uuuid_                              like(uuuid)
     d uflagg_                             like(uflagg)
      *  Prototype for 'SAD132RB'
     d SAD132RB        pr                  extpgm('SAD132RB')
     d u_avd_                              like(u_avd)
     d u_tdn_                              like(u_tdn)
     d utype_                              like(utype)
     d uflagg_                             like(uflagg)
      *  Prototype for 'SAD132RA'
     d SAD132RA        pr                  extpgm('SAD132RA')
     d upro_                               like(upro)
      *  Prototype for SAD132R
     d sad132r         pr
     d uavd_                               like(uavd)
     d upro_                               like(upro)
     d uuuid_                              like(uuuid)
     d utype_                              like(utype)
     d uflagg_                             like(uflagg)
      *  *ENTRY Interface for Main Procedure
     d sad132r         pi
     d uavd                           4  0
     d upro                           8  0
     d uuuid                         36a
     d utype                          3a
     d uflagg                         1  0
      *
     d scatyp          s              2a
     d xtype           s              3a
     d yrecno          s                   like(zrecno)
     d u_avd           s              4  0
     d u_tdn           s              7  0
     d u_pro           s              8  0
     d u_sg            s              3
     d u_type          s              3
     d u_ie            s              1
     d u_runde         s             10
     d wdt             s              8a
     d wtm             s              6a
      *
      /free

         exsr init;

         *in93 = *on;
         write subctl;
         *in93 = *off;
         *in94 = *off;
         zrecno = 0;

         dow *in03 = *off;            // START                                   // 1<b

           setll upro sadefcl1;
           reade(n) upro sadefcl1;

           dow not %eof;                                                         // 2<b
             chain (clavd: cltdn) sadefcsl1;
             if %found;
               *in51 = *on;
             else;
               *in51 = *off;
             endif;
             zrecno = zrecno + 1;
             write subfil;
             reade(n) upro sadefcl1;
           enddo;                                                                // 2<e
           yrecno = zrecno;
           if zrecno > 19;                                                       // 2<b
             zrecno = 1;
           endif;                                                                // 2<e

           dow *in03 = *off;               // START3                             // 2<b
           // vis subfil og få input
             *in91 = *on;
             *in92 = *on;
             write bunn;
             if zrecno = 0;                                                      // 3<b
               exfmt nyvare;
             else;                                                               // 3
               exfmt subctl;
             endif;                                                              // 3<e

             *in30 = *off;
             *in98 = *off;
             *in99 = *off;
             select;                                                             // 3<b
             when *in03;                                                         // 3
               uflagg = 3;
               *inlr = *on;
               return;
             when *in05;                                                         // 3
               *in93 = *on;
               write subctl;
               *in93 = *off;
               *in94 = *off;
               zrecno = 0;
               leave;
             when *in06;                                                         // 3
               exsr lagopd;
               *in93 = *on;
               write subctl;
               *in93 = *off;
               *in94 = *off;
               zrecno = 0;
               leave;
             when *in10;                                                         // 3
               exsr plkopd;
               *in93 = *on;
               write subctl;
               *in93 = *off;
               *in94 = *off;
               zrecno = 0;
               leave;
             when *in12;                                                         // 3
               uflagg = 1;
               *inlr = *on;
               return;
             when zrecno <> 0;                                                   // 3
               exsr hent;
             endsl;                                                              // 3<e

           enddo;                                                                // 2<e

         enddo;                                                                  // 1<e

         *inlr = *on;
         return;

      //*********************************************
       begsr hent;

       msgnr = *blanks;
       *in90 = *off;
       *in91 = *off;
       *in92 = *off;
       *in98 = *off;
       *in99 = *off;

       readc subfil;
       dow not %eof;
         if cletypt <> '* SLETTET *';
           select;
           when wsnr = 2 and *in49 = *off;
             *in99 = *off;
             exsr chgopd;
           when wsnr = 4 and *in49 = *off;
             *in99 = *off;
             exsr sltopd;
           when wsnr = 5;
             *in99 = *off;
             exsr dspopd;
           when wsnr = 11 and cltdn > 0;
             *in99 = *off;
             exsr splopd;
           when wsnr = 15 and cltdn > 0;
             *in99 = *off;
             exsr vissad;
           when wsnr = 51;
             *in99 = *off;
             exsr arkivs;
           when wsnr = 52;
             *in99 = *off;
             exsr prtskill;
           endsl;
         endif;

         readc subfil;
       enddo;

       if *in99 = *off;
         msgnr = 'DIV0012';
         *in30 = *on;
       endif;
       *in99 = *off;

       endsr;

      //*********************************************
       begsr dspopd;

       xtype = 'DSP';            // SPØRRING
       u_avd = clavd;
       u_tdn = cltdn;
       sad134r (upro: u_avd: u_tdn: xtype: uflagg);

       if *in90 = *off;
         chain (clavd: cltdn) sadefcsl1;
         if %found;
           *in51 = *on;
         else;
           *in51 = *off;
         endif;
         wsnr = 0;
         update subfil;
       endif;

       endsr;

      //*********************************************
       begsr vissad;

       u_avd = clavd;
       u_tdn = cltdn;
       if cltrnr = *blanks;
         sad005a (u_avd: u_tdn: uflagg);
       else;
         u_type = 'DSP';
         u_ie   = 'E';
         tr011c (u_avd: u_tdn: u_pro: u_sg: u_type: u_ie: uflagg);
       endif;

       if *in90 = *off;
         chain (clavd: cltdn) sadefcsl1;
         if %found;
           *in51 = *on;
         else;
           *in51 = *off;
         endif;
         wsnr = 0;
         update subfil;
       endif;

       endsr;

      //*********************************************
       begsr arkivs;

       u_avd = clavd;
       u_tdn = cltdn;
       u_runde = *blanks;
       arc003 (u_avd: u_tdn: u_runde);

       if *in90 = *off;
         chain (clavd: cltdn) sadefcsl1;
         if %found;
           *in51 = *on;
         else;
           *in51 = *off;
         endif;
         wsnr = 0;
         update subfil;
       endif;

       endsr;

      //*********************************************
       begsr prtskill;

       u_avd = clavd;
       u_tdn = cltdn;
       scatyp  = *blanks;
       scastr  = *blanks;
       scastr4 = clavd;
       scastr7 = cltdn;
       arca000cl (scatyp: scastr);

       if *in90 = *off;
         chain (clavd: cltdn) sadefcsl1;
         if %found;
           *in51 = *on;
         else;
           *in51 = *off;
         endif;
         wsnr = 0;
         update subfil;
       endif;

       endsr;

      //*********************************************
       begsr chgopd;

       xtype = 'CHG';            // ENDRING
       u_avd = clavd;
       u_tdn = cltdn;
       sad134r (upro: u_avd: u_tdn: xtype: uflagg);

       chain(n) (upro: clavd: cltdn) sadefcl1;

       if *in90 = *off;
         chain (clavd: cltdn) sadefcsl1;
         if %found;
           *in51 = *on;
         else;
           *in51 = *off;
         endif;
         wsnr = 0;
         update subfil;
       endif;

       endsr;

      //*********************************************
       begsr sltopd;

       chain (upro: clavd: cltdn) sadefcl1;
       if %found;
         clst  = 'S';
         clpro = 0;
         update sadefcfr;
         cletypt = '* SLETTET *';
         exsr ttsr;
       endif;

       if *in90 = *off;
         chain (clavd: cltdn) sadefcsl1;
         if %found;
           *in51 = *on;
         else;
           *in51 = *off;
         endif;
         wsnr = 0;
         update subfil;
       endif;

       endsr;

      //*********************************************
       begsr splopd;

       chain (clavd: cltdn) sadh;
       if not %found;
         u_avd = clavd;
         u_tdn = cltdn;
         sad132rb (u_avd: u_tdn: utype: uflagg);
       endif;

       chain(n) (upro: clavd: cltdn) sadefcl1;

       if *in90 = *off;
         chain (clavd: cltdn) sadefcsl1;
         if %found;
           *in51 = *on;
         else;
           *in51 = *off;
         endif;
         wsnr = 0;
         update subfil;
       endif;

       endsr;

      //*********************************************
       begsr plkopd;

       sad133r (uavd: upro: uuuid: uflagg);

       endsr;

      //*********************************************
       begsr lagopd;

       xtype = 'CRT';
       u_avd = 0;
       u_tdn = 0;
       uflagg = 0;
       sad134r (upro: u_avd: u_tdn: xtype: uflagg);

       endsr;

      //*********************************************
       begsr ttsr;

       syge15c (wdt: wtm);

       ttavd  = clavd;
       ttopd  = cltdn;
       tttur  = 0;
       ttacti = 'EFC';
       ttdate = %dec(wdt:8:0);
       tttime = %dec(wtm:6:0);
       ttdatl = ttdate;
       tttiml = tttime;
       ttuser = uusr;
       tttext = 'Oppdrag er fjernet fra E.F. manifest('
               + %TRIM(%EDITC(UPRO:'Z')) + ').';
       tttexl = tttext;
       ttedev = 'RMV';

       write trackfr;

       endsr;

      //*********************************************
       begsr init;

       syge15c (wdt: wtm);
       if utype = 'DSP';
         *in49 = *on;
       else;
         chain upro turer14;
         if %found;
         // Klagjør for SADEFCF
           sad132ra (upro);
         endif;
       endif;
       write windowsz;

       endsr;

      /end-free
