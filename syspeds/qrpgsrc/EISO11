      *********************************************************************
      * PROGRAM VISE UTVALGTE SAKER
      *
CRTPG+*  CRTPGM SYSPED/EISO11 MODULE(*LIBL/EISO11
CRTPGM*        *LIBL/CHECKUSR *LIBL/INCGI) ACTGRP(CGI) AUT(*USE)
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10035 * 01 YBC PTF10 Full omskriving i forb men innføring av TABLESORT mm
10222 * 02 YBC PTF10 VIS FARGE PÅ SAKER SOM ER ELDRE ENN X-DAGER
********************************************************************
      *
      *********************************************************************
      * MAIN PROGRAM FLOW
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
     FBRIDF     UF   E           K DISK    USROPN
     FBRPARF    IF   E           K DISK    USROPN
N02  FCUNDL01   IF   E           K DISK    USROPN
N02  FKALENDER  IF   E           K DISK    USROPN
     FISOFL9    IF   E           K DISK    USROPN
     FISOFL1    IF   E           K DISK    USROPN
     F                                     RENAME(ISOFR:ISOFR1)
     FISOFL3    IF   E           K DISK    USROPN
     F                                     RENAME(ISOFR:ISOFR3)
     FISOFL7    IF   E           K DISK    USROPN
     F                                     RENAME(ISOFR:ISOFR7)
     FISOFL8    IF   E           K DISK    USROPN
     F                                     RENAME(ISOFR:ISOFR8)
     FISOFL11   IF   E           K DISK    USROPN
     F                                     RENAME(ISOFR:ISOFR11)
     FISOFL12   IF   E           K DISK    USROPN
     F                                     RENAME(ISOFR:ISOFR12)
     FISOFL13   IF   E           K DISK    USROPN
     F                                     RENAME(ISOFR:ISOFR13)
     FISOFL14   IF   E           K DISK    USROPN
     F                                     RENAME(ISOFR:ISOFR14)
     FISOKDT    IF   E           K DISK    USROPN
     FISOKDA    IF   E           K DISK    USROPN
     FISOKDP    IF   E           K DISK    USROPN
     FISOKDS    IF   E           K DISK    USROPN
     FKOFAST    IF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Prototype defintions and standard system API error structure
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      *--------------------------------------------------------------------
      * Variables common to CGI programs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
     D IfsMultIndicators...
     d                 ds
     D  NoErrors                       n
     D  NameTooLong                    n
     D  NotAccessible                  n
     D  NoFilesUsable                  n
     D  DupSections                    n
     D  FileIsEmpty                    n
      *
      * Variables received from the input string
     D lng             s              2
     D BckGnd          s             10
     D lstbrd          s              1
     D UsrSpcName      s             10
     D WSUSER          S             10
     D WSFORST         S              1
     D WSLEDET1        S             15
     D WSLEDET2        S             15
     D WSLEDET3        S             15
     D WSRES           S              9  2
     D TOTFORV         S             11  2
     D TOTRES          S             11  2
     D TOTSAK          S              5  0
     D WSKNA           S              8
     D XWSKNA          S              8  0
     D WSDTF           S             10
     D XWSDTF          S              8  0
     D WSSAKN          S              7
     D XWSSAKN         S              7  0
     D WSAVD           S              4
     D XWSAVD          S              4  0
     D WSOPD           S              7
     D XWSOPD          S              7  0
     D WSFN            S              7
     D XWSFN           S              7  0
     D WSFNIN          S             17
     D WSTN            S              8
     D XWSTN           S              8  0
     D WSKNT           S              8
     D XWSKNT          S              8  0
     D WSOIT           S              1
     D WSPT            S              5
     D WSAA            S              5
     D WSAP            S              5
     D WSST            S              5
     D WSUSRS          S             10
     D VALG01          S             50
     D VALG02          S             50
     D VALG03          S             50
     D VALG04          S             50
     D VALG05          S             50
     D VALG06          S             50
     D VALG07          S             50
     D VALG08          S             50
     D VALG09          S             50
     D VALG10          S             50
     D VALG11          S             50
N02  D SORTKNA         S             32
N02  D SORTSAKN        S             32
N02  D SORTOPD         S             32
N02  D SORTTN          S             32
N02  D SORTFN          S             32
N02  D SORTFIN         S             32
N02  D SORTKNT         S             32
     D ANTSND          s              5  0
     D MaxSN           s              5
     D MaxSND          s              5  0
     D ANTRCD          s              5  0
     D MaxRC           s              5
     D MaxRCD          s              5  0
     D PGM             C                   CONST('SYSPED/CHECKUSR')
      * Variables received from the input string
     D Lib             s             10
     D Fn              s             10
     D Mbr             s             10
      * tekstkonstanter
     D VALG01no        C                   CONST('Anmeldersnr.')
     D VALG02no        C                   CONST('Saksnr.')
     D VALG03no        C                   CONST('Oppdragsref.')
     D VALG04no        C                   CONST('Turnr.')
     D VALG05no        C                   CONST('Vis oppdrag på tur:')
     D VALG06no        C                   CONST('Fakturanr.')
     D VALG07no        C                   CONST('Inngående fakturaref.')
     D VALG08no        C                   CONST('Transportørsnr.')
     D VALG09no        C                   CONST('Kun posttype:')
     D VALG10no        C                   CONST('Kun arvvikets art:')
     D VALG11no        C                   CONST('Kun Ansvarlig part:')
      *
      *--------------------------------------------------------------------
      * Prolog common to CGI programs
      * -Receive input from the remote browser
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prolog3
      *=====================================================================******
      * MAIN PROCESS LINE
      *=====================================================================******
      * Get program start time for calculating execution time
     C                   time                    timedata1
      * Parse input
     C                   exsr      Parse
      *
     C                   EXSR      INIT
      *
      * Set output skeleton html member name
     C                   exsr      LoadHtml
      *
      * 1-Section /$top
N02  C                   EVAL      SORTKNA  = *BLANKS
N02  C                   EVAL      SORTSAKN = *BLANKS
N02  C                   EVAL      SORTOPD  = *BLANKS
N02  C                   EVAL      SORTTN   = *BLANKS
N02  C                   EVAL      SORTFN   = *BLANKS
N02  C                   EVAL      SORTFIN  = *BLANKS
N02  C                   EVAL      SORTKNT  = *BLANKS
N02  C                   SELECT
N02  C                   WHEN      XWSKNA  <> 0
N02  C                   EVAL      SORTKNA  = ', "asSorting": [ "asc", "desc" ]'
N02  C                   WHEN      XWSSAKN <> 0
N02  C                   EVAL      SORTSAKN = ', "asSorting": [ "asc", "desc" ]'
N02  C                   WHEN      XWSAVD  <> 0
N02  C                   EVAL      SORTOPD  = ', "asSorting": [ "asc", "desc" ]'
N02  C                   WHEN      ((XWSTN <> 0) AND (WSOIT <> 'Y'))
N02  C                   EVAL      SORTTN   = ', "asSorting": [ "asc", "desc" ]'
N02  C                   WHEN      ((XWSTN <> 0) AND (WSOIT = 'Y'))
N02  C                   EVAL      SORTTN   = ', "asSorting": [ "asc", "desc" ]'
N02  C                   WHEN      XWSFN <> 0
N02  C                   EVAL      SORTFN   = ', "asSorting": [ "asc", "desc" ]'
N02  C                   WHEN      WSFNIN <> *BLANKS
N02  C                   EVAL      SORTFIN  = ', "asSorting": [ "asc", "desc" ]'
N02  C                   WHEN      XWSKNT  <> 0
N02  C                   EVAL      SORTKNT  = ', "asSorting": [ "asc", "desc" ]'
N02  C                   OTHER
N02  C                   EVAL      SORTSAKN = ', "asSorting": [ "desc", "asc" ]'
N02  C                   ENDSL
     C                   exsr      SetTop
     C                   callp     wrtsection('stdtop')
     C                   callp     wrtsection('top')
      *
     C                   SELECT
     C                   WHEN      XWSKNA  <> 0
     C     ISOFL14K      SETLL     ISOFL14
     C                   WHEN      XWSSAKN <> 0
     C     ISOFL8K       SETLL     ISOFL8
     C                   WHEN      XWSAVD  <> 0
     C     ISOFL1K       SETLL     ISOFL1
     C                   WHEN      ((XWSTN <> 0) AND (WSOIT <> 'Y'))
     C     ISOFL7K       SETLL     ISOFL7
     C                   WHEN      ((XWSTN <> 0) AND (WSOIT = 'Y'))
     C     ISOFL11K      SETLL     ISOFL11
     C                   WHEN      XWSFN <> 0
     C     XWSFN         SETLL     ISOFL12
     C                   WHEN      WSFNIN <> *BLANKS
     C     WSFNIN        SETLL     ISOFL13
     C                   WHEN      XWSKNT  <> 0
     C     ISOFL3K       SETLL     ISOFL3
     C                   OTHER
     C     *HIVAL        SETLL     ISOFL9
     C                   ENDSL
     C                   Move      *zeros        AntSnd
     C                   Move      *zeros        AntRcd
     C     MaxSnd        IFEQ      *ZEROS
     C                   Z-add     1000          MaxSnd
     C                   END
     C     MaxRcd        IFEQ      *ZEROS
     C                   Z-add     5000          MaxRcd
     C                   END
      *
     C     STHENTOPD     TAG
     C                   SELECT
     C                   WHEN      XWSKNA  <> 0
     C     XWSKNA        READE     ISOFL14                                50
     C                   WHEN      XWSSAKN <> 0
     C                   READ      ISOFL8                                 50
     C                   WHEN      XWSAVD  <> 0
     C     ISOFL1K       READE     ISOFL1                                 50
     C                   WHEN      ((XWSTN <> 0) AND (WSOIT <> 'Y'))
     C     XWSTN         READE     ISOFL7                                 50
     C                   WHEN      ((XWSTN <> 0) AND (WSOIT = 'Y'))
     C     XWSTN         READE     ISOFL11                                50
     C                   WHEN      XWSFN <> 0
     C                   READ      ISOFL12                                50
     C                   WHEN      WSFNIN <> *BLANKS
     C                   READ      ISOFL13                                50
     C                   WHEN      XWSKNT  <> 0
     C     XWSKNT        READE     ISOFL3                                 50
     C                   OTHER
     C                   READP     ISOFL9                                 50
     C                   ENDSL
      *
     C  N50              IF        ISSLUT = ' '
      *
     C                   Add       1             AntRcd
     C     AntRcd        CabGe     MaxRcd        SLHENTOPD
     C                   MOVE      *OFF          *IN33
     C                   IF        WSPT <> '*ALL'
     C     WSPT          COMP      ISFPFO                             3333
     C                   END
     C  N33              IF        WSAA <> '*ALL'
     C     WSAA          COMP      ISFAVA                             3333
     C                   END
     C  N33              IF        WSAP <> '*ALL'
     C     WSAP          COMP      ISFANS                             3333
     C                   END
     C  N33              IF        WSST <> '*ALL'
     C     WSST          COMP      ISST                               3333
     C                   END
     C  N33              IF        WSUSRS <> '*ALL'
     C     WSUSRS        COMP      ISUSRS                             3333
     C                   END
     C     *IN33         CABEQ     *ON           STHENTOPD
     C                   IF        ((XAUT = 'L') OR (WSUSER = ISUSRS))
     C     ISST          CHAIN     ISOKDS                             33
     C  N33              IF        SSLUTT <> 'J'
     C                   EXSR      SETROW
     C                   callp     wrtsection('row')
     C                   Add       1             AntSnd
     C     AntSnd        CabGe     MaxSnd        SLHENTOPD
     C                   END
     C                   END
      *
S05  C*                  GOTO      STHENTOPD
S05  C*                  END
N05  C                   END
N05  C  N50              GOTO      STHENTOPD
     C     SLHENTOPD     TAG
      *
     C                   callp     updhtmlvar('TOTFORV':
     C                             %trim(%editc(TOTFORV:'3')))
     C                   callp     updhtmlvar('TOTRES':
     C                             %trim(%editc(TOTRES:'3')))
     C                   callp     updhtmlvar('TOTSAK':
     C                             %trim(%editc(TOTSAK:'3')))
     C                   callp     wrtsection('tableend')
     C                   callp     updHTMLvar('ANTSND':
     C                             %trim(%editc(ANTSND:'3')))
     C                   callp     updHTMLvar('MaxSnd':
     C                             %trim(%editc(MaxSnd:'3')))
     C                   callp     updHTMLvar('ANTRCD':
     C                             %trim(%editc(ANTRCD:'3')))
     C                   callp     updHTMLvar('MaxRcd':
     C                             %trim(%editc(MaxRcd:'3')))
     C                   callp     updhtmlvar('runtime':
     C                             %trim(%editw(sec:'     0 .   ')))
      *
     C                   callp     wrtsection('bot')
     C                   callp     wrtsection('stdbot')
     C                   callp     wrtsection('*fini')
     C     SLUTT         tag
     C                   EXSR      EXIT
     C                   return
      *
      *=====================================================================******
      * Parse input
      *=====================================================================******
     C     Parse         begsr
     C                   eval      lng     = zhbgetvar('lng')
     C                   eval      BckGnd  = zhbgetvar('bckgnd')
     C                   eval      lstbrd  = zhbgetvar('lstbrd')
     C                   EVAL      WSUSER  = zhbgetvar('USER')
     C                   EVAL      WSFORST = zhbgetvar('WSFORST')
     C                   EVAL      UsrSpcName  = zhbgetvar('UsrSpcName')
      *
     C                   EVAL      WSKNA   = zhbgetvar('WSKNA')
     C                   EVAL      WSDTF   = zhbgetvar('WSDTF')
     C                   EVAL      WSSAKN  = zhbgetvar('WSSAKN')
     C                   EVAL      WSAVD   = zhbgetvar('WSAVD')
     C                   EVAL      WSOPD   = zhbgetvar('WSOPD')
     C                   EVAL      WSFN    = zhbgetvar('WSFN')
     C                   EVAL      WSFNIN  = zhbgetvar('WSFNIN')
     C                   EVAL      WSTN    = zhbgetvar('WSTN')
     C                   EVAL      WSKNT   = zhbgetvar('WSKNT')
     C                   EVAL      WSOIT   = zhbgetvar('WSOIT')
     C                   EVAL      WSPT    = zhbgetvar('WSPT')
     C                   EVAL      WSAA    = zhbgetvar('WSAA')
     C                   EVAL      WSAP    = zhbgetvar('WSAP')
     C                   EVAL      WSST    = zhbgetvar('WSST')
     C                   EVAL      WSUSRS  = zhbgetvar('WSUSRS')
      *
     C                   EVAL      XWSKNA  = c2n2(WSKNA)
     C                   EVAL      XWSDTF  = c2n2(WSDTF)
     C                   EVAL      XWSSAKN = c2n2(WSSAKN)
     C                   EVAL      XWSAVD  = c2n2(WSAVD)
     C                   EVAL      XWSOPD  = c2n2(WSOPD)
     C                   EVAL      XWSFN   = c2n2(WSFN)
     C                   EVAL      XWSTN   = c2n2(WSTN )
     C                   EVAL      XWSKNT  = c2n2(WSKNT)
      *
     C                   endsr
      *
      *=====================================================================******
      *  Set output variables for section /$top
      *  (search criteria)
      *=====================================================================******
     C     SetTop        begsr
OddEvC                   callp     updHTMLvar('LogoImg':LogoImg)
      * KUNDE
     C                   callp     updhtmlvar('TITLE':'ARBEID MED ISO-SAK')
     C                   callp     updhtmlvar('WSNA':BIBESK)
     C                   callp     updhtmlvar('USER':WSUSER)
     C                   callp     updHtmlVar('UsrSpcName':UsrSpcName)
     C                   callp     updhtmlvar('WSKN':
     C                             %trim(%editc(BIKUNR:'Z')))
     C                   callp     updHTMLvar('sdato':
     C                             %trim(%editw(BIDTV:'    .  .  ')))
     C                   callp     updHTMLvar('stid':
     C                             %trim(%editw(BITIDV:'  .  .  ')))
N02   *
N02  C                   callp     updHTMLvar('SORTKNA':SORTKNA)
N02  C                   callp     updHTMLvar('SORTSAKN':SORTSAKN)
N02  C                   callp     updHTMLvar('SORTOPD':SORTOPD)
N02  C                   callp     updHTMLvar('SORTTN':SORTTN)
N02  C                   callp     updHTMLvar('SORTFN':SORTFN)
N02  C                   callp     updHTMLvar('SORTFIN':SORTFIN)
N02  C                   callp     updHTMLvar('SORTKNT':SORTKNT)
      *
     C                   callp     updHTMLvar('WSKNA':WSKNA)
     C                   callp     updHTMLvar('WSDTF':WSDTF)
     C                   callp     updHTMLvar('WSSAKN':WSSAKN)
     C                   callp     updHTMLvar('WSAVD':WSAVD)
     C                   callp     updHTMLvar('WSOPD':WSOPD)
     C                   callp     updHTMLvar('WSFN':WSFN)
     C                   callp     updHTMLvar('WSFNIN':WSFNIN)
     C                   callp     updHTMLvar('WSTN':WSTN)
     C                   callp     updHTMLvar('WSKNT':WSKNT)
     C                   callp     updHTMLvar('WSOIT':WSOIT)
     C                   callp     updHTMLvar('WSPT':WSPT)
     C                   callp     updHTMLvar('WSAA':WSAA)
     C                   callp     updHTMLvar('WSAP':WSAP)
     C                   callp     updHTMLvar('WSST':WSST)
     C                   callp     updHTMLvar('WSUSRS':WSUSRS)
     C                   callp     updHTMLvar('WSFORST':WSFORST)
      *
     C     KOFASTK1      CHAIN     KOFAST                             33
     C                   IF        *IN33
     C                   EVAL      WSLEDET1 = 'posttype'
     C                   ELSE
     C                   EVAL      WSLEDET1 = KFTXT
     C                   END
     C                   callp     updHTMLvar('WSLEDET1':WSLEDET1)
     C     KOFASTK2      CHAIN     KOFAST                             33
     C                   IF        *IN33
     C                   EVAL      WSLEDET2 = 'avvikets art'
     C                   ELSE
     C                   EVAL      WSLEDET2 = KFTXT
     C                   END
     C                   callp     updHTMLvar('WSLEDET2':WSLEDET2)
     C     KOFASTK3      CHAIN     KOFAST                             33
     C                   IF        *IN33
     C                   EVAL      WSLEDET3 = 'ansvarlig part'
     C                   ELSE
     C                   EVAL      WSLEDET3 = KFTXT
     C                   END
     C                   callp     updHTMLvar('WSLEDET3':WSLEDET3)
      *
     C                   endsr
      *
      *=====================================================================******
      *  Set output variables for section /$tablerow (table row)
      *=====================================================================******
     C     SetRow        begsr
     C                   callp     updhtmlvar('ISSAKN':
     C                             %trim(%editc(ISSAKN:'Z')))
     C                   callp     updhtmlvar('ISFTUN':
     C                             %trim(%editc(ISFTUN:'Z')))
     C                   callp     updhtmlvar('ISFAVD':
     C                             %trim(%editc(ISFAVD:'Z')))
     C                   callp     updhtmlvar('ISFOPD':
     C                             %trim(%editc(ISFOPD:'Z')))
     C                   callp     updhtmlvar('ISFTRF':
     C                             %trim(%editc(ISFTRF:'Z')))
     C                   callp     updhtmlvar('ISFBIL':ISFBIL)
     C                   callp     updhtmlvar('ISFADA':
     C                             %trim(%editw(ISFADA:'    .  .  ')))
     C                   callp     updhtmlvar('ISFAND':
     C                             %trim(%editw(ISFAND:'    .  .  ')))
N02  C     CUNDL01K      CHAIN     CUNDL01                            33
N02  C   33              EVAL      KNAVN = *BLANKS
N02  C                   callp     updhtmlvar('WSNAA':KNAVN)
     C                   callp     updhtmlvar('TTEXT':ISFPFO)
     C     ISFPFO        CHAIN     ISOKDT                             33
     C                   IF        (*IN33 OR (TFARGE = *BLANKS))
     C                   callp     updhtmlvar('PTFARGE':'')
     C                   ELSE
     C                   EVAL      XFARGE = 'BGCOLOR="#'
     C                   CAT       TFARGE:0      XFARGE           30
     C                   CAT       '"':0         XFARGE
     C                   callp     updhtmlvar('PTFARGE':XFARGE)
     C                   ENDIF
     C                   callp     updhtmlvar('ATEXT':ISFAVA)
N02   *
N02  C  N33              IF        TANTDG > 0
N02  C     XIDAG         SETLL     KALENDER
N02  C     1             DO        TANTDG
N02  C                   READP     KALENDER
N02  C                   ENDDO
N02  C                   IF        ISSDT >= KADATE
N02  C                   EVAL      *IN33 = *ON
N02  C                   END
N02  C                   ELSE
N02  C                   EVAL      *IN33 = *ON
N02  C                   END
N02  C                   IF        *IN33 OR TADFAR = *BLANKS
N02  C                   callp     updhtmlvar('ADFARGE':'')
N02  C                   ELSE
N02  C                   EVAL      XFARGE = 'BGCOLOR="#'
N02  C                   CAT       TADFAR:0      XFARGE           30
N02  C                   CAT       '"':0         XFARGE
N02  C                   callp     updhtmlvar('ADFARGE':XFARGE)
N02  C                   ENDIF
N02   *
     C     ISOKDAK1      CHAIN     ISOKDA                             33
     C   33ISOKDAK2      CHAIN     ISOKDA                             33
     C                   IF        (*IN33 OR (AFARGE = *BLANKS))
     C                   callp     updhtmlvar('AVFARGE':'')
     C                   ELSE
     C                   EVAL      XFARGE = 'BGCOLOR="#'
     C                   CAT       AFARGE:0      XFARGE           30
     C                   CAT       '"':0         XFARGE
     C                   callp     updhtmlvar('AVFARGE':XFARGE)
     C                   ENDIF
     C                   callp     updhtmlvar('PTEXT':ISFANS)
     C     ISFANS        CHAIN     ISOKDP                             33
     C                   IF        (*IN33 OR (PFARGE = *BLANKS))
     C                   callp     updhtmlvar('ANFARGE':'')
     C                   ELSE
     C                   EVAL      XFARGE = 'BGCOLOR="#'
     C                   CAT       PFARGE:0      XFARGE           30
     C                   CAT       '"':0         XFARGE
     C                   callp     updhtmlvar('ANFARGE':XFARGE)
     C                   ENDIF
     C                   callp     updhtmlvar('ISFAV1':ISFAV1)
     C                   callp     updhtmlvar('ISFORV':
     C                             %trim(%editc(ISFORV:'L')))
     C                   EVAL      WSRES = ISFKOS + ISINT
     C                   EVAL      TOTFORV = TOTFORV + ISFORV
     C                   EVAL      TOTRES  = TOTRES  + WSRES
     C                   EVAL      TOTSAK  = TOTSAK  + 1
     C                   callp     updhtmlvar('WSRES':
     C                             %trim(%editc(WSRES:'L')))
     C                   callp     updhtmlvar('ISUSRS':ISUSRS)
     C                   callp     updhtmlvar('ISFRDA':
     C                             %trim(%editw(ISFRDA:'    .  .  ')))
     C                   callp     updhtmlvar('ISSDT':
     C                             %trim(%editw(ISSDT:'    .  .  ')))
     C                   callp     updhtmlvar('ISST':ISST)
      *
     C                   endsr
      *
      *=====================================================================******
      * Set html output skeleton member before its read into memory
      *=====================================================================******
     C     LoadHtml      begsr
cccccC                   eval      IfsMultIndicators = gethtmlifsmult(
cccccC                             '/syspeddev/html/Header/eSpedTop.html -
cccccC                             /syspeddev/html/EISO11.html -
cccccC                             /syspeddev/html/Footer/eSpedBot.html':
cccccC                             '/CGITAG/')
      *
     C                   endsr
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
     C                   OPEN      BRIDF
     C     WSUSER        CHAIN(N)  BRIDF                              50
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
      *
     C                   EXSR      TSTUSER
      *
      * hent Parametre som går igjen i mange prog:
      *      Odd/Even/Rowhead-farger,Logobilde,Språk,Intern/Ekstern bruker,  mm
     c                   call      'ESGETPAR'
     c                   parm                    BIBRID
     c                   parm                    BIFIRM
     c                   parm                    BIKUNR
     c                   parm                    BIKNG
     c                   parm                    RowHead          10
     c                   parm                    Odd              10
     c                   parm                    Even             10
     c                   parm                    LogoImg          50
     c                   parm                    XSPRÅK            2
     c                   parm                    XINTERN           1
     c                   parm                    ParmDS1        1024
     c                   parm                    ParmDS2        1024
     c                   parm                    ParmDS3        1024
      *
     C                   open      BRPARF                               50
N02  C                   open      CUNDL01                              50
N02  C                   open      KALENDER                             50
     C                   open      ISOFL9                               50
     C                   open      ISOFL1                               50
     C                   open      ISOFL3                               50
     C                   open      ISOFL7                               50
     C                   open      ISOFL8                               50
     C                   open      ISOFL14                              50
     C                   open      ISOFL11                              50
     C                   open      ISOFL12                              50
     C                   open      ISOFL13                              50
     C                   open      ISOKDT                               50
     C                   open      ISOKDA                               50
     C                   open      ISOKDP                               50
     C                   open      ISOKDS                               50
     C                   OPEN      KOFAST
      *
     C     KOFASTK1      klist
     C                   kfld                    KFTYP
     C                   kfld                    X1KFKOD           5
     C     KOFASTK2      klist
     C                   kfld                    KFTYP
     C                   kfld                    X2KFKOD           5
     C     KOFASTK3      klist
     C                   kfld                    KFTYP
     C                   kfld                    X3KFKOD           5
     C     ISOFL1K       klist
     C                   kfld                    XWSAVD
     C                   kfld                    XWSOPD
     C     ISOFL3K       klist
     C                   kfld                    XWSKNT
     C     ISOFL7K       klist
     C                   kfld                    XWSTN
     C     ISOFL8K       klist
     C                   kfld                    XWSSAKN
     C     ISOFL14K      klist
     C                   kfld                    XWSKNA
     C                   kfld                    XWSDTF
     C     ISOFL11K      klist
     C                   kfld                    XWSTN
     C     ISOFL12K      klist
     C                   kfld                    XWSFN
     C     ISOKDAK1      klist
     C                   kfld                    ISFPFO
     C                   kfld                    ISFAVA
     C     ISOKDAK2      klist
     C                   kfld                    X_ISFPFO          5
     C                   kfld                    ISFAVA
N02  C     CUNDL01K      klist
N02  C                   kfld                    BIFIRM
N02  C                   kfld                    ISKNA
     C     BRPARFK       klist
     C                   kfld                    PABRID
     C                   kfld                    PAKUNR
     C                   kfld                    PAID
      *
     C                   EVAL      KFTYP   = 'AVLETEKST'
     C                   EVAL      X1KFKOD = 'ISTYP'
     C                   EVAL      X2KFKOD = 'ISAVA'
     C                   EVAL      X3KFKOD = 'ISANS'
     C                   MOVE      99999999      XADA              8 0
     C                   EVAL      PABRID = WSUSER
     C                   MOVEL     'SPRÅK'       PAID
     C     BRPARFK       chain     BRPARF                             33
     C                   IF        (*IN33 OR (PAVRDA <> 'EN'))
     C                   MOVE      *BLANKS       XSPRÅK            2
     C                   ELSE
     C                   MOVE      'EN'          XSPRÅK            2
     C                   END
     C                   MOVEL     'AUTOR'       PAID
     C     BRPARFK       chain     BRPARF                             33
     C                   IF        *IN33
     C                   MOVE      'N'           XAUT              1
     C                   ELSE
     C                   MOVEL     PAVRDA        XAUT
     C                   END
N02   *
N02  C                   CALL      'SYGE15C'
N02  C                   PARM                    WDT               8
N02  C                   PARM                    WTM               6
N02  C                   MOVE      WDT           XIDAG             8 0
      *
     C                   endsr
      *
      *////////////////////////////////////////////////////////////
     C     TSTUSER       BEGSR
      *////////////////////////////////////////////////////////////
      /copy syspeds/qrpgsrcpy,tstuser
      *
     C                   endsr
      *
      *=====================================================================******
      *  AVSLUTT
      *=====================================================================******
     C     EXIT          begsr
     C                   close     BRPARF
     C                   close     BRIDF
N02  C                   close     CUNDL01
N02  C                   close     KALENDER
     C                   close     ISOFL9
     C                   close     ISOFL1
     C                   close     ISOFL3
     C                   close     ISOFL7
     C                   close     ISOFL8
     C                   close     ISOFL14
     C                   close     ISOFL11
     C                   close     ISOFL12
     C                   close     ISOFL13
     C                   close     ISOKDT
     C                   close     ISOKDA
     C                   close     ISOKDP
     C                   close     ISOKDS
     C                   close     KOFAST
     C                   eval      *inlr = *on
      *
     C                   endsr
      *
