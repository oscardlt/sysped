      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/TJGE003 MODULE(*LIBL/TJGE003 *LIBL/INCGI)
CRTPGM*        ACTGRP(*NEW) AUT(*USE)
      *
      *********************************************************************
      /copy SYSPEDS/qrpgsrcpy,hspecs
      /copy SYSPEDS/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     UF   E           K DISK    USROPN
     FTRAN      IF   E           K DISK    USROPN
     FTRANL01   IF   E           K DISK    USROPN
     F                                     RENAME(TRANR:TRANR1)
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy SYSPEDS/qrpgsrcpy,prototypeb
      /copy SYSPEDS/qrpgsrcpy,usec
      /copy SYSPEDS/qrpgsrcpy,variables3
      *
      * Varible for
     D WSUSER          S             10
     D WSONAVN         S             10
     D WTNRL           S              8
     D WTNRLN          S              8  0                                      Liste fra og med
     D WTNR            S              8
     D WTNRN           S              8  0                                      Kun 1
     D STOPP           S             10
     D JSONstring      s          32000
     D Error           S            150
     D AntLest         S              9  0

      *get input-string
      /copy syspeds/qrpgsrcpy,prolog3

      * Parse input
     C                   exsr      Parse
      * Open files etc
     C                   EXSR      INIT
      *
     c                   callp     gethtmlifs(
     C                             '/syspeddev/html/JSON.html':
     C                             '/CGITAG/')
     C                   callp     wrtsection('top')
      *
      * ............................................................................................
      * skriv JSON-Object start..(alltid '{' + x ant param. m/komma mellom (IKKE komma etter siste))
      * ............................................................................................
      *
      /free
        JSONstring='{'
        +'¬user¬ : ¬'+%trim(WSUSER)+'¬, '
        +'¬sonavn¬ : ¬'+%trim(WSONAVN)+'¬, '
        +'¬tnrL¬ : ¬'+%trim(WTNRL)+'¬, '
        +'¬tnr¬ : ¬'+%trim(WTNR)+'¬, '
        +'¬errMsg¬ : ¬'+%trim(Error)+'¬, ';
      /end-free
      * JSONfix escaper " i tekst,  for deretter å bytte ¬->"
     c                   call      'JSONFIX'
     c                   parm                    JSONstring
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      *
      * ............................................................................................
      * Skriv JSON-LISTE Start (en liste er EN parameter(fra [ til   )
      * ............................................................................................
     c                   eval      JSONstring ='"transporterlist" : ['
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      *
      * Hæmta kund/kunder
      *
     c     Error         ifeq      *blanks
     C                   select
     C                   when      WTNRN> 0                                     KUN 1 - Oppslag
     C     KEY1          SETLL     TRAN
     C     KEY1          READE     TRAN                                   55
     C                   when      WTNRLN > 0                                   Liste på nr
     C     KEY           SETLL     TRAN
     C     BIFIRM        READE     TRAN                                   55
     C                   when      WSONAVN > *blanks                            Liste på navn
     C     KEYA          SETLL     TRANL01
     C     BIFIRM        READE     TRANL01                                55
N01  C  N55VMALFA        CABGT     STOPP         UTLES
     C                   endsl
     C     *IN55         DOWEQ     *OFF
     C                   Add       1             AntLest
     c                   move      VMINCR        TrType            1
      * ............................................................................................
      * Skriv en JSON-Array-linje pr menypunkt - komma før hvert nytt element
      * ............................................................................................
      /free
       if AntLest=1;
          JSONstring=*blanks;
          else;
          JSONstring=', ';
          endif;
       JSONstring=%trim(JSONstring)+'{'
          +'¬tranNr¬ : ¬'+%trim(%editc(VMTRAN:'3'))+'¬, '
          +'¬tnavn¬ : ¬'+%trim(VMNAVN)+'¬, '
          +'¬tranType¬ : ¬'+%trim(TRTYPE)+'¬, '
          +'¬kundNr¬ : ¬'+%trim(%editc(VMTRKU:'3'))+'¬, '
          +'¬leveNr¬ : ¬'+%trim(%editc(VMTRLE:'3'))+'¬}';
      /end-free
     C                   call      'JSONFIX'
     C                   parm                    JSONstring
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      *
     C                   select
     C                   when      WTNRN > 0
     C     KEY1          READE     TRAN                                   55
     C                   when      WTNRLN> 0
     c     AntLest       cabge     50            Utles
     C     BIFIRM        READE     TRAN                                   55
     C                   when      WSONAVN > *blanks
     C     BIFIRM        READE     TRANL01                                55
N01  C  N55VMALFA        CABGT     STOPP         UTLES
     C                   endsl
     C                   ENDDO
     C     UTLES         TAG
     c                   endif
      * ............................................................................................
      * Skriv JSON-Liste/Array  Avslutning
      * ............................................................................................
     c                   eval      JSONstring =']'
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      * ............................................................................................
      * Skriv JSON-string Avsluttning
      * ............................................................................................
     c                   eval      JSONstring ='}'
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      *
      * Quit
     C                   exsr      Exit


      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Lukk fil
     C                   CLOSE     BRIDF
     C  N50              CLOSE     TRAN
     C  N50              CLOSE     TRANL01

      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   MOVE      *ON           *INLR
     C                   return
     C                   endsr
      *
      *********************************************************
     C     Parse         Begsr
      *********************************************************
      * Get input
     C                   EVAL      WSUSER  = zhbgetvar('USER')
     C                   EVAL      WSONAVN = zhbgetvar('SONAVN')
     C                   EVAL      WTNRL= zhbgetvar('TNRL')
     C                   EVAL      WTNRLN = c2n2(WTNRL)
     C                   EVAL      WTNR = zhbgetvar('TNR')
     C                   EVAL      WTNRN = c2n2(WTNR)
      *
     c                   eval      STOPP = %trim(WSONAVN)+'9999999999'
     C
     c                   endsr
      *
      *********************************************************
     C     INIT          Begsr
      *********************************************************
     C                   OPEN      BRIDF
      * Test innlogging
     C     WSUSER        CHAIN     BRIDF                              50
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C                   call      BILIBL
     C                   end
      *
     C     KEYA          KLIST
     C                   KFLD                    BIFIRM
     C                   KFLD                    WSONAVN
     C     KEY           KLIST                                                  Liste
     C                   KFLD                    BIFIRM
     C                   KFLD                    WTNRLN
     C     KEY1          KLIST                                                  KUN 1
     C                   KFLD                    BIFIRM
     C                   KFLD                    WTNRN
      *
     C   50              eval      Error = 'Invalid userID'
     C  N50              OPEN      TRAN
     C  N50              OPEN      TRANL01

     c                   endsr
      *
