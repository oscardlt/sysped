      *
      ***********************************************************
      *** PROGRAM SKAL VEDLIKEHOLDE/SPØRRE SAMMENSLÅINGSKODER ***
      ***********************************************************
      *
     FKODSAM    UF A E           K DISK
     FSYFA06FM  CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     C*
     C                   EXSR      INIT
     C*
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     *LOVAL        SETLL     KODSAM
     C     START1        TAG
     C                   READ(N)   KODSAM                                 94
     C  N94              EXSR      MOVFS
      *
     C     START2        TAG
      *
     C  N94              DO
     C                   DO        14
     C                   ADD       1             ZRECNO
     C                   MULT      -1            COLOR                50
     C                   WRITE     SUBFIL
     C                   READ(N)   KODSAM                                 94
     C  N94              EXSR      MOVFS
     C  N94              END
     C                   END
     C                   Z-ADD     ZRECNO        YRECNO            5 0
     C                   Z-ADD     8             LINNBR
     C                   Z-ADD     3             POSNBR
     C*
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C*
     C* KOMMER HIT VED TOM FIL
     C*
     C     ZRECNO        IFEQ      0
     C     MODE          IFEQ      'E'
     C                   GOTO      SLUTT
     C                   END
     C                   SETOFF                                       94
     C                   ADD       1             ZRECNO
     C                   MULT      -1            COLOR                50
     C                   WRITE     SUBFIL
     C                   SETON                                        94
     C                   WRITE     SUBCTL
     C                   MOVE      'N'           MODE
     C                   EXSR      NY
     C                   GOTO      START
     C                   ELSE
     C                   WRITE     SUBCTL
     C     STBUNN        TAG
     C     MODE          IFEQ      'N'
     C                   EXSR      NY
     C                   GOTO      START
     C                   ELSE
     C                   EXFMT     BUNN
     C                   END
     C                   END
     C*
     C***
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C***
     C*
     C                   SETOFF                                       999830
     C*** AVSLUTT
     C   03              GOTO      SLUTT
     C*** AVSLUTT
     C   12              GOTO      SLUTT
     C*
     C*** FRISK OPP
     C*
     C   05              EXSR      MOVB
     C   05              GOTO      START
     C*
     C*** LAGE
     C*
     C   06              DO
     C                   MOVE      'N'           MODE              1
     C                   EXSR      NY
     C                   GOTO      START
     C                   END
     C*
     C*** SØK
     C*
     C   08              GOTO      START6
     C*
     C*** BLA VIDERE
     C   25              DO
     C                   Z-ADD     YRECNO        ZRECNO
     C                   READP(N)  KODSAM                                 94
     C                   READ(N)   KODSAM                                 94
     C  N94              EXSR      MOVFS
     C                   SETOFF                                       25
     C                   GOTO      START2
     C                   END
     C*
     C   26              DO
     C     ZRECNO        IFGT      7
     C                   SUB       7             ZRECNO
     C                   END
     C                   GOTO      STBUNN
     C                   END
     C*
     C     ZRECNO        IFNE      0
     C                   EXSR      HENT
     C   27SFNR          IFEQ      '1'
     C                   GOTO      SLUTT
     C                   END
     C                   END
     C                   GOTO      START3
     C*
     C*** SØK FRA AVDELING
     C     START6        TAG
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     7             POSNBR
     C                   EXFMT     BUNNSØ
     C                   Z-ADD     8             LINNBR
     C                   Z-ADD     7             POSNBR
     C   12              GOTO      STBUNN
     C                   GOTO      START
     C*
     C*
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
     C*
     C***********************************************
     C     HENT          BEGSR
     C***********************************************
     C                   SETOFF                                       98
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C     *IN98         IFEQ      '0'
     C  N27SFNR          IFEQ      '2'
     C                   EXSR      ENDRE
     C                   MOVE      ' '           SFNR
     C                   UPDATE    SUBFIL
     C                   END
     C  N27SFNR          IFEQ      '4'
     C                   EXSR      SLETT
     C                   MOVE      *BLANKS       WSKODE
     C                   MOVE      *BLANKS       WSNORS
     C                   MOVE      ' '           SFNR
     C  N12              MOVEL     '*DELETE '    WSNORS
     C  N12              SETON                                        51
     C                   UPDATE    SUBFIL
     C  N12              SETOFF                                       51
     C                   END
     C   27SFNR          IFEQ      '1'
     C                   MOVE      WSKODE        UKODE
     C                   GOTO      SLHENT
     C                   END
     C                   END
     C                   END
     C*
     C     SLHENT        TAG
     C                   Z-ADD     8             LINNBR
     C                   Z-ADD     3             POSNBR
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVB          BEGSR
     C***********************************************
     C*
     C* FLYTTER BLANK TIL SUBFIL
     C*
     C                   MOVE      *BLANKS       WSKODE
     C                   MOVE      *BLANKS       WSNORS
     C                   MOVE      *BLANKS       WSENGE
     C                   MOVE      *BLANKS       WSTYSK
     C*
     C                   ENDSR
     C*
     C*
     C***********************************************
     C     MOVFS         BEGSR
     C***********************************************
     C*
     C* FLYTTER FRA FYSISK FIL TIL SUBFIL
     C*
     C                   MOVE      SAKODE        WSKODE
     C                   MOVE      SANORS        WSNORS
     C                   MOVE      SAENGE        WSENGE
     C                   MOVE      SATYSK        WSTYSK
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVSF         BEGSR
     C***********************************************
     C*
     C* FLYTTER FRA SUBFIL TIL FYSIKS
     C*
     C                   MOVE      WSKODE        SAKODE
     C                   MOVE      WSNORS        SANORS
     C                   MOVE      WSENGE        SAENGE
     C                   MOVE      WSTYSK        SATYSK
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     ENDRE         BEGSR
     C***********************************************
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     12            POSNBR
     C*
     C     STEND         TAG
     C                   EXFMT     BUNNE
     C                   SETOFF                                       30
     C  N12              DO
     C     WSNORS        IFEQ      *BLANKS
     C     WSENGE        OREQ      *BLANKS
     C     WSTYSK        OREQ      *BLANKS
     C                   MOVE      'SHU0306'     MSGNR
     C                   SETON                                        30
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     12            POSNBR
     C                   GOTO      STEND
     C                   END
     C     WSKODE        CHAIN     KODSAM                             33
     C                   EXSR      MOVSF
     C  N33              UPDATE    KODSAMR
     C                   END
     C                   ENDSR
     C***********************************************
     C     SLETT         BEGSR
     C***********************************************
     C                   EXFMT     BUNNS
     C  N12              DO
     C     WSKODE        CHAIN     KODSAM                             33
     C  N33              DELETE    KODSAM
     C                   END
     C                   ENDSR
     C***********************************************
     C     NY            BEGSR
     C***********************************************
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     8             POSNBR
     C                   EXSR      MOVB
     C     STNY          TAG
     C                   EXFMT     BUNNN
     C                   SETOFF                                       30
     C  N12              DO
     C     WSKODE        IFEQ      *BLANKS
     C                   MOVE      'SHU0307'     MSGNR
     C                   SETON                                        30
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     8             POSNBR
     C                   GOTO      STNY
     C                   END
     C     WSNORS        IFEQ      *BLANKS
     C     WSENGE        OREQ      *BLANKS
     C     WSTYSK        OREQ      *BLANKS
     C                   MOVE      'SHU0306'     MSGNR
     C                   SETON                                        30
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     12            POSNBR
     C                   GOTO      STNY
     C                   END
     C     WSKODE        CHAIN     KODSAM                             33
     C   33              EXSR      MOVSF
     C   33              WRITE     KODSAMR
     C                   END
     C   12              MOVE      'E'           MODE
     C                   ENDSR
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     *ENTRY        PLIST
     C                   PARM                    UPLUKK            1
     C                   PARM                    UKODE             1
     C                   Z-ADD     1             COLOR             1 0
     C*
     C     UPLUKK        COMP      'J'                                    27
     C*
     C*
     C                   ENDSR
