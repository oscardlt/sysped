      *********************************************************************
CRTPG+*  CRTPGM SYSPED/ESARKSC3 MODULE(*LIBL/ESARKSC3 *LIBL/INCGI)
CRTPGM*         ACTGRP(CGI) AUT(*USE)
      *********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 JOV PTF10 Div forbedr ved visning /kontroll av type mm
10XXX * 02 CB  PTF10 Monitor av externe kommandoer
10XXX * 03 JOV PTF10 Slett via FLytt til mappe droppet / for tungvint
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd

     FBRIDF     IF   E           K DISK    USROPN
     FBRPARF    IF   E           K DISK    USROPN
     FCUNDL01   IF   E           K DISK    USROPN
      *_____________________________
      * Variables common to all CGIs
      *"""""""""""""""""""""""""""""
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      /copy syspeds/qrpgsrcpy,variables3
      *_______________________
      * Client input variables
      *"""""""""""""""""""""""
     D WUSER           S             10
     D ORG             S            100
     D KAT             S            100
     D AVSLUTT         S              1
     D CMDLINE         S            512
     D CMDLEN          S             15  5
      /copy syspeds/qrpgsrcpy,prolog3

      * Get program start time for calculating execution time
     C                   TIME                    timedata1
      * Parse input / cvt to numeric
     C                   EXSR      PARSE
      * File open / keys
     C                   EXSR      INIT
      * Read output skeleton html member into memory
     C                   EXSR      LOADHTML
      *
     C                   EXSR      SLETT
      * Set section variables
     C                   EXSR      SETTOP
     C                   CALLP     wrtsection('top')
      * Compute run time
     C                   TIME                    timedata2
     C     timedata2     SUBDUR    timedata1     ms:*ms
     C                   EVAL      sec = ms / 1000000
     C                   CALLP     updhtmlvar('runtime':
     C                             %trim(%editw(sec:'     0 .   ')))

     C                   EXSR      EXIT

     C                   EVAL      *inlr = *on
     C                   RETURN
      *////////////////////////////////////////////////////////////
      *  Hente variabler                                    PARSE /
      *////////////////////////////////////////////////////////////
     C     PARSE         BEGSR
      * Parse variables from QUERY_STRING environment variable
     C                   EVAL      ORG      = zhbgetvar('ORG')
     C                   EVAL      KAT      = zhbgetvar('KAT')
      * Userid.....
     C                   EVAL      WUSER = zhbgetvar('USER')
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Close output html and quit                          EXIT /
      *////////////////////////////////////////////////////////////
     C     EXIT          BEGSR
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   CALLP     wrtsection('*fini')
      * Quit
     C                   CLOSE     BRIDF
     C                   CLOSE     BRPARF
     C                   CLOSE     CUNDL01

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Read output skeleton html member into memory    LOADHTML /
      *////////////////////////////////////////////////////////////
     C     LOADHTML      BEGSR
     C                   CALLP     gethtml('HTMLSRC':
     C                             '*LIBL':
     C                             'ESARKSC3':'/CGITAG/')
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Set variable data for section /$top               SETTOP /
      *////////////////////////////////////////////////////////////
     C     SETTOP        BEGSR
BODY C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   CALLP     updHTMLvar('USER':WUSER)
     C                   CALLP     updHTMLvar('BESK':BIBESK)
     C                   CALLP     updHtmlVar('KUNDE':
     C                             %trim(%editc(BIKUNR:'Z')))
     C                   CALLP     updHTMLvar('KNAVN':KNAVN)
     C                   CALLP     updHTMLvar('ORG':ORG)
     C                   CALLP     updHTMLvar('KAT':KAT)
     C                   EVAL      AVSLUTT='Y'
     C                   CALLP     updHTMLvar('AVSLUTT':AVSLUTT)
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Slett                                              SLETT /
      *////////////////////////////////////////////////////////////
     C     SLETT         BEGSR
     C     PARKEY        SETLL     BRPARF                                 51
     C                   IF        *IN51
     C     PARKEY        READE     BRPARF                                 52
     C* N52              IF        PAVRDA = KAT
     C*                  EXSR      SRSLETT
     C*                  ELSE
     C*                  EXSR      SRFLYTT
     C*                  END
N03  C  N52              exsr      SRSLETT
     C                   END

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Init                                                INIT /
      *////////////////////////////////////////////////////////////
     C     INIT          BEGSR
     C                   OPEN      BRIDF
     C     WUSER         CHAIN     BRIDF                              50
      * Definiere parameterliste
     C     QCMDEXC       PLIST
     C                   PARM                    CMDLINE
     C                   PARM                    CMDLEN
      * En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        IFEQ      *BLANKS
     C                   CALLB     'INCGI'
     C                   PARM                    BISTDL
     C                   ELSE
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   CALL      BILIBL
     C                   ENDIF

     C                   OPEN      CUNDL01
     C                   OPEN      BRPARF

     C     PARKEY        KLIST
     C                   KFLD                    WUSER
     C                   KFLD                    PAKUNR
     C                   KFLD                    PAID
     C                   EVAL      PAID='ARTSL'
     C     KUNKEY        KLIST
     C                   KFLD                    BIFIRM
     C                   KFLD                    BIKUNR

     C     KUNKEY        CHAIN     CUNDL01                            33

     C                   ENDSR

      *////////////////////////////////////////////////////////////
      *  Flytt til katalog "SLETT"                        SRFLYTT /
      *////////////////////////////////////////////////////////////
     C     SRFLYTT       BEGSR
      * Flytte objekt
s01  C*                  EVAL      CmdLine = 'MOV OBJ(' + X'7D'
s01  C*                            + %trimr(ORG) + X'7D'
s01  C*                            + ') TODIR(' +X'7D'
s01  C*                            + %trimr(PAVRDA) + X'7D' + ')'
s01  C*                  EVAL      CmdLen = %len(%trim(CmdLine))
s01  C*                  CALL      'QCMDEXC'     Qcmdexc
n01  C                   MOVE      'J'           OkMov             1
n01  C                   MOVEl     ORG           FRA             120
n01  C                   MOVEl     PAVRDA        Til             120
N03   * OBS!!! dersom en skal blåse liv i denne igjen må dokumentnavn inn bak PAVRDA i TIL!!!
n01  C                   CALL      'ARCAMOVC'
n01  C                   PARM                    FRA
n01  C                   PARM                    TIL
n01  C                   PARM                    OkMov
n01  c     OKMov         ifeq      'N'
n01  c                   exsr      SRSLETT
n01  c                   end

     C                   ENDSR

      *////////////////////////////////////////////////////////////
      *  Slett fil fra katalog                            SRSLETT /
      *////////////////////////////////////////////////////////////
     C     SRSLETT       BEGSR
      * Flytte objekt
     C                   EVAL      CmdLine = 'RMVLNK OBJLNK(' + X'7D'
     C                             + %trimr(ORG) + X'7D' + ')'
     C                   EVAL      CmdLen = %len(%trim(CmdLine))
S02  C*                  CALL      'QCMDEXC'     Qcmdexc
N02  C                   CALL      'QCMDEXC'     Qcmdexc                98

     C                   ENDSR
