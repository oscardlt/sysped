      *********************************************************************
      *  After compiling this RPG MODULE,
      *  create the related program with the following command:
      *
CRTPG+*  CRTPGM SYSPED/STSTURT1 MODULE(*LIBL/STSTURT1
CRTPGM*         *LIBL/INCGI) ACTGRP(*NEW) AUT(*USE)
      *
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
P10036* 01 JOV PTF10 HOPP UT HVIS UGYLDIG USER
10172 * 02 SH  PTF10 norsk text til Track and trace
********************************************************************
      *
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
      *--------------------------------------------------------------------
      * Data base files
      *--------------------------------------------------------------------
     FBRIDF     IF   E           K DISK    usropn
     FHead14    IF   E           K DISK    usropn
     FBRPARF    IF   E           K DISK    usropn
     FTurer14   IF   E           K DISK    usropn
     FTRACKL02  UF A E           K DISK    usropn
      *--------------------------------------------------------------------
      * Prototype defintions and standard system API error structure
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      *--------------------------------------------------------------------
      * Variables common to CGI programs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
     D OddEven         s             10
     D RowHead         s             10a   inz('#B0E0E6')
     D Odd             s             10a   inz('#F8F8FF')
     D Even            s             10a   inz('#E6E6FA')
      * Variables received from the input string
     D TurNr           s              8
     D TurNrN          s              8  0
     D DAT             s              8
     D DATN            s              8  0
     D KL              s              4
     D KLN             s              4  0
     D WSUSER          s             10
     D TERM            s              5
     D RestOK          s              1
     D FEILMELDING     s             70
     D FEILKODE        s              1
     D LINETXT         s            200
     D BilHeng         s             20
     D HENTOK          s              9  0
     D xxDATE          s              8  0
     D xxTIME          s              6  0
     D xxTIM4          s              4  0
     D xdDATL          s              8  0
     D xdTIML          s              6  0
     D xxTEXT          s             70
     D Merknad         s             70
      * andre variabler
     D Fn              s             10
     D Lib             s             10
     D Mbr             s             10
      *
     D                 DS
     D  XULTID                 1     14  0
     D  XULTM                  1      6  0
     D  XULDD                  7      8  0
     D  XULMM                  9     10  0
     D  XULÅÅ                 11     14  0
     D                 DS
     D  ULÅÅ                   1      4  0
     D  ULMM                   5      6  0
     D  ULDD                   7      8  0
     D  ULDT                   1      8  0
     D                 DS
     D PAVRDA                  1     50
     D  PAVRDA1_5              1      5
     D  PAVRDA7_46             7     46
      * Definere  hex 0D / 25 (CRLF)
     D Hex0D           c                   x'0D'
     D Hex25           c                   x'25'
      *--------------------------------------------------------------------
      * Prolog common to CGI programs
      * -Receive input from the remote browser
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prolog3
      *=====================================================================******
      * MAIN PROCESS LINE
      *=====================================================================******
      * Parse input string
     C                   EXSR      PARSE

     C                   EXSR      INIT
      *
     C                   exsr      SetSkl
      * Read skeleton output html, etc.
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
     C                   exsr      SetTop
      * Html body
     C                   exsr      SetRows
     C                   callp     wrtsection('*fini')
      *
     C                   close     BRIDF
     C                   close     HEAD14
     C                   close     BRPARF
     C                   close     TURER14
     C                   close     TRACKL02
     C                   eval      *inlr = *on
     C                   RETURN
      *
      *
      *=====================================================================******
      * Parse input
      *=====================================================================******
     C     Parse         begsr
     C                   eval      TURNR    = zhbgetvar('TURNR')
     C                   eval      TURNRN   = c2n2(TURNR)
     C                   eval      DAT      = zhbgetvar('DAT')
     C                   eval      DATN     = c2n2(DAT)
     C                   eval      KL       = zhbgetvar('KL')
     C                   eval      KLN      = c2n2(KL)
     C                   eval      wsuser = zhbgetvar('USER')
     C                   eval      TERM   = zhbgetvar('TERM')
     c     Term          ifeq      *blanks
     C                   eval      TERM   = 'IN'
     c                   end
     C                   eval      RESTOK = zhbgetvar('RESTOK')
     C                   endsr
      *=====================================================================******
      * Set html output skeleton member before its read into memory
      *=====================================================================******
     C     SetSkl        begsr
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC'
     C                   eval      Mbr = 'STSTURT1'
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$top
      *=====================================================================******
     C     SetTop        begsr
     C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updhtmlvar('USER':WSUSER)
     C                   callp     updhtmlvar('TERM':TERM)
     C                   callp     updhtmlvar('TERMNAVN':TERMNAVN)
     C                   callp     updhtmlvar('TURNR':TURNR)
     C                   callp     updHTMLvar('DATN':
     c                             %trim(%editc(DATN:'Z')))
     C                   callp     updHTMLvar('KLN':
     c                             %trim(%editc(KLN:'Z')))
     C                   callp     updHTMLvar('DAT':
     c                             %trim(%editw(DATN:'    .  .  ')))
     C                   callp     updHTMLvar('KL':
     c                             %trim(%editw(KLN:'  :  ')))
     C                   callp     updHTMLvar('ROWHEAD':RowHead)
     c     TurNrN        chain     Turer14                            33
     c     *in33         ifeq      *on
     c                   eval      BilHeng = 'UKJENT TUR !!'
     c                   else
     c                   eval      BilHeng = TUBILN
     c     TUHENG        ifne      *blanks
     c                   eval      BilHeng = %trim(BilHeng) +' / '+ TUHENG
     c                   endif
     c                   endif
     C                   callp     updhtmlvar('BilHeng':BilHeng)
     C                   callp     updHTMLvar('TUDT':
     C                             %trim(%editw(TUDT:'    .  .  ')))
      *
     C                   endsr
     C***********************************************
     C     SetRows       BEGSR
     C***********************************************
      *
     C                   callp     wrtsection('RowTop')
     c     TurNrN        cabeq     *zeros        UtTur
      * fyll liste med de ÅPNE sendingene (de som ikke har TE2 på aktuelt sted) fra turen
     C     TurNrN        setll     HEAD14
     C     TurNrN        reade     HEAD14                                 94
     C     *in94         doweq     '0'
     c     OddEven       IFEQ      ODD
     c                   eval      OddEven = EVEN
     c                   else
     c                   eval      OddEven = ODD
     c                   end
     C                   callp     updHTMLvar('ODDEVEN':OddEven)
     C                   if        %subst(hxsndn:1:3)='401'
     C                             and %subst(hxsndn:20:1)<>' '
     c                   eval      hxsndn=%subst(hxsndn:4:17)
     c                   endif
     C                   callp     updHTMLvar('HEKNA':
     c                             %trim(%editc(HEKNA:'Z')))
     c                   movel     HEPOS1        HEPOS             3
     C                   callp     updHTMLvar('HEPOS':HEPOS)
     C                   callp     updHTMLvar('HEAVD':
     c                             %trim(%editc(HEAVD:'Z')))
     C                   callp     updHTMLvar('HEOPD':
     c                             %trim(%editc(HEOPD:'Z')))
     c     HXsndn        ifeq      *blanks
     c                   eval      Hxsndn=%trim(%editc(HEAVD:'Z'))
     c                             +'/'+  %trim(%editc(HEOPD:'Z'))
     c                   end
     C                   callp     updHTMLvar('HXSNDN':HXSNDN)
     C                   callp     updHTMLvar('HENAS':HENAS)
     C                   callp     updHTMLvar('HENAK':HENAK)
     C                   callp     updHTMLvar('HEADK3':HEADK3)
     C                   callp     updHTMLvar('HEVKT':
     c                             %trim(%editc(HEVKT:'Z')))
     C                   callp     updHTMLvar('HENT':
     c                             %trim(%editc(HENT:'Z')))
      *
     c                   exsr      tstAllerede
      *
     C                   callp     updHTMLvar('TTDATE':
     c                             %trim(%editw(xxDATE:'    .  .  ')))
     C                   callp     updHTMLvar('TTTIME':
     c                             %trim(%editw(xxTIM4:'  :  ')))
     C                   callp     updHTMLvar('TTTEXT':xxTEXT)
     C                   callp     updHTMLvar('MERKNAD':MERKNAD)
     C                   callp     wrtsection('Row')
     c     Next          tag
     C     TurNrN        reade     HEAD14                                 94
     c                   end
      *
     c     UtTur         tag
     C                   callp     wrtsection('RowBot')
      *
     C     UtUpd         ENDSR
N04   *
     C***********************************************
     C     tstAllerede   BEGSR
     C***********************************************
     c                   eval      xxTEXT = *blanks
     c                   eval      xxDATE = *zeros
     c                   eval      xxTIME = *zeros
     c                   eval      xxTIM4 = *zeros
     c                   eval      MERKNAD=*blanks
     c                   eval      xdDATL = *zeros                              deletes
     c                   eval      xdTIML = *zeros                              deletes
      * henter opp evt eksisterende TE2 fra T&T...
     c                   move      'TE2'         TTACTI
     c     FinnTT        tag
     c     TTKEY         setgt     TRACKL02
     c     TTKEY         readpe    TRACKL02                               33
     c     *in33         doweq     '0'
     c     TERM          ifeq      TTDEPO
      * RestOK overstyrer en tidl. SHORT-eks: helmanko inn en dag (kommer OK(annen tur) neste dag)
     c     RestOK        ifeq      'Y'
     c     TTACTI        andeq     'T2S'                                        =SHORT/helmanko
     c     TTDATE        andlt     DATN
     c                   eval      xdDATL = TTDATL                               delete evt AVV
     c                   eval      xdTIML = TTTIML
     c                   delete    TRACKFR
     c                   goto      NextTT
     c                   endif
      * funnet en ekisterede TE2/T2S på denne term
     c                   eval      xxTEXT = TTTEXT
     c                   eval      xxDATE = TTDATE
     c                   eval      xxTIME = TTTIME
     c                   movel     xxtime        xxTIM4
     c                   goto      UtAllerede                                   eldste/samme depot
     c                   end
     c                   update    TRACKFR
     c     NextTT        tag
     c     TTKEY         readpe    TRACKL02                               33
     c                   enddo
      * prøv med T2S
     c     TTACTI        ifeq      'TE2'
     c                   move      'T2S'         TTACTI
     c                   goto      finnTT
     c                   endif
     C     UtAllerede    tag
      *
      * Er avvik/merknad registrert
      * (+dersom gammel T2S er slettet skal samsvarende AVV også slettes)
     c     xxTEXT        ifne      *blanks
     c     xdDATL        orne      *zeros
     c                   move      'AVV'         TTACTI
     c     TTKEY         setgt     TRACKL02
     c     TTKEY         readpe    TRACKL02                               33
     c     *in33         doweq     '0'
     c     TERM          ifeq      TTDEPO
     c     RestOK        ifeq      'Y'
     c     TTDATL        andeq     xdDATL
     c     TTTIML        andeq     xdTIML
     c                   delete    TRACKFR
     c                   goto      NextTTA
     c                   endif
     c     xxDATE        ifeq      TTDATE
     c     xxTIME        andeq     TTTIME
     c                   eval      MERKNAD = TTTEXT
     c                   leave
     c                   endif
     c                   endif
     c     NextTTA       tag
     c     TTKEY         readpe    TRACKL02                               33
     c                   enddo
     c                   endif
      *
     c     RestOK        ifeq      'Y'
     c     xxDATE        andeq     *zeros                                       finnes ikke TE2/T2S
     C                   MOVE      HEAVD         TTAVD
     C                   MOVE      HEOPD         TTOPD
     C                   MOVE      *zeros        TTFBNR
     C                   TIME                    XULTID
     C                   MOVE      XULDD         ULDD
     C                   MOVE      XULMM         ULMM
     C                   MOVE      XULÅÅ         ULÅÅ
     C                   MOVE      ULDT          TTDATL
     C                   MOVE      XULTM         TTTIML
     C                   MOVE      DATN          TTDATE
     C                   MOVE      *zeros        TTTIME
     C                   MOVEL     KLN           TTTIME
     C                   MOVE      'TE2'         TTACTI
     C                   EVAL      TTTEXT = 'Scanned '
     c                             + %trim(TermNavn)
     c                             + ' Checked: '
     c                             + %trim(%editc(HENT:'3'))
     c                             + '/' + %trim(%editc(HENT:'3'))
n02  C                   EVAL      TTTEXL = 'Skannet '
n02  c                             + %trim(TermNavn)
n02  c                             + ' Sjekket: '
n02  c                             + %trim(%editc(HENT:'3'))
n02  c                             + '/' + %trim(%editc(HENT:'3'))
     C                   MOVEL     TERM          TTDEPO
     C                   MOVEL     WSUSER        TTUSER
     c                   move      'S'           TTMANU
     C                   WRITE     TRACKFR
     c                   eval      xxTEXT = TTTEXT
     c                   eval      xxDATE = TTDATE
     c                   eval      xxTIME = TTTIME
     c                   movel     xxtime        xxTIM4
      *
     c                   endif
      *
     C                   ENDSR
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C                   open      BRIDF
     C     WSUSER        CHAIN     BRIDF                              50
N01  c     *in50         ifeq      '1'
N01  C                   callp     gethtml('HTMLSRC':'*LIBL':'ERRUSR':
N01  c                             '/CGITAG/')
N01  C                   callp     updhtmlvar('User':WSUSER)
N01  C                   callp     wrtsection('all')
N01  C                   callp     wrtsection('*fini')
N01  C                   close     BRIDF
N01  C                   eval      *inlr = *on
N01  C                   return
N01  c                   endif
N01   *
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
      *
     C                   open      HEAD14
     C                   open      BRPARF
     C                   open      TURER14
     C                   open      TRACKL02
      *
     C                   MOVE      *BLANKS       FEILMELDING
     C                   MOVE      *BLANKS       FEILKODE
      *
     C     TTKEY         KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C                   KFLD                    TTFBNR
     C                   KFLD                    TTACTI
     C     BrParKey      KLIST
     C                   kfld                    PABRID
     C                   kfld                    PAKUNR
     C                   kfld                    PAID
     C                   MOVE      WSUSER        PABRID
     C                   MOVE      *zeros        PAKUNR
     C                   MOVEL     'TERMK'       PAID
      * Ved fremmed terminal - finn terminaldefinisjon, enten på usernivå eller på firmanivå
     c                   move      *blanks       Termnavn         40
      *
     c     BrParKey      setll     BRPARF
     c     BrParKey      reade     BRPARF                                 33
     c     *in33         doweq     '0'
     c     Pavrda1_5     ifeq      TERM
     c                   eval      TermNavn= Pavrda7_46
     c                   leave
     c                   end
     c     BrParKey      reade     BRPARF                                 33
     c                   end
      *
     c     TermNavn      ifeq      *blanks
     c                   eval      TermNavn= TERM
     c                   end
      *
      *
     C                   ENDSR
