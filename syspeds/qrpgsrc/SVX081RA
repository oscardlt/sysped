     FSVNH      UF   E           K DISK
     FSVNHE     UF   E           K DISK
     FSVTHA     IF   E           K DISK
     FEDIC      UF   E             DISK
     FEDIM      O  A E             DISK
     FSVTFI     IF   E             DISK
      *//////////////
      *  Hodelogikk /
      *//////////////
     C                   EXSR      SRA
     C                   EXSR      SRB
     C                   MOVE      '1'           *INLR
      *////////////////////////////////////////////////////////////
      *  Initiering                                           SRA /
      *////////////////////////////////////////////////////////////
     C     SRA           BEGSR
      *____________________
      * Definiere variabler
      *""""""""""""""""""""
     C     *LIKE         DEFINE    TIAVD         XAVD
     C     *LIKE         DEFINE    TITDN         XTDN
     C     *LIKE         DEFINE    TISGDK        XSGDK
     C     *LIKE         DEFINE    TISGUT        XSGUT
      *_________________
      * Input parametere
      *"""""""""""""""""
     C     *ENTRY        PLIST
     C                   PARM                    XAVD
     C                   PARM                    XTDN
      *__________________
      * Definiere Nøkkler
      *""""""""""""""""""
     C     SVNHK         KLIST
     C                   KFLD                    XAVD
     C                   KFLD                    XTDN
      *_________________________________
      * Hæmta dagens datum och klockslag
      *"""""""""""""""""""""""""""""""""
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
      *________________________
      * Hæmta firmaupplysningar
      *""""""""""""""""""""""""
     C     1             CHAIN     SVTFI                              51
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Bearbeide Formater                                   SRB /
      *////////////////////////////////////////////////////////////
     C     SRB           BEGSR
     C     SVNHK         CHAIN(N)  SVNH                               90
     C                   EVAL      XSGDK=TISGDK
     C                   EVAL      XSGUT=TISGUT
      * Sigiller via keypad eller automatsik ?
     C     TISG          CHAIN     SVTHA
     C*    SVTH_DKEYN    IFNE      *BLANKS
     C*                  EXSR      SRCC
     C*                  ENDIF
     C     SVNHK         CHAIN     SVNH                               90
     C                   EVAL      TISGDK=XSGDK
     C                   EVAL      TISGUT=XSGUT
     C                   UPDATE    SVNHR
      * Slett edifact
     C     SVNHK         SETLL     SVNHE
     C     SVNHK         READE     SVNHE                                  51
     C     *IN51         DOWEQ     *OFF
     C                   DELETE    SVNHER
     C     SVNHK         READE     SVNHE                                  51
     C                   ENDDO
      * Lag edifact pånytt med riktig sigill
     C     TISGDK        IFNE      *BLANKS
     C     TISGDK        ANDNE     '000000'
     C                   CALL      'SVX151R'
     C                   PARM                    XAVD
     C                   PARM                    XTDN
     C                   PARM                    TIMF

     C                   ENDIF
      *_____________
      * Ændra status
      *"""""""""""""
     C     SVNHK         CHAIN     SVNH                               52
     C                   EVAL      TIST='A'
     C                   UPDATE    SVNHR
      *_____________________________
      * Hæmta næsta meddelandenummer
      *"""""""""""""""""""""""""""""
     C     1             CHAIN     EDIC                               52
     C                   ADD       1             CMN
     C                   UPDATE    EDICR
      *_______________
      * Skriv til EDIM
      *"""""""""""""""
     C                   CLEAR                   EDIMR
     C                   EVAL      M0004=SVTF_0004
     C                   EVAL      M0010=SVTF_0010
     C                   EVAL      M0035=SVTF_0035
     C                   MOVEL     TIAVD         W0062            11
     C                   MOVE      TITDN         W0062
     C                   EVAL      M0062=W0062
     C                   EVAL      M0065='CUSDEC'
     C                   EVAL      M1001='STI'
     C                   EVAL      M0068=TITUID
     C                   EVAL      M1004=TITUID
     C                   EVAL      M1225=TIMF
     C                   EVAL      M1N07=TIMF
     C                   EVAL      MMN=CMN
     C                   EVAL      MSR='S'
     C                   MOVEL     WDT           MDT
     C                   MOVEL     WTM           MTM
     C                   EVAL      MAVD=TIAVD
     C                   EVAL      MTDN=TITDN

     C                   WRITE     EDIMR

     C     SRB9          TAG
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Berækna utstællarsigill                             SRCC /
      *////////////////////////////////////////////////////////////
     C     SRCC          BEGSR
      * Send oppdrag til sigillering
     C                   MOVEL     XAVD          QTFIEL
     C                   MOVE      XTDN          QTFIEL
     C                   CALL      'QSNDDTAQ'
     C                   PARM      'SVGSRQ'      QTNAME           10
     C                   PARM      '*LIBL'       QTLIB            10
     C                   PARM      11            QTLEN             5 0
     C                   PARM                    QTFIEL           11
      * Sigillering
     C                   CALL      'SEALCTL'
     C                   PARM      'SVG003RN  '  QPARM            10
      * Retur fra sigillering
     C                   MOVEL     XAVD          QKEY
     C                   MOVE      XTDN          QKEY
     C                   CALL      'QRCVDTAQ'
     C                   PARM      'SVGSSQ'      QRNAME           10
     C                   PARM      '*LIBL'       QRLIB            10
     C                   PARM      6             QRLEN             5 0
     C                   PARM                    XSGUT
     C                   PARM      99            QWAIT             5 0
     C                   PARM      'EQ'          QORDER            2
     C                   PARM      11            QKILEN            3 0
     C                   PARM                    QKEY             11
     C                   PARM      10            QSNDRL            3 0
     C                   PARM                    QSNDR            10
     C                   ENDSR
