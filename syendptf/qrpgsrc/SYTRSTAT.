      ***********************************************************************
      *  Finn oppdrags transportstatus ut fra T&T     (skilt ut fra SYTR10 )
      *    obs SETTER IKKE *LR - viktige variable må alltid initieres
      ***********************************************************************
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
12XXX * 01 JOV PTF12 Hvis ennå ikke ankommet terminal - sjekk komme (skyting påbegynt)
********************************************************************
     FTRACKL02  IF   E           K DISK
     FHEADF     IF   E           K DISK
     FSYPARL1   IF   E           K DISK
N01  FDOKUFM    IF   E           K DISK
      *
     C                   exsr      INIT
     C                   exsr      TTstatus
      *
     C                   RETURN
      *
     C***********************************************
     C     TTstatus      BEGSR
     C***********************************************
      * returnert status som TEKST - overlat til kallende prog å sette evt indikatore mm
     C                   eval      Status='Levert'
      * sjekk om den er levert / evt hentet
      * LEVERT ??
     C                   MOVE      'POD'         TTACTI
     c     TT02key       Chain     TRACKl02                           33
     C                   if        *in33=*on and TRSLAG='FF'                    FF+TEI=Levert
     C   33              MOVE      'TEI'         TTACTI
     c   33TT02key       Chain     TRACKl02                           33
     c                   endif
     C     *in33         ifeq      *ON
     C                   MOVE      'TE2'         TTACTI
     c     TT02key       setll     TRACKl02
     c     TT02key       reade     TRACKl02                               33
     c     *in33         doweq     '0'
     c                   if        %subst(TTTEXT:1:9)='Signed by'
     c                   leave
     c                   endif
     c     TT02key       reade     TRACKl02                               33
     c                   enddo
     c                   endif
      * er ikke levert...
      * HENTET ??
     C     *in33         ifeq      *ON
     C                   eval      Status='Hentet'
     C                   MOVE      'COL'         TTACTI
     c     TT02key       Chain     TRACKl02                           33
     C   33              MOVE      'TEU'         TTACTI
     c   33TT02key       Chain     TRACKl02                           33
      * heller ikke hentet
     C   33              eval      Status='Urørt'
      * På term ?
     c     *in33         ifeq      '1'
     C                   MOVE      'TEI'         TTACTI
     c     TT02key       Chain     TRACKl02                           33
     C   33              MOVE      'TE2'         TTACTI
     c   33TT02key       Chain     TRACKl02                           33
     C  N33              eval      Status='På Term'
     c                   endif
     c                   endif
N01
N01   * Hvis ennå ikke ankommet teminal (TEI) sjekk om påbegynt skyting..
N01  C                   if        Status='Urørt' or
N01  C                             Status='Hentet'
N01  c     DokufmKey     setll     DOKUFM
N01  c     DokufmKey     reade     DOKUFM
N01  c                   dow       not %eof
N01  c                   if        FMITER = 'X'
N01  C                   eval      Status='På Te(D)'
N01  c                   leave
N01  c                   endif
N01  c     DokufmKey     reade     DOKUFM
N01  c                   enddo
N01  c                   endif
N01
     c                   ENDSR
     C*
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C*
     C     *ENTRY        PLIST
     C                   PARM                    TTAVD
     C                   PARM                    TTOPD
     C                   PARM                    UUmanu            1
     C                   PARM                    Status           15
     C                   eval      Status=*blanks
      *
     C     HeaKey        KLIST
     C                   KFLD                    TTAVD
     C                   KFLD                    TTOPD
     C     TT02KEY       KLIST
     C                   KFLD                    TTAVD
     C                   KFLD                    TTOPD
     C                   KFLD                    TTFBNR
     C                   KFLD                    TTACTI
      *
     C     SyParKey      KLIST
     C                   KFLD                    SYKUNR
     C                   KFLD                    SYPAID
      *
N01  C     DokufmKey     KLIST
N01  C                   KFLD                    FMRI
N01  C                   KFLD                    FMAVD
N01  C                   KFLD                    FMOPD
N01  C                   KFLD                    FMFBNR
N01  C                   KFLD                    FMLINR
N01  C                   move      'F'           FMRI
N01  C                   move      001           FMFBNR
      *
      * vis kun dersom det er parametersatt
     c                   if        UUmanu = ' '
     C                   move      'VISTT'       SYPAID
     C     SyParKey      CHAIN     SYPARL1
     c                   if        SYVRDA <> 'J' and SYVRDA = 'V'
     c                   return
     c                   endif
     c                   endif
      *
     C     HeaKey        CHAIN     HEADF
     c                   if        not %found
     c                   return
     c                   endif
N01  c                   move      HEAVD         FMAVD
N01  c                   move      HEOPD         FMOPD
      *
     c                   ENDSR
     C*
