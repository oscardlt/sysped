     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HELP
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  5  2VALUES(' ' '5')
     A            BLBBK          5   O  5  5
     A            BLBREI        14   O  5 11
     A            BLBOMH         5   O  5 26
     A            XBDT           6  0O  5 32EDTCDE(4)
     A            BLBBLN        16   O  5 39
     A            BLOPD          7  0O  5 56EDTCDE(4)
     A            BLPRO          8  0O  5 64EDTCDE(4)
     A            BLSG           3   O  5 73
     A            BLUR           1   O  5 77
     A            BLST           1   O  5 79
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0017)
     A                                      SFLPAG(0016)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A            ZRECNO         4S 0O  1 77SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYSPED/SYSJ22A'
     A                                      COLOR(BLU)
     A                                  1 61DATE
     A                                      EDTCDE(Y)
     A                                  1 70TIME
     A                                  1 20MSGCON(40 DIV3662 SYMSGF)
     A                                      COLOR(WHT)
     A                                  2  2MSGCON(15 DIV3663 SYMSGF)
     A            UAVD           4Y 0B  2 19DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A  41                              2 30MSGCON(15 DIV3665 SYMSGF)
     A  41        UOPD           7Y 0O  2 47DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A  42                              2 30MSGCON(15 DIV3666 SYMSGF)
     A  42        UOB            5A  O  2 47DSPATR(UL)
     A                                      COLOR(WHT)
     A  42                              2 65MSGCON(6 DIV3667 SYMSGF)
     A  42        WSDTB          6Y 0O  2 73DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A  43                              2 30MSGCON(15 DIV3668 SYMSGF)
     A  43        UREIS         14A  O  2 47DSPATR(UL)
     A                                      COLOR(WHT)
     A  43                              2 65MSGCON(6 DIV3667 SYMSGF)
     A  43        WSDTR          6Y 0O  2 73DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A  44                              2 30MSGCON(15 DIV3669 SYMSGF)
     A  44        UBLNR         16A  O  2 47DSPATR(UL)
     A                                  3  2MSGCON(26 DIV0016 SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 32MSGCON(20 DIV3671 SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(78 YBC0109 SYMSGF)
     A                                      COLOR(WHT)
     A          R BUNN
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF01(01 'BYTT AVDELINGER')
     A                                      CF03(03 'Avslutt')
     A                                      CF12(12 'FORRIGE')
     A                                      SLNO(23)
     A                                      CLRL(02)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2MSGCON(18 DIV0067 SYMSGF)
     A                                      COLOR(BLU)
     A                                  1 22MSGCON(18 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A                                  1 32MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  2  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                              2 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A          R NYVARE
     A                                  1  2'SYSPED/SYSJ22A'
     A                                      COLOR(BLU)
     A                                  1 61DATE
     A                                      EDTCDE(Y)
     A                                  1 70TIME
     A                                  1 20MSGCON(40 DIV3662 SYMSGF)
     A                                      COLOR(WHT)
     A                                  2  2MSGCON(15 DIV3663 SYMSGF)
     A            UAVD           4Y 0B  2 19DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A  41                              2 30MSGCON(15 DIV3665 SYMSGF)
     A  41        UOPD           7Y 0O  2 47DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A  42                              2 30MSGCON(15 DIV3666 SYMSGF)
     A  42        UOB            5A  O  2 47DSPATR(UL)
     A                                      COLOR(WHT)
     A  42                              2 65MSGCON(6 DIV3667 SYMSGF)
     A  42        WSDTB          6Y 0O  2 73DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(Y)
     A  43                              2 30MSGCON(15 DIV3668 SYMSGF)
     A  43        UREIS         14A  O  2 47DSPATR(UL)
     A                                      COLOR(WHT)
     A  43                              2 65MSGCON(6 DIV3667 SYMSGF)
     A  43        WSDTR          6Y 0O  2 73DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(Y)
     A  44                              2 30MSGCON(15 DIV3669 SYMSGF)
     A  44        UBLNR         16A  O  2 47DSPATR(UL)
     A                                  3  2MSGCON(26 DIV0016 SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 32MSGCON(20 DIV3671 SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(78 YBC0109 SYMSGF)
     A                                      COLOR(WHT)
