********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 YBC PTF10 VAREBESKRIVELSE ER ENDRET
********************************************************************
     FEDIM2     UF   E           K DISK
     FEDIS      UF   E           K DISK
     FTRIH09    UF   E           K DISK
     FTRIHFS    UF   E           K DISK
     FTRIHPK    UF   E           K DISK
     FTRIL      UF   E           K DISK
     FTRILCN    UF   E           K DISK
     FTRILDK    UF   E           K DISK
     FTRILKL    UF   E           K DISK
     FTRILMN    UF   E           K DISK
     FTRILPK    UF   E           K DISK
N01  FTRILVT    UF   E           K DISK
     FTRIV      UF   E           K DISK
     FTRIVCN    UF   E           K DISK
     FTRIVFS    UF   E           K DISK
     FTRIHCMP   O    E           K DISK
     FTVINF     UF   E           K DISK
     D                 DS
     D  XDT2                   1      6  0
     D  XD                     1      2  0
     D  XM                     3      4  0
     D  XÅ                     5      6  0
     D                 DS
     D  WDT                    1      8  0
     D  WÅ                     1      4  0
     D  WM                     5      6  0
     D  WD                     7      8  0
      *___________
      * KEY FIELDS
      *"""""""""""
     C     TRIH09K       KLIST
     C                   KFLD                    TIAVD
     C                   KFLD                    TITDN
      *____________
      * WORK FIELDS
      *""""""""""""
     C     *LIKE         DEFINE    TIAVD         XAVD
     C     *LIKE         DEFINE    XDT2          XDT
      *_______________
      * PARAMETER LIST
      *"""""""""""""""
     C     *ENTRY        PLIST
     C                   PARM                    XAVD
     C     XDT2          PARM                    XDT
     C                   PARM                    XDLT              1
      *__________________________________________
      * SETLL, READ AND DELETE RECORDS FROM SADHD
      *""""""""""""""""""""""""""""""""""""""""""
     C                   Z-ADD     XÅ            WÅ
     C                   Z-ADD     XM            WM
     C                   Z-ADD     XD            WD
     C     XÅ            IFGT      50
     C                   MOVEL     '19'          WÅ
     C                   ELSE
     C                   MOVEL     '20'          WÅ
     C                   END
      *
     C     TIDT          SETLL     TRIH09
     C                   READ      TRIH09                                 51
     C  N51TIDT          COMP      WDT                                51
     C     *IN51         DOWEQ     '0'                                          <----------I
     C     XAVD          IFEQ      TIAVD                                         <--------II
     C     XAVD          OREQ      0
     C                   EXSR      SRA                                          SADV
     C                   EXSR      SRB                                          SADF
     C                   EXSR      SRC                                          SADW
     C     XDLT          IFEQ      'J'
     C                   EXSR      SRD                                          EDIM2/EDIS/TVINF
     C                   END
     C                   WRITE     TRIHRCMP
     C                   DELETE    TRIHR
     C                   END                                                     <--------II
     C                   READ      TRIH09                                 51
     C  N51TIDT          COMP      WDT                                51
     C  N51              END                                                    <----------I
      *
     C                   MOVE      '1'           *INLR
     C                   RETURN
      *___________________________________________
      * SETLL, READ AND DELETE RECORDS FROM TRIH??
      *"""""""""""""""""""""""""""""""""""""""""""
     C     SRA           BEGSR
      * TRIHFS
     C     TRIH09K       SETLL     TRIHFS
     C     TRIH09K       READE     TRIHFS                                 52
     C                   DOW       *IN52 = *OFF
     C                   DELETE    TRIHFSR
     C     TRIH09K       READE     TRIHFS                                 52
     C                   ENDDO
      * TRIHPK
     C     TRIH09K       SETLL     TRIHPK
     C     TRIH09K       READE     TRIHPK                                 52
     C                   DOW       *IN52 = *OFF
     C                   DELETE    TRIHPKR
     C     TRIH09K       READE     TRIHPK                                 52
     C                   ENDDO
      *
     C                   ENDSR
      *___________________________________________
      * SETLL, READ AND DELETE RECORDS FROM TRIL??
      *"""""""""""""""""""""""""""""""""""""""""""
     C     SRB           BEGSR
      * TRILCN
     C     TRIH09K       SETLL     TRILCN
     C     TRIH09K       READE     TRILCN                                 52
     C                   DOW       *IN52 = *OFF
     C                   DELETE    TRILCNR
     C     TRIH09K       READE     TRILCN                                 52
     C                   ENDDO
      * TRILDK
     C     TRIH09K       SETLL     TRILDK
     C     TRIH09K       READE     TRILDK                                 52
     C                   DOW       *IN52 = *OFF
     C                   DELETE    TRILDKR
     C     TRIH09K       READE     TRILDK                                 52
     C                   ENDDO
      * TRILKL
     C     TRIH09K       SETLL     TRILKL
     C     TRIH09K       READE     TRILKL                                 52
     C                   DOW       *IN52 = *OFF
     C                   DELETE    TRILKLR
     C     TRIH09K       READE     TRILKL                                 52
     C                   ENDDO
      * TRILMN
     C     TRIH09K       SETLL     TRILMN
     C     TRIH09K       READE     TRILMN                                 52
     C                   DOW       *IN52 = *OFF
     C                   DELETE    TRILMNR
     C     TRIH09K       READE     TRILMN                                 52
     C                   ENDDO
      * TRILPK
     C     TRIH09K       SETLL     TRILPK
     C     TRIH09K       READE     TRILPK                                 52
     C                   DOW       *IN52 = *OFF
     C                   DELETE    TRILPKR
     C     TRIH09K       READE     TRILPK                                 52
     C                   ENDDO
N01   * TRILVT
N01  C     TRIH09K       SETLL     TRILVT
N01  C     TRIH09K       READE     TRILVT                                 52
N01  C                   DOW       *IN52 = *OFF
N01  C                   DELETE    TRILVTR
N01  C     TRIH09K       READE     TRILVT                                 52
N01  C                   ENDDO
      * TRIL
     C     TRIH09K       SETLL     TRIL
     C     TRIH09K       READE     TRIL                                   52
     C                   DOW       *IN52 = *OFF
     C                   DELETE    TRILR
     C     TRIH09K       READE     TRIL                                   52
     C                   ENDDO
      *
     C                   ENDSR
      *___________________________________________
      * SETLL, READ AND DELETE RECORDS FROM TRIV??
      *"""""""""""""""""""""""""""""""""""""""""""
     C     SRC           BEGSR
      * TRIVCN
     C     TRIH09K       SETLL     TRIVCN
     C     TRIH09K       READE     TRIVCN                                 52
     C                   DOW       *IN52 = *OFF
     C                   DELETE    TRIVCNR
     C     TRIH09K       READE     TRIVCN                                 52
     C                   ENDDO
      * TRIVFS
     C     TRIH09K       SETLL     TRIVFS
     C     TRIH09K       READE     TRIVFS                                 52
     C                   DOW       *IN52 = *OFF
     C                   DELETE    TRIVFSR
     C     TRIH09K       READE     TRIVFS                                 52
     C                   ENDDO
      * TRIV
     C     TRIH09K       SETLL     TRIV
     C     TRIH09K       READE     TRIV                                   52
     C                   DOW       *IN52 = *OFF
     C                   DELETE    TRIVR
     C     TRIH09K       READE     TRIV                                   52
     C                   ENDDO
      *
     C                   ENDSR
      *_____________________________________________________
      * SETLL, READ AND DELETE RECORDS FROM EDIM2/EDIS/TVINF
      *"""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     SRD           BEGSR
     C                   MOVE      '0'           *IN52
     C     TRIH09K       SETLL     EDIM2
     C     TRIH09K       READE     EDIM2                                  52
      *
     C     *IN52         DOWEQ     '0'                                          <----------I
     C     M1001         IFEQ      'TEI'                                         <--------II
     C     MMN           SETLL     TVINF                                                  II
     C     MMN           READE     TVINF                                  53              II
     C     *IN53         DOWEQ     '0'                                            <------III
     C                   DELETE    TVINFF                                                III
     C     MMN           READE     TVINF                                  53             III
     C  N53              END                                                      <------III
     C     MSN           CHAIN     EDIS                               53                  II
     C  N53              DELETE    EDISR                                                  II
     C                   DELETE    EDIMR                                                  II
     C                   ELSE                                                             II
     C                   UPDATE    EDIMR                                                  II
     C                   END                                                     <--------II
     C     TRIH09K       READE     EDIM2                                  52
     C  N52              END                                                    <----------I
     C                   ENDSR
