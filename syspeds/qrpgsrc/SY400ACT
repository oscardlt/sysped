      * INPUT PARAMTERS TO GENERATE USER SPACE
     D                 DS
     D  USRSPC                 1     20
     D  SPCNAM                 1     10
     D  SPCLIB                11     20
      * INPUT PARMS TO GENERATE JOB LIST
     D                 DS
     D  QJOBI                  1     26
     D  JNAMEI                 1     10
     D  JUSERI                11     20
     D  JNUMI                 21     26
      * VARIABLE TO HOLD LIST/ENTRY SIZE INFORMATION
     D ØLIST           DS
     D  NBRENT                 9     12B 0
      * VARIABLE TO HOLD ERROR INFORMATION
     D ERR             DS
     D  ERRBP                  1      4B 0
     D  ERRBA                  5      8B 0
     D  ERRMSG                 9     15  0
      * BINARY NUMBER DEFINITIONS
     D                 DS
     D  LENDTA                 1      4B 0
     D  STRPOS                 5      8B 0
     D  LENSPC                13     16B 0
      * PARAMETERS
     C     *ENTRY        PLIST
     C                   PARM                    ACTJOB           10
     C                   PARM                    ACTYN             1
     C* INIT VARIABLES
     C                   MOVEL     'SY400ACT'    SPCNAM
     C                   MOVEL     'QTEMP   '    SPCLIB
     C                   MOVEL     ACTJOB        JNAMEI
     C                   MOVEL     'SY400USR'    JUSERI
     C                   MOVEL     '*ALL'        JNUMI
     C                   Z-ADD     8             ERRBP
     C                   Z-ADD     0             ERRBA
     C* DELETE USER SPACE
     C                   CALL      'QUSDLTUS'
     C                   PARM                    USRSPC
     C                   PARM                    ERR
     C* CREATE USER SPACE
     C                   CALL      'QUSCRTUS'
     C                   PARM                    USRSPC
     C                   PARM      *BLANK        ATTRIB           10
     C                   PARM      3072          LENSPC
     C                   PARM      *BLANK        VALUE             1
     C                   PARM      '*CHANGE'     PUBAUT           10
     C                   PARM      'SY400ACT'    ØTEXT            50
      * LIST JOBS
     C                   CALL      'QUSLJOB'
     C                   PARM                    USRSPC
     C                   PARM      'JOBL0100'    FMTNAM            8
     C                   PARM                    QJOBI
     C                   PARM      '*ACTIVE'     JSTATI           10
      * FIND OFFSET, SIZE, NUMBER OF ENTRIES
      * SPECIFIED IN 16 BYTES STARTING IN POSTION 125 OF GENERIC
      * HEADER.
     C                   CALL      'QUSRTVUS'
     C                   PARM                    USRSPC
     C                   PARM      125           STRPOS
     C                   PARM      16            LENDTA
     C                   PARM                    ØLIST
     C                   Z-ADD     NBRENT        J                 7 0
     C* VAR: J INCLUDE NO OF ACTIVE JOBS
     C     J             IFEQ      0
     C                   MOVE      'N'           ACTYN
     C                   ELSE
     C                   MOVE      'Y'           ACTYN
     C                   END
      *
     C                   RETURN
