********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
11XXX * 01 SH  PTF11 overstyre valuta   .....
********************************************************************
      ************************************************************
      *** UTSKRIFT AV FERJEFAKTURA                             ***
      ************************************************************
     FFIRM      IF   E           K DISK
     FKODTV     IF   E           K DISK
     FKODTW     IF   E           K DISK
     FKODTSF    IF   E           K DISK
     FTRAN      IF   E           K DISK
     FKODTÅ     IF   E           K DISK
     FTUFE3     IF   E           K DISK
     FSYTR52FA  CF   E             WORKSTN
     D                 DS
     D  WSPER                  1      6  0
     D  WSPERÅ                 3      4  0
     D  WSPERM                 5      6  0
     D                 DS
     D  MINPER                 1      6  0
     D  KOÅCC                  1      2  0
     D  KOÅÅR                  3      4  0
     D  KOÅMND                 5      6  0
     C                   EXSR      INIT
     C     WSVAVD        IFGT      0
     C                   GOTO      START2
     C                   ELSE
     C                   SETON                                        50        IKKE VIS NEDRE DEL.
     C                   END
     C*
     C*
     C     START         TAG
     C                   EXFMT     BILDE01
     C     START2        TAG
     C                   SETOFF                                       309899
     C                   SETOFF                                       97
     C*
     C   03              MOVE      '1'           UIN01
     C   03              GOTO      SLUTT
     C   12              MOVE      '1'           UIN01
     C   12              GOTO      SLUTT
     C*
     C     KODTVK        CHAIN     KODTV                              99
     C  N99KODTWK        CHAIN     KODTW                              99
     C   99              MOVE      'SPA0001'     MSGNR
     C   99              SETON                                        3050
     C   99              GOTO      START
     C*
     C     KODSFK        CHAIN     KODTSF                             98
     C   98              MOVE      'SPS0001'     MSGNR
     C   98              SETON                                        3050
     C   98              GOTO      START
     C*
     C* HENTER STANDARD PRINTERSTYRING - FAKTURA
     C*
     C                   Z-ADD     4             PRTNR             3 0
     C                   CALL      'SYGE01'
     C                   PARM                    WSVAVD
     C                   PARM                    PRTNR
     C                   PARM                    WSPRT
     C                   PARM                    WSFORM
     C                   PARM                    WSHEAD            1
     C                   PARM                    WKOPFM            5
     C*
     C* HENTER STANDARD PRINTERSTYRING - SEPARAT GIRO
     C* - VISER IKKE DE PÅ SKJERM FOR OVERSTYRING - BØR VIDEREUTVIKLES
     C                   Z-ADD     27            PRTNR             3 0
     C                   CALL      'SYGE01'
     C                   PARM                    WSVAVD
     C                   PARM                    PRTNR
     C                   PARM                    WSPRT2           10
     C                   PARM                    WSFOR2           10
     C                   PARM                    WSHEA2            1
     C                   PARM                    WKOPFM
     C*
     C                   SETOFF                                       50         VIS NEDRE DEL
     C* MELDING OM FORIGE UTSKRIFT?
     C     UMSGNR        IFNE      'DIV0000'
     C                   MOVE      UMSGNR        MSGNR
     C                   MOVE      'DIV0000'     UMSGNR
     C                   SETON                                        30
     C                   END
     C*
     C     STA11         TAG
     C                   EXFMT     BILDE01
     C* NY AVD??
     C   12              DO
     C                   SETON                                        50
     C                   MOVE      *BLANKS       WSPRT
     C                   MOVE      *BLANKS       WSFORM
     C                   MOVE      *BLANKS       WSHEAD
     C                   MOVE      *BLANKS       WSPRT2
     C                   MOVE      *BLANKS       WSFOR2
     C                   MOVE      *BLANKS       WSHEA2
     C                   GOTO      START
     C                   END
     C*
     C   03              MOVE      '1'           UIN01
     C   03              GOTO      SLUTT
     C*
     C   04              DO
     C                   CALL      'SYTR04A'
     C                   PARM                    WSTNR
     C                   END
     C*
     C                   EXSR      TESTB1
     C   30              GOTO      STA11
     C*
     C     SLUTT         TAG
     C                   MOVE      WSTNR         UUTNR                           *ALPHA
     C                   MOVE      WSVAVD        UUVAVD                          *ALPHA
     C                   MOVE      WSPERÅ        UUPERÅ                          *ALPHA
     C                   MOVE      WSPERM        UUPERM                          *ALPHA
     C     WSJOBQ        IFEQ      'Y'
     C     WSJOBQ        OREQ      'J'
     C                   MOVE      'SPU0010'     UMSGNR            7
     C                   ELSE
     C                   MOVE      'SPU0011'     UMSGNR
     C                   END
     C                   SETON                                        LR
     C                   RETURN
     C*
     C***********************************************
     C     TESTB1        BEGSR
     C***********************************************
     C*
     C                   MOVEA     '0'           *IN(30)
     C                   MOVEA     '00000000'    *IN(70)
     C* TRANSPORTØR
     C     WSTNR         IFNE      *ZEROS
     C     WSTYPE        ANDNE     'K'
     C     WSTYPE        ANDNE     'C'
     C     TRANKE        CHAIN     TRAN                               70
     C   70              DO
     C                   MOVE      'SPT0133'     MSGNR
     C                   SETON                                        30
     C                   GOTO      SLTB
     C                   END
     C* KAN IKKE AVREGNE TRANSP- SOM IKKE HAR KUNDENR
     C     VMTRKU        IFEQ      *ZEROS
     C                   MOVE      'DIV2366'     MSGNR
     C                   SETON                                        3070
     C                   GOTO      SLTB
     C                   END
     C                   END
     C* VED KOPIKJØRING FÅ DET FINNES FAKTURA FOR GITT NR
     C* (TRANSP.NR ER DA BENYTTET TIL FAKTURANR FOR KOPIKJØRING)
     C     WSTYPE        IFEQ      'K'
     C                   MOVE      'F'           YYOPKO
     C                   Z-ADD     WSTNR         YYFAKT
     C     TUFE3K        SETLL     TUFE3
     C     TUFE3K        READE     TUFE3                                  70
     C   70              DO
     C                   SETOFF                                       70
     C                   MOVE      'O'           YYOPKO
     C     TUFE3K        SETLL     TUFE3
     C     TUFE3K        READE     TUFE3                                  70
     C                   END
     C   70              DO
     C                   MOVE      'DIV2802'     MSGNR
     C                   SETON                                        30
     C                   GOTO      SLTB
     C                   END
     C                   END
     C* 0 I TRANSPORTØR (ALLE) MÅ BEKREFTES MED F23
     C  N23WSTNR         IFEQ      *ZEROS
     C                   MOVE      'DIV2430'     MSGNR
     C                   SETON                                        3070
     C                   GOTO      SLTB
     C                   END
     C* PERIODISERING  (MÅ VÆRE STØRRE ENN MINSTE ÅPNE)
     C     WSTYPE        IFNE      'K'
     C     WSPERM        IFEQ      0
     C     WSPERÅ        ANDEQ     0
     C                   MOVE      *ZEROS        WSPER
     C                   ELSE
     C     WSPERÅ        IFGT      50
     C                   MOVEL     19            WSPER
     C                   ELSE
     C                   MOVEL     20            WSPER
     C                   END
     C                   END
     C     WSPER         COMP      MINPE2                               71
     C   71              DO
     C                   MOVE      'DIV2295'     MSGNR
     C                   SETON                                        3071
     C                   GOTO      SLTB
     C                   END
     C                   END
N01  C     WSVAL         ifne      *blanks
N01  C     wstype        ifeq      'K'
N01  c                   seton                                        72
N01  c                   end
N01  C     wstnr         ifeq      *zeros
N01  c                   seton                                        72
N01  c                   end
N01  C   72              SETON                                        30
N01  C                   MOVE      'SHU1476'     MSGNR
N01  C                   GOTO      SLTB
N01  c                   end
     C     SLTB          ENDSR
     C*
     C*
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C*
     C     *ENTRY        PLIST
     C                   PARM                    UIN01             1            *AVBRUDD (=1)
     C                   PARM                    UUVAVD            4            *ALPHA
     C                   PARM                    WSSFSI                         *SIGN
     C                   PARM                    WSPRT                          *PRINTER/UTKØ
     C                   PARM                    WSFORM                         *BLANKETT
     C                   PARM                    KOWHOD                         *HODE-FAKTURA
     C                   PARM                    KOWBBS                         *BBS -FAKTURA
     C                   PARM                    KOWAWB                         *SEPAR. GIROA
     C                   PARM                    WSHEAD                         *HODE ØVRIGE
     C                   PARM                    WSJOBQ                         *JOBKØ
     C                   PARM                    WSTYPE                         *T=TST/K=KOPI
     C                   PARM                    UMSGNR                         *MELDING FRA FOREG.
     C* TIL SLUTT LEGGES PARAMETRE SOM SKILLER SEG FRA PROG TIL PROG.
     C                   PARM                    UUTNR             8            *TRANSP(ALP)
     C                   PARM                    UUPERÅ            2            *PERÅR (ALP)
     C                   PARM                    UUPERM            2            *PERMND(ALP)
     C* SEPARAT GIRO
     C                   PARM                    WSPRT2                         *PRINTER/UTKØ
     C                   PARM                    WSFOR2                         *BLANKETT
N01  C                   PARM                    WSVAL                          *BLANKETT
     C*
     C     KODSFK        KLIST
     C                   KFLD                    WSSFUN
     C                   KFLD                    WSSFSI
     C     KODTVK        KLIST
     C                   KFLD                    WSVUNI
     C                   KFLD                    WSVAVD
     C     KODTWK        KLIST
     C                   KFLD                    WSWUNI
     C                   KFLD                    WSVAVD
     C     TRANKE        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    WSTNR
     C     KODÅKE        KLIST
     C                   KFLD                    KOÅUNI
     C                   KFLD                    KOÅCC
     C                   KFLD                    KOÅÅR
     C                   KFLD                    KOÅMND
     C     TUFE3K        KLIST
     C                   KFLD                    YYOPKO            1
     C                   KFLD                    YYFAKT            7 0
     C*
     C     UUVAVD        IFGT      '0000'
     C     UUVAVD        ANDLE     '9999'
     C                   MOVE      UUVAVD        WSVAVD                         *NUMERIC
     C                   ELSE
     C                   MOVE      *ZEROS        WSVAVD
     C                   MOVE      *BLANK        WSSFSI
     C                   END
     C                   MOVE      '0'           UIN01
     C                   MOVE      'SF'          WSSFUN            2
     C                   MOVE      'V'           WSVUNI            1
     C                   MOVE      'W'           WSWUNI            1
     C*
     C                   MOVE      *ZEROS        WSPER
     C                   MOVE      *ZEROS        MINPER
     C*
     C*
     C* HENTER MINSTE ÅPNE PERIODE
     C                   MOVE      'Å'           KOÅUNI
     C     KODÅKE        SETLL     KODTÅ
     C     KOÅUNI        READE     KODTÅ                                  33
     C     *IN33         IFEQ      '1'
     C     UYEAR         IFGT      50
     C     UYEAR         ADD       1900          MINPE2            6 0
     C                   ELSE
     C     UYEAR         ADD       2000          MINPE2
     C                   END
     C                   MULT      100           MINPE2
     C                   MOVE      UMONTH        MINPE2
     C                   ELSE
     C                   MOVE      MINPER        MINPE2
     C                   END
     C*
     C*
     C                   READ      FIRM                                   33
     C*
     C     UTI           ENDSR
     C*
     C*
