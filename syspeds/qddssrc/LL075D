     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      ALTHELP
     A                                      HELP
      *
     A          R SUBFIL                    SFL
     A            PLSTAT         1A  H
     A            WSNR           1A  B  5  2
     A                                      VALUES(' ' '1' '4' '5')
     A            PLFIRM         2A  O  5  5
     A            PLAAR          4  0O  5  8EDTCDE(4)
     A            PLPAKN         7  0O  5 13EDTCDE(4)
     A            PLLAGN         2A  H
     A            PLLAND         3A  O  5 21
     A            PLEKSN         7  0O  5 25EDTCDE(4)
     A            PLTXT         30A  O  5 33
     A            PLUDAT         8  0O  5 64EDTWRD('    .  .  ')
     A            PLUTID         4  0O  5 75EDTWRD('  :  ')
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0017)
     A                                      SFLPAG(0016)
     A                                      CA03(03 'Avslutt')
     A                                      CA05(05 'FORNY')
     A                                      CA12(12 'Cancel')
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A                                  1  2'SYSPED/LL075R'
     A                                      COLOR(BLU)
     A                                  1 20'Hent plukkliste'
     A                                      COLOR(WHT)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2  2'Lagerkunde:'
     A            XXPNR          8  0O  2 17COLOR(WHT)
     A                                      EDTCDE(4)
     A            KNAVN         30A  O  2 27COLOR(WHT)
     A            ZRECNO         4S 0O  2 56SFLRCDNBR
     A                                  2 72TIME
     A                                  3  2'Velg opsjon,   1=Plukk   4=Slett  -
     A                                      5=Vis'
     A                                      COLOR(BLU)
     A                                  4  2'Op FK   År Pl.list LKD Eksp.nr Bes-
     A                                      krivelse                    Utlagt -
     A                                      dato/tid'
     A                                      COLOR(WHT)
      *
     A          R BUNN
     A                                      SLNO(23)
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'F3/F12=Avbryt   F5=Forny'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  2  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R NOREC
     A                                      CA03(03 'Avslutt')
     A                                      CA12(12 'Cancel')
     A                                      OVERLAY
     A                                  1  2'SYSPED/LL075R'
     A                                      COLOR(BLU)
     A                                  1 20'Hent plukkliste'
     A                                      COLOR(WHT)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2  2'Lagerkunde:'
     A            XXPNR          8  0O  2 17COLOR(WHT)
     A                                      EDTCDE(4)
     A            KNAVN         30A  O  2 27COLOR(WHT)
     A                                  4  2'Op FK   År Pl.list LKD Eksp.nr Bes-
     A                                      krivelse                    Utlagt -
     A                                      dato/tid'
     A                                      COLOR(WHT)
     A                                 23  2'F3/F12=Avbryt'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
