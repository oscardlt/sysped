      *
      *OBS! OBS! OBS! OBS! OBS! OBS! OBS! OBS! OBS! OBS! OBS! OBS! OBS!
      * VED ENDING AV DENNE PRINTERFILEN MÅ MAN OGSÅ ENDRE SYSJ27PF.
      * SYSJ27PF ER SAMMENSATT AV SYSJ27P OG SYSJ27P2.
      *OBS! OBS! OBS! OBS! OBS! OBS! OBS! OBS! OBS! OBS! OBS! OBS! OBS!
      *
     A          R HFRAKT
     A                                  2  5 'To:'
     A            UNAVN         30      2 12
     A                                  2 51MSGCON(35 DIV3651 SYMSGF)
     A                                      HIGHLIGHT
     A                                  2126MSGCON(6 DIV3636 SYMSGF)
     A            XIDAG          6  0   2133EDTCDE(Y)
     A                                  2155MSGCON(6 DIV3638 SYMSGF)
     A            XFSIDE         2  0   2162EDTCDE(3)
     A                                  3  5 'From:'
     A            KNAVN         30      3 12
     A                                  4  3MSGCON(8 DIV3639 SYMSGF)
     A                                      HIGHLIGHT
     A            CBLK           2      4 12
     A            CBNAB         30      4 15
     A                                  4 45MSGCON(5 DIV3640 SYMSGF)
     A                                      HIGHLIGHT
     A            UREIS         14      4 51
     A                                  4 68MSGCON(18 DIV3641 SYMSGF)
     A                                      HIGHLIGHT
     A            XLSHN         30      4 87
     A                                  4120MSGCON(15 DIV3642 SYMSGF)
     A                                      HIGHLIGHT
     A            XDT            6  0   4136EDTCDE(Y)
     A                                  4147MSGCON(18 DIV3643 SYMSGF)
     A                                      HIGHLIGHT
     A            XLOHN         30      4166
     A                                  6  1MSGCON(99 DIV3361 SYMSGF)
     A                                  6100MSGCON(99 DIV3361 SYMSGF)
     A                                  7  3MSGCON(97 DIV3652 SYMSGF)
     A                                  7100MSGCON(85 YBC0014 SYMSGF)
     A                                  8  1MSGCON(99 DIV3361 SYMSGF)
     A                                  8100MSGCON(99 DIV3361 SYMSGF)
      *
     A          R DFRAKT                    SPACEB(1)
     A            BLBLNR        16         1
     A                                    17 '|'
     A            XAN1           5  0     19EDTCDE(Z)
     A                                    25 '|'
     A            XTX1          51        26
     A                                    77 '|'
     A            XVK1           9  0     78EDTCDE(Z)
     A            XM31           9  3     88EDTCDE(4)
     A                                    99 '|'
     A            XKOL2         30       100
     A                                   130 '|'
     A            XKOL3         13  2    131EDTCDE(M)
     A                                   146 '|'
     A            XKOL6         13  2    147EDTCDE(M)
     A                                   162 '|'
     A            XKOL4         20       163
     A            XKOL5         13  2    184EDTCDE(M)
     A                                      SPACEA(1)
     A            BLOPD          7  0      7EDTCDE(Z)
     A                                    17 '|'
     A            XAN2           5  0     19EDTCDE(Z)
     A                                    25 '|'
     A            XTX2          51        26
     A                                    77 '|'
     A            XVK2           9  0     78EDTCDE(Z)
     A            XM32           9  3     88EDTCDE(4)
     A                                    99 '|'
     A            XKOL22        30       100
     A                                   130 '|'
     A            XKOL32        13  2    131EDTCDE(M)
     A                                   146 '|'
     A            XKOL62        13  2    147EDTCDE(M)
     A                                   162 '|'
     A            XKOL42        20       163
     A            XKOL52        13  2    184EDTCDE(M)
      *
     A          R DFRAK2                    SPACEB(1)
     A            XKOL1         10         1
     A                                    17 '|'
     A            XAN            5  0     19EDTCDE(Z)
     A                                    25 '|'
     A            XTX           51        26
     A                                    77 '|'
     A            XVK            9  0     78EDTCDE(Z)
     A            XM3            9  3     88EDTCDE(4)
     A                                    99 '|'
     A            XKOL2         30       100
     A                                   130 '|'
     A            XKOL3         13  2    131EDTCDE(M)
     A                                   146 '|'
     A            XKOL6         13  2    147EDTCDE(M)
     A                                   162 '|'
     A            XKOL4         20       163
     A            XKOL5         13  2    184EDTCDE(M)
      *
     A          R BFRAKT                    SPACEB(1)
     A                                     1MSGCON(99 DIV3361 SYMSGF)
     A                                   100MSGCON(99 DIV3361 SYMSGF)
      *
     A          R TFRAKT                    SPACEB(1)
     A                                     1MSGCON(7 DIV3650 SYMSGF)
     A                                    17 '|'
     A            XTOTAN         5  0     19EDTCDE(Z)
     A                                    25 '|'
     A                                    77 '|'
     A            XTOTVK         9  0     78EDTCDE(Z)
     A            XTOTM3         9  3     88EDTCDE(4)
     A                                    99 '|'
     A  54        XVALUT         3       121
     A                                   130 '|'
     A            XTOTPP        13  2    131EDTCDE(M)
     A                                   146 '|'
     A            XTOTCC        13  2    147EDTCDE(M)
     A                                   162 '|'
     A                                      SPACEA(1)
     A                                     1MSGCON(99 DIV3361 SYMSGF)
     A                                   100MSGCON(99 DIV3361 SYMSGF)
