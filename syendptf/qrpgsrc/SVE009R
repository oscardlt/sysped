********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
12XXX * 01 YBC PTF12 IKKE DESIMAL VEKT NÅR VEKT > 1 KG
12062 * 02 CB  PTF12 Ta med fakturanummer
********************************************************************
     FSADV      IF   E           K DISK
N02  FSADF      IF   E           K DISK
N02  FSADH      IF   E           K DISK
     FTARIEU    IF   E           K DISK
     FHEADF     IF   E           K DISK
     FSVEV      O  A E             DISK
     D                 DS
     D  SVVNT                  1      8S 0
     D  SVVNT6                 1      6
      *//////////////
      *  Hodelogikk /
      *//////////////
     C                   EXSR      SRA
     C                   EXSR      SRB
     C                   MOVE      '1'           *INLR
      *////////////////////////////////////////////////////////////
      *  Initiering                                           SRA /
      *////////////////////////////////////////////////////////////
     C     SRA           BEGSR
      *_________________
      * Input parametrer
      *"""""""""""""""""
     C     *ENTRY        PLIST
     C                   PARM                    UVEV_SYAV
     C                   PARM                    UVEV_SYOP
      *___________________
      * Definiere Workfelt
      *"""""""""""""""""""
     C     *LIKE         DEFINE    SVEV_SYAV     UVEV_SYAV
     C     *LIKE         DEFINE    SVEV_SYOP     UVEV_SYOP
     C     *LIKE         DEFINE    SVAVD         USVAVD
     C     *LIKE         DEFINE    SVTDN         USVTDN
     C                   EVAL      USVAVD=UVEV_SYAV
     C                   EVAL      USVTDN=UVEV_SYOP
      *__________________
      * Definiere Nøkkler
      *""""""""""""""""""
     C     SVKEY         KLIST
     C                   KFLD                    USVAVD
     C                   KFLD                    USVTDN
      *_________________________________
      * Hæmta dagens datum och klockslag
      *"""""""""""""""""""""""""""""""""
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Bearbeide Formater                                   SRB /
      *////////////////////////////////////////////////////////////
     C     SRB           BEGSR
      * Oppdrag
     C     SVKEY         CHAIN     HEADF                              55
N02  C     SVKEY         CHAIN     SADH                               55
N02  C     SVKEY         CHAIN     SADF                               56
      * Varuposter
     C     SVKEY         SETLL     SADV
     C     SVKEY         READE     SADV                                   53
     C     *IN53         DOWEQ     *OFF
     C                   IF        SVVKTB > 0
     C                   CLEAR                   SVEVR
      *
     C                   EVAL      SVEV_SYAV=SVAVD
     C                   EVAL      SVEV_SYOP=SVTDN
     C                   EVAL      SVEV_SYLI=SVLI
     C                   EVAL      SVEV_KOTA=1
     C                   EVAL      SVEV_KOSL='PA'
     C                   EVAL      SVEV_VASL=SVVT01
     C                   EVAL      SVEV_VANO=SVLI
      * De 6 första tecknen är lika i EU
     C                   MOVEL     SVVNT6        SVEV_VATA
      * Finne x-ref ?
     C     SVVNT         CHAIN     TARIEU                             54
     C     *IN54         IFEQ      *OFF
     C     TARIEUE       ANDNE     *BLANKS
     C                   EVAL      SVEV_VATA=TARIEUE
     C                   ENDIF
     C                   EVAL      SVEV_ULKD=SVLK
     C                   EVAL      SVEV_EUP1='1000'
N01  C                   IF        SVVKTB > 1
N01  C                   Z-ADD(H)  SVVKTB        XN09              9 0
N01  C                   EVAL      SVEV_BRUT=XN09
N01  C                   ELSE
     C                   EVAL      SVEV_BRUT=SVVKTB
N01  C                   END
N01  C                   IF        SVVKTN > 1
N01  C                   Z-ADD(H)  SVVKTN        XN09
N01  C                   EVAL      SVEV_NETO=XN09
N01  C                   ELSE
     C                   EVAL      SVEV_NETO=SVVKTN
N01  C                   END
     C                   EVAL      SVEV_ANKV=SVNTM
     C                   EVAL      SVEV_FABL=SVBELT
     C                   EVAL      SVEV_STVA=SVBELT
     C  N55              MOVEL     HEGM1         SVEV_GODM
N02  C                   IF        *IN56
N02  C                   EVAL      SVEV_BIT1='N380'
N02  C                   MOVEL     SIFIF         SVEV_BII1
N02  C                   ENDIF
      *
     C                   WRITE     SVEVR
     C                   ENDIF
     C     SVKEY         READE     SADV                                   53
     C                   ENDDO
     C                   ENDSR
