      *
      ***********************************************************
      *** PROGRAM SKAL VEDLIKEHOLDE KURERMANIFEST - VARELINJE ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01 02 07 09-14 17-24             ***
      *** LEDIGE INDIKATORER 29 31 32       52-75 78-89       ***
      ***********************************************************
      *
     FSADKMVL1  UF A E           K DISK
     FTARI      IF   E           K DISK
     FSAD117D   CF   E             WORKSTN SFILE(SUBFIL:ZRECNO)
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D                SDS
     D  UUSR                 254    263
      *
     C                   EXSR      INIT
      *
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C                   EVAL      WSLI = 0
     C     SADKMVL1K     SETLL     SADKMVL1
     C     START1        TAG
     C     SADKMVL1K     READE(N)  SADKMVL1                               94
      *
     C     START2        TAG
      *
     C                   DOW       *IN94 = *OFF
     C                   EVAL      WSLI = MVLI
     C                   ADD       1             ZRECNO
     C                   WRITE     SUBFIL
     C     SADKMVL1K     READE(N)  SADKMVL1                               94
     C                   ENDDO
     C                   EVAL      WSLI = WSLI + 1                              NESTE LINJENR
      *
     C                   Z-ADD     ZRECNO        YRECNO            5 0
     C                   IF        ZRECNO > 19
     C                   EVAL      ZRECNO = 1
     C                   END
      *
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C                   WRITE     BUNN
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYVARE
     C                   ELSE
     C                   EXFMT     SUBCTL
     C                   END
      *
      *** AVSLUTT
     C                   MOVE      '0'           *IN30
     C                   MOVEA     '00'          *IN(98)
     C   03              Z-ADD     3             UFLAGG
     C   03              GOTO      SLUTT
     C   05              GOTO      START
     C     START4        TAG
     C   06              DO
     C                   EXSR      CRTLIN
     C                   GOTO      START
     C                   ENDDO
     C   12              Z-ADD     1             UFLAGG
     C   12              GOTO      SLUTT
      *
     C     ZRECNO        IFNE      0
     C                   EXSR      HENT
     C                   END
     C                   GOTO      START3
      *
     C     SLUTT         TAG
      *
     C                   SETON                                        LR
     C                   RETURN
      *
      ***********************************************
     C     HENT          BEGSR
      ***********************************************
     C                   MOVE      *BLANKS       MSGNR
     C                   MOVEA     '000'         *IN(90)
     C                   MOVEA     '00'          *IN(98)
     C     *IN98         DOWEQ     '0'                                          1<-B
     C                   READC     SUBFIL                                 98
     C     *IN98         IFEQ      '0'                                           2<-B
     C     MVVOEC        ANDNE     '* SLETTET *'
     C     ZRECNO        DIV       15            LINNBR
     C                   MVR                     XLIN              2 0
     C                   SELECT                                                   3<-B
     C     WSNR          WHENEQ    2                                              3
     C                   MOVE      '1'           *IN99
     C                   EXSR      CHGLIN
     C     WSNR          WHENEQ    4                                              3
     C                   MOVE      '1'           *IN99
     C                   EXSR      DLTLIN
     C                   ENDSL                                                    3<-E
     C                   END                                                     2<-E
     C                   ENDDO                                                  1<-E
      *
     C     SLHENT        TAG
     C   98
     CANN99              DO
     C                   MOVE      'DIV0012'     MSGNR
     C                   MOVE      '1'           *IN30
     C                   END
     C                   MOVE      '0'           *IN99
      *
     C                   ENDSR
      *
     C***********************************************
     C     CHGLIN        BEGSR
     C***********************************************
     C                   EXSR      MOVKMW
     C                   Z-ADD     25            LINNBR
     C                   Z-ADD     2             POSNBR
      *
     C     STCHG         TAG
     C                   EXFMT     VARELI
     C                   MOVEA     '000000'      *IN(85)
     C                   MOVE      '0'           *IN30
     C   04              DO
     C                   EXSR      SØKDTA
     C                   GOTO      STCHG
     C                   END
      *
     C     *IN12         IFEQ      '1'
     C                   UPDATE    SUBFIL
     C                   ELSE
      *** TESTE INNTASTEDE TADA. HVIS IKKE NOE FEIL, OPPDRATERE PÅ FIL
     C                   EXSR      TESTB1
     C   30              GOTO      STCHG
     C                   EXSR      OPDKMV
     C                   END
      *
     C     XLIN          IFEQ      0
     C                   Z-ADD     19            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     DLTLIN        BEGSR
     C***********************************************
     C     STDLT         TAG
     C                   EXFMT     VAREL2
      *
     C     *IN12         IFEQ      '1'
     C                   UPDATE    SUBFIL
     C                   ELSE
     C     SADKMVL1K2    CHAIN     SADKMVL1                           33
     C  N33              DELETE    SADKMVFR
     C                   EVAL      MVVOEC = '* SLETTET *'
     C                   END
      *
     C                   EVAL      WSNR = 0
     C                   UPDATE    SUBFIL
      *
     C     XLIN          IFEQ      0
     C                   Z-ADD     19            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
      *
     C                   ENDSR
      *
      ***********************************************
     C     CRTLIN        BEGSR
      ***********************************************
     C                   EXSR      MOVB2
     C                   Z-ADD     25            LINNBR
     C                   Z-ADD     2             POSNBR
      *
     C     STCRT         TAG
     C                   EXFMT     VARELI
     C                   MOVEA     '000000'      *IN(85)
     C                   MOVE      '0'           *IN30
      *
      *** F4  ---> SØKE DATA FOR ET ELLER ANNET FELT.
     C   04              DO
     C                   EXSR      SØKDTA
     C                   GOTO      STCRT
     C                   END
      *
      *** F12 --> AVBRYTTE REGISTRERING AV NYE DETALJE LINJER.
     C  N12              DO
      *** TESTE INNTASTEDE DATA. HVIS FEIL, SETTE IND. PÅ OG VISE
      *** FEILMELDING.
     C                   EXSR      TESTB1
     C   30              GOTO      STCRT
      *
      *** HVIS IKKE FEIL, OPPDATERE FIL, SUBFIL OG VISE DEN PÅ SKJERMEN
     C                   EXSR      OPDKMV
     C                   MOVEA     '11'          *IN(91)
     C                   WRITE     SUBCTL
      *
     C                   ENDDO
      *
     C                   ENDSR
      *
     C***********************************************
     C     TESTB1        BEGSR
     C***********************************************
     C                   IF        WSBL = 0
     C                   Z-ADD     25            LINNBR
     C                   Z-ADD     2             POSNBR
     C                   MOVE      'SPT0033'     MSGNR
     C                   MOVE      '1'           *IN30
     C                   MOVE      '1'           *IN90
     C                   END
      *
     C     WSVNT         CHAIN     TARI                               89
     C  N30              IF        *IN89
     C                   Z-ADD     25            LINNBR
     C                   Z-ADD     15            POSNBR
     C                   MOVE      'SPT0033'     MSGNR
     C                   MOVE      '1'           *IN30
     C                   ELSE
     C                   IF        WSVS = *BLANKS
     C                   EVAL      WSVS = TAALFA
     C                   END
     C                   END
      *
     C                   IF        WSVKTB = 0 AND WSVKTN = 0
     C                   MOVE      '1'           *IN88
     C                   IF        *IN30 = *OFF
     C                   Z-ADD     25            LINNBR
     C                   Z-ADD     24            POSNBR
     C                   MOVE      'SPV0001'     MSGNR
     C                   MOVE      '1'           *IN30
     C                   END
     C                   ELSE
     C                   SELECT
     C                   WHEN      WSVKTB <> 0 AND WSVKTN = 0
     C                   EVAL(H)   WSVKTN = WSVKTB * 0.9
     C                   WHEN      WSVKTB = 0 AND WSVKTN <> 0
     C                   EVAL(H)   WSVKTB = WSVKTN * 1.1
     C                   ENDSL
     C                   END
      *
     C     WSNT          COMP      0                                      87
     C  N30              IF        *IN87
     C                   Z-ADD     25            LINNBR
     C                   Z-ADD     46            POSNBR
     C                   MOVE      'YBC0824'     MSGNR
     C                   MOVE      '1'           *IN30
     C                   END
      *
     C                   IF        UVOEC = *BLANKS
     C     WSVOEC        COMP      *BLANKS                                86
     C  N30              IF        *IN86
     C                   Z-ADD     25            LINNBR
     C                   Z-ADD     52            POSNBR
     C                   MOVE      'YBC2077'     MSGNR
     C                   MOVE      '1'           *IN30
     C                   END
     C                   END
      *
     C     WSVS          COMP      *BLANKS                                85
     C  N30              IF        *IN85
     C                   Z-ADD     25            LINNBR
     C                   Z-ADD     70            POSNBR
     C                   MOVE      'YBC0840'     MSGNR
     C                   MOVE      '1'           *IN30
     C                   END
      *
     C     SLTB1         ENDSR
      *
     C***********************************************
     C     OPDKMV        BEGSR
     C***********************************************
     C     SADKMVL1K2    CHAIN     SADKMVL1                           33
      *** FLYTTE INNTASTET DATA TIL FIL-VARIABLER OG OPPDATERE DEM.
     C                   EXSR      MOVWKM
     C     *IN33         IFEQ      '0'
     C                   UPDATE    SADKMVFR
     C                   ELSE
     C                   WRITE     SADKMVFR
     C                   EVAL      WSLI = WSLI + 1
     C                   END
      *
     C                   EVAL      WSNR = 0
     C   40              UPDATE    SUBFIL
     C  N40              ADD       1             YRECNO
     C  N40              Z-ADD     YRECNO        ZRECNO
     C  N40              WRITE     SUBFIL
      *
     C                   ENDSR
      *
     C***********************************************
     C     MOVB2         BEGSR
     C***********************************************
     C                   EVAL      *IN40 = *OFF
     C                   EVAL      MVPRO  = UPRO
     C                   EVAL      MVCN   = UCN
     C                   EVAL      MVLI   = WSLI
     C                   EVAL      WSBL   = 0
     C                   EVAL      WSVNT  = 0
     C                   EVAL      WSVKTB = 0
     C                   EVAL      WSVKTN = 0
     C                   EVAL      WSNT   = 0
     C                   IF        WSVOEC = *BLANKS
     C                   EVAL      WSVOEC = UVOEC
     C                   END
     C                   EVAL      WSVS   = *BLANKS
      *
     C                   ENDSR
      *
     C***********************************************
     C     MOVWKM        BEGSR
     C***********************************************
     C                   EVAL      MVBL   = WSBL
     C                   EVAL      MVVNT  = WSVNT
     C                   EVAL      MVVKTB = WSVKTB
     C                   EVAL      MVVKTN = WSVKTN
     C                   EVAL      MVNT   = WSNT
     C                   EVAL      MVVOEC = WSVOEC
     C                   EVAL      MVVS   = WSVS
      *
     C                   ENDSR
      *
     C***********************************************
     C     MOVKMW        BEGSR
     C***********************************************
     C                   EVAL      *IN40 = *ON
     C*                  EVAL      WSLI   = MVLI
     C                   EVAL      WSBL   = MVBL
     C                   EVAL      WSVNT  = MVVNT
     C                   EVAL      WSVKTB = MVVKTB
     C                   EVAL      WSVKTN = MVVKTN
     C                   EVAL      WSNT   = MVNT
     C                   EVAL      WSVOEC = MVVOEC
     C                   EVAL      WSVS   = MVVS
      *
     C                   ENDSR
      *
     C***********************************************
     C     SØKDTA        BEGSR
     C***********************************************
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
      *
     C     LINNBR        IFEQ      25
     C     POSNBR        ANDGE     15
     C     POSNBR        ANDLE     22
     C                   MOVE      WSVNT         WSVNTI            8
     C                   CALL      'SAD010A'
     C                   PARM                    WSVNTI
     C                   PARM                    XIDAG
     C                   MOVE      WSVNTI        WSVNT
     C                   Z-ADD     24            POSNBR
     C                   GOTO      SLSØK
     C                   END
      *
     C                   MOVE      'DIV1037'     MSGNR
     C                   SETON                                        30
      *
     C     SLSØK         ENDSR
      *
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     SADKMVL1K     KLIST
     C                   KFLD                    UPRO
     C                   KFLD                    UCN
     C     SADKMVL1K2    KLIST
     C                   KFLD                    UPRO
     C                   KFLD                    UCN
     C                   KFLD                    MVLI
      *
     C     *ENTRY        PLIST
     C                   PARM                    UPRO              8 0
     C                   PARM                    UCN               2 0
     C                   PARM                    UVOEC            17
     C                   PARM                    UTYPE             3
     C                   PARM                    UFLAGG            1 0
      *
     C     UTYPE         COMP      'DSP'                                  49
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVE      WDT           XIDAG             8 0
      *
     C     *LIKE         DEFINE    MVLI          WSLI
      *
     C                   ENDSR
      *
