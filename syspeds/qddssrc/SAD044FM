     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPDOC('SAD04401' 'SAD044' 'SY400-
     A                                      LO/SADTVINN')
     A                                      HELP
     A                                      ALTHELP
      *
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  5  2VALUES(' ' '2' '4')
     A            STTTYP         1A  O  5  6
     A            STTNR          5  0O  5 11EDTCDE(4)
     A            STTDS         19A  O  5 20
     A            STTNV         15A  O  5 43
     A            STTAN          6A  O  5 66
     A            STTLK          2A  O  5 77
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0016)
     A                                      SFLPAG(0015)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A            ZRECNO         4S 0O  2 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYSPED/SAD044'
     A                                      COLOR(BLU)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                  1 70TIME
     A                                  1 19'Vedlikehold av transitteringsnr.'
     A                                      DSPATR(HI)
     A                                  3  2MSGCON(30 DIV0016 SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 34MSGCON(30 DIV0017 SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2'Op  Type Nestenr. Avgangstollsted -
     A                                             Avsender               Autor-
     A                                      i.nr. LK'
     A                                      DSPATR(HI)
      *
     A          R BUNN
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF03(03 'AVSLUTT')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      SLNO(23)
     A                                      CLRL(02)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2MSGCON(18 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A                                  1 22MSGCON(18 DIV0021 SYMSGF)
     A                                      COLOR(BLU)
     A                                  1 42MSGCON(18 DIV0022 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  2  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                              2 40'(C) COPYRIGHT  SYSTEMA A/S  1996  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R VARELI
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'    Type Nestenr. Avgangstollsted -
     A                                             Avsender               Autor-
     A                                      i.nr. LK'
     A                                      DSPATR(HI)
     A            WSTYP          1A  B  2  6DSPATR(UL)
     A  40                                  DSPATR(HI)
     A  40                                  DSPATR(PR)
     A  90                                  DSPATR(RI)
     A            WSTNR          5Y 0B  2 11DSPATR(UL)
     A  89                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A            WSDS          19A  B  2 20DSPATR(UL)
     A  88                                  DSPATR(RI)
     A            WSNV          15A  B  2 43DSPATR(UL)
     A  87                                  DSPATR(RI)
     A            WSAN           6A  B  2 66DSPATR(UL)
     A  86                                  DSPATR(RI)
     A            WSLK           2A  B  2 77DSPATR(UL)
     A  85                                  DSPATR(RI)
     A                                  3  2MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  4  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                              4 40'(C) COPYRIGHT  SYSTEMA A/S  1996  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R VAREL3
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A                                  1  2'    Type Nestenr. Avgangstollsted -
     A                                             Avsender               Autor-
     A                                      i.nr. LK'
     A                                      DSPATR(HI)
     A            STTTYP         1A  O  2  6DSPATR(UL)
     A                                      DSPATR(PC)
     A                                      DSPATR(HI)
     A            STTNR          5Y 0O  2 11DSPATR(UL)
     A                                      DSPATR(HI)
     A                                      EDTCDE(4)
     A            STTDS         19A  O  2 20DSPATR(UL)
     A                                      DSPATR(HI)
     A            STTNV         15A  O  2 43DSPATR(UL)
     A                                      DSPATR(HI)
     A            STTAN          6A  O  2 66DSPATR(UL)
     A                                      DSPATR(HI)
     A            STTLK          2A  O  2 77DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  3  2MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 40'(C) COPYRIGHT  SYSTEMA A/S  1996  '
     A                                      COLOR(WHT)
      *
     A          R NYVARE
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF03(03 'AVSLUTT')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/SAD044'
     A                                      COLOR(BLU)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                  1 69TIME
     A                                  1 19'Vedlikehold av transitteringsnr.'
     A                                      DSPATR(HI)
     A                                  3  2MSGCON(30 DIV0016 SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 34MSGCON(30 DIV0017 SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2'Op  Type Nestenr. Avgangstollsted -
     A                                             Avsender               Autor-
     A                                      i.nr. LK'
     A                                      DSPATR(HI)
     A                                 23  2MSGCON(18 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(18 DIV0021 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 42MSGCON(18 DIV0022 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                             24 40'(C) COPYRIGHT  SYSTEMA A/S  1996  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
