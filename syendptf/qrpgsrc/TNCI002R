********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
12163 * 03 CB  PTF12 Signera direkt
12XXX * 04 YBC PTF12 Nummer i SVNSTD er neste oppdragsnr nummer
12XXX * 05 CB  PTF12 NCTS 5
      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/TNCI002R MODULE(*LIBL/TNCI002R *LIBL/INCGI)
CRTPGM*        ACTGRP(*NEW) AUT(*USE)
      *
      *********************************************************************
      /copy SYSPEDS/qrpgsrcpy,hspecs
      /copy SYSPEDS/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FSVNH      UF A E           K DISK    USROPN
     F                                     PREFIX(X_)
     FSVNH05    IF   E           K DISK    USROPN
     F                                     RENAME(SVNHR:SVNHR05)
     F                                     PREFIX(X05_)
     FSVNH10    IF   E           K DISK    USROPN
     F                                     RENAME(SVNHR:SVNHR10)
     F                                     PREFIX(X10_)
     FSVNSTD    UF   E           K DISK    USROPN
     F                                     PREFIX(X_)
     FSVTHA2    IF   E           K DISK    USROPN
     F                                     RENAME(SVTHAR:SVTHAR2)
     FSVTHA     IF   E           K DISK    USROPN
     FSVNV      IF A E           K DISK    USROPN
     FSVNVCN    IF A E           K DISK    USROPN
     FSVNVFS    IF A E           K DISK    USROPN
     FGODSFI    IF   E           K DISK    USROPN
     FGODSGFSE  UF A E           K DISK    USROPN
     FGODSJF    O  A E           K DISK    USROPN
N03  FEDIC      UF   E             DISK    USROPN
N03  FSVTFI     IF   E             DISK    USROPN
N03  FEDIM      O  A E             DISK    USROPN
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy SYSPEDS/qrpgsrcpy,prototypeb
      /copy SYSPEDS/qrpgsrcpy,usec
      /copy SYSPEDS/qrpgsrcpy,variables3
      *
      * Varible for
     D WSUSER          S             10
     D TIAVD_A         S              4
     D TIAVD           S              4  0
     D TITDN_A         S              7
     D TITDN           S              7  0
     D NEWSYAVA        S              4
     D NEWAVD          S              4  0
     D NEWOPD          S              7  0
     D NEWSIGN         S              3
     D WMODE           S              2
     D WSIGN           S              3
     D TISG            S              3
     D TIST            S              1
     D TIDT_A          S              8
     D TIDT            S              8  0
     D TIENKL          S              1
     D TITRNR          S             18
     D TIGN            S             35
     D TIGNSK          S              2
     D TIALSK          S             17
     D TIALS           S             35
     D TIALSS          S              2
     D TIGLSK          S             17
     D TIACTS          S             17
     D TISKB           S              2
     D TITSB           S              8
     D TIALK           S              2
     D TITARF          S             16
     D TIKN_A          S              8
     D TIKN            S              8  0
     D TINA            S             30
     D TIAD1           S             30
     D TIPN            S              9
     D TIPS            S             24
     D TILK            S              2
     D TISK            S              2
     D TITIN           S             17
     D TIMF            S              3
     D TIMP_A          S              1
     D TIMP            S              1  0
     D TIKDV           S              1
     D TI0035          S              1
     D TIWS            S             10
     D TITUID          S             14
     D TISGDK          S              6
     D TISGME          S              3
     D TISGUT          S              6
     D TISGID          S             10
     D TISGDT          S             12
     D TIBYTE_A        S              7
     D TIBYTE          S              7  0
N05  D TIVERS          S              1
     D JSONstring      S          32000
     D Error           S            150
N03   *   SYEDI data area
N03  D EDIDTA          DS           256
N03  D  UELIBF               171    180

      *get input-string
      /copy syspeds/qrpgsrcpy,prolog3

      * Parse input
     C                   EXSR      Parse
      * Open files etc
     C                   EXSR      INIT
      *
     c                   callp     gethtmlifs(
     C                             '/syspeddev/html/JSON.html':
     C                             '/CGITAG/')
     C                   callp     wrtsection('top')
      * Teste input og utfør
     C  N50              EXSR      TEST

      * ............................................................................................
      * skriv JSON-Object start..(alltid '{' + x ant param. m/komma mellom (IKKE komma etter siste))
      * ............................................................................................
      *
      /free
        JSONstring='{'
        +'¬user¬ : ¬'+%trim(WSUSER)+'¬, '
        +'¬tiavd¬ : ¬'+%trim(%editc(TIAVD:'Z'))+'¬, '
        +'¬titdn¬ : ¬'+%trim(%editc(TITDN:'Z'))+'¬, '
        +'¬newavd¬ : ¬'+%trim(%editc(NEWAVD:'Z'))+'¬, '
        +'¬newopd¬ : ¬'+%trim(%editc(NEWOPD:'Z'))+'¬, '
        +'¬newsign¬ : ¬'+%trim(NEWSIGN)+'¬, '
        +'¬mode¬ : ¬'+%trim(WMODE)+'¬, '
        +'¬tituid¬ : ¬'+%trim(TITUID)+'¬, '
        +'¬tisg¬ : ¬'+%trim(TISG)+'¬, '
        +'¬tist¬ : ¬'+%trim(TIST)+'¬, '
        +'¬tidt¬ : ¬'+%trim(%editc(TIDT:'Z'))+'¬, '
        +'¬tienkl¬ : ¬'+%trim(TIENKL)+'¬, '
        +'¬titrnr¬ : ¬'+%trim(TITRNR)+'¬, '
        +'¬tign¬ : ¬'+%trim(TIGN)+'¬, '
        +'¬tignsk¬ : ¬'+%trim(TIGNSK)+'¬, '
        +'¬tialsk¬ : ¬'+%trim(TIALSK)+'¬, '
        +'¬tials¬ : ¬'+%trim(TIALS)+'¬, '
        +'¬tialss¬ : ¬'+%trim(TIALSS)+'¬, '
        +'¬tiglsk¬ : ¬'+%trim(TIGLSK)+'¬, '
        +'¬tiacts¬ : ¬'+%trim(TIACTS)+'¬, '
        +'¬tiskb¬ : ¬'+%trim(TISKB)+'¬, '
        +'¬titsb¬ : ¬'+%trim(TITSB)+'¬, '
        +'¬tialk¬ : ¬'+%trim(TIALK)+'¬, '
        +'¬titarf¬ : ¬'+%trim(TITARF)+'¬, '
        +'¬tikn¬ : ¬'+%trim(%editc(TIKN:'Z'))+'¬, '
        +'¬tina¬ : ¬'+%trim(TINA)+'¬, '
        +'¬tiad1¬ : ¬'+%trim(TIAD1)+'¬, '
        +'¬tipn¬ : ¬'+%trim(TIPN)+'¬, '
        +'¬tips¬ : ¬'+%trim(TIPS)+'¬, '
        +'¬tilk¬ : ¬'+%trim(TILK)+'¬, '
        +'¬tisk¬ : ¬'+%trim(TISK)+'¬, '
        +'¬titin¬ : ¬'+%trim(TITIN)+'¬, '
        +'¬timf¬ : ¬'+%trim(TIMF)+'¬, '
        +'¬timp¬ : ¬'+%trim(%editc(TIMP:'Z'))+'¬, '
        +'¬tikdv¬ : ¬'+%trim(TIKDV)+'¬, '
        +'¬ti0035¬ : ¬'+%trim(TI0035)+'¬, '
        +'¬tisgdk¬ : ¬'+%trim(TISGDK)+'¬, '
        +'¬tisgme¬ : ¬'+%trim(TISGME)+'¬, '
        +'¬tisgut¬ : ¬'+%trim(TISGUT)+'¬, '
        +'¬tisgid¬ : ¬'+%trim(TISGID)+'¬, '
        +'¬tisgdt¬ : ¬'+%trim(TISGDT)+'¬, '
        +'¬tibyte¬ : ¬'+%trim(%editc(TIBYTE:'Z'))+'¬, '
        +'¬tivers¬ : ¬'+%trim(TIVERS)+'¬, '
        +'¬errMsg¬ : ¬'+%trim(Error)+'¬, ';
      /end-free
      * JSONfix escaper " i tekst,  for deretter å bytte ¬->"
     c                   call      'JSONFIX'
     c                   parm                    JSONstring
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')
      *
      * ............................................................................................
      * Skriv JSON-LISTE Start (en liste er EN parameter(fra [ til   )
      * ............................................................................................
     c                   eval      JSONstring ='"orderupdate" : ['
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')
      * ............................................................................................
      * Skriv JSON-Liste/Array  Avslutning
      * ............................................................................................
     c                   eval      JSONstring =']'
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')
      * ............................................................................................
      * Skriv JSON-string Avsluttning
      * ............................................................................................
     c                   eval      JSONstring ='}'
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')
      *
      * Quit
     C                   exsr      Exit


      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Lukk fil
     C                   CLOSE     BRIDF
     C  N50              CLOSE     SVNH
     C  N50              CLOSE     SVNH05
     C  N50              CLOSE     SVNH10
     C  N50              CLOSE     SVNSTD
     C  N50              CLOSE     SVTHA
     C  N50              CLOSE     SVTHA2
N03  C  N50              CLOSE     EDIC
N03  C  N50              CLOSE     SVTFI
N03  C  N50              CLOSE     EDIM

      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   MOVE      *ON           *INLR
     C                   return
     C                   endsr
      *
      *********************************************************
     C     Parse         Begsr
      *********************************************************
      * Get input
     C                   EVAL      WSUSER   = zhbgetvar('USER')
     C                   EVAL      TIAVD_A  = zhbgetvar('TIAVD')
     C                   EVAL      TIAVD    = c2n2(TIAVD_A)
     C                   EVAL      TITDN_A  = zhbgetvar('TITDN')
     C                   EVAL      TITDN    = c2n2(TITDN_A)
     C                   EVAL      NEWSYAVA = zhbgetvar('NEWAVD')
     C                   EVAL      NEWAVD   = c2n2(NEWSYAVA)
     C                   EVAL      NEWSIGN  = zhbgetvar('NEWSIGN')
     C                   EVAL      WMODE    = zhbgetvar('MODE')
     C                   EVAL      TISG     = zhbgetvar('TISG')
     C                   EVAL      WSIGN    = zhbgetvar('SIGN')
     C                   EVAL      TIST     = zhbgetvar('TIST')
     C                   EVAL      TIDT_A   = zhbgetvar('TIDT')
     C                   EVAL      TIDT     = c2n2(TIDT_A)
     C                   EVAL      TIENKL   = zhbgetvar('TIENKL')
     C*                  EVAL      TITARF   = zhbgetvar('TITARF')
     C                   EVAL      TIWS     = zhbgetvar('TIWS')
     C                   EVAL      TITRNR   = zhbgetvar('TITRNR')
     C                   EVAL      TIGN     = zhbgetvar('TIGN')
     C                   EVAL      TIGNSK   = zhbgetvar('TIGNSK')
     C                   EVAL      TIALSK   = zhbgetvar('TIALSK')
     C                   EVAL      TIALS    = zhbgetvar('TIALS')
     C                   EVAL      TIALSS   = zhbgetvar('TIALSS')
     C                   EVAL      TIGLSK   = zhbgetvar('TIGLSK')
     C                   EVAL      TIACTS   = zhbgetvar('TIACTS')
     C                   EVAL      TISKB    = zhbgetvar('TISKB')
     C                   EVAL      TITSB    = zhbgetvar('TITSB')
     C                   EVAL      TIALK    = zhbgetvar('TIALK')
     C                   EVAL      TIKN_A   = zhbgetvar('TIKN')
     C                   EVAL      TIKN     = c2n2(TIKN_A)
     C                   EVAL      TINA     = zhbgetvar('TINA')
     C                   EVAL      TIAD1    = zhbgetvar('TIAD1')
     C                   EVAL      TIPN     = zhbgetvar('TIPN')
     C                   EVAL      TIPS     = zhbgetvar('TIPS')
     C                   EVAL      TILK     = zhbgetvar('TILK')
     C                   EVAL      TISK     = zhbgetvar('TISK')
     C                   EVAL      TITIN    = zhbgetvar('TITIN')
     C                   EVAL      TIMF     = zhbgetvar('TIMF')
     C                   EVAL      TIMP_A   = zhbgetvar('TIMP')
     C                   EVAL      TIMP     = c2n2(TIMP_A)
     C                   EVAL      TIKDV    = zhbgetvar('TIKDV')
     C*                  EVAL      TI0035   = zhbgetvar('TI0035')
     C*                  EVAL      TITUID   = zhbgetvar('TITUID')
     C*                  EVAL      TISGDK   = zhbgetvar('TISGDK')
     C*                  EVAL      TISGME   = zhbgetvar('TISGME')
     C*                  EVAL      TISGUT   = zhbgetvar('TISGUT')
     C*                  EVAL      TISGID   = zhbgetvar('TISGID')
     C*                  EVAL      TISGDT   = zhbgetvar('TISGDT')
     C*                  EVAL      TIBYTE_A = zhbgetvar('TIBYTE')
     C*                  EVAL      TIBYTE   = c2n2(TIBYTE_A)
N05  C                   EVAL      TIVERS   = zhbgetvar('TIVERS')
     c                   ENDSR
      *********************************************************
     C     INIT          Begsr
      *********************************************************
     C                   OPEN      BRIDF
      * Test innlogging
     C     WSUSER        CHAIN     BRIDF                              50
     C  N50BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C                   call      BILIBL
     C                   end
      *
     C     SVNHKey       klist
     C                   kfld                    TIAVD
     C                   kfld                    TITDN
     C     SVNHKeyG      klist
     C                   kfld                    GM_TIAVD
     C                   kfld                    GM_TITDN
     C     *LIKE         DEFINE    X_TIAVD       GM_TIAVD
     C     *LIKE         DEFINE    X_TITDN       GM_TITDN
      *
     C   50              eval      Error = 'Invalid userID'
     C  N50              OPEN      SVNH
     C  N50              OPEN      SVNH05
     C  N50              OPEN      SVNH10
     C  N50              OPEN      SVNSTD
     C  N50              OPEN      SVTHA
     C  N50              OPEN      SVTHA2
     C  N50              OPEN      GODSFI
     C  N50              OPEN      GODSGFSE
     C  N50              OPEN      GODSJF
N03  C  N50              OPEN      EDIC
N03  C  N50              OPEN      SVTFI
N03  C  N50              OPEN      EDIM
N03   * Hæmta firmaupplysningar
N03  C  N501             CHAIN     SVTFI                              51
N03   * Åpne dataarea
N03  C     *DTAARA       DEFINE                  EDIDTA
N03  C                   IN        EDIDTA
N03   * Hæmta næsta meddelandenummer
N03  C                   IF        WMODE='S'
N03  C                   IF        *IN50=*OFF
N03  C     1             CHAIN     EDIC                               51
N03  C                   ADD       1             CMN
N03  C                   UPDATE    EDICR
N03  C                   ENDIF
N03  C                   ENDIF
      *_________________________________
      * Hæmta dagens datum och klockslag
      *"""""""""""""""""""""""""""""""""
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C  N50TISG          CHAIN     SVTHA                              33
     C                   MOVEL     SVTH_NAMN     TITARF

     c                   endsr
      *
      *______________________________________________________
      * TEST                                             TEST
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     TEST          BEGSR
      * Hæmta userid
     C     WSUSER        CHAIN     SVTHA2
      * Hæmta ærende
     C     WMODE         IFNE      'A'
     C     SVNHKey       CHAIN     SVNH                               55
     C                   ENDIF
      * MODE A=Add  D=Delete  U=Update
     C                   SELECT
     C     WMODE         WHENEQ    'A'
     C                   eval      Error = *blanks
     C                   EXSR      SRA
     C     WMODE         WHENEQ    'C'
     C                   IF        *IN55=*ON
     C                   eval      Error = 'Order not found'
     C                   ELSE
     C                   UPDATE    SVNHR
     C                   eval      Error = *blanks
     C                   EXSR      SRC
     C                   ENDIF
     C     WMODE         WHENEQ    'D'
     C                   IF        *IN55=*ON
     C                   eval      Error = 'Order not found'
     C                   ELSE
     C                   eval      Error = *blanks
     C                   EXSR      SRD
     C                   ENDIF
     C     WMODE         WHENEQ    'S'
     C                   IF        *IN55=*ON
     C                   eval      Error = 'Order not found'
     C                   ELSE
     C                   eval      Error = *blanks
     C                   EXSR      SRS
     C                   ENDIF
     C     WMODE         WHENEQ    'U'
     C                   IF        *IN55=*ON
     C                   eval      Error = 'Order not found'
     C                   ELSE
     C                   eval      Error = *blanks
      * Test om godsnr finnes fra før
     C     TIGN          CHAIN     SVNH10                             33
     C                   SELECT
     C                   WHEN      *IN33
     C                   EVAL      *IN33 = *OFF
     C                   WHEN      X10_TIAVD <> TIAVD OR X10_TITDN <> TITDN
     C                   eval      Error = 'Goods number used by another order '
     C                             + '(' + %trim(%editc(X10_TIAVD:'Z'))
     C                             + '/' + %trim(%editc(X10_TITDN:'Z')) + ')'
     C                   ENDSL
      * Test om MRN-nr finnes fra før
     C                   IF        *IN33 = *OFF
     C     TITRNR        CHAIN     SVNH05                             33
     C                   SELECT
     C                   WHEN      *IN33
     C                   EVAL      *IN33 = *OFF
     C                   WHEN      X05_TIAVD <> TIAVD OR X05_TITDN <> TITDN
     C                   eval      Error = 'MRN number used by another order '
     C                             + '(' + %trim(%editc(X05_TIAVD:'Z'))
     C                             + '/' + %trim(%editc(X05_TITDN:'Z')) + ')'
     C                   ENDSL
     C                   END
     C                   IF        *IN33 = *OFF
     C                   EXSR      SRU
     C                   END
     C                   ENDIF
     C                   OTHER
     C                   eval      Error = 'Wrong mode'
     C                   ENDSL
     C                   ENDSR
      *______________________________________________________
      * Add                                               SRA
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     SRA           BEGSR
     C                   CLEAR                   SVNHR
      * Hæmta uppdragsnummer
     C     TIAVD         CHAIN     SVNSTD                             52
     C                   ADD       1             X_TITDN
     C                   UPDATE    SVNSTDR
N04  C                   SUB       1             X_TITDN
     C                   EVAL      X_TIAVD    = TIAVD
     C                   EVAL      TIST     = ' '
     C                   EVAL      X_TIST   = ' '
     C                   EVAL      X_TISG   = WSIGN
     C                   MOVEL     WDT           X_TIDT
     C                   MOVEL     WDT           TIDT
      *
      * Hæmta tullid
     C                   CALL      'SVG001R'
     C                   PARM                    WTUID            10
     C                   MOVE      UYEAR         XÅR               2
     C                   EVAL      X_TITUID = 'SE' + XÅR + WTUID
      * Write
     C                   WRITE     SVNHR
      * Oppdater WEB-variabler
     C                   EVAL      TITDN  = X_TITDN
     C                   EVAL      TITUID = X_TITUID
      *
     C                   ENDSR
      *______________________________________________________
      * Copy                                              SRC
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     SRC           BEGSR
     C                   EVAL      WSIGN = X_TISG
      * Hæmta uppdragsnummer
     C     NEWAVD        CHAIN     SVNSTD                             52
     C                   ADD       1             X_TITDN
     C                   UPDATE    SVNSTDR
N04  C                   SUB       1             X_TITDN
     C                   EVAL      NEWOPD   = X_TITDN
      *
     C                   EVAL      GM_TIAVD = TIAVD
     C                   EVAL      GM_TITDN = TITDN
     C     SVNHKeyG      CHAIN(N)  SVNH                               55
     C                   EVAL      X_TIAVD  = NEWAVD
     C                   EVAL      X_TITDN  = NEWOPD
     C                   EVAL      TIAVD    = NEWAVD
     C                   EVAL      TITDN    = NEWOPD
     C                   EVAL      X_TIST   = ' '
     C                   EVAL      X_TISG   = NEWSIGN
     C                   MOVEL     WDT           X_TIDT
     C                   MOVEL     WDT           TIDT
     C                   EVAL      X_TITRNR = *BLANKS
     C                   EVAL      X_TIGN   = *BLANKS
     C                   EVAL      X_TIGNSK = *BLANKS
     C                   EVAL      X_TIALSK = *BLANKS
     C                   EVAL      X_TIALS  = *BLANKS
     C                   EVAL      X_TIALSS = *BLANKS
     C                   EVAL      X_TIGLSK = *BLANKS
     C                   EVAL      X_TIACTS = *BLANKS
     C                   EVAL      X_TISKB  = *BLANKS
     C                   EVAL      X_TITSB  = *BLANKS
     C                   EVAL      X_TIALK  = *BLANKS
     C                   EVAL      X_TIKN   = 0
     C                   EVAL      X_TINA   = *BLANKS
     C                   EVAL      X_TIAD1  = *BLANKS
     C                   EVAL      X_TIPN   = *BLANKS
     C                   EVAL      X_TIPS   = *BLANKS
     C                   EVAL      X_TILK   = *BLANKS
     C                   EVAL      X_TISK   = *BLANKS
     C                   EVAL      X_TITIN  = *BLANKS
     C                   EVAL      X_TIMF   = *BLANKS
     C                   EVAL      X_TIMP   = 0
     C                   EVAL      X_TISGDK = *BLANKS
     C                   EVAL      X_TISGME = *BLANKS
     C                   EVAL      X_TISGUT = *BLANKS
     C                   EVAL      X_TISGID = *BLANKS
     C                   EVAL      X_TISGDT = *BLANKS
     C                   EVAL      X_TIBYTE = 0
N05  C                   EVAL      X_TIVERS = TIVERS
      * Hæmta tullid
     C                   CALL      'SVG001R'
     C                   PARM                    WTUID            10
     C                   MOVE      UYEAR         XÅR               2
     C                   EVAL      X_TITUID = 'SE' + XÅR + WTUID
      * Write
     C                   WRITE     SVNHR
      * Oppdater WEB-variabler
     C                   EVAL      TISG     = X_TISG
     C                   EVAL      TIST     = X_TIST
     C                   EVAL      TIDT     = X_TIDT
     C                   EVAL      TITUID   = X_TITUID
     C                   EVAL      TIENKL   = X_TIENKL
     C                   EVAL      TITARF   = X_TITARF
     C                   EVAL      TIWS     = X_TIWS
     C                   EVAL      TITRNR   = X_TITRNR
     C                   EVAL      TIGN     = X_TIGN
     C                   EVAL      TIGNSK   = X_TIGNSK
     C                   EVAL      TIALSK   = X_TIALSK
     C                   EVAL      TIALS    = X_TIALS
     C                   EVAL      TIALSS   = X_TIALSS
     C                   EVAL      TIGLSK   = X_TIGLSK
     C                   EVAL      TIACTS   = X_TIACTS
     C                   EVAL      TISKB    = X_TISKB
     C                   EVAL      TITSB    = X_TITSB
     C                   EVAL      TIALK    = X_TIALK
     C                   EVAL      TITARF   = X_TITARF
     C                   EVAL      TIKN     = X_TIKN
     C                   EVAL      TINA     = X_TINA
     C                   EVAL      TIAD1    = X_TIAD1
     C                   EVAL      TIPN     = X_TIPN
     C                   EVAL      TIPS     = X_TIPS
     C                   EVAL      TILK     = X_TILK
     C                   EVAL      TISK     = X_TISK
     C                   EVAL      TITIN    = X_TITIN
     C                   EVAL      TIMF     = X_TIMF
     C                   EVAL      TIMP     = X_TIMP
     C                   EVAL      TIKDV    = X_TIKDV
     C                   EVAL      TI0035   = X_TI0035
     C                   EVAL      TISGDK   = X_TISGDK
     C                   EVAL      TISGUT   = X_TISGUT
     C                   EVAL      TISGID   = X_TISGID
     C                   EVAL      TISGDT   = X_TISGDT
     C                   EVAL      TIBYTE   = X_TIBYTE
N05  C                   EVAL      TIVERS   = X_TIVERS
      * Kopiera också varuposter
     C                   OPEN      SVNV
     C                   OPEN      SVNVFS
     C                   OPEN      SVNVCN

     C     SVNHKeyG      SETLL     SVNV
     C     SVNHKeyG      READE     SVNV                                   52
     C     *IN52         DOWEQ     *OFF
     C                   EVAL      TVAVD = NEWAVD
     C                   EVAL      TVTDN = NEWOPD
     C                   WRITE     SVNVR
     C     SVNHKeyG      READE     SVNV                                   52
     C                   ENDDO

     C     SVNHKeyG      SETLL     SVNVFS
     C     SVNHKeyG      READE     SVNVFS                                 52
     C     *IN52         DOWEQ     *OFF
     C                   EVAL      TVAVD = NEWAVD
     C                   EVAL      TVTDN = NEWOPD
     C                   WRITE     SVNVFSR
     C     SVNHKeyG      READE     SVNVFS                                 52
     C                   ENDDO

     C     SVNHKeyG      SETLL     SVNVCN
     C     SVNHKeyG      READE     SVNVCN                                 52
     C     *IN52         DOWEQ     *OFF
     C                   EVAL      TVAVD = NEWAVD
     C                   EVAL      TVTDN = NEWOPD
     C                   WRITE     SVNVCNR
     C     SVNHKeyG      READE     SVNVCN                                 52
     C                   ENDDO

     C                   CLOSE     SVNV
     C                   CLOSE     SVNVFS
     C                   CLOSE     SVNVCN
     C                   CLOSE     GODSFI
     C                   CLOSE     GODSGFSE
     C                   CLOSE     GODSJF

     C                   ENDSR
      *______________________________________________________
      * Delete                                            SRD
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     SRD           BEGSR
     C                   EVAL      X_TIST    = 'D'
     C                   EVAL      TIST      = 'D'
     C                   UPDATE    SVNHR
     C                   ENDSR
      *______________________________________________________
      * Skicka                                            SRS
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     SRS           BEGSR
     C                   eval      Error = *blanks
     C                   UPDATE    SVNHR
      *____________________
      * Skapa edifact + fil
      *""""""""""""""""""""
     C                   CALL      'SVX063R'
     C                   PARM                    TIAVD
     C                   PARM                    TITDN
     C                   PARM      '007'         X1N07             3
     C                   PARM                    UFLAGG            1 0
N03   *_____________
N03   * Ændra status
N03   *"""""""""""""
N03  C     SVNHKEY       CHAIN     SVNH
N03  C                   EVAL      X_TIST='A'
N03  C                   UPDATE    SVNHR
N03   *_______________
N03   * Skriv til EDIM
N03   *"""""""""""""""
N03  C                   CLEAR                   EDIMR
N03  C                   EVAL      M0004=SVTF_0004
N03  C                   EVAL      M0010=SVTF_0010
N03  C                   EVAL      M0035=SVTF_0035
N03  C                   MOVEL     X_TIAVD       W0062            11
N03  C                   MOVE      X_TITDN       W0062
N03  C                   EVAL      M0062=W0062
N03  C                   EVAL      M0065='CUSDEC'
N03  C                   EVAL      M1001='STI'
N03  C                   EVAL      M1004=X_TITUID
N03  C                   EVAL      M0068=X_TITUID
N03  C                   IF        X_NIKONF = 1 OR X_NIMN1 <> *BLANKS
N03  C                   MOVEL     '044'         M1N07
N03  C                   MOVEL     '044'         M1225
N03  C                   ELSE
N03  C                   MOVEL     '007'         M1N07
N03  C                   MOVEL     '007'         M1225
N03  C                   END
N03  C                   EVAL      MMN=CMN
N03  C                   EVAL      MSR='S'
N03  C                   MOVEL     WDT           MDT
N03  C                   MOVEL     WTM           MTM
N03  C                   EVAL      MAVD=X_TIAVD
N03  C                   EVAL      MTDN=X_TITDN
N03
N03  C                   WRITE     EDIMR
     C                   ENDSR
      *______________________________________________________
      * Update                                            SRU
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     SRU           BEGSR
      *                  EVAL      X_TISG   = TISG
     C                   EVAL      X_TIST   = TIST
      *                  EVAL      X_TIDT   = TIDT
      *                  EVAL      X_TITUID = TITUID
     C                   EVAL      X_TIENKL = TIENKL
     C                   EVAL      X_TITARF = TITARF
     C                   EVAL      X_TIWS   = TIWS
     C                   EVAL      X_TITRNR = TITRNR
      * Hent neste godsnr
     C                   IF        %SUBST(TIGN:1:3) <> *BLANKS AND
     C                             %SUBST(TIGN:4:4) = *BLANKS
     C                   EVAL      %SUBST(TIGN:4:1) = '-'
     C                   EVAL      %SUBST(TIGN:5:2) = %SUBST(WDT:3:2)
     C                   EVAL      %SUBST(TIGN:7:1) = '-'
     C                   END
     C                   IF        %SUBST(TIGN:1:7) <> *BLANKS AND
     C                             %SUBST(TIGN:8:5) = *BLANKS
     C                   EVAL      GFLBKO = %SUBST(TIGN:1:3)
     C     GFLBKO        CHAIN     GODSFI                             33
     C                   IF        *IN33 = *OFF
     C                   EVAL      GSEGN1 = %SUBST(TIGN:1:7)
     C     GSEGN1        CHAIN     GODSGFSE                           33
     C                   IF        *IN33
     C                   Z-ADD     2             XNR05             5 0
     C                   MOVE      XNR05         GSEGN2
     C                   WRITE     GODSGFSER
     C                   ELSE
     C                   MOVE      GSEGN2        XNR05
     C                   ADD       1             XNR05
     C                   MOVE      XNR05         GSEGN2
     C                   UPDATE    GODSGFSER
     C                   END
     C                   SUB       1             XNR05
     C                   MOVE      XNR05         GSEGN2
     C                   EVAL      %SUBST(TIGN:8:5) = GSEGN2
     C                   EVAL      GOGN   = TIGN
     C                   EVAL      GOTRNR = TITRNR
     C                   EVAL      GOTRDT = TIDT
     C                   EVAL      GOGRDT = TIDT
     C                   WRITE     GODSJR
     C                   END
     C                   EVAL      *IN33 = *OFF
     C                   END
      *
     C                   EVAL      X_TIGN   = TIGN
     C                   EVAL      X_TIGNSK = TIGNSK
     C                   EVAL      X_TIALSK = TIALSK
     C                   EVAL      X_TIALS  = TIALS
     C                   EVAL      X_TIALSS = TIALSS
     C                   EVAL      X_TIGLSK = TIGLSK
     C                   EVAL      X_TIACTS = TIACTS
     C                   EVAL      X_TISKB  = TISKB
     C                   EVAL      X_TITSB  = TITSB
     C                   EVAL      X_TIALK  = TIALK
     C                   EVAL      X_TIKN   = TIKN
     C                   EVAL      X_TINA   = TINA
     C                   EVAL      X_TIAD1  = TIAD1
     C                   EVAL      X_TIPN   = TIPN
     C                   EVAL      X_TIPS   = TIPS
     C                   EVAL      X_TILK   = TILK
     C                   EVAL      X_TISK   = TISK
     C                   EVAL      X_TITIN  = TITIN
     C*                  EVAL      X_TIMF   = TIMF
     C                   EVAL      X_TIMF   = '007'
     C                   EVAL      X_TIMP   = TIMP
     C                   EVAL      X_TIKDV  = TIKDV
     C*                  EVAL      X_TI0035 = TI0035
     C*                  EVAL      X_TISGDK = TISGDK
     C*                  EVAL      X_TISGUT = TISGUT
     C*                  EVAL      X_TISGID = TISGID
     C*                  EVAL      X_TISGDT = TISGDT
     C*                  EVAL      X_TIBYTE = TIBYTE
N05  C                   EVAL      X_TIVERS = TIVERS
     C                   UPDATE    SVNHR
     C                   ENDSR
