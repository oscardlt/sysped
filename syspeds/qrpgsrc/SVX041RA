     FSVXH      UF   E           K DISK
     FSVXHE     UF   E           K DISK
     FSVTHA     IF   E           K DISK
     FEDIC      UF   E             DISK
     FEDIM      O  A E             DISK
     FSVTFI     IF   E             DISK
      *//////////////
      *  Hodelogikk /
      *//////////////
     C                   EXSR      SRA
     C                   EXSR      SRB
     C                   MOVE      '1'           *INLR
      *////////////////////////////////////////////////////////////
      *  Initiering                                           SRA /
      *////////////////////////////////////////////////////////////
     C     SRA           BEGSR
      *____________________
      * Definiere variabler
      *""""""""""""""""""""
     C     *LIKE         DEFINE    THAVD         XAVD
     C     *LIKE         DEFINE    THTDN         XTDN
     C     *LIKE         DEFINE    THSGDK        XSGDK
     C     *LIKE         DEFINE    THSGUT        XSGUT
      *_________________
      * Input parametere
      *"""""""""""""""""
     C     *ENTRY        PLIST
     C                   PARM                    XAVD
     C                   PARM                    XTDN
      *__________________
      * Definiere Nøkkler
      *""""""""""""""""""
     C     SVXHK         KLIST
     C                   KFLD                    XAVD
     C                   KFLD                    XTDN
      *_________________________________
      * Hæmta dagens datum och klockslag
      *"""""""""""""""""""""""""""""""""
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
      *________________________
      * Hæmta firmaupplysningar
      *""""""""""""""""""""""""
     C     1             CHAIN     SVTFI                              51
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Bearbeide Formater                                   SRB /
      *////////////////////////////////////////////////////////////
     C     SRB           BEGSR
     C     SVXHK         CHAIN(N)  SVXH                               90
     C                   EVAL      XSGDK=THSGDK
     C                   EVAL      XSGUT=THSGUT
      * Sigiller via keypad eller automatsik ?
     C     THSG          CHAIN     SVTHA
     C*    SVTH_DKEYN    IFNE      *BLANKS
     C*                  EXSR      SRCC
     C*                  ENDIF
     C     SVXHK         CHAIN     SVXH                               90
     C                   EVAL      THSGDK=XSGDK
     C                   EVAL      THSGUT=XSGUT
     C                   UPDATE    SVXHR
      * Slett edifact
     C     SVXHK         SETLL     SVXHE
     C     SVXHK         READE     SVXHE                                  51
     C     *IN51         DOWEQ     *OFF
     C                   DELETE    SVXHER
     C     SVXHK         READE     SVXHE                                  51
     C                   ENDDO
      * Lag edifact pånytt med riktig sigill
     C     THSGDK        IFNE      *BLANKS
     C     THSGDK        ANDNE     '000000'
     C                   CALL      'SVX101R'
     C                   PARM                    XAVD
     C                   PARM                    XTDN
     C                   PARM                    THMF

     C                   ENDIF
      *_____________
      * Ændra status
      *"""""""""""""
     C     SVXHK         CHAIN     SVXH                               52
     C                   EVAL      THST='A'
     C                   UPDATE    SVXHR
      *_____________________________
      * Hæmta næsta meddelandenummer
      *"""""""""""""""""""""""""""""
     C     1             CHAIN     EDIC                               52
     C                   ADD       1             CMN
     C                   UPDATE    EDICR
      *_______________
      * Skriv til EDIM
      *"""""""""""""""
     C                   CLEAR                   EDIMR
     C                   EVAL      M0004=SVTF_0004
     C                   EVAL      M0010=SVTF_0010
     C                   EVAL      M0035=SVTF_0035
     C                   MOVEL     THAVD         W0062            11
     C                   MOVE      THTDN         W0062
     C                   EVAL      M0062=W0062
     C                   EVAL      M0065='CUSDEC'
     C                   EVAL      M1001='STE'
     C                   EVAL      M0068=THTUID
     C                   EVAL      M1004=THTUID
     C                   EVAL      M1225=THMF
     C                   EVAL      M1N07=THMF
     C                   EVAL      MMN=CMN
     C                   EVAL      MSR='S'
     C                   MOVEL     WDT           MDT
     C                   MOVEL     WTM           MTM
     C                   EVAL      MAVD=THAVD
     C                   EVAL      MTDN=THTDN

     C                   WRITE     EDIMR

     C     SRB9          TAG
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Berækna utstællarsigill                             SRCC /
      *////////////////////////////////////////////////////////////
     C     SRCC          BEGSR
      * Send oppdrag til sigillering
     C                   MOVEL     XAVD          QTFIEL
     C                   MOVE      XTDN          QTFIEL
     C                   CALL      'QSNDDTAQ'
     C                   PARM      'SVGSRQ'      QTNAME           10
     C                   PARM      '*LIBL'       QTLIB            10
     C                   PARM      11            QTLEN             5 0
     C                   PARM                    QTFIEL           11
      * Sigillering
     C                   CALL      'SEALCTL'
     C                   PARM      'SVG003RX  '  QPARM            10
      * Retur fra sigillering
     C                   MOVEL     XAVD          QKEY
     C                   MOVE      XTDN          QKEY
     C                   CALL      'QRCVDTAQ'
     C                   PARM      'SVGSSQ'      QRNAME           10
     C                   PARM      '*LIBL'       QRLIB            10
     C                   PARM      6             QRLEN             5 0
     C                   PARM                    XSGUT
     C                   PARM      99            QWAIT             5 0
     C                   PARM      'EQ'          QORDER            2
     C                   PARM      11            QKILEN            3 0
     C                   PARM                    QKEY             11
     C                   PARM      10            QSNDRL            3 0
     C                   PARM                    QSNDR            10
     C                   ENDSR
