      *********************************************************************
      * for å kompiler :   SYEXCEL i libl +  ovrdbf X_MRAP LLAGJF
      *
CRTPG+*  CRTPGM SYSPED/SYL022E  MODULE(*LIBL/SYL022E *LIBL/INCGI)
CRTPGM*        ACTGRP(CGI) AUT(*USE)
      *
      *
      *********************************************************************
      * MAIN PROGRAM FLOW
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
     FBRIDF     IF   E           K DISK    USROPN
     FLLAGJL07  IF   E           K DISK    USROPN
     FX_MRAP    O  a E             DISK    usropn
     F                                     RENAME(LLAGJR:X_MRAPR)
      *--------------------------------------------------------------------
      * Prototype defintions and standard system API error structure
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      *--------------------------------------------------------------------
      * Variables common to CGI programs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
      * Variables received from the input string
     D WSUSER          S             10
     D UsrSpcName      s             10
     D XXMRNR          S              5
     D XXMRNRN         S              5  0
     D IFSFIL          S             30
     D Dialog          S              1
     D CMD             S            250
      * Number of records matching search criteria
      *
     D Lib             s             10
     D Fn              s             10
     D Mbr             s             10
      *
     D Spaces          s             12a   inz('&nbsp;&nbsp;')
      *--------------------------------------------------------------------
      * Prolog common to CGI programs
      * -Receive input from the remote browser
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prolog3
      *=====================================================================******
      * MAIN PROCESS LINE
      *=====================================================================******
      * Parse input
     C                   exsr      Parse
      *
     C                   EXSR      INIT
      **
      ** Ved behov for dialog FØR spool kjøres legges den her...
     C     Dialog        IFeq      'J'
     c                   else
      *
      * ALT OK - aktuell jobb skal kjøres...
      *  Kall prog for å generere html-fil eller PDF-fil i IFS.
     C                   move      XXMRNRN       XXMRNRA           5
     C                   eval      IFSFil = 'Mrapport'+XXMRNRA+'.xls'
     c                   exsr      LagFil
      * Set output skeleton html member name
      *-dummy html for å få kjørt prog                                      ******
      * "bytter ut seg selv" med Html-fil i IFS umiddelbart                 ******
     C                   exsr      SetSklSpool
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
     C                   callp     updhtmlvar('IFSFIL':IFSFIL)
     C                   callp     wrtsection('top')
     c                   end
      *------------------                                                   ******
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
     C*
     C                   close     BRIDF
     C                   close     LLagjl07
     C                   eval      *inlr = *on
     C                   return
      *=====================================================================******
      * Parse input
      *=====================================================================******
     C     Parse         begsr
     C                   EVAL      WSUSER  = zhbgetvar('USER')
     C                   EVAL      UsrSpcName  = zhbgetvar('UsrSpcName')
     C                   EVAL      XXMRNR  = zhbgetvar('WSMRNR')
     C                   EVAL      XXMRNRN = c2n2(XXMRNR)
     C                   eval      Dialog = zhbgetvar('Dialog')
      *
      *
     C                   endsr
      *=====================================================================******
      * Set html output skeleton member before its read into memory
      *=====================================================================******
     C     SetSklSpool   begsr
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC'
     C                   eval      Mbr = 'SPLHTMDUM'
     C                   endsr
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
     C                   OPEN      BRIDF
     C     WSUSER        CHAIN     BRIDF                              50
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   CALLB     'INCGI'
     C                   PARM                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
     C                   OPEN      LLagjl07
     C     LLKEY         klist
     C                   kfld                    GRP               8 0
     C                   kfld                    BIKUNR
     C                   kfld                    XXMRNRN
      *
     C                   endsr
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     LAGFIL        begsr
     c
     C                   eval      rc = docmd(' +
     C                             dltf FILE(*LIBL/X_MRAP)  +
     C                             ')
     C                   eval      rc = docmd(' +
     C                             CRTDUPOBJ OBJ(LLAGJF) FROMLIB(*LIBL) +
     C                             OBJTYPE(*FILE) TOLIB(SYCGI) NEWOBJ(X_MRAP) +
     C                             ')
     C                   OPEN      X_MRAP
     C     LLKey         setll     LLAGJL07
     C     LLKey         reade     LLAGJL07                               33
     C     *in33         doweq     '0'
     C                   write     X_MRAPR
     C     LLKey         reade     LLAGJL07                               33
     C                   end
      * OBS!!! Close fila FØR prog for å konvertere den til excel..
      *  (hvis ikke for du ikke med postene... ennå i "I/O-buffer"??)
     C                   Close     X_MRAP
      *
      * konvertere fil til excel via kommando i syexcel
     C                   eval      rc = docmd(' +
     C                             ADDLIBLE   LIB(SYEXCEL)  +
     C                             ')
     C                   eval      cmd='XLCRT2 FILE(*LIBL/X_MRAP) XLSPATH(' +
     C                             %trim(IFSFil) +')'
     C                   eval      rc = docmd(cmd)
     C                   eval      rc = docmd(' +
     C                             RMVLIBLE   LIB(SYEXCEL)  +
     C                             ')
      *
     C                   eval      rc = docmd(' +
     C                             dltf FILE(*LIBL/X_MRAP)  +
     C                             ')
     C                   endsr
