     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A          R SUBFIL                    SFL
     A            WSFIRM    R        H      REFFLD(TRABR/TBFIRM *LIBL/TRAB)
SV5  A*           TBDAY     R        O  6 55REFFLD(TRABR/TBDAY *LIBL/TRAB)
SV5  A*           TBMONT    R        O  6 58REFFLD(TRABR/TBMONT *LIBL/TRAB)
SV5  A*           TBYEAR    R        O  6 61REFFLD(TRABR/TBYEAR *LIBL/TRAB)
     A            TBDATE    R        H      REFFLD(TRABR/TBDATE *LIBL/TRAB)
     A            WSNR           1A  B  6  2VALUES(' ' '2' '4')
     A            TBLIN     R        O  6  5REFFLD(TRABR/TBLIN *LIBL/TRAB)
     A            TBTKAT    R        O  6 12REFFLD(TRABR/TBTKAT *LIBL/TRAB)
     A            TBUNIT    R        O  6 16REFFLD(TRABR/TBUNIT *LIBL/TRAB)
     A            TBAVD     R        O  6 30REFFLD(TRABR/TBAVD *LIBL/TRAB)
     A                                      EDTCDE(4)
     A            TBGEB     R        O  6 38REFFLD(TRABR/TBGEB *LIBL/TRAB)
     A            TBLK      R        O  6 45REFFLD(TRABR/TBLK *LIBL/TRAB)
     A            TBOPTY    R        O  6 50REFFLD(TRABR/TBOPTY *LIBL/TRAB)
     A            TBVKTT    R        O  6 54REFFLD(TRABR/TBVKTT *LIBL/TRAB)
     A                                      EDTCDE(4)
     A            WBDATE         6Y 0O  6 61EDTCDE(Y)
     A            TBPRO     R        O  6 71REFFLD(TRABR/TBPRO *LIBL/TRAB)
     A                                      EDTCDE(J)
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                  1  2'SYSPED/SYTR05'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(045 DIV3301 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                  1 67DATE
     A                                      EDTCDE(Y)
     A            WSFIRM    R        O  2  2REFFLD(TRABR/TBFIRM *LIBL/TRAB)
     A                                      COLOR(WHT)
     A            WSNAVN        30A  O  2 11COLOR(WHT)
     A                                  2 67TIME
     A            ZRECNO         4S 0O  2 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  3  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(030 DIV0017 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  2MSGCON(078 JOV1448 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A          R BUNN
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF03(03 'EXIT')
     A                                      CF12(03 'EXIT')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  3  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 42MSGCON(018 DIV0022 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 62MSGCON(018 DIV0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  98                              4  2MSGCON(078 DIV0012 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A          R VARELI
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF12(12 'Forrige')
     A                                      CF03(12)
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1  2MSGCON(078 JOV1448 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            WSLIN     R        B  2  5REFFLD(TRABR/TBLIN *LIBL/TRAB)
     A  40                                  DSPATR(HI)
     A  40                                  DSPATR(PR)
     A  90N40                               DSPATR(RI)
     A            WSTKAT    R        B  2 12REFFLD(TRABR/TBTKAT *LIBL/TRAB)
     A N39                                  DSPATR(PR)
     A            WSUNIT    R        B  2 16REFFLD(TRABR/TBUNIT *LIBL/TRAB)
     A            WSAVD     R        B  2 30REFFLD(TRABR/TBAVD *LIBL/TRAB)
     A                                      EDTCDE(4)
     A            WSGEB     R        B  2 38REFFLD(TRABR/TBGEB *LIBL/TRAB)
     A            WSLK      R        B  2 45REFFLD(TRABR/TBLK *LIBL/TRAB)
     A            WSOPTY    R        B  2 50REFFLD(TRABR/TBOPTY *LIBL/TRAB)
     A            WSVKTT    R        B  2 54REFFLD(TRABR/TBVKTT *LIBL/TRAB)
     A                                      EDTCDE(4)
SV5  A*           WSDAY     R        B  2 55REFFLD(TRABR/TBDAY *LIBL/TRAB)
SV5  A*                                     EDTCDE(4)
SV5  A* 90                                  DSPATR(RI)
SV5  A*           WSMONT    R        B  2 58REFFLD(TRABR/TBMONT *LIBL/TRAB)
SV5  A*                                     EDTCDE(4)
SV5  A* 90                                  DSPATR(RI)
SV5  A*           WSYEAR    R        B  2 61REFFLD(TRABR/TBYEAR *LIBL/TRAB)
SV5  A*                                     EDTCDE(4)
SV5  A* 90                                  DSPATR(RI)
     A            WSDATE         6Y 0B  2 62EDTCDE(4)
     A  90                                  DSPATR(RI)
     A            WSPRO     R        B  2 71REFFLD(TRABR/TBPRO *LIBL/TRAB)
     A                                      EDTCDE(J)
     A  90                                  DSPATR(RI)
     A                                  3  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  4  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A          R VAREL2
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF12(12 'Forrige')
     A                                      CF03(12)
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2MSGCON(078 JOV0126 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            W2LIN          3Y 0B  2  4EDTCDE(4)
     A          R VAREL3
     A*%%TS  SD  20080511  184016  JOVO        REL-V5R4M0  5722-WDS
     A                                      CF12(12 'Forrige')
     A                                      CF03(12)
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
SV5  A*           TBDAY     R        O  2 55REFFLD(TRABR/TBDAY *LIBL/TRAB)
SV5  A*                                     COLOR(WHT)
SV5  A*           TBMONT    R        O  2 58REFFLD(TRABR/TBMONT *LIBL/TRAB)
SV5  A*                                     COLOR(WHT)
SV5  A*           TBYEAR    R        O  2 61REFFLD(TRABR/TBYEAR *LIBL/TRAB)
SV5  A*                                     COLOR(WHT)
     A            TBDATE    R        H      REFFLD(TRABR/TBDATE *LIBL/TRAB)
     A                                  1  2MSGCON(078 JOV1448 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            TBLIN     R        O  2  5REFFLD(TRABR/TBLIN *LIBL/TRAB)
     A                                      COLOR(WHT)
     A            TBTKAT    R        O  2 12REFFLD(TRABR/TBTKAT *LIBL/TRAB)
     A                                      COLOR(WHT)
     A            TBUNIT    R        O  2 16REFFLD(TRABR/TBUNIT *LIBL/TRAB)
     A                                      COLOR(WHT)
     A            TBAVD     R        O  2 30REFFLD(TRABR/TBAVD *LIBL/TRAB)
     A                                      COLOR(WHT)
     A            TBGEB     R        O  2 38REFFLD(TRABR/TBGEB *LIBL/TRAB)
     A                                      COLOR(WHT)
     A            TBLK      R        O  2 45REFFLD(TRABR/TBLK *LIBL/TRAB)
     A                                      COLOR(WHT)
     A            TBOPTY    R        O  2 50REFFLD(TRABR/TBOPTY *LIBL/TRAB)
     A                                      COLOR(WHT)
     A            TBVKTT    R        O  2 54REFFLD(TRABR/TBVKTT *LIBL/TRAB)
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A            WBDATE         6Y 0O  2 61EDTCDE(Y)
     A                                      COLOR(WHT)
     A            TBPRO     R        O  2 71REFFLD(TRABR/TBPRO *LIBL/TRAB)
     A                                      EDTCDE(J)
     A                                      COLOR(WHT)
     A                                  3  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A          R NYWRK
     A                                      CF03(03 'EXIT')
     A                                      CF12(03)
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                  1  2'SYSPED/SYTR05'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(045 DIV3301 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                  1 67DATE
     A                                      EDTCDE(Y)
     A            WSFIRM    R        O  2  2REFFLD(TRABR/TBFIRM *LIBL/TRAB)
     A                                      COLOR(WHT)
     A            WSNAVN        30A  O  2 11COLOR(WHT)
     A                                  2 67TIME
     A                                  3  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(030 DIV0017 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  2MSGCON(078 JOV1448 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 42MSGCON(018 DIV0022 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 62MSGCON(018 DIV0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A          R BILDE1
     A                                      CF03(03 'EXIT')
     A                                      CF12(03)
     A                                      CF04(04 'SEARCH')
     A                                      CF06(06 'GENERELLE PRISAVTALER')
     A                                  1  2'SYSPED/SYTR05'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(045 DIV3301 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                  1 67DATE
     A                                      EDTCDE(Y)
     A                                  2 67TIME
     A                                  4  1MSGCON(018 DIV3303 *LIBL/SYMSGF)
     A            WSFIRM    R        B  4 20REFFLD(TRABR/TBFIRM *LIBL/TRAB)
     A                                      COLOR(WHT)
     A            WSNAVN        30A  O  4 32COLOR(WHT)
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(018 DIV0032 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 42MSGCON(018 JOV1449 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  44        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
