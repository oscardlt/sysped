      * SEND IFCSUM BASERT PÅ TRANSP-MODUL OPTIMALISERT INNLAND..
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
11XXX * 01 JOV PTF11 TSR 'sikret' Varme/farlig / innført Lengde/adviser...
11XXX * 02 JOV PTF11 TSR skriver kode både til T7085(som før) og T4065 (som ved IFTMIN..)
********************************************************************
     FTURER     IF   E           K DISK
     FHEAD11    IF   E           K DISK
     FFIRM      IF   E             DISK
     FCUNDL01   IF   E           K DISK
     FDOKUFM    IF   E           K DISK
     FDOK29     IF   E           K DISK
     FFREETXT2  IF   E           K DISK
     FTRAN      IF   E           K DISK
     FHETRIU    IF   E           K DISK
     FKOSBU     IF   E           K DISK
     FECNI93A   O  A E             DISK
     FEDTM93A   O  A E             DISK
     FEFTX93A   O  A E             DISK
     FEGID93A   O  A E             DISK
     FENAD93A   O  A E             DISK
     FEMEA93A   O  A E             DISK
     FEPCI93A   O  A E             DISK
     FERFF93A   O  A E             DISK
     FETDT93A   O  A E             DISK
     FETSR93A   O  A E             DISK
     FEMOA93A   O  A E             DISK
     FETOD93A   O  A E             DISK
      *___________________________
      * Arrays for string handling
      *"""""""""""""""""""""""""""
     D TX              S             70    DIM(99)
     D                 DS
     D  WTIME                  1     14  0
     D  WTID                   1      6  0
     D  WDD                    7      8  0
     D  WMM                    9     10  0
     D  WÅÅ                   11     14  0
     D                 DS
     D  WDATO                  1      8  0
     D  WÅ                     1      4  0
     D  WM                     5      6  0
     D  WD                     7      8  0
     D                 DS
     D  W1004                  1     12
     D  WUAVD                  1      4  0
     D  WUPRO                  5     12  0
     D  WUKNA                 13     20  0
     D  WUMN                  21     29  0
     D                 DS
     D  W1004B                 1     20
     D  HXSNDN                 1     20
     D                 DS
     D  HEADS3                 1     30
     D  XAVSPN                 1      4
     D  XAVSST                 6     30
     D                 DS
     D  HEADK3                 1     30
     D  XMOTPN                 1      4
     D  XMOTST                 6     30
     D                 DS
     D  WKUNDN                 1      8  0
     D                 DS
     D  WADR                   1     30
     D  W3251                  1      5
     D  W3164A                 6     10
     D  W3164B                11     30
     D  W3164                  6     30
      *____________________________________
      * DS for leftjustify numericel fields
      *""""""""""""""""""""""""""""""""""""
     D                 DS
     D  ØN153                  1     15  3
     D  ØA153H                 1     12
     D  ØA153X                 5     12
     D  ØA153D                13     15
     D                 DS
     D  ØA12                   1     12
     D  Ø12                    1     12
     D                                     DIM(12)                              Num edit
     D                 DS
     D  HEVS1                  1     25
     D  HEVS1A                 1      3
     D  HEVS1B                 5     25
     D                 DS
     D  HEVS2                  1     25
     D  HEVS2A                 1      3
     D  HEVS2B                 5     25
N01  D                 DS
N01  D  TRFA11                 1      7
N01  D  WSVARM                 1      1
N01  D                 DS
N01  D  TRFA21                 1      7
N01  D  WSFARL                 1      1
N01  D                 DS
N01  D  TRFA12                 1      4
N01  D  WSLENG                 1      1
N01  D                 DS
N01  D  TRFA22                 1      4
N01  D  WSADVI                 1      1
      */////////////
      * MAIN LOGIC /
      */////////////
     C                   EXSR      SRINIT
     C                   EXSR      SRA                                          CONSIGNMENT
     C                   EXSR      SRB                                          TRIP
     C                   MOVE      '1'           *INLR
      *////////////////////////////////////////////////////////////
      * SR for init                                        SRINIT /
      *////////////////////////////////////////////////////////////
     C     SRINIT        BEGSR
      * INPUT PARAMETERS
     C     *ENTRY        PLIST
     C                   PARM                    WAVD              4
     C                   PARM                    WPRO              8
     C                   PARM                    WKNA              8
     C                   PARM                    WMN               9
     C                   PARM                    WANTO
     C                   PARM                    WBRUDD            1
     C                   MOVE      WAVD          WUAVD
     C                   MOVE      WPRO          WUPRO
     C                   MOVE      WKNA          WUKNA
     C                   MOVE      WMN           WUMN
     C                   Z-ADD     0             WANTO
      * HENTE DAGENS DATO OG TID
     C                   TIME                    WTIME
     C                   Z-ADD     WÅÅ           WÅ
     C                   Z-ADD     WDD           WD
     C                   Z-ADD     WMM           WM
      * INIT AV ARBEIDSFELT
     C     *LIKE         DEFINE    CREP0         WREP0
     C     *LIKE         DEFINE    CREP1         WREP1
     C     *LIKE         DEFINE    CREP2         WREP2
     C     *LIKE         DEFINE    CREP3         WREP3
     C     *LIKE         DEFINE    CREP4         WREP4
     C     *LIKE         DEFINE    CREP5         WREP5
     C     *LIKE         DEFINE    CREP6         WREP6
     C     *LIKE         DEFINE    CREP7         WREP7
     C     *LIKE         DEFINE    CREP8         WREP8
     C                   Z-ADD     0             ØX                2 0                      --I
     C                   MOVE      *BLANKS       ØAN              12            Num alpha
     C                   Z-ADD     1             WEPOS1            4 0
     C                   READ      FIRM                                   51
      * DEF AV KEYS
     C     TURKEY        KLIST
     C                   KFLD                    WUAVD
     C                   KFLD                    WUPRO
     C     KUNKEY        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    WKUNDN
     C     VFKEY         KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C                   KFLD                    TISLAG
     C                   MOVE      'VF'          TISLAG
     C     TRAKEY        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    TUKNT2
     C     FREE2KEY      KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C                   KFLD                    FRTKOD
     C     R29KEY        KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C                   KFLD                    D29FNR
     C                   z-add     1             D29FNR
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR CONSIGNEMENT INFORMATION                           SRA /
      *////////////////////////////////////////////////////////////
     C     SRA           BEGSR
      *_____________
      * Get tripinfo
      *"""""""""""""
     C     TURKEY        CHAIN     TURER                              51
     C* ER GODSLISTEMOTTAKER BÅDE AGENT OG TRANSPORTØR???
     C                   MOVE      'N'           AGTRAN            1
     C     *IN51         IFEQ      '0'
     C     WBRUDD        ANDNE     'T'
     C     TRAKEY        CHAIN     TRAN                               51
     C     *IN51         IFEQ      '0'
     C     VMTRKU        ANDEQ     WUKNA
     C                   MOVE      'J'           AGTRAN            1
     C                   END
     C                   END
      *
     C     WUPRO         SETLL     HEAD11                                 20
     C     *IN20         IFEQ      '1'
     C     WUPRO         READE     HEAD11                                 51
     C     *IN51         DOWEQ     '0'
     C     HEST          IFNE      'S'
     C     HEUR          IFEQ      'H'
      *
     C     WBRUDD        IFEQ      'J'
     C     HEKNA         ANDNE     WUKNA
     C                   GOTO      SRA1
     C                   ENDIF
      *
     C                   ADD       1             WANTO             5 0
     C                   ADD       HENT          WENT              9 0
     C                   ADD       HEVKT         WEVKT            11 0
     C                   ADD       HELM          WELM              7 2
     C                   ADD       HEM3          WEM3             11 3
      *
     C                   Z-ADD     WANTO         WREP0
     C                   EXSR      CNI15
      *
     C                   ENDIF
     C                   ENDIF
     C     SRA1          TAG
     C     WUPRO         READE     HEAD11                                 51
     C  N51              ENDDO
     C                   ENDIF
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR TRIP ONFORMATION                                   SRB /
      *////////////////////////////////////////////////////////////
     C     SRB           BEGSR
     C                   EXSR      RFF01
     C                   EXSR      TDT05
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  CNI15 /
      *////////////////////////////////////////////////////////////
     C     CNI15         BEGSR
     C                   Z-ADD     0             WREP1
     C                   Z-ADD     0             WREP2
     C                   Z-ADD     0             WREP3
     C                   Z-ADD     0             WREP4
     C                   Z-ADD     0             WREP5
     C                   Z-ADD     0             WREP6
     C                   Z-ADD     0             WREP7
     C                   Z-ADD     0             WREP8
     C                   CLEAR                   CNIR
      *_______________
      * Other segments
      *"""""""""""""""
     C                   EXSR      TSR15
     C                   EXSR      MOA15
     C                   EXSR      FTX15
     C                   EXSR      TOD17
     C                   EXSR      RFF18
     C                   EXSR      NAD25
     C                   EXSR      GID31
      *______
      * CNI15
      *""""""
      * 1490 Consolidation item number
     C                   Z-ADD     WANTO         ØN153
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           C1490
      * 1004 Document/message number
     C                   MOVEL     W1004B        C1004
      * Write record
     C                   Z-ADD     WUMN          CMN
     C                   Z-ADD     15            CGN
     C                   Z-ADD     WREP0         CREP0
     C                   WRITE     CNIR
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  DTM01 /
      *////////////////////////////////////////////////////////////
     C     DTM01         BEGSR
     C                   Z-ADD     1             WREP1
     C                   Z-ADD     0             WREP2
     C                   Z-ADD     0             WREP3
     C                   Z-ADD     0             WREP4
     C                   Z-ADD     0             WREP5
     C                   Z-ADD     0             WREP6
     C                   Z-ADD     0             WREP7
     C                   Z-ADD     0             WREP8
     C                   CLEAR                   DTMR
     C                   Z-ADD     WUMN          DMN
     C                   Z-ADD     1             DGN
     C                   Z-ADD     WREP0         DREP0
     C                   Z-ADD     WREP1         DREP1
      *
     C                   MOVEL     '171'         D2005
     C                   MOVEL     TUDT          D2380
     C                   MOVEL     '102'         D2379
     C                   Z-ADD     WREP1         DREP1
     C                   WRITE     DTMR
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  DTM05 /
      *////////////////////////////////////////////////////////////
     C     DTM05         BEGSR
     C                   Z-ADD     1             WREP1
     C                   Z-ADD     0             WREP2
     C                   Z-ADD     0             WREP3
     C                   Z-ADD     0             WREP4
     C                   Z-ADD     0             WREP5
     C                   Z-ADD     0             WREP6
     C                   Z-ADD     0             WREP7
     C                   Z-ADD     0             WREP8
     C                   CLEAR                   DTMR
     C                   Z-ADD     WUMN          DMN
     C                   Z-ADD     5             DGN
     C                   Z-ADD     WREP0         DREP0
     C                   Z-ADD     WREP1         DREP1
      *_______________
      * Departure date
      *"""""""""""""""
     C     TUDT          IFGT      0
     C                   MOVEL     '186'         D2005
     C                   MOVEL     TUDT          D2380
     C     D2380         CAT       '0000':0      D2380
     C                   MOVEL     '203'         D2379
     C                   Z-ADD     WREP1         DREP1
     C                   WRITE     DTMR
     C                   ENDIF
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  FTX31 /
      *////////////////////////////////////////////////////////////
     C     FTX31         BEGSR
     C                   Z-ADD     1             WREP2
     C                   Z-ADD     0             WREP3
     C                   Z-ADD     0             WREP4
     C                   Z-ADD     0             WREP5
     C                   Z-ADD     0             WREP6
     C                   Z-ADD     0             WREP7
     C                   Z-ADD     0             WREP8
     C                   CLEAR                   FTXR
     C                   Z-ADD     WUMN          FMN
     C                   Z-ADD     31            FGN
     C                   Z-ADD     WREP0         FREP0
     C                   Z-ADD     WREP1         FREP1
     C                   Z-ADD     WREP2         FREP2
      *__________________
      * Goods description
      *""""""""""""""""""
     C                   MOVEL     'AAA'         F4451
     C                   MOVEL     HEVS1B        F4440A
     C                   WRITE     FTXR
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  GID31 /
      *////////////////////////////////////////////////////////////
     C     GID31         BEGSR
     C                   Z-ADD     1             WREP1
     C                   Z-ADD     0             WREP2
     C                   Z-ADD     0             WREP3
     C                   Z-ADD     0             WREP4
     C                   Z-ADD     0             WREP5
     C                   Z-ADD     0             WREP6
     C                   Z-ADD     0             WREP7
     C                   Z-ADD     0             WREP8
      *_______________
      * Other segments
      *"""""""""""""""
     C     HEVS1B        IFNE      *BLANKS
     C                   EXSR      FTX31
     C                   END
     C                   EXSR      MEA34
     C                   EXSR      PCI37
      *___________________
      * Goods item details
      *"""""""""""""""""""
     C                   CLEAR                   GIDR
     C                   MOVEL     '99999'       G1496                          goods item no
     C                   Z-ADD     HENT          ØN153
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           G7224A
     C                   MOVEL     'KLL'         G7065A                         type of pack
     C                   Z-ADD     WUMN          GMN
     C                   Z-ADD     31            GGN
     C                   Z-ADD     WREP0         GREP0
     C                   Z-ADD     WREP1         GREP1
     C                   WRITE     GIDR
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  MEA34 /
      *////////////////////////////////////////////////////////////
     C     MEA34         BEGSR
     C                   Z-ADD     1             WREP2
     C                   Z-ADD     0             WREP3
     C                   Z-ADD     0             WREP4
     C                   Z-ADD     0             WREP5
     C                   Z-ADD     0             WREP6
     C                   Z-ADD     0             WREP7
     C                   Z-ADD     0             WREP8
      *_________________________
      * Consignment gross weight
      *"""""""""""""""""""""""""
     C                   CLEAR                   MEAR
     C                   Z-ADD     WUMN          MMN
     C                   Z-ADD     34            MGN
     C                   Z-ADD     WREP0         MREP0
     C                   Z-ADD     WREP1         MREP1
     C                   Z-ADD     WREP2         MREP2
     C                   MOVEL     'WT '         M6311
     C                   MOVEL     'G  '         M6313
     C                   MOVEL     'KGM'         M6411
     C                   Z-ADD     HEVKT         ØN153
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           M6314
     C                   WRITE     MEAR
      *_______
      * Volume
      *"""""""
     C     HEM3          IFGT      0
     C                   CLEAR                   MEAR
     C                   Z-ADD     WUMN          MMN
     C                   Z-ADD     34            MGN
     C                   Z-ADD     WREP0         MREP0
     C                   Z-ADD     WREP1         MREP1
     C                   ADD       1             WREP2
     C                   Z-ADD     WREP2         MREP2
     C                   MOVEL     'VOL'         M6311
     C                   MOVEL     'MTQ'         M6411
     C                   Z-ADD     HEM3          ØN153
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           M6314
     C                   WRITE     MEAR
     C                   ENDIF
      *_______________
      * Loading meters
      *"""""""""""""""
     C     HELM          IFGT      0
     C                   CLEAR                   MEAR
     C                   Z-ADD     WUMN          MMN
     C                   Z-ADD     34            MGN
     C                   Z-ADD     WREP0         MREP0
     C                   Z-ADD     WREP1         MREP1
     C                   ADD       1             WREP2
     C                   Z-ADD     WREP2         MREP2
     C                   MOVEL     'LMT'         M6311
     C                   MOVEL     'M'           M6411
     C                   Z-ADD     HELM          ØN153
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           M6314
     C                   ADD       1             WREP2
     C                   WRITE     MEAR
     C                   ENDIF
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  NAD25 /
      *////////////////////////////////////////////////////////////
     C     NAD25         BEGSR
     C                   Z-ADD     0             WREP1
     C                   Z-ADD     0             WREP2
     C                   Z-ADD     0             WREP3
     C                   Z-ADD     0             WREP4
     C                   Z-ADD     0             WREP5
     C                   Z-ADD     0             WREP6
     C                   Z-ADD     0             WREP7
     C                   Z-ADD     0             WREP8
      *__________
      * CONSIGNOR
      *""""""""""
     C                   CLEAR                   NADR
     C                   ADD       1             WREP1
     C                   MOVEL     'CZ'          N3035
     C     HEKNS         IFNE      *ZEROS
     C                   MOVEL     HEKNS         N3039
     C                   MOVEL     '91'          N3055
     C                   END
     C                   MOVEL     HENAS         N3036A                         party
     C                   MOVEL     HEADS1        N3042A                         street
     C                   MOVEL     XAVSST        N3164                          city
     C                   MOVEL     XAVSPN        N3251                          postnr
     C                   MOVEL     'NO'          N3207                          land
      *
     C                   Z-ADD     WUMN          NMN
     C                   Z-ADD     25            NGN
     C                   Z-ADD     WREP0         NREP0
     C                   ADD       1             WREP1
     C                   Z-ADD     WREP1         NREP1
     C                   WRITE     NADR
      *__________
      * CONSIGNEE
      *""""""""""
     C                   CLEAR                   NADR
     C                   ADD       1             WREP1
     C                   MOVEL     'CN'          N3035
     C     HEKNK         IFNE      *ZEROS
     C                   MOVEL     HEKNK         N3039
     C                   MOVEL     '91'          N3055
     C                   END
     C                   MOVEL     HENAK         N3036A                         party
     C                   MOVEL     HEADK1        N3042A                         street
     C                   MOVEL     XMOTST        N3164                          city
     C                   MOVEL     XMOTPN        N3251                          postnr
     C                   MOVEL     'NO'          N3207                          land
      *
     C                   Z-ADD     WUMN          NMN
     C                   Z-ADD     25            NGN
     C                   Z-ADD     WREP0         NREP0
     C                   ADD       1             WREP1
     C                   Z-ADD     WREP1         NREP1
     C                   WRITE     NADR
      *__________
      * ANNEN FRAKTBETALER
      *""""""""""
     C     HEFR          IFEQ      'A  '
     C                   CLEAR                   NADR
     C                   ADD       1             WREP1
     C                   MOVEL     'IV'          N3035
     C                   Z-ADD     HEKNSF        WKUNDN
     C     KUNKEY        CHAIN     CUNDL01                            20
     C     *IN20         IFEQ      '1'
     C                   MOVEL     HENASF        N3036A                         party
     C                   ELSE
     C                   MOVEL     HEKNSF        N3039
     C                   MOVEL     '91'          N3055
     C                   MOVEL     KNAVN         N3036A                         party
     C                   MOVEL     ADR1          N3042A                         street
     C                   MOVEL     ADR3          N3164                          city
     C                   MOVEL     POSTNR        N3251                          postnr
     C                   ENDIF
     C                   MOVEL     'NO'          N3207                          land
      *
     C                   Z-ADD     WUMN          NMN
     C                   Z-ADD     25            NGN
     C                   Z-ADD     WREP0         NREP0
     C                   ADD       1             WREP1
     C                   Z-ADD     WREP1         NREP1
     C                   WRITE     NADR
     C                   ENDIF
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  PCI37 /
      *////////////////////////////////////////////////////////////
     C     PCI37         BEGSR
     C                   Z-ADD     1             WREP2
     C                   Z-ADD     0             WREP3
     C                   Z-ADD     0             WREP4
     C                   Z-ADD     0             WREP5
     C                   Z-ADD     0             WREP6
     C                   Z-ADD     0             WREP7
     C                   Z-ADD     0             WREP8
      * KOLLI-ID...
     C     DFMKEY        KLIST
     C                   KFLD                    FMRI
     C                   KFLD                    FMAVD
     C                   KFLD                    FMOPD
     C                   KFLD                    FMFBNR
     C                   KFLD                    FMLINR
     C                   MOVE      'F'           FMRI
     C                   MOVE      HEAVD         FMAVD
     C                   MOVE      HEOPD         FMOPD
     C                   Z-ADD     1             FMFBNR
     C                   MOVE      *ZEROS        FMLINR
      *
     C     DFMKEY        SETLL     DOKUFM
     C     DFMKEY        READE     DOKUFM                                 33
     C     *IN33         DOWEQ     '0'
     C     FMST          IFEQ      'I'
     C     FMST          OREQ      'E'
     C                   CLEAR                   PCIR
     C                   Z-ADD     WUMN          PMN
     C                   Z-ADD     37            PGN
     C                   Z-ADD     WREP0         PREP0
     C                   Z-ADD     WREP1         PREP1
     C                   MOVEL     '18'          P4233
     C                   MOVEL     FMMRK1        P7102A
     C                   Z-ADD     WREP2         PREP2
     C                   WRITE     PCIR
     C                   END
     C     DFMKEY        READE     DOKUFM                                 33
     C                   END
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  RFF01 /
      *////////////////////////////////////////////////////////////
     C     RFF01         BEGSR
     C                   Z-ADD     1             WREP0
     C                   Z-ADD     0             WREP1
     C                   Z-ADD     0             WREP2
     C                   Z-ADD     0             WREP3
     C                   Z-ADD     0             WREP4
     C                   Z-ADD     0             WREP5
     C                   Z-ADD     0             WREP6
     C                   Z-ADD     0             WREP7
     C                   Z-ADD     0             WREP8
     C                   CLEAR                   RFFR
      *
      * Key
     C                   Z-ADD     WUMN          RMN
     C                   Z-ADD     1             RGN
     C                   Z-ADD     WREP0         RREP0
      * Data
     C                   MOVEL     'FF'          R1153
     C                   MOVEL     WPRO          R1154
      * Write record
     C                   WRITE     RFFR
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  RFF18 /
      *////////////////////////////////////////////////////////////
     C     RFF18         BEGSR
     C                   Z-ADD     0             WREP1
     C                   Z-ADD     0             WREP2
     C                   Z-ADD     0             WREP3
     C                   Z-ADD     0             WREP4
     C                   Z-ADD     0             WREP5
     C                   Z-ADD     0             WREP6
     C                   Z-ADD     0             WREP7
     C                   Z-ADD     0             WREP8
      * FORWARDER REFERENCE
     C                   CLEAR                   RFFR
     C                   Z-ADD     WUMN          RMN
     C                   Z-ADD     18            RGN
     C                   Z-ADD     WREP0         RREP0
     C                   ADD       1             WREP1
     C                   Z-ADD     WREP1         RREP1
     C                   MOVEL     'FF'          R1153
     C                   MOVEL     HEAVD         R1154
     C     R1154         CAT       '/':0         R1154
     C                   MOVEL     HEOPD         WFOPDA            7
     C     R1154         CAT       WFOPDA:0      R1154
     C     R1154         CAT       '/':0         R1154
     C     R1154         CAT       HESG:0        R1154
     C                   WRITE     RFFR
      * CUSTOMER REFERENCE
     C     HERFA         IFNE      *BLANKS
     C                   CLEAR                   RFFR
     C                   Z-ADD     WUMN          RMN
     C                   Z-ADD     18            RGN
     C                   Z-ADD     WREP0         RREP0
     C                   ADD       1             WREP1
     C                   Z-ADD     WREP1         RREP1
     C                   MOVEL     'CU'          R1153
     C                   MOVEL     HERFA         R1154
     C                   WRITE     RFFR
     C                   ENDIF
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  RFF29 /
      *////////////////////////////////////////////////////////////
     C     RFF29         BEGSR
     C                   Z-ADD     1             WREP2
     C                   Z-ADD     0             WREP3
     C                   Z-ADD     0             WREP4
     C                   Z-ADD     0             WREP5
     C                   Z-ADD     0             WREP6
     C                   Z-ADD     0             WREP7
     C                   Z-ADD     0             WREP8
     C                   CLEAR                   RFFR
      *
     C*                    EXSR DTM01
      * Key
     C                   Z-ADD     WUMN          RMN
     C                   Z-ADD     29            RGN
     C                   Z-ADD     WREP0         RREP0
     C                   Z-ADD     WREP1         RREP1
     C                   Z-ADD     WREP2         RREP2
      * Data
     C                   MOVE      'ACL'         R1153
     C                   MOVEL     HEAVD         WEREF             9
     C                   MOVE      HEPRO         WEREF
     C                   MOVEL     WEREF         R1154
      * Write record
     C                   WRITE     RFFR
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  TDT05 /
      *////////////////////////////////////////////////////////////
     C     TDT05         BEGSR
     C                   Z-ADD     1             WREP0
     C                   Z-ADD     0             WREP1
     C                   Z-ADD     0             WREP2
     C                   Z-ADD     0             WREP3
     C                   Z-ADD     0             WREP4
     C                   Z-ADD     0             WREP5
     C                   Z-ADD     0             WREP6
     C                   Z-ADD     0             WREP7
     C                   Z-ADD     0             WREP8
     C                   CLEAR                   TDTR
      *______
      * TDT05
      *""""""
     C                   MOVEL     '20 '         T8051
     C                   MOVEL     '3  '         T8067
     C                   MOVEL     TUBILN        T8212
      *_____________
      * Write record
      *"""""""""""""
     C                   Z-ADD     WUMN          TMN
     C                   Z-ADD     5             TGN
     C                   Z-ADD     WREP0         TREP0
     C                   WRITE     TDTR
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  TSR15 /
      *////////////////////////////////////////////////////////////
     C     TSR15         BEGSR
     C                   Z-ADD     0             WREP1
     C                   Z-ADD     0             WREP2
     C                   Z-ADD     0             WREP3
     C                   Z-ADD     0             WREP4
     C                   Z-ADD     0             WREP5
     C                   Z-ADD     0             WREP6
     C                   Z-ADD     0             WREP7
     C                   Z-ADD     0             WREP8
     C                   CLEAR                   TSRR
     C                   Z-ADD     WUMN          TMN
     C                   Z-ADD     15            TGN
     C                   Z-ADD     WREP0         TREP0
      *__________________
      * TRANSP. SERVICE REQUIREMENTS
      *""""""""""""""""""
      * ADR-MERKING LAGER FARLIG-GODS-FLAGG
S01  C*    TRFA21        IFNE      *BLANKS
N01  C     WSFARL        IFNE      *BLANKS
N02  C                   MOVEL     'RMD'         T4065
     C                   MOVEL     'RMD'         T7085
     C                   ADD       1             WREP1
     C                   Z-ADD     WREP1         TREP1
     C                   WRITE     TSRR
     C                   END
      * IMD-MERKING LAGER VARM-GODS-FLAGG
S01  C*    TRFA11        IFNE      *BLANKS
N01  C     WSVARM        IFNE      *BLANKS
N02  C                   MOVEL     'ZHO'         T4065
     C                   MOVEL     'ZHO'         T7085
     C                   ADD       1             WREP1
     C                   Z-ADD     WREP1         TREP1
     C                   WRITE     TSRR
     C                   END
N01   * LENGDEGODS
N01  C     WSLENG        IFNE      *BLANKS
N02  C                   MOVEL     'ZLN'         T4065
N01  C                   MOVEL     'ZLN'         T7085
N01  C                   ADD       1             WREP1
N01  C                   Z-ADD     WREP1         TREP1
N01  C                   WRITE     TSRR
N01  C                   END
N01   * ADVISERING
N01  C     WSADVI        IFNE      *BLANKS
N02  C                   MOVEL     'ZAD'         T4065
N01  C                   MOVEL     'ZAD'         T7085
N01  C                   ADD       1             WREP1
N01  C                   Z-ADD     WREP1         TREP1
N01  C                   WRITE     TSRR
N01  C                   END
N02   * En-kolli
N02  C     HEKDPL        ifeq      'K'
N02  C                   MOVEL     'ZKL'         T4065
N02  C                   MOVEL     'ZKL'         T7085
N02  C                   ADD       1             WREP1
N02  C                   Z-ADD     WREP1         TREP1
N02  C                   WRITE     TSRR
N02  C                   END
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  MOA15 /
      *////////////////////////////////////////////////////////////
     C     MOA15         BEGSR
     C                   Z-ADD     0             WREP1
     C                   Z-ADD     0             WREP2
     C                   Z-ADD     0             WREP3
     C                   Z-ADD     0             WREP4
     C                   Z-ADD     0             WREP5
     C                   Z-ADD     0             WREP6
     C                   Z-ADD     0             WREP7
     C                   Z-ADD     0             WREP8
     C                   CLEAR                   MOAR
     C                   Z-ADD     WUMN          MMN
     C                   Z-ADD     15            MGN
     C                   Z-ADD     WREP0         MREP0
      *__________________
      * MONETARY AMOUNT
      *""""""""""""""""""
      * AGENT-BELØP FINNES SOM REKV PÅ UTKJØRINGSSIDEN
     C                   MOVE      *ZEROS        ABELØP            9 0
     C     WBRUDD        IFNE      'T'
     C     VFKEY         CHAIN     HETRIU                             33
     C     *IN33         IFEQ      '0'
     C     TIBURF        CHAIN     KOSBU                              33
     C  N33              Z-ADD     BUBL          ABELØP
     C                   END
     C                   END
     C     WBRUDD        IFEQ      'T'
     C     AGTRAN        OREQ      'J'
     C                   CALL      'SYTR12Z'
     C                   PARM                    HEPRO
     C                   PARM                    HEAVD
     C                   PARM                    HEOPD
     C                   PARM                    TRADET            9 2
     C                   ADD       TRADET        ABELØP
     C                   END
     C                   ADD       1             WREP1
     C                   Z-ADD     WREP1         MREP1
     C                   MOVEL     '209'         M5025
     C                   Z-ADD     ABELØP        ØN153
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           M5004
     C                   WRITE     MOAR
     C                   ENDSR

      *////////////////////////////////////////////////////////////
      * SR                                                  FTX15 /
      *////////////////////////////////////////////////////////////
     C     FTX15         BEGSR
     C                   Z-ADD     0             WREP1
     C                   Z-ADD     0             WREP2
     C                   Z-ADD     0             WREP3
     C                   Z-ADD     0             WREP4
     C                   Z-ADD     0             WREP5
     C                   Z-ADD     0             WREP6
     C                   Z-ADD     0             WREP7
     C                   Z-ADD     0             WREP8
     C                   CLEAR                   FTXR
     C                   Z-ADD     WUMN          FMN
     C                   Z-ADD     15            FGN
     C                   Z-ADD     WREP0         FREP0
      *
      * Notisblokk kode G  "SIC" (Senders Instr. to Carrier)
     C                   MOVE      'G'           FRTKOD
     C                   MOVEL     'SIC'         F4451
     C                   Z-ADD     0             X                 3 0
     C                   CLEAR                   TX
     C     Free2Key      SETLL     Freetxt2
     C     Free2Key      READE     Freetxt2                               90
     C     *IN90         DOWEQ     *OFF
     C                   ADD       1             X
     C                   MOVEL     FRTTXT        TX(X)
     C     Free2Key      READE     Freetxt2                               90
     C                   ENDDO
      * Legg ut 5 og 5 array-element i ett TXT_segment
     C     X             IFGT      0
     C                   EXSR      FTX15B
     C                   END
      *
      * Notisblokk kode R  "ICN" (melding til mottaker)
     C                   MOVE      'R'           FRTKOD
     C                   MOVEL     'ICN'         F4451
     C                   Z-ADD     0             X
     C                   CLEAR                   TX
     C     Free2Key      SETLL     Freetxt2
     C     Free2Key      READE     Freetxt2                               90
     C     *IN90         DOWEQ     *OFF
     C                   ADD       1             X
     C                   MOVEL     FRTTXT        TX(X)
     C     Free2Key      READE     Freetxt2                               90
     C                   ENDDO
      * linjer fra R38 i evt fraktbrev..
     C     R29KEY        SETLL     DOK29
     C     R29KEY        READE     DOK29                                  90
     C     *IN90         DOWEQ     *OFF
     C                   IF        X >= 95
     C                   LEAVE
     C                   END
     C                   ADD       1             X
     C                   MOVEL     D29TXT        TX(X)
     C     R29KEY        READE     DOK29                                  90
     C                   ENDDO
      * Legg ut 5 og 5 array-element i ett TXT_segment
     C     X             IFGT      0
     C                   EXSR      FTX15B
     C                   END
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Skriv fritekst fra array til segment (5 og 5)      FTX00B/
      *////////////////////////////////////////////////////////////
     C     FTX15B        BEGSR
      * Når SR er kalt - Alltid minst ett segment a 5. linjer,
     C                   Z-ADD     1             XA                3 0
     C     STRFTX        TAG
     C     XA            ADD       1             XB                3 0
     C     XA            ADD       2             XC                3 0
     C     XA            ADD       3             XD                3 0
     C     XA            ADD       4             XE                3 0
     C*
     C                   ADD       1             WREP1
     C                   Z-ADD     WREP1         FREP1
     C     TX(XA)        IFEQ      *BLANKS
     C                   EVAL      TX(XA) = '.'
     C                   ENDIF
     C                   MOVEL     TX(XA)        F4440A
     C                   MOVEL     TX(XB)        F4440B
     C                   MOVEL     TX(XC)        F4440C
     C                   MOVEL     TX(XD)        F4440D
     C                   MOVEL     TX(XE)        F4440E
     C                   WRITE     FTXR
      * Flere??
     C     XE            IFLT      X
     C                   ADD       5             XA
     C                   GOTO      STRFTX
     C                   END
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  TOD17 /
      *////////////////////////////////////////////////////////////
     C     TOD17         BEGSR
     C                   Z-ADD     0             WREP1
     C                   Z-ADD     0             WREP2
     C                   Z-ADD     0             WREP3
     C                   Z-ADD     0             WREP4
     C                   Z-ADD     0             WREP5
     C                   Z-ADD     0             WREP6
     C                   Z-ADD     0             WREP7
     C                   Z-ADD     0             WREP8
     C                   CLEAR                   TODR
     C                   Z-ADD     WUMN          TMN
     C                   Z-ADD     17            TGN
     C                   Z-ADD     WREP0         TREP0
      *__________________
      * Terms of delivery
      *""""""""""""""""""
     C                   MOVEL     '5'           T4055
     C                   SELECT
     C     HEFR          WHENEQ    'M  '
     C                   MOVEL     'CC '         T4053
     C     HEFR          WHENEQ    'A  '
     C                   MOVEL     'TP '         T4053
     C                   OTHER
     C                   MOVEL     'PP '         T4053
     C                   ENDSL
     C                   ADD       1             WREP1
     C                   Z-ADD     WREP1         TREP1
     C                   WRITE     TODR
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Leftjustify num.fields                              SRNE /
      *////////////////////////////////////////////////////////////
     C     SRNE          BEGSR
      * Move correct value into alpha 12-field
     C                   MOVE      *ZEROS        ØA12
     C     ØA153D        IFEQ      '000'                                        <-------------I
     C                   MOVE      ØA153H        ØA12                                         I
     C                   ELSE                                                   <-------------I
     C                   MOVEL     ØA153X        ØA12                                         I
     C                   MOVE      ',   '        ØA12                                         I
     C                   MOVE      ØA153D        ØA12                                         I
     C                   END                                                    <-------------I
      * search from left and find first sign over 0
     C     1             DO        11            ØX                             <-------------I
     C     Ø12(ØX)       IFNE      '0'                                           <-----------II
     C                   GOTO      SRNE1                                                     II
     C                   END                                                     <-----------II
     C                   END                                                    <-------------I
     C     SRNE1         TAG
      * Leftjustify to a new field
     C     ØA153D        IFNE      '000'                                        <-------------I
     C                   SUB       1             ØX                                           I
     C                   ENDIF                                                  <-------------I
     C                   MOVE      *BLANKS       ØAN
     C                   MOVEA     Ø12(ØX)       ØAN
      *
     C                   ENDSR
