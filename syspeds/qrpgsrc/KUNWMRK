*****F**************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnrF*Sek Sig Vers Beskrivelse...........................
"""""F*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
P0607F* 01 TR  PTF6 RETTELSER M.H.T NY LØSNING1
P08XXF* 02 TR  PTF8 RETTELSER M.H.T NY LØSNING1
P08XXF*             KJØP I ÅR/KJØP I FJOR ER FLYTTET TIL EGEN FIL
P0607F* 03 TR  PTF6 nullstille kjifjor
P0607F* 04 sh  PTF6 *** stygg feil *
*****F**************************************************************
     FKUNDF     UP   E             DISK
     FCUNDL01   UF   E           K DISK
     F                                     RENAME(KUNDR:CUNDR)
     FWRKK      IF   E           K DISK
     FSYSRL01   UF   E           K DISK
     FFIRM      IF   E           K DISK
N02  FKUNSL01   IF   E           K DISK
N02  D                 DS
N02  D  XULTID                 1     14  0
N02  D  XULÅÅ                 11     14  0
N01  ICUNDR
N01  I              BANKG                       SYBAGI
N01  I              POSTG                       SYPOGI
     C     *IN99         IFNE      *ON
     C                   READ      FIRM                                   33
     C                   MOVE      *ON           *IN99
     C                   END
     C     WRKKEY        KLIST
     C                   KFLD                    FIRMA
     C                   KFLD                    KUNDNR
     C*
     C* Kun merking av kundene tilhørende SYSPED - aktuelt filsett
     C     FIRMA         IFEQ      FIFIRM
N02   * Hent kjøp i år/kjøp ifjor
N02  C                   EXSR      KJOP
     C* Ingen saldo/omsetning i det siste....
     C     KSALDO        IFEQ      *ZEROS
     C     KJIAAR        ANDEQ     *ZEROS
     C     KJIFJ         ANDEQ     *ZEROS
     C* Dersom kunden heller ikke er brukt i spedisjonssystemet:merk I
     C     WRKKEY        CHAIN     WRKK                               44
     C     *IN44         IFEQ      '1'
     C     AKTKOD        ANDEQ     'A'
     C                   MOVE      'I'           AKTKOD
     C                   UPDATE    KUNDR
     C                   ADD       1             ANTMRK            5 0
     C     WRKKEY        CHAIN     CUNDL01                            44
     C     *IN44         IFEQ      '0'
     C                   MOVE      'I'           AKTKOD
     C                   UPDATE    CUNDR
     C                   END
     C                   END
     C                   END
     C                   END
     C* Oppdater antall inaktive
     CLR   SYSKEY        KLIST
     CLR                 KFLD                    FIFIRM
     CLR                 KFLD                    REGNAV
     CLR                 MOVEL     'KUNDF'       REGNAV
     CLR   SYSKEY        CHAIN     SYSRL01                            44
     CLR   *IN44         IFEQ      '0'
     CLR                 ADD       ANTMRK        ANTINA
     CLR                 UPDATE    SYSRR
     CLR                 END
     C*
N02   *****************************************************
N02  C     KJOP          BEGSR
N02   *****************************************************
N02  C     KUNSKY        KLIST
N02  C                   KFLD                    FIFIRM
N02  C                   KFLD                    WAARKU
N02  C                   KFLD                    WKUNDE
N03  C                   Z-ADD     0             KJIFJ
N02  C                   Z-ADD     KUNDNR        WKUNDE            8 0
N02   * Hent årstall
N02  C                   TIME                    XULTID
N02   * Hent kjøp ifjor
N02  C                   Z-ADD     XULÅÅ         WIÅR              4 0
N02  C     WIÅR          SUB       1             WIFJOR            4 0
N02  C                   Z-ADD     WIFJOR        WAARKU            4 0
N02  C     KUNSKY        CHAIN     KUNSL01                            33
N02  C     *IN33         IFEQ      *OFF
N02  C                   Z-ADD     KJIAAR        KJIFJ            13 2
N02  C                   ENDIF
N02   * Hent kjøp i år
N02  C                   Z-ADD     WIÅR          WAARKU
s04  C*    KUNSKY        CHAIN     KUNSL01
N04  C     KUNSKY        CHAIN     KUNSL01                            33
N02  C     *IN33         IFEQ      *ON
N02  C                   Z-ADD     0             KJIAAR
N02  C                   ENDIF
N02  C                   ENDSR
