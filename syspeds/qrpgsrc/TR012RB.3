
     ‚***********************************************************
     ‚*** PROGRAM SKAL OVERFØRE SAD-LINJER TIL TET            ***
     ‚*** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
     ‚*** LEDIGE INDIKATORER 01-26                            ***
     ‚*** LEDIGE INDIKATORER 27-32 34-99                      ***
     ‚***********************************************************
     fsaeh      if   e           k disk
     fsaev4     if   e           k disk
     ftruv      o  a e             disk
     ftruvam    o  a e             disk
     fcundl01   if   e           k disk
     d                 ds
     d tvvktb123                     12  3
     d  tvvktb                        9  0 overlay(tvvktb123)
     d                 ds
     d tvvktn123                     12  3
     d  tvvktn                        9  0 overlay(tvvktn123)
     d                 ds
     d syrg                          14
     d  syfore                       11    overlay(syrg)
     d   syfor9                       9    overlay(syfore)
     ‚*  Prototype for tr012rb
     d tr012rb         pr
     d ufirm_                              like(ufirm)
     d uavd_                               like(uavd)
     d utdn_                               like(utdn)
     d uavd2_                              like(uavd2)
     d utdn2_                              like(utdn2)
     ‚*  *ENTRY Interface for Main Procedure
     d tr012rb         pi
     d ufirm                          2
     d uavd                           4
     d utdn                           7
     d uavd2                          4
     d utdn2                          7
     ‚*----------------------------------------------------------------------
     ‚* Stand Alone Fields - TOP
     ‚*----------------------------------------------------------------------
     d xln             s              5  0
     ‚*----------------------------------------------------------------------
     ‚* Stand Alone Fields - BOTTOM
     ‚*----------------------------------------------------------------------

      /free
       exsr init;

       xln = *zeros;
       setll ( seavd : setdn ) saev4;
       reade ( seavd : setdn ) saev4;
       dow not %eof;
         select;
         when xln = 0;
           exsr srhent;                                                         // HENT DATA FRA SAE
         when svln = xln;
           exsr srsum;                                                          // SUMMERE VEKT
         other;
           tvvktb123 = tvvktb123 + 0,5;
           tvvktn123 = tvvktn123 + 0,5;
           write truvr;
           exsr srhent;                                                         // HENT DATA FRA SAE
         endsl;
         reade ( seavd : setdn ) saev4;
       enddo;

       if tvli <> 0;
           tvvktb123 = tvvktb123 + 0,5;
           tvvktn123 = tvvktn123 + 0,5;
         write truvr;
       endif;

       *inlr = *on;

       //**********************************************
       begsr srhent;
         //**********************************************
         xln = svln;
         tvli = tvli +1;
         tvvnt = svvnt;
         tvvktb123 = svvktb;
         tvvktn123 = svvktn;
         tvnteh = svntm;
         tvmn = svft;
         tvvnt = svvnt;
         tveh = sveh;
         %subst(tvvt:1:30) = svvt;
         tvdty = '830';
         tvdref = %trim(setle) + ' ' + %trim(setll);
         exsr hadr12;

       endsr;

       //**********************************************
       begsr srsum;
         //**********************************************
           tvvktb123 = tvvktb123 + svvktb;
           tvvktn123 = tvvktn123 + svvktn;
           tvnteh = tvnteh + svntm;
       endsr;

       //**********************************************
       begsr hadr12;
         //**********************************************
         tvkns = seknk;
         tvnas = senak;
         tvads1 = seadk1;
         tvpns = seadk3;
         tvpss = seadk3;
         tvsks = 'NO';
         tvlks = 'NO';
         if seknk = 0;                                                          // 1<-B
           tvtins = *blanks;
         else;                                                                  // 1
           kundnr = seknk;
           chain ( ufirm : kundnr ) cundl01;
           *in34 = not %found;
           tvtins = *blanks;
          if %found;
           select;                                                              // 2<-B
           when eori = *blanks;                                                 // 2
             if ((syfor9 <> *blanks) and (syfor9 <> *zeros));                   //   3<-B
               tvtins = tvlks + syfor9;
             else;                                                              //  3
               tvtins = *blanks;
             endif;                                                             //   3<-E
           other;                                                               // 2
             tvtins = eori;
           endsl;                                                               //  2<-E
          endif;
         endif;                                                                 // 1<-E

         tvknk = sekns;
         tvnak = senas;
         tvadk1 = seads1;
         tvpnk = seads3;
         tvpsk = seads3;
         tvskk = 'NO';
         tvlkk = selkb;
         if sekns = 0;                                                          // 1<-B
           tvtink = *blanks;
         else;                                                                  // 1
           kundnr = sekns;
           chain ( ufirm : kundnr ) cundl01;
           *in34 = not %found;
           tvtink = *blanks;
          if %found;
           select;                                                              // 2<-B
           when eori = *blanks;                                                 // 2
             if ((syfor9 <> *blanks) and (syfor9 <> *zeros));                   //   3<-B
               tvtink = tvlkk + syfor9;
             else;                                                              //  3
               tvtink = *blanks;
             endif;                                                             //   3<-E
           other;                                                               // 2
             tvtink = eori;
           endsl;                                                               //  2<-E
          endif;
         endif;                                                                 // 1<-E

         if tvads1 = *blanks;                                                   // 1<-B
           %subst(tvads1:1:1) = '.';
         endif;                                                                 // 1<-E
         if tvadk1 = *blanks;                                                   // 1<-B
           %subst(tvadk1:1:1) = '.';
         endif;                                                                 // 1<-E

         write truvamr;

       endsr;

       //**********************************************
       begsr init;
         //**********************************************

         seavd = %dec(uavd2:4:0);
         setdn = %dec(utdn2:7:0);
         tvavd = %dec(uavd:4:0);
         tvtdn = %dec(utdn:7:0);
         tvavd2 = %dec(uavd2:4:0);
         tvtdn2 = %dec(utdn2:7:0);

         chain ( seavd : setdn ) saeh;

       endsr;
      /end-free
