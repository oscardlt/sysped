      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/TNOI064R MODULE(*LIBL/TNOI064R *LIBL/INCGI)
CRTPGM*        ACTGRP(*NEW) AUT(*USE)
      *
      *********************************************************************
      /copy SYSPEDS/qrpgsrcpy,hspecs
      /copy SYSPEDS/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FKODTS8    IF   E           K DISK    USROPN
     FKODTS8L2  IF   E           K DISK    USROPN
     F                                     RENAME(KOS8R:KOS8RL2)
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy SYSPEDS/qrpgsrcpy,prototypeb
      /copy SYSPEDS/qrpgsrcpy,usec
      /copy SYSPEDS/qrpgsrcpy,variables3
      *
      * Varible for
     D WUSER           S             10
     D UBVNV           S              4  2
     D SIBEL3          S             13  2
     D SIBEL4          S              9  0
     D SIBELR          S             11  2
     D SIBELS          S              9  0
     D WG              S              2                                         SVKDAÆ
     D WH              S              3                                         SVKDSÆ
     D WK              S              7  2                                      SVBLSÆ
     D SVBELT          S             11  2
     D SVVKTB          S             12  3
     D SVVKTN          S             12  3
     D SVNTM           S              9  0
     D WJ              S             11  2                                      SVBLGÆ
     D WI              S             11  2                                      SVBLÆ
     D WA              S             15
     D JSONstring      S          32000
     D Error           S            150
     D                 DS
     D  KS8FTX                 1     52
     D  KS8MIL                52     52
     D  KS8ØRE                51     51

      *get input-string
      /copy syspeds/qrpgsrcpy,prolog3

      * Parse input
     C                   exsr      Parse
      * Open files etc
     C                   EXSR      INIT
      *
     c                   callp     gethtmlifs(
     C                             '/syspeddev/html/JSON.html':
     C                             '/CGITAG/')
     C                   callp     wrtsection('top')
      *
      * ............................................................................................
      * skriv JSON-Object start..(alltid '{' + x ant param. m/komma mellom (IKKE komma etter siste))
      * ............................................................................................
      *
      /free
        JSONstring='{'
        +'¬user¬ : ¬'+%trim(WUSER)+'¬, '
        +'¬ubvnv¬ : ¬'+%trim(%editc(UBVNV:'4'))+'¬, '
        +'¬sibel3¬ : ¬'+%trim(%editc(SIBEL3:'4'))+'¬, '
        +'¬sibel4¬ : ¬'+%trim(%editc(SIBEL4:'Z'))+'¬, '
        +'¬sibelr¬ : ¬'+%trim(%editc(SIBELR:'4'))+'¬, '
        +'¬sibels¬ : ¬'+%trim(%editc(SIBELS:'Z'))+'¬, '
        +'¬wg¬ : ¬'+%trim(WG)+'¬, '
        +'¬wh¬ : ¬'+%trim(WH)+'¬, '
        +'¬wk¬ : ¬'+%trim(%editc(WK:'4'))+'¬, '
        +'¬svbelt¬ : ¬'+%trim(%editc(SVBELT:'4'))+'¬, '
        +'¬svvktb¬ : ¬'+%trim(%editc(SVVKTB:'4'))+'¬, '
        +'¬svvktn¬ : ¬'+%trim(%editc(SVVKTN:'4'))+'¬, '
        +'¬svntm¬ : ¬'+%trim(%editc(SVNTM:'Z'))+'¬, '
        +'¬errMsg¬ : ¬'+%trim(Error)+'¬, ';
      /end-free
      * JSONfix escaper " i tekst,  for deretter å bytte ¬->"
     c                   call      'JSONFIX'
     c                   parm                    JSONstring
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')
      *
      * ............................................................................................
      * Skriv JSON-LISTE Start (en liste er EN parameter(fra [ til   )
      * ............................................................................................
     C                   eval      JSONstring ='"calcavgifter" : ['
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')

     C     Error         ifeq      *blanks
     C                   EXSR      AVG

     c                   endif
     c                   eval      JSONstring =']'
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')
      * ............................................................................................
      * Skriv JSON-string Avsluttning
      * ............................................................................................
     c                   eval      JSONstring ='}'
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')
      *
      * Quit
     C                   exsr      Exit


      *********************************************************
     C     INIT          Begsr
      *********************************************************
     C                   OPEN      BRIDF
      * Test innlogging
     C     WUSER         CHAIN     BRIDF                              50
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C                   call      BILIBL
     C                   end
      *
     C     KODS8K        KLIST
     C                   KFLD                    WG
     C                   KFLD                    WH

     C   50              EVAL      Error = 'Invalid userID'
     C  N50              OPEN      KODTS8
     C  N50              OPEN      KODTS8L2

     C                   ENDSR
      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Lukk fil
     C                   CLOSE     BRIDF
     C  N50              CLOSE     KODTS8
     C  N50              CLOSE     KODTS8L2

      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   MOVE      *ON           *INLR
     C                   return
     C                   endsr
      *________________________________________________
      * Avgiftsberegning                            AVG
      *""""""""""""""""""""""""""""""""""""""""""""""""
     C     AVG           BEGSR
      * FINN FAKTOR FOR VARELINJE
     C     SIBEL3        IFNE      0
     C     SIBEL4        DIV(H)    SIBEL3        XTVFAK           17 9
     C                   ENDIF
     C     SIBELR        IFNE      0
     C     SIBEL3        ANDNE     0
     C     SIBELS        DIV(H)    SIBEL3        XTVFA2           17 9
     C                   ELSE
     C                   MOVE      *ZEROS        XTVFA2
     C                   ENDIF
      * AVGIFT KAN OVERSTYRES BÅDE SATS OG BELØP
     C     WG            IFNE      *BLANKS                                      1<-B
     C     WG            ANDNE     '99'
     C     WG            IFEQ      'TL'
     C     WG            OREQ      'TA'
     C                   MOVE      '1  '         WH
     C                   END
     C     KODS8K        CHAIN     KODTS8                             79
     C   79WG            CHAIN     KODTS8L2                           79
      *** NÅR GRUNNLAG IKKE OPPGIS HENTER SYSTEMET GRUNNLAG AVHENGIG AV
      *** AVGIFT TYPE
      *** AVGIFT MED '%' I SATSTYPE - STAT. VERDI PÅ LINJE SETTES INN
      *** AVGIFT MED 'P' I SATSTYPE - NETTO VEKT  PÅ LINJE SETTES INN
      *** AVGIFT MED 'H' I SATSTYPE - NETTO VEKT  PÅ LINJE SETTES INN
      *** AVGIFT MED 'K' I SATSTYPE - NETTO VEKT  PÅ LINJE SETTES INN
      *** AVGIFT MED ' ' I SATSTYPE - ANTALL STK. PÅ LINJE SETTES INN
      *** AVGIFT MED '*' I SATSTYPE - ANTALL STK. PÅ LINJE SETTES INN
      *** AVGIFT MED 'G' I SATSTYPE - SATS PR GRAM (NETTO)
      *** AVGIFT MED 'M' I SATSTYPE - MÅ ANGIS MANUELT
      *** AVGIFT MED 'D' I SATSTYPE - STYKK/12 SETTES INN
      *** AVGIFT MED 'T' I SATSTYPE - NETTO VEKT/1000 SETTES INN
      *** AVGIFT MED 'L' I SATSTYPE - MANUELL SETT PÅ AVGIFTGRUNNLAG
      *** AVGIFT MED 'B' I SATSTYPE - MANUELL SETT PÅ AVGIFTGRUNNLAG, ANTALL HESTEKRAFT
     C  N79WJ            IFEQ      0                                             2<-B
     C     KS8STY        ANDNE     'M'
     C     KS8STY        IFEQ      '%'                                            3<-B
     C     XTVFA2        IFEQ      0
     C     SVBELT        MULT(H)   XTVFAK        XBEL9N            9 0
     C                   ELSE
     C     SVBELT        MULT(H)   XTVFA2        XBEL9N            9 0
     C                   END
     C                   Z-ADD     XBEL9N        WJ
     C                   ELSE                                                     3
     C     KS8STY        IFEQ      'P'                                             4<-B
     C     KS8STY        OREQ      'H'
     C     KS8STY        OREQ      'K'
     C     KS8STY        OREQ      'G'
     C                   IF        KS8AVG <> 'FK'
     C     SVVKTN        IFEQ      0
     C     SVVKTB        MULT(H)   UBVNV         SVVKTN
     C                   ENDIF
     C                   Z-ADD     SVVKTN        WJ
     C     KS8STY        IFEQ      'G'
     C                   MULT      1000          WJ
     C                   END
     C                   END
     C                   ELSE                                                      4
     C     KS8STY        IFEQ      'D'
     C                   EVAL(H)   WJ     = SVNTM / 12 + 0.5
     C                   ELSE
     C     KS8STY        IFEQ      'T'
     C                   EVAL(H)   WJ     = SVVKTN / 1000 + 0.5
     C                   ELSE
     C     KS8STY        IFNE      'M'
     C     KS8STY        ANDNE     'A'
     C     KS8STY        ANDNE     'B'
      * KS8STY = '*' ELLER ' '  ELLER 'L'
     C                   Z-ADD     SVNTM         WJ
     C                   END
     C                   END
     C                   END
     C                   END                                                       4<-E
     C                   END                                                      3<-E
     C                   END                                                     2<-E
     C                   MOVE      KS8ØRE        XS8ØRE            1
     C  N79WI            IFGE      999999999                                     2<-B
     C                   Z-ADD     0             WI
     C                   MOVE      ' '           KS8STY            1
     C                   ELSE                                                    2
      * HVIS SATS=0, HENTES SATS FRA KODE-REGISTER
     C*    WK            IFEQ      0
     C*    SDBLSÆ        IFEQ      0
     C*                  MOVE      KS8SAT        WK
     C*                  ELSE
     C*    SDBLSÆ        IFNE      99999,99
     C*    SDBLSÆ        ANDNE     999,99
     C*                  MOVE      SDBLSÆ        WK
     C*                  END
     C*                  END
     C*                  END

     C     WK            IFEQ      99999,99
     C     WK            OREQ      999,99
     C                   GOTO      STTB1
     C                   END

     C     WI            IFEQ      0                                              3<-B
     C                   SELECT
     C     KS8STY        WHENEQ    ' '
     C     KS8STY        OREQ      'K'
     C     KS8STY        OREQ      'G'
     C     KS8STY        OREQ      'M'
     C     KS8STY        OREQ      'D'
     C     KS8STY        OREQ      'T'
     C     KS8STY        OREQ      'L'
     C     KS8STY        OREQ      'B'
     C                   EVAL      WI    = WJ     * WK
     C     KS8STY        WHENEQ    '%'
     C     KS8STY        OREQ      'H'
     C     KS8STY        OREQ      '*'
     C                   EVAL      WI    = WJ     * WK     / 100
     C     KS8STY        WHENEQ    'P'
     C                   EVAL      WI    = WJ     * WK     / 1000
     C                   ENDSL
     C     XS8ØRE        IFEQ      '*'
     C                   DIV(H)    100           WI
     C                   END
     C                   END
     C                   END
     C                   END

     C     STTB1         TAG

      /free
          JSONstring=*blanks;
       JSONstring=%trim(JSONstring)+'{'
        +'¬wj¬ : ¬'+%trim(%editc(WJ:'4'))+'¬, '
          +'¬wi¬ : ¬'+%trim(%editc(WI:'4'))+'¬}';
      /end-free
     C                   call      'JSONFIX'
     C                   parm                    JSONstring
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')

     C                   ENDSR
      *********************************************************
     C     Parse         Begsr
      *********************************************************
     C                   EVAL      WUSER   = zhbgetvar('USER')
     C                   EVAL      WA      = zhbgetvar('UBVNV')
     C                   EVAL      UBVNV   = c2n2(WA)
     C                   EVAL      WA      = zhbgetvar('SIBEL3')
     C                   EVAL      SIBEL3  = c2n2(WA)
     C                   EVAL      WA      = zhbgetvar('SIBEL4')
     C                   EVAL      SIBEL4  = c2n2(WA)
     C                   IF        SIBEL4=*ZEROS
     C                   EVAL      SIBEL4=SIBEL3
     C                   ENDIF
     C                   EVAL      WA      = zhbgetvar('SIBELR')
     C                   EVAL      SIBELR  = c2n2(WA)
     C                   EVAL      WA      = zhbgetvar('SIBELS')
     C                   EVAL      SIBELS  = c2n2(WA)
     C                   EVAL      WG      = zhbgetvar('WG')
     C                   EVAL      WH      = zhbgetvar('WH')
     C                   EVAL      WA      = zhbgetvar('WK')
     C                   EVAL      WK      = c2n2(WA)
     C                   EVAL      WA      = zhbgetvar('SVBELT')
     C                   EVAL      SVBELT  = c2n2(WA)
     C                   EVAL      WA      = zhbgetvar('SVVKTB')
     C                   EVAL      SVVKTB  = c2n2(WA)
     C                   EVAL      WA      = zhbgetvar('SVVKTN')
     C                   EVAL      SVVKTN  = c2n2(WA)
     C                   EVAL      WA      = zhbgetvar('SVNTM')
     C                   EVAL      SVNTM   = c2n2(WA)
     c                   ENDSR
