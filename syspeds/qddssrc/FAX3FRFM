********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
P8068 * 01 TH PTF8 NY HELP - FUNKSJON
********************************************************************
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
S01  A*                                     HLPDOC('FAX3FR10' 'FAX3FR' 'SY400L-
S01  A*                                     O/FAX')
N01  A                                      HLPPNLGRP(HLPSCR FAX3FRPNL)
N01  A                                      HLPTITLE('SENDING AV FLYMANIFEST P-
N01  A                                      Å FAX')
     A                                      HELP
     A                                      ALTHELP
      *
     A          R BILDE
     A                                      CF03(03 'Avslutt')
     A                                      CF04(04 'SØK')
     A                                      CF10(10 'Avslutt')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1  2'SYSPED/FAX3FR'
     A                                      COLOR(BLU)
     A                                  1 20MSGCON(040 SHU0226 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 61DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  1 71TIME
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(026 DIV1404 *LIBL/SYMSGF)
     A            WSAVD          4Y 0B  4 31DSPATR(UL)
     A                                      EDTCDE(4)
     A  99                                  DSPATR(RI)
     A                                      COLOR(GRN)
     A                                  5  2MSGCON(026 DIV1405 *LIBL/SYMSGF)
     A            WSSG           3A  B  5 31DSPATR(UL)
     A  98                                  DSPATR(RI)
     A                                      COLOR(GRN)
     A                                  6  2MSGCON(026 DIV1618 *LIBL/SYMSGF)
     A            WSSD           3A  B  6 31DSPATR(UL)
     A  97                                  DSPATR(RI)
     A                                      COLOR(GRN)
     A                                      DSPATR(PC)
     A            WSLNR          3Y 0B  6 36
     A  97                                  DSPATR(RI)
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(GRN)
     A                                  7  2MSGCON(024 SHU0208 *LIBL/SYMSGF)
     A            KUNDNR         8Y 0B  7 31COLOR(YLW)
     A                                      EDTCDE(4)
     A            KNAVN         30A  B  7 40COLOR(YLW)
     A                                      DSPATR(UL)
     A                                  8  2MSGCON(024 SHU0167 *LIBL/SYMSGF)
     A            FAXNR         15A  B  8 31COLOR(YLW)
     A                                  8 48MSGCON(012 SHU0261 *LIBL/SYMSGF)
     A            WSLAND         2A  B  8 61COLOR(YLW)
     A                                  8 64'='
     A            FAXRET         6A  O  8 66
     A                                  9  2MSGCON(006 SHU0536 *LIBL/SYMSGF)
     A            EPOST         70A  B  9 10COLOR(YLW)
     A                                      CHECK(LC)
     A                                 10  2MSGCON(025 SHU0173 *LIBL/SYMSGF)
     A            WSTIME         6Y 0B 10 31DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(YLW)
     A                                 11  2MSGCON(025 SHU0174 *LIBL/SYMSGF)
     A            WSDATE         6Y 0B 11 31DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(YLW)
     A                                 12  2MSGCON(025 SHU0343 *LIBL/SYMSGF)
     A            WSCVRP         1A  B 12 31COLOR(YLW)
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                             24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A                                 23 22MSGCON(018 SHU0591 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
