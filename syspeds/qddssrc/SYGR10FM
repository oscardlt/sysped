********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 YBC PTF10 TOLLSTEDSKODE ER ENDRET TIL 4.0
********************************************************************
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPDOC('ORI0' 'HELPNU' 'HLP400')
     A                                      HELP
     A                                      ALTHELP
     A          R BILDE01
     A                                      CF03(03 'Avslutt program')
     A                                      CF04(04 'SØK PÅ TUR')
     A                                      CF05(05 'Gå direkte til faktura')
     A                                      CF07(07 'SPØRRIND OPPDRAG')
     A                                      CF09(09 'Bytt signatur')
     A                                      CF20(20 'FAX')
     A          H                           HLPDOC('IO1' 'HELPNU' 'HLP400')
     A                                      HLPARA(09 015 09 037)
     A          H                           HLPDOC('IO2' 'HELPNU' 'HLP400U')
     A                                      HLPARA(11 015 11 038)
     A          H                           HLPDOC('IO3' 'HELPNU' 'HLP400')
     A                                      HLPARA(13 015 13 039)
     A          H                           HLPDOC('IO4' 'HELPNU' 'HLP400')
     A                                      HLPARA(15 015 15 046)
     A          H                           HLPDOC('IO5' 'HELPNU' 'HLP400')
     A                                      HLPARA(18 015 18 045)
     A                                  1  2'SYSPED/SYGR10'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(035 TBR0272 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  2 31MSGCON(003 TBR0273 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  2 70TIME
     A                                      COLOR(BLU)
     A                                  3 24MSGCON(020 TBR0274 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  9  2MSGCON(018 DIV0599 *LIBL/SYMSGF)
     A            WSSG           3A  B  9 23TEXT('Signatur')
     A                                      DSPATR(UL)
     A  99                                  DSPATR(PC)
     A  99                                  DSPATR(RI)
     A                                 11  2MSGCON(018 DIV0600 *LIBL/SYMSGF)
     A            WSAVD          4Y 0B 11 23TEXT('Avdeling')
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
     A  98                                  DSPATR(PC)
     A  98                                  DSPATR(RI)
     A                                 13  2MSGCON(018 DIV0601 *LIBL/SYMSGF)
     A            WSOPD          7Y 0B 13 23TEXT('Oppdragsnummer')
     A                                      DSPATR(UL)
     A N96                                  DSPATR(PC)
     A  97                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                 13 38MSGCON(020 DIV0604 *LIBL/SYMSGF)
     A                                 15  2MSGCON(018 DIV0602 *LIBL/SYMSGF)
     A            HEAVD          4Y 0B 15 23TEXT('Avdeling')
     A                                      DSPATR(UL)
     A  96                                  DSPATR(PC)
     A  96                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A            HEOPD          7Y 0B 15 29TEXT('Oppdragsnummer')
     A                                      DSPATR(UL)
     A  96                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                 15 38MSGCON(020 DIV0036 *LIBL/SYMSGF)
     A                                 18  2MSGCON(018 DIV0603 *LIBL/SYMSGF)
     A            WSOPDG         7Y 0O 18 23COLOR(WHT)
     A                                      EDTCDE(4)
     A            WSMSG         62A  O 20  2COLOR(RED)
     A                                      DSPATR(BL)
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(018 DIV0605 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 42MSGCON(018 DIV0606 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 62MSGCON(018 DIV0033 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A                                 22  2MSGCON(018 SHU0133 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A            MSGNR          7A  P
     A          R BILDE02
     A N31                                  CF03(03 'Avslutt registrering av op-
     A                                      pdrag')
     A                                      CF04(04 'Søke')
     A N31                                  CF05(05 'Gå til faktura')
     A                                      CF06(06 'Gå til faktura')
     A                                      CF07(07 'SPØRRING PÅ OPPDRAG')
     A N31                                  CF09(09 'Ny tur')
     A                                      CA12(12 'Avbryt registrering av opp-
     A                                      drag')
     A                                      CF14(14 'Vise opplysning til kjøper-
     A                                       / SELGER')
     A N31                                  CF16(16 'Neste')
     A                                      CF24(24 'FLERE TASTER')
     A                                      CF18(18 'Freetext')
     A                                      CF15(15 'FrI SØKEVEI')
     A                                      CF20(20 'FAX diekte')
     A                                      CF21(21 'fax manuell')
     A                                      CF17(17 'FORDELING')
     A  27                                  ALARM
     A                                      CSRLOC(LINNBR     POSNBR)
     A          H                           HLPDOC('SYFA10010' 'SYFA10' 'SY400L-
     A                                      O/LAND')
     A                                      HLPARA(02 001 02 018)
     A          H                           HLPDOC('SYFA10015' 'SYFA10' 'SY400L-
     A                                      O/LAND')
     A                                      HLPARA(02 019 02 042)
     A          H                           HLPDOC('SYFA10020' 'SYFA10' 'SY400L-
     A                                      O/LAND')
     A                                      HLPARA(02 043 02 080)
     A          H                           HLPDOC('SYFA10025' 'SYFA10' 'SY400L-
     A                                      O/LAND')
     A                                      HLPARA(03 001 03 010)
     A          H                           HLPDOC('SYFA10030' 'SYFA10' 'SY400L-
     A                                      O/LAND')
     A                                      HLPARA(03 011 03 033)
     A          H                           HLPDOC('SYFA10035' 'SYFA10' 'SY400L-
     A                                      O/LAND')
     A                                      HLPARA(03 034 03 080)
     A          H                           HLPDOC('SYFA10040' 'SYFA10' 'SY400L-
     A                                      O/LAND')
     A                                      HLPARA(04 001 04 080)
     A          H                           HLPDOC('SYFA10045' 'SYFA10' 'SY400L-
     A                                      O/LAND')
     A                                      HLPARA(05 001 05 024)
     A          H                           HLPDOC('SYFA10050' 'SYFA10' 'SY400L-
     A                                      O/LAND')
     A                                      HLPARA(05 025 05 080)
     A          H                           HLPDOC('SYFA10055' 'SYFA10' 'SY400L-
     A                                      O/LAND')
     A                                      HLPARA(06 001 06 080)
     A          H                           HLPDOC('SYFA10060' 'SYFA10' 'SY400L-
     A                                      O/LAND')
     A                                      HLPARA(07 001 07 023)
     A          H                           HLPDOC('SYFA10065' 'SYFA10' 'SY400L-
     A                                      O/LAND')
     A                                      HLPARA(07 024 07 036)
     A          H                           HLPDOC('SYFA10070' 'SYFA10' 'SY400L-
     A                                      O/LAND')
     A                                      HLPARA(07 037 07 080)
     A          H                           HLPDOC('SYFA10075' 'SYFA10' 'SY400L-
     A                                      O/LAND')
     A                                      HLPARA(08 001 09 080)
     A          H                           HLPDOC('SYFA10080' 'SYFA10' 'SY400L-
     A                                      O/LAND')
     A                                      HLPARA(10 001 10 080)
     A          H                           HLPDOC('SYFA10085' 'SYFA10' 'SY400L-
     A                                      O/LAND')
     A                                      HLPARA(11 001 13 080)
     A          H                           HLPDOC('SYFA10090' 'SYFA10' 'SY400L-
     A                                      O/LAND')
     A                                      HLPARA(14 001 14 080)
     A          H                           HLPDOC('SYFA10095' 'SYFA10' 'SY400L-
     A                                      O/LAND')
     A                                      HLPARA(15 001 15 024)
     A          H                           HLPDOC('SYFA10095' 'SYFA10' 'SY400L-
     A                                      O/LAND')
     A                                      HLPARA(16 001 16 024)
     A          H                           HLPDOC('SYFA10095' 'SYFA10' 'SY400L-
     A                                      O/LAND')
     A                                      HLPARA(17 001 17 024)
     A          H                           HLPDOC('SYFA10100' 'SYFA10' 'SY400L-
     A                                      O/LAND')
     A                                      HLPARA(15 025 15 058)
     A          H                           HLPDOC('SYFA10100' 'SYFA10' 'SY400L-
     A                                      O/LAND')
     A                                      HLPARA(16 025 16 058)
     A          H                           HLPDOC('SYFA10100' 'SYFA10' 'SY400L-
     A                                      O/LAND')
     A                                      HLPARA(17 025 17 058)
     A          H                           HLPDOC('SYFA10105' 'SYFA10' 'SY400L-
     A                                      O/LAND')
     A                                      HLPARA(15 059 15 080)
     A          H                           HLPDOC('SYFA10105' 'SYFA10' 'SY400L-
     A                                      O/LAND')
     A                                      HLPARA(16 059 16 080)
     A          H                           HLPDOC('SYFA10105' 'SYFA10' 'SY400L-
     A                                      O/LAND')
     A                                      HLPARA(17 059 17 080)
     A          H                           HLPDOC('SYFA10110' 'SYFA10' 'SY400L-
     A                                      O/LAND')
     A                                      HLPARA(18 001 18 070)
     A          H                           HLPDOC('SYFA10115' 'SYFA10' 'SY400L-
     A                                      O/LAND')
     A                                      HLPARA(18 071 18 080)
     A          H                           HLPDOC('SYFA10120' 'SYFA10' 'SY400L-
     A                                      O/LAND')
     A                                      HLPARA(19 014 19 080)
     A          H                           HLPDOC('SYFA10125' 'SYFA10' 'SY400L-
     A                                      O/LAND')
     A                                      HLPARA(20 001 20 080)
     A          H                           HLPDOC('SYFA10130' 'SYFA10' 'SY400L-
     A                                      O/LAND')
     A                                      HLPARA(21 001 21 039)
     A          H                           HLPDOC('SYFA10135' 'SYFA10' 'SY400L-
     A                                      O/LAND')
     A                                      HLPARA(21 040 21 080)
     A          H                           HLPDOC('SYFA10140' 'SYFA10' 'SY400L-
     A                                      O/LAND')
     A                                      HLPARA(22 001 22 080)
     A          H                           HLPDOC('SYFA10999' 'SYFA10' 'SY400L-
     A                                      O/LAND')
     A                                      HLPARA(23 001 24 080)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            WSSTD          4Y 0H      TEXT('Kostnadssted')
     A            WSPRO          8Y 0H      TEXT('Prosjektnummer')
     A            WSPLAN         1A  H      TEXT('Planleggingskode')
     A            WSPOS1         7A  H      TEXT('Posisjon-1')
     A            WSPOS2         4Y 0H      TEXT('Posisjon-2')
     A            WSDT           6Y 0H
     A            WSGN          11A  H      TEXT('Godsnummer')
     A            WSLKA          2A  H      TEXT('Landkode')
     A            WSSDF          5A  H      TEXT('Frakt fra')
     A            FIRLK          2A  H
     A            WSSDT          5A  H      TEXT('Frakt til')
     A            WSVALT         3A  H      TEXT('V.kode tillegg')
     A            WSVALP         3Y 1H
     A            WSHAWB         9A  H      TEXT('House Airway Billno')
     A            WSKNA          8Y 0H      TEXT('Kundenr Agent')
     A            WSNAA         30A  H      TEXT('Navn Agent')
     A            WSTRM          2Y 0H      TEXT('Transportmåte')
     A            WSTRI          2A  H      TEXT('Lankode Transport')
     A            WSTRC          1Y 0H      TEXT('Transport cont')
     A            WSTRCN        17A  H      TEXT('Transport cnr')
     A            WSSDL         20A  H      TEXT('Lossested')
     A            WSVALS         3A  H      TEXT('Valutakode Selger')
     A            WSNASF        30A  H      TEXT('Navn Selg.fakm')
     A            WSADS1        30A  H      TEXT('Adresse-1 Selger')
     A            WSLKS          2A  H
     A            WSSDFF         5A  H      TEXT('Forfrakt fra')
     A            WSPNFR        30A  H
     A            WSANK          1Y 0H      TEXT('Avtalenr Kjøper')
     A            WSKDFK         1A  H      TEXT('Fakturakode Kjøper')
     A            WSVALK         3A  H      TEXT('Valutakode Kjøper')
     A            WSKNKF         8Y 0H      TEXT('Kundenr Kjøp.fakm')
     A            WSNAKF        30A  H      TEXT('Navn Kjøp.fakm')
     A            WSADK1        30A  H      TEXT('Adresse-1 Kjøper')
     A            WSLKK          2A  H
     A            WSSDVT         5A  H      TEXT('Viderefrakt til')
     A            WSPNTI        30A  H
     A            WSOT           2A  H      TEXT('Oppdragstype')
     A            WSFR           3A  H      TEXT('Frankatur')
     A            WSKF           2A  H
     A            WSKFT1         1A  H
     A            WSFT1A        30A  H
     A            WSFT1B        15A  H
     A            WSKDPL         1A  H      TEXT('Planleggingskode')
     A            WSVERV         3A  H
     A            WSVERB        13Y 2H
     A            WSKDL         15A  H      TEXT('Lagerkode')
     A            WSPK1          1A  H      TEXT('Printerkode-1')
     A            WSPK2          1A  H      TEXT('Printerkode-2')
     A            WSPK3          1A  H      TEXT('Printerkode-3')
     A            WSPK4          1A  H      TEXT('Printerkode-4')
     A            WSMSG         62A  H
S01  A*           W2TOLL         2Y 0H      TEXT('Tollsted')
N01  A            W2TOLL         4Y 0H      TEXT('Tollsted')
     A            W2KDE          1A  H      TEXT('Etterkrav  /X')
     A            WSNTVS         3Y 0H      TEXT('Antall varepos SAD')
     A            MSGL2         18A  H
     A            MSGL3         18A  H
     A            MSGL4         18A  H
     A                                  1  2'SYGR10'
     A                                      COLOR(BLU)
     A                                  1 21MSGCON(035 TBR0257 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  3  2MSGCON(004 DIV0071 *LIBL/SYMSGF)
     A            WSAVD          4Y 0O  3  8TEXT('Avdeling')
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  3 13'/'
     A                                      COLOR(WHT)
     A            WSOPD          7Y 0O  3 15TEXT('Oppdragsnummer')
     A N29                                  DSPATR(BL)
     A N29                                  COLOR(RED)
     A  29                                  COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  3 23MSGCON(010 TBR0283 *LIBL/SYMSGF)
     A            WSSG           3A  O  3 34TEXT('Signatur')
     A                                      COLOR(WHT)
     A                                  5 22MSGCON(012 TBR0260 *LIBL/SYMSGF)
     A            WSANS          1Y 0B  5 35TEXT('Avtalenr Selger')
     A                                      DSPATR(UL)
     A  34                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  6  2MSGCON(010 TBR0261 *LIBL/SYMSGF)
     A                                  6 44MSGCON(026 TBR0262 *LIBL/SYMSGF)
     A            WSKNSF         8Y 0B  6 71TEXT('Kundenr Selg.fakm')
     A                                      DSPATR(UL)
     A  34                                  DSPATR(PR)
     A  99                                  DSPATR(BL)
     A                                      EDTCDE(4)
     A  99                                  COLOR(RED)
     A N99                                  COLOR(YLW)
     A                                  7  2MSGCON(009 TBR0264 *LIBL/SYMSGF)
     A            WSKNS          8Y 0B  7 12TEXT('Kundenr Selger')
     A                                      DSPATR(UL)
     A  67
     AO 68                                  DSPATR(RI)
     A  34                                  DSPATR(PR)
     A  55                                  DSPATR(BL)
     A                                      EDTCDE(4)
     A  55                                  COLOR(RED)
     A N55                                  COLOR(YLW)
     A            WSKDFS         1A  B  7 44TEXT('Fakturakode Selger')
     A                                      DSPATR(UL)
     A  34                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'X' 'B' 'E')
     A                                  7 46MSGCON(032 TBR0263 *LIBL/SYMSGF)
     A                                  8  2MSGCON(005 TBR0265 *LIBL/SYMSGF)
     A            WSNAS         30A  B  8 12TEXT('Navn Selger')
     A                                      DSPATR(UL)
     A  67                                  DSPATR(RI)
     A  34                                  DSPATR(PR)
     A  55                                  DSPATR(BL)
     A  55                                  COLOR(RED)
     A N55                                  COLOR(YLW)
     A                                  9  2MSGCON(007 TBR0266 *LIBL/SYMSGF)
     A            WSADS2        30A  B  9 12TEXT('Adresse-3 Selger')
     A                                      DSPATR(UL)
     A  34                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                  9 44MSGCON(009 TBR0268 *LIBL/SYMSGF)
     A            WSADS3        30A  B 10 12TEXT('Adresse-2 Selger')
     A                                      COLOR(YLW)
     A                                 10 44MSGCON(010 TBR0267 *LIBL/SYMSGF)
     A            WSRFA         15A  B 10 55TEXT('Referanse Agent')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 11  2MSGCON(077 TBR0269 *LIBL/SYMSGF)
     A                                 12  2MSGCON(008 TBR0270 *LIBL/SYMSGF)
     A                                 12 44MSGCON(018 TBR0271 *LIBL/SYMSGF)
     A                                 13  2MSGCON(009 TBR0264 *LIBL/SYMSGF)
     A            WSKNK          8Y 0B 13 12TEXT('Kundenr Kjøper')
     A                                      DSPATR(UL)
     A  62
     AO 61                                  DSPATR(RI)
     A  32                                  DSPATR(PR)
     A  54                                  DSPATR(BL)
     A                                      EDTCDE(4)
     A  54                                  COLOR(RED)
     A N54                                  COLOR(YLW)
     A*           FRANM1        34A  B 13 44COLOR(YLW)
     A                                 14  2MSGCON(005 TBR0265 *LIBL/SYMSGF)
     A            WSNAK         30A  B 14 12TEXT('Navn Kjøper')
     A                                      DSPATR(UL)
     A  62                                  DSPATR(RI)
     A  32                                  DSPATR(PR)
     A  54                                  DSPATR(BL)
     A  54                                  COLOR(RED)
     A N54                                  COLOR(YLW)
     A*           FRANM2        34A  B 14 44COLOR(YLW)
     A                                 15  2MSGCON(007 TBR0266 *LIBL/SYMSGF)
     A            WSADK2        30A  B 15 12TEXT('Adresse-2 Kjøper')
     A                                      DSPATR(UL)
     A  32                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A            WSADK3        30A  B 16 12TEXT('Adresse-3 Kjøper')
     A                                      COLOR(YLW)
     A                                 17  2MSGCON(011 TBR0275 *LIBL/SYMSGF)
     A            WSKNT          8Y 0B 17 14TEXT('Kundenr Transportør')
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSTRNV        18A  B 17 23COLOR(YLW)
     A                                 19  2MSGCON(078 TBR0281 *LIBL/SYMSGF)
     A            WSGM1         15A  B 20  2TEXT('Godsmerking-1')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSNT           7Y 0B 20 18TEXT('Antall')
     A                                      DSPATR(UL)
     A  87                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSVS1         25A  B 20 26TEXT('Vareslag-1')
     A                                      DSPATR(UL)
     A  86                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSVKT          9Y 0B 20 52TEXT('Vekt')
     A                                      DSPATR(UL)
     A  85                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSLM           4Y 2B 20 62TEXT('Lastemeter')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSM3           7Y 3B 20 68TEXT('Kubikk')
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                      DSPATR(UL)
     A            WSGM2         15A  B 21  2TEXT('Godsmerking-2')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSVS2         25A  B 21 26TEXT('Vareslag-2')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 22  2MSGCON(018 TBR0258 *LIBL/SYMSGF)
     A            W2KDME         1A  B 22 53TEXT('Merkelapp  /X')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'X' 'L')
     A            WSOPDG         7Y 0O 22 21DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 22 55MSGCON(010 TBR0282 *LIBL/SYMSGF)
     A            MSGL1         78A  O 23  2MSGID(&MSGLN1 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGLN1         7A  P
     A            MSGNR          7A  P
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  6  2VALUES(' ' '2' '3')
     A  98                                  DSPATR(RI)
     A  98                                  DSPATR(PC)
     A            HEOPD          7Y 0O  6 10EDTCDE(4)
     A            HEST           1A  O  6 18
     A            HESG           3A  O  6 20
     A            WHEGN         11A  O  6 24
     A            HEPOS1    R        H      REFFLD(HEADFR/HEPOS1 *LIBL/HEADF)
     A            W2POS1         4A  O  6 36
     A            HEPOS2    R        O  6 41REFFLD(HEADFR/HEPOS2 *LIBL/HEADF)
     A            WSPOSK         8A  O  6 46
     A            HEOT           2A  O  6 55
     A            HEFR           3A  O  6 58
     A            HENT           7Y 0O  6 62EDTCDE(4)
     A            HEVKT          9Y 0O  6 70EDTCDE(4)
     A            HEDTR          8Y 0H
     A            WSDTR          6Y 0O  7  6DSPATR(UL)
     A                                      EDTCDE(4)
     A            HESDF          5A  O  7 13DSPATR(UL)
     A            HESDT          5A  O  7 19DSPATR(UL)
     A            HEPRO          8Y 0O  7 25DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
NRFA A*           HERFA         15A  O  7 34DSPATR(UL)
NRFA A            HERFA15       15A  O  7 34DSPATR(UL)
     A            WHEGNN        11A  O  7 50DSPATR(UL)
     A            HEUR           1A  O  7 71DSPATR(UL)
     A            HEAVD          4Y 0O  7 74EDTCDE(4)
     A                                      DSPATR(UL)
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0009)
     A                                      SFLPAG(0008)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                  1  2'SYSPED/SYGR10'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(045 DIV0596 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 65DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  2 65TIME
     A                                      COLOR(BLU)
     A            ZRECNO         4S 0O  1 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  2  2MSGCON(009 DIV0050 *LIBL/SYMSGF)
     A            WSAVD          4Y 0O  2 12EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  2 20MSGCON(023 DIV0058 *LIBL/SYMSGF)
     A            WSPRO          8  0O  2 44DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  3  2MSGCON(015 DIV0656 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 24MSGCON(040 SHU0134 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(078 DIV0045 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  5  2MSGCON(078 DIV0046 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A*
     A          R BUNN
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CA12(12)
     A                                      SLNO(22)
     A                                      CLRL(03)
     A                                      OVERLAY
     A            MSGNR          7A  P
     A                                  1  2MSGCON(023 DIV0058 *LIBL/SYMSGF)
     A            WSPRO          8Y 0B  1 27DSPATR(UL)
     A                                      EDTCDE(4)
     A                                  2  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  3  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A*
     A          R NYVARE
     A                                      OVERLAY
     A                                  1  3'SYSPED/SYGR10'
     A                                      COLOR(BLU)
     A                                  1 20MSGCON(045 DIV0596 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 67DATE
     A                                      EDTCDE(Y)
     A                                  2 67TIME
     A                                  3  2MSGCON(078 DIV0045 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  4  2MSGCON(078 DIV0046 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                 22  2MSGCON(023 DIV0058 *LIBL/SYMSGF)
     A            WSPRO          8Y 0B 22 27DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      DSPATR(PC)
