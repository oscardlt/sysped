      *
      ***********************************************************
      *** PROGRAM SKAL ARBEIDE MED X-AVTALER                  ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01 02 04-11 13-24
      *** LEDIGE INDIKATORER 28 29 31 32 34-88 95-97
      ***********************************************************
      *
     FLLKAFL01  IF   E           K DISK
     FLLXFAK    UF A E           K DISK
     FKOFAST    IF   E           K DISK
     FLL059D    CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D                 DS
     D  KFTXT                  1     50
     D  XFTXT                  1     40
      *
     C                   EXSR      INIT
      *
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
      *
     C     LLKAFL01K1    SETLL     LLKAFL01
     C     LLKAFL01K2    READE     LLKAFL01                               94
     C  N94KALPT         COMP      'X99'                              94
     C     *IN94         CABEQ     *ON           SLUTT
      *
     C                   DOW       *IN94 = *OFF
     C                   MOVE      KALPT         LXLPT
     C     LLXFAKK       CHAIN(N)  LLXFAK                             33
     C                   IF        *IN33
     C                   MOVE      *ZEROS        LXANT
     C                   END
     C                   MOVEL     KALPT         KFKOD
     C     KOFASTK       CHAIN     KOFAST                             33
     C                   ADD       1             ZRECNO
     C                   WRITE     SUBFIL
     C     LLKAFL01K2    READE     LLKAFL01                               94
     C  N94KALPT         COMP      'X99'                              94
     C  N94              END
      *
     C                   Z-ADD     ZRECNO        YRECNO            5 0
     C                   Z-ADD     1             ZRECNO
     C                   Z-ADD     3             LINNBR
     C                   Z-ADD     2             POSNBR
      *
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C                   WRITE     BUNN
     C                   EXFMT     SUBCTL
      *
     C                   SETOFF                                       9830
      *
     C  N03              GOTO      START3
     C                   EXSR      OPDFIL
      *
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
      *
     C***********************************************
     C     OPDFIL        BEGSR
     C***********************************************
     C     1             DO        YRECNO        ZRECNO
     C     ZRECNO        CHAIN     SUBFIL                             33
     C                   MOVE      LXANT         XXANT             9 2
     C     LLXFAKK       CHAIN     LLXFAK                             33
     C                   MOVE      XXANT         LXANT
     C                   IF        LXANT = 0
     C  N33              DELETE    LLXFAKR
     C                   ELSE
     C  N33              UPDATE    LLXFAKR
     C   33              WRITE     LLXFAKR
     C                   END
     C                   ENDDO
      *
     C                   ENDSR
      *
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     LLXFAKK       KLIST
     C                   KFLD                    LXMN
     C                   KFLD                    LXLPT
     C     LLKAFL01K1    KLIST
     C                   KFLD                    KAGRUP
     C                   KFLD                    KAKJED
     C                   KFLD                    KATYPE
     C                   KFLD                    KALLT
     C                   KFLD                    XALPT
     C     LLKAFL01K2    KLIST
     C                   KFLD                    KAGRUP
     C                   KFLD                    KAKJED
     C                   KFLD                    KATYPE
     C                   KFLD                    KALLT
     C     KOFASTK       KLIST
     C                   KFLD                    KFTYP
     C                   KFLD                    KFKOD
      *
     C     *ENTRY        PLIST
     C                   PARM                    UGRP              8 0
     C                   PARM                    UPNR              8 0
     C                   PARM                    UMN               7 0
      *
     C                   WRITE     WINDOWSZ
     C                   MOVE      UGRP          KAGRUP
     C                   MOVE      UPNR          KAKJED
     C                   MOVE      'U'           KATYPE
     C                   MOVE      *BLANKS       KALLT
     C                   MOVE      'X  '         XALPT             3
     C                   MOVE      UMN           LXMN
     C                   EVAL      KFTYP = 'LAGERUTTAK'
      *
     C                   ENDSR
      *
