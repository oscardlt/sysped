      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/TNCI017R MODULE(*LIBL/TNCI017R *LIBL/INCGI)
CRTPGM*        ACTGRP(*NEW) AUT(*USE)
      *
      *********************************************************************
      /copy SYSPEDS/qrpgsrcpy,hspecs
      /copy SYSPEDS/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FSVNL      UF A E           K DISK    USROPN
     F                                     PREFIX(X)
     FSVNLCN    UF A E           K DISK    USROPN
     F                                     PREFIX(X)
     FSVNLDK    UF A E           K DISK    USROPN
     F                                     PREFIX(X)
     FSVNLKL    UF A E           K DISK    USROPN
     F                                     PREFIX(X)
     FSVNLMN    UF A E           K DISK    USROPN
     F                                     PREFIX(X)
     FSVNLPK    UF A E           K DISK    USROPN
     F                                     PREFIX(X)
     FSVNLVT    UF A E           K DISK    USROPN
     F                                     PREFIX(X)
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy SYSPEDS/qrpgsrcpy,prototypeb
      /copy SYSPEDS/qrpgsrcpy,usec
      /copy SYSPEDS/qrpgsrcpy,variables3
      *
      * Varible for
     D WSUSER          S             10
     D TVAVD_A         S              4
     D TVAVD           S              4  0
     D TVTDN_A         S              7
     D TVTDN           S              7  0
     D TVLI_A          S              5
     D TVLI            S              5  0
     D WMODE           S              2
     D NVVNT_A         S              8
     D NVVNT           S              8  0
     D NVVT            S             70
     D NVVTSK          S              2
     D NVVKTB_A        S              9
     D NVVKTB          S              9  3
     D NVVKTN_A        S              9
     D NVVKTN          S              9  3
     D NVCNR           S             17
     D NVNT_A          S              5
     D NVNTEH_A        S              5
     D NVFV            S              2
     D NVFVNT_A        S             11
     D NVFVNT          S             11  3
     D NVCT            S              2
     D NVCTB           S             70
     D NVCTSK          S              2
     D NVCTP           S             35
     D JSONstring      S          32000
     D Error           S            150
     D                 DS
     D  NVVT2                  1     70
     D  NVVT3                 71    140
     D  NVVT4                141    210
     D  VT                     1    210    DIM(3)
     D                 DS
     D  NVDTY                  1      4
     D  NVDREF                 5     39
     D  NVDSK                 40     41
     D  NVDO                  42     67
     D  NVDOSK                68     69
     D  ORIDK                  1     69
     D                 DS
     D  NVDTY2                 1      4
     D  NVDREF2                5     39
     D  NVDSK2                40     41
     D  NVDO2                 42     67
     D  NVDOSK2               68     69
     D  NVDTY3                70     73
     D  NVDREF3               74    108
     D  NVDSK3               109    110
     D  NVDO3                111    136
     D  NVDOSK3              137    138
     D  NVDTY4               139    142
     D  NVDREF4              143    177
     D  NVDSK4               178    179
     D  NVDO4                180    205
     D  NVDOSK4              206    207
     D  NVDTY5               208    211
     D  NVDREF5              212    246
     D  NVDSK5               247    248
     D  NVDO5                249    274
     D  NVDOSK5              275    276
     D  DK                     1    276    DIM(4)
     D                 DS
     D  NVCNR2                 1     17
     D  NVCNR3                18     34
     D  NVCNR4                35     51
     D  NVCNR5                52     68
     D  NVCNR6                69     85
     D  CN                     1     85    DIM(5)
     D                 DS
     D  NVMN                   1     42
     D  NVMNSK                43     44
     D  ORIMN                  1     44
     D                 DS
     D  NVMN2                  1     42
     D  NVMNSK2               43     44
     D  NVMN3                 45     86
     D  NVMNSK3               87     88
     D  NVMN4                 89    130
     D  NVMNSK4              131    132
     D  NVMN5                133    174
     D  NVMNSK5              175    176
     D  NVMN6                177    218
     D  NVMNSK6              219    220
     D  MN                     1    220    DIM(5)
     D                 DS
     D  NVEH                   1      2
     D  NVNT                   3      7  0
     D  NVNTEH                 8     12  0
     D  ORIKL                  1     12
     D                 DS
     D  NVEH2                  1      2
     D  NVNT2                  3      7  0
     D  NVNTEH2                8     12  0
     D  NVEH3                 13     14
     D  NVNT3                 15     19  0
     D  NVNTEH3               20     24  0
     D  NVEH4                 25     26
     D  NVNT4                 27     31  0
     D  NVNTEH4               32     36  0
     D  NVEH5                 37     38
     D  NVNT5                 39     43  0
     D  NVNTEH5               44     48  0
     D  NVEH6                 49     50
     D  NVNT6                 51     55  0
     D  NVNTEH6               56     60  0
     D  KL                     1     60    DIM(5)
     D                 DS
     D  NVCTP2                 1     35
     D  NVCTP3                36     70
     D  NVCTP4                71    105
     D  NVCTP5               106    140
     D  NVCTP6               141    175
     D  PK                     1    175    DIM(5)

      *get input-string
      /copy syspeds/qrpgsrcpy,prolog3

      * Parse input
     C                   EXSR      Parse
      * Open files etc
     C                   EXSR      INIT
      *
     c                   callp     gethtmlifs(
     C                             '/syspeddev/html/JSON.html':
     C                             '/CGITAG/')
     C                   callp     wrtsection('top')
      * Teste input og utfør
     C  N50              EXSR      TEST

      * ............................................................................................
      * skriv JSON-Object start..(alltid '{' + x ant param. m/komma mellom (IKKE komma etter siste))
      * ............................................................................................
      *
      /free
        JSONstring='{'
        +'¬user¬ : ¬'+%trim(WSUSER)+'¬, '
        +'¬tvavd¬ : ¬'+%trim(%editc(tvavd:'Z'))+'¬, '
        +'¬tvtdn¬ : ¬'+%trim(%editc(tvtdn:'Z'))+'¬, '
        +'¬tvli¬ : ¬'+%trim(%editc(tvli:'Z'))+'¬, '
        +'¬mode¬ : ¬'+%trim(WMODE)+'¬, '
        +'¬errMsg¬ : ¬'+%trim(Error)+'¬, ';
      /end-free
      * JSONfix escaper " i tekst,  for deretter å bytte ¬->"
     c                   call      'JSONFIX'
     c                   parm                    JSONstring
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')
      *
      * ............................................................................................
      * Skriv JSON-LISTE Start (en liste er EN parameter(fra [ til   )
      * ............................................................................................
     c                   eval      JSONstring ='"lineupdate" : ['
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')
      * ............................................................................................
      * Skriv JSON-Liste/Array  Avslutning
      * ............................................................................................
     c                   eval      JSONstring =']'
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')
      * ............................................................................................
      * Skriv JSON-string Avsluttning
      * ............................................................................................
     c                   eval      JSONstring ='}'
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')
      *
      * Quit
     C                   exsr      Exit


      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Lukk fil
     C                   CLOSE     BRIDF
     C  N50              CLOSE     SVNL
     C  N50              CLOSE     SVNLCN
     C  N50              CLOSE     SVNLDK
     C  N50              CLOSE     SVNLKL
     C  N50              CLOSE     SVNLMN
     C  N50              CLOSE     SVNLPK
     C  N50              CLOSE     SVNLVT

      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   MOVE      *ON           *INLR
     C                   return
     C                   endsr
      *
      *********************************************************
     C     Parse         Begsr
      *********************************************************
      * Get input
     C                   EVAL      WSUSER  = zhbgetvar('USER')
     C                   EVAL      TVAVD_A   = zhbgetvar('AVD')
     C                   EVAL      TVAVD     = c2n2(TVAVD_A)
     C                   EVAL      TVTDN_A   = zhbgetvar('OPD')
     C                   EVAL      TVTDN     = c2n2(TVTDN_A)
     C                   EVAL      TVLI_A    = zhbgetvar('LIN')
     C                   EVAL      TVLI      = c2n2(TVLI_A)
     C                   EVAL      WMODE     = zhbgetvar('MODE')
     C                   EVAL      NVVNT_A   = zhbgetvar('NVVNT')
     C                   EVAL      NVVNT     = c2n2(NVVNT_A)
     C                   EVAL      NVVT      = zhbgetvar('NVVT')
     C                   EVAL      NVVTSK    = zhbgetvar('NVVTSK')
     C                   EVAL      NVVT2     = zhbgetvar('NVVT2')
     C                   EVAL      NVVT3     = zhbgetvar('NVVT3')
     C                   EVAL      NVVT4     = zhbgetvar('NVVT4')
     C                   EVAL      NVVKTB_A  = zhbgetvar('NVVKTB')
     C                   EVAL      NVVKTB    = c2n2(NVVKTB_A)
     C                   EVAL      NVVKTN_A  = zhbgetvar('NVVKTN')
     C                   EVAL      NVVKTN    = c2n2(NVVKTN_A)
     C                   EVAL      NVDTY     = zhbgetvar('NVDTY')
     C                   EVAL      NVDREF    = zhbgetvar('NVDREF')
     C                   EVAL      NVDSK     = zhbgetvar('NVDSK')
     C                   EVAL      NVDO      = zhbgetvar('NVDO')
     C                   EVAL      NVDOSK    = zhbgetvar('NVDOSK')
     C                   EVAL      NVDTY2    = zhbgetvar('NVDTY2')
     C                   EVAL      NVDREF2   = zhbgetvar('NVDREF2')
     C                   EVAL      NVDSK2    = zhbgetvar('NVDSK2')
     C                   EVAL      NVDO2     = zhbgetvar('NVDO2')
     C                   EVAL      NVDOSK2   = zhbgetvar('NVDOSK2')
     C                   EVAL      NVDTY3    = zhbgetvar('NVDTY3')
     C                   EVAL      NVDREF3   = zhbgetvar('NVDREF3')
     C                   EVAL      NVDSK3    = zhbgetvar('NVDSK3')
     C                   EVAL      NVDO3     = zhbgetvar('NVDO3')
     C                   EVAL      NVDOSK3   = zhbgetvar('NVDOSK3')
     C                   EVAL      NVDTY4    = zhbgetvar('NVDTY4')
     C                   EVAL      NVDREF4   = zhbgetvar('NVDREF4')
     C                   EVAL      NVDSK4    = zhbgetvar('NVDSK4')
     C                   EVAL      NVDO4     = zhbgetvar('NVDO4')
     C                   EVAL      NVDOSK4   = zhbgetvar('NVDOSK4')
     C                   EVAL      NVDTY5    = zhbgetvar('NVDTY5')
     C                   EVAL      NVDREF5   = zhbgetvar('NVDREF5')
     C                   EVAL      NVDSK5    = zhbgetvar('NVDSK5')
     C                   EVAL      NVDO5     = zhbgetvar('NVDO5')
     C                   EVAL      NVDOSK5   = zhbgetvar('NVDOSK5')
     C                   EVAL      NVCNR     = zhbgetvar('NVCNR')
     C                   EVAL      NVCNR2    = zhbgetvar('NVCNR2')
     C                   EVAL      NVCNR3    = zhbgetvar('NVCNR3')
     C                   EVAL      NVCNR4    = zhbgetvar('NVCNR4')
     C                   EVAL      NVCNR5    = zhbgetvar('NVCNR5')
     C                   EVAL      NVCNR6    = zhbgetvar('NVCNR6')
     C                   EVAL      NVMN      = zhbgetvar('NVMN')
     C                   EVAL      NVMNSK    = zhbgetvar('NVMNSK')
     C                   EVAL      NVMN2     = zhbgetvar('NVMN2')
     C                   EVAL      NVMNSK2   = zhbgetvar('NVMNSK2')
     C                   EVAL      NVMN3     = zhbgetvar('NVMN3')
     C                   EVAL      NVMNSK3   = zhbgetvar('NVMNSK3')
     C                   EVAL      NVMN4     = zhbgetvar('NVMN4')
     C                   EVAL      NVMNSK4   = zhbgetvar('NVMNSK4')
     C                   EVAL      NVMN5     = zhbgetvar('NVMN5')
     C                   EVAL      NVMNSK5   = zhbgetvar('NVMNSK5')
     C                   EVAL      NVMN6     = zhbgetvar('NVMN6')
     C                   EVAL      NVMNSK6   = zhbgetvar('NVMNSK6')
     C                   EVAL      NVEH      = zhbgetvar('NVEH')
     C                   EVAL      NVNT_A    = zhbgetvar('NVNT')
     C                   EVAL      NVNT      = c2n2(NVNT_A)
     C                   EVAL      NVNTEH_A  = zhbgetvar('NVNTEH')
     C                   EVAL      NVNTEH    = c2n2(NVNTEH_A)
     C                   EVAL      NVEH2     = zhbgetvar('NVEH2')
     C                   EVAL      NVNT_A    = zhbgetvar('NVNT2')
     C                   EVAL      NVNT2     = c2n2(NVNT_A)
     C                   EVAL      NVNTEH_A  = zhbgetvar('NVNTEH2')
     C                   EVAL      NVNTEH2   = c2n2(NVNTEH_A)
     C                   EVAL      NVEH3     = zhbgetvar('NVEH3')
     C                   EVAL      NVNT_A    = zhbgetvar('NVNT3')
     C                   EVAL      NVNT3     = c2n2(NVNT_A)
     C                   EVAL      NVNTEH_A  = zhbgetvar('NVNTEH3')
     C                   EVAL      NVNTEH3   = c2n2(NVNTEH_A)
     C                   EVAL      NVEH4     = zhbgetvar('NVEH4')
     C                   EVAL      NVNT_A    = zhbgetvar('NVNT4')
     C                   EVAL      NVNT4     = c2n2(NVNT_A)
     C                   EVAL      NVNTEH_A  = zhbgetvar('NVNTEH4')
     C                   EVAL      NVNTEH4   = c2n2(NVNTEH_A)
     C                   EVAL      NVEH5     = zhbgetvar('NVEH5')
     C                   EVAL      NVNT_A    = zhbgetvar('NVNT5')
     C                   EVAL      NVNT5     = c2n2(NVNT_A)
     C                   EVAL      NVNTEH_A  = zhbgetvar('NVNTEH5')
     C                   EVAL      NVNTEH5   = c2n2(NVNTEH_A)
     C                   EVAL      NVEH6     = zhbgetvar('NVEH6')
     C                   EVAL      NVNT_A    = zhbgetvar('NVNT6')
     C                   EVAL      NVNT6     = c2n2(NVNT_A)
     C                   EVAL      NVNTEH_A  = zhbgetvar('NVNTEH6')
     C                   EVAL      NVNTEH6   = c2n2(NVNTEH_A)
     C                   EVAL      NVFV      = zhbgetvar('NVFV')
     C                   EVAL      NVFVNT_A  = zhbgetvar('NVFVNT')
     C                   EVAL      NVFVNT    = c2n2(NVFVNT_A)
     C                   EVAL      NVCT      = zhbgetvar('NVCT')
     C                   EVAL      NVCTB     = zhbgetvar('NVCTB')
     C                   EVAL      NVCTSK    = zhbgetvar('NVCTSK')
     C                   EVAL      NVCTP     = zhbgetvar('NVCTP')
     C                   EVAL      NVCTP2    = zhbgetvar('NVCTP2')
     C                   EVAL      NVCTP3    = zhbgetvar('NVCTP3')
     C                   EVAL      NVCTP4    = zhbgetvar('NVCTP4')
     C                   EVAL      NVCTP5    = zhbgetvar('NVCTP5')
     C                   EVAL      NVCTP6    = zhbgetvar('NVCTP6')
      *
     c                   ENDSR
      *********************************************************
     C     INIT          Begsr
      *********************************************************
     C                   OPEN      BRIDF
      * Test innlogging
     C     WSUSER        CHAIN     BRIDF                              50
     C  N50BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C                   call      BILIBL
     C                   end
      *
     C     SVNLKey       klist
     C                   kfld                    TVAVD
     C                   kfld                    TVTDN
     C                   kfld                    TVLI
      *
     C   50              eval      Error = 'Invalid userID'
     C  N50              OPEN      SVNL
     C  N50              OPEN      SVNLCN
     C  N50              OPEN      SVNLDK
     C  N50              OPEN      SVNLKL
     C  N50              OPEN      SVNLMN
     C  N50              OPEN      SVNLPK
     C  N50              OPEN      SVNLVT

     c                   endsr
      *
      *______________________________________________________
      * TEST                                             TEST
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     TEST          BEGSR
     C     WMODE         IFNE      'A'
     C     SVNLKey       CHAIN     SVNL                               55
     C                   ENDIF
      * MODE A=Add  D=Delete  U=Update  R=Undo
     C                   SELECT
     C     WMODE         WHENEQ    'A'
     C                   eval      Error = *blanks
     C                   EXSR      SRA
     C     WMODE         WHENEQ    'D'
     C                   IF        *IN55=*ON
     C                   eval      Error = 'Order not found'
     C                   ELSE
     C                   eval      Error = *blanks
     C                   EXSR      SRD
     C                   ENDIF
     C     WMODE         WHENEQ    'R'
     C                   IF        *IN55=*ON
     C                   eval      Error = 'Order not found'
     C                   ELSE
     C                   eval      Error = *blanks
     C                   EXSR      SRR
     C                   ENDIF
     C     WMODE         WHENEQ    'U'
     C                   IF        *IN55=*ON
     C                   eval      Error = 'Order not found'
     C                   ELSE
     C                   eval      Error = *blanks
     C                   EXSR      SRU
     C                   ENDIF
     C                   OTHER
     C                   eval      Error = 'Wrong mode'
     C                   ENDSL
     C                   ENDSR
      *______________________________________________________
      * Add                                               SRA
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     SRA           BEGSR
     C                   CLEAR                   SVNLR
     C                   EVAL      XTVAVD  = TVAVD
     C                   EVAL      XTVTDN  = TVTDN
     C                   EVAL      XTVLI   = TVLI
     C                   EXSR      MOVSVNL
     C                   WRITE     SVNLR
      * Update additional info.
     C                   EXSR      UPDSVNL
      *
     C                   ENDSR
      *______________________________________________________
      * Delete                                            SRD
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     SRD           BEGSR
     C                   DELETE    SVNLR
      * Delete additional info.
     C                   EXSR      DLTSVNL
     C                   ENDSR
      *______________________________________________________
      * Undo                                              SRR
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     SRR           BEGSR
     C                   EVAL      XTV01   = *BLANKS
     C                   EVAL      XNVVNT  = XTVVNT
     C                   EVAL      XNVVT   = XTVVT
     C                   EVAL      XNVVTSK = XTVVTSK
     C                   EVAL      XNVVKTB = XTVVKTB
     C                   EVAL      XNVVKTN = XTVVKTN
     C                   EVAL      XNVCNR  = XTVCNR
     C                   EVAL      XNVDTY  = XTVDTY
     C                   EVAL      XNVDREF = XTVDREF
     C                   EVAL      XNVDSK  = XTVDSK
     C                   EVAL      XNVDO   = XTVDO
     C                   EVAL      XNVDOSK = XTVDOSK
     C                   EVAL      XNVMN   = XTVMN
     C                   EVAL      XNVMNSK = XTVMNSK
     C                   EVAL      XNVEH   = XTVEH
     C                   EVAL      XNVNT   = XTVNT
     C                   EVAL      XNVNTEH = XTVNTEH
     C                   EVAL      XNVFV   = XTVFV
     C                   EVAL      XNVFVNT = XTVFVNT
     C                   EVAL      XNVCT   = *BLANKS
     C                   EVAL      XNVCTB  = *BLANKS
     C                   EVAL      XNVCTSK = *BLANKS
     C                   EVAL      XNVCTP  = *BLANKS
     C                   UPDATE    SVNLR
      *
     C     SVNLKey       SETLL     SVNLDK
     C     SVNLKey       READE     SVNLDK                                 33
     C                   DOW       *IN33 = *OFF
     C                   IF        XTVDTY = *BLANKS
     C                   DELETE    SVNLDKR
     C                   ELSE
     C                   MOVE      XTVDTY        XNVDTY
     C                   MOVE      XTVDREF       XNVDREF
     C                   MOVE      XTVDSK        XNVDSK
     C                   MOVE      XTVDO         XNVDO
     C                   MOVE      XTVDOSK       XNVDOSK
     C                   UPDATE    SVNLDKR
     C                   END
     C     SVNLKey       READE     SVNLDK                                 33
     C  N33              END
      *
     C     SVNLKey       SETLL     SVNLMN
     C     SVNLKey       READE     SVNLMN                                 33
     C                   DOW       *IN33 = *OFF
     C                   IF        XTVMN = *BLANKS
     C                   DELETE    SVNLMNR
     C                   ELSE
     C                   MOVE      XTVMN         XNVMN
     C                   MOVE      XTVMNSK       XNVMNSK
     C                   UPDATE    SVNLMNR
     C                   END
     C     SVNLKey       READE     SVNLMN                                 33
     C  N33              END
      *
     C     SVNLKey       SETLL     SVNLCN
     C     SVNLKey       READE     SVNLCN                                 33
     C                   DOW       *IN33 = *OFF
     C                   IF        XTVCNR = *BLANKS
     C                   DELETE    SVNLCNR
     C                   ELSE
     C                   MOVE      XTVCNR        XNVCNR
     C                   UPDATE    SVNLCNR
     C                   END
     C     SVNLKey       READE     SVNLCN                                 33
     C  N33              END
      *
     C     SVNLKey       SETLL     SVNLKL
     C     SVNLKey       READE     SVNLKL                                 33
     C                   DOW       *IN33 = *OFF
     C                   IF        XTVEH = *BLANKS
     C                   DELETE    SVNLKLR
     C                   ELSE
     C                   MOVE      XTVEH         XNVEH
     C                   MOVE      XTVNT         XNVNT
     C                   MOVE      *ZEROS        XNVNTEH
     C                   UPDATE    SVNLKLR
     C                   END
     C     SVNLKey       READE     SVNLKL                                 33
     C  N33              END
      *
     C     SVNLKey       SETLL     SVNLVT
     C     SVNLKey       READE     SVNLVT                                 33
     C                   DOW       *IN33 = *OFF
     C                   IF        XTVVT = *BLANKS
     C                   DELETE    SVNLVTR
     C                   ELSE
     C                   MOVE      XTVVT         XNVVT
     C                   UPDATE    SVNLVTR
     C                   END
     C     SVNLKey       READE     SVNLVT                                 33
     C  N33              END
      *
     C     SVNLKey       SETLL     SVNLPK
     C     SVNLKey       READE     SVNLPK                                 33
     C                   DOW       *IN33 = *OFF
     C                   DELETE    SVNLPKR
     C     SVNLKey       READE     SVNLPK                                 33
     C  N33              END
      *
     C                   ENDSR
      *
      *______________________________________________________
      * Update                                            SRU
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     SRU           BEGSR
     C                   EXSR      MOVSVNL
     C                   UPDATE    SVNLR
      * Delete additional info.
     C                   EXSR      DLTSVNL
      * Update additional info.
     C                   EXSR      UPDSVNL
      *
     C                   ENDSR
      *
      *______________________________________________________
      * Flytt                                         MOVSVNL
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     MOVSVNL       BEGSR
      *
     C                   EVAL      XNVVNT  = NVVNT
     C                   EVAL      XNVVT   = NVVT
     C                   EVAL      XNVVTSK = NVVTSK
     C                   EVAL      XNVVKTB = NVVKTB
     C                   EVAL      XNVVKTN = NVVKTN
     C                   EVAL      XNVDTY  = NVDTY
     C                   EVAL      XNVDREF = NVDREF
     C                   EVAL      XNVDSK  = NVDSK
     C                   EVAL      XNVDO   = NVDO
     C                   EVAL      XNVDOSK = NVDOSK
     C                   EVAL      XNVCNR  = NVCNR
     C                   EVAL      XNVMN   = NVMN
     C                   EVAL      XNVMNSK = NVMNSK
     C                   EVAL      XNVEH   = NVEH
     C                   EVAL      XNVNT   = NVNT
     C                   EVAL      XNVNTEH = NVNTEH
     C                   EVAL      XNVFV   = NVFV
     C                   EVAL      XNVFVNT = NVFVNT
     C                   EVAL      XNVCT   = NVCT
     C                   EVAL      XNVCTB  = NVCTB
     C                   EVAL      XNVCTSK = NVCTSK
     C                   EVAL      XNVCTP  = NVCTP
      *
     C                   ENDSR
      *
      *______________________________________________________
      * Slett tillegg info.                           DLTSVNL
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     DLTSVNL       BEGSR
     C     SVNLKey       SETLL     SVNLCN
     C     SVNLKey       READE     SVNLCN                                 33
     C                   DOW       *IN33 = *OFF
     C                   DELETE    SVNLCNR
     C     SVNLKey       READE     SVNLCN                                 33
     C                   ENDDO
      *
     C     SVNLKey       SETLL     SVNLDK
     C     SVNLKey       READE     SVNLDK                                 33
     C                   DOW       *IN33 = *OFF
     C                   DELETE    SVNLDKR
     C     SVNLKey       READE     SVNLDK                                 33
     C                   ENDDO
      *
     C     SVNLKey       SETLL     SVNLKL
     C     SVNLKey       READE     SVNLKL                                 33
     C                   DOW       *IN33 = *OFF
     C                   DELETE    SVNLKLR
     C     SVNLKey       READE     SVNLKL                                 33
     C                   ENDDO
      *
     C     SVNLKey       SETLL     SVNLMN
     C     SVNLKey       READE     SVNLMN                                 33
     C                   DOW       *IN33 = *OFF
     C                   DELETE    SVNLMNR
     C     SVNLKey       READE     SVNLMN                                 33
     C                   ENDDO
      *
     C     SVNLKey       SETLL     SVNLPK
     C     SVNLKey       READE     SVNLPK                                 33
     C                   DOW       *IN33 = *OFF
     C                   DELETE    SVNLPKR
     C     SVNLKey       READE     SVNLPK                                 33
     C                   ENDDO
      *
     C     SVNLKey       SETLL     SVNLVT
     C     SVNLKey       READE     SVNLVT                                 33
     C                   DOW       *IN33 = *OFF
     C                   DELETE    SVNLVTR
     C     SVNLKey       READE     SVNLVT                                 33
     C                   ENDDO
      *
     C                   ENDSR
      *
      *______________________________________________________
      * Oppdater tilleggs info.                       UPDSVNL
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     UPDSVNL       BEGSR
      *
     C     1             DO        3             XN                3 0
     C                   IF        VT(XN) <> *BLANKS
     C                   EVAL      XTVVT   = VT(XN)
     C                   WRITE     SVNLVTR
     C                   END
     C                   ENDDO
      *
     C     1             DO        4             XN                3 0
     C                   IF        DK(XN) <> *BLANKS
     C                   EVAL      ORIDK   = DK(XN)
     C                   EVAL      XNVDTY  = NVDTY
     C                   EVAL      XNVDREF = NVDREF
     C                   EVAL      XNVDSK  = NVDSK
     C                   EVAL      XNVDO   = NVDO
     C                   EVAL      XNVDOSK = NVDOSK
     C                   WRITE     SVNLDKR
     C                   END
     C                   ENDDO
      *
     C     1             DO        5             XN                3 0
     C                   IF        CN(XN) <> *BLANKS
     C                   EVAL      XNVCNR  = CN(XN)
     C                   WRITE     SVNLCNR
     C                   END
     C                   ENDDO
      *
     C     1             DO        5             XN                3 0
     C                   IF        MN(XN) <> *BLANKS
     C                   EVAL      ORIMN   = MN(XN)
     C                   EVAL      XNVMN   = NVMN
     C                   EVAL      XNVMNSK = NVMNSK
     C                   WRITE     SVNLMNR
     C                   END
     C                   ENDDO
      *
     C     1             DO        5             XN                3 0
     C                   IF        %SUBST(KL(XN):1:2) <> *BLANKS
     C                   EVAL      ORIKL   = KL(XN)
     C                   EVAL      XNVEH   = NVEH
     C                   EVAL      XNVNT   = NVNT
     C                   EVAL      XNVNTEH = NVNTEH
     C                   WRITE     SVNLKLR
     C                   END
     C                   ENDDO
      *
     C     1             DO        5             XN                3 0
     C                   IF        PK(XN) <> *BLANKS
     C                   EVAL      XNVCTP  = PK(XN)
     C                   WRITE     SVNLPKR
     C                   END
     C                   ENDDO
      *
     C                   ENDSR
      *
