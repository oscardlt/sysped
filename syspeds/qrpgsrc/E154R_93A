     H DATEDIT(*YMD)
     FFIRM      IF   E           K DISK
     FEDIM      UF   E           K DISK
     FENAD93A   IF   E           K DISK
     FE153F     IF   E           K DISK
     FE153FL01  IF   E           K DISK
     F                                     RENAME(E153FR:E153FR1)
     FE154F     UF A E           K DISK
     FE154D     CF   E             WORKSTN
     FE154P     O    E             PRINTER OFLIND(*IN27)
      *
     D                 DS
     D  XADVN1                 1      7  0
     D  XADVN2                 8     14  0
     D  XADVN3                15     21  0
     D  XADVN4                22     28  0
     D  XADVN5                29     35  0
     D  XADVN6                36     42  0
     D  XADVN7                43     49  0
     D  XADVN8                50     56  0
     D  XT                     1     56  0
     D                                     DIM(8)
     D                 DS
     D  XDTÅ                   1      2  0
     D  XDTM                   3      4  0
     D  XDTD                   5      6  0
     D  XDT                    1      6  0
     D                 DS
     D  XTIME                  1     12  0
     D  XTM                    1      6  0
     D                 DS
     D XFT01           C                   CONST('The exporter of the -
     D                                     products covered by -
     D                                     this document (custo-
     D                                     m')
     D XFT02           C                   CONST('authorization no. 12-
     D                                     /020) decleares that-
     D                                     , except where other-
     D                                     wice')
     D XFT03           C                   CONST('clearly indicated, t-
     D                                     hese products are of-
     D                                      EEA preferential')
     D XFT04           C                   CONST('origin.')
     D XFT05           C                   CONST(' ')
     D XFT06           C                   CONST('I further declare th-
     D                                     at all origin market-
     D                                      *** are not subject-
     D                                      to')
     D XFT07           C                   CONST('the above declaratio-
     D                                     n and do therefore n-
     D                                     ot obtain originatin-
     D                                     g')
     D XFT08           C                   CONST('status in preferenti-
     D                                     al trade')
     D XFT09           C                   CONST(' ')
     D XFT10           C                   CONST(' ')
     D XFT11           C                   CONST('" NOTICE TO IMPORTER-
     D                                     : Goods spesified in-
     D                                      this document have -
     D                                     been')
     D XFT12           C                   CONST('shipped from Norway -
     D                                     in accordance with t-
     D                                     he Ministry of Forei-
     D                                     gn')
     D XFT13           C                   CONST('Affairs, Official No-
     D                                     tification on Export-
     D                                      Control and may be')
     D XFT14           C                   CONST('subjected to restric-
     D                                     tion if re-exported -
     D                                     from your country."')
     D XFT15           C                   CONST(' ')
     D XNAIX           C                   CONST('RANK XEROX NORDIC AB')
     D SEADI1          C                   CONST('C/O FRAKTARNG LOGIST-
     D                                     IC CENTER')
     D SEADI2          C                   CONST('DOMNARVSVAGEN 9')
     D SEADI3          C                   CONST('S - 166 33 SPÅNGA')
     D DKADI1          C                   CONST('C/O DAN TRANSPORT A/-
     D                                     S')
     D DKADI2          C                   CONST('HELGESHØJ ALLE 8-10')
     D DKADI3          C                   CONST('DK-2630 TAASTRUP')
     D FIADI1          C                   CONST('C/O HUOLINTAKESKUS')
     D FIADI2          C                   CONST('TIKKURILANTIE 63')
     D FIADI3          C                   CONST('FI-01510 VANTAA')
     D XLV             C                   CONST('DDP  EXCL DUTIES -
     D                                     AND TAXES')
     D                UDS
     D  ULK                  301    302
     D  UPRT                 303    312
     D  ULN                  313    318
     D  UDT                  319    324
     D  UWS                  325    334
     D  UPGM                 335    344
      *___________
      * Initiering
      *"""""""""""
     C                   EXSR      INIT
      *_________
      * GET DATA
      *"""""""""
     C     START         TAG
     C                   EXFMT     E154DA
     C                   MOVEA     '00000'       *IN(95)
      *
     C   12              GOTO      SLUTT
     C                   EXSR      TESTB1
     C  N10
     COR 30              GOTO      START
      *___________________
      * PRINT STD-DOCUMENT
      *"""""""""""""""""""
     C                   EXSR      PRTSTD
      *____________
      * END PROGRAM
      *""""""""""""
     C     SLUTT         TAG
     C                   MOVE      '1'           *INLR
     C                   RETURN
      *////////////////////////////////////////////////////////////
      *                                                      INIT /
      *////////////////////////////////////////////////////////////
     C     INIT          BEGSR
     C     KEYMS0        KLIST
     C                   KFLD                    EWMN
     C                   KFLD                    ZGN0
     C     E153FK        KLIST
     C                   KFLD                    EWCCD
     C                   KFLD                    EWGCD
     C     *LIKE         DEFINE    NMN           ZMN
     C     *LIKE         DEFINE    NGN           ZGN0
      *
     C                   MOVE      UDT           XDTP
     C                   MOVEL     XLV           WSLV
     C                   MOVEL     XFT01         WSFT01
     C                   MOVEL     XFT02         WSFT02
     C                   MOVEL     XFT03         WSFT03
     C                   MOVEL     XFT04         WSFT04
     C                   MOVEL     XFT05         WSFT05
     C                   MOVEL     XFT06         WSFT06
     C                   MOVEL     XFT07         WSFT07
     C                   MOVEL     XFT08         WSFT08
     C                   MOVEL     XFT09         WSFT09
     C                   MOVEL     XFT10         WSFT10
     C                   MOVEL     XFT11         WSFT11
     C                   MOVEL     XFT12         WSFT12
     C                   MOVEL     XFT13         WSFT13
     C                   MOVEL     XFT14         WSFT14
     C                   MOVEL     XFT15         WSFT15
     C                   MOVEL     XNAIX         XNAI
     C                   SELECT
     C     ULK           WHENEQ    'SE'
     C                   MOVEL     SEADI1        XADI1
     C                   MOVEL     SEADI2        XADI2
     C                   MOVEL     SEADI3        XADI3
     C     ULK           WHENEQ    'DK'
     C                   MOVEL     DKADI1        XADI1
     C                   MOVEL     DKADI2        XADI2
     C                   MOVEL     DKADI3        XADI3
     C     ULK           WHENEQ    'FI'
     C                   MOVEL     FIADI1        XADI1
     C                   MOVEL     FIADI2        XADI2
     C                   MOVEL     FIADI3        XADI3
     C                   ENDSL
      *
     C     FIFIRM        SETLL     FIRM
     C                   READ      FIRM                                   33
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  TESTE DATA                                        TESTB1 /
      *////////////////////////////////////////////////////////////
     C     TESTB1        BEGSR
      *
     C     WSTRM         COMP      *BLANKS                                99
     C   99              MOVE      '1'           *IN30
     C     WSLV          COMP      *BLANKS                                98
     C   98              MOVE      '1'           *IN30
     C     WSCOL         COMP      0                                      97
     C   97              MOVE      '1'           *IN30
     C     WSWT          COMP      0                                      96
     C   96              MOVE      '1'           *IN30
     C     WSFT01        COMP      *BLANKS                                95
     C   95WSFT02        COMP      *BLANKS                                95
     C   95WSFT03        COMP      *BLANKS                                95
     C   95WSFT04        COMP      *BLANKS                                95
     C   95WSFT05        COMP      *BLANKS                                95
     C   95WSFT06        COMP      *BLANKS                                95
     C   95WSFT07        COMP      *BLANKS                                95
     C   95WSFT08        COMP      *BLANKS                                95
     C   95WSFT09        COMP      *BLANKS                                95
     C   95WSFT10        COMP      *BLANKS                                95
     C   95              MOVE      '1'           *IN30
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * PRINT STD-DOCUMENT                                 PRTSTD /
      *////////////////////////////////////////////////////////////
     C     PRTSTD        BEGSR
      *____________
      * GET STD NO.
      *""""""""""""
     C     ULN           IFEQ      *BLANKS
     C                   TIME                    XTIME
     C     ULK           CHAIN     E154F                              33
     C   33              Z-ADD     1             E154FC
     C                   MOVE      E154FC        XSTDN
     C                   ADD       1             E154FC
     C  N33              UPDATE    E154FR
     C   33              WRITE     E154FR
     C                   ELSE
     C                   MOVE      UDT           XDT
     C                   MOVE      ULN           XSTDN
     C                   END
      *
     C                   SELECT
     C     ULK           WHENEQ    'SE'
     C                   MOVEL     'RXS'         XSTDN
     C     ULK           WHENEQ    'DK'
     C                   MOVEL     'RXD'         XSTDN
     C     ULK           WHENEQ    'FI'
     C                   MOVEL     'RXF'         XSTDN
     C                   ENDSL
      *
      *___________________
      * PRINT STD-DOCUMENT
      *"""""""""""""""""""
     C     E153FK        SETLL     E153F
     C                   MOVEA     '00'          *IN(50)
     C                   READ      E153F                                  51
     C     *IN51         DOWEQ     '0'
      *
     C     *IN50         IFEQ      '0'
      *_______________________________________
      * GET EXPORTER/IMPORTER/DELIVERY ADDRESS
      *"""""""""""""""""""""""""""""""""""""""
     C                   EXSR      NAD02
      *
     C                   MOVE      '1'           *IN50
     C                   MOVE      '0'           *IN51
     C                   ADD       1             XSIDE
     C                   WRITE     E154PA
     C                   WRITE     E154PB
     C                   ENDIF
      *________________________
      * PRINT PRODUCT SUB TOTAL
      *""""""""""""""""""""""""
     C     EWGCD         IFNE      XGCD
     C   27              ADD       1             XSIDE
     C   27              WRITE     E154PB
     C   27              MOVE      '0'           *IN27
     C                   WRITE     E154PE
     C                   MOVE      *ZEROS        XVALG
     C                   MOVE      EWGCD         XGCD              5
     C                   END
      *_______________________
      * PRINT ORIGIN SUB TOTAL
      *"""""""""""""""""""""""
     C     EWCCD         IFNE      XCCD
     C   27              ADD       1             XSIDE
     C   27              WRITE     E154PB
     C   27              MOVE      '0'           *IN27
     C                   WRITE     E154PF
     C                   MOVE      *ZEROS        XVALC
     C                   MOVE      EWCCD         XCCD              2
     C                   END
      *_____________
      * PRINT HEADER
      *"""""""""""""
     C   27              ADD       1             XSIDE
     C   27              WRITE     E154PB
     C   27              MOVE      '0'           *IN27
      *______________
      * PRINT DETAILS
      *""""""""""""""
     C                   ADD       1             XLN
     C                   WRITE     E154PD
     C                   ADD       EWVAL         XVALC
     C                   ADD       EWVAL         XVALG
     C     EWVAL         DIV(H)    EWCOL         XVALUW            9 2
     C                   ADD       XVALUW        XVALUP
     C                   ADD       EWVAL         XVALT
      *____________________
      * UPDATE MESSAGE FILE
      *""""""""""""""""""""
     C     ULN           IFNE      *BLANKS
     C     EWMN          CHAIN     EDIM                               33
     C     *IN33         IFEQ      '0'
     C                   MOVE      'C'           MST
     C                   MOVE      XDT           MDT
     C                   MOVE      XTM           MTM
     C                   MOVEL     XSTDN         M1004
     C                   UPDATE    EDIMR
     C                   END
     C                   END
      *
     C                   READ      E153F                                  51
     C  N51              ENDDO
      *
      *________________________
      * PRINT PRODUCT SUB TOTAL
      *""""""""""""""""""""""""
     C   27              ADD       1             XSIDE
     C   27              WRITE     E154PB
     C   27              MOVE      '0'           *IN27
     C                   WRITE     E154PF
      *_______________________
      * PRINT ORIGIN SUB TOTAL
      *"""""""""""""""""""""""
     C   27              ADD       1             XSIDE
     C   27              WRITE     E154PB
     C   27              MOVE      '0'           *IN27
     C                   WRITE     E154PF
      *____________
      * PRINT TOTAL
      *""""""""""""
     C   27              ADD       1             XSIDE
     C   27              WRITE     E154PB
     C   27              MOVE      '0'           *IN27
     C                   WRITE     E154PG
      *_________________
      * PRINT ADVICE NO.
      *"""""""""""""""""
     C   27              ADD       1             XSIDE
     C   27              WRITE     E154PB
     C   27              MOVE      '0'           *IN27
     C                   WRITE     E154PH
     C                   MOVE      *ZEROS        EWONO
      *
     C     EWONO         SETLL     E153FL01
     C                   MOVE      '0'           *IN51
     C                   READ      E153FL01                               51
     C     *IN51         DOWEQ     '0'
      *
     C     XN            IFLT      8
     C                   ADD       1             XN                1 0
     C                   MOVE      EWONO         XT(XN)
     C                   ELSE
     C   27              ADD       1             XSIDE
     C   27              WRITE     E154PB
     C   27              MOVE      '0'           *IN27
     C                   WRITE     E154PI
     C                   MOVEA     *ZEROS        XT
     C                   Z-ADD     1             XN
     C                   MOVE      EWONO         XT(XN)
     C                   END
      *
     C                   READ      E153FL01                               51
     C  N51              ENDDO
      *
     C   27              ADD       1             XSIDE
     C   27              WRITE     E154PB
     C   27              MOVE      '0'           *IN27
     C                   WRITE     E154PI
      *_______________
      * PRINT MESSAGES
      *"""""""""""""""
     C   27              ADD       1             XSIDE
     C   27              WRITE     E154PB
     C   27              MOVE      '0'           *IN27
     C                   WRITE     E154PJ
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  NAD02                                              NAD02 /
      *////////////////////////////////////////////////////////////
     C     NAD02         BEGSR
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     2             ZGN0
     C     KEYMS0        SETLL     ENAD93A                                33
     C     *IN33         IFEQ      '1'
     C     KEYMS0        READE     ENAD93A                                51
     C     *IN51         DOWEQ     '0'
      *
     C                   SELECT
     C     N3035         WHENEQ    'CN'
     C                   MOVEL     N3124A        XNAE
     C                   MOVEL     N3124B        XKNE
     C                   MOVEL     N3042A        XADE1
     C                   MOVEL     N3042B        XADE2
     C                   MOVEL     N3042C        XADE3
     C                   MOVEL     N3164         XADE4
     C                   MOVEL     N3229         XADE5
     C                   MOVE      N3251         XADE5
     C     N3035         WHENEQ    'DP'
     C     XNAD          ANDEQ     *BLANKS
     C                   MOVEL     N3124A        XNAD
     C                   MOVEL     N3042A        XADD1
     C                   MOVEL     N3042B        XADD2
     C                   MOVEL     N3164         XADD3
     C                   MOVEL     N3229         XADD4
     C                   MOVE      N3251         XADD4
     C     N3035         WHENEQ    'ZZ'
     C                   MOVEL     N3124A        XNAD
     C                   MOVEL     N3042A        XADD1
     C                   MOVEL     N3042B        XADD2
     C                   MOVEL     N3164         XADD3
     C                   MOVEL     N3229         XADD4
     C                   MOVE      N3251         XADD4
     C                   ENDSL
      *
     C     KEYMS0        READE     ENAD93A                                51
     C  N51              ENDDO
     C                   ENDIF
      *
     C                   ENDSR
