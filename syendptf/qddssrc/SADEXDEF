      *__________________________________
      * EKSP.MANIFEST (MASTERCONSIGNMENT)
      *""""""""""""""""""""""""""""""""""
     A                                      REF(SR)
     A          R SADEXDEFR
     A            EMST      R               REFFLD(ST)
     A                                      COLHDG('Status')
      * ' ' --> NY REGISTRERT MANIFEST
      * 'B' --> MANIFEST ER LAGT I UTGÅENDE MAPPE
      * 'C' --> MANIFEST ER SENDT TIL TOLLVESENET
      * 'P' --> MANIFEST ER AVSLUTTET
      * 'X' --> MANIFEST ER UNDERARBEID AV EN BRUKER
     A            EMUUID        36A         COLHDG('LRN')
     A            EMMID         18A         COLHDG('MRN')
      *
     A            EMAVD     R               REFFLD(AVD)
     A                                      COLHDG('Avdeling')
     A            EMPRO     R               REFFLD(PRO)
     A                                      COLHDG('Turnummer')
     A            EMDTR     R               REFFLD(DT)
     A                                      COLHDG('Registreringsdato')
     A            EMSG      R               REFFLD(SG)
      *
     A            EMST2     R               REFFLD(ST)
     A                                      COLHDG('Status om manifest')
      * 'U' --> UTKAST
      * 'I' --> INNSENDT
      * 'P' --> PASSERT
      *
      * ----- Passeringsinfo (customsOfficeOfFirstEntry) ------
     A            EMTSD          8A         COLHDG('Passeringstollsted')
     A            EMST3     R               REFFLD(ST)
     A                                      COLHDG('Status om innpass.')
      * 'O' --> Tillatt
      * 'E' --> Ikke tillatt
     A            EMDTIN    R               REFFLD(DT)
     A                                      COLHDG('Innsendingsdato')
     A            EMATDD    R               REFFLD(DT)
     A                                      COLHDG('Akt. avgang ATD')
     A            EMATDT    R               REFFLD(TM)
     A                                      COLHDG('ATD tid')
     A            EMETDD    R               REFFLD(DT)
     A                                      COLHDG('Estimert avgang ETD')
     A            EMETDT    R               REFFLD(TM)
     A                                      COLHDG('ETD tid')
     A            EMETAD    R               REFFLD(DT)
     A                                      COLHDG('Estimert ank. ETA')
     A            EMETAT    R               REFFLD(TM)
     A                                      COLHDG('ETA tid')
     A            EMATAD    R               REFFLD(DT)
     A                                      COLHDG('Akt. ankomst ATA')
     A            EMATAT    R               REFFLD(TM)
     A                                      COLHDG('ATA tid')
     A*           EM3039E        6S 0       COLHDG('Ekspedisjonsenhet')
     A*           EMEID         18A         COLHDG('EksportId')
      *
      *
      * ----- Manifest info ------
      * Deklarant
     A            EMKND          8S 0       COLHDG('Deklarant')
     A            EMRGD         17A         COLHDG('Org.nr deklarant')
     A            EMNAD         30A         COLHDG('Navn deklarant')
     A            EMNA2D        30A         COLHDG('SubDivvision dekla.')
     A            EMAD1D        30A         COLHDG('Gateadr. dekla.')
     A*           EMAD2D        30A         COLHDG('Ekstra ADR1 dekla.')
     A            EMNRD         15A         COLHDG('Husnr dekla.')
     A            EMPND          9A         COLHDG('Postnr deklarant')
     A            EMPSD         24A         COLHDG('P.sted deklarant')
     A            EMLKD          2A         COLHDG('L.kode deklarant')
     A            EMPBD         15A         COLHDG('Postbox dekla.')
     A            EMEMD         50A         COLHDG('Epostadr dekla.')
     A            EMEMDT         2A         COLHDG('Kodetype decla.')
      * EM = Epostadr. TE = Telefon
      * Representative
     A            EMKNR          8S 0       COLHDG('Representative')
     A            EMRGR         17A         COLHDG('Org.nr representativ')
     A            EMSTR          1A         COLHDG('Status representativ')
      * 2 = Direkte, 3 = Indirekte
     A            EMNAR         30A         COLHDG('Navn representative')
     A            EMNA2R        30A         COLHDG('SubDivvision repre.')
     A            EMAD1R        30A         COLHDG('Gateadr. repre.')
     A*           EMAD2R        30A         COLHDG('Ekstra ADR1 repre.')
     A            EMNRR         15A         COLHDG('Husnr repre.')
     A            EMPNR          9A         COLHDG('Postnr representativ')
     A            EMPSR         24A         COLHDG('P.sted representativ')
     A            EMLKR          2A         COLHDG('L.kode representativ')
     A            EMPBR         15A         COLHDG('Postbox repre.')
     A            EMEMR         50A         COLHDG('Epostadr/tlf repre.')
     A            EMEMRT         2A         COLHDG('Kodetype repre.')
      * EM = Epostadr. TE = Telefon
      * ActiveBorderTransportMeans
     A            EMKMRK        30A         COLHDG('Kjennemerke')
     A            EMKTYP         2A         COLHDG('Kjøretøy type')
     A            EMKTYPT       50A         COLHDG('Kjøretøy type tekst')
     A            EMKTM          4A         COLHDG('Transportmåte')
     A            EMKTMT        50A         COLHDG('Transportmåte tekst')
     A            EMKLK          2A         COLHDG('Kjøretøynasjonalitet')
     A            EMKTKD         1A         COLHDG('Mode av transportKD')
     A            EMLKR1         2A         COLHDG('Landkode1 reiserute')
     A            EMLKR2         2A         COLHDG('Landkode2 reiserute')
     A            EMLKR3         2A         COLHDG('Landkode3 reiserute')
     A            EMLKR4         2A         COLHDG('Landkode4 reiserute')
     A            EMLKR5         2A         COLHDG('Landkode5 reiserute')
     A            EMLKR6         2A         COLHDG('Landkode6 reiserute')
     A            EMLKR7         2A         COLHDG('Landkode7 reiserute')
     A            EMLKR8         2A         COLHDG('Landkode8 reiserute')
      * Kjøretøy passiv
     A            EMPMRK        30A         COLHDG('Kjennemerke')
     A            EMPTYP         2A         COLHDG('Kjøretøy type')
     A            EMPTM          4A         COLHDG('Transportmåte')
     A            EMPTMT        50A         COLHDG('Transportmåte tekst')
     A            EMPLK          2A         COLHDG('Kjøretøynasjonalitet')
      * Sjåfør
     A            EMSJAF        50A         COLHDG('Sjåfør navn')
     A            EMSJALK        2A         COLHDG('Sjåfør nasjonalitet')
     A            EMSJADT        8S 0       COLHDG('Sjåfør fødselsdato')
      * Sjåfør 2
     A            EMSJ2F        50A         COLHDG('Sjåfør-2 navn')
     A            EMSJ2LK        2A         COLHDG('Sjåfør-2 nasjonalit.')
     A            EMSJ2DT        8S 0       COLHDG('Sjåfør-2 fødselsdato')
      *
      * consignmentMasterLevel
      *
      * Container
     A            EMCN           1S 0       COLHDG('Container 1/0')
     A            EMCNR         11A         COLHDG('Containernr')
      * Bruttovekt
     A            EMVKB          9S 0       COLHDG('Bruttovekt')
      * transportør
     A            EMKNT          8S 0       COLHDG('Transportør')
     A            EMRGT         17A         COLHDG('Org.nr transportør')
     A            EMNAT         30A         COLHDG('Navn transportør')
     A            EMNA2T        30A         COLHDG('SubDivvision trans.')
     A            EMAD1T        30A         COLHDG('Gateadr. trans.')
     A*           EMAD2T        30A         COLHDG('Ekstra ADR1 trans.')
     A            EMNRT         15A         COLHDG('Husnr trans.')
     A            EMPNT          9A         COLHDG('Postnr transportør')
     A            EMPST         24A         COLHDG('P.sted transortør')
     A            EMLKT          2A         COLHDG('L.kode transportør')
     A            EMPBT         15A         COLHDG('Postbox trans.')
     A            EMEMT         50A         COLHDG('Epostadr/tlf trans.')
     A            EMEMTT         2A         COLHDG('Kodetype trans.')
      * EM = Epostadr. TE = Telefon
      * transportDocumentMasterLevel
     A            EMDKM         50A         COLHDG('Master Dokumentnr')
     A            EMDKMT         4A         COLHDG('Master Dokumenttype')
      * referenceNummerUCR
     A            EMUCR         50A         COLHDG('Ref.nr UCR')
      * mottaker
     A            EMKNM          8S 0       COLHDG('Mottaker')
     A            EMRGM         17A         COLHDG('Org.nr mottaker')
     A            EMNAM         30A         COLHDG('Navn mottaker')
     A            EMNA2M        30A         COLHDG('SubDivvision mottak.')
     A            EMAD1M        30A         COLHDG('Gateadr. mottaker')
     A*           EMAD2M        30A         COLHDG('Ekstra ADR1 mottaker')
     A            EMNRM         15A         COLHDG('Husnr mottaker')
     A            EMPNM          9A         COLHDG('Postnr mottaker')
     A            EMPSM         24A         COLHDG('P.sted mottaker')
     A            EMLKM          2A         COLHDG('L.kode mottaker')
     A            EMPBM         15A         COLHDG('Postbox mottaker')
     A            EMEMM         50A         COLHDG('Ep.adr/tlf mottaker')
     A            EMEMMT         2A         COLHDG('Kodetype mottaker')
      * EM = Epostadr. TE = Telefon
      * avsender
     A            EMKNS          8S 0       COLHDG('Avsender')
     A            EMRGS         17A         COLHDG('Org.nr avsender')
     A            EMNAS         30A         COLHDG('Navn avsender')
     A            EMNA2S        30A         COLHDG('SubDivvision avsend.')
     A            EMAD1S        30A         COLHDG('Gateadr. avsender')
     A*           EMAD2S        30A         COLHDG('Ekstra ADR1 avsender')
     A            EMNRS         15A         COLHDG('Husnr avsender')
     A            EMPNS          9A         COLHDG('Postnr avsender')
     A            EMPSS         24A         COLHDG('P.sted avsender')
     A            EMLKS          2A         COLHDG('L.kode avsender')
     A            EMPBS         15A         COLHDG('Postbox avsender')
     A            EMEMS         50A         COLHDG('Epostadr avsender')
     A            EMEMST         2A         COLHDG('Kodetype mottaker')
      * EM = Epostadr. TE = Telefon
      * places
     A            EMLKL          2A         COLHDG('Land of loading')
     A            EMSDL          5A         COLHDG('Place of loading')
     A            EMSDLT        30A         COLHDG('Place of loading')
     A            EMLKU          2A         COLHDG('Land of unloading')
     A            EMSDU          5A         COLHDG('Place of unloading')
     A            EMSDUT        30A         COLHDG('Place of unloading')
      * additionalSupplyChainActor
     A            EMSCA         17A         COLHDG('Org.nr SupplyChainAc')
     A            EMSCAR         2A         COLHDG('Role SupplyChainActo')
      * releasedConfirmation
     A            EMRCEM1       50A         COLHDG('ReleasedConf. epost.')
     A            EMRCEM2       50A         COLHDG('ReleasedConf. epost.')
     A            EMRCEM3       50A         COLHDG('ReleasedConf. epost.')
      *
     A            EMERR         50A         COLHDG('Feilmelding ved SND')
      *
     A          K EMAVD
