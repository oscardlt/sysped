********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
12XXX * 01 YBC PTF12 Sett garantibeløp tilbake ved godkjening av kansellering.
********************************************************************
      *****************************************************************
      *** PROGRAM SKAL KONVERTERE INNGÅENDE IE09                    ***
      *** Invalidation Decision                                     ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER          ***
      *** LEDIGE INDIKATORER 01-26                                  ***
      *** LEDIGE INDIKATORER 28-32 34-97                            ***
      *****************************************************************
     H DFTACTGRP(*NO)
     FXMLRCVF   IF   E           K DISK
     FXMLRCLF   IF   E           K DISK
     FTRACKF    O  A E           K DISK
     FNCTSEH    UF   E           K DISK
     FNCTSEHK   UF A E           K DISK
N01  FTRUGH     UF   E           K DISK
     FTVINF     O  A E             DISK
     FEDIC      UF   E             DISK
     FEDIM      O  A E             DISK
     FEDIM5     IF   E           K DISK    RENAME(EDIMR:EDIMR5)
     D                sds
     D ZUSER                 254    263
      *  Prototype for 'SYGE15C'
     d syge15c         pr                  extpgm('SYGE15C')
     d wdt_                                like(wdt)
     d wtm_                                like(wtm)
      *  Prototype for ncts104r2
     d ncts104r2       pr
     d usn_                                like(usn)
      *  *ENTRY Interface for Main Procedure
     d ncts104r2       pi
     d usn                            9
      *----------------------------------------------------------------------
      * Stand Alone Fields - TOP
      *----------------------------------------------------------------------
     d x1_xrlev1       s                   like(xrlev1)
     d x1_xrlev2       s                   like(xrlev2)
     d x2_xrlev1       s                   like(xrlev1)
     d x2_xrlev2       s                   like(xrlev2)
     d x2_xrlev3       s                   like(xrlev3)
     d x3_xrlev1       s                   like(xrlev1)
     d x3_xrlev2       s                   like(xrlev2)
     d x3_xrlev3       s                   like(xrlev3)
     d x3_xrlev4       s                   like(xrlev4)
     d xn              s              3s 0
     d x_thtrdt        s              8
     d x_thtrnr        s                   like(thtrnr)
     d x_IE09          s              1a
     d x_ref           s              8a
     d x_des           s              1a
     d x_init          s              1a
     d x_xrvrd         s                   like(xrvrd)
     d wdt             s              8
     d wtm             s              6
     d                 ds
     d x_rdt                   1      8  0
     d x_rdta                  1      8
     d                 ds
     d x_rtm                   1      6  0
     d x_rtma                  1      6
     d                 ds
     d x_ddt                   1      8  0
     d x_ddta                  1      8
     d                 ds
     d x_dtm                   1      6  0
     d x_dtma                  1      6
     d                 ds
     d f0078a                  1     70a
     d f0078b                 71    140a
     d f0078c                141    210a
     d f0078d                211    280a
     d f0078e                281    350a
     d ft                      1    350a   dim(5)
      *
     D UP              C                   CONST('ABCDEFGHIJKLMNOPQRST-
     D                                     UVWXYZÆØÅ')
     D LO              C                   CONST('abcdefghijklmnopqrst-
     D                                     uvwxyzæøå')
      *----------------------------------------------------------------------
     IEDIMR5
     I              M0004                       X0004
     I              M0010                       X0010
     I              M0035                       X0035
     I              M0062                       X0062
     I              M0065                       X0065
     I              M0068                       X0068
     I              M0068A                      X0068A
     I              M0068B                      X0068B
     I              M0068C                      X0068C
     I              M0068D                      X0068D
     I              M0068E                      X0068E
     I              M0068F                      X0068F
     I              M1001                       X1001
     I              M1004                       X1004
     I              M1225                       X1225
     I              M2005B                      X2005B
     I              M3039D                      X3039D
     I              M3039E                      X3039E
     I              M5004D                      X5004D
     I              M1N07                       X1N07
     I              M1N08                       X1N08
     I              M9N01                       X9N01
     I              MSN                         XSN
     I              MMN                         XMN
     I              MSR                         XSR
     I              MST                         XST
     I              MDT                         XDT
     I              MTM                         XTM
     I              MVEN                        XVEN
     I              MAVD                        XAVD
     I              MTDN                        XTDN
      /free

       exsr init;

       exsr srxml;

       *inlr = *on;
       return;

       //**********************************************
       begsr srxml;
       //**********************************************

         x1_xrlev1 = 0;
         x1_xrlev2 = 0;

         chain (xrsn:x1_xrlev1:x1_xrlev2) xmlrcvf;
         dow %found;
           select;
           when xrnv = 'messageSender';
             m0004  = xrverd;
           when xrnv = 'messageRecipient';
             m0010  = xrverd;
           when xrnv = 'preparationDateAndTime';
           when xrnv = 'messageIdentification';
             m0062  = xrverd;
             m1004  = xrverd;
           when xrnv = 'messageType' and xrverd = 'CC009C';
             x_ie09 = 'J';
             chain 1 edic;
             cmn = cmn + 1;
             update edicr;
             fmn = cmn;
             mmn = cmn;
           when xrnv = 'correlationIdentifier';
           when xrnv = 'TransitOperation';
             exsr srxml1;
           when xrnv = 'Invalidation';
             exsr srxml2;
           when xrnv = 'CustomsOfficeOfDeparture';
             exsr srxml3;
           when xrnv = 'HolderOfTheTransitProcedure';
           //exsr srxml4;
           endsl;

           x1_xrlev1 = x1_xrlev1 + 1;
           chain (xrsn:x1_xrlev1:x1_xrlev2) xmlrcvf;
         enddo;

         if x_ie09 = 'J';
           exsr opdopd;
           exsr ttsr;
         endif;

         endsr;

       //**********************************************
       begsr srxml1;
       //**********************************************

         ft    = *blanks;
         fln   = fln + 1;
         f0077 = 'INF';
         xn    = 0;

         x2_xrlev1 = xrlev1;
         x2_xrlev2 = 1;
         x2_xrlev3 = 0;

         chain (xrsn:x2_xrlev1:x2_xrlev2:x2_xrlev3) xmlrcvf;
         dow %found;
           select;
           when xrnv = 'LRN';
             x0068 = xrverd;
           when xrnv = 'MRN';
             x_thtrnr = xrverd;
           endsl;

           x2_xrlev2 = x2_xrlev2 + 1;
           chain (xrsn:x2_xrlev1:x2_xrlev2:x2_xrlev3) xmlrcvf;
         enddo;

       endsr;

       //**********************************************
       begsr srxml2;
       //**********************************************

         x2_xrlev1 = xrlev1;
         x2_xrlev2 = 1;
         x2_xrlev3 = 0;

         chain (xrsn:x2_xrlev1:x2_xrlev2:x2_xrlev3) xmlrcvf;
         dow %found;
           select;
           when xrnv = 'requestDateAndTime';
             x_rdta = %subst(xrverd:1:4) + %subst(xrverd:6:2)
                   + %subst(xrverd:9:2);
             x_rtma = %subst(xrverd:12:2) + %subst(xrverd:15:2)
                   + %subst(xrverd:18:2);
             xn     = xn + 1;
             ft(xn) = 'Request date and time:' + %trim(xrverd);
           when xrnv = 'decisionDateAndTime';
             x_ddta = %subst(xrverd:1:4) + %subst(xrverd:6:2)
                   + %subst(xrverd:9:2);
             x_dtma = %subst(xrverd:12:2) + %subst(xrverd:15:2)
                   + %subst(xrverd:18:2);
             xn     = xn + 1;
             ft(xn) = 'Decition date and time:' + %trim(xrverd);
           when xrnv = 'decision';
             x_des = xrverd;
             xn     = xn + 1;
             if x_des = '0';
               ft(xn) = 'Kansellering avslått av tollvesenet.';
             else;
               ft(xn) = 'Kansellering akseptert av tollvesenet.';
             endif;
             write tvinff;
           when xrnv = 'initiatedByCustoms';
             x_init = xrverd;
           when xrnv = 'justification';
             ft     = *blanks;
             fln    = fln + 1;
             if xrverd = '*EXT, TILLEGGSFIL';
               chain (xrsn:x2_xrlev1:x2_xrlev2:x2_xrlev3:xrlev4) xmlrclf;
             else;
               xrvrd = xrverd;
             endif;
             ft(1)  = %subst(xrvrd:1:70);
             ft(2)  = %subst(xrvrd:71:70);
             ft(3)  = %subst(xrvrd:141:70);
             ft(4)  = %subst(xrvrd:211:70);
             ft(5)  = %subst(xrvrd:281:70);
             write tvinff;
             x_xrvrd = xrvrd;
           endsl;

           x2_xrlev2 = x2_xrlev2 + 1;
           chain (xrsn:x2_xrlev1:x2_xrlev2:x2_xrlev3) xmlrcvf;
         enddo;

       endsr;

       //**********************************************
       begsr srxml3;
       //**********************************************

         x2_xrlev1 = xrlev1;
         x2_xrlev2 = 1;
         x2_xrlev3 = 0;

         chain (xrsn:x2_xrlev1:x2_xrlev2:x2_xrlev3) xmlrcvf;
         dow %found;
           select;
           when xrnv = 'referenceNumber';
             x_ref = xrverd;
             ft     = *blanks;
             xn     = 0;
             fln    = fln + 1;
             f0077  = *blanks;
             ft(1)  = 'Customs office of departure: ' + xrverd;
             write tvinff;
           endsl;

           x2_xrlev2 = x2_xrlev2 + 1;
           chain (xrsn:x2_xrlev1:x2_xrlev2:x2_xrlev3) xmlrcvf;
         enddo;

       endsr;

       //**********************************************
       begsr srxml4;
       //**********************************************

         x2_xrlev1 = xrlev1;
         x2_xrlev2 = 1;
         x2_xrlev3 = 0;

         chain (xrsn:x2_xrlev1:x2_xrlev2:x2_xrlev3) xmlrcvf;
         dow %found;
           select;
           when xrnv = 'identificationNumber';
           when xrnv = 'TIRHolderIdentificationNumber';
           when xrnv = 'name';
           when xrnv = 'Address';
           when xrnv = 'ContactPerson';
           endsl;

           x2_xrlev2 = x2_xrlev2 + 1;
           chain (xrsn:x2_xrlev1:x2_xrlev2:x2_xrlev3) xmlrcvf;
         enddo;

       endsr;

       //**********************************************
       begsr opdopd;
       //**********************************************

         chain x0068 edim5;
         if %found;
           chain (xavd: xtdn) nctseh;
           if %found;
             select;
             when x_des = '1';
               thst = 'D';            // Aksept
N01            if thgbl > 0;
N01              chain (THGFT1) trugh;
N01              if %found;
N01                tggblb = tggblb - thgbl;
N01                update trughr;
N01                thgbl = thgbl * -1;
N01              endif;
N01            endif;
             when thtrnr = *blanks;
               thst = 'C';            // Ikke aksept, kansellering sendte før IE28 mottatt
             other;
               thst = 'U';            // Ikke aksept, kansellering sendte etter IE28 mottatt
             endsl;
             thst009 = 'J';
             update nctsehr;
           endif;
           chain (xavd: xtdn) nctsehk;
           if %found;
             tkdta = x_ddt;
             select;
             when x_des = '1';
               tkkd1a = 1;
             other;
               tkkd1a = 0;
             endsl;
             select;
             when x_init = '1';
               tkkd2a = 1;
             other;
               tkkd2a = 0;
             endsl;
             tkft1a = %subst(x_xrvrd:1:70);
             tkft2a = %subst(x_xrvrd:71:70);
             tkft3a = %subst(x_xrvrd:141:70);
             tkft4a = %subst(x_xrvrd:211:70);
             tkft5a = %subst(x_xrvrd:281:70);
             update nctsehkr;
           endif;
         endif;

         m0065  = 'XMLRES';
         if x0068 = *blanks;
           m0068  = x_thtrnr;
         else;
           m0068  = x0068;
         endif;
         m1001  = 'TET';
         m1225  = '009';
         msn    = xrsn;
         msr    = 'R';
         mst    = 'C';
         mdt    = %float(wdt);
         mtm    = %float(wtm);
         m0068a = x0068a;
         m0068b = x0068b;
         m1n07  = '009';
         mavd   = thavd;
         mtdn   = thtdn;
         write edimr;

       endsr;

       //**********************************************
       begsr ttsr;
       //**********************************************

         ttavd  = thavd;
         ttopd  = thtdn;
         ttacti = 'T09';
         ttdate = %float(wdt);
         tttime = %float(wtm);
         ttuser = zuser;
         if x_des = '1';
           tttext = 'Cancellation accected by customs: '
                    + %trim(%editw(x_ddt:'    .  .  '))
                    + ' ' + %trim(%editw(x_dtm:'  :  :  '));
           tttexl = 'Kansellering akseptert av tollvesenet: '
                    + %trim(%editw(x_ddt:'    .  .  '))
                    + ' ' + %trim(%editw(x_dtm:'  :  :  '));
         else;
           tttext = 'Cancellation rejected by customs: '
                    + %trim(%editw(x_ddt:'    .  .  '))
                    + ' ' + %trim(%editw(x_dtm:'  :  :  '));
           tttexl = 'Kansellering avvist av tollvesenet: '
                    + %trim(%editw(x_ddt:'    .  .  '))
                    + ' ' + %trim(%editw(x_dtm:'  :  :  '));
         endif;
         write trackfr;

       endsr;

       //**********************************************
       begsr init;
       //**********************************************

         xrsn = %dec(usn:9:0);
         syge15c ( wdt : wtm );

       endsr;

      /end-free
