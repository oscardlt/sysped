********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
12XXX * 01 YBC PTF12 HISTORIKK MAPPE
12XXX * 02 YBC PTF12 Test om vedlegg finnes.
12XXX * 03 YBC google lagring
12XXX * 04 YBC Sjekk om IFS-fil er låst
********************************************************************
      *
N03  FFIRMGOOG  IF   E           K DISK
     FARKIVL02  IF   E           K DISK
     FARKTXT    IF   E           K DISK
     FARKEXT    IF   E           K DISK
     FFIRMARC   IF   E           K DISK
     D KOMMSTR         C                   CONST('CONCATPDF -
     D                                     INPDF(')
     D KOMMEND         C                   CONST(')')
     D PAR1            C                   CONST('INPDF(')
     D PAR2            C                   CONST('OUTPDF(')
     D PAR3            C                   CONST('JDEBUG(')
      * Definere  hex appostrof
     D Ap              c                   x'7D'
     D                SDS
     D  UUSER                254    263
     D                 DS
     D  AVKVED                 1     60
     D  AVK                    1     60    DIM(30)
      *
     C     *ENTRY        PLIST
     C                   PARM                    UFIRM             2
     C                   PARM                    UFN               7
     C                   PARM                    UAVD              4
     C                   PARM                    UOPD              7
     C     AVKVED        PARM                    UARKVED          60
      *
     C     ARKIVL02K     KLIST
     C                   KFLD                    ARSTAT
     C                   KFLD                    ARAVD
     C                   KFLD                    AROPD
     C     ARKLAGK       KLIST
     C                   KFLD                    UFIRM
     C                   KFLD                    ARKLAG
      *
N03  c     UFIRM         CHAIN     FIRMGOOG                           33
N03  c  N33              MOVE      'J'           GOOGLE            1
     C                   MOVE      'A'           ARSTAT
     C                   MOVE      UAVD          ARAVD
     C                   MOVE      UOPD          AROPD
     C                   MOVE      *BLANKS       XLINK            70
     C                   EVAL      XLINK = '/pdf/tmp/EHF' + UFN + '.pdf'
     C                   move      '*NO '        nei               4
     C                   Z-ADD     30000         UCMDL            15 5

     C                   Eval      utstreng='CONCATPDF INPDF('
     C                   MOVE      'fa'          XTYPE             2
     C                   z-add     0             epn               3 0
     C                   EXSR      LESPDF
     C                   MOVE      *BLANKS       XTYPE
     C                   EXSR      LESPDF
      *
      * HVIS FLERE DOKUMENT SLÅ SAMMEN
      *
     C     EPN           IFGT      1
     C                   cat       ') OUTPDF(':0 utstreng      30000
     c                   cat       Ap:0          utstreng
     c                   cat       XLINK:0       utstreng
     c                   cat       Ap:0          utstreng
     C                   cat       ') JDEBUG(':0 utstreng
     c                   cat       Ap:0          utstreng
     C                   cat       NEI:0         utstreng
     c                   cat       Ap:0          utstreng
     C                   cat       ')':0         utstreng
     c                   movel     utstreng      UCMD
     C                   CALL      'QCMDEXC'                            33
     C                   PARM                    UCMD          30000
     C                   PARM                    UCMDL
     C                   END
      *
     C                   EVAL      *INLR = *ON
     C                   RETURN
      *
     C***********************************************************************
     C     LESPDF        BEGSR
     C***********************************************************************
      *
      * skriver linker
      *
     C     ARKIVL02K     SETLL     ARKIVL02
     C     ARKIVL02K     READE     ARKIVL02                               33
     C                   DOW       *IN33 = *OFF                                 1<-B
     C                   SELECT                                                  2<-B
     C                   WHEN      XTYPE = 'fa' AND ARTYPE <> 'fa'               2
     C                   GOTO      LESNESTE
     C                   WHEN      XTYPE <> 'fa' AND ARTYPE = 'fa'               2
     C                   GOTO      LESNESTE
     C                   ENDSL                                                   2<-E
     C                   IF        ARTYPE = 'fa'                                 2<-B
     C                   IF        ARUNDE <> UFN                                  3<-B
     C                   GOTO      LESNESTE
     C                   END                                                      3<-E
     C                   ELSE                                                    2
     C     1             DO        30            XN                3 0            3<-B
     C                   SELECT                                                    4<-B
     C                   WHEN      AVK(XN) = ARTYPE                                4
     C                   LEAVE
     C                   WHEN      AVK(XN) = *BLANKS                               4
     C                   GOTO      LESNESTE
     C                   ENDSL                                                     4<-E
     C                   ENDDO                                                    3<-E
     C                   END                                                     2<-E
      *
     C     '.'           SCAN      ARLINK                                 33
     C                   IF        *IN33                                         2<-B
     C     '.pdf'        SCAN      ARLINK                                 33
     C  N33              GOTO      LESNESTE
     C                   ELSE                                                    2
     C                   EVAL      ARLINK = %TRIM(ARLINK) + '.pdf'
     C                   END                                                     2<-E
      *
     C     ARTYPE        CHAIN     ARKTXT                             33
     C                   IF        ARKLAG <> *BLANKS                             2<-B
     C     ARKLAGK       CHAIN     ARKEXT                             33
     C                   ELSE                                                    2
     C     UFIRM         CHAIN     FIRMARC                            33
     C                   END                                                     2<-E
N03   *
N03  C                   IF        GOOGLE = 'J'
N03  c                   eval      gotmpf100=gotmpf
N03  c                   eval      arlenke=arlink
N03  C     '.'           SCAN      ARLINK                                 33
N03  C  N33              EVAL      ARLENKE = %TRIM(ARLINK) + '.pdf'
N03  c                   eval      newfile=arlenke
N03   * %trim(artype)
N03  c                   call      'ARC99WSD1'
N03  c                   parm                    gobuid           10
N03  c                   parm                    gotmpf100       100
N03  c                   parm                    arlenke          60
N03  c                   parm                    NewFile          60
N03  c                   parm                    uok               1
N03  c                   IF        UOK <> 'J'
N03  c                   GOTO      LESNESTE
N03  C                   END
S04  C*                  Z-ADD     1             USEC              3 0
S04  C*                  CALL      'DLYJBSEC'
S04  C*                  PARM                    USEC
N04   * SJEKK OM FIL ER LÅST. HVIS JA, VENT INNTIL 10 GANGER 1 SEKUND
N04  C                   EVAL      X_IFS = %TRIM(GOTMPF) + %TRIM(ARLENKE)
N04  C     1             DO        10
N04  C                   CALL      'SYGE140CL'
N04  C                   PARM                    X_IFS           256
N04  C                   PARM                    UOK
N04  C                   Z-ADD     1             USEC              3 0
N04  C                   CALL      'DLYJBSEC'
N04  C                   PARM                    USEC
N04  C                   IF        UOK = '0'
N04  C                   LEAVE
N04  C                   END
N04  C                   ENDDO
N04   *
N03  C                   ELSE
N02   *
N02   * Test om dokument finnes.
N02  C                   SELECT
N02  C                   WHEN      ARBHIS <> *BLANKS
N02  C                   EVAL      UBANET = '/' + %TRIM(ARBHIS)
N02  C                             + '/' + %TRIM(ARLINK)
N02  C                   WHEN      ARCANE <> *BLANKS
N02  C                   EVAL      UBANET = '/' + %TRIM(ARCANE)
N02  C                             + '/' + %TRIM(ARLINK)
N02  C                   OTHER
N02  C                   EVAL      UBANET = '/'
N02  C                   ENDSL
N02  C                   EVAL      NYOBJ  = ARLINK
N02  C                   call      'SYGE98CL'
N02  C                   PARM                    ubanet          256
N02  C                   PARM                    nyobj            50
N02  C                   parm                    fins              1
N02  C                   IF        FINS = 'N'
N02  C                   GOTO      LESNESTE
N02  C                   END
N02  C
N03  C                   END
      *
     C                   ADD       1             epn
     C     epn           ifgt      1                                             2<-B
     C                   cat       Ap:1          utstreng
     C                   else                                                    2
     C                   cat       Ap:0          utstreng
     C                   end                                                     2<-E
N03   *
N03  C                   IF        GOOGLE = 'J'
N03  C                   EVAL      utstreng = %TRIM(utstreng)
N03  C                             + %TRIM(GOTMPF) + %TRIM(NEWFILE)
N03  C                             + AP + ' '
N03  C                   ELSE
N03   *
N01  C                   IF        ARBHIS <> *BLANKS                             2<-B
N01  C                   EVAL      ARCANE = ARBHIS
N01  C                   END                                                     2<-E
     C                   IF        %SUBST(ARCANE:1:1) <> '/'                     2<-B
     C                   cat       '/':0         utstreng
     C                   END                                                     2<-E
     C                   cat       ARCANE:0      utstreng
     C                   cat       '/':0         utstreng
     C                   cat       ARLINK:0      utstreng
     C                   cat       Ap:0          utstreng
     C                   cat       ' ':0         utstreng
N03   *
N03  C                   END
     C     epn           ifgt      149                                           2<-B
     C                   goto      avbryt
     C                   end                                                     2<-E
      *
     C     LESNESTE      TAG
     C     ARKIVL02K     READE     ARKIVL02                               33
     C                   ENDDO                                                  1<-E
      *
     C     AVBRYT        ENDSR
      *
