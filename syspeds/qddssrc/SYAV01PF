     A*****************************************************************
     A* AMETAUTSKRIFT                                                 *
     A* ANTALL LINJE=72,  ANTALL TEGN PR. LINJE=96,  OVERFLOW=70      *
     A*****************************************************************
     A          R OPDH1
     A
     A* HEADING
     A            HOHT1         80      1  1
     A            HOHT2         80      2  1
     A            HOHT3         80      3  1
     A            HOHT4         80      4  1
     A            HOHT5         80      5  1
     A            HOHT6         80      6  1
     A            HOHT7         80      7  1
     A  08                              7 75'** PRØVEUTSKRIFT **'
     A            LNAVN     R          11 11REFFLD(KNAVN CUNDF)
     A            ADR1      R          12 11REFFLD(ADR1 CUNDF)
     A            ADR2      R          13 11REFFLD(ADR2 CUNDF)
     A            POSTNR    R          14 11REFFLD(POSTNR CUNDF)
     A            ADR3      R          14 17REFFLD(ADR3 CUNDF)
     A                                 17  2'AVREGNING NR:'
     A            HTAVRN         5S 0  17 20EDTCDE(Z)
     A                                 18  2'Transportør :'
     A            TUKNT          8S 0  18 20EDTCDE(Z)
     A                                 19  2'Turnr:'
     A            H1PRO          8S 0  19 20EDTCDE(Z)
     A                                 20  2'Dato:'
     A            H1DATO         6S 0  20 20EDTCDE(Y)
     A                                 21  2'Bilnr:'
     A            H1BILN        10A    21 20
     A                                 17 65'Side:'
     A            SIDET          2S 0  17 77EDTCDE(Z)
     A                                 18 65'Dato/sign:'
     A            UDAY           2S 0  18 77
     A                                 18 79'.'
     A            UMONTH         2S 0  18 80
     A                                 18 82'.'
     A            UYEAR          2S 0  18 83
     A                                 18 85'/'
     A            BRUKER        10A    18 86
     A*DETALJ HEADINGER
     A          R OPDH2                     SPACEB(2)
     A                                     2'Pos.'
     A                                    10'Sendingsnr.'
     A                                    21'Ref.'
     A                                    34'Mott.'
     A                                    45'Lev.adr/sted'
     A                                    59'Hpl/Krt'
     A                                    69'Antall'
     A                                    77'Kg/Fr.vekt'
     A                                    89'Beløp'
     A          R OPDDET                    SPACEB(1)
     A            AVPOS          4S        2EDTCDE(Z)
     A            AV1004        10A       10
     A            AVAVD          4S 0     21EDTCDE(Z)
     A                                    25'/'
     A            AVOPD          7S 0     26EDTCDE(Z)
     A            AVNAK         10A       34
     A            AVADK3        15A       45
     A            AVVS1          3A       62
     A            AVANT          5S 0     67EDTCDE(Z)
     A            AVVKT          5S 0     75EDTCDE(Z)
     A                                    80'/'
     A            AVFBV          5S 0     81EDTCDE(Z)
     A            AVBELN         7S 2     88EDTCDE(M)
     A* SUM
     A          R OPDSUM                    SPACEB(2)
     A                                    43'Sum beløp'
     A            AMBELN        10S 2     85EDTCDE(M)
