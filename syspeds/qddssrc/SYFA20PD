     A                                      REF(HEADF)
      *_________________________________
      * Format for markering av ny side.
      *"""""""""""""""""""""""""""""""""
     A          R SKJØP1                    SPACEB(2)
     A  70                                65'** Continues **'
     A N70N79                             65MSGCON(15 DIV1673 SYMSGF)
     A  79                                65'* Fortsetzung *'
     A          R SKJØPD
     A            KFMT1         75      1  1
     A            KFMT2         75      1  1
     A  46        HP1           80      1  1
     A  46        HP2           80      2  1
     A  46        HP3           80      3  1
     A  46        HP4           80      4  1
     A  46        HP5           80      5  1
     A  46        HP6           80      6  1
     A  46        HP7           80      7  1
     A N48        KNAVN     R          11 11REFFLD(KNAVN CUNDF)
     A  48        XDINV         30     11 11
     A N88N51                          11 48'******************************'
     A N88N51                          11 48'******************************'
     A N88
     AA 51N70N79                       11 48MSGCON(30 DIV1674 SYMSGF)
     A N88
     AA 51N70N79                       11 48MSGCON(30 DIV1674 SYMSGF)
     A N88 51 70                       11 48'*** COPY  OF   ***************'
     A N88 51 70                       11 48'*** COPY  OF   ***************'
     A N88 51 79                       11 48'*** KOPI  VON  ***************'
     A N88 51 79                       11 48'*** KOPI  VON  ***************'
     A  70 88                          11 48'********  P A G E '
     A  70 88                          11 48'********  P A G E '
     A N70N79 88                       11 48MSGCON(24 DIV1676 SYMSGF)
     A N70N79 88                       11 48MSGCON(24 DIV1676 SYMSGF)
     A  79 88                          11 48'********  S E I T E'
     A  79 88                          11 48'********  S E I T E'
     A  88        XSIDE          2  0  11 71EDTCDE(Z)
     A  88                             11 75'***'
     A  88                             11 75'***'
     A N48        ADR1      R          12 11REFFLD(ADR1 CUNDF)
     A  48        XDIAD1        30     12 11
     A N73                             12 48'*'
     A N73                             12 48'*'
     A                                 12 77'*'
     A                                 12 77'*'
     A N70N73N79  KO2NTX    R          12 54REFFLD(KO2NTX KODTOT2)
     A  70N73     KO2ETX    R          12 54REFFLD(KO2ETX KODTOT2)
     A N70 73N79                       12 48MSGCON(24 DIV1677 SYMSGF)
     A N70 73N79                       12 48MSGCON(24 DIV1677 SYMSGF)
     A  70 73                          12 48'*   C R E D I T N O T E '
     A  70 73                          12 48'*   C R E D I T N O T E '
     A  73 79                          12 48'*   G U T S C H R I F T '
     A  73 79                          12 48'*   G U T S C H R I F T '
     A N73 79                          12 48'*   R E C H N U N G     '
     A N73 79                          12 48'*   R E C H N U N G     '
     A N48        ADR2      R          13 11REFFLD(ADR2 CUNDF)
     A  48        XDIAD2        30     13 11
     A N57                             13 48'******************************'
     A N57                             13 48'******************************'
     A  57 70                          13 48'*   CREDIT OF INV:'
     A  57 70                          13 48'*   CREDIT OF INV:'
     A  57N70N79                       13 48MSGCON(24 DIV1675 SYMSGF)
     A  57N70N79                       13 48MSGCON(24 DIV1675 SYMSGF)
     A  57 79                          13 48'*   GUTSCHRIFT VON:'
     A  57 79                          13 48'*   GUTSCHRIFT VON:'
     A  57        XKREDA         7S 0  13 71EDTCDE(Z)
     A N37N48     POSTNR    R          14 11REFFLD(POSTNR CUNDF)
     A                                      DLTEDT
     A N37N48     ADR3      R          14 17REFFLD(ADR3 CUNDF)
     A N38N48     SYPOGE         9     14 11
     A N38N48     ADR3B     R          14 22REFFLD(ADR3 CUNDF)
     A  37 38N48  ADR3C     R          14 11REFFLD(ADR3 CUNDF)
     A  48        XDIAD3        30     14 11
     A N70N79                          14 48MSGCON(15 DIV1667 SYMSGF)
     A  70                             14 48'Invoice no....:'
     A  79                             14 48'Rechnung nr...:'
     A            XFN            7S 0  14 71EDTCDE(Z)
     A            KLKNVN    R          15 11REFFLD(KLKNVN KODTLK)
     A N70N79                          15 48MSGCON(15 DIV1668 SYMSGF)
     A  70                             15 48'Customer no...:'
     A  79                             15 48'Kunden nr.....:'
     A            KUNDNR    R          15 70REFFLD(KUNDNR CUNDF)
     A                                      EDTCDE(Z)
     A N70N79                          16 48MSGCON(15 DIV1666 SYMSGF)
     A  70                             16 48'Date of invoice'
     A  79                             16 48'Rechn.datum...:'
     A            IDAGX          6S 0  16 70EDTCDE(Y)
     A N70N79                          17 48MSGCON(15 DIV1665 SYMSGF)
     A  70                             17 48'Date due......:'
     A  79                             17 48'Verfallstag...:'
     A N39        XFFDT          6S 0  17 70EDTCDE(Y)
     A          R SHEAD2                    SPACEB(1)
     A N70N79                             17MSGCON(30 DIV1680 SYMSGF)
     A                                      SPACEB(2)
     A N70N79                             69MSGCON(12 DIV1681 SYMSGF)
     A  70                                32'D E S C R I P T I O N'
     A                                      SPACEB(2)
     A  70                                71'AMOUNT'
     A  79                                22'K O S T E N B E R E C H N U N G'
     A                                      SPACEB(2)
     A  79                                71'KOSTEN'
     A                                     1' '
     A                                      SPACEB(1)
     A          R SKJØPFS                   SPACEB(1)
     A            KFSOTX        30         2
     A                                    32':'
     A            FSSOK         35        35SPACEA(1)
     A          R UFB20PD                   SPACEB(1)
     A                                     2MSGCON(24 YBC0491 SYMSGF)
     A                                      HIGHLIGHT
     A                                    32':'
     A                                      HIGHLIGHT
     A            HEFT1A        30        35HIGHLIGHT
     A            HEFT1B        15        66HIGHLIGHT
     A          R SFRIEL                    SPACEB(1)
     A            FRTTXT        70         2
      *_______________________________
      * Format for faktura-varelinjer.
      *"""""""""""""""""""""""""""""""
     A          R SDET                      SPACEB(1)
     A            FAVT      R             17REFFLD(FAVT FAKT)
     A N70N79     KGENOT    R             41REFFLD(KGENOT KODTGE)
     A  70        KGEENT    R             41REFFLD(KGEENT KODTGE)
     A  79        KG2TYT    R             41REFFLD(KG2TYT KODTG2)
     A  36 70     XVAL           3        61
     A  36 79     XVALB          3        61
     A  36N70 79  XVALC          3        61
     A  36        XPFNOK        13  2     61EDTCDE(J)
     A  34 36                             79'*'
      *__________________________________
      * Format for faktura-total. IND: 72
      *""""""""""""""""""""""""""""""""""
     A          R STOTA                     SPACEB(2)
     A N70N79                              2MSGCON(16 DIV1682 SYMSGF)
     A  70                                 2'VAT of (* lines)'
     A  79                                 2'Tax von(* linen)'
     A            XMVAGR        13  2     22EDTCDE(J)
     A N70N79                             41MSGCON(20 DIV1683 SYMSGF)
     A  70                                41'VAT Domestic:      %'
     A  79                                41'Kosten stat:       %'
     A            PRITAX         4  2     54EDTCDE(J)
     A  70        XVAL           3        61
     A  79        XVALB          3        61
     A N70 71     XVALC          3        61
     A            XPMOMS        13  2     61EDTCDE(J)
     A                                      SPACEA(1)
     A N70N79                              2MSGCON(17 SHU1120 SYMSGF)
     A  79                                 2'Taxfree total:  '
     A  70                                 2'Taxfree total:  '
     A            XMVAFR        13  2     22EDTCDE(J)
      *___________________________________
      * Format for faktura-total. IND: N35
      *"""""""""""""""""""""""""""""""""""
     A          R STOTB                     SKIPB(44)
     A N70N73N79                           2MSGCON(24 DIV1684 SYMSGF)
     A  70N73N79                           2'Terms of payment'
     A  79N73N70                           2'Zahlungbedingungen'
     A N70N79                             45MSGCON(12 DIV1685 SYMSGF)
     A  70                                45'Total:'
     A  79                                45'Gesamtbetrag'
     A            XVAL           3        61
     A            XBRUTT        13  2     61EDTCDE(J)
     A                                     2' '
     A                                      SPACEB(1)
     A N39N73     BETTXT        30         2
     A  39N70                              2'Kontant'
     A  39 70                              2'Cash:'
     A                                    45'================================='
     A  44        HP8           80         1SPACEB(1)
     A  44        HP9           80         1SPACEB(1)
     A  44        HP10          80         1SPACEB(1)
      *_________________________________________________
      * Format for faktura-total. without Norwegian giro.
      *""""""""""""""""""""""""""""""""""""""""""""""""""
     A          R STOTC                     SPACEB(4)
     A N70N73N79                           2MSGCON(24 DIV1684 SYMSGF)
     A  70N73N79                           2'Terms of payment'
     A N70N79                             45MSGCON(12 DIV1685 SYMSGF)
     A  70                                45'Total:'
     A  79                                45'Gesamtbetrag'
     A            XVAL           3        61
     A            XBRUTT        13  2     61EDTCDE(J)
     A                                     2' '
     A                                      SPACEB(1)
     A N39N73     BETTXT        30         2
     A  39N70                              2'Kontant'
     A  39 70                              2'Cash:'
     A                                    45'================================='
     A  44        HP8           80         1SPACEB(1)
     A  44        HP9           80         1SPACEB(1)
     A  44        HP10          80         1SPACEB(1)
      *________________________________
      * Format for Ordinary giro Norway
      *""""""""""""""""""""""""""""""""
     A          R SBGIR
     A N39 59     KFFDT          6  0  73 67 EDTCDE(Y)
     A  39 59                          73 67'Kontant'
     A  39 70 59                       73 67'Cash'
     A N70N79                          74  7'Faktura nummer:'
     A  70                             74  7'Invoice number:'
     A  79                             74  7'Rechnung nr...:'
     A            XFN            7  0  74 29 EDTCDE(3)
     A N70N79                          75  7'Kundenummer...:'
     A  70                             75  7'Customer no...:'
     A  79                             75  7'Kunden nr.....:'
     A            KUNDNR    R          75 28 REFFLD(KUNDNR CUNDF)
     A                                       EDTCDE(3)
     A N70N79                          76  7'Faktura dato..:'
     A  70                             76  7'Invoice date..:'
     A  79                             76  7'Rechnung datum'
     A            IDAGX          6  0  76 28 EDTCDE(Y)
     A N70N79                          77  7'Forfallsdato..:'
     A  70                             77  7'Date due......:'
     A  79                             77  7'Verfallstag...:'
     A N39        XFFDT          6  0  77 28 EDTCDE(Y)
     A  39N70                          77 28'Kontant'
     A  39 70                          77 28'Cash'
     A N70N79                          78  7'Vår Referanse.:'
     A  70                             78  7'Our reference.:'
     A  79                             78  7'Unsere ref....:'
     A            HEAVD     R          78 22
     A                                 78 26'/'
     A            HEOPD     R          78 27
     A                                 78 34'/'
     A            HESG      R          78 35
     A N48        KNAVN     R          82  7REFFLD(KNAVN CUNDF)
     A  48        XDINV         30     82  7
     A            XFNAVN        30     82 46
     A N48        ADR1      R          83  7REFFLD(ADR1 CUNDF)
     A  48        XDIAD1        30     83  7
     A            XFADR1        30     83 46
     A N48        ADR2      R          84  7REFFLD(ADR2 CUNDF)
     A  48        XDIAD2        30     84  7
     A            XFADR2        30     84 46
     A N37N48     POSTNR    R          85  7REFFLD(POSTNR CUNDF)
     A                                      DLTEDT
     A N37N48     ADR3      R          85 13REFFLD(ADR3 CUNDF)
     A N38N48     SYPOGE         9     85  7
     A N38N48     ADR3B     R          85 18REFFLD(ADR3 CUNDF)
     A  37 38N48  ADR3C     R          85  7REFFLD(ADR3 CUNDF)
     A  48        XDIAD3        30     85  7
     A            XFPONR         4  0  85 46
     A            XFADR3        24     85 52
      *_________________________________________
      * Format for H-line giro, OCR    5224/5225
      *"""""""""""""""""""""""""""""""""""""""""
     A          R SBGIR2A
     A                                      CPI(10)
     A                                      DFNCHR( +
     A                                      X'F0' X'7E818181817E000000' +
     A                                      X'F1' X'00004000FF00000000' +
     A                                      X'F2' X'438589819161000000' +
     A                                      X'F3' X'818191A1D18E000000' +
     A                                      X'F4' X'040C1424448F040000' +
     A                                      X'F5' X'E1A1A1A1928C000000' +
     A                                      X'F6' X'1E2949890906000000' +
     A                                      X'F7' X'80808F90A0C0800000' +
     A                                      X'F8' X'066991919169060000' +
     A                                      X'F9' X'609091929478000000')
     A  59        BANKGK        24     87 17HIGHLIGHT
     A N59        BANKG         15     88 13HIGHLIGHT
     A            OCRF          75     89  1
     A                                 91  1'H'
     A N59N62     XWKID         19     91 11HIGHLIGHT
     A N59N62     XBAK           1  0  91 30HIGHLIGHT
     A N59 62     KID8           8     91 16HIGHLIGHT
     A N59        XTOTKR        11  0  91 31EDTCDE(3)
     A                                      HIGHLIGHT
     A N59                             91 31'<'
     A                                      HIGHLIGHT
     A N59        XTOTØR         2  0  91 46HIGHLIGHT
     A  59N62     KWKID         19     91  9HIGHLIGHT
     A  59N62     KBAK           1     91 28HIGHLIGHT
     A  59 62     KID8B          8     91 14HIGHLIGHT
     A  59        KTOTKR        11  0  91 59EDTCDE(3)
     A  59                                  HIGHLIGHT
     A  59        KTOTØR         2  0  91 42HIGHLIGHT
     A  59        KNYMOD         1     91 47HIGHLIGHT
     A            XFBANK        15     91 52HIGHLIGHT
     A            OCRE          75     92  1
     A          R SBGIR2D
     A                                      CPI(10)
     A                                      DFNCHR( +
     A                                      X'F0' X'7E818181817E000000' +
     A                                      X'F1' X'00004000FF00000000' +
     A                                      X'F2' X'438589819161000000' +
     A                                      X'F3' X'818191A1D18E000000' +
     A                                      X'F4' X'040C1424448F040000' +
     A                                      X'F5' X'E1A1A1A1928C000000' +
     A                                      X'F6' X'1E2949890906000000' +
     A                                      X'F7' X'80808F90A0C0800000' +
     A                                      X'F8' X'066991919169060000' +
     A                                      X'F9' X'609091929478000000')
     A            BANKG         15     88 13HIGHLIGHT
     A                                 91  1'H'
     A                                      HIGHLIGHT
     A            XTOTKR        11  0  91 31EDTCDE(3)
     A                                      HIGHLIGHT
     A            XTOTØR         2  0  91 46HIGHLIGHT
     A            XFBANK        15     91 52HIGHLIGHT
      *___________________________________________
      * Format for H-line giro, OCR    4224(*IPDS)
      *"""""""""""""""""""""""""""""""""""""""""""
     A          R SBGIR2B
     A                                      FONT(011)
     A  59        BANKGK        24     87 17HIGHLIGHT
     A                                      CHRID
     A N59        BANKG         15     88 13HIGHLIGHT
     A                                      CHRID
     A            OCRF          75     90  1
     A                                 91  1'H'
     A                                      HIGHLIGHT
     A N59N62     XWKID         19     91 11HIGHLIGHT
     A                                      CHRID
     A N59N62     ABAK           1     91 30HIGHLIGHT
     A                                      CHRID
     A N59 62     KID8           8     91 16HIGHLIGHT
     A                                      CHRID
     A N59        ATOTKR        11     91 31HIGHLIGHT
     A                                      CHRID
     A N59        HAKE           1     91 31HIGHLIGHT
     A                                      CHRID
     A N59        ATOTØR         2     91 46HIGHLIGHT
     A                                      CHRID
     A  59N62     KWKID         19     91  9HIGHLIGHT
     A                                      CHRID
     A  59N62     KBAK           1     91 28HIGHLIGHT
     A                                      CHRID
     A  59 62     KID8B          8     91 14HIGHLIGHT
     A                                      CHRID
     A  59        ATOTKK        11     91 29HIGHLIGHT
     A                                      CHRID
     A  59        ATOTØK         2     91 42HIGHLIGHT
     A                                      CHRID
     A  59        KNYMOD         1     91 47HIGHLIGHT
     A                                      CHRID
     A            XFBANK        15     91 52HIGHLIGHT
     A                                      CHRID
     A            OCRE          75     92  1
     A          R SBGIR2E
     A                                      FONT(011)
     A            OCRF          75     87  1
     A            BANKG         15     88 13HIGHLIGHT
     A                                      CHRID
     A                                 91  1'H'
     A                                      HIGHLIGHT
     A            ATOTKR        11     91 31HIGHLIGHT
     A                                      CHRID
     A            ATOTØR         2     91 46HIGHLIGHT
     A                                      CHRID
     A            XFBANK        15     91 52HIGHLIGHT
     A                                      CHRID
     A            OCRE          75     92  1
      *_____________________________________________
      * Format for H-line giro, OCR    BELTESSKRIVER
      *"""""""""""""""""""""""""""""""""""""""""""""
     A          R SBGIR2C
     A  59        BANKGK        24     87 17HIGHLIGHT
     A N59        BANKG         15     88 13HIGHLIGHT
     A            OCRF          75     90  1
     A                                 91  1'H'
     A                                      HIGHLIGHT
     A N59N62     XWKID         19     91 11HIGHLIGHT
     A N59N62     XBAK           1  0  91 30HIGHLIGHT
     A N59 62     KID8           8     91 16HIGHLIGHT
     A N59        XTOTKR        11  0  91 31EDTCDE(3)
     A                                      HIGHLIGHT
     A N59                             91 31'<'
     A                                      HIGHLIGHT
     A N59        XTOTØR         2  0  91 46HIGHLIGHT
     A  59N62     KWKID         19     91  9HIGHLIGHT
     A  59N62     KBAK           1     91 28HIGHLIGHT
     A  59 62     KID8B          8     91 14HIGHLIGHT
     A  59        KTOTKR        11  0  91 29EDTCDE(3)
     A                                      HIGHLIGHT
     A  59        KTOTØR         2  0  91 42HIGHLIGHT
     A  59        KNYMOD         1     91 47HIGHLIGHT
     A            XFBANK        15     91 52HIGHLIGHT
     A            OCRE          75     92  1
      *_____________________________________________
      * Format for H-line giro, OCR    BELTESSKRIVER
      * LE*2391(4214-2)  ¤1=DRAFT, ¤2=LOUVIER 10, ¤3=OCR-B(KODESETT877)
      *"""""""""""""""""""""""""""""""""""""""""""""
     A          R SBGIR2F
     A  59        BANKGK        24     87 17HIGHLIGHT
     A N59        BANKG         15     88 13HIGHLIGHT
     A            OCRF          75     90  1
     A                                 91  1'H'
     A                                      HIGHLIGHT
     A N59N62     XWKID         19     91 11HIGHLIGHT
     A N59N62     XBAK           1  0  91 30HIGHLIGHT
     A N59 62     KID8           8     91 16HIGHLIGHT
     A N59        XTOTKR        11  0  91 31EDTCDE(3)
     A                                      HIGHLIGHT
     A N59                             91 31'<'
     A                                      HIGHLIGHT
     A N59        XTOTØR         2  0  91 46HIGHLIGHT
     A  59N62     KWKID         19     91  9HIGHLIGHT
     A  59N62     KBAK           1     91 28HIGHLIGHT
     A  59 62     KID8B          8     91 14HIGHLIGHT
     A  59        KTOTKR        11  0  91 29EDTCDE(3)
     A                                      HIGHLIGHT
     A  59        KTOTØR         2  0  91 42HIGHLIGHT
     A  59        KNYMOD         1     91 47HIGHLIGHT
     A            XFBANK        15     91 52HIGHLIGHT
     A            OCRE          75     92  1
      *________________________________
      * Format for H-line giro, Not OCR
      *""""""""""""""""""""""""""""""""
     A          R SBGIR3
     A                                 91  2'H'
     A                                      HIGHLIGHT
     A            XTOTKR        11  0  91 29EDTCDE(3)
     A                                      HIGHLIGHT
     A            XTOTØR         2  0  91 42HIGHLIGHT
     A            XFBANK        15     91 52HIGHLIGHT
