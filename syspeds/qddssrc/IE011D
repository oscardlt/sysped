     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HELP
     A                                      ALTHELP
      * "NOERASE" MÅ HA FOR Å IKKE TØMME BILDE SOM MAN KOMME FRA
     A          R NOERASE
     A                                      ASSUME
     A                                  1  2' '
      *
     A          R WINDOW1
     A                                      WINDOW(*DFT 13 70 *NOMSGLIN)
     A                                      CF12(12 'Avslutt')
     A                                      CF03(12)
     A                                      CF04(04 'SØK')
     A*                                     BLINK
     A*                                     WDWBORDER((*DSPATR HI) (*CHAR '...:-
     A*                                     ::.:'))
     A                                  1  2'IE011R'
     A                                      COLOR(BLU)
     A                                  1 11MSGCON(032 JOV1177 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  2  1MSGCON(012 JOV1188 *LIBL/SYMSGF)
     A            WSKN           8Y 0B  2 14EDTCDE(4)
     A                                      COLOR(WHT)
     A                                      DSPATR(PR)
     A            KNAVN         30A  O  2 23COLOR(WHT)
     A            WSLIB         10   O  2 54COLOR(WHT)
     A                                  3  1MSGCON(015 JOV1178 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  1MSGCON(012 JOV1181 *LIBL/SYMSGF)
     A            HEUR           1A  O  4 14COLOR(WHT)
     A            WSUR           1A  B  4 18COLOR(YLW)
     A                                  4 20MSGCON(050 JOV1182 *LIBL/SYMSGF)
     A                                  5  1MSGCON(012 JOV1180 *LIBL/SYMSGF)
     A            HEOT           2A  O  5 14COLOR(WHT)
     A            WSOT           2A  B  5 18COLOR(YLW)
     A                                  6  1MSGCON(012 JOV1179 *LIBL/SYMSGF)
     A            HEFR           3A  O  6 14COLOR(WHT)
     A            WSFR           3A  B  6 18COLOR(YLW)
     A                                  7  1MSGCON(012 JOV1183 *LIBL/SYMSGF)
     A            HEGM1     R        O  7 14REFFLD(HEADFR/HEGM1 *LIBL/HEADF)
     A                                      COLOR(WHT)
     A            HEGM2     R        O  7 30REFFLD(HEADFR/HEGM2 *LIBL/HEADF)
     A                                      COLOR(WHT)
     A            WSGM1     R        B  8 14REFFLD(HEADFR/HEGM1 *LIBL/HEADF)
     A                                      COLOR(YLW)
     A            WSGM2     R        B  8 30REFFLD(HEADFR/HEGM2 *LIBL/HEADF)
     A                                      COLOR(YLW)
     A                                  9  1MSGCON(012 JOV1184 *LIBL/SYMSGF)
     A            HEVS1     R        O  9 14REFFLD(HEADFR/HEVS1 *LIBL/HEADF)
     A                                      COLOR(WHT)
     A            HEVS2     R        O  9 40REFFLD(HEADFR/HEVS2 *LIBL/HEADF)
     A                                      COLOR(WHT)
     A            WSVS1     R        B 10 14REFFLD(HEADFR/HEVS1 *LIBL/HEADF)
     A                                      COLOR(YLW)
     A            WSVS2     R        B 10 40REFFLD(HEADFR/HEVS2 *LIBL/HEADF)
     A                                      COLOR(YLW)
     A                                 11  1MSGCON(012 JOV1185 *LIBL/SYMSGF)
     A*           HERFA     R        O 11 14REFFLD(HEADFR/HERFA *LIBL/HEADF)
     A            HERFA15       15A  O 11 14
     A                                      COLOR(WHT)
     A*           WSRFA     R        B 11 30REFFLD(HEADFR/HERFA *LIBL/HEADF)
     A            WSRFA15       15A  B 11 30
     A                                      COLOR(YLW)
     A                                 12  1'Fritekst...:'
     A            WSFK1          1A  B 12 14COLOR(YLW)
     A            WSFK2          1A  B 12 16COLOR(YLW)
     A            WSFK3          1A  B 12 18COLOR(YLW)
     A            WSFK4          1A  B 12 20COLOR(YLW)
     A            WSFK5          1A  B 12 22COLOR(YLW)
     A                                 12 25MSGCON(030 JOV1186 *LIBL/SYMSGF)
     A N30                             13 25MSGCON(018 DIV0032 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N30                             13 50MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
