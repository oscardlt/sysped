/*  BYTTET GETPR2 MED GETPRN FOR DRAWER OG ANTAL.L KOPIER */
/* *********************************************************************   */
/*       * RETTINGER I DETTE PROGRAM:                                      */
/* PTFnr:*Sek Sig Vers  Beskrivelse..........................              */
/* """"""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""    */
/* 10XXX * 01 YBC PTF10 SYFA55CP                                           */
/* 10XXX * 02 JOV PTF10 SYFA55CP - IKKE CMR VED INNLAND                    */
/*       * 03 JOV PTF10 TA HENSYN TIL F=AFP                                */
/* ********************************************************************    */
             PGM        PARM(&TUR)
             DCL        VAR(&DRAWER) TYPE(*CHAR) LEN(7)
             DCL        VAR(&DRAWUT) TYPE(*CHAR) LEN(11)
             DCL        VAR(&COPIES)  TYPE(*CHAR) LEN(6)
             DCL        VAR(&PNAVN)  TYPE(*CHAR) LEN(10)
             DCL        VAR(&FORM)  TYPE(*CHAR) LEN(10)
             DCL        VAR(&LOGO)  TYPE(*CHAR) LEN(9)
             DCL        VAR(&ATYPE) TYPE(*CHAR) LEN(2) VALUE('go')
             DCL        VAR(&AUSER) TYPE(*CHAR) LEN(10)
             DCL        VAR(&AJOB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&ANBR) TYPE(*CHAR) LEN(6)
             DCL        VAR(&FINS) TYPE(*CHAR) LEN(1) VALUE('N')
             DCL        VAR(&IN01) TYPE(*LGL) LEN(1) VALUE('0') /* +
                          Hvis &in01 på, betyr det avslutt program +
                          fra valgbilde. */
             DCL        VAR(&IN03) TYPE(*LGL) LEN(1) /* Hvis &in03 +
                          på, skal losseliste  være stående. */
             DCL        VAR(&SAML) TYPE(*CHAR) LEN(1)
             DCL        VAR(&FRBR) TYPE(*CHAR) LEN(1)
             DCL        VAR(&KODE) TYPE(*CHAR) LEN(1) VALUE('F')
             DCL        VAR(&VB) TYPE(*CHAR) LEN(1)
             DCL        VAR(&EKSP) TYPE(*CHAR) LEN(1)
             DCL        VAR(&LG) TYPE(*CHAR) LEN(1)
             DCL        VAR(&PO) TYPE(*CHAR) LEN(1) VALUE('T')
             DCL        VAR(&UNIK) TYPE(*CHAR) LEN(7)
             DCL        VAR(&JOBN) TYPE(*CHAR) LEN(6)
             DCL        VAR(&FAXJN) TYPE(*CHAR) LEN(1)
             DCL        VAR(&BRUDDA) TYPE(*CHAR) LEN(1)
             DCL        VAR(&FAXNR) TYPE(*CHAR) LEN(15)
             DCL        VAR(&AVD) TYPE(*CHAR) LEN(4)
             DCL        VAR(&TUR) TYPE(*CHAR) LEN(8)
             DCL        VAR(&AGNULL) TYPE(*CHAR) LEN(8) +
                          VALUE('00000000')
             DCL        VAR(&EDIJN) TYPE(*CHAR) LEN(1)
             DCL        VAR(&EDITR) TYPE(*CHAR) LEN(1)
             DCL        VAR(&LISJN) TYPE(*CHAR) LEN(1)
             DCL        VAR(&BELJN) TYPE(*CHAR) LEN(1)
             DCL        VAR(&BELJN2) TYPE(*CHAR) LEN(1)
             DCL        VAR(&UDSFAX) TYPE(*CHAR) LEN(1) VALUE('N')
  /*LÅNER PARM. FOR DISTR.ADRESSE FOR Å INDIKERE GODSLISTE INNLAND*/
             DCL        VAR(&DADRJN) TYPE(*CHAR) LEN(1) VALUE('G')
    /* */
             DCL        VAR(&PRT1) TYPE(*CHAR) LEN(10) /* Printernavn +
                          til stående losseliste. */
             DCL        VAR(&PT1) TYPE(*CHAR) LEN(10) /* Papirtype +
                          til stående losseliste. */
             DCL        VAR(&PRT2) TYPE(*CHAR) LEN(10) /* Printernavn +
                          til liggende losseliste. */
             DCL        VAR(&PT2) TYPE(*CHAR) LEN(10) /* Papirtype +
                          til liggende losseliste. */
/*N01*/      DCL        VAR(&PRT3) TYPE(*CHAR) LEN(10) /* Printernavn +
                          til CMR-FRAKTBREV        */
/*N01*/      DCL        VAR(&PT3) TYPE(*CHAR) LEN(10) /* Papirtype +
                          til CMR-FRAKTBREV        */
             DCL        VAR(&UTSNR) TYPE(*DEC) LEN(3 0)
             DCL        VAR(&LASER) TYPE(*CHAR) LEN(1)
             DCL        VAR(&LPI2) TYPE(*DEC) LEN(2 0)
    /* */
    /* HENTE AVDELING, SIGNATUR OG FIRMAKODE */
             CALL       PGM(SYFA00) PARM(&IN01)
             IF         COND(&IN01 = '1') THEN(GOTO CMDLBL(SLUTT)) /* +
                          Hvis &IN01 på, avslutter program. */

 RTVUNIK:    RTVJOBA    JOB(&AJOB) USER(&AUSER) NBR(&ANBR)
             CHGVAR     VAR(&UNIK) VALUE('W' *CAT &ANBR)
             RMVM       FILE(WRKAGL01) MBR(&UNIK)
             MONMSG     MSGID(CPF7310)
             RMVM       FILE(WRKAG) MBR(&UNIK)
             MONMSG     MSGID(CPF7310)
             ADDPFM     FILE(WRKAG) MBR(&UNIK)
             MONMSG     MSGID(CPF7306) EXEC(GOTO CMDLBL(RTVUNIK))
             ADDLFM     FILE(WRKAGL01) MBR(&UNIK) DTAMBRS((WRKAG +
                          (&UNIK)))
             MONMSG     MSGID(CPF7306) EXEC(GOTO CMDLBL(RTVUNIK))
             OVRDBF     FILE(WRKAG) TOFILE(WRKAG) MBR(&UNIK)
             OVRDBF     FILE(WRKAGL01) TOFILE(WRKAGL01) MBR(&UNIK)
    /* */
    /* HENTE OPPLYSNINGER OM HVA SLAGS UTSKRIFT MAN ØNSKER */
             CALL       PGM(SYFAGI) PARM(&IN03 &IN01 +
                          &FAXJN &FAXNR &AVD &TUR &BRUDDA &EDIJN +
                          &LISJN &BELJN &BELJN2 &SAML &FRBR &EDITR)

             IF         COND(&IN01 = '1') THEN(GOTO CMDLBL(SLUTT)) /* +
                          Hvis &IN01 på, avslutter program. */
    /* */
    /* ----------------------------------------------------------- */
    /* SKRIVER UT GODSLISTE  INNLAND PÅ PAPIR - ALLTID DIREKTE */
    /* ----------------------------------------------------------- */
             IF         COND(&LISJN = 'J') THEN(DO)
             RTVDTAARA  DTAARA(*LDA (821 10)) RTNVAR(&PRT1)
             RTVDTAARA  DTAARA(*LDA (831 10)) RTNVAR(&PT1)
             RTVDTAARA  DTAARA(*LDA (841 10)) RTNVAR(&PRT2)
             RTVDTAARA  DTAARA(*LDA (851 10)) RTNVAR(&PT2)
/*N01*/      RTVDTAARA  DTAARA(*LDA (861 10)) RTNVAR(&PRT3)
/*N01*/      RTVDTAARA  DTAARA(*LDA (871 10)) RTNVAR(&PT3)
             /*STÅENDE  */
    /* ----------------------------------------------------------- */
    /* ORDNER PRINTER FOR GODSLISTE STÅENDE  */
    /* ----------------------------------------------------------- */
             IF         COND(&IN03 = '1') THEN(DO)
             CHGVAR     VAR(&UTSNR) VALUE(022)
             CALL       PGM(GETPRN) PARM(&AVD &UTSNR &PNAVN &FORM +
                          &LASER &LPI2 &DRAWER &COPIES &LOGO &DRAWUT)
             IF         COND(&LASER *NE 'P') THEN(DO)
             OVRPRTF    FILE(SYFA55PF) PAGESIZE(70 120) CPI(15) +
                          OVRFLW(62) DRAWER(&DRAWER) +
                          OUTBIN(&DRAWUT) FRONTOVL(&LOGO) +
                          OUTQ(&PRT1) FORMTYPE(&PT1) COPIES(&COPIES)
                        ENDDO
             IF         COND(&LASER *EQ 'P') THEN(DO)
             OVRPRTF    FILE(SYFA55PF) DEVTYPE(*AFPDS) PAGESIZE(70 +
                          120) CPI(16.7) FRONTMGN(0.14 0.76) +
                          OVRFLW(62) DRAWER(&DRAWER) +
                          OUTBIN(&DRAWUT) FRONTOVL(&LOGO) +
                          OUTQ(&PRT1) FORMTYPE(&PT1) COPIES(&COPIES)
                        ENDDO
  /*N03 F=AFP..*/
             IF         COND(&LASER *EQ 'F') THEN(DO)
             OVRPRTF    FILE(SYFA55PF) DEVTYPE(*AFPDS) PAGESIZE(70 +
                          120) CPI(16.7) FRONTMGN(0.14 0.76) +
                          OVRFLW(62) DRAWER(&DRAWER) +
                          OUTBIN(&DRAWUT) FRONTOVL(&LOGO) +
                          OUTQ(&PRT1) FORMTYPE(&PT1) COPIES(&COPIES)
                        ENDDO

             ENDDO
             /*LIGGENDE */
    /* ----------------------------------------------------------- */
    /* ORDNER PRINTER FOR GODSLISTE LIGGENDE */
    /* ----------------------------------------------------------- */
             IF         COND(&IN03 = '0') THEN(DO)
             CHGVAR     VAR(&UTSNR) VALUE(023)
             CALL       PGM(GETPRN) PARM(&AVD &UTSNR &PNAVN &FORM +
                          &LASER &LPI2 &DRAWER &COPIES &LOGO &DRAWUT)
             IF         COND(&LASER *NE 'P') THEN(DO)
             OVRPRTF    FILE(SYFA55PF) PAGESIZE(48 120) CPI(12) +
                          OVRFLW(44) DRAWER(&DRAWER) +
                          OUTBIN(&DRAWUT) PAGRTT(90) +
                          FRONTOVL(&LOGO) OUTQ(&PRT2) +
                          FORMTYPE(&PT2) COPIES(&COPIES)

                        ENDDO
             IF         COND(&LASER *EQ 'P') THEN(DO)
             OVRPRTF    FILE(SYFA55PF) DEVTYPE(*AFPDS) PAGESIZE(48 +
                          120) CPI(12) FRONTMGN(0.14 0.80) +
                          OVRFLW(44) DRAWER(&DRAWER) +
                          OUTBIN(&DRAWUT) PAGRTT(90) +
                          FRONTOVL(&LOGO) OUTQ(&PRT2) +
                          FORMTYPE(&PT2) COPIES(&COPIES)
                        ENDDO
  /*N03 F=AFP..*/
             IF         COND(&LASER *EQ 'F') THEN(DO)
             OVRPRTF    FILE(SYFA55PF) DEVTYPE(*AFPDS) PAGESIZE(48 +
                          120) CPI(12) FRONTMGN(0.14 0.80) +
                          OVRFLW(44) DRAWER(&DRAWER) +
                          OUTBIN(&DRAWUT) PAGRTT(90) +
                          FRONTOVL(&LOGO) OUTQ(&PRT2) +
                          FORMTYPE(&PT2) COPIES(&COPIES)
                        ENDDO

             ENDDO
/*S02  OVERSTYRINGER AV SYFA55CP - CMR ER FJERNET - INGEN CMR VED INNLAND*/


             CALL       PGM(SYFA55) PARM(&AVD &TUR &AGNULL &BELJN +
                          &UDSFAX &DADRJN)
    /* ----------------------------------------------------------- */
    /* ARKIVERER                                                   */
    /* ----------------------------------------------------------- */
             SBMJOB     CMD(CALL PGM(ARC002CL) PARM(&ATYPE &AUSER +
                          &FINS)) JOB(ARKIV) JOBD(*USRPRF) +
                          JOBQ(SYJOBQARK)
    /* */
    /* */
                        ENDDO

    /* ----------------------------------------------------------- */
    /* MERKER SAMLEFAKTURAER                                       */
    /* ----------------------------------------------------------- */
             IF         COND(&SAML = 'J') THEN(DO)
             CALL       PGM(SYTR25) PARM(&TUR)
             ENDDO
    /* ----------------------------------------------------------- */
    /* SKRIVER FRAKTBREV  (ALLE PÅ TUREN UANSETT AVD.)             */
    /* ----------------------------------------------------------- */
             IF         COND(&FRBR = 'J') THEN(DO)
                            CALL PGM(SYFA12CLJ) PARM(&KODE &VB &EKSP +
                          &LG &PO)
             ENDDO
    /* ----------------------------------------------------------- */
    /* SENDER EDIFACT  AV GODSLISTEN - IFCSUM-INNL.IMPL.           */
    /* ----------------------------------------------------------- */
             IF         COND(&EDIJN = 'J') THEN(DO)
             CALL       PGM(E300R) PARM(&BRUDDA)
                        ENDDO
    /* ----------------------------------------------------------- */
    /* SENDER EDIFACT  TIL TRANSPORTØR                             */
    /* ----------------------------------------------------------- */
             IF         COND(&EDITR = 'J') THEN(DO)
             CHGVAR     VAR(&BRUDDA) VALUE('T')
             CALL       PGM(E301R) PARM(&AVD &TUR &AGNULL &BRUDDA)
             ENDDO
    /* ----------------------------------------------------------- */
    /* SENDER GODSLISTE PÅ FAX   */
    /* ----------------------------------------------------------- */
             IF         COND(&FAXJN = 'J') THEN(DO)
             IF         COND(&BRUDDA = 'J') THEN(DO)
             CALL       PGM(SYFA55A)   PARM(&AVD &TUR &BELJN2)
             GOTO       CMDLBL(SLUTT)
                        ENDDO
             CALL       PGM(FAX055CLD) PARM(&AVD &TUR &BELJN2 &AGNULL)
                        ENDDO
    /* */
             RMVM       FILE(WRKAGL01) MBR(&UNIK)
             MONMSG     MSGID(CPF7310)
             RMVM       FILE(WRKAG) MBR(&UNIK)
             MONMSG     MSGID(CPF7310)
 SLUTT:      ENDPGM
