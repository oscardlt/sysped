********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 SH  PTF10 endret litt på hawbnummer
11XXX * 01 SH  PTF11 endret litt på hawbnummer
12129 * 03 CB  PTF12 OTHER CUSTOMS INFORMATION
      *
      ***********************************************************
      *** PROGRAM SKAL SKRIVE UT MANIFEST (OMKJØRING)         ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01-26                            ***
      *** LEDIGE INDIKATORER 32 34-70 73-87 89-99             ***
      ***********************************************************
      *
     FKODTP     IF   E           K DISK
     FKODTFM    IF   E           K DISK
     FFIRM      IF   E           K DISK
     FSUMMAN    UF A E           K DISK
     FKODTLK    IF   E           K DISK
     FHEADF     IF   E           K DISK
     FHEAD19    IF   E           K DISK
     F                                     RENAME(HEADFR:HEAD7R)
     FSAMLB     IF   E           K DISK
     FFAK2      IF   E           K DISK
     FDOKEF     IF   E           K DISK
     FHODE      IF   E           K DISK
     FCUNDL01   IF   E           K DISK
     FKODTH     IF   E           K DISK
     FSYEF30P   O    E             PRINTER OFLIND(*IN01)
N03  FDOKOCI    IF   E           K DISK
     D                 DS
     D  ADR3                   1     24
     D  ADR3A                  1     24
     D                 DS
     D  SMSD                   1      3
     D  SMSDA                  1      3
     D                 DS
     D  HENT                   1      7  0
     D  XNT                    2      7  0
     D                 DS
     D  HEVKT                  1      9  0
     D  XVKT                   2      9  0
     D                 DS
     D  HEDTKF                 1      8  0
     D  XEDTKÅ                 1      4  0
     D  XEDTKM                 5      6  0
     D  XEDTKD                 7      8  0
     D                 DS
     D  XFFDT                  1      6  0
     D  XFFDTD                 1      2  0
     D  XFFDTM                 3      4  0
     D  XFFDTÅ                 5      6  0
     D                 DS
     D  XSD                    1      3
     D  XLNR                   4      6  0
     D  XBLANK                 7     15
     D  YRFA                   1     15
s02  D*                DS
s02  D* HEHAWB                 1      9
s02  D* XHAWB                  4      9
     D                 DS
     D  HEGN                   1     15
     D  XFSN                   5      7
     D  XGN8                   8     15
     D                 DS
     D  WKEY19                 1      6
     D  WUSD                   1      3
     D  WULNR                  4      6  0
     D                UDS
     D  UFIRM                  1      2
     D  UAVD                  15     18  0
     D  USG                   19     21
     D  USD                   36     38
     D  ULNR                  39     41  0
     D  UFAK                  42     42
     D  UHOD                  47     47
     D  UBBS                  48     48
     C*
     C                   EXSR      INIT
     C*
     C     KOPFM         SETLL     KODTFM
     C     START0        TAG
     C     KOPFM         IFNE      *BLANKS                                      1<-B
     C     KOPFM         READE     KODTFM                                 30
     C     *IN30         CABEQ     '1'           SLUTT
     C                   END                                                    1<-E
     C     WKEY19        SETLL     HEAD19
     C     WKEY19        READE     HEAD19                                 30
     C                   EXSR      KDTFMT
     C     KOPFM         IFNE      *BLANKS                                      1<-B
     C                   WRITE     LHEADH1
     C     KFMT2         IFNE      *BLANKS                                        3<-B
     C                   WRITE     LHEADH2
     C                   END                                                      3<-E
     C                   END                                                    1<-E
     C                   WRITE     LHEAD
     C     WKEY19        SETLL     HEAD19                             30
     C     START         TAG
     C     WKEY19        READE     HEAD19                                 30
     C  N30HEST          IFEQ      'S'
     C                   GOTO      START
     C                   END
     C*
     C  N30              EXSR      DETA
     C  N30              GOTO      START
     C     '   '         SETLL     SUMMAN
     C                   SETOFF                                       30
     C                   WRITE     LTOT0
     C                   MOVE      *ZEROS        XTANT
     C                   MOVE      *ZEROS        XTVKT
     C     STSUMT        TAG
     C                   READ      SUMMAN                                 30
     C  N30              DO
     C   01              DO
     C                   EXSR      KDTFMT
     C                   SETOFF                                       01
     C     KOPFM         IFNE      *BLANKS                                      1<-B
     C                   WRITE     LHEADH1
     C     KFMT2         IFNE      *BLANKS                                       2<-B
     C                   WRITE     LHEADH2
     C                   END                                                     2<-E
     C                   END                                                    1<-E
     C                   WRITE     LHEAD
     C                   END
     C                   WRITE     LTOT
     C                   MOVE      *ZEROS        SUVALB
     C                   GOTO      STSUMT
     C                   END
     C                   WRITE     LTOT9
     C     KOPFM         CABNE     *BLANKS       START0
     C*
     C*
     C     SLUTT         TAG
     C                   MOVE      XPRANT        UPRANT
     C                   SETON                                        LR
     C                   RETURN
     C*
     C******************************************
     C     DETA          BEGSR
     C******************************************
     C     DOKEFK        CHAIN     DOKEF                              31
     C     DFROA3        IFNE      *BLANKS
     C                   MOVE      DFROA3        XTIL2             3
     C                   ELSE
     C     DFROA2        IFNE      *BLANKS
     C                   MOVE      DFROA2        XTIL2
     C                   ELSE
     C                   MOVE      DFROA1        XTIL2
     C                   END
     C                   END
N03   * Hent ev. OCI
N03  C                   CLEAR                   DOKOCIR
N03  C                   EVAL      WOCI=*BLANKS
N03  C                   EVAL      OCI1=*BLANKS
N03  C                   EVAL      OCI2=*BLANKS
N03  C                   EVAL      OCI3=*BLANKS
N03  C                   EVAL      OCI4=*BLANKS
N03  C                   EVAL      OCI5=*BLANKS
N03  C                   EVAL      OCI6=*BLANKS
N03  C                   EVAL      OCI7=*BLANKS
N03  C                   EVAL      OCI8=*BLANKS
N03  C                   EVAL      *IN89=*OFF
N03  C     OCIKEY        SETLL     DOKOCI
N03  C     OCIKEY        READE     DOKOCI                                 89
N03  C     *IN89         DOWEQ     *OFF
N03
N03  C     *LIKE         DEFINE    OCI1          WOCI
N03  C                   EVAL      WOCI='OCI/' +
N03  C                                  %trim(DOKOCI293) +
N03  C                                  '/' +
N03  C                                  %trim(DOKOCI295) +
N03  C                                  '/' +
N03  C                                  %trim(DOKOCI297) +
N03  C                                  '/' +
N03  C                                  %trim(DOKOCI299)
N03  C                   SELECT
N03  C     OCI1          WHENEQ    *BLANKS
N03  C                   EVAL      OCI1=WOCI
N03  C     OCI2          WHENEQ    *BLANKS
N03  C                   EVAL      OCI2=WOCI
N03  C     OCI3          WHENEQ    *BLANKS
N03  C                   EVAL      OCI3=WOCI
N03  C     OCI4          WHENEQ    *BLANKS
N03  C                   EVAL      OCI4=WOCI
N03  C     OCI5          WHENEQ    *BLANKS
N03  C                   EVAL      OCI5=WOCI
N03  C     OCI6          WHENEQ    *BLANKS
N03  C                   EVAL      OCI6=WOCI
N03  C     OCI7          WHENEQ    *BLANKS
N03  C                   EVAL      OCI7=WOCI
N03  C     OCI8          WHENEQ    *BLANKS
N03  C                   EVAL      OCI8=WOCI
N03  C                   ENDSL
N03
N03  C     OCIKEY        READE     DOKOCI                                 89
N03  C                   ENDDO
     C*
     C                   ADD       HENT          XTANT             7 0
     C                   ADD       HEVKT         XTVKT
     C                   EXSR      TDUASR
     C   01              DO
     C                   EXSR      KDTFMT
     C                   SETOFF                                       01
     C     KOPFM         IFNE      *BLANKS                                      1<-B
     C                   WRITE     LHEADH1
     C     KFMT2         IFNE      *BLANKS                                       2<-B
     C                   WRITE     LHEADH2
     C                   END                                                     2<-E
     C                   END                                                    1<-E
     C                   WRITE     LHEAD
     C                   END
     C                   WRITE     LDET
     C  N27              DO
     C     HEVALK        CHAIN     SUMMAN                             89
     C   89              DO
     C                   MOVE      HEVALK        SUVAL
     C                   Z-ADD     XKDUA         SUVALB
     C                   Z-ADD     XXFAKT        SUFANR
     C                   WRITE     SUMMAR
     C                   END
     C  N89              DO
     C                   ADD       XKDUA         SUVALB
     C                   Z-ADD     XXFAKT        SUFANR
     C                   UPDATE    SUMMAR
     C                   END
     C                   END
     C*
     C                   ENDSR
     C*
     C******************************************
     C     TDUASR        BEGSR
     C******************************************
     C                   MOVE      *ZEROS        MVAGRL           13 2
     C                   MOVE      *ZEROS        XKDUA            13 2
     C     FAK2K         SETLL     FAK2
     C                   SETOFF                                       31
     C     ST2           TAG
     C     FAK2K         READE     FAK2                                   31
     C  N31HEKNA         IFNE      FAKUNR
     C                   GOTO      ST2
     C                   END
     C*
     C  N31FAKDA         IFEQ      'D'
     C                   GOTO      ST2
     C                   END
     C*
     C*
     C  N31FAKDA         IFEQ      'K'
     C                   GOTO      ST2
     C                   END
     C*
     C  N31FICURR        IFEQ      HEVALK
     C                   ADD       FABELN        XKDUA
     C     FAKDM         IFEQ      'J'
     C                   ADD       FABELN        MVAGRL
     C                   END
     C                   ELSE
     C                   ADD       FABELV        XKDUA
     C     FAKDM         IFEQ      'J'
     C                   ADD       FABELV        MVAGRL
     C                   END
     C                   END
     C     FAFAKT        IFNE      *ZEROS
     C                   Z-ADD     FAFAKT        XXFAKT            6 0
     C                   END
     C  N31              GOTO      ST2
     C* VARIABLE VAT
     C                   MOVE      FITAX         FITAXB            4 4
     C     FITAXD        IFNE      *ZEROS
     C     FITAX2        ANDNE     *ZEROS
     C     FADATO        ANDLT     FITAXD
     C                   MOVE      FITAX2        FITAXB            4 4
     C                   END
     C     MVAGRL        MULT(H)   FITAXB        MOMS             13 2
     C                   ADD       MOMS          XKDUA
     C                   ADD       XKDUA         XTDUA            13 2
     C*
     C     HEADFK        CHAIN     HEADF                              31
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
      *
     C     KODLKK        KLIST
     C                   KFLD                    YLKUNI
     C                   KFLD                    SYLAND            2
     C                   MOVE      'LK '         YLKUNI            3
      *
     C     KODTPK        KLIST
     C                   KFLD                    WSPUNI
     C                   KFLD                    UAVD
     C                   KFLD                    WSPLNR
     C                   MOVE      'P'           WSPUNI            1
     C                   Z-ADD     18            WSPLNR            3 0
     C  N99KODTPK        CHAIN     KODTP                              99
     C     UFAX          IFEQ      'J'
     C                   MOVE      *BLANKS       KOPFM
     C                   SETON                                        77
     C                   END
     C*
     C     HEADFK        KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C     SAMLBK        KLIST
     C                   KFLD                    USD
     C                   KFLD                    ULNR
     C     FAK2K         KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C                   KFLD                    YSK
     C     HODEK         KLIST
     C                   KFLD                    YNET
     C                   KFLD                    UAVD
     C     DOKEFK        KLIST
     C                   KFLD                    YRI
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C     KUND1K        KLIST
     C                   KFLD                    UFIRM
     C                   KFLD                    SMKNS
     C     KODTK         KLIST
     C                   KFLD                    UFIRM
     C                   KFLD                    BETBET
     C     KODTHK        KLIST
     C                   KFLD                    YHUNI
     C                   KFLD                    UAVD
N03  C     OCIKEY        KLIST
N03  C                   KFLD                    DFAVD
N03  C                   KFLD                    DFOPD
N03  C                   KFLD                    DFLOP
     C*
     C     *ENTRY        PLIST
     C                   PARM                    UIN07             1
     C                   PARM                    UIN08             1
     C                   PARM                    UPRANT            2
     C                   PARM                    UFAX              1
     C     UIN07         COMP      '1'                                    27
     C     UIN08         COMP      '1'                                    28
     C*
     C                   READ      FIRM                                   33
     C*
     C                   MOVE      'K'           YSK               1
     C                   MOVE      'H'           YHUNI             1
     C                   MOVE      'F'           YRI               1
     C                   MOVE      USD           XSD
     C                   MOVE      ULNR          XLNR
     C                   MOVE      *BLANKS       XBLANK
     C                   MOVE      *ZEROS        XPRANT            2 0
     C                   MOVE      *ZEROS        HEDTKF
     C*
     C     KODTHK        CHAIN     KODTH                              33
     C     KOHMAN        COMP      'J'                                    29
     C*
     C     SAMLBK        CHAIN     SAMLB                              33
     C     SMROB2        COMP      *BLANKS                            7171
     C     SMROB3        COMP      *BLANKS                            7272
     C     KUND1K        CHAIN     CUNDL01                            33
      *
     C                   MOVE      *BLANKS       KJLAND
     C     KODLKK        CHAIN     KODTLK                             34
     C  N34              MOVE      KLKNVN        KJLAND           30
     C*
     C   29
     CANN33SPRAAK        IFEQ      'E'
     C                   MOVE      'E'           YNET              1
     C                   ELSE
     C     SPRAAK        IFEQ      'T'
     C                   MOVEL     'T'           YNET
     C                   ELSE
     C                   MOVEL     ' '           YNET
     C                   END
     C                   END
     C   29
     CANN33HODEK         CHAIN     HODE                               33
     C*
     C     POSTNR        COMP      *ZEROS                                 88
     C*
     C                   MOVEL     USD           WUSD
     C                   Z-ADD     ULNR          WULNR
     C                   ENDSR
     C*
     C***********************************************
     C     KDTFMT        BEGSR
     C***********************************************
     C                   MOVE      XEDTKD        UDT1              2 0
     C                   MOVE      XEDTKM        UDT2              2 0
     C                   MOVE      XEDTKÅ        UDT3              2 0
     C                   CALL      'DATOFMT'
     C                   PARM                    UDT1
     C                   PARM                    UDT2
     C                   PARM                    UDT3
     C                   MOVE      UDT1          XFFDTD
     C                   MOVE      UDT2          XFFDTM
     C                   MOVE      UDT3          XFFDTÅ
     C*
     C                   ENDSR
     C*
