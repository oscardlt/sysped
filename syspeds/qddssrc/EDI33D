     A*%%TS  SD  20140908  122610  SVEIN       REL-V7R1M0  5770-WDS
     A**************************************************************
     A* RETTINGER I DETTE PROGRAM:
PTFnrA*Sek Sig Vers Beskrivelse...........................
"""""A*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
P0017A*  01   CB   VARIABLE FOR POLLVENTING
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3                  -
     A                                             27 132 *DS4)
     A                                      REF(*LIBL/EDIC)
     A                                      PRINT
     A                                      CF03(03 'Avslutt program')
     A                                      CF12(12 'Annuller')
     A                                      HLPDOC('STD' 'HELPNU' 'HLP400')
     A                                      HELP
     A*
     A          R EDI33DA
     A*%%TS  SD  20140908  122610  SVEIN       REL-V7R1M0  5770-WDS
     A                                      OVERLAY
     A                                  1  2'EDI'
     A                                      COLOR(BLU)
     A                                  1 32MSGCON(019 EDI0001 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 73'EDI33D A'
     A                                      COLOR(BLU)
     A                                  3  2MSGCON(034 EDI0602 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  6MSGCON(034 EDI0003 *LIBL/SYMSGF)
     A            ZSN       R     D  B  5 43REFFLD(CSN)
     A  32                                  DSPATR(PC)
     A  32                                  COLOR(RED)
     A            ZMN       R     D  B  5 53REFFLD(CMN)
     A  33                                  DSPATR(PC)
     A  33                                  COLOR(RED)
     A                                  7  6MSGCON(034 EDI0004 *LIBL/SYMSGF)
     A            ZDEV1         10A  B  7 43DSPATR(PC)
     A  34                                  COLOR(RED)
     A            ZDEV2         10A  B  7 56
     A  35                                  DSPATR(PC)
     A  35                                  COLOR(RED)
     A            ZDEV3         10A  B  7 69
     A  36                                  DSPATR(PC)
     A  36                                  COLOR(RED)
     A            ZDEV4         10A  B  8 43
     A  37                                  DSPATR(PC)
     A  37                                  COLOR(RED)
     A            ZDEV5         10A  B  8 56
     A  38                                  DSPATR(PC)
     A  38                                  COLOR(RED)
     A            ZDEV6         10A  B  8 69
     A  39                                  DSPATR(PC)
     A  39                                  COLOR(RED)
     A                                 10  6MSGCON(034 EDI0005 *LIBL/SYMSGF)
     A            ZLIND         10A  B 10 43
     A  40                                  DSPATR(PC)
     A  40                                  COLOR(RED)
     A                                 10 56MSGCON(023 EDI0015 *LIBL/SYMSGF)
     A                                 11  8MSGCON(032 EDI0006 *LIBL/SYMSGF)
     A            ZCTLD         10A  B 11 43
     A  41                                  DSPATR(PC)
     A  41                                  COLOR(RED)
     A                                 12 10MSGCON(030 EDI0007 *LIBL/SYMSGF)
     A            ZDEVD         10A  B 12 43
     A  42                                  DSPATR(PC)
     A  42                                  COLOR(RED)
     A                                 14  6MSGCON(034 EDI0008 *LIBL/SYMSGF)
     A            ZIBSTR         6A  B 14 43
     A  46                                  DSPATR(PC)
     A  46                                  COLOR(RED)
     A                                 14 56MSGCON(019 EDI0009 *LIBL/SYMSGF)
     A                                 15  8MSGCON(032 EDI0010 *LIBL/SYMSGF)
     A            ZIBEND         6A  B 15 43
     A  47                                  DSPATR(PC)
     A  47                                  COLOR(RED)
     A                                 15 56MSGCON(019 EDI0011 *LIBL/SYMSGF)
     A                                 16  8MSGCON(032 EDI0012 *LIBL/SYMSGF)
     A            ZIBDLY         5A  B 16 43
     A  48                                  DSPATR(PC)
     A  48                                  COLOR(RED)
     A                                 16 56MSGCON(008 EDI0013 *LIBL/SYMSGF)
     A                                 18  6'Installerte produkter  Y/N . . . :'
     A                                 18 43'SYSPED'
     A            ZESYSP         1A  B 18 50VALUES('N' 'Y')
     A                                 18 54'TDVIS'
     A            ZETDV          1A  B 18 60VALUES('N' 'Y')
     A                                 18 64'IBML1'
     A            ZEL1           1A  B 18 70VALUES('N' 'Y')
     A            ZEL1FI         2A  B 18 72
     A                                 19  6'Filbibliotek for produksjon  . . :'
     A            ZELIBF        10A  B 19 43RANGE('A         ' 'Z999999999')
     A                                 19 56'Navn'
     A                                 20  8'Filbibliotek for historikk . . :'
     A            ZELIBO        10A  B 20 43RANGE('A         ' 'Z999999999')
     A                                 20 56'Navn'
     A                                 21  6'Tømme segmentfiler hver natt . . :'
     A            ZCLRF          1A  B 21 43VALUES(' ' 'Y' 'N')
     A                                 21 56'Y/N'
     A                                 21 62'T.komponeter'
     A            ZTOTM          5Y 0O 21 75EDTCDE(4)
     A                                      COLOR(WHT)
     A                                 22  6'Bevare kopi av EDI i IFS mappe . :'
     A            ZIFSD          5Y 0B 22 43EDTCDE(Z)
     A                                 22 56'Dager'
     A                                 23  2MSGCON(010 EDI0803 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 15MSGCON(010 EDI0812 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
      *
     A          R EDI33DØ
     A                                      OVERLAY
     A            ZMSGTX        79A  O 24  2MSGID(&ZMSGNO SYMSGF)
     A N30                                  COLOR(WHT)
     A  30                                  COLOR(RED)
     A            ZMSGNO         7A  P
