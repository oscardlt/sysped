     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPDOC('IMFORE09B' 'HELPNU' 'HLP400-
     A                                      ')
     A                                      HELP
     A                                      ALTHELP
     A          R BILDE01
     A                                      CF03(03 'Avslutt')
     A                                      CF04(04 'SØKE')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/SYIS40 '
     A                                      COLOR(BLU)
     A                                  1 21MSGCON(050 TBR0173 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  2 72TIME
     A                                      COLOR(BLU)
     A                                  3 13MSGCON(010 TBR0174 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  5 13'Anmelder:'
     A            WSKNA          8Y 0B  5 29EDTCDE(4)
     A  31                                  DSPATR(RI)
     A                                  7 13MSGCON(015 TBR0176 *LIBL/SYMSGF)
     A            WSTRA          8Y 0B  7 29EDTCDE(4)
     A  31                                  DSPATR(RI)
     A                                  9 13'Ansvarlig part:'
     A            WSKNAN1        8Y 0B  9 29EDTCDE(4)
     A  31                                  DSPATR(RI)
     A            WSKNAN2        8Y 0B 10 29EDTCDE(4)
     A            WSKNAN3        8Y 0B 11 29EDTCDE(4)
     A                                 13 13'Posttype:'
     A            WSFPFO         5A  B 13 29
     A  31                                  DSPATR(RI)
     A                                 15 13'Fra reg.dato:'
     A            WSFDAT         6Y 0B 15 29EDTCDE(Y)
     A  32                                  DSPATR(RI)
     A                                 15 42'Til reg.dato:'
     A            WSTDAT         6Y 0B 15 56EDTCDE(Y)
     A  33                                  DSPATR(RI)
     A                                 17 13'Eller saksnr:'
     A            WSSAKN         7Y 0B 17 29EDTCDE(4)
     A                                 21 13'Printernavn:'
     A            WSPRT         10A  B 21 29
     A  34                                  DSPATR(RI)
     A                                 23  2MSGCON(010 TBR0013 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(010 DIV0032 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30        UTXT          62A  O 24  2COLOR(WHT)
     A                                 23 57MSGCON(020 TBR0078 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A            MSGNR          7A  P
