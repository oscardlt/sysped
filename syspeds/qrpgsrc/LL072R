********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
11XXX * 01 CB  PTF11 Extern tullid Sverige
********************************************************************
      ***********************************************************
      *** PLUKK BESTILLING FRA LØSNING1                       ***
      ***********************************************************
      *
     FISTATOL1  UF   E           K DISK
     FISTATOL2  IF   E           K DISK    RENAME(ISTATOLR:ISTATOLR2)
     FISTATOL3  IF   E           K DISK    RENAME(ISTATOLR:ISTATOLR3)
     FISTATOL4  IF   E           K DISK    RENAME(ISTATOLR:ISTATOLR4)
     FLLHEAD15  IF   E           K DISK
     FLLBL1F    IF   E           K DISK
     FCUNDL01   IF   E           K DISK
     FFIRM      IF   E           K DISK
     FLL072F    IF A E           K DISK    USROPN
     FLL072D    CF   E             WORKSTN
     F                                     INFDS(FBAREA)
     F                                     SFILE(SUBFIL:ZRECNO)
     D UAN             S              7    DIM(90)
     D UBA             S              4    DIM(400)
     D UBN             S              7    DIM(100)
     D UEN             S              7    DIM(100)
     D UEB             S             14    DIM(100)
     D UCMD            DS          1024
     D XCMD01          C                   CONST('CLRPFM -
     D                                     FILE(QTEMP/LL072F)')
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D                 DS
     D  JOTXT                  1     30
     D  XXTXT                  1     13
     D                 DS
     D  JCONR                  1     20
     D  XXCONR                 1     11
     D                 DS
     D  UJN                    1     50  0
     D                                     DIM(10)
     D  WSJN1                  1      5  0
     D  WSJN2                  6     10  0
     D  WSJN3                 11     15  0
     D  WSJN4                 16     20  0
     D  WSJN5                 21     25  0
     D  WSJN6                 26     30  0
     D  WSJN7                 31     35  0
     D  WSJN8                 36     40  0
     D  WSJN9                 41     45  0
     D  WSJNA                 46     50  0
     D SAVUDS         UDS
     D  UDSTXT               101    201
      *______________________
      * Initiering
      *""""""""""""""""""""""
     C                   EXSR      INIT
      *______________
      * Clear Subfil
      *""""""""""""""
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     0             ZRECNO
     C                   MOVE      ' '           WSNR
      *
     C                   SELECT
     C                   WHEN      WSVALG = 1
     C                   MOVEA     '1000'        *IN(41)
     C                   WHEN      WSVALG = 2
     C                   MOVEA     '0100'        *IN(41)
     C                   WHEN      WSVALG = 3
     C                   MOVEA     '0010'        *IN(41)
     C                   WHEN      WSVALG = 4
     C                   MOVEA     '0001'        *IN(41)
     C                   ENDSL
      *
     C   41*LOVAL        SETLL     ISTATOL1
     C   41              READ(N)   ISTATOL1                               94
     C   41              MOVE      *ZEROS        XJONR             5 0
     C   42*LOVAL        SETLL     ISTATOL2
     C   42              READ      ISTATOL2                               94
     C   42              MOVE      *ZEROS        XJEKSNR           7 0
     C   43*LOVAL        SETLL     ISTATOL3
     C   43              READ      ISTATOL3                               94
     C   43              MOVE      *ZEROS        XJBESTN           7 0
     C   44*LOVAL        SETLL     ISTATOL4
     C   44              READ      ISTATOL4                               94
     C   44              MOVE      *BLANKS       XJCONR           20
      *
     C     *IN94         DOWEQ     '0'
     C     WSVALG        IFEQ      1
     C     JONR          ANDNE     XJONR
     C     WSVALG        OREQ      2
     C     JEKSNR        ANDNE     XJEKSNR
     C     WSVALG        OREQ      3
     C     JBESTN        ANDNE     XJBESTN
     C     WSVALG        OREQ      4
     C     JCONR         ANDNE     XJCONR
     C                   EVAL      *IN51 = *OFF
     C                   ELSE
     C                   EVAL      *IN51 = *ON
     C                   END
     C                   ADD       1             ZRECNO
     C                   WRITE     SUBFIL
     C                   MOVE      JONR          XJONR
     C                   MOVE      JEKSNR        XJEKSNR
     C                   MOVE      JBESTN        XJBESTN
     C                   MOVE      JCONR         XJCONR
     C   41              READ(N)   ISTATOL1                               94
     C   42              READ      ISTATOL2                               94
     C   43              READ      ISTATOL3                               94
     C   44              READ      ISTATOL4                               94
     C  N94              ENDDO                                                  <DO 14
      *
     C     ZRECNO        IFGT      1
     C                   Z-ADD     1             ZRECNO
     C                   END
     C                   MOVE      WSVALG        XVALG             1 0
      *________________
      * Display Subfil
      *""""""""""""""""
     C     START3        TAG
     C  N30              Z-ADD     6             LINNBR
     C  N30              Z-ADD     2             POSNBR
     C                   MOVEA     '11'          *IN(91)
      * TOM SUBFIL....
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NOREC
     C                   ELSE                                                   <ZRECNO
     C                   WRITE     BUNN
     C                   EXFMT     SUBCTL
     C                   ENDIF                                                  <ZRECNO
      *
     C                   MOVE      '0'           *IN30
     C                   MOVEA     '00'          *IN(98)
      *
     C     WSVALG        CABNE     XVALG         START
      *
     C     *IN03         IFEQ      '1'
     C     *IN12         OREQ      '1'
     C                   MOVE      ' '           UFLAGG
     C                   GOTO      ENDPGM
     C                   ENDIF
      *** F5=FORNY
     C     *IN05         IFEQ      '1'
     C                   GOTO      START
     C                   ENDIF                                                  <*IN05
      *** Roll
     C     *IN25         IFEQ      '1'
     C                   MOVE      '0'           *IN25
     C                   GOTO      START3
     C                   ENDIF                                                  <*IN25
      *** Finn valg
     C     ZRECNO        IFNE      0
     C                   EXSR      GETOPT
     C     XSLT          IFEQ      'J'
     C     *IN12         OREQ      '1'
     C                   GOTO      START3
     C                   END
     C                   END
     C   98              GOTO      START3
      *
     C     XPLK          IFEQ      'J'
      * SORTE EKSPEDISJONSNR FORAN BESTILLINGSNR
     C                   Z-ADD     1024          UCMDL            15 5
     C                   MOVE      *BLANKS       UCMD
     C                   MOVEL     XCMD01        UCMD
     C                   CALL      'QCMDEXC'
     C                   PARM                    UCMD
     C                   PARM                    UCMDL
     C                   OPEN      LL072F
     C     1             DO        100           X1
     C                   IF        UBN(X1) = *ZEROS
     C                   LEAVE
     C                   END
     C                   MOVE      UBN(X1)       L72BNR
     C                   MOVE      UEN(X1)       L72ENR
     C                   WRITE     LL072FR
     C                   ENDDO
     C                   MOVE      *ZEROS        UEB
     C                   MOVE      *ZEROS        X1
     C     *LOVAL        SETLL     LL072F
     C                   READ      LL072F
     C                   DOW       *IN33 = *OFF
     C                   ADD       1             X1
     C                   MOVEL     L72ENR        UEB(X1)
     C                   MOVE      L72BNR        UEB(X1)
     C                   READ      LL072F
     C                   ENDDO
      *
     C                   CALL      'LL073C'
     C                   PARM                    UGRPA
     C                   PARM                    UPNRA
     C                   PARM                    JOFIRM
     C                   PARM                    UBA
     C                   PARM                    UEB
     C                   PARM                    UALN
     C                   PARM                    UTLAGER
     C                   END
      *
     C     *LOVAL        SETLL     LLBL1F
     C                   READ      LLBL1F                                 33
     C     *IN33         IFEQ      '0'
     C                   MOVE      UGRPA         UGRP2             8 0
     C                   MOVE      UPNRA         UPNR2             8 0
     C                   MOVE      BLMRNR        UMRNR2            5 0
     C                   MOVE      'E'           USPØR2            1
     C                   MOVE      BLMRNR        UMRNRA
     C                   CALL      'LL023R'
     C                   PARM                    UGRP2
     C                   PARM                    UPNR2
     C                   PARM                    UNKJE2           30
     C                   PARM                    UMRNR2
     C                   PARM                    USPØR2
     C                   CALL      'LL079C'
     C                   PARM                    UMRNRA
     C                   PARM                    UPNRA
     C                   END
      *
     C     ENDPGM        TAG
     C                   MOVE      '1'           *INLR
     C                   RETURN
      *
      ***********************************************
     C     GETOPT        BEGSR
      ***********************************************
     C                   MOVE      'N'           XSLT              1
     C                   MOVE      'N'           XPLK              1
     C                   MOVE      *ZEROS        UBA
     C                   MOVE      *ZEROS        UBN
     C                   MOVE      *ZEROS        UEN
     C                   MOVE      *ZEROS        X1                3 0
     C                   MOVEA     '00'          *IN(98)
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C     *IN98         IFEQ      '1'
     C                   LEAVE
     C                   END
      *
     C     WSNR          IFEQ      '1'
     C     JOTXT         ANDNE     '*DELETE'
     C     WSNR          OREQ      '9'
     C     JOTXT         ANDNE     '*DELETE'
     C                   IF        WSNR = '9'
     C                   MOVE      'J'           UTLAGER           1
     C                   END
     C                   MOVE      '1'           *IN99
      *
     C                   SELECT
     C                   WHEN      WSVALG = 1
     C     ISTT1K        SETLL     ISTATOL1
     C     ISTT1K        READE     ISTATOL1                               33
     C                   WHEN      WSVALG = 2
     C     ISTT2K        SETLL     ISTATOL2
     C     ISTT2K        READE     ISTATOL2                               33
     C                   WHEN      WSVALG = 3
     C                   MOVE      *OFF          *IN33
     C                   WHEN      WSVALG = 4
     C     ISTT4K        SETLL     ISTATOL4
     C     ISTT4K        READE     ISTATOL4                               33
     C                   ENDSL
      *
     C                   DOW       *IN33 = *OFF
     C     1             DO        X1            X2                3 0
     C                   MOVE      UBA(X2)       XXAAR             4 0
     C                   MOVE      UBN(X2)       XXBESTN           7 0
     C                   IF        ((JBESAA = XXAAR) AND (JBESTN = XXBESTN))
     C                   GOTO      STNESTE
     C                   END
     C                   ENDDO
     C                   ADD       1             X1
     C                   MOVE      JBESAA        UBA(X1)
     C                   MOVE      JBESTN        UBN(X1)
     C                   MOVE      JEKSNR        UEN(X1)
     C     STNESTE       TAG
     C                   SELECT
     C                   WHEN      WSVALG = 1
     C     ISTT1K        READE     ISTATOL1                               33
     C                   WHEN      WSVALG = 2
     C     ISTT2K        READE     ISTATOL2                               33
     C                   WHEN      WSVALG = 3
     C                   MOVE      *ON           *IN33
     C                   WHEN      WSVALG = 4
     C     ISTT4K        READE     ISTATOL4                               33
     C                   ENDSL
     C                   ENDDO
      *
     C                   MOVE      'J'           XPLK              1
     C                   MOVE      ' '           WSNR
     C                   UPDATE    SUBFIL
     C                   END
      *
     C     WSNR          IFEQ      '4'
     C     JOTXT         ANDNE     '*DELETE'
     C                   MOVE      '1'           *IN99
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C     ISTL1K        CHAIN     ISTATOL1                           33
     C                   MOVE      'S'           JOSTAT
     C                   MOVE      WDT           JOBDAT
     C                   MOVE      WTM           JOBTID
     C                   UPDATE    ISTATOLR
     C                   MOVE      ' '           WSNR
     C                   MOVE      *BLANKS       JOTXT
     C                   MOVEL     '*DELETE'     JOTXT
     C                   UPDATE    SUBFIL
     C                   MOVE      'J'           XSLT
     C                   END
      *
     C                   ENDDO
      *
     C     SLOPT         TAG
     C     *IN99         IFEQ      '1'
     C                   MOVE      '0'           *IN98
     C                   END
      *
     C                   ENDSR
      *
      ***********************************************
     C     INIT          BEGSR
      ***********************************************
     C     *ENTRY        PLIST
     C                   PARM                    UGRPA             8
     C                   PARM                    UPNRA             8
     C                   PARM                    UMRNRA            5
     C                   PARM                    UALN              7
     C                   PARM                    UFLAGG            1
      *
     C     *LOVAL        SETLL     FIRM
     C                   READ      FIRM                                   33
      *
     C     UMRNRA        IFNE      *ZEROS
     C                   MOVE      UMRNRA        XXMRNR            5 0
     C     XXMRNR        CHAIN     LLBL1F                             33
     C                   READ      LLBL1F                                 33
     C     *IN33         IFEQ      '0'
     C                   MOVE      UGRPA         UGRP2             8 0
     C                   MOVE      UPNRA         UPNR2             8 0
     C                   MOVE      BLMRNR        UMRNR2            5 0
     C                   MOVE      'E'           USPØR2            1
     C                   MOVE      BLMRNR        UMRNRA
     C                   CALL      'LL023R'
     C                   PARM                    UGRP2
     C                   PARM                    UPNR2
     C                   PARM                    UNKJE2           30
     C                   PARM                    UMRNR2
     C                   PARM                    USPØR2
     C                   CALL      'LL079C'
     C                   PARM                    UMRNRA
     C                   PARM                    UPNRA
     C                   ELSE
     C                   MOVE      *ZEROS        UMRNRA
     C                   MOVE      ' '           UFLAGG
     C     UALN          IFNE      *BLANKS
     C                   EXSR      TILOPD
     C                   END
     C                   END
     C                   MOVE      '1'           *INLR
     C                   RETURN
     C                   END
      *
     C                   MOVE      UPNRA         XXKJED
     C                   Z-ADD     2             WSVALG
      *
     C     KUNKEY        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    XXKJED
     C     ISTL1K        KLIST
     C                   KFLD                    JOFIRM
     C                   KFLD                    JOAAR
     C                   KFLD                    JONR
     C     ISTT1K        KLIST
     C                   KFLD                    JOFIRM
     C                   KFLD                    JOAAR
     C                   KFLD                    JONR
     C     ISTT2K        KLIST
     C                   KFLD                    JOFIRM
     C                   KFLD                    JEKSNR
     C     ISTT4K        KLIST
     C                   KFLD                    JOFIRM
     C                   KFLD                    JCONR
      *
     C     KUNKEY        CHAIN     CUNDL01                            33
      *
     C                   ENDSR
      *
      ***********************************************
     C     TILOPD        BEGSR
      ***********************************************
     C                   MOVE      'A'           UMOD              1
     C                   MOVE      '0001'        UAVD              4
     C                   MOVE      'EDI'         USG               3
     C                   MOVE      UALN          UAN(1)
     C                   MOVE      'N'           UFBR              1
     C                   CALL      'LL017R'
     C                   PARM                    FIFIRM
     C                   PARM                    UMOD
     C                   PARM                    UAVD
     C                   PARM                    USG
     C                   PARM                    UAN
     C                   PARM                    UFBR
     C                   PARM                    UPAFF             1
     C                   PARM                    UPFAK             1
     C                   PARM                    UPFKØ             1
N01  C                   PARM                    UTUID            10
      *
     C                   EXSR      TILSAD
      *
     C                   ENDSR
      *
      ***********************************************
     C     TILSAD        BEGSR
      ***********************************************
      * LAGE SAD-IMPORT
     C                   MOVE      UGRPA         UGRP2             8 0
     C                   MOVE      UPNRA         UPNR2             8 0
     C                   MOVE      UALN          XALN              7 0
     C     LHE15K        KLIST
     C                   KFLD                    UGRP2
     C                   KFLD                    UPNR2
     C                   KFLD                    XALN
     C     LHE15K        CHAIN     LLHEAD15                           33
     C                   Z-ADD     1             UAVD2             4 0
     C                   MOVE      LHOPD         UTDN2             7 0
     C                   CALL      'LL078R'
     C                   PARM                    FIFIRM
     C                   PARM                    UGRPA
     C                   PARM                    UPNRA
     C                   PARM                    UAVD2
     C                   PARM                    UTDN2
     C                   PARM                    UALN
      *
     C     *DTAARA       DEFINE    *LDA          SAVUDS
     C                   IN        SAVUDS
     C                   MOVE      *BLANKS       UDSTXT
     C                   OUT       SAVUDS
     C                   MOVE      'CHG'         UTYPE             3
     C                   CALL      'SAD061CL'
     C                   PARM                    UAVD2
     C                   PARM                    UTDN2
     C                   PARM                    USG
     C                   PARM                    UTYPE
     C                   PARM                    UMODE             1
      *
     C                   ENDSR
      *
