      *
      ***********************************************************
      *** PROGRAM SKAL VEDLIKEHOLDE KURERMANIFEST             ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01 02 07 09-14 17-24             ***
      *** LEDIGE INDIKATORER 29 31 32       52-75 78-89       ***
      ***********************************************************
      *
     FSADKML01  IF   E           K DISK    RENAME(SADKMFR:SADKMFR01)
     FSADKML02  IF   E           K DISK    RENAME(SADKMFR:SADKMFR02)
     FSADKML03  IF   E           K DISK    RENAME(SADKMFR:SADKMFR03)
     FSADKML04  IF   E           K DISK    RENAME(SADKMFR:SADKMFR04)
     FSADKML05  IF   E           K DISK    RENAME(SADKMFR:SADKMFR05)
     FSADKMFOPD UF   E           K DISK
     FSADKMCL1  IF   E           K DISK
     FECMSG1    IF   E           K DISK
     FKODTA     IF   E           K DISK
     FCUNDL01   IF   E           K DISK
     FFIRM      IF   E           K DISK
     FSAD110D   CF   E             WORKSTN SFILE(SUBFIL:ZRECNO)
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D                SDS
     D  UUSR                 254    263
     D                 DS
     D  WSATDDD                1      2  0
     D  WSATDDM                3      4  0
     D  WSATDDA                5      6  0
     D  WSATDD                 1      6  0
     D                 DS
     D  XSATDDA                1      4  0
     D  XSATDDM                5      6  0
     D  XSATDDD                7      8  0
     D  XSATDD                 1      8  0
     D                 DS
     D  W2ATDDD                1      2  0
     D  W2ATDDM                3      4  0
     D  W2ATDDA                5      6  0
     D  W2ATDD                 1      6  0
     D                 DS
     D  X2ATDDA                1      4  0
     D  X2ATDDM                5      6  0
     D  X2ATDDD                7      8  0
     D  X2ATDD                 1      8  0
     D                 DS
     D  KMATDDA                1      4  0
     D  KMATDDM                5      6  0
     D  KMATDDD                7      8  0
     D  KMATDD                 1      8  0
     D                 DS
     D  X_KMATDDD              1      2  0
     D  X_KMATDDM              3      4  0
     D  X_KMATDDA              5      6  0
     D  X_KMATDD               1      6  0
     D                 DS
     D  WSDTRD                 1      2  0
     D  WSDTRM                 3      4  0
     D  WSDTRA                 5      6  0
     D  WSDTR                  1      6  0
     D                 DS
     D  XSDTRA                 1      4  0
     D  XSDTRM                 5      6  0
     D  XSDTRD                 7      8  0
     D  XSDTR                  1      8  0
     D                 DS
     D  W2DTRD                 1      2  0
     D  W2DTRM                 3      4  0
     D  W2DTRA                 5      6  0
     D  W2DTR                  1      6  0
     D                 DS
     D  X2DTRA                 1      4  0
     D  X2DTRM                 5      6  0
     D  X2DTRD                 7      8  0
     D  X2DTR                  1      8  0
     D                 DS
     D  KMDTRA                 1      4  0
     D  KMDTRM                 5      6  0
     D  KMDTRD                 7      8  0
     D  KMDTR                  1      8  0
     D                 DS
     D  X_KMDTRD               1      2  0
     D  X_KMDTRM               3      4  0
     D  X_KMDTRA               5      6  0
     D  X_KMDTR                1      6  0
     D                UDS
     D  UFIRM                  1      2
     D  UAVD                  15     18  0
     D  USG                   19     21
      *
     C                   EXSR      INIT
     C                   GOTO      START
      *
     C     START0        TAG
     C                   EXFMT     BILDE01
     C                   MOVEA     '00'          *IN(98)
     C                   SETOFF                                       30
      *
     C   03              GOTO      SLUTT
     C   04              DO
     C                   EXSR      SØKKUN
     C                   GOTO      START0
     C                   END
      *
     C                   EXSR      TESTB1
     C   30              GOTO      START0
      *
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C                   EXSR      SETKEY
     C     START1        TAG
     C                   EXSR      LESREC
      *
     C     START2        TAG
      *
     C  N94              DO
     C                   DO        19
     C                   MOVE      KMATDDD       X_KMATDDD
     C                   MOVE      KMATDDM       X_KMATDDM
     C                   MOVE      KMATDDA       X_KMATDDA
     C                   MOVE      KMDTRD        X_KMDTRD
     C                   MOVE      KMDTRM        X_KMDTRM
     C                   MOVE      KMDTRA        X_KMDTRA
     C     KMPRO         CHAIN     SADKMCL1                           33
     C   33              EVAL      MCNAS = *BLANKS
     C                   ADD       1             ZRECNO
     C                   WRITE     SUBFIL
     C                   MOVE      ZRECNO        XRECN2            5 0
     C                   EXSR      LESREC
     C  N94              END
     C                   END
      *
     C                   MOVE      ZRECNO        XRECNO            5 0
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C                   WRITE     BUNN
     C     ZRECNO        IFEQ      0
     C  N27              DO
     C                   MOVE      '1'           *IN27
     C                   GOTO      START0
     C                   ENDDO
     C                   EXFMT     NYVARE
     C                   ELSE
     C                   EXFMT     SUBCTL
     C                   END
      *
      *** AVSLUTT
     C                   MOVE      '0'           *IN30
     C                   MOVEA     '00'          *IN(98)
     C   03              GOTO      SLUTT
     C   05              GOTO      START
     C     START4        TAG
     C   06              DO
     C                   EXSR      CRTTUR
     C                   GOTO      START3
     C                   ENDDO
     C   08
     COR 12              EXSR      SETCUR
     C   08
     COR 12              GOTO      START0
      *
      *** BLA VIDERE
     C   25              DO
     C                   Z-ADD     XRECNO        ZRECNO
     C                   SETOFF                                       25
     C                   GOTO      START2
     C                   ENDDO
      *
     C     W2NR          IFNE      0                                            1<-B
     C     W2PRO         ORNE      0
     C     W2UUID        ORNE      *BLANKS
     C     W2ATDD        ORNE      0
     C     W2ST          ORNE      *BLANKS
     C     W2DTR         ORNE      0
     C     W2NR          IFEQ      0                                             2<-B
     C                   EXSR      NYSØK
     C                   GOTO      START
     C                   ELSE                                                    2
     C     W2NR          COMP      1                                      06
     C   06              DO                                                       3<-B
     C     W2AVD         IFNE      0                                               4<-B
     C                   MOVE      W2AVD         WSAVD
     C                   END                                                       4<-E
     C                   MOVE      *ZEROS        W2NR
     C                   MOVE      *ZEROS        W2AVD
     C                   GOTO      START4
     C                   ENDDO                                                    3<-E
     C     W2PRO         IFEQ      0                                              3<-B
     C                   SETON                                        9830
     C                   MOVE      'YBC2034'     MSGNR
     C                   ELSE                                                     3
     C                   EXSR      NYTUR
     C                   END                                                      3<-E
     C                   MOVE      *ZEROS        W2PRO
     C                   MOVE      *BLANKS       W2UUID           36
     C                   MOVE      *ZEROS        W2ATDD
     C                   MOVE      *BLANKS       W2ST
     C                   MOVE      *ZEROS        W2DTR
     C                   GOTO      START3
     C                   END                                                     2<-E
     C                   END                                                    1<-E
      *
     C     ZRECNO        IFNE      0
     C                   EXSR      HENT
     C                   END
     C                   GOTO      START3
      *
     C     SLUTT         TAG
     C     WSAVD         IFNE      0
     C                   MOVE      WSAVD         UAVD
     C                   END
      *
     C                   SETON                                        LR
     C                   RETURN
      *
     C***********************************************
     C     NYSØK         BEGSR
     C***********************************************
     C                   CLEAR                   BILDE01
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
      *
     C     W2AVD         IFEQ      0
     C                   MOVE      UAVD          WSAVD
     C                   ELSE
     C                   MOVE      W2AVD         WSAVD
     C                   END
     C     W2PRO         IFNE      0                                            1<-B
     C                   MOVE      W2PRO         WSPRO
     C                   ELSE                                                   1
     C     W2UUID        IFNE      *BLANKS                                       2<-B
     C                   MOVE      W2UUID        WSUUID
     C                   ELSE                                                    2
     C     W2ATDD        IFNE      0                                              3<-B
     C                   MOVE      W2ATDD        WSATDD
     C                   ELSE                                                     3
     C     W2ST          IFNE      *BLANKS                                         4<-B
     C                   MOVE      W2ST          WSST
     C                   ELSE                                                      4
     C     W2DTR         IFNE      0                                                5<-B
     C                   MOVE      W2DTR         WSDTR
     C                   END                                                        5<-E
     C                   END                                                       4<-E
     C                   END                                                      3<-E
     C                   END                                                     2<-E
     C                   END                                                    1<-E
      *
     C                   MOVE      *ZEROS        W2NR
     C                   MOVE      *ZEROS        W2AVD
     C                   MOVE      *ZEROS        W2PRO
     C                   MOVE      *BLANKS       W2UUID
     C                   MOVE      *ZEROS        W2ATDD
     C                   MOVE      *BLANKS       W2ST
     C                   MOVE      *ZEROS        W2DTR
      *
     C                   MOVEA     '00'          *IN(98)
     C                   EXSR      TESTB1
      *
     C                   ENDSR
      *
     C***********************************************
     C     NYTUR         BEGSR
     C***********************************************
     C     W2AVD         IFEQ      0
     C                   MOVE      UAVD          W2AVD
     C                   END
     C     SADKML01K1    CHAIN     SADKML01                           98
     C   98              DO
     C                   MOVE      'YBC2035'     MSGNR
     C                   GOTO      SLTUR
     C                   END
     C                   MOVE      '1'           *IN90
      *
     C                   SELECT
     C     W2NR          WHENEQ    2
      * kan ikke gå inn på normale oppdrag fra dummy
     C                   IF        KMST <> ' ' AND KMST <> 'C' AND KMST <> 'M'
     C                   MOVE      '1'           *IN30
     C                   MOVE      'YBC2036'     MSGNR
     C                   GOTO      SLTUR
     C                   END
     C                   EXSR      CHGTUR
     C     W2NR          WHENEQ    3
     C                   EXSR      CPYTUR
     C     W2NR          WHENEQ    5
     C                   EXSR      DSPTUR
     C     W2NR          WHENEQ    9
     C                   EXSR      DSPLOG
     C                   ENDSL
      *
     C     SLTUR         TAG
     C   98              MOVE      '1'           *IN30
     C                   MOVE      '0'           *IN90
     C                   MOVE      *ZEROS        W2NR
     C  N98              MOVE      *ZEROS        W2AVD
     C  N98              MOVE      *ZEROS        W2PRO
      *
     C                   ENDSR
      *
     C***********************************************
     C     HENT          BEGSR
     C***********************************************
     C                   MOVE      *BLANKS       MSGNR
     C                   MOVEA     '000'         *IN(90)
     C                   MOVEA     '00'          *IN(98)
     C     *IN98         DOWEQ     '0'                                          1<-B
     C                   READC     SUBFIL                                 98
     C     *IN98         IFEQ      '0'                                           2<-B
     C                   SELECT                                                   3<-B
     C     WSNR          WHENEQ    2                                              3
     C                   MOVE      '1'           *IN99
     C                   IF        KMST <> ' ' AND KMST <> 'C' AND KMST <> 'M'
     C                   MOVE      '1'           *IN30
     C                   MOVE      'YBC2036'     MSGNR
     C                   MOVE      *ZEROS        WSNR
     C                   UPDATE    SUBFIL
     C                   GOTO      SLHENT
     C                   END
     C                   EXSR      CHGTUR
     C     WSNR          WHENEQ    3                                              3
     C                   MOVE      '1'           *IN99
     C                   EXSR      CPYTUR
     C     WSNR          WHENEQ    5                                              3
     C                   MOVE      '1'           *IN99
     C                   EXSR      DSPTUR
     C     WSNR          WHENEQ    9                                              3
     C                   MOVE      '1'           *IN99
     C                   EXSR      DSPLOG
     C     WSNR          WHENEQ    90                                             3
     C                   IF        KMST <> ' ' AND KMST <> 'C' AND KMST <> 'M'
     C                   MOVE      '1'           *IN30
     C                   MOVE      'YBC2036'     MSGNR
     C                   GOTO      SLHENT
     C                   END
     C                   MOVE      '1'           *IN99
     C                   EXSR      SNDTUR
     C                   ENDSL                                                    3<-E
     C                   END                                                     2<-E
     C                   ENDDO                                                  1<-E
      *
     C     SLHENT        TAG
     C   98
     CANN99              DO
     C                   MOVE      'DIV0012'     MSGNR
     C                   MOVE      '1'           *IN30
     C                   END
     C                   MOVE      '0'           *IN99
      *
     C                   ENDSR
      *
     C***********************************************
     C     SØKKUN        BEGSR
     C***********************************************
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
      *
     C                   ENDSR
      *
      ***********************************************
     C     SETCUR        BEGSR
      ***********************************************
     C                   Z-ADD     35            POSNBR
     C                   SELECT
     C                   WHEN      WSPRO  <> 0
     C                   Z-ADD     8             LINNBR
     C                   WHEN      WSUUID <> *BLANKS
     C                   Z-ADD     9             LINNBR
     C                   WHEN      WSATDD <> 0
     C                   Z-ADD     10            LINNBR
     C                   WHEN      WSDTR <> 0
     C                   Z-ADD     11            LINNBR
     C                   WHEN      WSST   <> *BLANKS
     C                   Z-ADD     12            LINNBR
     C                   OTHER
     C                   Z-ADD     8             LINNBR
     C                   ENDSL
      *
     C                   ENDSR
      *
     C***********************************************
     C     DSPTUR        BEGSR
     C***********************************************
     C                   MOVE      'DSP'         XTYPE                          SPØRRING
     C                   CALL      'SAD111R'
     C                   PARM      KMAVD         U_AVD             4 0
     C                   PARM      KMPRO         U_PRO             8 0
     C                   PARM                    USG
     C                   PARM                    XTYPE             3
     C                   PARM                    UFLAGG            1 0
     C     UFLAGG        IFEQ      3
     C                   MOVE      '1'           *INLR
     C                   RETURN
     C                   END
     C  N90              MOVE      *ZEROS        WSNR
     C  N90              UPDATE    SUBFIL
      *
     C                   ENDSR
      *
     C***********************************************
     C     CHGTUR        BEGSR
     C***********************************************
     C                   MOVE      'CHG'         XTYPE             3
     C                   CALL      'SAD111R'
     C                   PARM      KMAVD         U_AVD             4 0
     C                   PARM      KMPRO         U_PRO             8 0
     C                   PARM                    USG
     C                   PARM                    XTYPE
     C                   PARM                    UFLAGG            1 0
      *
     C                   MOVE      KMST          X_KMST            1
     C     SADKMFOPDK    CHAIN     SADKMFOPD                          33
     C                   EVAL      KMST = X_KMST
     C                   UPDATE    SADKMFR
     C                   MOVE      KMATDDD       X_KMATDDD
     C                   MOVE      KMATDDM       X_KMATDDM
     C                   MOVE      KMATDDA       X_KMATDDA
     C                   MOVE      KMDTRD        X_KMDTRD
     C                   MOVE      KMDTRM        X_KMDTRM
     C                   MOVE      KMDTRA        X_KMDTRA
     C     KMPRO         CHAIN     SADKMCL1                           33
     C   33              EVAL      MCNAS = *BLANKS
     C  N90              MOVE      *ZEROS        WSNR
     C  N90              UPDATE    SUBFIL
      *
     C                   ENDSR
      *
     C***********************************************
     C     CPYTUR        BEGSR
     C***********************************************
     C                   MOVE      'CPY'         XTYPE             3
     C                   CALL      'SAD111R'
     C                   PARM      KMAVD         U_AVD             4 0
     C                   PARM      KMPRO         U_PRO             8 0
     C                   PARM                    USG
     C                   PARM                    XTYPE
     C                   PARM                    UFLAGG            1 0
     C  N90              MOVE      *ZEROS        WSNR
     C  N90              UPDATE    SUBFIL
     C     U_PRO         IFNE      0
     C     U_PRO         ANDNE     KMPRO
     C     SADKMFOPDK    CHAIN(N)  SADKMFOPD                          33
     C                   MOVE      KMATDDD       X_KMATDDD
     C                   MOVE      KMATDDM       X_KMATDDM
     C                   MOVE      KMATDDA       X_KMATDDA
     C                   MOVE      KMDTRD        X_KMDTRD
     C                   MOVE      KMDTRM        X_KMDTRM
     C                   MOVE      KMDTRA        X_KMDTRA
     C     KMPRO         CHAIN     SADKMCL1                           33
     C   33              EVAL      MCNAS = *BLANKS
     C                   ADD       1             XRECN2
     C                   MOVE      XRECN2        ZRECNO
     C                   WRITE     SUBFIL
     C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     CRTTUR        BEGSR
     C***********************************************
     C                   MOVE      'CRT'         XTYPE             3
     C                   IF        WSAVD = 0
     C                   MOVE      UAVD          U_AVD
     C                   ELSE
     C                   MOVE      WSAVD         U_AVD
     C                   END
     C                   MOVE      *ZEROS        U_PRO
     C                   CALL      'SAD111R'
     C                   PARM                    U_AVD             4 0
     C                   PARM                    U_PRO             8 0
     C                   PARM                    USG
     C                   PARM                    XTYPE
     C                   PARM                    UFLAGG            1 0
     C     U_PRO         IFNE      0
     C     SADKMFOPDK    CHAIN(N)  SADKMFOPD                          33
     C  N33              DO
     C                   IF        U_AVD <> 0
     C                   MOVE      U_AVD         UAVD
     C                   ELSE
     C                   MOVE      U_AVD         WSAVD
     C                   END
     C                   MOVE      KMATDDD       X_KMATDDD
     C                   MOVE      KMATDDM       X_KMATDDM
     C                   MOVE      KMATDDA       X_KMATDDA
     C                   MOVE      KMDTRD        X_KMDTRD
     C                   MOVE      KMDTRM        X_KMDTRM
     C                   MOVE      KMDTRA        X_KMDTRA
     C     KMPRO         CHAIN     SADKMCL1                           33
     C   33              EVAL      MCNAS = *BLANKS
     C                   ADD       1             XRECN2
     C                   MOVE      XRECN2        ZRECNO
     C                   WRITE     SUBFIL
     C                   ENDDO
     C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     DSPLOG        BEGSR
     C***********************************************
     C                   MOVE      '7'           WIN03             1
     C                   MOVE      *ZEROS        XAN11            11
     C                   MOVE      KMPRO         XAN11
     C                   MOVEL     XAN11         U_AVD
     C                   MOVE      XAN11         U_TDN
     C                   CALL      'SAD003'
     C                   PARM                    U_AVD             4 0
     C                   PARM                    U_TDN             7 0
     C                   PARM                    WIN03
     C     WIN03         IFEQ      '1'
     C                   MOVE      '1'           *INLR
     C                   RETURN
     C                   END
     C  N90              MOVE      *ZEROS        WSNR
     C  N90              UPDATE    SUBFIL
      *
     C                   ENDSR
      *
     C***********************************************
     C     SNDTUR        BEGSR
     C***********************************************
     C                   IF        KMST = ' ' OR KMST = 'C' OR KMST = 'M'
     C     ECMSG1K       CHAIN     ECMSG1                             33
     C                   IF        *IN33
     C                   EXFMT     ERRMSG
     C                   ELSE
     C                   MOVE      KMPRO         U_PRO_8           8
     C                   CALL      'SADKMJSON'
     C                   PARM                    U_PRO_8
      *
     C     SADKMFOPDK2   CHAIN     SADKMFOPD                          33
     C                   EVAL      KMST  = 'B'
     C                   EVAL      KMERR = *BLANKS
     C                   UPDATE    SADKMFR
     C                   END
     C                   END
      *
     C  N90              MOVE      *ZEROS        WSNR
     C  N90              UPDATE    SUBFIL
      *
     C                   ENDSR
      *
     C***********************************************
     C     TESTB1        BEGSR
     C***********************************************
     C                   MOVE      *BLANKS       KOANVN
     C     WSAVD         IFNE      0
     C     KODTAK        CHAIN     KODTA                              99
     C   99              Z-ADD     4             LINNBR
     C   99              Z-ADD     26            POSNBR
     C   99              MOVE      'SPA0001'     MSGNR
     C   99              SETON                                        30
     C   99              GOTO      SLTB1
     C                   MOVE      WSAVD         UAVD
     C     CUNDL01K      CHAIN     CUNDL01                            33
     C                   END
      *
     C                   MOVEA     '00000'       *IN(41)
      *
     C     WSPRO         COMP      *ZEROS                             4141
     C   41              GOTO      STTB1
     C     WSUUID        COMP      *BLANKS                            4242
     C   42              GOTO      STTB1
     C     WSATDD        COMP      *ZEROS                             4343
     C   43              GOTO      STTB1
     C     WSDTR         COMP      *ZEROS                             4444
     C   44              GOTO      STTB1
     C     WSST          COMP      *BLANKS                            4545
     C   45              GOTO      STTB1
      *
     C                   MOVE      'DIV0064'     MSGNR
     C                   SETON                                        3098
     C                   Z-ADD     8             LINNBR
     C                   Z-ADD     35            POSNBR
     C                   GOTO      SLTB1
      *
     C     STTB1         TAG
      *
     C     SLTB1         ENDSR
      *
     C***********************************************
     C     SETKEY        BEGSR
     C***********************************************
     C   41              DO
     C                   END
     C   42              DO
     C                   END
     C   43              DO
     C                   IF        WSATDD = 999999
     C                   EVAL      XSATDD = 99999999
     C                   ELSE
     C                   MOVE      WSATDDD       XSATDDD
     C                   MOVE      WSATDDM       XSATDDM
     C     WSATDDA       ADD       2000          XSATDDA
     C                   END
     C                   END
     C   44              DO
     C                   IF        WSDTR = 999999
     C                   EVAL      XSDTR = 99999999
     C                   ELSE
     C                   MOVE      WSDTRD        XSDTRD
     C                   MOVE      WSDTRM        XSDTRM
     C     WSDTRA        ADD       2000          XSDTRA
     C                   END
     C                   END
     C   45              DO
     C                   END
      *
     C                   EXSR      MINTUR
      *
     C                   ENDSR
      *
     C***********************************************
     C     MINTUR        BEGSR
     C***********************************************
      * SET KEY NÅR SØK KUN I EN !!!!  AVDELING
     C   41              IF        WSAVD = 0
     C     WSPRO         SETLL     SADKML02
     C                   ELSE
     C     SADKML01K2    SETLL     SADKML01
     C                   END
     C   42WSUUID        SETLL     SADKML03
     C   43XSATDD        SETLL     SADKML04
     C   44XSDTR         SETLL     SADKML05
     C*  45SADKML0XK     SETLL     SADKML0X
      *
     C                   ENDSR
      *
     C***********************************************
     C     LESREC        BEGSR
     C***********************************************
     C   41              IF        WSAVD = 0
     C                   READ      SADKML02                               94
     C                   ELSE
     C     WSAVD         READE     SADKML01                               94
     C                   END
     C   42              READ      SADKML03                               94
     C   43              IF        XSATDD = 99999999
     C                   READP     SADKML04                               94
     C                   ELSE
     C                   READ      SADKML04                               94
     C                   END
     C   44              IF        XSDTR = 99999999
     C                   READP     SADKML05                               94
     C                   ELSE
     C                   READ      SADKML05                               94
     C                   END
     C*  45              READ      SADKML0X                               94
      *
     C                   ENDSR
      *
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     SADKML01K1    KLIST
     C                   KFLD                    WSAVD
     C                   KFLD                    W2PRO
     C     SADKML01K2    KLIST
     C                   KFLD                    WSAVD
     C                   KFLD                    WSPRO
     C     SADKMFOPDK    KLIST
     C                   KFLD                    U_AVD
     C                   KFLD                    U_PRO
     C     SADKMFOPDK2   KLIST
     C                   KFLD                    KMAVD
     C                   KFLD                    KMPRO
     C     ECMSG1K       KLIST
     C                   KFLD                    ECSR
     C                   KFLD                    EC0065
     C     CUNDL01K      KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    KOAKNR
     C     KODTAK        KLIST
     C                   KFLD                    KOAUNI
     C                   KFLD                    WSAVD
      *
     C                   MOVE      *ZEROS        WSPRO
     C                   MOVE      *ZEROS        WSATDD
     C                   EVAL      ECSR   = 'S'
     C                   EVAL      EC0065 = 'KURERM'
     C                   MOVE      'A'           KOAUNI
      *
     C     FIFIRM        SETLL     FIRM                               33
     C                   READ      FIRM                                   33
     C                   MOVE      UAVD          WSAVD
     C     KODTAK        CHAIN     KODTA                              33
     C     CUNDL01K      CHAIN     CUNDL01                            33
      *
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVEA     '00010'       *IN(41)
     C                   Z-ADD     0             WSATDD
     C                   Z-ADD     999999        WSDTR
     C                   Z-ADD     0             W2ATDD
     C                   Z-ADD     0             W2DTR
      *
     C                   ENDSR
      *
