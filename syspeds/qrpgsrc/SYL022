      *********************************************************************
      * ARBEID MED MOTTAKSRAPPORT (HEADING)
      * PROGRAM BEHANDLER INPUT FRA SYL022 OG VISER UTVALGT HEADLING
      *
CRTPG+*  CRTPGM SYSPED/SYL022 MODULE(*LIBL/SYL022
CRTPGM*        *LIBL/CHECKUSR *LIBL/INCGI) ACTGRP(CGI) AUT(*USE)
      *
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 YBC PTF10 GODSNR OG MRN-NR
********************************************************************
      *
      *********************************************************************
      * MAIN PROGRAM FLOW
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
     FCUNDL01   IF   E           K DISK    USROPN
     FLLMRF     IF   E           K DISK    USROPN
     FLLVADR1   IF   E           K DISK    USROPN
     FLLVADR3   IF   E           K DISK    USROPN
     F                                     RENAME(LLVADRR:LLVADRR3)
     FLLGRPK05  if   e           k disk    USROPN
     FBRIDF     UF   E           K DISK    USROPN
     FBRPARF    UF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Prototype definitions and standard system API error structure
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      * Variables common to CGI programs
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
      * VARIABLES SPECIFIC TO THIS PROGRAM
      *--------------------------------------------------------------------
      * Variables received from the input string
     D lng             s              2
     D BckGnd          s             10
     D UsrSpcName      s             10
      * Variables used to load the external html source member
     D Lib             s             10
     D Fn              s             10
     D Mbr             s             10
      *
     D Spaces          s             12a   inz('&nbsp;&nbsp;')
     D XXUSER          S             10
     D XXMRNR          S              5
     D XXLNR           S             17
     D XXENDRE         S              1
     D PGM             C                   CONST('SYSPED/CHECKUSR')
     D                 DS
     D  MRLPN                  1      4  0
     D  MRLPNAA                1      4
     D                 DS
     D  VADRN3                 1     30
     D  VXPNR                  1      4
     D  VXBLAN                 5      5
     D  VXSTED                 6     30
      *
      *--------------------------------------------------------------------
      * Prolog common to CGI programs
      * -Receive input from the remote browser
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prolog3
      *=====================================================================
      * MAIN PROCESSING LINE
      *=====================================================================
      * Get program start time for calculating execution time
     C                   time                    timedata1
      * Parse input string
     C                   exsr      Parse
      * Initier
     C                   EXSR      INIT
     C                   CALLB     PGM
     C                   PARM                    BIBRID
     C                   PARM                    UsrSpcName
     C                   PARM                    UINN              1
     C                   IF        UINN = 'N'
     C                   GOTO      SLUTT
     C                   END
     C     XXUSER        CHAIN     BRIDF                              50
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVE      WDT           BIDTF
     C                   MOVE      WTM           BITIDF
     C                   UPDATE    BRIDFR
      * Set output skeleton html member name
     C                   exsr      SetSkl
      * Read skeleton output html into core
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
      * Retrieve environment variables
     C                   exsr      RtvEnvVar
      *
      * Issue html section /$top
     C                   exsr      SetTop
     C                   callp     wrtsection('top')
      *------------------
      * Retrieve number of boats to be shown
      *
     C     LLMRFK        CHAIN     LLMRF                              60
     C                   IF        *IN60
     C     MRMRNR        CABNE     0             SLUTT
     C                   CLEAR                   LLMRFR
     C                   MOVE      WDT           MRDT
     C                   END
     C                   exsr      SetFound
     C                   callp     wrtsection('found')
      * Hent Leverandører
     C                   exsr      HENTLEV
      * Send output buffer and quit
     C     SLUTT         TAG
     C                   exsr      Exit
      *=====================================================================
      * Send output buffer and quit
      *=====================================================================
     C     Exit          begsr
      * Compute and display run time
     C                   time                    timedata2
     C     timedata2     subdur    timedata1     ms:*ms
     C                   eval      sec = ms / 1000000
     C                   callp     updhtmlvar('runtime':
     C                             %trim(%editw(sec:'     0 .   ')))
     C                   callp     wrtsection('endhtml')
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      *
     C                   close     BRIDF
     C                   close     BRPARF
     C                   close     CUNDL01
     C                   close     LLMRF
     C                   close     LLVADR1
     C                   close     LLVADR3
     C                   close     LLGRPK05
     C                   eval      *inlr = *on
      *
     C                   return
     C                   endsr
      *=====================================================================
      * Parse input string
      *=====================================================================
     C     Parse         begsr
     C                   eval      lng     = zhbgetvar('lng')
     C                   eval      BckGnd  = zhbgetvar('bckgnd')
     C                   EVAL      XXUSER  = zhbgetvar('USER')
     C                   EVAL      UsrSpcName  = zhbgetvar('UsrSpcName')
     C                   EVAL      XXMRNR  = zhbgetvar('WSMRNR')
     C                   EVAL      MRMRNR  = c2n2(XXMRNR)
     C                   EVAL      XXLNR   = zhbgetvar('LNR')
     C                   EVAL      XXENDRE = zhbgetvar('XENDRE')
     C                   endsr
      *=====================================================================
      * Set html output variables for section /$top
      *=====================================================================
     C     SetTop        begsr
      * Repeat national language for the next invocation
     C                   callp     updhtmlvar('LNG':lng:
     C                             InitHTMLVars)
      * Repeat background color for the next invocation
     C                   callp     updhtmlvar('TXTCOLOR':'black')
     C                   callp     updhtmlvar('BOLD':' ')
     C                   callp     updhtmlvar('BGCOLOR':BIFARG)
     C                   callp     updhtmlvar('LINK':'0000FF')
     C                   callp     updhtmlvar('VLINK':'FF0000')
     C                   callp     updhtmlvar('ALINK':'00FF00')
BODY C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updHtmlVar('USER':XXUSER)
     C                   callp     updHtmlVar('UsrSpcName':UsrSpcName)
     C                   callp     updhtmlvar('MRMRNR':
     C                             %trim(%editc(MRMRNR:'Z')))
      * Protocol
     C                   IF        ((BIBRID <> *BLANKS) AND
     C                              (BIINN <> *BLANKS))
     C                   callp     updHtmlVar('PROTOCOL':S_Protocol)
     C                   END
     C                   endsr
      *=====================================================================
      * Set html output variables for section /$found (display a boat)
      *=====================================================================
     C     SetFound      begsr
      *
     C                   MOVE      XXLNR         MRLNR
     C                   IF        XXENDRE = 'J'
     C                   MOVE      *BLANKS       MRLNA
     C                   END
     C                   IF        MRLNA = *BLANKS
     C                   IF        XXLNR <> *BLANKS
     C     LLVADR3K      CHAIN     LLVADR3                            33
     C                   MOVE      VADRNA        MRLNA
     C                   MOVE      VADRN1        MRLAD1
     C                   MOVE      VADRN2        MRLAD2
      * NORSK POSTNR??
     C                   TESTN                   VXPNR                33
     C     *IN33         IFEQ      '1'
     C     VXBLAN        ANDEQ     ' '
     C                   MOVE      VXPNR         MRLPN
     C                   MOVEL     VXSTED        MRLAD3
     C                   ELSE
     C                   MOVEL     VADRN3        MRLAD3
     C                   END
     C                   END
     C                   MOVE      *BLANKS       XXLNR
     C                   END
      * Sett inn i HTMLvariabler
     C                   callp     updHtmlVar('FMELDNR':'2')
     C                   callp     updHtmlVar('USER':XXUSER)
     C                   callp     updHtmlVar('UsrSpcName':UsrSpcName)
     C                   callp     updhtmlvar('MRMRNR':
     C                             %trim(%editc(MRMRNR:'Z')))
     C                   callp     updhtmlvar('MRKJED':
     C                             %trim(%editc(BIKUNR:'Z')))
     C                   callp     updHtmlVar('WSLNA':MRLNA)
     C                   callp     updHtmlVar('WSLAD1':MRLAD1)
     C                   callp     updHtmlVar('WSLAD2':MRLAD2)
     C                   callp     updHtmlVar('WSLAD3':MRLAD3)
     C                   callp     updHtmlVar('WSLPN':MRLPNAA)
     C                   callp     updhtmlvar('WSDT':
     C                             %trim(%editw(MRDT:'    .  .  ')))
     C                   callp     updhtmlvar('WSLREF':MRLREF)
     C                   callp     updhtmlvar('WSKREF':MRKREF)
N01  C                   callp     updhtmlvar('WSGN':MRGN)
N01  C                   callp     updhtmlvar('WSTRNR':MRTRNR)
     C                   callp     updhtmlvar('WSFT1':MRFT1)
     C                   callp     updhtmlvar('WSFT2':MRFT2)
     C                   callp     updhtmlvar('FEILMELDING':spaces)
      *
     C                   endsr
      *=====================================================================
      * Set html output skeleton member before its read into memory
      *=====================================================================
     C     SetSkl        begsr
     C                   eval      lng = uppify(lng)
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC' + lng
     C                   eval      Mbr = 'SYL022'
     C                   CAT       XSPRÅK:0      MBR
     C                   endsr
      *=====================================================================
      *  Retrieve environment variables
      *=====================================================================
     C     RtvEnvVar     begsr
      * Use getenvp to get this server's protocol
     C                   eval      S_Protocol =getenv('SERVER_PROTOCOL':
     C                             qusec)
      *
     C                   endsr
      *=====================================================================
      * Read output skeleton html member into memory
      *=====================================================================
     C     HENTLEV       begsr
     C                   MOVE      'N'           XBLANKSIST        1
      * VIS PLUKKET VAREMOTTAKER FØRST
     C                   IF        MRLNR <> *BLANKS                             1<-B
     C     LLVADR3K      CHAIN     LLVADR3                            33
     C                   EVAL      XXNAVN = VADRNA
     C                   CAT       VADRN3:1      XXNAVN           35
     C                   callp     updHtmlVar('WSLNR1':MRLNR)
     C                   callp     updhtmlvar('WSLNR2':XXNAVN)
     C                   callp     wrtsection('typsltrow')
     C                   MOVE      'J'           XBLANKSIST
     C                   ELSE                                                   1
      * VIS BLANK
     C                   IF        ((MRMRNR <> 0) AND (MRLNR <> *BLANKS))        2<-B
     C                   EVAL      XXNAVN = MRLNA
     C                   CAT       MRLAD3:1      XXNAVN           35
     C                   callp     updHtmlVar('WSLNR1':MRLNR)
     C                   callp     updhtmlvar('WSLNR2':XXNAVN)
     C                   callp     wrtsection('typsltrow')
     C                   MOVE      ' '           XBLANKSIST
     C                   END                                                     2<-E
     C                   END                                                    1<-E
      *
      * VIS BLANK
     C                   IF        XBLANKSIST = 'N'
     C                   MOVE      *BLANKS       VAKURE
     C                   callp     updhtmlvar('WSLNR1':VAKURE)
     C                   MOVE      *BLANKS       XXNAVN
     C                   callp     updhtmlvar('WSLNR2':XXNAVN)
     C                   callp     wrtsection('typsltrow')
     C                   END
      *
     C     LLVADR1K      SETLL     LLVADR1
     C     LLVADR1K      READE     LLVADR1                                40
     C     *IN40         DOWEQ     '0'
     C                   IF        VAKURE <> MRLNR
     C                   EVAL      XXNAVN = VADRNA
     C                   CAT       VADRN3:1      XXNAVN           35
     C                   callp     updhtmlvar('WSLNR1':VAKURE)
     C                   callp     updhtmlvar('WSLNR2':XXNAVN)
     C                   callp     wrtsection('typsltrow')
     C                   END
     C     LLVADR1K      READE     LLVADR1                                40
     C  N40              ENDDO
      *
     C                   IF        XBLANKSIST = 'J'
     C                   MOVE      *BLANKS       VAKURE
     C                   callp     updhtmlvar('WSLNR1':VAKURE)
     C                   MOVE      *BLANKS       XXNAVN
     C                   callp     updhtmlvar('WSLNR2':XXNAVN)
     C                   callp     wrtsection('typsltrow')
     C                   END
      *
     C                   callp     wrtsection('typsltend')
      *
     C                   endsr
      *=====================================================================
      *  Initier
      *=====================================================================
     C     INIT          begsr
     C                   OPEN      BRIDF
     C     XXUSER        CHAIN(N)  BRIDF                              50
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   CALLB     'INCGI'
     C                   PARM                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
     C                   open      CUNDL01                              50
     C                   open      LLMRF                                50
     C                   open      LLVADR1                              50
     C                   open      LLVADR3                              50
     C                   open      LLGRPK05                             50
     C                   open      BRPARF
     C     LLMRFK        klist
     C                   kfld                    GKGRP
     C                   kfld                    BIKUNR
     C                   kfld                    MRMRNR
     C     CUND1K        klist
     C                   kfld                    BIFIRM
     C                   kfld                    BIKUNR
     C     LLVADRK       klist
     C                   kfld                    BIFIRM
     C                   kfld                    BIKUNR
     C                   KFLD                    VADRNR
     C     LLVADR1K      klist
     C                   kfld                    BIFIRM
     C                   kfld                    BIKUNR
     C     LLVADR3K      klist
     C                   kfld                    BIFIRM
     C                   kfld                    BIKUNR
     C                   kfld                    MRLNR
     C     LLVADR3K2     klist
     C                   kfld                    BIFIRM
     C                   kfld                    BIKUNR
     C                   kfld                    XXLNR
     C     BRPARFK       klist
     C                   kfld                    PABRID
     C                   kfld                    PAKUNR
     C                   kfld                    PAID

     C                   MOVE      BIBRID        PABRID
     C                   MOVEL     'SPRÅK'       PAID
     C     BRPARFK       chain     BRPARF                             33
     C                   IF        (*IN33 OR (PAVRDA <> 'EN'))
     C                   MOVE      *BLANKS       XSPRÅK            2
     C                   ELSE
     C                   MOVE      'EN'          XSPRÅK            2
     C                   END
     C     BIKUNR        chain     LLGrpK05                           33
     C     CUND1K        CHAIN     CUNDL01                            50
     C                   MOVE      *ZEROS        MRLPN
      *
     C                   endsr
