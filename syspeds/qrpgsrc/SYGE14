      * PROGRAMMET ER BLÅKOPI AV SYGE04, MEN MED FORSKJELL AT HEADF
      * IKKE CHAINES.(ALLE NØDVENDIGE HEADF-INFO MOTTAS SOM PARAMETRE).
      *
      * OBS!! INNTIL SYGE03 "FASES UT"?? MÅ GE04/GE14 VÆRE "SYNKRONE"
      *****************************************************************
      *** PROGRAM SKAL BEREGNE FRAKT KUNDERSAVTALE                  ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER          ***
      *** LEDIGE INDIKATORER 01-26                                  ***
      *** LEDIGE INDIKATORER 27-32 34-99                            ***
      *****************************************************************
P06XXF* 01 JOV PTF10 GRPRIS(param->ge14) + HALFadjust  på omregninger
PXXXX * 02 SH  PTF10 fr1 fr2 feil ved transportdisponering
PXXXX * 03 SH  PTF10 ny parameter syge13
P10034* 04 SH  PTF11 avvik tabell ved over en fraktvekt
*****F**************************************************************
      *
     FSTED2D    IF   E           K DISK
     FBASES     IF   E           K DISK
     FAVFRH     IF   E           K DISK
     FAVFRHL02  IF   E           K DISK
     F                                     RENAME(AVFRHR:AVFRHR2)
     FAVFRHL04  IF   E           K DISK
     F                                     RENAME(AVFRHR:AVFRHR4)
     FAVFRD     IF   E           K DISK
     FVALUL01   IF   E           K DISK
     FFIRM      IF   E           K DISK
     D                 DS
     D  INKLUD                 1     50
     D  AFGE01                 1      3
     D  AFGE02                 5      7
     D  AFGE03                 9     11
     D  AFGE04                13     15
     D  AFGE05                17     19
     D  AFGE06                21     23
     D  AFGE07                25     27
     D  AFGE08                29     31
     D  AFGE09                33     35
     D  AFGE10                37     39
     D                 DS
     D  HXHEAD                 1    256
     D  HEUR                   1      1
     D  HEFR                   2      4
     D  HEKDPL                 5      5
     D  HELKA                  6      7
     D  HESDF                  8     12
     D  HETRI                 13     14
     D  HESDT                 15     19
     D  HELKS                 20     21
     D  HESDFF                22     26
     D  HELKK                 27     28
     D  HESDVT                29     33
     D  HEVKT                 34     42  0
     D  HEFBV                 43     51  0
     D  HEM3                  52     58  3
     D  HELM                  59     62  2
     D  HENT                  63     69  0
     D  TRVERV                70     72
     D  TRVERB                73     75  2
      *
     C                   EXSR      INIT
      *
      *
     C                   EXSR      SRSTED
      *
      *
     C                   EXSR      HOVED
      *
      *
      *    TESTER SONEFRA
      *
     C
     C     UBELØP        IFEQ      *ZEROS
     C                   MOVE      SONEFR        HESDFF
     C                   MOVE      STEDTI        HESDVT
      *
     C                   EXSR      HOVED
     C                   END
      *
      *
      *    TESTER SONETIL
      *
     C
     C     UBELØP        IFEQ      *ZEROS
      *
      * SPLITT PARAMETER UHEAD, VIA DS HXHEAD TIL HEADF-FELT
     C                   MOVE      UHEAD         HXHEAD
      *
     C                   MOVE      STEDFR        HESDFF
     C                   MOVE      SONETI        HESDVT
      *
     C                   EXSR      HOVED
     C                   END
      *
      *
      *
      *    TESTER SONEFRA SONETIL
      *
     C
     C     UBELØP        IFEQ      *ZEROS
      *
      * SPLITT PARAMETER UHEAD, VIA DS HXHEAD TIL HEADF-FELT
     C                   MOVE      UHEAD         HXHEAD
      *
     C                   MOVE      SONEFR        HESDFF
     C                   MOVE      SONETI        HESDVT
      *
     C                   EXSR      HOVED
     C                   END
      *
      *
      *
     C                   MOVE      HXHEAD        UHEAD
     C                   SETON                                        LR
     C                   RETURN
      *
      *
     C******************************************************************
     C     SRSTED        BEGSR
     C******************************************************************
      *
     C     KEYFRA        KLIST
     C                   KFLD                    HELKS
     C                   KFLD                    HESDFF
      *
     C     KEYTIL        KLIST
     C                   KFLD                    HELKK
     C                   KFLD                    HESDVT
      *
      * I transportoppdrag ligger "ytterpunktenee" I HESDF / HESDT
     C     HEUR          IFEQ      'H'
     C                   MOVE      HESDF         HESDFF
     C                   MOVE      HESDT         HESDVT
     C                   MOVE      HELKA         HELKS
     C                   MOVE      HETRI         HELKK
      * I transportoppdrag ligger KUBIKK I TRM3 I HEADTR
s02  C*                  END
n02  C                   ELSE
     C     GEBKO         IFEQ      'FR1'
     C                   MOVE      HELKS         HELKK
     C                   MOVE      HESDF         HESDVT
     C                   END
     C     GEBKO         IFEQ      'FR2'
     C                   MOVE      HELKK         HELKS
     C                   MOVE      HESDT         HESDFF
     C                   END
n02  C                   END
      *
      *
      * HENTER SONEFR  OG SONETI FOR SENERE BEREGNING
      *
     C     HEUR          IFEQ      'H'
     C     KEYFRA        CHAIN     STED2D                             33
     C  N33              MOVE      ST2KO2        SONEFR            5
     C     KEYTIL        CHAIN     STED2D                             33
     C  N33              MOVE      ST2KO2        SONETI            5
     C                   ELSE
     C                   MOVE      HESDF         SONEFR
     C                   MOVE      HESDT         SONETI
     C                   END
     C                   MOVE      HESDFF        STEDFR            5
     C                   MOVE      HESDVT        STEDTI            5
     C                   ENDSR
      *
     C******************************************************************
     C     HOVED         BEGSR
     C******************************************************************
     C                   MOVE      UAVD          YAVD
     C     START         TAG
     C                   MOVE      HEFR          YFR
     C                   MOVE      UGEBY         YGEBY
      * Finn kundersfraktavtale
     C                   EXSR      FAVTAL
     C   33              DO
      * Ikke finnes kundersfraktavtale med oppgitt frankatur
      * Frøv å finne kundersfraktavtale med generell frankatur (blank)
     C                   MOVE      *BLANKS       YFR
     C                   MOVE      UGEBY         YGEBY
     C                   EXSR      FAVTAL
     C                   END
      *
     C   33YAVD          IFNE      0
      * Prøv å finne kundersfraktavtale med avdeling = 0
     C                   MOVE      *ZEROS        YAVD
     C                   GOTO      START
     C                   END
      *
     C   33HEUR          IFEQ      'A'
      * Prøv å finne generell kundersfraktavtale (frasted = blank)
     C                   EXSR      FAVTA2
     C                   END
     C   33HEUR          IFEQ      'B'
      * Prøv å finne generell kundersfraktavtale (tilsted = blank)
     C                   EXSR      FAVTA3
     C                   END
      * 33 av  --->  Kundersfraktavtale finnes  --->  Beregn frakt
     C  N33              EXSR      RFRAKT
      *
      * HAR KUNDEN AVTALE SOM IKKE ER BENYTTET??? (IGNORER %% POSTER)
     C     UAVNR         IFEQ      *ZEROS
     C     KEYHAR        SETLL     AVFRHL02
     C     NXTHAR        TAG
     C     KEYHAR        READE     AVFRHL02                               33
     C     *IN33         IFEQ      '0'
     C     AFLKFR        ANDEQ     '%%'
     C                   GOTO      NXTHAR
     C                   END
     C  N33              EXSR      GETTXT
     C                   END
      * TA HØYDE FOR GRUNNPRIS
     C     AFGRPR        IFNE      0
     C     UBELØP        ANDNE     0
     C                   ADD       AFGRPR        UBELØP
     C                   END
     C     AFMAXI        IFNE      *ZEROS
     C     UBELØP        ANDNE     *ZEROS
     C     UBELØP        ANDGE     AFMAXI
     C                   Z-ADD     AFMAXI        UBELØP
     C                   END
     C     AFMINI        IFNE      *ZEROS
     C     UBELØP        ANDNE     *ZEROS
     C     UBELØP        ANDLT     AFMINI
     C                   Z-ADD     AFMINI        UBELØP
     C                   END
      *
     C                   ENDSR
      *
     C***************************************************
     C     FAVTAL        BEGSR
     C***************************************************
     C     AVLKLK        SETLL     AVFRHL04                           33
     C     STFAVT        TAG
     C     AVLKLK        READE     AVFRHL04                               33
      *
      *
      *
      *
      *
     C  N33GEBKO         IFEQ      'FR1'                                        1<-B
     C     AFBFR1        IFEQ      ' '                                           2<-B
     C                   GOTO      STFAVT
     C                   END
     C                   END
     C  N33GEBKO         IFEQ      'FRA'                                        1<-B
     C     AFBFRA        IFEQ      ' '                                           2<-B
     C                   GOTO      STFAVT
     C                   END
     C                   END
     C  N33GEBKO         IFEQ      'FR2'                                        1<-B
     C     AFBFR2        IFEQ      ' '                                           2<-B
     C                   GOTO      STFAVT
     C                   END
     C                   END
      *
      * SKJEKKER OM AVTALEN TILFREDSTILLER KRITERIER
      *
     C  N33HESDFF        IFGE      AFPNF1
     C     HESDFF        ANDLE     AFPNF2
     C     HESDVT        ANDGE     AFPNT1
     C     HESDVT        ANDLE     AFPNT2
     C                   GOTO      SLFAVT
     C                   ELSE
     C                   GOTO      STFAVT
     C                   END
      *
      *  TESTER OGSÅ GEBYRKODE BLANK
      *
     C   33YGEBY         IFEQ      *BLANKS
     C                   GOTO      SLFAVT
     C                   ELSE
     C                   MOVE      *BLANKS       YGEBY
     C     AVLKLK        SETLL     AVFRHL04                           33
     C                   GOTO      STFAVT
     C                   END
      *
     C     SLFAVT        ENDSR
      *
      *
     C***************************************************
     C     FAVTA2        BEGSR
     C***************************************************
     C                   MOVE      UAVD          YAVD
     C                   MOVE      *BLANKS       YPNF1
     C                   MOVE      *BLANKS       HELKS
     C                   MOVE      HEFR          YFR
     C                   MOVE      UGEBY         YGEBY
     C     STFAV2        TAG
     C     AVFRHK        SETGT     AVFRH                              33
     C     STFA21        TAG
     C     AVFHXK        READPE    AVFRH                                  33
      * Les kundersfraktavtale og test om frapostnr. ligger inne
     C  N33AFPNF2        IFNE      *BLANKS
     C     AFPNT1        ORGT      HESDVT
     C     AFPNT2        ORLT      HESDVT
     C                   GOTO      STFA21
     C                   END
      *
     C   33YGEBY         IFNE      *BLANKS
     C                   MOVE      *BLANKS       YGEBY
     C                   GOTO      STFAV2
     C                   END
      *
     C   33YFR           IFNE      *BLANKS
     C                   MOVE      *BLANKS       YFR
     C                   MOVE      UGEBY         YGEBY
     C                   GOTO      STFAV2
     C                   END
      *
     C   33YAVD          IFNE      0
     C                   MOVE      *ZEROS        YAVD
     C                   MOVE      HEFR          YFR
     C                   MOVE      UGEBY         YGEBY
     C                   GOTO      STFAV2
     C                   END
      *
     C                   ENDSR
      *
     C***************************************************
     C     FAVTA3        BEGSR
     C***************************************************
     C                   MOVE      UAVD          YAVD
     C                   MOVE      HEFR          YFR
     C                   MOVE      UGEBY         YGEBY
     C     STFAV3        TAG
     C     AVFH2K        SETGT     AVFRHL02                           33
     C     STFA31        TAG
     C     AVF21K        READPE    AVFRHL02                               33
      * Les kundersfraktavtale og test om frapostnr. ligger inne
     C  N33AFPNF2        COMP      HESDFF                               33
      * Les kundersfraktavtale og test om frapostnr. ligger inne
     C  N33AFPNT1        IFNE      *BLANKS
     C     AFPNT2        ORNE      *BLANKS
     C     AFLKTI        ORNE      *BLANKS
     C                   GOTO      STFA31
     C                   END
      *
     C   33YGEBY         IFNE      *BLANKS
     C                   MOVE      *BLANKS       YGEBY
     C                   GOTO      STFAV3
     C                   END
      *
     C   33YFR           IFNE      *BLANKS
     C                   MOVE      *BLANKS       YFR
     C                   MOVE      UGEBY         YGEBY
     C                   GOTO      STFAV3
     C                   END
      *
     C   33YAVD          IFNE      0
     C                   MOVE      *ZEROS        YAVD
     C                   MOVE      HEFR          YFR
     C                   MOVE      UGEBY         YGEBY
     C                   GOTO      STFAV3
     C                   END
      *
     C                   ENDSR
      *
     C******************************************
     C     FINNVK        BEGSR
     C******************************************
     C*
     C* FINNER FRAKTBEREGNINGSVEKT
     C*
s01  C*    HELM          MULT      AFOMRL        XFBV1             9 0
N01  C     HELM          MULT(H)   AFOMRL        XFBV1             9 0
     C*
     C* TAR EVENTUELLT OMREGNINGSFAKTOR NUMMER 2
     C     HEVKT         IFGT      AFOMR1
     C     AFOMR2        ANDNE     0
     C     AFOMR1        ANDNE     0
     C                   Z-ADD     AFOMR2        AFOMRM
     C                   END
s01  C*    HEM3          MULT      AFOMRM        XFBV2             9 0
N01  C     HEM3          MULT(H)   AFOMRM        XFBV2             9 0
     C     HEVKT         IFGE      XFBV1
     C                   Z-ADD     HEVKT         HEFBV             9 0
     C                   ELSE
     C                   Z-ADD     XFBV1         HEFBV
     C                   END
     C     HEFBV         IFLT      XFBV2
     C                   Z-ADD     XFBV2         HEFBV
     C                   END
     C*
     C                   ENDSR
     C***************************************************
     C     RFRAKT        BEGSR
     C***************************************************
     C     AFTYPE        IFEQ      'M'
     C                   MOVEL     HEM3          XXXM3             6 0            *HEM3=7,3
     C                   Z-ADD     XXXM3         HEFBV
     C                   ELSE
     C     AFTYPE        IFEQ      'L'
     C                   MOVE      *ZEROS        HEFBV
     C                   MOVE      HELM          HEFBV
     C                   END
     C                   END
     C     AFTYPE        IFEQ      ' '
     C                   EXSR      FINNVK
     C                   END
     C     AFTYPE        IFEQ      'B'
     C                   Z-ADD     HEVKT         HEFBV
     C                   END
     C     AFTYPE        IFEQ      'X'
     C                   Z-ADD     HENT          HEFBV
     C                   END
      *
      * FORFRAKT
      *
     C     GEBKO         IFEQ      'FR1'                                        1<-B
     C     AFTYPE        IFEQ      'V'                                           2<-B
      * Ikke finnes avtale for å beregne forfrakt
      *SPESIAL VAREVERDI
     C     AFBFR1        IFEQ      'T'                                            3<-B
     C     TRVERB        MULT      AFPFR1        UBELØP
     C                   DIV       100           UBELØP
     C                   MOVE      TRVERV        UVAL
     C                   Z-ADD     AFAVNR        UAVNR
     C                   END                                                      3<-E
     C                   GOTO      SLRFRA
     C                   END                                                     2<-E
      *
     C     AFBFR1        IFEQ      'P'                                           2<-B
     C     AFBFR1        OREQ      'B'
      * Bruk sonetabell for å beregne forfrakt
     C                   MOVE      AFSFR1        USONE             5
     C                   Z-ADD     AFPFR1        URABAT            5 2
     C     AFPFR1        IFEQ      *ZEROS
     C                   MOVE      'F'           FRKODE            1
     C                   EXSR      VKTPRO
     C                   ENDIF
     C                   EXSR      RSONEF
     C                   ELSE                                                    2
      * Beregn forfrakt ved oppgitt frakttabell
     C                   MOVE      'F'           YCDE1
     C                   EXSR      RTABF
     C                   END                                                     2<-E
      *
     C                   END                                                    1<-E
      *
      * VIDEREFRAKT
      *
     C     GEBKO         IFEQ      'FR2'                                        1<-B
     C     AFTYPE        IFEQ      'V'                                           2<-B
      * Ikke finnes avtale for å beregne viderefrakt
      *SPESIAL VAREVERDI
     C     AFBFR2        IFEQ      'T'                                            3<-B
     C     TRVERB        MULT      AFPFR2        UBELØP
     C                   DIV       100           UBELØP
     C                   MOVE      TRVERV        UVAL
     C                   Z-ADD     AFAVNR        UAVNR
     C                   END                                                      3<-E
     C                   GOTO      SLRFRA
     C                   END                                                     2<-E
      *
     C     AFBFR2        IFEQ      'P'                                           2<-B
     C     AFBFR2        OREQ      'B'
      * Bruk sonetabell for å beregne viderefrakt
     C                   MOVE      AFSFR2        USONE
     C                   Z-ADD     AFPFR2        URABAT
     C     AFPFR2        IFEQ      *ZEROS
     C                   MOVE      'V'           FRKODE
     C                   EXSR      VKTPRO
     C                   ENDIF
     C                   EXSR      RSONEF
     C                   ELSE                                                    2
      * Beregn viderefrakt ved oppgitt frakttabell
     C                   MOVE      'V'           YCDE1
     C                   EXSR      RTABF
     C                   END                                                     2<-E
      *
     C                   END                                                    1<-E
      *
      * HOVEDFRAKT
      *
     C     GEBKO         IFEQ      'FRA'                                        1<-B
     C     AFTYPE        IFEQ      'V'                                           2<-B
      * Ikke finnes avtale for å beregne hovedfrakt
      *SPESIAL VAREVERDI
     C     AFBFRA        IFEQ      'T'                                            3<-B
     C     TRVERB        MULT      AFPFRA        UBELØP
     C                   DIV       100           UBELØP
     C                   MOVE      TRVERV        UVAL
     C                   Z-ADD     AFAVNR        UAVNR
     C                   END                                                      3<-E
     C                   GOTO      SLRFRA
     C                   END                                                     2<-E
      *
     C     AFBFRA        IFEQ      'P'                                           2<-B
     C     AFBFRA        OREQ      'B'
      * Bruk sonetabell for å beregne hovedfrakt
     C                   MOVE      AFSFRA        USONE
     C     HEUR          IFNE      'H'
     C     USONE         ANDEQ     *BLANKS
     C     BASEK         CHAIN     BASES                              33
N04  C     *in33         ifeq      '0'
N04  C     baslmt        ifne      *zeros
N04  C     hefbv         andgt     baslmt
N04  c                   move      bassol        bassot
N04  c                   end
s04  C* N33              MOVE      BASSOT        USONE
n04  C                   MOVE      BASSOT        USONE
N04  c                   end
     C                   END
     C                   Z-ADD     AFPFRA        URABAT
     C     AFPFRA        IFEQ      *ZEROS
     C                   MOVE      'H'           FRKODE
     C                   EXSR      VKTPRO
     C                   ENDIF
     C                   EXSR      RSONEF
     C                   ELSE                                                    2
      * Beregn hovedfrakt ved oppgitt frakttabell
     C                   MOVE      'H'           YCDE1
     C                   EXSR      RTABF
     C                   END                                                     2<-E
      *
     C                   END                                                    1<-E
      *
     C     UBELØP        IFGT      0
     C                   MOVE      AFVALK        UVAL
     C                   Z-ADD     AFAVNR        UAVNR
     C     AFBFR1        IFEQ      'N'                                           2<-B
     C                   MOVE      'FR1'         AFGE10
     C                   END
     C     AFBFR2        IFEQ      'N'                                           2<-B
     C                   MOVE      'FR2'         AFGE09
     C                   END
     C     INKLUD        IFEQ      *BLANKS
     C                   MOVE      AFFRTX        UMSG
     C                   ELSE
     C                   MOVEL     '*inklud*'    AAA               8
     C                   MOVE      AAA           INKLUD
     C                   MOVEL     INKLUD        UMSG
     C                   END
     C     FICURR        IFEQ      UVAL
     C     FIDECI        IFEQ      0
     C                   ADD       0.5           UBELØP
     C                   MOVE      '00'          UBELØP
     C                   ELSE
     C     FIDECI        IFEQ      1
     C                   ADD       0.05          UBELØP
     C                   MOVE      '0'           UBELØP
     C                   END
     C                   END
     C                   END
     C                   END
      *
     C     SLRFRA        ENDSR
      *
     C***************************************************
     C     RSONEF        BEGSR
     C***************************************************
     C                   MOVE      *ZEROS        UBELØ2           13 2
N01  c     Usone         ifeq      'G    '
N01  C                   MOVE      KUFRBL        UBELØ2
N01  c                   end
     C                   MOVE      *BLANKS       UVAL2             3
     C                   MOVE      *ZEROS        UUKUNR            8 0
     C                   CALL      'SYGE13'
     C                   PARM                    GEBKO
     C                   PARM                    UAVD
     C                   PARM                    UOPD
     C                   PARM                    UVAL2
     C                   PARM                    UBELØ2
     C                   PARM                    USONE
     C                   PARM                    URABAT
     C                   PARM                    AFOMRL
     C                   PARM                    AFOMRM
     C                   PARM                    AFOMR1
     C                   PARM                    AFOMR2
     C                   PARM                    UUKUNR
     C                   PARM                    UHEAD           256
N03  C                   parm                    aftype            1
      *
     C     UBELØ2        IFNE      0                                            1<-B
     C     UVAL2         IFNE      AFVALK                                        2<-B
      * Valutakode i sonefrakt er ulik med valutakode i k.fraktavtale
      * Omregn frakt til valuta i firmasvalutakode
     C                   MOVE      UVAL2         YVK
     C     VALU1K        CHAIN     VALUL01                            34
     C     UBELØ2        MULT(H)   VALKU1        XWORK
     C     XWORK         DIV(H)    OMRFAK        UBELØ2
      * Hent firmasvalutakode
     C     FIVALK        IFNE      AFVALK                                         3<-B
      * Valutakode i k.fraktavtale er ulik med firamsvalutakode
      * Omregn frakt til valuta i k.fraktavtale
     C                   MOVE      AFVALK        YVK
     C     VALU1K        CHAIN     VALUL01                            34
     C     UBELØ2        DIV(H)    VALKU1        XWORK
     C     XWORK         MULT(H)   OMRFAK        UBELØ2
     C                   END                                                      3<-E
     C                   END                                                     2<-E
      * Adder frakt til UBELØP for å sende tilbake
     C                   ADD       UBELØ2        UBELØP
     C                   END                                                    1<-E
      *
     C                   ENDSR
      *
     C***************************************************
     C     RTABF         BEGSR
     C***************************************************
     C                   MOVE      *ZEROS        UBELØ2
      *
     C     AVFRDK        SETLL     AVFRD
     C     STRMIN        TAG
     C     AVFDXK        READE     AVFRD                                  33
     C   33              GOTO      SLRTF
      *
     C     AFCOBE        IFNE      'T'                                          1<-B
      * Ikke benytt frakttabell i denne vektintervallen.
      * Frakt skal beregnes ved sonefrakttabell
     C                   MOVE      *BLANKS       USONE
     C                   Z-ADD     AFPRIA        URABAT
     C                   EXSR      RSONEF
      *
     C                   ELSE                                                   1
      *
      * Beregn frakt med frakttabell
     C     AFPRPR        IFEQ      0                                             2<-B
      * Fast frakt i denne vektintervallen
     C                   Z-ADD     AFPRIA        UBELØ2
     C                   ELSE                                                    2
      *
     C     AFADJU        IFGT      0                                              3<-B
     C     HEFBV         ANDLT     999999999
      * Vekt skal oppjusteres med AFADJU
     C                   SUB       1             HEFBV
     C                   ADD       AFADJU        HEFBV
     C                   DIV       AFADJU        HEFBV
     C                   MULT      AFADJU        HEFBV
     C                   END                                                      3<-E
      *
      * Flytt pris inn i XWORK
     C                   Z-ADD     AFPRIA        XWORK            17 6
     C     AFPRPR        IFGT      1                                              3<-B
      * Divider pris med AFPRPR (pris pr.)
     C     AFPRIA        DIV(H)    AFPRPR        XWORK
     C                   END                                                      3<-E
      *
      * Beregn frakt ved å mutipilsere pris og vekt
     C     XWORK         MULT(H)   HEFBV         UBELØ2
      *
     C     AMIMAP        IFNE      0                                              3<-B
     C     AFMIMA        IFEQ      '1'                                             4<-B
     C     AMIMAP        ANDGT     UBELØ2
      * Frakt er mindre enn minimumfrakt i denne vektintervallen
      * Bruk minimumfrakt som frakt
     C                   Z-ADD     AMIMAP        UBELØ2
     C                   ELSE                                                      4
     C     AFMIMA        IFEQ      '2'                                              5<-B
     C     AMIMAP        ANDLT     UBELØ2
      * Frakt er større enn maximumfrakt i denne vektintervallen
      * Bruk maximumfrakt som frakt
     C                   Z-ADD     AMIMAP        UBELØ2
     C                   END                                                        5<-E
     C                   END                                                       4<-E
     C                   END                                                      3<-E
      *
     C                   END                                                     2<-E
      *
     C                   ADD       UBELØ2        UBELØP
      *
     C                   END                                                    1<-E
      *
     C     SLRTF         ENDSR
      *
     C***********************************************
     C     GETTXT        BEGSR
     C***********************************************
     C                   Z-ADD     3             XLEVEL            1 0          NIVÅ
     C                   Z-ADD     0             XRETUR            1 0          RETURKODE
     C                   MOVE      *BLANKS       XTSTAN           80            TEKST
     C                   Z-ADD     80            XMSGL             4 0          LENGDE PÅ MELDING
     C                   MOVE      'SHU0147'     XMSGNR            7
     C*
     C                   CALL      'SUBR23R3'
     C                   PARM                    XMSGNR
     C                   PARM                    XTSTAN
     C                   PARM                    XLEVEL
     C                   PARM                    XRETUR
     C                   PARM                    XMSGL
     C                   MOVEL     XTSTAN        UMSG
     C*
     C                   ENDSR
     C*
      ***********************************************
     C     VKTPRO        BEGSR
      ***********************************************
      * SJEKKER FØRST OM DET FINNES NOE REG. I DENNE TABELLEN
     C     VKTKEY        SETLL     AVFRD                                  90
     C     *IN90         IFEQ      *ON
      * DET FINNES REG. I DENNE TABELLEN. SLÅ OPP PÅ VEKTINTERVALLET
      * OG HENT PROSENTSATSEN.
     C     VKTKE2        SETLL     AVFRD                                  90
     C     VKTKEY        READE     AVFRD                                  90
     C     *IN90         IFEQ      *OFF
     C                   Z-ADD     AFPRIA        URABAT
     C                   ENDIF
     C                   ENDIF
      *
     C                   ENDSR
     C*
      ***********************************************
     C     INIT          BEGSR
      ***********************************************
      *
     C     *ENTRY        PLIST
     C                   PARM                    UGEBY             3
     C                   PARM                    GEBKO             3
     C                   PARM                    UAVD              4 0
     C                   PARM                    UOPD              7 0
     C                   PARM                    UKN               8 0
     C                   PARM                    UVAL              3
     C                   PARM                    UBELØP           13 2
     C                   PARM                    UAVNR             9 0
     C                   PARM                    UMSG             50
     C                   PARM                    USK               1
     C                   PARM                    UHEAD           256
      *
      * SPLITT PARAMETER UHEAD, VIA DS HXHEAD TIL HEADF-FELT
     C                   MOVE      UHEAD         HXHEAD
      *
N01   * ved avrregning/grunnprisberegning er kundefrakt innparm (kundefr - xx % = max GRprbel)
N01  C                   MOVE      UBELØP        KUFRBL           13 2
      *
N01  C                   MOVE      *ZEROS        UBELØP
     C                   MOVE      *ZEROS        UAVNR
     C                   MOVE      *BLANKS       UMSG
      *
     C     HEAKEY        KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    UOPD
     C     AVFRHK        KLIST
     C                   KFLD                    UKN
     C                   KFLD                    HEKDPL
     C                   KFLD                    YAVD              4 0
     C                   KFLD                    HELKS
     C                   KFLD                    YPNF1             5
     C                   KFLD                    YFR               3
     C                   KFLD                    YGEBY             3
     C                   KFLD                    HELKK
     C                   KFLD                    HESDVT
     C     AVFHXK        KLIST
     C                   KFLD                    UKN
     C                   KFLD                    HEKDPL
     C                   KFLD                    YAVD
     C                   KFLD                    HELKS
     C                   KFLD                    YPNF1
     C                   KFLD                    YFR
     C                   KFLD                    YGEBY
     C                   KFLD                    HELKK
     C     AVFH2K        KLIST
     C                   KFLD                    UKN
     C                   KFLD                    HEKDPL
     C                   KFLD                    YAVD
     C                   KFLD                    YFR
     C                   KFLD                    YGEBY
     C                   KFLD                    HELKS
     C                   KFLD                    HESDFF
     C     AVLKLK        KLIST
     C                   KFLD                    UKN
     C                   KFLD                    HEKDPL
     C                   KFLD                    YAVD
     C                   KFLD                    YFR
     C                   KFLD                    YGEBY
     C                   KFLD                    HELKS
     C                   KFLD                    HELKK
     C     BASEK         KLIST
     C                   KFLD                    HELKS
     C                   KFLD                    HESDF
     C                   KFLD                    HELKK
     C                   KFLD                    HESDT
     C     AVF21K        KLIST
     C                   KFLD                    UKN
     C                   KFLD                    HEKDPL
     C                   KFLD                    YAVD
     C                   KFLD                    YFR
     C                   KFLD                    YGEBY
     C                   KFLD                    HELKS
     C     KEYHAR        KLIST
     C                   KFLD                    UKN
     C     AVFRDK        KLIST
     C                   KFLD                    AFAVNR
     C                   KFLD                    YCDE1             1
     C                   KFLD                    HEFBV
     C     AVFDXK        KLIST
     C                   KFLD                    AFAVNR
     C                   KFLD                    YCDE1
     C     VALU1K        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    YVK               3
     C     VKTKEY        KLIST
     C                   KFLD                    AFAVNR
     C                   KFLD                    FRKODE
     C     VKTKE2        KLIST
     C                   KFLD                    AFAVNR
     C                   KFLD                    FRKODE
     C                   KFLD                    HEFBV
      *
     C     FIFIRM        SETLL     FIRM
     C                   READ      FIRM                                   33
      *
     C                   ENDSR
