     A*%%TS  SD  20211214  082659  JOVO        REL-V7R4M0  5770-WDS
*****A**************************************************************
     A* RETTINGER I DETTE PROGRAM:
PTFnrA*Sek Sig Vers  Beskrivelse...........................
"""""A*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10019A* 02 YBC PTF10 FLERE PARAMETRE FOR OVERSJØ-SYSTEM
10XXXA* 03 YBC PTF10 F23 GJELDER OGSÅ FOR OVERSJØ-MODUL. INGEN MERKING
10XXXA* 04 JOV PTF12 UTVIDET NED 2 LINKER DATO/TID ESTIMATE 2
*****A**************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3                  -
     A                                             27 132 *DS4)
     A                                      PRINT
     A                                      HELP
     A                                      ALTHELP(CA01)
      * "NOERASE" MÅ HA FOR Å IKKE TØMME BILDE SOM MAN KOMME FRA
     A          R NOERASE
     A                                      ASSUME
     A                                  1  2' '
      * MAN KUN DEFINNERE VINDU PÅ REKORDNIVÅ
     A          R WINDOWSZ
S04  A*                                     WINDOW(*DFT 15 55 *NOMSGLIN)
N04  A                                      WINDOW(*DFT 17 55 *NOMSGLIN)
     A                                      OVERLAY
     A*
     A          R BILDE
     A*%%TS  SD  20211214  082659  JOVO        REL-V7R4M0  5770-WDS
     A  *DS3                                WINDOW(WINDOWSZ)
     A  *DS4                                WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A                                      CA03(03 'Avslutt')
     A                                      CA12(12 'Forrige')
     A                                      CA23(23 'OPPHEV PROTECT')
     A            MSGNR          7A  P
     A                                  1  2'SYTT02'
     A                                      COLOR(BLU)
     A                                  1 10MSGCON(040 YBC0646 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  2  2MSGCON(010 YBC0223 *LIBL/SYMSGF)
     A            UAVD           4Y 0O  2 13EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  2 18'/'
     A                                      COLOR(WHT)
     A            UOPD           7Y 0O  2 20EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  2 31MSGCON(025 YBC0648 *LIBL/SYMSGF)
     A                                  3 31MSGCON(025 YBC0649 *LIBL/SYMSGF)
     A                                  4  2MSGCON(018 YBC0631 *LIBL/SYMSGF)
     A                                  4 21MSGCON(010 YBC0630 *LIBL/SYMSGF)
     A            WSETDD         6Y 0B  4 34EDTCDE(4)
     A  93                                  DSPATR(RI)
     A  93                                  DSPATR(PC)
     A  50                                  DSPATR(PR)
     A  50                                  COLOR(WHT)
     A            WSETDK         4Y 0B  4 41EDTWRD('  :  ')
     A  92                                  DSPATR(RI)
     A  92                                  DSPATR(PC)
     A  50                                  DSPATR(PR)
     A  50                                  COLOR(WHT)
     A                                  4 48'(ETD)'
     A                                  5  2MSGCON(018 YBC0631 *LIBL/SYMSGF)
     A                                  5 21MSGCON(010 YBC0633 *LIBL/SYMSGF)
     A            WSATDD         6Y 0B  5 34EDTCDE(4)
     A  97                                  DSPATR(RI)
     A  97                                  DSPATR(PC)
     A  53
     AO 50                                  DSPATR(PR)
     A  53
     AO 50                                  COLOR(WHT)
     A            WSATDK         4Y 0B  5 41EDTWRD('  :  ')
     A  96                                  DSPATR(RI)
     A  96                                  DSPATR(PC)
     A  53
     AO 50                                  DSPATR(PR)
     A  53
     AO 50                                  COLOR(WHT)
     A                                  5 48'(ATD)'
     A                                  7 21MSGCON(010 YBC0630 *LIBL/SYMSGF)
     A                                  7  2MSGCON(018 YBC0632 *LIBL/SYMSGF)
     A            WSETAD         6Y 0B  7 34EDTCDE(4)
     A  95                                  DSPATR(RI)
     A  95                                  DSPATR(PC)
     A  50                                  DSPATR(PR)
     A  50                                  COLOR(WHT)
     A            WSETAK         4Y 0B  7 41EDTWRD('  :  ')
     A  94                                  DSPATR(RI)
     A  94                                  DSPATR(PC)
     A  50                                  DSPATR(PR)
     A  50                                  COLOR(WHT)
     A                                  7 48'(ETA)'
     A                                  8 21MSGCON(010 YBC0633 *LIBL/SYMSGF)
     A                                  8  2MSGCON(018 YBC0632 *LIBL/SYMSGF)
     A            WSATAD         6Y 0B  8 34EDTCDE(4)
     A  99                                  DSPATR(RI)
     A  99                                  DSPATR(PC)
     A  54
     AO 50                                  DSPATR(PR)
     A  54
     AO 50                                  COLOR(WHT)
     A            WSATAK         4Y 0B  8 41EDTWRD('  :  ')
     A  98                                  DSPATR(RI)
     A  98                                  DSPATR(PC)
     A  54
     AO 50                                  DSPATR(PR)
     A  54
     AO 50                                  COLOR(WHT)
     A                                  8 48'(ATA)'
     A                                 10  2MSGCON(010 YBC0634 *LIBL/SYMSGF)
     A                                 10 14'PO forv. lev-dato'
     A            WSEX1D         6Y 0B 10 34EDTCDE(4)
     A  91                                  DSPATR(RI)
     A  91                                  DSPATR(PC)
     A  51
     AO 50                                  DSPATR(PR)
     A  51
     AO 50                                  COLOR(WHT)
     A            WSEX1K         4Y 0B 10 41EDTWRD('  :  ')
     A  90                                  DSPATR(RI)
     A  90                                  DSPATR(PC)
     A  51
     AO 50                                  DSPATR(PR)
     A  51
     AO 50                                  COLOR(WHT)
     A                                 11 14'Ready for pick up'
     A            WSEX2D         6Y 0B 11 34EDTCDE(4)
     A  89                                  DSPATR(RI)
     A  89                                  DSPATR(PC)
     A  52
     AO 50                                  DSPATR(PR)
     A  52
     AO 50                                  COLOR(WHT)
     A            WSEX2K         4Y 0B 11 41EDTWRD('  :  ')
     A  88                                  DSPATR(RI)
     A  88                                  DSPATR(PC)
     A  52
     AO 50                                  DSPATR(PR)
     A  52
     AO 50                                  COLOR(WHT)
E04  A* VIDERE LINJER JUSTERT 2 OPP..
     A  23                             15  2'Årsak,endr. ATD/ATA mm:'
     A            WSTXT         30A  B 15 26
     A  56                                  DSPATR(RI)
     A  56                                  DSPATR(PC)
     A N23                                  DSPATR(ND)
     A N23                                  DSPATR(PR)
     A                                 16  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 16 22MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 16 41'F23=Åpne alle.'
     A                                      COLOR(BLU)
     A  30        MSGTXT        53A  O 17  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A                                 14  2'Ank.dat/tid: Ny/Justert ETA.:'
     A            WSETAD2        6Y 0B 14 34EDTCDE(4)
     A  84                                  DSPATR(RI)
     A  84                                  DSPATR(PC)
     A  50                                  DSPATR(PR)
     A  50                                  COLOR(WHT)
     A            WSETAK2        4Y 0B 14 41EDTWRD('  :  ')
     A  83                                  DSPATR(RI)
     A  83                                  DSPATR(PC)
     A  50                                  DSPATR(PR)
     A  50                                  COLOR(WHT)
     A            TXETA2K        1   O 14 47COLOR(WHT)
     A                                 14 49'G,V,S,M'
N01   *
N01  A          R BILDE02
N01  A                                      WINDOW(WINDOWSZ)
N01  A                                      OVERLAY
N01  A                                      CA03(03 'Avslutt')
N01  A                                      CA12(12 'Forrige')
     A                                      CA23(23 'OPPHEV PROTECT')
N01  A            MSGNR          7A  P
N01  A                                  1  2'SYTT02'
N01  A                                      COLOR(BLU)
N01  A                                  1 10MSGCON(040 YBC0646 *LIBL/SYMSGF)
N01  A                                      COLOR(WHT)
N01  A                                  2  2MSGCON(010 YBC0223 *LIBL/SYMSGF)
N01  A            UAVD           4Y 0O  2 13EDTCDE(4)
N01  A                                      COLOR(WHT)
N01  A                                  2 18'/'
N01  A                                      COLOR(WHT)
N01  A            UOPD           7Y 0O  2 20EDTCDE(4)
N01  A                                      COLOR(WHT)
N01  A                                  2 31MSGCON(025 YBC0648 *LIBL/SYMSGF)
N01  A                                  3 31MSGCON(025 YBC0649 *LIBL/SYMSGF)
N01  A                                  4  2MSGCON(018 YBC0631 *LIBL/SYMSGF)
N01  A                                  4 21MSGCON(010 YBC0630 *LIBL/SYMSGF)
N01  A            WSETDD         6Y 0B  4 34EDTCDE(4)
N01  A  93                                  DSPATR(RI)
N01  A  93                                  DSPATR(PC)
N01  A  50                                  DSPATR(PR)
N01  A  50                                  COLOR(WHT)
N01  A            WSETDK         4Y 0B  4 41EDTWRD('  :  ')
N01  A  92                                  DSPATR(RI)
N01  A  92                                  DSPATR(PC)
N01  A  50                                  DSPATR(PR)
N01  A  50                                  COLOR(WHT)
N01  A                                  4 48'(ETD)'
N01  A                                  5  2MSGCON(018 YBC0631 *LIBL/SYMSGF)
N01  A                                  5 21MSGCON(010 YBC0633 *LIBL/SYMSGF)
N01  A            WSATDD         6Y 0B  5 34EDTCDE(4)
N01  A  97                                  DSPATR(RI)
N01  A  97                                  DSPATR(PC)
N01  A  50                                  DSPATR(PR)
N01  A  50                                  COLOR(WHT)
N01  A            WSATDK         4Y 0B  5 41EDTWRD('  :  ')
N01  A  96                                  DSPATR(RI)
N01  A  96                                  DSPATR(PC)
N01  A  50                                  DSPATR(PR)
N01  A  50                                  COLOR(WHT)
N01  A                                  5 48'(ATD)'
N01  A                                  7 21MSGCON(010 YBC0630 *LIBL/SYMSGF)
N01  A                                  7  2MSGCON(018 YBC0632 *LIBL/SYMSGF)
N01  A            WSETAD         6Y 0B  7 34EDTCDE(4)
N01  A  95                                  DSPATR(RI)
N01  A  95                                  DSPATR(PC)
N01  A  50                                  DSPATR(PR)
N01  A  50                                  COLOR(WHT)
N01  A            WSETAK         4Y 0B  7 41EDTWRD('  :  ')
N01  A  94                                  DSPATR(RI)
N01  A  94                                  DSPATR(PC)
N01  A  50                                  DSPATR(PR)
N01  A  50                                  COLOR(WHT)
N01  A                                  7 48'(ETA)'
N01  A                                  8  2MSGCON(018 YBC0632 *LIBL/SYMSGF)
N01  A                                  8 21MSGCON(010 YBC0633 *LIBL/SYMSGF)
N01  A            WSATAD         6Y 0B  8 34EDTCDE(4)
N01  A  99                                  DSPATR(RI)
N01  A  99                                  DSPATR(PC)
N01  A  50                                  DSPATR(PR)
N01  A  50                                  COLOR(WHT)
N01  A            WSATAK         4Y 0B  8 41EDTWRD('  :  ')
N01  A  98                                  DSPATR(RI)
N01  A  98                                  DSPATR(PC)
N01  A  50                                  DSPATR(PR)
N01  A  50                                  COLOR(WHT)
N01  A                                  8 48'(ATA)'
N01  A                                 10  2'Klar fra leverandør:'
N01  A            WSOS01         6Y 0B 10 23EDTCDE(4)
N01  A  91                                  DSPATR(RI)
N01  A  91                                  DSPATR(PC)
N01  A  50                                  DSPATR(PR)
N01  A  50                                  COLOR(WHT)
N01  A                                 11  2'Shipping frist:'
N01  A            WSOS02         6Y 0B 11 23EDTCDE(4)
N01  A  90                                  DSPATR(RI)
N01  A  90                                  DSPATR(PC)
N01  A  50                                  DSPATR(PR)
N01  A  50                                  COLOR(WHT)
N01  A                                 11 30'Vindu:'
N01  A            WSOS04         6Y 0B 11 37EDTCDE(4)
N01  A  89                                  DSPATR(RI)
N01  A  89                                  DSPATR(PC)
N01  A  50                                  DSPATR(PR)
N01  A  50                                  COLOR(WHT)
N01  A            WSOS05         6Y 0B 11 44EDTCDE(4)
N01  A  88                                  DSPATR(RI)
N01  A  88                                  DSPATR(PC)
N01  A  50                                  DSPATR(PR)
N01  A  50                                  COLOR(WHT)
N01  A                                 12  2'Ankomst frist:'
N01  A            WSOS03         6Y 0B 12 23EDTCDE(4)
N01  A  87                                  DSPATR(RI)
N01  A  87                                  DSPATR(PC)
N01  A  50                                  DSPATR(PR)
N01  A  50                                  COLOR(WHT)
N01  A                                 12 30'Vindu:'
N01  A            WSOS06         6Y 0B 12 37EDTCDE(4)
N01  A  86                                  DSPATR(RI)
N01  A  86                                  DSPATR(PC)
N01  A  50                                  DSPATR(PR)
N01  A  50                                  COLOR(WHT)
N01  A            WSOS07         6Y 0B 12 44EDTCDE(4)
N01  A  85                                  DSPATR(RI)
N01  A  85                                  DSPATR(PC)
N01  A  50                                  DSPATR(PR)
N01  A  50                                  COLOR(WHT)
E04   * VIDERE LINJER 2 OPP
     A  23                             15  2'Årsak,endr. ATD/ATA mm:'
     A            WSTXT         30A  B 15 26
     A  56                                  DSPATR(RI)
     A  56                                  DSPATR(PC)
     A N23                                  DSPATR(ND)
     A N23                                  DSPATR(PR)
N01  A                                 16  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
N01  A                                      COLOR(BLU)
N01  A                                 16 22MSGCON(018 DIV0024 *LIBL/SYMSGF)
N01  A                                      COLOR(BLU)
     A                                 16 41'F23=Åpne alle.'
     A                                      COLOR(BLU)
N01  A  30        MSGTXT        53A  O 17  2MSGID(&MSGNR SYMSGF)
N01  A                                      COLOR(WHT)
