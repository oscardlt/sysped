     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPDOC('STD' 'HELPNU' 'HLP400')
     A                                      HELP
     A          R BILDE01
     A                                      CF03(03 'Avslutt program')
     A                                      CF04(04 'SØKING')
     A                                      CSRLOC(LINNBR POSNBR)
     A                                      OVERLAY
     A            LINNBR         3  0H
     A            POSNBR         3  0H
     A                                  1  2'SYSPED/SYTR92A'
     A                                      COLOR(BLU)
     A                                  1 28MSGCON(025 TBR0031 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 62DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  1 73TIME
     A                                      COLOR(BLU)
     A                                  4  3MSGCON(025 TBR0032 *LIBL/SYMSGF)
     A            BILNR         10A  B  4 29
     A  70                                  DSPATR(RI)
     A                                  5  8MSGCON(006 TBR0033 *LIBL/SYMSGF)
     A                                  6  3MSGCON(025 TBR0034 *LIBL/SYMSGF)
     A            BILKOD         3A  B  6 29
     A  71                                  DSPATR(RI)
     A                                  7  8MSGCON(006 TBR0033 *LIBL/SYMSGF)
     A                                  8  3MSGCON(025 TBR0035 *LIBL/SYMSGF)
     A            TRANNR         8Y 0B  8 29EDTCDE(4)
     A  72                                  DSPATR(RI)
     A                                 11  5MSGCON(023 TBR0036 *LIBL/SYMSGF)
     A            FPER           4S 0B 11 29
     A                                 13  5MSGCON(023 TBR0037 *LIBL/SYMSGF)
     A            TPER           4S 0B 13 29
     A                                 17  2MSGCON(024 DIV4058 *LIBL/SYMSGF)
     A  50                                  DSPATR(ND)
     A            WSPRT         10A  B 17 28
     A  50                                  DSPATR(PR)
     A N50                                  DSPATR(PC)
     A  50                                  DSPATR(ND)
     A                                 19  2MSGCON(024 DIV4059 *LIBL/SYMSGF)
     A  50                                  DSPATR(ND)
     A            WSFORM        10A  B 19 28
     A  50                                  DSPATR(PR)
     A  50                                  DSPATR(ND)
     A                                 21  2MSGCON(024 DIV4060 *LIBL/SYMSGF)
     A  50                                  DSPATR(ND)
     A            WSJOBQ         1A  B 21 28
     A  50                                  DSPATR(PR)
     A  50                                  DSPATR(ND)
     A                                 23  3MSGCON(010 TBR0013 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 20MSGCON(010 TBR0030 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
