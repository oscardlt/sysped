      *
      ***********************************************************
      *** PROGRAM SKAL BEREGN STATISTIKK FOR NCTS-EKSPORT     ***
      ***********************************************************
      *
     FTRUH09    IF   E           K DISK
     FTRUV      IF   E           K DISK
     FSAEH      IF   E           K DISK
     FKODTVAL2  IF   E           K DISK
     FEDIM2     IF   E           K DISK
     FKALENDER  IF   E           K DISK
     FTR007F    IF A E           K DISK
      *  Prototype for 'SYGE15C'
     d syge15c         pr                  extpgm('SYGE15C')
     d wdt_                                like(wdt)
     d wtm_                                like(wtm)
      *
     D wdt             S              8A
     D wtm             S              6A
     D xant            S              7S 0
     D xantopd         S              7S 0
     D xantdg          S              7S 0
     D xantdg2         S              7S 0
     D xantdg3         S              7S 0
     D xaar            S              4S 0
     D xuke            S              2S 0
     D x_t7aar         S              4S 0
     D x_t7uke         S              2S 0
     d                 ds
     d xdt                     1      8  0
     d xdta                    1      4  0
     d xdtm                    5      6  0
     d xdtd                    7      8  0

      /free
        exsr init;

        setll (xdt) truh09;
        read truh09;
        dow not %eof;
          if thst = 'Z';
            clear tr007fr;
            setll (thavd:thtdn) edim2;
            reade (thavd:thtdn) edim2;
            dow not %eof;
              select;
              when m1n07 = '015';
                t7dt1 = mdt;
              when m1n07 = '029';
                t7dt2 = mdt;
              when m1n07 = '045';
                t7dt3 = mdt;
              endsl;
              reade (thavd:thtdn) edim2;
            enddo;
            t7avd = thavd;
            t7opd = thtdn;
            t7trnr= thtrnr;
            t7dt4 = thddt;
            t7vkt = thvkb;
            t7gbl = thgbl * -1;
            chain t7dt1 kalender;
            t7uke = kauke;
            t7aar = kayear;
            t7adg = 0;
            if t7dt3 > t7dt1;
              setll t7dt1 kalender;
              read kalender;
              dow not %eof;
                if t7dt3 > kadate;
                  t7adg = t7adg + 1;
                else;
                  leave;
                endif;
                read kalender;
              enddo;
            endif;
            t7adg2 = 0;
            if t7dt4 > t7dt1;
              setll t7dt1 kalender;
              read kalender;
              dow not %eof;
                if t7dt4 > kadate;
                  t7adg2 = t7adg2 + 1;
                else;
                  leave;
                endif;
                read kalender;
              enddo;
            endif;
            setll (thavd:thtdn) truv;
            reade (thavd:thtdn) truv;
            if not %eof;
            dow not %eof;
              t7li  = tvli;
              t7vnt = tvvnt;
              t7vktb= tvvktb;
              t7vktn= tvvktn;
              t7vt  = tvvt;
              t7val = *blanks;
              t7bls = 0;
              t7krs = 0;
              t7bln = 0;
              chain (thavd: thtdn) saeh;
              if %found;                     // En NCTS -> en fortolling
                t7val = seval1;
                t7bls = sebel1;
                t7krs = sevku;
                chain seval1 kodtval2;
                if %found;
                  t7bln = sebel1 * sevku / kvaomr;
                else;
                  t7bln = sebel1;
                endif;
              endif;
               t7avdl = tvavd2;
               t7opdl = tvtdn2;
               t7vall = *blanks;
               t7blsl = 0;
               t7krsl = 0;
               t7blnl = 0;
              if tvavd2 <> 0;
                chain (tvavd2: tvtdn2) saeh;
                if %found;                   // En NCTS linje -> en fortolling
                  t7vall = seval1;
                  t7blsl = sebel1;
                  t7krsl = sevku;
                  chain seval1 kodtval2;
                  if %found;
                    t7blnl = sebel1 * sevku / kvaomr;
                  else;
                    t7blnl = sebel1;
                  endif;
                endif;
              endif;
              write tr007fr;
              reade (thavd:thtdn) truv;
            enddo;
            else;
              t7li  = 0;
              t7vnt = 0;
              t7vt  = *blanks;
            endif;
          endif;
          read truh09;
        enddo;

        setll *loval tr007f;
        read tr007f;
        dow not %eof;
          if xaar <> t7aar or xuke <> t7uke;
            if xuke <> 0;
              x_t7aar = t7aar;
              x_t7uke = t7uke;
              clear tr007fr;
              t7aar  = xaar;
              t7uknr = xuke;
              t7ant  = xant;
              write tr007fr;
              t7aar = x_t7aar;
              t7uke = x_t7uke;
              xant = 0;
            endif;
            xaar = t7aar;
            xuke = t7uke;
          endif;
          xant = xant + 1;
          xantopd = xantopd + 1;
          xantdg  = xantdg  + t7adg;
          xantdg2 = xantdg2 + t7adg2;
          if t7adg2 < t7adg;
            xantdg3 = xantdg3 + t7adg2;
          else;
            xantdg3 = xantdg3 + t7adg;
          endif;
          read tr007f;
        enddo;
        if xaar <> 0;
          clear tr007fr;
          t7aar  = xaar;
          t7uknr = xuke;
          t7ant  = xant;
          write tr007fr;
        endif;
        clear tr007fr;
        t7snit = xantdg / xantopd;
        t7snit2 = xantdg2 / xantopd;
        t7snit3 = xantdg3 / xantopd;
        write tr007fr;

        *inlr = *on;
        return;

        // ***********************************************
        begsr init;
        // ***********************************************

        syge15c (wdt: wtm);
        xdt = %dec(wdt:8:0);
        //xdt = xdt - 10000;
        if xdtm > 6;
          xdtm = xdtm - 6;
        else;
          xdta = xdta - 1;
          xdtm = xdtm + 6;
        endif;

        endsr;

      /end-free
