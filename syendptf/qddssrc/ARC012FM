     A*%%TS  SD  20161122  072608  SVEIN       REL-V7R1M0  5770-WDS
     A*%%EC
     A                                      DSPSIZ(27 132 *DS4)
     A                                      PRINT
     A                                      HLPDOC('SYFA5201' 'SYFA52' 'SY400LO-
     A                                      /MAINT')
     A                                      HELP
     A*
     A          R SUBFIL                    SFL
     A*%%TS  SD  20161122  072608  SVEIN       REL-V7R1M0  5770-WDS
     A            EPTIL     R        H      REFFLD(ARKLOGHR/EPTIL *LIBL/ARKLOGH)
     A            EPTUR     R        H      REFFLD(ARKLOGHR/EPTUR *LIBL/ARKLOGH)
     A            EPKUND    R        H      REFFLD(ARKLOGHR/EPKUND *LIBL/ARKLOG-
     A                                      H)
     A            EPUNIK    R        H      REFFLD(ARKLOGHR/EPUNIK *LIBL/ARKLOG-
     A                                      H)
     A            WSNR           1A  B  6  2VALUES(' ' '5' 'D' 'R' 'V')
     A            EPUSER        10A  O  6  4
     A            EPTIL48       48A  O  6 15
     A            EPDATE    R        O  6 64REFFLD(ARKLOGHR/EPDATE *LIBL/ARKLOG-
     A                                      H)
     A            EPTIME    R        O  6 73REFFLD(ARKLOGHR/EPTIME *LIBL/ARKLOG-
     A                                      H)
     A            EPAVD     R        O  6 80REFFLD(ARKLOGHR/EPAVD *LIBL/ARKLOGH)
     A                                      EDTCDE(Z)
     A            EPOPD     R        O  6 85REFFLD(ARKLOGHR/EPOPD *LIBL/ARKLOGH)
     A                                      EDTCDE(Z)
N25  A  73                                  DSPATR(RI)
     A            KNAVNM        30A  O  6 93
     A            EPAUTO    R        O  6125REFFLD(ARKLOGHR/EPAUTO *LIBL/ARKLOG-
     A                                      H)
     A            CVRP         129   O  7  4DSPATR(UL)
     A*
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A*%%TS  SD  20150205  124420  SVEIN       REL-V7R1M0  5770-WDS
     A  *DS4                                SFLSIZ(0009)
     A  *DS4                                SFLPAG(0008)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                  1  2'SYSPED/ARC012'
     A                                      COLOR(BLU)
     A                                  1 17MSGCON(050 SHU0906 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A            WSUSER    R        O  2  2REFFLD(ARKLOGHR/EPUSER *LIBL/ARKLOG-
     A                                      H)
     A                                      COLOR(YLW)
     A                                  2 17'Ref:'
     A            SCAVD          4Y 0O  2 22EDTCDE(Z)
     A                                  2 27'/'
     A            SCOPD          7Y 0O  2 29EDTCDE(Z)
     A            ZRECNO         4S 0O  2 75SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  3  2MSGCON(015 SHU0907 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 22MSGCON(027 SHU0914 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 51MSGCON(020 SHU1039 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 51MSGCON(015 SHU1040 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  1MSGCON(130 SHU1158 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
      *
     A          R BUNN
     A                                      CF03(03 'EXIT')
     A                                      CF05(05 'Frisk')
     A                                      CF08(08 'SØK')
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      SLNO(22)
     A                                      CLRL(06)
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  3  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 62MSGCON(018 DIV0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        70A  O  4  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(RED)
     A N30                              4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A*
     A          R VAREL2
     A*%%TS  SD  20160628  111043  SVEIN       REL-V7R1M0  5770-WDS
     A                                      CF12(12 'Forrige')
     A                                      CF04(04 'SØK')
     A                                      SLNO(22)
     A                                      CLRL(06)
     A                                      OVERLAY
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  3  2MSGCON(007 SHU1682 *LIBL/SYMSGF)
     A            WSKUND         8Y 0B  3 11EDTCDE(Z)
     A                                  2  2MSGCON(008 SHU0908 *LIBL/SYMSGF)
     A            WSUSER        10A  B  2 11
     A                                  2 22MSGCON(010 SHU1159 *LIBL/SYMSGF)
     A                                  1  2MSGCON(004 BBA0070 *LIBL/SYMSGF)
     A            WSDATE         8Y 0B  1 11EDTCDE(4)
     A                                  3 30MSGCON(008 SHU0909 *LIBL/SYMSGF)
     A            WSSTAT         1A  B  3 39
     A                                  4 30MSGCON(017 SHU1266 *LIBL/SYMSGF)
     A            WSTIL         70A  B  4 48CHECK(LC)
     A                                  4  2MSGCON(010 SHU1541 *LIBL/SYMSGF)
     A            WSAVD          4Y 0B  4 14EDTCDE(Z)
     A            WSOPD          7Y 0B  4 19EDTCDE(Z)
     A                                  5  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5 21MSGCON(018 DIV0032 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        70A  O  6  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(RED)
      *
     A          R NYWRK
     A                                      CF03(03 'EXIT')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                  1  2'SYSPED/ARC012'
     A                                      COLOR(BLU)
     A                                  1 17MSGCON(050 SHU0906 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A            WSUSER    R        O  2  2REFFLD(ARKLOGHR/EPUSER *LIBL/ARKLOG-
     A                                      H)
     A                                      COLOR(YLW)
     A                                  3  2MSGCON(015 SHU0907 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  1MSGCON(077 SHU0880 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 62MSGCON(018 DIV0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  98        MSG205        78A  O 24  2MSGID(DIV 0012 SYMSGF)
     A                                      COLOR(WHT)
     A N98                             24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
      *
     A          R VIDERE
     A                                      CF12(12 'Forrige')
     A                                      SLNO(22)
     A                                      CLRL(06)
     A                                      OVERLAY
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  2  2MSGCON(015 SHU1041 *LIBL/SYMSGF)
     A            WSTIL         70A  B  2 19CHECK(LC)
     A  72                                  DSPATR(RI)
     A                                  3  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        70A  O  4  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(RED)
     A N30                              4 40'(C) COPYRIGHT  SYSTEMA A/S  2005  '
     A                                      COLOR(WHT)
