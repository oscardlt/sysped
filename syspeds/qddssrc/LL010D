********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
P6XXX * 01 YBC PTF6 BEVARE GRUPENR I SUBFIL
P06XX * 02 YBC PTF6 SALDO ER ENDRET FRA 5.0 TIL 7.0
********************************************************************
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      REF(*LIBL/LLHEAD LLHEADR)
     A                                      PRINT
     A                                      CA02(02 'SPØR')
     A                                      CA03(03 'Exit')
      *
     A          R NOREC
     A                                      TEXT('Select')
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CA04(04 'SØKING')
     A                                      OVERLAY
     A                                      CA12(12)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/LL010R'
     A                                      COLOR(BLU)
     A                                  1 20MSGCON(40 YBC0970 SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 73DATE
     A                                      EDTCDE(Y)
     A                                  2 20MSGCON(30 YBC0971 SYMSGF)
     A                                      COLOR(WHT)
     A                                  2 73TIME
     A                                  3  2MSGCON(40 DIV2055 SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  2MSGCON(12 YBC0972 SYMSGF)
     A            WSGRP          8Y 0B  5 16EDTCDE(4)
     A  89                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A            WSGNV         30A  O  5 26DSPATR(HI)
     A                                  6  2MSGCON(78 YBC0973 SYMSGF)
     A                                      COLOR(WHT)
     A            WSPNR          8Y 0B  7  4EDTCDE(4)
     A                                      DSPATR(UL)
     A  90                                  DSPATR(HI)
     A                                 23  2MSGCON(18 YBC0974 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(18 DIV0032 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 42MSGCON(10 DIV2056 SYMSGF)
     A                                      COLOR(BLU)
      *
     A          R SUBFIL                    SFL
     A                                      TEXT('SUBFILE')
     A            WSNR           1A  B  8  2VALUES(' ' '1')
N01YBA            GMGRP2         8  0H
     A            GMPNR          8  0O  8  5EDTCDE(4)
     A                                      COLOR(WHT)
S02  A*           PNAVN         14A  O  8 14COLOR(WHT)
S02  A*           SUMMLD         3Y 0O  8 29EDTCDE(4)
S02  A*           SUMSTK         5Y 0O  8 33EDTCDE(4)
S02  A*           SUMSTL         5Y 0O  8 39EDTCDE(4)
N02  A            PNAVN         10A  O  8 14COLOR(WHT)
N02  A            SUMMLD         3Y 0O  8 25EDTCDE(4)
N02  A            SUMSTK         7Y 0O  8 29EDTCDE(4)
N02  A            SUMSTL         7Y 0O  8 37EDTCDE(4)
     A            SUMESK         5Y 0O  8 45EDTCDE(4)
     A            SUMKRT         5Y 0O  8 51EDTCDE(4)
     A            SUMPAL         5Y 0O  8 57EDTCDE(4)
     A            SUMVEK         7Y 2O  8 63EDTCDE(4)
     A            SUMKUB         7Y 3O  8 72EDTCDE(4)
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      TEXT('SUBCTL')
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                      CA04(04 'SØKING')
     A                                      CA05(05 'Forny')
     A                                      CA12(12 'Annuller')
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/LL010R'
     A                                      COLOR(BLU)
     A                                  1 20MSGCON(40 YBC0970 SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 73DATE
     A                                      EDTCDE(Y)
     A                                  2 20MSGCON(30 YBC0971 SYMSGF)
     A                                      COLOR(WHT)
     A                                  2 73TIME
     A                                  3  2MSGCON(40 DIV2057 SYMSGF)
     A                                      COLOR(BLU)
     A            ZRECNO         4S 0O  3 68SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  4  2MSGCON(40 YBC0975 SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  2MSGCON(12 YBC0972 SYMSGF)
     A            WSGRP          8Y 0B  5 16EDTCDE(4)
     A  89                                  DSPATR(HI)
     A                                      DSPATR(UL)
     A            WSGNV         30A  O  5 26DSPATR(HI)
     A                                  6  2MSGCON(78 YBC0973 SYMSGF)
     A                                      COLOR(WHT)
     A            WSPNR          8Y 0B  7  4EDTCDE(4)
     A                                      DSPATR(UL)
     A  90                                  DSPATR(HI)
      *
     A          R BOTREC
     A                                      OVERLAY
     A                                 22  2MSGCON(18 YBC0974 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(018 DIV0032 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 42MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 62MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A          R BLANKREC                  TEXT('Message')
     A                                      OVERLAY
     A            ZMSGTX        79A  O 24  2MSGID(&ZMSGNO SYMSGF)
     A N30                                  COLOR(WHT)
     A  30                                  COLOR(RED)
     A            ZMSGNO         7A  P
