      *********************************************************************
      *  After compiling this RPG MODULE,
      *  create the related program with the following command:
      *
CRTPG+*  CRTPGM SYSPED/STS111 MODULE(*LIBL/STS111 *LIBL/INCGI)
CRTPGM*         ACTGRP(*NEW) AUT(*USE)
      *
      *
      *********************************************************************
      *
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
      *--------------------------------------------------------------------
      * Data base files
      *--------------------------------------------------------------------
     FBRIDF     IF   E           K DISK    usropn
     Fdokuf     UF   E           K DISK    usropn
     Fdokuf7    IF   E           K DISK    usropn
     F                                     rename(DOKUFR:DOKUFR7)
     FDOKUFM    IF   E           K DISK    usropn
     FSTSFBR    UF A E           K DISK    usropn
     FSTSCLI    UF A E           K DISK    usropn
     FSTSCLI3   IF   E           K DISK    USROPN
     F                                     rename(STSCLIR:STSCLIR3)
     FBRPARF    IF   E           k disk    usropn
     FGSSORF    UF   E           K DISK    USROPN
     FGSSORL21  IF   E           K DISK    USROPN
     F                                     rename(GSSORFR:GSSORFR21)
     FGSSOLF    UF   E           K DISK    USROPN
     FGSSOLL04  UF   E           K DISK    USROPN
     F                                     rename(GSSOLFR:GSSOLFR4)
     FGSSPOF    IF   E           K DISK    USROPN
     FGSSTRF    IF   E           K DISK    USROPN
     FGSSORL10  IF   E           K DISK    usropn
     F                                     rename(GSSORFR:GSSORF10R)
      *--------------------------------------------------------------------
      * Prototype defintions and standard system API error structure
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      *--------------------------------------------------------------------
      * Variables common to CGI programs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
      * Variables received from the input string
     D lng             s              2
     D Sendnr35        s             35
     D WSUSER          s             10
     D WSCLID          s             20
     D MANK            s              1
     D WSTOT           s              5  0
     D WSLEST          s              5  0
     D CURTUR          S             11
     D CURTURN         S             11  0
     D xxTNO           S             11  0
     D SlTurTx         s             30
     D SlTurW          s             20
     D Feil            s             80
     D FeilKode        s              1
     D UpdSendNr       s              1
      * andre variabler
     D Fn              s             10
     D Lib             s             10
     D Mbr             s             10
     D ItemCount       s             10i 0
     D SernoC          s             49a
     D i               s             10i 0
     D Spaces          s             12a   inz('&nbsp;&nbsp;')
     D                 DS
     D DF1004                  1     35
     D DF1004sub               8     17
     D                 DS
     D TRDTS                   1      8  0
     D TRDTSsub                5      8
     D                 DS
     D TRAD1                   1     30
     D TRAD1sub                1     10
     D                 DS
     D LocCoTx                 1     50
     D LocCode                 1      5
     D LocTxt                  6     50
     D LocInOut                6      8
     D                 DS
     D PONR                    1     27
     D PONRONR                 1      7
     D PONRPO                  8     27
     D                 DS
     D SENDNR                  1     20
     D SENDNR17                4     20
     D                 DS
     D  XDT                    1      8  0
     D  XDTÅÅ                  1      4  0
     D  XDTMM                  5      6  0
     D  XDTDD                  7      8  0
     D                 DS
     D  XTIME                  1     14  0
     D  XTID                   1      4  0
     D  XDD                    7      8  0
     D  XMM                    9     10  0
     D  XÅÅ                   11     14  0
     D                 DS
     D  FMAI00                 1      2
     D  FMMRK1                 3     37
     D  FMMRK18                3     20
     D  FMCLID                 1     20
      *
      *--------------------------------------------------------------------
      * Prolog common to CGI programs
      * -Receive input from the remote browser
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prolog3
      *=====================================================================******
      * MAIN PROCESS LINE
      *=====================================================================******
      * Parse input string
     C                   EXSR      PARSE
      *
     C                   EXSR      INIT
      *
      * Er ikke riktig innlogget...
     C     *in60         Ifeq      *ON
     C     BIINN         Oreq      ' '
     C                   exsr      SetSkl01B
     C                   callp     wrtsection('top')
     C                   callp     wrtsection('bot')
     c                   goto      slutt
     c                   end
      *
      * Utfør input-behandling...
     C                   exsr      MAINSR
      *
      * Klargjør skjermbilde for ny visning
     C                   exsr      SetSkl11
      *
      * Read skeleton output html, etc.
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
      * 1-Section /$top
     C                   exsr      SetTop
     C                   callp     wrtsection('top')
     C     SetPO         IFEQ      'J'
     C                   EXSR      SLTPO
     C                   Else
      * kun for at inp.param PONR skal være defienrt (brukes i javascript)
     C                   callp     wrtsection('posltdummy')
     C                   END
     C                   callp     updHTMLvar('SetPO':SetPO)
     C                   callp     wrtsection('bot')
      *
     c     Slutt         tag
     C                   callp     wrtsection('endhtml')
     C                   callp     wrtsection('*fini')
      *
     C                   exsr      EXIT
     c
      *
      *********************************************************
     C     EXIT          begsr
      *********************************************************
     C                   close     BRIDF
     c                   close     dokuf
     c                   close     dokuf7
     C                   close     DOKUFM
     C                   close     STSFBR
     C                   close     STSCLI
     C                   close     STSCLI3
     C                   close     BRPARF
     C                   close     GSSORF
     C                   close     GSSORL21
     C                   close     GSSOLF
     C                   close     GSSOLL04
     C                   close     GSSPOF
     C                   close     GSSORL10
     C                   close     GSSTRF
     C                   eval      *inlr = *on
     C                   RETURN
     C                   ENDSR
      *
      *=====================================================================******
      * Generate selection box for PO's
      *=====================================================================******
     C     SLTPO         begsr
     C                   callp     wrtsection('posltstr')
      * Sjekk om det er "TOMT", slå i så fall av selection
     c                   move      'N'           SetPO             1
     C     ORTNO         setll     GSSORL21
     C     ORTNO         reade     GSSORL21                               33
     C     *in33         Doweq     '0'
     C     ORONO         setll     GSSOLF
     C     ORONO         reade(N)  GSSOLF                                 34
     C     *in34         Doweq     '0'
     C     OLMN          IFEQ      *blanks
     c                   move      'J'           SetPO
     C     ORONO         CHAIN     GSSPOF                             35
     C   35              MOVE      *BLANKS       POCONO
      *(låner inp-variabel-ds for å komponer selection/option-felt
     c                   move      ORONO         PONRONR
     c                   move      POCONO        PONRPO
     C                   callp     updhtmlvar('SLPO':PONR)
     C                   callp     updhtmlvar('SLPOTXT':POCONO)
     C                   callp     wrtsection('posltrow')
     c                   leave
     C                   END
     C     ORONO         reade(N)  GSSOLF                                 34
     C                   END
     C     ORTNO         reade     GSSORL21                               33
     C                   END
     C*
      * hvis TOMT legg ut en linje med " " / "*FINISHED*"
     C     SetPO         IFEQ      'N'
     C                   callp     updhtmlvar('SLPO':spaces)
     C                   callp     updhtmlvar('SLPOTXT':'*FINISHED*')
     C                   callp     wrtsection('posltrow')
     C                   END
      *
     C                   callp     wrtsection('posltend')
     C                   endsr
      *=====================================================================******
      * Parse input
      *=====================================================================******
     C     Parse         begsr
     C                   eval      SENDNR = zhbgetvar('SENDNR')
     C                   eval      wsCLID = zhbgetvar('CLID')
     C                   eval      MANK = zhbgetvar('MANK')
     C                   eval      wsuser = zhbgetvar('USER')
     C                   eval      PONR   = zhbgetvar('PONR')
     C                   eval      SETPO  = zhbgetvar('SETPO')
     C                   eval      CURTUR = zhbgetvar('CURTUR')
     C                   eval      CURTURN = c2n2(CURTUR)
     C                   eval      UpdSendNr = zhbgetvar('UpdSendNr')
     C                   endsr
      *=====================================================================******
      * Set html output skeleton member before its read into memory
      *=====================================================================******
     C     SetSkl01B     begsr
     C* inngangsbilde for valg av fraktbrevsnr.
     C                   eval      lng = uppify(lng)
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC' + lng
     C                   eval      Mbr = 'STS111I'
      *
     C                   endsr
      *=====================================================================******
      * Set html output skeleton member before its read into memory
      *=====================================================================******
     C     SetSkl11      begsr
      * scannerbilde
     C                   eval      lng = uppify(lng)
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC' + lng
     C                   eval      Mbr = 'STS111'
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$top
      *  (search criteria)
      *=====================================================================******
     C     SetTop        begsr
      * Repeat national language for the next invocation
     C                   callp     updhtmlvar('LNG':lng:
     C                             InitHTMLVars)
     C     FBRKEY        CHAIN(N)  STSFBR                             33
     C                   callp     updhtmlvar('WSLEST':
     C                             %trim(%editc(SFANKK:'3')))
     C                   callp     updhtmlvar('WSTOT':
     C                             %trim(%editc(SFANKI:'3')))
     c     sfankk        Ifgt      0
     c     sfankk        andeq     sfanki
     C                   callp     updhtmlvar('ENDTXT':'  End   ')
     C                   callp     updhtmlvar('ENDCODE':'U')
     c                   else
     C                   callp     updhtmlvar('ENDTXT':'  End   ')
     C                   callp     updhtmlvar('ENDCODE':'E')
     c                   end
     c     SFSTAT        IfEQ      'F'
     C                   callp     updhtmlvar('ENDTXT':' Abort ')
     C                   callp     updhtmlvar('ENDCODE':'E')
     c                   end
BODY C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updhtmlvar('USER':WSUSER)
     C                   MOVE      *BLANKS       WSCLID
     C                   callp     updhtmlvar('LOCATION':LocTxt)
     C                   callp     updhtmlvar('SENDNR':SENDNR)
     C                   callp     updhtmlvar('MOTT':DFNAVM)
     C                   callp     updhtmlvar('CLID':WSCLID)
     C                   callp     updHTMLvar('FEILTXT':FEIL)
     C                   callp     updHTMLvar('FEILKODE':FEILKODE)
     C                   callp     updhtmlvar('YTY':YTYPE)
     C                   callp     updhtmlvar('YPR':
     C                             %trim(%editc(YPRO:'Z')))
     C                   callp     updhtmlvar('AVD':
     C                             %trim(%editc(DFAVD:'Z')))
     C                   callp     updhtmlvar('OPD':
     C                             %trim(%editc(DFOPD:'Z')))
     C                   callp     updhtmlvar('FBN':
     C                             %trim(%editc(DFFBNR:'Z')))
      *
     C                   endsr
     C***********************************************
     C     MAINSR        BEGSR
     C***********************************************
     C                   Move      *blanks       Feil
     C                   Move      *blanks       FeilKode
      *
      * ingenting lest (f.eks retur fra kolli-oversikt)
      * kun bygg/vis bildet på ny - ingen behandling i denne syklusen.
     c     wsclid        IFEQ      *blanks
     c                   goto      utles
     c                   end
      *
      * 99 = Første kall - hent fraktbrev/kolli - Vis bilde..
      * 98 = Bedt om å slette/gjenoppbygge åpen (nullstille)
     c     wsclid        IFEQ      '99'
     c     wsclid        OREQ      '98'
     c                   exsr      HFBR
     c                   goto      utles
     c                   end
      *
      * 97 = Avslutt med oppdatering
     c     wsclid        IFEQ      '97'
     c                   exsr      CLOSEWB
     c                   goto      utles
     c                   end
      * 96 = Merk resten manko
     c     wsclid        IFEQ      '96'
     c                   exsr      ALLMA
     c                   goto      utles
     c                   end
      *
      * ellers - Gyldig Kolli-ID skal være scannet
      * må begynne på 003..
     C                   MOVEL     WSCLID        XWSCLID           2
     C     XWSCLID       IFNE      '00'
     C                   eval      feil = wsclid + ' is invalid ' +
     C                                    'Must start 00..'
     C                   eval      feilKode = 'X'
     c                   goto      utles
     c                   end
      *
      * må finnes i skytefila
     C     CLIKEY        CHAIN     STSCLI                             30
     c     *in30         ifeq      '1'
      * men om kolliet finnes på PO som ikke alt er satt på tur...
     C                   exsr      AddPo
      *
     c     *in30         ifeq      '1'
     C                   eval      feil = wsclid + ' does not exist ' +
     C                                    'on this waybill.'
     C                   eval      feilKode = 'X'
     c                   goto      utles
     C                   end                                                    1
     C                   end                                                    1
      *
      * Må ikke være lest før
     C     SCSTAT        IFEQ      'X'                                           2<-B
     C     SCSTAT        OREQ      'M'                                           2<-B
     C                   UPDATE    STSCLIR
     C                   eval      feil = wsclid + ' is already ' +
     C                                    'scanned..'
     C                   eval      feilKode = 'X'
     c                   goto      utles
     C                   end                                                     2
      *
      * OK kolli lest - Oppdater / reduser DIFF med 1                  !
      *
     C                   TIME                    XTIME
     C                   MOVE      XDD           XDTDD
     C                   MOVE      XMM           XDTMM
     C                   MOVE      XÅÅ           XDTÅÅ
     C                   MOVE      XDT           SCDT
     C                   MOVE      XTID          SCTID
     C                   MOVE      WSUSER        SCUSER
     C     MANK          IFEQ      'M'
     C                   MOVE      'M'           SCSTAT
     C                   else
     C                   MOVE      'X'           SCSTAT
     C                   end
     C                   UPDATE    STSCLIR
      * Hvis kolli-id ikke er koblet mot PO ennå - oppdater det nå
     c     SetPo         Ifeq      'J'
     c                   exsr      UPDPO
     c                   end
      *
      * Oppdater Nivået over (Fraktbr.nivå..)
     C     FBRKEY        CHAIN     STSFBR                             33
     C     SFANKK        IFLT      SFANKI                                         3<-B
     C                   ADD       1             SFANKK                          *FIL
     C                   end
     C     SFANKK        IFGE      SFANKI                                         3<-B
     C                   MOVE      'X'           SFSTAT
     C                   eval      feil = 'Scan complete (So far). Press End ' +
     C                                 'to exit (Update/leave for later scan.)'
     C                   eval      feilKode = 'X'
     C                   END                                                     2<-E
     C                   UPDATE    STSFBRR
      *
     C     Utles         tag
      *
     C                   ENDSR
     C***********************************************
     C     HFBR          BEGSR
     C***********************************************
     C     FBRKEY        CHAIN     STSFBR                             33
      * Fraktbrev finnes fra før i skytefila ...
     C     *in33         IFEQ      *OFF
      * Status FERDIG
     C     SFSTAT        IFEQ      'F'
     C                   eval      feil = 'This waybill is already ' +
     C                                    'Finished.  Press Abort.'
     C                   eval      feilKode = 'X'
     c                   Update    stsfbrr
     c                   goto      utHFBR
     c                   else
      * Status ÅPEN
      * Jobbe videre med eksist. der ingen er skutt -> full gjennoppb.
     c     Wsclid        IFEQ      '99'
     C     SFANKK        ANDEQ     *zeros
     c                   Movel     '98'          WsClid
     c                   end
      * Kommer inn med 98 - har bedt om sletting(bygger ny fra bunn)    ÅPEN
     c     Wsclid        IFEQ      '98'
     c                   delete    stsfbrr
     c                   exsr      delcli
     c                   else
      *   Jobb videre på esisterende (men legg til nye evt nye..)
     C                   exsr      HCLI
     c                   Update    stsfbrr
     c                   goto      utHFBR
     c                   end
     c                   end
     c                   end
      *
     C                   CLEAR                   STSFBRR
     C                   MOVE      YTYPE         SFTYPE
     C                   MOVE      YPRO          SFPRO
     C                   MOVE      DFAVD         SFAVD
     C                   MOVE      DFOPD         SFOPD
     C                   MOVE      DFFBNR        SFFBNR
      *
     C     LocInOut      IFEQ      'OUT'
     c                   move      'N'           RefrJN            1
     C                   EXSR      HCLI
     C                   ELSE
     C                   EXSR      HCLIIN
     C                   END
      *
     C                   WRITE     STSFBRR
      *
     C     UTHFBR        ENDSR
      *
     C***********************************************
     C     HCLI          BEGSR
     C***********************************************
     C     DOKFMK        SETLL     DOKUFM
     C     DOKFMK        READE     DOKUFM                                 33
     C     *IN33         DOWEQ     '0'
      * Kolli kan finnes fra før (v refresh av eksisterende..)
     C                   eval      XXCLID = '00'+FMMRK1
     C     GmCliKey      Chain(N)  STSCLI                             33
     c     *in33         CABEQ     '0'           NxtCli
     C                   ADD       1             SFANKI
     C                   CLEAR                   STSCLIR
     C                   MOVE      SFTYPE        SCTYPE
     C                   MOVE      YPRO          SCPRO
     C                   MOVE      FMAVD         SCAVD
     C                   MOVE      FMOPD         SCOPD
     C                   MOVE      FMFBNR        SCFBNR
     C                   MOVE      FMCLID        SCCLID
     C                   WRITE     STSCLIR
     c     NxtCli        TAG
     C     DOKFMK        READE     DOKUFM                                 33
     C  N33              ENDDO
      *
      * Hent også frie lapper rett fra ordresystem
     C     ORTNO         setll     GSSORL21
     C     ORTNO         reade     GSSORL21                               33
     C     *in33         Doweq     '0'
     c                   exsr      HCLI2
     C     ORTNO         reade     GSSORL21                               33
     C                   END
      *
     C                   ENDSR
     C***********************************************
     C     HCLIIN        BEGSR
     C***********************************************
      * Inngående skyting - legge ut de som VAR skutt på utgående
      * (antar at det SKAL finnes ETT og bare ETT "sett" med kollier
      *  som har en ANNEN "SCTYPE" (sted skutt) enn egen, må være UTGÅENDE)
      * (en trip kan jo ha max 2 skytinger utgående / inngående..)
     C     Cli3Key       SETLL     STSCLI3
     C     Cli3Key       READE     STSCLI3                                33
     C     *IN33         DOWEQ     '0'
     C     SCSTAT        IFEQ      'X'
     C     SCTYPE        ANDNE     SFTYPE
     C                   ADD       1             SFANKI
     C                   MOVE      SFTYPE        SCTYPE
     C                   MOVE      YPRO          SCPRO
     C                   MOVE      SFAVD         SCAVD
     C                   MOVE      SFOPD         SCOPD
     C                   MOVE      SFFBNR        SCFBNR
     C                   MOVE      ' '           SCSTAT
     C                   WRITE     STSCLIR
     C                   END
     C     Cli3Key       READE     STSCLI3                                33
     C  N33              ENDDO
     C                   ENDSR
      *
     C***********************************************
     C     DELCLI        BEGSR
     C***********************************************
     C     FBRKEY        SETLL     STSCLI
     C     FBRKEY        READE     STSCLI                                 33
     C     *IN33         DOWEQ     '0'
     C                   DELETE    STSCLIR
     C     FBRKEY        READE     STSCLI                                 33
     C  N33              ENDDO
      *
     C                   ENDSR
     C***********************************************
     C     ALLMA         BEGSR
     C***********************************************
     C     FBRKEY        SETLL     STSCLI
     C     FBRKEY        READE     STSCLI                                 33
     C     *IN33         DOWEQ     '0'
     C     SCSTAT        IFEQ      ' '
     C     SCSTAT        OREQ      'E'
     C                   TIME                    XTIME
     C                   MOVE      XDD           XDTDD
     C                   MOVE      XMM           XDTMM
     C                   MOVE      XÅÅ           XDTÅÅ
     C                   MOVE      XDT           SCDT
     C                   MOVE      XTID          SCTID
     C                   MOVE      WSUSER        SCUSER
     C     MANK          IFEQ      'M'
     C                   MOVE      'M'           SCSTAT
     C                   else
     C                   MOVE      'X'           SCSTAT
     C                   end
     C                   UPDATE    STSCLIR
      * Hvis kolli-id ikke er koblet mot PO ennå - oppdater det nå
     c     SetPo         Ifeq      'J'
     c                   exsr      UPDPO
     c                   end
      * Oppdater Nivået over (Fraktbr.nivå..)
     C     FBRKEY        CHAIN     STSFBR                             33
     C     SFANKK        IFLT      SFANKI                                         3<-B
     C                   ADD       1             SFANKK                          *FIL
     C                   end
     C     SFANKK        IFGE      SFANKI                                         3<-B
     C                   MOVE      'X'           SFSTAT
     C                   eval      feil = 'The scan is complete. ' +
     C                                    'Press Update to close the waybill.'
     C                   eval      feilKode = 'X'
     C                   END                                                     2<-E
     C  N33              UPDATE    STSFBRR
     C                   end
     C     FBRKEY        READE     STSCLI                                 33
     C  N33              ENDDO
      *
     C                   ENDSR
     C***********************************************
     C     AddPO         BEGSR
     C***********************************************
      * HAR STØTT PÅ CLL-ID SOM IKKE ER I SKYTEFILA.
      * Sjekk om kolli finnes i GSSOLF .. OG aktuell PO er på rett LOCATION
      * Og aktuell PO er IKKE på annen tur
      * i så fall legg inn PO'en i TUREN - legg alle kolli-ID inn i skytelis
      * (dette er tilatt kun ved UTGÅENDE SKYTING!!! - testen mot at PO'en
      *  må være på samme LOCATION (ORLC om den er Åpen/ ORLC2 om avsluttet-
      *  ONWAY) som "skyteren" sørger for det...)
     C                   movel     WSCLID        OLMN
     C     OLMN          Chain     Gssoll04                           33
     C     *IN33         CABEQ     '1'           UtAddPO
     C     OLONO         Chain(N)  GSSORF                             33
     C     *IN33         CABEQ     '1'           UtAddPO
     C     SHIPFROM      IFNE      ORLC
     C     SHIPFROM      ANDNE     ORLC2
     C                   goto      UtAddPO
     C                   end
     c                   movel     dfavd         xxTNO
     c                   move      dfopd         xxTNO
     c     ORTNO         IFNE      *ZEROS
     c     ORTNO         ANDNE     xxTNO
     c                   Movel     ORTNO         Cl2Avd            4 0
     c                   Move      ORTNO         Cl2Opd            7 0
      * KAN kolli være "manko" på en tidligere tur?? (ikke alt fikk plass?)
     C     ManCliK       CHAIN(N)  STSCLI                             33
     C     *in33         IFEQ      '0'
     C     SCSTAT        andeq     'M'
      * Legg alle kolli andre mankokolli fra den turen inn i skyteliste
     C     FBRKEY        CHAIN     STSFBR                             33
     C     *IN33         IFEQ      *OFF
     C                   EXSR      HCLI3
     C                   UPDATE    STSFBRR                                       *FIL
      * Nå skal aktuell kolli være i skytefila
     C     CLIKEY        CHAIN     STSCLI                             30
     C     *IN30         IFEQ      '0'
     C                   Move      WSCLID        clid10           10
     C                   Movel     ORTNO         ShL               9
     C                   eval      feil = clid10 + ' is MANKO on ' +
     C                                    'Sh.L. '+ShL+'. '+AntExA+
     C                                    ' Cll Added as Extra on y. S.L.!  '
     C                   eval      feilKode = 'X'
     C                   END
     C                   END
     c                   end
     c                   goto      UtAddPO
     c                   end
      * Kolliet IKKE i skytefila, tilhører rett LOC/ikke fremmed tur
     C* legg inn på turen.
     c     ORTNO         IFEQ      *Zeros
     C     OLONO         Chain     GSSORF                             33
     c                   move      '1'           ORST
     c     ORST2         ifeq      'IPR'
     c                   movel     'RFP'         ORST2
     c                   end
     c                   move      xxTNO         ORTNO
     c                   update    GSSORFR
     c                   end
      *
      * Legg alle kolli inn i skyteliste
     C     FBRKEY        CHAIN     STSFBR                             33
     C     *IN33         IFEQ      *OFF
     C                   EXSR      HCLI2
     C                   UPDATE    STSFBRR                                       *FIL
      * Nå skal aktuell kolli være i skytefila
     C     CLIKEY        CHAIN     STSCLI                             30
     C                   END
      *
      *
     C     UtAddPO       ENDSR
     C***********************************************
     C     HCLI2         BEGSR
     C***********************************************
     C     ORONO         SETLL     GSSOLF
     C     ORONO         READE(N)  GSSOLF                                 33
     C     *IN33         DOWEQ     '0'
     C     OLMN          IFNE      *BLANKS
      * Kolli kan finnes fra før (v refresh av eksisterende..)
     C                   eval      XXCLID = OLMN
     C     GmCliKey      Chain(N)  STSCLI                             33
     c     *in33         CABEQ     '0'           NxtCli2
     C                   ADD       1             SFANKI
     C                   CLEAR                   STSCLIR
     C                   MOVE      SFTYPE        SCTYPE
     C                   MOVE      YPRO          SCPRO
     C                   MOVE      SFAVD         SCAVD
     C                   MOVE      SFOPD         SCOPD
     C                   MOVE      SFFBNR        SCFBNR
     C                   MOVEL     OLMN          SCCLID
     C                   WRITE     STSCLIR
     c     NxtCli2       TAG
     C                   END
     C     ORONO         READE(N)  GSSOLF                                 33
     C  N33              ENDDO
      *
     C                   ENDSR
      *
     C***********************************************
     C     HCLI3         BEGSR
     C***********************************************
     C                   Move      *zeros        AntExt            2 0
     C     ManCliK2      SETLL     STSCLI
     C     ManCliK2      READE(N)  STSCLI                                 33
     C     *IN33         DOWEQ     '0'
     C     SCSTAT        IFEQ      'M'
     C                   ADD       1             SFANKI
     C                   MOVE      SFTYPE        SCTYPE
     C                   MOVE      YPRO          SCPRO
     C                   MOVE      SFAVD         SCAVD
     C                   MOVE      SFOPD         SCOPD
     C                   MOVE      SFFBNR        SCFBNR
     C                   MOVE      'E'           SCSTAT
     C                   WRITE     STSCLIR
     c                   add       1             AntExt
     C                   END
     C     ManCliK2      READE(N)  STSCLI                                 33
     C  N33              ENDDO
     c                   Move      AntExt        AntExA            2
      *
     C                   ENDSR
      *
     C***********************************************
     C     CLOSEWB       BEGSR
      ***********************************************
     C     FBRKEY        CHAIN     STSFBR                             33
     C     *in33         IFEQ      *OFF
     C     SFSTAT        IFEQ      'X'
     C                   Move      'F'           SFSTAT
     C                   eval      feilKode = 'F'
     c                   movel     DFAVD         UUTUR
     c                   move      DFOPD         UUTUR
     c                   call      'STS113'
     c                   parm                    UUTUR            11
     c                   parm                    LocInOut
     c                   parm                    LocCode
     C                   PARM                    WSUSER
     C                   else
     C                   eval      feil = 'This waybill does not ' +
     C                                    'have propper status for closing.'
     C                   eval      feilKode = 'X'
     C                   END
     C                   UPDATE    STSFBRR
     C                   END
     C                   ENDSR
     C***********************************************
     C     UPDPO         BEGSR
     C***********************************************
      *
     C                   eval      ORONO   = c2n2(PONRONR)
     C     ORONO         CABEQ     *ZEROS        UTUPDPO
     C     ORONO         setll     GSSOLF
     C     ORONO         reade     GSSOLF                                 34
     C     *in34         Doweq     '0'
     C     OLMN          IFEQ      *blanks
     C                   MOVEL     WSCLID        OLMN
     C                   update    GSSOLFR
     C                   leave
     C                   END
     c                   update    GSSOLFR
     C     ORONO         reade     GSSOLF                                 34
     c                   END
     C
      *
     C     UTUPDPO       ENDSR
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
      *
     C                   open      BRIDF
     C     WSUSER        CHAIN     BRIDF                              60
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   CALLB     'INCGI'
     C                   PARM                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
     c                   open      dokuf
     c                   open      dokuf7
     C                   open      DOKUFM
     C                   open      STSFBR
     C                   open      STSCLI
     C                   open      STSCLI3
     C                   open      BRPARF
     C                   open      GSSORF
     C                   open      GSSORL21
     C                   open      GSSOLF
     C                   open      GSSOLL04
     C                   open      GSSPOF
     C                   OPEN      GSSORL10
     C                   open      GSSTRF
      * definer KEYS m.m.
      *
     C     DOKFMK        KLIST
     C                   KFLD                    DFRI
     C                   KFLD                    DFAVD
     C                   KFLD                    DFOPD
     C                   KFLD                    DFFBNR
     C                   MOVE      'F'           DFRI
     C     DokufKey      klist
     C                   kfld                    DFRI
     C                   kfld                    DFAVD
     C                   kfld                    DFOPD
     C                   kfld                    DFFBNR
     c                   z-add     1             DFFBNR
     C     *LIKE         DEFINE    SCTYPE        YTYPE
     C     *LIKE         DEFINE    SCPRO         YPRO
     C     FBRKEY        KLIST
     C                   KFLD                    YTYPE
     C                   KFLD                    YPRO
     C                   KFLD                    DFAVD
     C                   KFLD                    DFOPD
     C                   KFLD                    DFFBNR
     C     CLIKEY        KLIST
     C                   KFLD                    YTYPE
     C                   KFLD                    YPRO
     C                   KFLD                    DFAVD
     C                   KFLD                    DFOPD
     C                   KFLD                    DFFBNR
     C                   KFLD                    WSCLID
     C     GmCliKey      KLIST
     C                   KFLD                    YTYPE
     C                   KFLD                    YPRO
     C                   KFLD                    DFAVD
     C                   KFLD                    DFOPD
     C                   KFLD                    DFFBNR
     C                   KFLD                    XXCLID           20
     C     Cli3Key       KLIST
     C                   KFLD                    YPRO
     C                   KFLD                    DFAVD
     C                   KFLD                    DFOPD
     C                   KFLD                    DFFBNR
     C     ManCliK       KLIST
     C                   KFLD                    YTYPE
     C                   KFLD                    YPRO
     C                   KFLD                    Cl2AVD
     C                   KFLD                    Cl2OPD
     C                   KFLD                    DFFBNR
     C                   KFLD                    WSCLID
     C     ManCliK2      KLIST
     C                   KFLD                    YTYPE
     C                   KFLD                    YPRO
     C                   KFLD                    Cl2AVD
     C                   KFLD                    Cl2OPD
     C                   KFLD                    DFFBNR
      * GSSOR10 inneholder statusene IPR/RPF med her leses bare turnr > 0
     C     GSSOR10K      klist
     C                   kfld                    SHIPFROM          5
     C                   kfld                    XXTUR            11 0
     C                   Z-add     1             XXTUR
     C     GSSOR10KE     klist
     C                   kfld                    SHIPFROM          5
      *
     C     BrParKey      klist
     C                   kfld                    PABRID
     C                   kfld                    PAKUNR
     C                   kfld                    PAID
     C                   MOVE      BIBRID        PABRID
     C                   MOVE      *zeros        PAKUNR
      * Hent fabrikk kode / Navn
     C                   MOVE      'SSFAB'       PAID
     C     BrParKey      Chain     BRPARF                             33
     c  n33              MoveL     PAVRDA        SHIPFROM
      * Hent "location" som denne userid kan skyte på
     C                   MOVE      'SSSCA'       PAID
     C     BrParKey      Chain     BRPARF                             33
     c  n33              MoveL     PAVRDA        LocCoTx
     c                   move      LocCode       YTYPE
      *
     C                   move      '00'          FMAI00
      *
      *
      *
      * Har plukket tur.nr for å finne send.nr ...
     c     CURTURN       IFNE      *ZEROS
     c     SENDNR        ANDEQ     *BLANKS
     c                   movel     CURTURN       dfavd
     c                   move      CURTURN       dfopd
     c     DokufKey      chain(N)  DOKUF                              33
     C     *in33         IFEQ      *OFF
     C     DF1004        ANDNE     *Blanks
     C                   eval      SENDNR   = '401' + DF1004
     C                   Move      *zeros        CURTURN
     C                   else
     C                   eval      feil = 'NO Wayb.nbr on this Shipment list. '+
     C                                    'Key/scan Wb.nbr and try again'
     C                   eval      feilKode = 'X'
     C                   end
     C                   end
      *** OBS!!!
      *** her bør det være kontroll mot at en ikke tar opp en tur som en
      *** IKKE har noe med å gjøre direkte via send.nr...
      *** Chaine tilbake til GSSTRF - sjekke at HUB er rett....
      *** Videre bør det OM en har sendt inn både sendnr  (som finnes)
      ***   OG TURNR og dette eren annen "gjøres noe" med sendingsnummert
      ***   på det fraktbrevet som alt finnes....(blankes...) før en evt
      ***   oppdaterer dette sendingsnr fraktbrevet på den "nye" turen....
     c                   eval      sendnr35 = SENDNR17
     c     Sendnr35      chain     dokuf7                             60
     C  N60sendnr        COMP      *BLANKS                                60
      * kommer inn med ukjent sendnr (=fremmed) Vis ing.bilde på ny for
     c     *in60         Ifeq      *on
     c     wsclid        andeq     '99'
     c                   exsr      VelgViaTur
     c                   end
      * sjekk om kolliene er koplet til PO. (MEN BARE VED "INNGANG")
      * 1. finn alle PO-er på trippen, 2.sjekk om ALLE KLL har EAN-nr.
     c                   movel     dfavd         ORTNO
     c                   move      dfopd         ORTNO
     C     WSCLID        IFEQ      '99'
     c                   move      'N'           SetPO             1
     C     ORTNO         setll     GSSORL21
     C     ORTNO         reade     GSSORL21                               33
     C     *in33         Doweq     '0'
     C     ORONO         setll     GSSOLF
     C     ORONO         reade(N)  GSSOLF                                 34
     C     *in34         Doweq     '0'
     C     OLMN          IFEQ      *blanks
     c                   move      'J'           SetPO
     C                   goto      UtTstPO
     C                   END
     C     ORONO         reade(N)  GSSOLF                                 34
     C                   END
     C     ORTNO         reade     GSSORL21                               33
     C                   END
     c     UtTstPO       tag
     C                   END
      *
     C                   ENDSR
     C***********************************************
     C     VelgViaTur    BEGSR
     C***********************************************
     c                   move      *off          *in60
      * Har lest via turnr men samtidig send med sendingsnr..
      * Oppdater sendingsnr i fraktbrevet hvis blank fra før eller hvis
      * en bekrefter at en ønsker å oppdater (endre eksisterende)
     c     CURTURN       IFNE      *ZEROS
     c     SENDNR        ANDNE     *BLANKS
     c                   movel     CURTURN       dfavd
     c                   move      CURTURN       dfopd
     c     DokufKey      chain     DOKUF                              33
     c     *in33         IfEQ      *off
     C     DF1004        IFEQ      *BLANKS
     C     UpdSendNr     OREQ      'Y'
     c                   movel     sendnr17      DF1004
     C                   else
      * Melding hvis det allerde finnes send.nr (og endring ikke bekreftet)
     C     DF1004        IFNE      *BLANKS
     C                   eval      feil = 'This ship. list already has Wb# (' +
     C                                     DF1004+') Tick the checkbox and ' +
     C                                    'try again to overwrite.'
     C                   eval      feilKode = 'X'
     C                   end
     C                   end
     c                   update    DOKUFR
     C                   GOTO      UTTUR
     c                   end
     c                   end
      *
      * DER ER IKKE VALGT (ET GYLDIG) TRIP-NR/=DOKUF-RECORD..
      * KLARGJØR HTML INKL PULLDOWNBOKS MED TURER - VIS INNG.BILDE PÅ NY.
     C     SENDNR        IFNE      *BLANKS
     C                   eval      feil = 'Unknown Waybill. Select a ship. ' +
     C                                    'list from the pulldown box to ' +
     C                                    'be matched/updated with the wayb.'
     C                   eval      feilKode = 'X'
     C                   END
      * Set/Read skeleton output html, etc.
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC'
     C                   eval      Mbr = 'STS111I'
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
      * sett html-variable
     C                   callp     updhtmlvar('USER':WSUSER)
     C                   callp     updhtmlvar('SENDNR':SENDNR)
      * Write HTML-section
     C                   callp     wrtsection('top')
     C                   callp     updhtmlvar('CURTUR':CURTUR)
     C                   callp     wrtsection('typslthea')
      * Vis turer (men bare de med minst 1 ordre på..) i pulldown..
      * (kunne gått via turregisteret - ville da fått også tomme turer)
     C     strtur        tag
     c     GSSOR10K      setll     GSSORL10
     c     GSSOR10KE     READE     GSSORL10                               33
     c     *IN33         IFeq      '0'
     C                   CLEAR                   TRDTS
     C                   CLEAR                   TRLOCT
     C                   CLEAR                   TRAD1
     C                   CLEAR                   DF1004
     c     ORTNO         chain     GSSTRF                             34
     c     *IN34         IFeq      '0'
     c                   movel     ortno         dfavd
     c                   move      ortno         dfopd
     c     DokufKey      chain(N)  DOKUF                              34
     c                   end
     C     DF1004        IFNE      *Blanks
     C                   eval      SLTURW  = 'W:' + DF1004sub
     c                   else
     C                   eval      SLTURW  = ''
     c                   end
     C                   eval      SLTURTX = 'D:'+TRDTSsub+ ' To:'+TRLOCT+' '+
     C                             TRAD1sub
     C                   callp     updhtmlvar('SLTUR':
     C                             %trim(%editc(ORTNO:'Z')))
     C                   callp     updhtmlvar('SLTURW':SLTURW)
     C                   callp     updhtmlvar('SLTURTX':SLTURTX)
     C                   callp     wrtsection('typsltrow')
     c     ORTNO         add       1             XXTUR
     c                   goto      strtur
     C                   end
      *
     C                   callp     updHTMLvar('FEILTXT':FEIL)
     C                   callp     updHTMLvar('FEILKODE':FEILKODE)
     C                   callp     wrtsection('typsltbot')
     C                   callp     wrtsection('bot')
      * dummy fini-section
     C                   callp     wrtsection('*fini')
      *
     C                   EXSR      EXIT
     C     UTTUR         ENDSR
