      *********************************************************************
      *  Webservice for spørring på arkiverte fakturaer på EN kunde
      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/SWSFAKTL MODULE(*LIBL/SWSFAKTL
CRTPGM*         *LIBL/INCGI) ACTGRP(*NEW) AUT(*USE)
      *
      *********************************************************************
      * MAIN PROGRAM FLOW
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
     Farkivl04  IF   E           K DISK    usropn
     Farkivl05  IF   E           K DISK    usropn
     F                                     RENAME(ARKIVR:ARKI05R)
     FFIRMARC   IF   E           K DISK    usropn
     FBRIDF     UF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Prototype defintions and standard system API error structure
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      *--------------------------------------------------------------------
      * Variables common to CGI programs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
     D string          s          32000
      * Variables received from the input string
     D userEnv         s             10
     D SYSKNR          S              8
     D SYSKNN          S              8  0
     D OddEven         s             10
     D lng             s              2
     D BckGnd          s             10
     D WSNA            S             30
     D U_name          S             10
     D DATOF           s              8
     D DATOFN          s              8  0
     D DATOT           s              8
     D DATOTN          s              8  0
     D FakNr           s              7
     D FakNrN          s              7  0
     D Arfak           s             40
     D ANTSND          s              5  0
     D MaxSN           s              5
     D MaxSND          s              5  0
     D ANTRCD          s              5  0
     D MaxRC           s              5
     D MaxRCD          s              5  0
     D Spaces          s             12a   inz('&nbsp;&nbsp;')
     D IfsMultIndicators...
     d                 ds
     D  NoErrors                       n
     D  NameTooLong                    n
     D  NotAccessible                  n
     D  NoFilesUsable                  n
     D  DupSections                    n
     D  FileIsEmpty                    n
      *
      *
      *--------------------------------------------------------------------
      * Prolog common to CGI programs
      * -Receive input from the remote browser
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prolog3
      *=====================================================================******
      * MAIN PROCESS LINE
      *=====================================================================******
      * Get program start time for calculating execution time
      *
      /free
         exsr Parse;
         exsr INIT;
       // hent html-mal
         gethtmlifs('/syspeddev/html/XML.html':'/CGITAG/');
       // for debug:
        AUTHTYPE=getenv('AUTH_TYPE':qusec);
        RMTUSER =getenv('REMOTE_USER':qusec);

       // skriv toppen
         wrtsection('top');

         string=
           '<?xml version="1.0" encoding="utf-8" standalone="yes"?>';
         updHTMLvar2('string':%addr(string):%len(string));
         wrtsection('lin');

         string=
           '<GetInvoiceList>';
         updHTMLvar2('string':%addr(string):%len(string));
         wrtsection('lin');

         exsr HentFl;

         string=
           '</GetInvoiceList>';
         updHTMLvar2('string':%addr(string):%len(string));
         wrtsection('lin');


         exsr Exit;
         *inlr = *on;
         return;
      /end-free
      *
      *
      *=====================================================================******
      * Parse input
      *=====================================================================******
     C     Parse         begsr
      * UserEnvironment (dataasemiljø)
     C                   eval      userEnv = zhbgetvar('userEnv')
      *
     C                   eval      SYSKNR   = zhbgetvar('SYSKNR')
     C                   eval      SYSKNN   = c2n2(SYSKNR)
     C                   eval      DATOF    = zhbgetvar('DATOF')
     C                   eval      DATOFN   = c2n2(DATOF)
     C                   eval      DATOT    = zhbgetvar('DATOT')
     C                   eval      DATOTN   = c2n2(DATOT)
     C                   eval      FAKNR    = zhbgetvar('FAKNR')
     C                   eval      FAKNRN   = c2n2(FAKNR)
      *
     C                   endsr
      *=====================================================================******
      * HENT ORDRE
      *=====================================================================******
     C     HENTFL        BEGSR
      *
     C                   Move      *zeros        AntSnd
     C                   Move      *zeros        AntRcd
     C     MaxSnd        IFEQ      *ZEROS
     C                   Z-add     1000          MaxSnd
     C                   END
     C     MaxRcd        IFEQ      *ZEROS
     C                   Z-add     5000          MaxRcd
     C                   END
     c                   Move      'fa'          ARTYPE
      *
     c     StrLoop1      tag
     c                   Move      DATOFN        ARDATE
     c                   Move      *blanks       ARUNDE
     C     FakNrN        Ifne      *zeros
     c                   MoveL     FakNrN        ARUNDE
     c                   end
      *
     C                   select
     C     FakNrN        Whenne    *zeros
     C     FakKey        SETLL     ARKIVL04
     c     FakKey        Reade     ARKIVL04                               35
     c                   other
     c     FraKey        setLL     ARKIVL05
     c     FrakeyE       ReadE     ARKIVL05                               35
     c                   endsl
      *
     C     *in35         doweq     '0'
     C                   Add       1             AntRcd
     C     AntRcd        CabGe     MaxRcd        SLHENTPL
     C     FakNrN        Ifeq      *zeros
     C     DATOTN        andne     *zeros
     C     ARDATE        andgt     DATOTN
     C                   leave
     C                   end
     C                   exsr      SetTabRow
     C                   callp     wrtsection('row')
     C                   Add       1             AntSnd
     C     AntSnd        CabGe     MaxSnd        SLHENTPL
      *
      * ved spørring på spesifikt nr - hopp ut etter første ...
     C                   select
     C     FakNrN        Whenne    *zeros
     c                   leave
     c                   other
      * manipuler Utype slik at alle poster med samme(s.fakt) nr hoppes over
     C                   Move      '.'           ARUNDE
     c     FraKey        setLL     ARKIVL05
     c     FrakeyE       ReadE     ARKIVL05                               35
     c                   endsl
     C                   ENDDO                                                  *hival
      *
      * Først single fakturaer / samle / samle spesifikasjon
     C                   select
     c     Artype        Wheneq    'fa'
     c                   move      'fs'          Artype
     c                   goto      StrLoop1
     c                   endsl
      *
     C     SLHENTPL      ENDSR
      *
      *=====================================================================******
      *  Set output variables for section /$tablerow (table row)
      *=====================================================================******
     C     SetTabRow     begsr
      *
     C                   select
     c     ArType        wheneq    'fa'
     c                   eval      ARFAK = 'Single faktura'
     c     ArType        wheneq    'fs'
     c                   eval      ARFAK = 'Samlefaktura'
     C                   other
     c                   eval      ARFAK = 'Faktura'
     C                   endsl
     C                   exsr      FakturaURL
     C                   MOVE      UPDF          FAKTLNK         200
      * Ved samle - hent spesifikasjon
     C                   MOVE      *blanks       SPESLNK         200
     C                   if        ArType = 'fs'
     C                   eval      ArType = 'fx'
     c     FakKey        chain     Arkivl04                           33
     C                   exsr      FakturaURL
     C                   MOVE      UPDF          SPESLNK         200
     c                   eval      ArType = 'fs'
     c                   endif
      /free
         string=
           '<InvoiceNo>'+%trim(ARUNDE)+'</InvoiceNo>';
         updHTMLvar2('string':%addr(string):%len(string));
         wrtsection('lin');

         string=
           '<InvoiceType>'+%trim(ARFAK)+'</InvoiceType>';
         updHTMLvar2('string':%addr(string):%len(string));
         wrtsection('lin');

         string=
           '<ArchiveDate>'+%trim(%editw(ARDATE:'    .  .  '))
                                          +'</ArchiveDate>';
         updHTMLvar2('string':%addr(string):%len(string));
         wrtsection('lin');

         string=
           '<ArchiveLink>'+%trim(FAKTLNK)+'</ArchiveLink>';
         updHTMLvar2('string':%addr(string):%len(string));
         wrtsection('lin');

         string=
           '<ArchiveSpesLink>'+%trim(SPESLNK)+'</ArchiveSpesLink>';
         updHTMLvar2('string':%addr(string):%len(string));
         wrtsection('lin');

      /end-free
     C                   endsr
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
     C                   OPEN      BRIDF
     C     userEnv       CHAIN(N)  BRIDF                              50
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
     C                   OPEN      arkivl04
     C                   OPEN      arkivl05
     C                   OPEN      firmarc
      *
     C     FakKey        KLIST
     C                   KFLD                    SYSKNN
     C                   KFLD                    artype
     C                   KFLD                    arunde
     C     FraKey        KLIST
     C                   KFLD                    SYSKNN
     C                   KFLD                    artype
     C                   KFLD                    ardate
     C                   KFLD                    arunde
     C     FraKeyE       KLIST
     C                   KFLD                    SYSKNN
     C                   KFLD                    artype
      *
     c                   read      firmarc                                41
      *
     C                   move      *blanks       RMTUSER          30
     C                   move      *blanks       RMTIDENT         30
     C                   move      *blanks       authtype         30
      *
     C                   endsr
      *=====================================================================******
      *  AVSLUTT
      *=====================================================================******
     C     EXIT          begsr
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
     C                   CLOSE     arkivl04
     C                   CLOSE     arkivl05
     C                   CLOSE     firmarc
     C                   eval      *inlr = *on
      *
     C                   endsr
      *
      ***********************************************
     C     FakturaURL    BEGSR
      ***********************************************
     C                   move      *blanks       UPDF            200
     c                   eval      UPDF= 'http://'+%trim(ARCPAE)+'/'
     c                             +%trim(ARCANE)+'/'
     c                             +%trim(ARLINK)+'.pdf'
     c                   endsr
