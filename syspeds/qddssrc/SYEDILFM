     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      REF(*LIBL/EDIM)
     A                                      CF03(03 'Avslutt program')
     A                                      CF05(05 'Forny')
     A                                      CF12(12 'Annuller')
     A  50                                  CF23(23 'BEKR. OMSEND')
     A                                      PRINT
     A                                      HLPDOC('SAD003010' 'SAD003' 'SY400L-
     A                                      O/SADTVINN')
     A                                      HELP
     A                                      ALTHELP
      * "NOERASE" MÅ HA FOR Å IKKE TØMME BILDE SOM MAN KOMME FRA
     A          R NOERASE
     A                                      ASSUME
     A                                  1  2' '
     A* MAN KUN DEFINNERE VINDU PÅ REKORDNIVÅ
     A          R WINDOWSZ
     A                                      OVERLAY
     A                                      WINDOW(*DFT 15 65 *NOMSGLIN)
     A          R SAD003A                   SFL
     A            MSN       R        H
     A            MMN       R        H
     A            MSR       R        H
     A            M0004     R        H
     A            M0010     R        H
     A            M0068A    R        H
     A            M0068B    R        H
     A            M0068C    R        H
     A            M3039E    R        H
     A            ZOP            1A  I  9  3VALUES(' ' '4' '5' '6' '7' '9')
     A            W0004          8A  O  9  6
     A  27                                  COLOR(PNK)
     A            W0010          8A  O  9 15
     A  27                                  COLOR(PNK)
     A            M0035     R        O  9 24
     A  27                                  COLOR(PNK)
     A            M0065     R        O  9 26
     A  27                                  COLOR(PNK)
     A            M1N07     R        O  9 33
     A  27                                  COLOR(PNK)
     A            MST       R        O  9 38
     A  27                                  COLOR(PNK)
     A            MDT       R        O  9 40
     A  27                                  COLOR(PNK)
     A            MTM       R        O  9 49
     A  27                                  COLOR(PNK)
     A            MVEN      R        O  9 57
     A  27                                  COLOR(PNK)
     A          R SAD003B                   SFLCTL(SAD003A)
     A*%%TS  SD  20000411  142504  JOVO        REL-V4R4M0  5769-PW1
     A                                      SFLSIZ(0007)
     A                                      SFLPAG(0006)
     A                                      WINDOW(WINDOWSZ)
     A                                      TEXT('EDI MESSAGE-LOG, Heading')
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A                                  1  3'EDI  '
     A                                      COLOR(BLU)
     A                                  1 24'Meldingslogg'
     A                                      COLOR(WHT)
     A                                  1 51'SYEDILFM A'
     A                                      COLOR(BLU)
     A                                  2 19'Meldingstype..:'
     A            WMLDTY         6A  O  2 35COLOR(WHT)
     A                                  3  2'Avdeling:'
     A            WAVD      R        O  3 12REFFLD(SIAVD *LIBL/SADH)
     A                                      EDTCDE(4)
     A                                  3 19'Oppdragsnummer:'
     A            WTDN      R        O  3 35REFFLD(SITDN *LIBL/SADH)
     A                                      EDTCDE(4)
     A  51                              3 43'Fraktbr.nr:'
     A  51        WFBNR          3Y 0O  3 55EDTCDE(4)
     A            ZRECNO         4S 0O  3 61SFLRCDNBR
     A                                      DSPATR(ND)
     A  50N53                           4  2'Post finnes alt i EDI-Loggen.... F-
     A                                      12=IKKE SEND F23=OMSEND'
     A                                      COLOR(WHT)
     A  50 53                           4  2'Åpen post finnes i EDI-logg! Må sl-
     A                                      ettes før ny kan lages!'
     A                                      COLOR(WHT)
     A                                  5  2'Velg og trykk Enter:'
     A                                      COLOR(BLU)
     A                                  6  4'5=Vise melding. 6=Vise feilm.tekst-
     A                                       7=Vise fysisk filkomp.'
     A                                      COLOR(BLU)
     A                                  7  4'4=Slette åpen                     -
     A                                       9=Vise snd strukturert'
     A                                      COLOR(BLU)
     A                                  8  2'Alt Avsender Mottaker T MSG ID Fun-
     A                                      k S Sendt           Ven  '
     A                                      COLOR(WHT)
     A          R SAD003C
     A                                      WINDOW(WINDOWSZ)
     A                                      SLNO(15)
     A                                      CLRL(01)
     A                                      OVERLAY
     A                                  1  2'F3=Gå ut   F5=Forny   F12=Annuller'
     A                                      COLOR(BLU)
     A  50                              1 43'F23=Bekrefte omsending'
     A                                      COLOR(BLU)
     A          R SAD003D                   OVERLAY
     A                                      ROLLUP(25)
     A                                  1  2'TVINN'
     A                                      COLOR(BLU)
     A                                  1 34'Meldingslogg'
     A                                      COLOR(WHT)
     A                                  1 69'SYEDILFM D'
     A                                      COLOR(BLU)
     A                                  3  2'Trykk Enter for å fortsette:'
     A                                      COLOR(BLU)
     A                                  5  2'Avdeling . . . . . . . . . . . . .-
     A                                       . :'
     A            MAVD      R        O  5 43REFFLD(SIAVD *LIBL/SADH)
     A                                      EDTCDE(4)
     A                                  6  4'Oppdragsnummer . . . . . . . . . .-
     A                                       :'
     A            MTDN      R        O  6 43REFFLD(SITDN *LIBL/SADH)
     A                                      EDTCDE(4)
     A                                  8  6'NODI Id Avsender . . . . . . . . :'
     A            M0004     R        O  8 43
     A                                  9  6'NODI Id Mottaker . . . . . . . . :'
     A            M0010     R        O  9 43
     A                                 10  6'Testindikator  . . . . . . . . . :'
     A            M0035     R        O 10 43
     A                                 11  6'Meldings Referanse . . . . . . . :'
     A            M0062     R        O 11 43
     A                                 12  6'Meldings Identitet . . . . . . . :'
     A            M0065     R        O 12 43
     A                                 13  6'Deklarasjonens dato  . . . . . . :'
EV5  A            M0068A    R        O 13 43EDTWRD('    .  .  ')
     A                                 14  8'Sekvensnummer  . . . . . . . . :'
     A            M0068B    R        O 14 43EDTCDE(4)
     A                                 15 10'Versjonsnummer . . . . . . . :'
     A            M0068C    R        O 15 43EDTCDE(4)
     A                                 16  6'Omberegning, Deklarasjonens dato :'
EV5  A            M0068D    R        O 16 43EDTWRD('    .  .  ')
     A                                 17  8'Sekvensnummer  . . . . . . . . :'
     A            M0068E    R        O 17 43EDTCDE(4)
     A                                 18 10'Versjonsnummer . . . . . . . :'
     A            M0068F    R        O 18 43EDTCDE(4)
     A                                 19  6'Ønsket Behandlingsdato . . . . . :'
     A            M2005B    R        O 19 43
     A                                 20  6'Deklarantens Referanse . . . . . :'
     A            M3039D    R        O 20 43EDTCDE(4)
     A                                 21  6'Ekspidisjons Enhet . . . . . . . :'
     A            M3039E    R        O 21 43EDTCDE(4)
     A                                 21 72'Mer...'
     A                                      COLOR(WHT)
     A                                 23  2'F3=Gå ut   F12=Annuller'
     A                                      COLOR(BLU)
     A          R SAD003I                   OVERLAY
     A                                      ROLLDOWN(26)
     A                                  1  2'TVINN'
     A                                      COLOR(BLU)
     A                                  1 34'Meldingslogg'
     A                                      COLOR(WHT)
     A                                  1 69'SYEDILFM I'
     A                                      COLOR(BLU)
     A                                  3  2'Trykk Enter for å fortsette:'
     A                                      COLOR(BLU)
     A                                  5  2'Avdeling . . . . . . . . . . . . .-
     A                                       . :'
     A            MAVD      R        O  5 43REFFLD(SIAVD *LIBL/SADH)
     A                                      EDTCDE(4)
     A                                  6  4'Oppdragsnummer . . . . . . . . . .-
     A                                       :'
     A            MTDN      R        O  6 43REFFLD(SITDN *LIBL/SADH)
     A                                      EDTCDE(4)
     A                                  8  7'Depositum Beløp . . . . . . . . :'
     A            M5004D    R        O  8 43EDTCDE(K)
     A                                  9  7'Meldingsfunksjon  . . . . . . . :'
     A            M1N07     R        O  9 43
     A                                 10  7'Meldingsfunksjon Korrigert  . . :'
     A            M1N08     R        O 10 43
     A                                 11  7'Ekspidisjons Prioritet  . . . . :'
     A            M9N01     R        O 11 43
     A                                 12  7'Sendningsnummer . . . . . . . . :'
     A            MSN       R        O 12 43EDTCDE(4)
     A                                 13  9'Meldingsnummer  . . . . . . . :'
     A            MMN       R        O 13 43EDTCDE(4)
     A                                 14  7'Send/Recive . . . . . . . . . . :'
     A            MSR       R        O 14 43
     A                                 15  7'Status  . . . . . . . . . . . . :'
     A            MST       R        O 15 43
     A                                 16  9'Status Dato . . . . . . . . . :'
     A            MDT       R        O 16 43
     A                                 17 11'Status Tid  . . . . . . . . :'
     A            MTM       R        O 17 43EDTWRD('  :  :  ')
     A                                 18  7'Ventekode . . . . . . . . . . . :'
     A            MVEN      R        O 18 43
     A                                 21 72'Slutt '
     A                                      COLOR(WHT)
     A                                 23  2'F3=Gå ut   F12=Annuller'
     A                                      COLOR(BLU)
     A          R SAD003E                   SFL
     A            F0077     R        O  8  2REFFLD(TVINFF/F0077 *LIBL/TVINF)
     A            F4815     R        O  8  6REFFLD(TVINFF/F4815 *LIBL/TVINF)
     A            F0078A    R        O  8 11REFFLD(TVINFF/F0078A *LIBL/TVINF)
     A            F0078B    R        O  9 11REFFLD(TVINFF/F0078B *LIBL/TVINF)
     A            F0078C    R        O 10 11REFFLD(TVINFF/F0078C *LIBL/TVINF)
     A            F0078D    R        O 11 11REFFLD(TVINFF/F0078D *LIBL/TVINF)
     A            F0078E    R        O 12 11REFFLD(TVINFF/F0078E *LIBL/TVINF)
     A          R SAD003F                   SFLCTL(SAD003E)
     A                                      SFLSIZ(0004)
     A                                      SFLPAG(0003)
     A  81N83N84                            ROLLUP(25)
     A  81N83N84                            ROLLDOWN(26)
     A                                      OVERLAY
     A  81                                  SFLDSP
     A  82                                  SFLDSPCTL
     A  83                                  SFLCLR
     A  84                                  SFLEND
     A                                  1  2'TVINN'
     A                                      COLOR(BLU)
     A                                  1 21'Meldingslogg - Feilmeldinger - Tek-
     A                                      st'
     A                                      COLOR(WHT)
     A                                  1 69'SYEDILFM E'
     A                                      COLOR(BLU)
     A                                  3  2'Avdeling:'
     A            MAVD      R        O  3 12EDTCDE(4)
     A                                  3 23'Oppdragsnummer:'
     A            MTDN      R        O  3 39EDTCDE(4)
     A                                  4  2'Dato....:'
EV5  A            M0068A    R        O  4 12EDTWRD('    .  .  ')
     A                                  4 23'Sekvensnummer :'
     A            M0068B    R        O  4 39EDTCDE(4)
     A                                  4 48'Versjonsnummer:'
     A            M0068C    R        O  4 64EDTCDE(4)
     A                                  5  2'Trykk Enter for å fortsette:'
     A                                      COLOR(BLU)
     A            ÆRECNO         4S 0O  5 68SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  7  2'Ref'
     A                                      COLOR(WHT)
     A                                  7  6'Funk'
     A                                      COLOR(WHT)
     A                                  7 11'Tekst'
     A                                      COLOR(WHT)
     A          R SAD003Ø
     A                                      OVERLAY
     A            ZMSGTX        79A  O 24  2MSGID(&ZMSGNO SYMSGF)
     A N30                                  COLOR(WHT)
     A  30                                  COLOR(RED)
     A            ZMSGNO         7A  P
