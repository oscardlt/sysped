********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 YBC PTF10 ARKIVERING
********************************************************************
      *
      ***********************************************************
      *** PROGRAM SKAL SKRIVE ADVISERING                      ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01-26                            ***
      *** LEDIGE INDIKATORER 27-32 35-55 57-59 62-90 94-99    ***
      ***********************************************************
      *
     FBILLOF    UF   E           K DISK
     FBLV1LF    IF   E           K DISK
     FBLV2LF    IF   E           K DISK
     FCUNDL01   IF   E           K DISK
     FSTED2     IF   E           K DISK
     FCDBOAT    IF   E           K DISK
     FKODTSF    IF   E           K DISK
     FKODTA     IF   E           K DISK
     FHODE      IF   E           K DISK
     FKODTFM    IF   E           K DISK
N01  FARKTXT    IF   E           K DISK
N01  FFIRMARC   IF   E           K DISK
N01  FARKIVL03  IF   E           K DISK    RENAME(ARKIVR:ARKIVR03)
N01  FARKIVP    O  A E             DISK    USROPN
     FSYSJ16P   O    E             PRINTER
     D MN              S             17    DIM(50)
     D NT              S              5  0 DIM(50)
     D VS              S             25    DIM(50)
     D VK              S              9  0 DIM(50)
     D M3              S              9  3 DIM(50)
     D TX              S             51    DIM(50)
     D KD              S              1    DIM(7)
     D                 DS
     D  BLDKA                  1      1
     D  BLDKM                  2      2
     D  BLDKN                  3      3
     D  BLDKSA                 4      4
     D  BLDKK                  5      5
     D  BLDKKA                 6      6
     D  BLDKRA                 7      7
     D  PR                     1      7
     D                                     DIM(7)
     D                 DS
     D  BLKNA                  1      8  0
     D  BLKNM                  9     16  0
     D  BLKNN                 17     24  0
     D  BLKNSA                25     32  0
     D  BLKNK                 33     40  0
     D  BLKNKA                41     48  0
     D  BLKNRA                49     56  0
     D  KN                     1     56  0
     D                                     DIM(7)
     D                 DS
     D  HOHT1                  1     80
     D  HOHT2                 81    160
     D  HOHT3                161    240
     D  HOHT4                241    320
     D  HOHT5                321    400
     D  HOHT6                401    480
     D  HOHT7                481    560
     D  HOBT1                561    640
     D  HOBT2                641    720
     D  HOBT3                721    800
     D  HP                     1    800
     D                                     DIM(10)
     D                 DS
     D  HOHT1N                 1     80
     D  HOHT2N                81    160
     D  HOHT3N               161    240
     D  HOHT4N               241    320
     D  HOHT5N               321    400
     D  HOHT6N               401    480
     D  HOHT7N               481    560
     D  HOBT1N               561    640
     D  HOBT2N               641    720
     D  HOBT3N               721    800
     D  HO                     1    800
     D                                     DIM(10)
     D                 DS
     D  HOHT1E                 1     80
     D  HOHT2E                81    160
     D  HOHT3E               161    240
     D  HOHT4E               241    320
     D  HOHT5E               321    400
     D  HOHT6E               401    480
     D  HOHT7E               481    560
     D  HOBT1E               561    640
     D  HOBT2E               641    720
     D  HOBT3E               721    800
     D  HE                     1    800
     D                                     DIM(10)
     D                 DS
     D  HOHT1T                 1     80
     D  HOHT2T                81    160
     D  HOHT3T               161    240
     D  HOHT4T               241    320
     D  HOHT5T               321    400
     D  HOHT6T               401    480
     D  HOHT7T               481    560
     D  HOBT1T               561    640
     D  HOBT2T               641    720
     D  HOBT3T               721    800
     D  HT                     1    800
     D                                     DIM(10)
     D                 DS
     D  XIDAG1                 1      2  0
     D  XIDAG2                 3      4  0
     D  XIDAG3                 5      6  0
     D  XIDAGX                 1      6  0
     D                 DS
     D  XDTA1                  1      2  0
     D  XDTA2                  3      4  0
     D  XDTA3                  5      6  0
     D  XDTA                   1      6  0
     D                UDS
     D  UFIRM                  1      2
N01  D                SDS
N01  D  UUSER                254    263
     C*
     C                   EXSR      INIT
     C*
     C* SKRIV TIL PRINTER
     C*
     C     BILLK         CHAIN(N)  BILLOF                             60
     C  N60              DO
     C     WOPFM         IFEQ      *BLANKS
     C                   EXSR      PRINT
     C                   ELSE
     C     WOPFM         SETLL     KODTFM
     C     *IN34         DOWEQ     '0'
     C     WOPFM         READE     KODTFM                                 34
     C     *IN34         IFEQ      '0'
     C                   EXSR      PRINT
     C                   END
     C                   END
     C                   END
     C     BILLK         CHAIN     BILLOF                             33
     C                   MOVEA     KD            PR
     C                   EXCEPT    BLMRK
     C                   END
     C*
     C                   SETON                                        LR
     C                   RETURN
     C*
     C******************************************
     C     PRINT         BEGSR
     C******************************************
     C                   MOVE      BLDTA         XDTA
     C                   MOVE      XDTA1         UDT3              2 0
     C                   MOVE      XDTA2         UDT2              2 0
     C                   MOVE      XDTA3         UDT1              2 0
     C                   EXSR      KDTFMT
     C                   MOVE      UDT3          XDTA3
     C                   MOVE      UDT2          XDTA2
     C                   MOVE      UDT1          XDTA1
     C*
     C                   MOVEA     PR            KD
     C                   MOVE      KN(UN2)       KUNDNR
     C     KUND1K        CHAIN     CUNDL01                            33
     C  N33              MOVE      SPRAAK        XSPRÅK            1
     C   33              MOVE      ' '           XSPRÅK
     C*
     C                   SELECT
     C     UN2           WHENEQ    1
     C                   MOVE      BLNAA         XNA              30
     C                   MOVE      BLAD1A        XAD1             30
     C                   MOVE      BLAD2A        XAD2             30
     C                   MOVE      BLAD3A        XAD3             30
     C     UN2           WHENEQ    2
     C                   MOVE      BLNAM         XNA
     C                   MOVE      BLAD1M        XAD1
     C                   MOVE      BLAD2M        XAD2
     C                   MOVE      BLAD3M        XAD3
     C     UN2           WHENEQ    3
     C                   MOVE      BLNAN         XNA
     C                   MOVE      BLAD1N        XAD1
     C                   MOVE      BLAD2N        XAD2
     C                   MOVE      BLAD3N        XAD3
     C                   OTHER
     C  N33              EXSR      MOVKUN
     C                   ENDSL
      *
     C                   EXSR      HEBLV
     C                   EXSR      PRINT2
     C                   MOVE      'P'           KD(UN2)
N01   *
N01  C                   exsr      arkivsr
     C*
     C                   ENDSR
     C*
     C******************************************
     C     MOVKUN        BEGSR
     C******************************************
     C                   MOVE      SPRAAK        XSPRÅK            1
     C                   MOVE      KNAVN         XNA              30
     C                   MOVE      ADR1          XAD1             30
     C                   MOVE      ADR2          XAD2             30
     C                   MOVE      *BLANKS       XAD3             30
     C     POSTNR        IFEQ      0
     C     SYPOGE        IFEQ      *BLANKS
     C                   MOVEL     ADR3          XAD3
     C                   ELSE
     C     SYPOGE        CAT       ADR3:1        XAD3
     C                   END
     C                   ELSE
     C                   MOVEL     POSTNR        XAD3
     C                   MOVE      ADR3          XAD3             30
     C                   END
     C*
     C                   ENDSR
     C*
     C******************************************
     C     HEBLV         BEGSR
     C******************************************
     C                   MOVE      BLKNK         KUNDNR
     C     KUND1K        CHAIN     CUNDL01                            33
     C   33              MOVE      BLNAM         XNAM             30
     C  N33              MOVE      KNAVN         XNAM
     C     BLLOH         CHAIN     STED2                              33
     C   33              MOVE      *BLANKS       XLOHN            30
     C  N33              MOVE      ST2NVN        XLOHN
     C     BLLSH         CHAIN     STED2                              33
     C   33              MOVE      *BLANKS       XLSHN            30
     C  N33              MOVE      ST2NVN        XLSHN
     C     BLOB          CHAIN     CDBOAT                             33
     C   33              MOVE      *BLANKS       XOBNA            30
     C  N33              MOVE      CBNAB         XOBNA
     C     KODSFK        CHAIN     KODTSF                             33
     C   33              MOVE      *BLANKS       KOSFNV
     C*
     C** HENTE MODELL 1
     C                   MOVE      *BLANKS       MN
     C                   MOVE      *BLANKS       VS
     C                   MOVE      *ZEROS        NT
     C                   MOVE      *ZEROS        VK
     C                   MOVE      *ZEROS        M3
     C                   MOVE      *ZEROS        XN                3 0
     C                   SETOFF                                       61
     C     BLV1K         SETLL     BLV1LF
     C     STP11         TAG
     C     BLV1K         READE     BLV1LF                                 61
     C  N61XN            IFLT      50
     C                   ADD       1             XN
     C                   MOVE      BVMN          MN(XN)
     C                   MOVE      BVNT          NT(XN)
     C                   MOVE      BVVS          VS(XN)
     C                   MOVE      BVVKT         VK(XN)
     C                   MOVE      BVM3          M3(XN)
     C                   GOTO      STP11
     C                   END
     C*
     C*** HENTE MODELL 2 (MERKE & NR.)
     C                   SETOFF                                       61
     C     BLV2K         SETLL     BLV2LF
     C     STP12         TAG
     C     BLV2K         READE     BLV2LF                                 61
     C  N61XN            IFLT      50
     C     BVMN          IFNE      *BLANKS
     C                   ADD       1             XN
     C                   MOVE      BVMN          MN(XN)
     C                   END
     C                   GOTO      STP12
     C                   END
     C*
     C*** HENTE MODELL 2
     C                   MOVE      *BLANKS       TX
     C                   MOVE      *ZEROS        X2                2 0
     C                   MOVE      *ZEROS        XLI               3 0
     C                   MOVE      *ZEROS        XLI2              1 0
     C                   SETOFF                                       61
     C     BLV2K         SETLL     BLV2LF
     C     STP13         TAG
     C     BLV2K         READE     BLV2LF                                 61
     C  N61BVKD          IFNE      'A'
     C     BVKD          ANDNE     'N'
     C                   GOTO      STP13
     C                   END
     C  N61              DO
     C     XLI           COMP      0                                      56
     C   56XLI2          COMP      0                                      56
     C   56              MOVE      BVLI          XLI
     C   56              Z-ADD     1             XLI2
     C     BVLI          COMP      XLI                                    56
     C  N56XLI2          DOWLE     4
     C                   ADD       1             X2
     C                   ADD       1             XLI2
     C                   END
     C  N56              MOVE      BVLI          XLI
     C  N56              Z-ADD     1             XLI2
     C     XLI2          DOWLT     BVLI2
     C                   ADD       1             X2
     C                   ADD       1             XLI2
     C                   END
     C                   ADD       1             XLI2
     C                   ADD       1             X2
     C                   MOVE      BVFT          TX(X2)
     C                   GOTO      STP13
     C                   END
     C*
     C                   ENDSR
     C*
     C******************************************
     C     PRINT2        BEGSR
     C******************************************
     C     XSPRÅK        COMP      'E'                                919192
     C  N92XSPRÅK        COMP      'T'                                919193
     C   92              SETOFF                                       93
     C                   MOVE      *ZEROS        X3                2 0
     C                   MOVE      *ZEROS        X4                2 0
     C*
     C     STP1          TAG
     C                   WRITE     LHEAD
     C                   MOVE      *ZEROS        XANTL             2 0
     C     X3            DOWLT     XN
     C                   ADD       1             X3
     C                   MOVE      MN(X3)        XMN
     C                   MOVE      VS(X3)        XVS
     C                   MOVE      NT(X3)        XNT
     C                   MOVE      VK(X3)        XVKT
     C                   MOVE      M3(X3)        XM3
     C                   WRITE     LMOD1
     C                   ADD       1             XANTL
     C     XANTL         IFGE      10
     C                   GOTO      STP2
     C                   END
     C                   END
     C*
     C     STP2          TAG
     C                   WRITE     LMIDT
     C                   MOVE      *ZEROS        XANTL2            2 0
     C     X4            DOWLT     X2
     C                   ADD       1             X4
     C                   MOVE      TX(X4)        XTXT
     C                   WRITE     LMOD2
     C                   ADD       1             XANTL2
     C     XANTL2        IFGE      8
     C                   GOTO      STP3
     C                   END
     C                   END
     C*
     C     STP3          TAG
     C                   WRITE     LBUNN
     C     X3            IFLT      XN
     C                   GOTO      STP1
     C                   END
     C     X4            IFLT      X2
     C                   GOTO      STP1
     C                   END
     C*
     C                   ENDSR
     C*
N01   *=====================================================================
N01  C     ARKIVSR       BEGSR
N01   *=====================================================================
N01  C     ARKIVL03K     CHAIN     ARKIVL03                           33
N01  C                   IF        *IN33
N01  C     UFIRM         CHAIN     FIRMARC                            33
N01  C                   IF        *IN33 = *OFF
N01  C     'ad'          CHAIN     ARKTXT                             33
N01  C  N33              IF        ARKJN = 'J'
N01   *
N01  C                   OPEN      ARKIVP
N01  C                   CALL      'SYGE15C'
N01  C                   PARM                    WDT               8
N01  C                   PARM                    WTM               6
N01  C                   MOVE      UFIRM         ARFIRM
N01  C                   MOVE      BLAVD         ARAVD
N01  C                   MOVE      BLOPD         AROPD
N01  C                   EVAL      ARUNDE = UN
N01  C                   MOVE      'ad'          ARTYPE
N01  C                   MOVE      UUSER         ARUSER
N01  C                   MOVE      WDT           ARDATE
N01  C                   MOVE      WTM           ARTIME
N01  C                   MOVE      KUNDNR        ARKUND
N01  C                   WRITE     ARKIVR
N01  C                   CLOSE     ARKIVP
N01  C                   CALL      'ARC002CLB'
N01  C                   PARM                    ARTYPE
N01  C                   PARM                    ARUSER
N01  C                   END
N01  C                   END
N01  C                   END
N01   *
N01  C                   ENDSR
N01   *
     C******************************************
     C     INIT          BEGSR
     C******************************************
     C     KUND1K        KLIST
     C                   KFLD                    UFIRM
     C                   KFLD                    KUNDNR
     C     BILLK         KLIST
     C                   KFLD                    BLAVD
     C                   KFLD                    BLOPD
     C     BLV1K         KLIST
     C                   KFLD                    BLAVD
     C                   KFLD                    BLOPD
     C     BLV2K         KLIST
     C                   KFLD                    BLAVD
     C                   KFLD                    BLOPD
     C     KODSFK        KLIST
     C                   KFLD                    YSFUNI
     C                   KFLD                    BLSG
     C     KODTAK        KLIST
     C                   KFLD                    YAUNI
     C                   KFLD                    BLAVD
     C     HODEK         KLIST
     C                   KFLD                    YNET
     C                   KFLD                    BLAVD
N01  C     ARKIVL03K     KLIST
N01  C                   KFLD                    XARTYPE
N01  C                   KFLD                    BLAVD
N01  C                   KFLD                    BLOPD
N01  C                   KFLD                    XARUNDE
     C*
     C     *ENTRY        PLIST
     C                   PARM                    UAVD              4
     C                   PARM                    UOPD              7
     C                   PARM                    UN                1
      *
     C*
      * HENT STRINGKODE
     C                   Z-ADD     31            WTSKRN            3 0
     C                   MOVE      UAVD          LOSAV2            4 0
     C                   CALL      'SYGE01B'
     C                   PARM                    LOSAV2
     C                   PARM                    WTSKRN
     C                   PARM                    WOPNVN           10
     C                   PARM                    WOPTY            10
     C                   PARM                    WOPHEA            1
     C                   PARM                    WOPFM             5
     C                   PARM                    WLASER            1
      *
      *LASER ELLER IKKE
      *
     C                   MOVE      UAVD          BLAVD
     C                   MOVE      UOPD          BLOPD
     C                   MOVE      UN            UN2               1 0
     C                   MOVE      *ZEROS        KN
     C                   MOVE      *BLANKS       PR
N01  C                   MOVE      'ad'          XARTYPE           2
N01  C                   MOVEL     UN            XARUNDE          10
     C*
     C                   MOVE      'SF'          YSFUNI            2
     C                   MOVE      'A'           YAUNI             1
     C*
     C                   MOVE      UYEAR         UDT3              2 0
     C                   MOVE      UMONTH        UDT2              2 0
     C                   MOVE      UDAY          UDT1              2 0
     C                   EXSR      KDTFMT
     C                   MOVE      UDT3          XIDAG3
     C                   MOVE      UDT2          XIDAG2
     C                   MOVE      UDT1          XIDAG1
     C*
     C     KODTAK        CHAIN     KODTA                              33
     C                   MOVE      KOAKNR        KUNDNR
     C     KUND1K        CHAIN     CUNDL01                            33
     C                   MOVE      TLF           XTLF
     C*
     C                   MOVE      *BLANKS       HO
     C                   MOVE      *BLANKS       HE
     C                   MOVE      *BLANKS       HT
     C     HODEK         CHAIN     HODE                               33
     C  N33              MOVEA     HP            HO
     C                   MOVEL     'E'           YNET              1
     C     HODEK         CHAIN     HODE                               33
     C  N33              MOVEA     HP            HE
     C                   MOVEL     'T'           YNET
     C     HODEK         CHAIN     HODE                               33
     C  N33              MOVEA     HP            HT
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     KDTFMT        BEGSR
     C***********************************************
     C                   CALL      'DATOFMT'
     C                   PARM                    UDT1
     C                   PARM                    UDT2
     C                   PARM                    UDT3
     C*
     C                   ENDSR
     C*
     OBILLOFR   E            BLMRK
     O                       BLDKA
     O                       BLDKM
     O                       BLDKN
     O                       BLDKSA
     O                       BLDKK
     O                       BLDKKA
     O                       BLDKRA
