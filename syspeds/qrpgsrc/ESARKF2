      *********************************************************************
      *
      *
CRTPG+*  CRTPGM SYSPED/esarkf2 MODULE(*LIBL/esarkf2 *LIBL/INCGI)
CRTPGM*         ACTGRP(CGI) AUT(*USE)
      *
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 JOV PTF10 tar hensyn til språkkode
********************************************************************
      *********************************************************************
      * 01 JOV 10 - sikre mot & i navn   WSNA = parameter
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FCUNDL01   IF   E           K DISK    USROPN
     FHeadf     IF   E           K DISK    usropn
     FFain      IF   E           K DISK    usropn
     FFak8      IF   E           K DISK    usropn
     FWRKCGI    UF A E           K DISK    usropn
      *********************************************************************
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      /copy syspeds/qrpgsrcpy,variables3
      *
     D OddEven         s             10
      * Client input variables
     D user            s             10
     D UsrSpcName      s             10
     D U_name          S             10
     D FakNr           s              7
     D FakNrN          s              7  0
     D Integ           s              6
     D Winteg          s              6  0
     D IkkeBrudd       s              1
     D ICHECKED        s             10
     D SORT            s              5
     D SORTTXT         s             30
     D Kunr2           s              8
     D Kunr2N          s              8  0
      *
     D MOMS            s             11  2
     D GMWKEY2         s             30
     D AntOpd          s              5  0
     D BRUBelF         s             11  2
     D BRUBelN         s             11  2
     D BRUBelM         s             11  2
     D BRUBelB         s             11  2
     D BRUKLL          s              9  0
     D BRUVKT          s             11  0
     D BRUM3           s             11  3
     D BRULM           s             11  2
     D BRUFVKT         s             11  0
     D DREF            s             35
     D Spaces          s             12a   inz('&nbsp;&nbsp;')
     D ItemCount       s             10i 0
     D i               s             10i 0
     D                 DS
     D  Wdata                  1    220
     D  FIAVD                  1      4  0
     D  FIOPD                  5     11  0
     D  BELF                  13     27  2
     D  BELN                  28     42  2
     D  BELM                  43     57  2
     D  BELB                  58     72  2
     D  DTKEY                 95    102
     D  DRKEY                103    137
     D  VRKEY                138    153
     D  AVKEY                154    168
     D  MOKEY                169    183
     D  ASKEY                184    198
     D  MSKEY                199    213
     D                 DS
     D  XXDATO                 1      8  0
     D  XXDATOD                1      2  0
     D  XXDATOM                3      4  0
     D  XXDATOÅ                5      8  0
     D                 DS
     D  DATON                  1      6  0
     D  DATOND                 1      2  0
     D  DATONM                 3      4  0
     D  DATONÅ                 5      6  0
     D                 DS
     D  VREF                   1     16
     D  HEAVD                  1      4  0
     D  HEAVDa                 1      4
     D  STREK1                 5      5
     D  HEOPD                  6     12  0
     D  HEOPDa                 6     12
     D  STREK2                13     13
     D  HESG                  14     16
     D                 DS
     D  HENAS                  1     30
     D  AVSEND                 1     15
     D                 DS
     D  HEADS3                 1     30
     D  AVSTED                 1     15
     D                 DS
     D  HENAK                  1     30
     D  MOTT                   1     15
     D                 DS
     D  HEADK3                 1     30
     D  MOSTED                 1     15
      *
      /copy syspeds/qrpgsrcpy,prolog3
      *
      * Get program start time for calculating execution time
     C                   time                    timedata1
      * Parse input / cvt to numeric
     C                   exsr      Parse
      * File open / keys
     C                   EXSR      INIT
      * Read output skeleton html member into memory
     C                   exsr      LoadHtml
      *
      * HOVEDRUTINE
      * 1. Kommer inn med INTEG = 0 - FØRSTE GANG - generer wfil
     c                   eval      SortTxt = 'Standard'
     C     WINTEG        IFEQ      *ZEROS
      * Tildel random 6-sifret nr som key i Workfil.
     c                   eval      WINTEG   = random(1:999999)
      * Just to be sure - slett evt gamle poster
     c     WINTEG        setLL     WRKCGI
     c     WINTEG        ReadE     WRKCGI                                 35
     c     *IN35         doweq     '0'
     C                   delete    WRKCGIR
     c     WINTEG        ReadE     WRKCGI                                 35
     c                   end
      * Vis liste over oppdrag
     C     FAKNRN        SETLL     FAIN
     c     FAKNRN        Reade     FAIN                                   35
     c     *IN35         doweq     '0'
      * diverse filter legges her..
      *
      * Ok - skriv til wfil
     C                   exsr      SetWrk
      *
     c     NextRec       Tag
     c     FAKNRN        Reade     FAIN                                   35
     c                   end
      * INTEG ER SATT - da er det ny visning av samme (resort...)
     C                   else
     C     Sort          ifne      *blanks
     C                   exsr      SortWrk
     C                   end
     C                   end
      *
      * Set section variables
     C                   exsr      Settop
     C                   callp     wrtsection('top')
      * Vis basert på WFIL (oppdater samtidig nøkkelfelt..)
     c     WINTEG        setLL     WRKCGI
     c     WINTEG        ReadE     WRKCGI                                 35
     C  N35              eval      GMWkey2  = Wkey2
     c     *IN35         doweq     '0'
      * Vis bruddlinje ved avvikende sortering / nullstill brudd-akkumul...
     c     IkkeBrudd     Ifne      'Y'
     C     SORT          Andne     *BLANKS
     C     WKey2         Andne     GMWkey2
     C                   exsr      SetBruRow
     C                   callp     wrtsection('brurow')
     C                   EXSR      NULLBRU
     C                   END
      * Vis sist leste linje
     C                   exsr      Setrow
     C                   callp     wrtsection('row')
     c                   Update    WRKCGIR
      * Adder inn siste linje i bruddverdie / husk key
     c     IkkeBrudd     Ifne      'Y'
     C     SORT          ANDNE     *BLANKS
     C                   EXSR      ADDBRU
     C                   eval      GMWkey2  = Wkey2
     C                   END
     c     WINTEG        ReadE     WRKCGI                                 35
     c                   end
      * siste bruddsum
     c     IkkeBrudd     Ifne      'Y'
     C     SORT          ANDNE     *BLANKS
     C                   exsr      SetBruRow
     C                   callp     wrtsection('brurow')
     C                   END
      * Quit
      * Compute run time
     C                   time                    timedata2
     C     timedata2     subdur    timedata1     ms:*ms
     C                   eval      sec = ms / 1000000
     C                   callp     updhtmlvar('runtime':
     C                             %trim(%editw(sec:'     0 .   ')))
      *
     C                   callp     wrtsection('bot')
      *
     C                   exsr      Exit
      *
     C                   eval      *inlr = *on
     C                   return
      *
      *=====================================================================
      * ADDER bruddsummer
      *=====================================================================
     C     ADDBRU        begsr
     c                   add       1             AntOpd
     c                   add       BelF          BruBelF
     c                   add       BelN          BruBelN
     c                   add       BelM          BruBelM
     c                   add       BelB          BruBelB
     c                   add       HENT          BruKLL
     c                   add       HEVKT         BruVKT
     c                   add       HEM3          BruM3
     c                   add       HELM          BruLM
     c                   add       HEFBV         BruFVKT
     C                   ENDSR
      *=====================================================================
      * Nullstill hjelpevariabler for bruddsummer
      *=====================================================================
     C     NULLBRU       begsr
     c                   move      *zeros        AntOpd
     c                   move      *zeros        BruBelF
     c                   move      *zeros        BruBelN
     c                   move      *zeros        BruBelM
     c                   move      *zeros        BruBelB
     c                   move      *zeros        BruKLL
     c                   move      *zeros        BruVKT
     c                   move      *zeros        BruM3
     c                   move      *zeros        BruLM
     c                   move      *zeros        BruFVKT
     C                   ENDSR
      *
      *=====================================================================
      * Parse input / cvt to numeric
      *=====================================================================
     C     Parse         begsr
      * Parse variables from QUERY_STRING environment variable
     C                   eval      FAKNR    = zhbgetvar('FAKNR')
     C                   eval      FAKNRN   = c2n2(FAKNR)
     C                   eval      KUNR2    = zhbgetvar('KUNR2')
     C                   eval      KUNR2N   = c2n2(KUNR2)
     C                   eval      INTEG    = zhbgetvar('INTEG')
     C                   eval      WINTEG   = c2n2(INTEG)
     C                   eval      SORT     = zhbgetvar('SORT')
     C                   eval      ICHECKED = zhbgetvar('ICHECKED')
      * Checkboks for Ikke brudd,
      *    ved KLIKK på checkboks kommer verdi Y/blank,
      *    ellesr kommer verdi fra siste valg via "ICHECKED" som checked / blank
     C     ICHECKED      Ifeq      'checked'
     C                   eval      IkkeBrudd= 'Y'
     C                   else
     C                   eval      IkkeBrudd= zhbgetvar('IkkeBrudd')
     C                   end
     c     IkkeBrudd     Ifne      'Y'
     C                   eval      IkkeBrudd= 'N'
     c                   end
      * Userid.....
     C                   eval      user = zhbgetvar('user')
      * Eksternt pålogget via access list..- Overstyrer parameter USERID
     C                   eval      U_Name   = getenv('REMOTE_USER':
     C                             qusec)
     C     U_Name        IFNE      *BLANKS
     C                   eval      USER   = uppify(U_Name)
     C                   END
     C                   EVAL      UsrSpcName  = zhbgetvar('UsrSpcName')
     C                   endsr
      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
      *
     C                   CLOSE     BRIDF
     C                   CLOSE     cundl01
     C                   CLOSE     HEADF
     C                   CLOSE     FAIN
     C                   CLOSE     FAK8
     C                   CLOSE     WRKCGI
     C                   endsr
      *=====================================================================
      * Read output skeleton html member into memory
      *=====================================================================
     C     LoadHtml      begsr
     C                   callp     gethtml('HTMLSRC':
     C                             '*LIBL':
S01  C*                            'ESARKF2':'/CGITAG/')
N01  C                             'ESARKF2'+XSPRÅK:
N01  C                             '/CGITAG/')
     C                   endsr
      *=====================================================================
      *  Set variable data for section /$top
      *=====================================================================
     C     SetTop        begsr
OddEvC                   callp     updHTMLvar('ROWHEAD':RowHead)
OddEvC                   callp     updHTMLvar('LogoImg':LogoImg)
      *
      * hvis *I*=Intern bruker - finn kunde via egen param (etter 20102007 parm XINTERN)
     c                   Move      BIBESK        TSTINTERN         3
     C     TSTINTERN     Ifeq      '*I*'
oddevC                   move      'J'           XINTERN
oddevC                   end
oddevC     XINTERN       Ifeq      'J'
     C     KUNR2n        Andne     *zeros
     c                   move      KUNR2N        BIKUNR
     C     KunKey        CHAIN     Cundl01                            33
     c                   end
     C                   callp     updHtmlVar('INTEG':
     C                             %trim(%editc(WINTEG:'Z')))
BODY C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updHTMLvar('USER':USER)
     C                   callp     updHtmlVar('UsrSpcName':UsrSpcName)
     C                   callp     updHTMLvar('BESK':BIBESK)
     C                   callp     updHtmlVar('KUNDE':
     C                             %trim(%editc(BIKUNR:'Z')))
     C                   callp     updHTMLvar('KNAVN':KNAVN)
n01  c     '&':' '       xlate     KNAVN         KNAVN
     C                   callp     updHtmlVar('WSNA':KNAVN)
      * Søkeparam
     C                   callp     updHTMLvar('FAKNR':FAKNR)
     C                   callp     updHTMLvar('Kunr2':Kunr2)
     C                   callp     updHTMLvar('SORTTXT':SORTTXT)
     C                   callp     updHTMLvar('SORT':SORT)
     C                   callp     updHTMLvar('IKKEBRUDD':IKKEBRUDD)
     C     IkkeBrudd     Ifeq      'Y'
     C                   eval      ICHECKED = 'checked'
     C                   end
     C                   callp     updHTMLvar('ICHECKED':ICHECKED)
      *
     C                   endsr
      *=====================================================================
      *  Legg ut aktuelle oppdrag/akkumuler beløp i Wfil kun 1. gand
      *=====================================================================
     C     SetWrk        begsr
     C     FAKKEY        SETLL     FAK8
     C     FAKKEY        READE     FAK8                                   33
     c  N33FAKDA         COMP      'K'                                    33
     c  N33FAKDA         COMP      'D'                                    33
     c  N33FAOPKO        COMP      'D'                                    33
     c                   IF        *IN33 = *OFF
      * Blank i Wkey2 gir samme sortering som fysisk faktura (som FAIN)
     c                   eval      wkey2   = *Blanks
     C                   exsr      FaktBEL
     c                   write     WRKCGIR
     c                   END
     C                   endsr
      *=====================================================================
      *  Resorter workfil etter valgt nøkkel
      *=====================================================================
     C     SortWrk       begsr
     c     WINTEG        setLL     WRKCGI
     c     WINTEG        ReadE     WRKCGI                                 35
     c     *IN35         doweq     '0'
     C                   select
     C     SORT          WHENEQ    'DR'
     c                   eval      wkey2   = DRKEY
     c                   eval      SortTxt = 'Deres ref/PO'
N01  c                   if        XSPRÅK='EN'
N01  c                   eval      SortTxt = 'Your ref/PO'
N01  c                   endif
     C     SORT          WHENEQ    'VR'
     c                   eval      wkey2   = VRKEY
     c                   eval      SortTxt = 'Vår ref'
N01  c                   if        XSPRÅK='EN'
N01  c                   eval      SortTxt = 'Our ref'
N01  c                   endif
     C     SORT          WHENEQ    'DT'
     c                   eval      wkey2   = *blanks
     c                   movel     DTKEY         wkey2
     c                   eval      SortTxt = 'Dato'
N01  c                   if        XSPRÅK='EN'
N01  c                   eval      SortTxt = 'Date'
N01  c                   endif
     C     SORT          WHENEQ    'AV'
     c                   eval      wkey2   = AVKEY+ASKEY
     c                   eval      SortTxt = 'Avsender(+sted)'
N01  c                   if        XSPRÅK='EN'
N01  c                   eval      SortTxt = 'Shipper/place'
N01  c                   endif
     C     SORT          WHENEQ    'MO'
     c                   eval      wkey2   = MOKEY+MSKEY
     c                   eval      SortTxt = 'Mottaker(+sted)'
N01  c                   if        XSPRÅK='EN'
N01  c                   eval      SortTxt = 'Consignee/place'
N01  c                   endif
     C     SORT          WHENEQ    'AS'
     c                   eval      wkey2   = ASKEY
     c                   eval      SortTxt = 'A.sted'
N01  c                   if        XSPRÅK='EN'
N01  c                   eval      SortTxt = 'Shp from'
N01  c                   endif
     C     SORT          WHENEQ    'MS'
     c                   eval      wkey2   = MSKEY
     c                   eval      SortTxt = 'M.sted'
N01  c                   if        XSPRÅK='EN'
N01  c                   eval      SortTxt = 'Shp to'
N01  c                   endif
     C                   other
     c                   eval      wkey2   = *Blanks
     c                   eval      SortTxt = 'Standard'
     C                   endsl
     C                   update    WRKCGIR
     c     WINTEG        ReadE     WRKCGI                                 35
     c                   end
     C                   endsr
      *=====================================================================
      *  Set variable data for section /$row
      *=====================================================================
     C     Setrow        begsr
     c     OddEven       IFEQ      ODD
     c                   eval      OddEven = EVEN
     c                   else
     c                   eval      OddEven = ODD
     c                   end
     C                   callp     updHTMLvar('ODDEVEN':OddEven)
     C     HeaKey        CHAIN     HEADF                              33
     C   33              CLEAR                   HEADFR
     C                   EVAL      DREF = HERFA
     C                   callp     updHTMLvar('DREF':DREF)
     C                   callp     updHTMLvar('VREF':VREF)
     C                   callp     updHTMLvar('avd':heavda)
     C                   callp     updHTMLvar('opd':heopda)
     C                   callp     updHTMLvar('TUR':
     C                             %trim(%editc(HEPRO:'Z')))
     C                   callp     updHTMLvar('DATO':
     C                             %trim(%editw(HEDTOP:'    .  .  ')))
     C                   callp     updhtmlvar('AVSEND':AVSEND)
     C                   callp     updhtmlvar('AVSTED':AVSTED)
     C                   callp     updhtmlvar('MOTT':MOTT)
     C                   callp     updhtmlvar('MOSTED':MOSTED)
     C                   callp     updHTMLvar('KLL':
     C                             %trim(%editc(HENT:'Z')))
     C                   callp     updhtmlvar('VBESK':HEVS1)
     C                   callp     updHTMLvar('VKT':
     C                             %trim(%editc(HEVKT:'Z')))
     C                   callp     updHTMLvar('M3':
     C                             %trim(%editc(HEM3:'3')))
     C                   callp     updHTMLvar('LM':
     C                             %trim(%editc(HELM:'3')))
     C                   callp     updHTMLvar('FVKT':
     C                             %trim(%editc(HEFBV:'Z')))
      * Beløp
     C**                 exsr      FaktBEL
     C                   callp     updHTMLvar('BELF':
     C                             %trim(%editc(BELF:'J')))
     C                   callp     updHTMLvar('BELN':
     C                             %trim(%editc(BELN:'J')))
     C                   callp     updHTMLvar('BELM':
     C                             %trim(%editc(BELM:'J')))
     C                   callp     updHTMLvar('BELB':
     C                             %trim(%editc(BELB:'J')))
      * Ta vare på mulige resorteringsfelt
     C                   Eval      DRKEY = DREF
     C                   Eval      VRKEY = VREF
     C                   MOVEL     HEDTOP        DTKEY
     C                   Eval      AVKEY = AVSEND
     C                   Eval      MOKEY = MOTT
     C                   Eval      ASKEY = AVSTED
     C                   Eval      MSKEY = MOSTED
      *
     C                   endsr
      *=====================================================================
      *  Set variable data for section /$brurow
      *=====================================================================
     C     SetBruRow     begsr
     C                   callp     updHTMLvar('ANTOPD':
     C                             %trim(%editc(ANTOPD:'Z')))
     C                   callp     updHTMLvar('BRUKLL':
     C                             %trim(%editc(BRUKLL:'Z')))
     C                   callp     updHTMLvar('BRUVKT':
     C                             %trim(%editc(BRUVKT:'Z')))
     C                   callp     updHTMLvar('BRUM3':
     C                             %trim(%editc(BRUM3:'3')))
     C                   callp     updHTMLvar('BRULM':
     C                             %trim(%editc(BRULM:'3')))
     C                   callp     updHTMLvar('BRUFVKT':
     C                             %trim(%editc(BRUFVKT:'Z')))
     C                   callp     updHTMLvar('BRUBELF':
     C                             %trim(%editc(BRUBELF:'J')))
     C                   callp     updHTMLvar('BRUBELN':
     C                             %trim(%editc(BRUBELN:'J')))
     C                   callp     updHTMLvar('BRUBELM':
     C                             %trim(%editc(BRUBELM:'J')))
     C                   callp     updHTMLvar('BRUBELB':
     C                             %trim(%editc(BRUBELB:'J')))
      *
     C                   endsr
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
     C                   OPEN      BRIDF
     C     USER          CHAIN     BRIDF                              50
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
      *
OddEv * hent Parametre som går igjen i mange prog:
OddEv *      Odd/Even/Rowhead-farger,Logobilde,Språk,Intern/Ekstern bruker,  mm
OddEvc                   call      'ESGETPAR'
OddEvc                   parm                    BIBRID
OddEvc                   parm                    BIFIRM
OddEvc                   parm                    BIKUNR
OddEvc                   parm                    BIKNG
OddEvc                   parm                    RowHead          10
OddEvc                   parm                    Odd              10
OddEvc                   parm                    Even             10
OddEvc                   parm                    LogoImg          50
OddEvc                   parm                    XSPRÅK            2
OddEvc                   parm                    XINTERN           1
OddEvc                   parm                    ParmDS1        1024
OddEvc                   parm                    ParmDS2        1024
OddEvc                   parm                    ParmDS3        1024
      *
     C                   OPEN      Cundl01
     C                   OPEN      HEADF
     C                   OPEN      FAIN
     C                   OPEN      FAK8
     C                   OPEN      WRKCGI
      *
     C     KunKey        KLIST
     C                   KFLD                    BIFIRM
     C                   KFLD                    BIKUNR
     C     HeaKey        KLIST
     C                   KFLD                    FIAVD
     C                   KFLD                    FIOPD
     C     FakKey        KLIST
     C                   KFLD                    FIAVD
     C                   KFLD                    FIOPD
     C                   KFLD                    FAKNRN
      *
     C     KunKey        CHAIN     Cundl01                            33
bhr   * Spesialvri for å kunne deme uten å avsløre kundenavn
bhr  c                   MOVEL     USER          tstjov            4
bhr  c     tstjov        ifeq      'JOVO'
bhr  c                   eval      KNAVN  = 'Jan Ottar Valderhaug'
bhr  c                   end
      *
      *
     C                   EVAL      STREK1  = '/'
     C                   EVAL      STREK2  = '/'
      *
     C                   endsr
      *
     C***********************************************
     C     FaktBEL       BEGSR
     C***********************************************
     C                   MOVE      *ZEROS        BELF
     C                   MOVE      *ZEROS        BELN
     C                   MOVE      *ZEROS        BELM
     C                   MOVE      *ZEROS        BELB
     C     FAKKEY        SETLL     FAK8
     C     FAKKEY        READE     FAK8                                   33
     C     *in33         DOWEQ     '0'
     c     FAKDA         cabeq     'K'           NEXTFA
     c     FAKDA         cabeq     'D'           NEXTFA
     c     FAOPKO        cabeq     'D'           NEXTFA
     C                   ADD       FABELN        BELN
     C                   ADD       FABELN        BELB
     C     FAVK          IFEQ      'FRA'
     C                   ADD       FABELN        BELF
     C                   END
     C     FAKDM         IFEQ      'J'
     C     FABELN        MULT      0,25          MOMS
     c                   ELSE
     C                   MOVE      *ZEROS        MOMS
     c                   end
     C                   ADD       MOMS          BELM
     C                   ADD       MOMS          BELB
     C     NEXTFA        TAG
     C     FAKKEY        READE     FAK8                                   33
     c                   end
     c                   endsr
