     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A          R B20
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  3'SYSPED/SYIS12 '
     A                                      COLOR(BLU)
     A                                  1 25MSGCON(030 TBR0167 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 57'Dato:'
     A                                      COLOR(BLU)
     A                                  1 63DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  2 57'Kl. :'
     A                                      COLOR(BLU)
     A                                  2 63TIME
     A                                      COLOR(BLU)
     A                                  4  3'Velg funksjon, trykk Enter'
     A                                      COLOR(BLU)
     A                                  4 40'  2=Endre  4=Slette'
     A                                      COLOR(BLU)
     A                                  6  3'Valg'
     A                                      COLOR(WHT)
     A                                  6  9MSGCON(010 TBR0168 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            ZVKODE         5A  B  7  9
     A                                 10 10'(Ingen poster i filen)   '
     A  30        MSGTXT        78A  O 24  2TEXT('FEIL I INNTASTEDE DATA')
     A                                      COLOR(WHT)
     A          R B40
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF03(03 'Avslutt')
     A N31                                  CF05(05 'Forny')
     A                                      CF06(06 'Ny linje')
     A  31                                  CF12(12 'Avbryt')
     A                                      SLNO(23)
     A                                      CLRL(02)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2MSGCON(010 TBR0013 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  2  2TEXT('FEIL I INNTASTEDE DATA')
     A*
     A          R H50                       SFL
     A            SFRRN         10S 0H
     A            WSNR           2A  I  8  3VALUES('  ' '1 ' '01' ' 1')
     A            SVKODE         5   O  8  9
     A            SVTEXT        35A  O  8 21
     A            SVPKD          5   O  8 57
     A*
     A          R K60                       SFLCTL(H50)
     A                                      SFLSIZ(2000)
     A                                      SFLPAG(0013)
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A                                  1  3'SYSPED/SYIS12'
     A                                      COLOR(BLU)
     A                                  1 23MSGCON(030 TBR0167 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 57'Dato:'
     A                                      COLOR(BLU)
     A                                  1 63DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  2 57'Kl. :'
     A                                      COLOR(BLU)
     A                                  2 63TIME
     A                                      COLOR(BLU)
     A            ZRECNO         4S 0O  2 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  4  4MSGCON(020 TBR0165 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  6  2'Valg'
     A                                      COLOR(WHT)
     A                                  6  9MSGCON(010 TBR0168 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  6 21MSGCON(020 TBR0105 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            ZVKODE         5A  B  7  9
