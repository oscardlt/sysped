      *   Tester ut kontrollsiffer for KID (MODULUS 11)
      *   KID i felt på 25 alfa kommer inn via LDA pos 701-725
      *   og returkode pos 726 settes lik 0 hvis OK og lik 1
      *   hvis ikke OK
      *   - Benytt FREE, av dette pgm. i det kallende program
      *     ved LR, da dette pgm. bare benytter return (*INRT)
      **********************************************************
      * Hjelpevariabler
     D ALFA            S             25
     D NUMMER          S             25  0
     D VERDI           S              1  0
     D VEKT            S              1  0
     D X               S              2  0
     D Y               S              2  0
     D REST            S              2  0
     D WKONTR          S                   LIKE(KONTRL)
      *
     D MO1             S              1    DIM(25)
     D MO2             S              3  0 INZ(0)
     D                                     DIM(25)
      *
     D                 DS
     D KONTRL                 25     25
     D WKID                    1     25
      *
      * Parameterliste
      *
     D P_ENTRY         DS
     D  PLKID                        25
     D  PRC                           1
      *
     C     *ENTRY        PLIST
     C                   PARM                    P_ENTRY
      *
      * Kid høyrestilles med foranstilte nuller
      *
     C                   EVAL      WKID        = *ZEROS
     C                   EVAL      PLKID       = %TRIML(PLKID)
     C                   EVAL      X           = %SCAN(' ' : PLKID)
     C                   EVAL      Y           = 26 - X
     C                   IF        Y < 26
     C                   EVAL      WKID        = %SUBST(WKID:1:Y) +
     C                                           %SUBST(PLKID:1:X-1)
     C                   ELSE
     C                   EVAL      WKID        = PLKID
     C                   ENDIF
      *
      * Oppgitt kontrollsiffer spares, mens siste siffer i kid nullstilles
      *
     C                   EVAL      WKONTR      = KONTRL
     C                   EVAL      KONTRL      = *ZEROS
      *
      * Beregning av tverrsum uten kontrollsiffer (X=24)
      *
     C                   MOVEA     WKID          MO1
      *
      * Finner vekttall for hver enkelt posisjon
      *
     C                   EVAL      X           = 24
     C     X             DOWGT     0
     C                   SELECT
     C     X             WHENEQ    24
     C     X             OREQ      18
     C     X             OREQ      12
     C     X             OREQ      6
     C                   EVAL      VEKT        = 2
     C     X             WHENEQ    23
     C     X             OREQ      17
     C     X             OREQ      11
     C     X             OREQ      5
     C                   EVAL      VEKT        = 3
     C     X             WHENEQ    22
     C     X             OREQ      16
     C     X             OREQ      10
     C     X             OREQ      4
     C                   EVAL      VEKT        = 4
     C     X             WHENEQ    21
     C     X             OREQ      15
     C     X             OREQ      9
     C     X             OREQ      3
     C                   EVAL      VEKT        = 5
     C     X             WHENEQ    20
     C     X             OREQ      14
     C     X             OREQ      8
     C     X             OREQ      2
     C                   EVAL      VEKT        = 6
     C     X             WHENEQ    19
     C     X             OREQ      13
     C     X             OREQ      7
     C     X             OREQ      1
     C                   EVAL      VEKT        = 7
     C                   ENDSL
      *
      * Beregner sum
      *
     C                   MOVE      MO1(X)        VERDI
     C                   EVAL      MO2(X)      = VERDI * VEKT
     C                   EVAL      X           = X - 1
     C                   ENDDO                                                  <X
      *
      * Beregner tverrsum
      *
     C                   XFOOT     MO2           PRODUK            5 0
      *
      * Tverrsum divideres med 11.
      * Rest trekkes ifra 11 som gir kontrollsiffer
      * Hvis REST = 10 skal kontrollsiffer settes = "-"
     C     PRODUK        DIV       11            PRODUK
     C                   MVR                     PRODUK
     C                   IF        PRODUK = 0
     C                   EVAL      KONTRL = '0'
     C                   ELSE
     C                   EVAL      REST   = 11 - PRODUK
     C                   IF        REST   < 10
     C                   MOVE      REST          KONTRL
     C                   ELSE
     C                   EVAL      KONTRL = '-'
     C                   ENDIF
     C                   ENDIF
      *
      * Test om beregnet kontrollsiffer er lik oppgitt kontrollsiffer
      *
     C                   IF        KONTRL      <> WKONTR
     C                   EVAL      PRC         = '1'
     C                   ELSE                                                   <KONTRL
     C                   EVAL      PRC         = '0'
     C                   ENDIF                                                  <KONTRL
      *
     C                   EVAL      *INRT       = *ON
