********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
11XXX * 01 YBC PTF11 TVINN INFO SENDES PÅ LOGINF OG SHPDOC
********************************************************************
     FSADH      IF   E           K DISK
     FSADV      IF   E           K DISK
     FEDIM4     IF   E           K DISK
     FFRISOK    IF   E           K DISK
     FTVINF     IF   E           K DISK
     FSHPDOC    UF   E           K DISK
     FLOG       O  A E             DISK
     FLOGINF    O  A E             DISK
     D                 DS
     D  FSSOK                  1     35
     D  FSSOK1                 1     10
     D  FSSOK2                11     20
N01  D                 DS
N01  D  M0068A                 1      8  0
N01  D  X_M0068A               1      8
N01  D                 DS
N01  D  M0068B                 1      6  0
N01  D  X_M0068B               1      6
N01  D                 DS
N01  D  M0068C                 1      2  0
N01  D  X_M0068C               1      2
     C                   EXSR      SRINIT
     C                   EXSR      SRA
     C                   EVAL      *INLR = *ON
      *__________________________________________________________
      * SR for initiering                                  SRINIT
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     SRINIT        BEGSR
     C     *ENTRY        PLIST
     C                   PARM                    WAVD              4
     C                   PARM                    WOPD              7

     C     *LIKE         DEFINE    CMPY          WCMPY
     C     *LIKE         DEFINE    WHSE          WWHSE
     C     *LIKE         DEFINE    SHPN          WSHPN
     C     *LIKE         DEFINE    DOCN          WDOCN
     C     *LIKE         DEFINE    SIAVD         WSIAVD
     C     *LIKE         DEFINE    SITDN         WSITDN
     C     *LIKE         DEFINE    SVLI          WSVLI
     C     *LIKE         DEFINE    MSR           WMSR
     C     *LIKE         DEFINE    M0068A        WM0068A
     C     *LIKE         DEFINE    M0068B        WM0068B
     C     *LIKE         DEFINE    M0068C        WM0068C
     C     *LIKE         DEFINE    FMN           WFMN
     C     *LIKE         DEFINE    FLN           WFLN
     C                   MOVE      WAVD          WSIAVD
     C                   MOVE      WOPD          WSITDN
     C                   EVAL      WCMPY = 'NIMATOPAAL'
     C                   EVAL      WWHSE = 'IDUN'
     C                   EVAL      WMSR = 'R'
     C                   EVAL      WM0068A = 99999999
     C                   EVAL      WM0068B = 999999
     C                   EVAL      WM0068C = 99
     C                   EVAL      WFLN=1

     C     FRIKEY        KLIST
     C                   KFLD                    WSIAVD
     C                   KFLD                    WSITDN
     C                   KFLD                    WFSKODE           3
     C                   MOVE      '*CR'         WFSKODE
     C     SHPDOCK       KLIST
     C                   KFLD                    WCMPY
     C                   KFLD                    WWHSE
     C                   KFLD                    WSHPN
     C                   KFLD                    WDOCN
     C     EDIM4K1       KLIST
     C                   KFLD                    WMSR
     C                   KFLD                    WSIAVD
     C                   KFLD                    WSITDN
     C                   KFLD                    WM0068A
     C                   KFLD                    WM0068B
     C                   KFLD                    WM0068C
     C     EDIM4K2       KLIST
     C                   KFLD                    WMSR
     C                   KFLD                    WSIAVD
     C                   KFLD                    WSITDN
     C     TVINFKEY      KLIST
     C                   KFLD                    WFMN
     C                   KFLD                    WFLN
N01  C     SADHKEY       KLIST
N01  C                   KFLD                    WSIAVD
N01  C                   KFLD                    WSITDN
     C     SADVKEY       KLIST
     C                   KFLD                    WSIAVD
     C                   KFLD                    WSITDN
     C                   KFLD                    WSVLI
      * HENTE DAGENS DATO OG TID
     C                   CALL      'SYGE15C'
     C                   PARM                    WDATO             8
     C                   PARM                    WTID              6
     C                   ENDSR
      *__________________________________________________________
      * SRA                                                  SRA
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     SRA           BEGSR
      * Hent nøkkel til Tyringeoppdrag
     C     FRIKEY        CHAIN     FRISOK                             51
     C     *IN51         CABEQ     *ON           SRA9
      * Finnes Tyringeoppdrag ?
     C                   MOVE      FSSOK1        WSHPN
     C                   MOVE      FSSOK2        WDOCN
     C     SHPDOCK       CHAIN     SHPDOC                             51
     C     *IN51         CABEQ     *ON           SRA9
      * Hent siste svar fra TVINN
     C     EDIM4K1       SETLL     EDIM4
     C     EDIM4K2       READPE    EDIM4                                  51
     C     *IN51         CABEQ     *ON           SRA9
N01   * Hent ekspedisjonsenhet og løpenr
N01  C     SADHKEY       CHAIN     SADH                               51
N01  C     *IN51         CABEQ     *ON           SRA9
      * Oppdater SHPDOC med ny status
     C                   SELECT
     C     M1N07         WHENEQ    'DFI'
     C                   EVAL      DOCSTATUS = 'E1'
     C                   EVAL      SEVC = 'ERR'
     C     M1N07         WHENEQ    'DME'
     C                   EVAL      DOCSTATUS = 'E1'
     C                   EVAL      SEVC = 'ERR'
     C                   EVAL      WFMN=MMN
     C                   EVAL      *IN51=*OFF
     C     TVINFKEY      CHAIN     TVINF                              51
     C                   IF        *IN51=*OFF
     C                   IF        F4815='NE'
     C                   IF        F0078A='972'
     C                   EVAL      DOCSTATUS = 'ZA'
     C                   ENDIF
     C                   ENDIF
     C                   ENDIF
     C     M1N07         WHENEQ    'DUA'
     C                   EVAL      DOCSTATUS = 'ZB'
     C                   EVAL      SEVC = 'INF'
     C     M1N07         WHENEQ    'DTK'
     C                   EVAL      DOCSTATUS = 'ZB'
     C                   EVAL      SEVC = 'INF'
     C                   ENDSL
N01  C                   EVAL      REFEXT2 = %TRIM(X_M0068A) + %TRIM(X_M0068B)
N01  C                             + %TRIM(X_M0068C)
N01  C                             + %TRIM(SITLE) + %TRIM(SITLL)
     C                   UPDATE    SHPDOCR
      * Skriv eventuell fritext fra TVINN til Tyringe logg
     C                   EVAL      *IN51=*OFF
     C                   EVAL      WFMN=MMN
     C     MMN           MULT      10            LOGN
     C     TVINFKEY      SETLL     TVINF
     C     WFMN          READE     TVINF                                  51
     C     *IN51         DOWEQ     *OFF

     C                   ADD       1             LOGN
     C                   EVAL      SEQN = LOGN
     C                   EVAL      EVTD = 'RCV'
     C                   EVAL      MSOR = 'CUSDEC'
     C                   EVAL      DOCTP = M1N07
     C                   MOVE      WDATO         CRTDTE
     C                   MOVE      WTID          CRTTME
     C                   EVAL      LOGT = *BLANKS
S01  C*                  EVAL      LOGT ='Ref.kod: ' + F0077 + '  Funk.kod: '
N01  C                   EVAL      LOGT ='Dato: ' + X_M0068A
N01  C                             + 'Sekv.nr: ' + X_M0068B
N01  C                             + 'Ver.nr: ' + X_M0068C
N01  C                             + '  Ref.kod: ' + F0077 + '  Funk.kod: '
     C                             + F4815 + '  Text:  ' + F0078A + '     '
     C                             + F0078B + '     ' + F0078C + '     '
     C                             + F0078D + '     ' + F0078E
     C                   MOVEL     WCMPY         CMPY
     C                   MOVEL     WWHSE         WHSE
     C                   MOVEL     WSHPN         SHPN
     C                   MOVEL     WDOCN         DOCN
     C                   WRITE     LOGR                                 21
     C                   WRITE     LOGINFR                              21
      * Finnes texten: VARELINJE XX ?
     C     'VARELINJE'   SCAN      F0078A                                 20
     C                   IF        *IN20
     C     5             SUBST     F0078A:11     WSVLIA            5
      /free
        monitor;
           WSVLI = %Dec(  %Xlate('%,*' : '   ' : WSVLIA) : 5 : 0);
        on-error 105;
           WSVLI = 0;
        endmon;
      /end-free
     C     SADVKEY       CHAIN     SADV                               20
     C                   IF        *IN20 = *OFF
     C                   EVAL      LOGT = *BLANKS
     C                   EVAL      LOGT ='Varunummer: ' + SVFT
     C                   ADD       1             LOGN
     C                   WRITE     LOGR                                 21
     C                   WRITE     LOGINFR                              21
     C                   ENDIF
     C                   ENDIF

     C     MMN           READE     TVINF                                  51
     C                   ENDDO

     C     SRA9          ENDSR
