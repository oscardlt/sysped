      *
      ***********************************************************
      *** PROGRAM SKAL plukke dokumenter til ekspressfortoll. ***
      ***********************************************************
     H DFTACTGRP(*NO)
     FARKTXT    IF   E           K DISK
     FARKEXT    IF   E           K DISK
     FFIRMARC   IF   E           K DISK
     FFIRM      IF   E           K DISK
     FARKEPOW   UF A E           K DISK
     FARKIVL15  IF   E           K DISK
     FTELLGE    UF A E           K DISK
      *  Prototype for tvi40r
     d tvi40r          pr
     d uavd_                               like(uavd)
     d uopd_                               like(uopd)
      *  *ENTRY Interface for Main Procedure
     d tvi40r          pi
     d uavd                           4a
     d uopd                           7a
      *  Prototype for 'TVI40C'
     d tvi40c          pr                  extpgm('TVI40C')
     d epost_                              like(epost)
     d epuser_                             like(epuser)
     d uavd_                               like(uavd)
     d uopd_                               like(uopd)
     d arcmrg_                             like(arcmrg)
      *
     d epost           s             70a
     d xpos            s              3s 0
     d                 ds
     d  xan07                  1      7a
     d  xn07                   1      7S 0

      /free
       exsr init;

       chain 'TVINEXPF' tellge;
       if %found;
         xn07 = geno;
         geno = geno + 1;
         update tellger;
       else;
         xn07 = 1000;
         geco = 'TVINEXPF';
         geno = 1001;
         write tellger;
       endif;

       epuser = 'EP-' + %trim(xan07);
       epavd  = aravd;
       epopd  = aropd;

       setll (aravd: aropd) arkivl15;
       reade (aravd: aropd) arkivl15;
       dow not %eof;
         if %subst(artype:1:1) = 'Z';
           chain (artype) arktxt;
           if arklag = *blanks;
             chain (fifirm) firmarc;
           else;
             chain (fifirm: arklag) arkext;
           endif;
           epbane = '/' + %trim(arcane) + '/' + %trim(arlink);
           xpos=%Scan('.pdf':arlink);
           if xpos = 0;
             xpos=%Scan('.PDF':arlink);
             if xpos = 0;
               xpos=%Scan('.':arlink);
             endif;
           endif;
           if xpos = 0;
             epbane = %trim(epbane) + '.pdf';
           endif;
           select;
           when artype = 'ZH';
             eptex1 = 'Faktura:';
           when artype = 'ZO';
             eptex1 = 'Opprinnelsesdokumentasjon:';
           when artype = 'ZC';
             eptex1 = 'Fraktbrev:';
           when artype = 'ZB';
             eptex1 = 'Lisens:';
           other;
             eptex1 = 'Dokument:';
           endsl;
           eptex2 = %trim(arlink);
           write arkepor;
         endif;
         reade (aravd: aropd) arkivl15;
       enddo;

       epost = '+@toll.no';
       epost = 'helpdesk@systema.no';
       arcmrg = 'J';
       tvi40c (epost: epuser: uavd: uopd: arcmrg);

       exsr toemfil;

       *inlr = *on;
       return;

       //***********************************************
       begsr init;
       //***********************************************

       aravd = %int(uavd);
       aropd = %int(uopd);
       setll *loval firm;
       read firm;

       endsr;

       //***********************************************
       begsr toemfil;
       //***********************************************

       setll (epuser) arkepow;
       reade (epuser) arkepow;
       dow not %eof;
         delete arkepor;
         reade (epuser) arkepow;
       enddo;

       endsr;

      /end-free
