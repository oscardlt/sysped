     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A          R BILDE1
     A                                      CF03(03 'EXIT')
     A                                      CF04(04 'SØK ')
     A                                      CF12(12 'UT')
     A                                  1 19MSGCON(045 DIV3731 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                  1  2'SYSPED/SYTR11'
     A                                      COLOR(BLU)
     A                                  1 67DATE
     A                                      EDTCDE(Y)
     A                                  2 67TIME
     A                                  3  2MSGCON(016 DIV3732 *LIBL/SYMSGF)
     A            DRANSA    R        B  3 19REFFLD(DRIVR/DRANSA *LIBL/DRIV)
     A                                      COLOR(WHT)
     A                                  5  2MSGCON(016 DIV3744 *LIBL/SYMSGF)
     A            WSKODE         1A  B  5 19VALUES('N' 'E' 'F')
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(018 DIV0032 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 42MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  44        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A          R BILDE2
     A                                      CF03(03 'EXIT')
     A                                      CF12(12 'UT')
     A                                  1  2'SYSPED/SYTR11'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(045 DIV3731 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                  1 67DATE
     A                                      EDTCDE(Y)
     A                                  2 67TIME
     A                                  3  2MSGCON(016 DIV3732 *LIBL/SYMSGF)
     A            DRANSA    R        O  3 19REFFLD(DRIVR/DRANSA *LIBL/DRIV)
     A                                      COLOR(WHT)
     A                                  5  2MSGCON(016 DIV3733 *LIBL/SYMSGF)
     A            WSNAVN    R        B  5 19REFFLD(DRIVR/DRNAVN *LIBL/DRIV)
     A                                  6  2MSGCON(016 DIV3734 *LIBL/SYMSGF)
     A            WSADR1    R        B  6 19REFFLD(DRIVR/DRADR1 *LIBL/DRIV)
     A                                  7  2MSGCON(016 DIV3735 *LIBL/SYMSGF)
     A            WSADR2    R        B  7 19REFFLD(DRIVR/DRADR2 *LIBL/DRIV)
     A                                  8  2MSGCON(016 DIV3736 *LIBL/SYMSGF)
     A            WSADR3    R        B  8 19REFFLD(DRIVR/DRADR3 *LIBL/DRIV)
     A                                  9  2MSGCON(016 DIV3737 *LIBL/SYMSGF)
     A            WSALFA    R        B  9 19REFFLD(DRIVR/DRALFA *LIBL/DRIV)
     A                                 10  2MSGCON(016 DIV3738 *LIBL/SYMSGF)
EV5  A            WSADAT         6  0B 10 19
     A                                      EDTCDE(4)
     A                                 11  2MSGCON(016 DIV3739 *LIBL/SYMSGF)
EV5  A            WSSDAT         6  0B 11 19
     A                                      EDTCDE(4)
     A                                 12  2MSGCON(016 DIV3740 *LIBL/SYMSGF)
     A            WSSE1K    R        B 12 19REFFLD(DRIVR/DRSE1K *LIBL/DRIV)
EV5  A            WSSE1D         6  0B 12 24
     A                                      EDTCDE(4)
     A            WSSE2K    R        B 12 36REFFLD(DRIVR/DRSE2K *LIBL/DRIV)
EV5  A            WSSE2D         6  0B 12 41
     A                                      EDTCDE(4)
     A            WSSE3K    R        B 12 53REFFLD(DRIVR/DRSE3K *LIBL/DRIV)
EV5  A            WSSE3D         6  0B 12 58
     A                                      EDTCDE(4)
     A            WSSE4K    R        B 12 70REFFLD(DRIVR/DRSE4K *LIBL/DRIV)
EV5  A            WSSE4D         6  0B 12 75
     A                                      EDTCDE(4)
     A            WSSE5K    R        B 13 19REFFLD(DRIVR/DRSE5K *LIBL/DRIV)
EV5  A            WSSE5D         6  0B 13 24
     A                                      EDTCDE(4)
     A            WSSE6K    R        B 13 36REFFLD(DRIVR/DRSE6K *LIBL/DRIV)
EV5  A            WSSE6D         6  0B 13 41
     A                                      EDTCDE(4)
     A            WSSE7K    R        B 13 53REFFLD(DRIVR/DRSE7K *LIBL/DRIV)
EV5  A            WSSE7D         6  0B 13 58
     A                                      EDTCDE(4)
     A            WSSE8K    R        B 13 70REFFLD(DRIVR/DRSE8K *LIBL/DRIV)
EV5  A            WSSE8D         6  0B 13 75
     A                                      EDTCDE(4)
     A                                 14  2MSGCON(016 DIV3741 *LIBL/SYMSGF)
     A            WSPASN    R        B 14 19REFFLD(DRIVR/DRPASN *LIBL/DRIV)
EV5  A            WSPASD         6  0B 14 32
     A                                      EDTCDE(4)
     A                                 15  2MSGCON(016 DIV3742 *LIBL/SYMSGF)
     A            WSTLF     R        B 15 19REFFLD(DRIVR/DRTLF *LIBL/DRIV)
     A                                 16  2MSGCON(016 DIV3743 *LIBL/SYMSGF)
     A            WSFRIT    R        B 16 19REFFLD(DRIVR/DRFRIT *LIBL/DRIV)
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  44        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
