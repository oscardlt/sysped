      *
      ************************************************************
      *** PROGRAM SKAL SPØRRE PÅ TURER (IMPORT)                ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER     ***
      *** LEDIGE INDIKATORER 01 02 04-26                       ***
      *** LEDIGE INDIKATORER 27-96                             ***
      ************************************************************
      *
     FFIRM      IF   E           K DISK
     FHEAD8     IF   E           K DISK
     FKODTA     IF   E           K DISK
     FTURER     IF   E           K DISK
     FTURER2    IF   E           K DISK
     F                                     RENAME(TURERR:TURER2R)
     FSYFF53FM  CF   E             WORKSTN
     D                 DS
     D  WSDT                   1      6  0
     D  XDTD                   1      2  0
     D  XDTM                   3      4  0
     D  XDTÅ                   5      6  0
     C*
     C     HEAD8K        KLIST
     C                   KFLD                    WSAVD
     C                   KFLD                    WSPRO
     C     KODTAK        KLIST
     C                   KFLD                    YUNI
     C                   KFLD                    WSAVD
     C     TURERK        KLIST
     C                   KFLD                    WSAVD
     C                   KFLD                    WSPRO
     C*
     C                   MOVE      'A'           YUNI              1
     C                   MOVE      'J'           USPØR             1
     C                   MOVE      ' '           UIMP              1
     C                   MOVE      *ZEROS        WSDT
     C     FIFIRM        SETLL     FIRM
     C                   READ      FIRM                                   99
     C*
     C     START         TAG
     C                   EXSR      KDTFMT
     C                   EXFMT     BILDE
     C                   EXSR      KDTFMT
     C                   SETOFF                                       30
     C                   SETOFF                                       979899
     C   03              GOTO      SLUTT
     C*
     C     WSAVD         IFNE      0
     C*--------------------------------------------------
     C*** TEST AVDELING OG TURNR
     C     KODTAK        CHAIN     KODTA                              99
     C   99              MOVE      'SPA0001'     MSGNR
     C   99              SETON                                        30
     C   99              GOTO      START
     C     TURERK        CHAIN     TURER                              98
     C  N98              DO
     C                   CALL      'SYEX88A'
     C                   PARM                    FIFIRM
     C                   PARM                    WSAVD
     C                   PARM                    WSPRO
     C                   PARM                    USPØR
     C                   PARM                    UIMP
     C                   PARM                    UFLAGG            1 0
     C     UFLAGG        COMP      3                                      03
     C  N03              GOTO      START
     C                   GOTO      SLUTT
     C                   END
     C*--------------------------------------------------
     C*** TEST OM DET FINNES OPPDRAG UTEN TURPLANELGGING
     C                   SETOFF                                       98
     C     HEAD8K        SETLL     HEAD8
     C     HEAD8K        READE     HEAD8                                  98
     C   98              MOVE      'SPT0017'     MSGNR
     C   98              SETON                                        30
     C   98              GOTO      START
     C*** VISE TUR PÅ SKJERMEN (AVDELING+TURNR)
     C                   Z-ADD     0             UFLAGG            1 0
     C                   CALL      'SYFF53A'
     C                   PARM                    FIFIRM
     C                   PARM                    WSAVD
     C                   PARM                    WSPRO
     C                   PARM                    UFLAGG
     C     UFLAGG        IFNE      3
     C                   MOVE      *ZEROS        WSAVD
     C                   MOVE      *ZEROS        WSPRO
     C                   GOTO      START
     C                   END
     C*--------------------------------------------------
     C                   ELSE
     C*==================================================
     C*** TEST OM BILNR ER 0
     C     WSBILN        COMP      *BLANKS                                97
     C  N97WSBILN        CHAIN     TURER2                             97
     C   97              MOVE      'SPB0003'     MSGNR
     C   97              SETON                                        30
     C   97              GOTO      START
     C*** VISE TUR PÅ SKJERMEN (BILNR+DATO)
     C     WSDT          IFEQ      0
     C                   MOVE      '510101'      UDT
     C                   ELSE
     C                   MOVE      WSDT          UDT               6 0
     C                   MOVEL     XDTÅ          UDT
     C                   MOVE      XDTD          UDT
     C                   END
     C                   Z-ADD     0             UFLAGG
     C                   CALL      'SYFF53B'
     C                   PARM                    FIFIRM
     C                   PARM                    WSBILN
     C                   PARM                    UDT
     C                   PARM                    UFLAGG
     C     UFLAGG        IFNE      3
     C                   MOVE      *BLANKS       WSBILN
     C                   MOVE      *ZEROS        WSDT
     C                   MOVE      *ZEROS        UDT
     C                   GOTO      START
     C                   END
     C*==================================================
     C                   END
     C*
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
     C*
     C***********************************************
     C     KDTFMT        BEGSR
     C***********************************************
     C                   MOVE      XDTD          UDT1              2 0
     C                   MOVE      XDTM          UDT2              2 0
     C                   MOVE      XDTÅ          UDT3              2 0
     C                   CALL      'DATOFMT'
     C                   PARM                    UDT1
     C                   PARM                    UDT2
     C                   PARM                    UDT3
     C                   MOVE      UDT1          XDTD
     C                   MOVE      UDT2          XDTM
     C                   MOVE      UDT3          XDTÅ
     C*
     C                   ENDSR
     C*
