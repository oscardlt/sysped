      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/TNOE033R MODULE(*LIBL/TNOE033R *LIBL/INCGI)
CRTPGM*        ACTGRP(CGI) AUT(*USE)
      *
      *********************************************************************
      /copy SYSPEDS/qrpgsrcpy,hspecs
      /copy SYSPEDS/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FSAEF      IF   E           K DISK    USROPN
     FKODTVAL2  IF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy SYSPEDS/qrpgsrcpy,prototypeb
      /copy SYSPEDS/qrpgsrcpy,usec
      /copy SYSPEDS/qrpgsrcpy,variables3
      *
      * Varible for
     D WSUSER          S             10
     D SYAVA           S              4
     D SYAV            S              4  0
     D SYOPA           S              7
     D SYOP            S              7  0
     D JSONstring      S          32000
     D Error           S            150
     D AntLest         S              9  0
      *get input-string
      /copy syspeds/qrpgsrcpy,prolog3

      * Parse input
     C                   exsr      Parse
      * Open files etc
     C                   EXSR      INIT
      *
     c                   callp     gethtmlifs(
     C                             '/syspeddev/html/JSON.html':
     C                             '/CGITAG/')
     C                   callp     wrtsection('top')
      *
      * ............................................................................................
      * skriv JSON-Object start..(alltid '{' + x ant param. m/komma mellom (IKKE komma etter siste))
      * ............................................................................................
      *
      /free
        JSONstring='{'
        +'¬user¬ : ¬'+%trim(WSUSER)+'¬, '
        +'¬avd¬ : ¬'+%trim(SYAVA)+'¬, '
        +'¬opd¬ : ¬'+%trim(SYOPA)+'¬, '
        +'¬errMsg¬ : ¬'+%trim(Error)+'¬, ';
      /end-free
      * JSONfix escaper " i tekst,  for deretter å bytte ¬->"
     c                   call      'JSONFIX'
     c                   parm                    JSONstring
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')
      *
      * ............................................................................................
      * Skriv JSON-LISTE Start (en liste er EN parameter(fra [ til   )
      * ............................................................................................
     c                   eval      JSONstring ='"invTot" : ['
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')
      *
      * Liste Ærende
      *
     c     Error         ifeq      *blanks
     C                   EXSR      TOT
     C                   Add       1             AntLest
      * ............................................................................................
      * Skriv en JSON-Array-linje pr menypunkt - komma før hvert nytt element
      * ............................................................................................
      /free
       if AntLest=1;
          JSONstring=*blanks;
          else;
          JSONstring=', ';
          endif;
       JSONstring=%trim(JSONstring)+'{'
          +'¬tot_vk28¬ : ¬'+%trim(tot_vk28)+'¬, '
          +'¬tot_kr28¬ : ¬'+%trim(%editc(tot_kr28:'4'))+'¬, '
          +'¬tot_om28¬ : ¬'+%trim(%editc(tot_om28:'4'))+'¬, '
          +'¬tot_bl28¬ : ¬'+%trim(%editc(tot_bl28:'4'))+'¬}';
      /end-free
     C                   call      'JSONFIX'
     C                   parm                    JSONstring
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')

     c                   ENDIF
      * ............................................................................................
      * Skriv JSON-Liste/Array  Avslutning
      * ............................................................................................
     c                   eval      JSONstring =']'
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')
      * ............................................................................................
      * Skriv JSON-string Avsluttning
      * ............................................................................................
     c                   eval      JSONstring ='}'
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')
      *
      * Quit
     C                   exsr      Exit


      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Lukk fil
     C                   CLOSE     BRIDF
     C  N50              CLOSE     SAEF
     C  N50              CLOSE     KODTVAL2

      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   MOVE      *ON           *INLR
     C                   return
     C                   endsr
      *
      *********************************************************
     C     Parse         Begsr
      *********************************************************
      * Get input
     C                   EVAL      WSUSER = zhbgetvar('USER')
     C                   EVAL      SYAVA = zhbgetvar('AVD')
     C                   EVAL      SYAV  = c2n2(SYAVA)
     C                   EVAL      SYOPA= zhbgetvar('OPD')
     C                   EVAL      SYOP = c2n2(SYOPA)
     c                   endsr
      *
      *********************************************************
     C     INIT          Begsr
      *********************************************************
     C                   OPEN      BRIDF
      * Test innlogging
     C     WSUSER        CHAIN     BRIDF                              50
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C                   call      BILIBL
     C                   end
      *
     C     SAEFKEY       KLIST
     C                   KFLD                    SYAV
     C                   KFLD                    SYOP
     C     KODTVKEY      KLIST
     C                   KFLD                    SFVK28
     C                   KFLD                    ZVVK_DTS
     C     *LIKE         DEFINE    SFVK28        tot_vk28
     C     *LIKE         DEFINE    SFKR28        tot_kr28
     C     *LIKE         DEFINE    SFOM28        tot_om28
     C     *LIKE         DEFINE    SFBL28        tot_bl28
     C     *LIKE         DEFINE    SFDT          ZVVK_DTS
     C     *LIKE         DEFINE    SFBL28        W_FABL
      *
     C   50              eval      Error = 'Invalid userID'
     C  N50              OPEN      SAEF
     C  N50              OPEN      KODTVAL2
      *_________________________________
      * Hæmta dagens datum och klockslag
      *"""""""""""""""""""""""""""""""""
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVEL     WDT           ZVVK_DTS

     c                   endsr
      *_____________________________________________
      * Summera alla fakturor                    TOT
      *"""""""""""""""""""""""""""""""""""""""""""""
     C     TOT           BEGSR
      * Finnes beløp ?
     C                   Z-ADD     0             X                 5 0
     C                   EVAL      *IN55=*OFF
     C     SAEFKEY       SETLL     SAEF
     C     SAEFKEY       READE     SAEF                                   55
     C     *IN55         DOWEQ     '0'
     C     SFTXT         IFNE      *BLANKS
     C     SFVK28        ANDNE     *BLANKS
     C     SFKR28        ANDNE     *ZEROS
     C     SFBL28        ANDNE     *ZEROS

     C                   ADD       1             X
     C                   IF        X=1
     C                   EVAL      TOT_VK28=SFVK28
     C                   ELSE
     C     SFVK28        IFNE      TOT_VK28
     C                   EVAL      *IN71=*ON
     C                   EVAL      TOT_VK28='NOK'
     C                   LEAVE
     C                   ENDIF
     C                   ENDIF

     C                   ENDIF
     C     SAEFKEY       READE     SAEF                                   55
     C                   ENDDO
     C     TOT_VK28      CABEQ     *BLANKS       TOT9
      * Summera belopp
     C                   EVAL      *IN55=*OFF
     C     SAEFKEY       SETLL     SAEF
     C     SAEFKEY       READE     SAEF                                   55
     C     *IN55         DOWEQ     '0'
     C     SFTXT         IFNE      *BLANKS
     C     SFVK28        ANDNE     *BLANKS
     C     SFKR28        ANDNE     *ZEROS
     C     SFBL28        ANDNE     *ZEROS
     C                   IF        *IN71
     C                   EXSR      TOTVAL
     C                   ADD       W_FABL        TOT_BL28
     C                   ELSE
     C                   ADD       SFBL28        TOT_BL28
     C                   ENDIF
     C                   ENDIF
     C     SAEFKEY       READE     SAEF                                   55
     C                   ENDDO
      * Hæmta valutakurs och omrækningsfaktor
     C                   IF        TOT_VK28='NOK'
     C                   EVAL      TOT_KR28=1
     C                   EVAL      TOT_OM28=1
     C                   ELSE
     C                   EVAL      *IN64=*OFF
     C                   EVAL      SFVK28=TOT_VK28
     C     KODTVKEY      SETGT     KODTVAL2
     C                   READP     KODTVAL2                               64
     C     *IN64         IFEQ      *OFF
     C                   EVAL      TOT_KR28=SFKR28
     C                   EVAL      TOT_OM28=SFOM28
     C                   ENDIF
     C                   ENDIF
     C     TOT9          ENDSR
      *_____________________________________________
      * Rækna om till NOK                     TOTVAL
      *"""""""""""""""""""""""""""""""""""""""""""""
     C     TOTVAL        BEGSR
     C                   EVAL      *IN64=*OFF
     C                   EVAL      W_FABL=*ZEROS
     C     KODTVKEY      SETGT     KODTVAL2
     C                   READP     KODTVAL2                               64
     C     *IN64         IFEQ      *OFF
     C     SFBL28        MULT      KVAKRS        W_FABL
     C                   IF        KVAOMR > 0
     C     W_FABL        DIV       KVAOMR        W_FABL
     C                   ENDIF
     C                   ENDIF
     C                   ENDSR
