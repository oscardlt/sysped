     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      ALTHELP
     A                                      HELP
      *
     A          R SVI002DA                  SFL
     A            ZOP            1A  I  8  2VALUES(' ' '2' '4' '5')
     A            SVIF_FATY      4A  O  8  4
     A            SVIF_FATX     17A  O  8  9
     A            SVIF_VAKD      3A  O  8 27
     A            SVIF_VAKU      7Y 4O  8 38EDTCDE(4)
     A            SVIF_FABL     11Y 3O  8 47EDTCDE(4)
      *
     A          R SVI002DB                  SFLCTL(SVI002DA)
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0012)
     A  91N93N94                            ROLLUP(25 'Rollup')
     A  91N93N94                            ROLLDOWN(26 'Rolldown')
     A                                      CF03(03 'Gå ut')
     A                                      CF04(04 'SØK EXTREF')
     A                                      CF05(05 'Refresh')
     A                                      CF06(06 'NY')
     A                                      CF12(12 'Forrige')
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A                                  1  2'SVI002D'
     A                                      COLOR(BLU)
     A                                  1 10'DA'
     A                                      COLOR(BLU)
     A                                  1 27'Bilagda handlingar '
     A                                      COLOR(WHT)
     A            ZRECNO         4S 0O  2 20SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  3  2'Vælj option eller tryck Enter.'
     A                                      COLOR(BLU)
     A                                  5  2'2=Ændra   4=Radera'
     A                                      COLOR(BLU)
     A                                  7  9'Identitet    '
     A                                      COLOR(WHT)
     A                                  7 27'Valutaslag'
     A                                      COLOR(WHT)
     A                                  7 42'Kurs'
     A                                      COLOR(WHT)
     A                                  7 53'Belopp'
     A                                      COLOR(WHT)
     A                                  7  4'Typ'
     A                                      COLOR(WHT)
      *
     A          R SVI002DC
     A                                      OVERLAY
     A                                 23  2'F3=Avsluta'
     A                                      COLOR(BLU)
     A                                 23 15'F4=Søk'
     A                                      COLOR(BLU)
     A                                 23 24'F6=Skapa'
     A                                      COLOR(BLU)
     A                                 23 35'F12=Avbryt'
     A                                      COLOR(BLU)
      *
     A          R SVI002DD
     A                                      OVERLAY
     A                                      CF03(03 'Gå ut')
     A                                      CF04(04 'SØK')
     A                                      CF12(12 'Forrige')
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            ZVIF_FATY      4A  B 22  4DSPATR(PC)
     A            ZVIF_FATX     17A  B 22  9
     A            ZVIF_VAKD      3A  B 22 27
     A            ZVIF_VAKU      7Y 4B 22 38EDTCDE(4)
     A            ZVIF_FABL     11Y 3B 22 47EDTCDE(4)
     A                                 23  4'F3=Avsluta'
     A                                      COLOR(BLU)
     A                                 23 17'F12=Avbryt'
     A                                      COLOR(BLU)
      *
     A          R SVI002DØ
     A                                      OVERLAY
     A                                 21  2' '
     A            ZMSGNO         7A  P
     A            ZMSGTX        79   O 24  2MSGID(&ZMSGNO SYMSGF)
     A                                      COLOR(WHT)
