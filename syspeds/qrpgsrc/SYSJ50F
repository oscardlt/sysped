      *
      ***********************************************************
      *** PROGRAM SKAL VEDLIKEHOLDE BILL OF LADING            ***
      ***        VARELINJER (TYPE-2)                          ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01 02 04 07-09 11 14 16-24       ***
      *** LEDIGE INDIKATORER 27 28 31 32 34-39 41-88 96 97    ***
      ***********************************************************
      *
     FBLV1      IF   E           K DISK
     FBLV2      UF A E           K DISK
     FBLV2LF    IF   E           K DISK
     F                                     RENAME(BLV2R:BLV2RLF)
     FKODTFT    IF   E           K DISK
     FKOFAST    IF   E           K DISK
     FSYSJ50FF  CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D                 DS
     D  BVMN                   1     17
     D  XDLT                   1      7
     D                 DS
     D  WSMN1                  1     17
     D  WSMN2                 18     34
     D  WSMN3                 35     51
     D  WSMN4                 52     68
     D  MN                     1     68
     D                                     DIM(4)
     D                 DS
     D  WSFT1                  1     51
     D  WSFT2                 52    102
     D  WSFT3                103    153
     D  WSFT4                154    204
     D  FT                     1    204
     D                                     DIM(4)
     C*
     C                   EXSR      INIT
     C*
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   MOVE      *ZEROS        ZRECNO
     C                   MOVE      *ZEROS        XNESTE            3 0
     C                   MOVE      XANTL1        XANTL             3 0
     C                   MOVE      *ZEROS        XLI               3 0
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     BLV2K1        SETLL     BLV2LF
     C     BLV2K1        READE     BLV2LF                                 94
     C  N94              MOVE      BVLI          YLI
     C  N94              ADD       4             XANTL
      *
     C  N94              DO
     C                   DO        999
     C     BVLI          COMP      XLI                                    95
     C                   ADD       1             ZRECNO
     C     BVLI          IFGT      XNESTE
     C                   MOVE      BVLI          XNESTE
     C                   END
     C                   WRITE     SUBFIL
     C                   MOVE      BVLI          XLI
     C     BLV2K1        READE     BLV2LF                                 94
     C  N94BVLI          IFNE      YLI
     C                   MOVE      BVLI          YLI
     C                   ADD       4             XANTL
     C                   END
     C  N94              END
     C                   END
      *
     C                   ADD       1             XNESTE
     C                   Z-ADD     ZRECNO        MAXRCN            4 0
     C     XANTL         COMP      10                                 29
     C     ZRECNO        IFGT      0
     C                   Z-ADD     1             ZRECNO
     C                   END
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     3             POSNBR
     C*
     C     START3        TAG
     C                   SETOFF                                       9998
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   WRITE     NYVARE
     C                   MOVE      MAXRCN        XSISTE
     C                   Z-ADD     XNESTE        YLI
     C     XNESTE        IFLE      900
     C     XNESTE        MULT      10            WSSORT
     C                   ELSE
     C                   MOVE      XNESTE        WSSORT
     C                   END
     C  N12              Z-ADD     19            LINNBR
     C  N12              Z-ADD     2             POSNBR
     C  N12              GOTO      START5
     C                   EXFMT     BUNN
     C                   ELSE
     C                   WRITE     SUBCTL
     C                   EXFMT     BUNN
     C                   END
     C*
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C                   Z-ADD     WSSUBN        XRECNO            4 0
     C     XRECNO        IFEQ      0
     C                   Z-ADD     ZRECNO        XRECNO            4 0
     C                   END
     C*
     C                   SETOFF                                       9830
     C*** AVSLUTT
     C   03              Z-ADD     3             UFLAGG
     C   03              GOTO      SLUTT
     C*** FRISK OPP
     C   05              GOTO      START
     C*** LAGE NY AVDELING
     C   06              DO
     C                   MOVE      MAXRCN        XSISTE
     C                   Z-ADD     XNESTE        YLI
     C     XNESTE        IFLE      900
     C     XNESTE        MULT      10            WSSORT
     C                   ELSE
     C                   MOVE      XNESTE        WSSORT
     C                   END
     C                   Z-ADD     19            LINNBR
     C                   Z-ADD     2             POSNBR
     C                   GOTO      START5
     C                   END
     C*** TIL TYPE 1
     C   10              Z-ADD     1             UFLAGG
     C   10              GOTO      SLUTT
     C*** AVSLUTT
     C   12              Z-ADD     0             UFLAGG
     C   12              GOTO      SLUTT
     C*** TIL SIDE 2
     C   13              DO
     C                   CALL      'SYSJ50G'
     C                   PARM                    UAVD
     C                   PARM                    UOPD
     C                   PARM                    UDTR
     C                   PARM                    USG
     C                   PARM                    UUKE
     C                   PARM                    UAAR
     C                   PARM                    UFLAGG
     C                   GOTO      START3
     C                   END
     C*** AVSLUTT
     C   15              DO
     C     BLK           SETLL     BLV1
     C     BLK           READE     BLV1                                   30
     C   30              MOVE      'DIV4241'     MSGNR
     C   30              GOTO      START3
     C                   Z-ADD     5             UFLAGG
     C                   GOTO      SLUTT
     C                   END
     C*
     C*** BLA VIDERE
     C   25              DO
     C                   ADD       17            ZRECNO
     C     ZRECNO        IFGT      MAXRCN
     C                   Z-ADD     MAXRCN        ZRECNO
     C                   END
     C                   Z-ADD     19            LINNBR
     C                   Z-ADD     2             POSNBR
     C                   SETOFF                                       25
     C                   GOTO      START3
     C                   END
     C*** BLA TILBAKE
     C   26              DO
     C                   SUB       17            ZRECNO
     C     ZRECNO        IFLE      0
     C     MAXRCN        IFEQ      0
     C                   Z-ADD     0             ZRECNO
     C                   ELSE
     C                   Z-ADD     1             ZRECNO
     C                   END
     C                   END
     C                   SETOFF                                       26
     C                   GOTO      START3
     C                   END
     C*
     C     ZRECNO        IFNE      0
     C                   EXSR      HENT
     C                   END
     C   98XRECNO        IFNE      0
     C                   Z-ADD     XRECNO        ZRECNO
     C                   END
     C                   GOTO      START3
     C*
     C*** LAGE NY
     C     START5        TAG
     C                   EXFMT     VARELI
     C                   MOVEA     '000'         *IN(88)
     C                   SETOFF                                           30
     C   04              DO
     C                   EXSR      SØKKD
     C                   GOTO      START5
     C                   END
     C   12              DO
     C                   EXSR      MOVB2
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     3             POSNBR
     C                   GOTO      START3
     C                   END
     C                   EXSR      TESTB1
     C   30              GOTO      START5
     C     WSKDF         IFNE      *BLANKS
     C                   MOVE      *BLANKS       WSKDF
     C                   GOTO      START5
     C                   END
     C                   MOVEA     '11'          *IN(91)
     C                   WRITE     SUBCTL
     C                   MOVE      MAXRCN        XSISTE
     C                   Z-ADD     XNESTE        YLI
     C                   GOTO      START5
     C*
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
     C*
     C***********************************************
     C     TESTB1        BEGSR
     C***********************************************
     C                   DO        4             XN                1 0
     C     MN(XN)        COMP      *BLANKS                                90
     C   90FT(XN)        COMP      *BLANKS                                90
     C   90              END
     C   90WSKDF         IFEQ      *BLANKS
     C                   MOVE      'DIV3174'     MSGNR
     C                   SETON                                        30
     C                   ELSE
     C                   SETOFF                                       90
     C                   END
     C*
     C     WSKDF         IFNE      *BLANKS
     C                   SETON                                        89
     C                   DO        4             YLN
     C     KODFTK        CHAIN     KODTFT                             33
     C  N33              MOVEL     KFTFTX        FT(YLN)
     C  N33              SETOFF                                       89
     C   33              MOVE      *BLANKS       FT(YLN)
     C                   END
     C   89
     CANN30              DO
     C                   MOVE      'SPK0131'     MSGNR
     C                   SETON                                        30
     C                   END
     C                   END
     C*
     C                   MOVEL     WSKD          KFKOD
     C     KOFASK        CHAIN     KOFAST                             88
     C   88
     CANN30              DO
     C                   MOVE      'SPK0132'     MSGNR
     C                   SETON                                        30
     C                   END
     C*
     C   30
     CANN89              MOVE      *BLANKS       WSKDF
     C   30              EXSR      SETCUR
     C   30              GOTO      SLTB1
     C     WSKDF         IFNE      *BLANKS
     C                   GOTO      SLTB1
     C                   END
     C*
     C*
     C*** OPPDATERE PÅ REGISTER OG SUBFIL
     C                   EXSR      DLTOLD
     C                   SETOFF                                       95
     C                   DO        4             YLN
     C     BLV2K         CHAIN     BLV2                               33
     C     MN(YLN)       IFEQ      *BLANKS
     C     FT(YLN)       ANDEQ     *BLANKS
     C  N33              DELETE    BLV2R
     C                   ELSE
     C                   EXSR      MOVWBV
     C  N33              UPDATE    BLV2R
     C   33              WRITE     BLV2R
     C                   ADD       1             MAXRCN
     C                   Z-ADD     MAXRCN        ZRECNO
     C                   WRITE     SUBFIL
     C  N95              SETON                                        95
     C                   END
     C                   END
     C*
     C  N40              ADD       1             XNESTE
     C  N40              ADD       4             XANTL
     C  N40XANTL         COMP      10                                 29
     C     XSISTE        IFEQ      0
     C                   Z-ADD     1             ZRECNO
     C                   ELSE
     C                   Z-ADD     XSISTE        ZRECNO
     C                   END
     C*                    WRITESUBCTL
     C                   EXSR      MOVB2
     C*
     C     SLTB1         ENDSR
     C*
     C***********************************************
     C     SETCUR        BEGSR
     C***********************************************
     C   88
     COR 89              Z-ADD     22            POSNBR
     C   90              Z-ADD     2             POSNBR
     C*
     C   88              Z-ADD     21            LINNBR
     C   89
     COR 90              Z-ADD     19            LINNBR
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     HENT          BEGSR
     C***********************************************
     C                   Z-ADD     0             LINNBR
     C                   SETOFF                                       919299
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C   98              DO
     C   99              SETOFF                                       98
     C                   Z-ADD     1             LINNBR
     C                   GOTO      SLHENT
     C                   END
     C     ZRECNO        DIV       17            LINNBR
     C                   MVR                     XLIN              2 0
     C     XLIN          IFEQ      0
     C                   Z-ADD     17            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C     WSNR          IFEQ      '2'
     C                   MOVE      ' '           WSNR
     C     XDLT          COMP      '*DELETE'                              98
     C   98              GOTO      SLHENT
     C                   EXSR      CHGBV
     C   12              GOTO      SLHENT
     C                   END
     C     WSNR          IFEQ      '3'
     C                   MOVE      ' '           WSNR
     C     XDLT          COMP      '*DELETE'                              98
     C   98              GOTO      SLHENT
     C                   EXSR      CPYBV
     C   12              GOTO      SLHENT
     C                   END
     C     WSNR          IFEQ      '4'
     C                   MOVE      ' '           WSNR
     C     XDLT          COMP      '*DELETE'                              98
     C   98              GOTO      SLHENT
     C                   EXSR      DLTBV
     C   12              GOTO      SLHENT
     C                   END
     C                   END
     C*
     C     SLHENT        TAG
     C                   ADD       4             LINNBR
     C                   Z-ADD     3             POSNBR
     C                   SETOFF                                       99
     C   98              DO
     C                   MOVE      'DIV0012'     MSGNR
     C                   SETON                                            30
     C                   END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     CHGBV         BEGSR
     C***********************************************
     C                   EXSR      MOVBVW
     C                   SETON                                        99
     C                   Z-ADD     19            LINNBR
     C                   Z-ADD     2             POSNBR
     C     STCHG         TAG
     C                   EXFMT     VARELI
     C                   MOVEA     '000'         *IN(88)
     C                   SETOFF                                         30
     C   04              DO
     C                   EXSR      SØKKD
     C                   GOTO      STCHG
     C                   END
     C   12              DO
     C                   Z-ADD     XSISTE        ZRECNO
     C                   EXSR      MOVB2
     C                   END
     C  N12              EXSR      TESTB1
     C  N12
     CAN 30              GOTO      STCHG
     C     XLIN          IFEQ      0
     C                   Z-ADD     15            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     CPYBV         BEGSR
     C***********************************************
     C                   MOVE      '0'           *IN95
     C                   UPDATE    SUBFIL
     C                   EXSR      MOVBVW
     C                   MOVE      MAXRCN        XSISTE
     C                   Z-ADD     XNESTE        YLI
     C                   ADD       1             WSSORT
     C                   SETOFF                                       40
     C                   SETON                                        99
     C                   Z-ADD     19            LINNBR
     C                   Z-ADD     2             POSNBR
     C     STCPY         TAG
     C                   EXFMT     VARELI
     C                   MOVEA     '000'         *IN(88)
     C                   SETOFF                                         30
     C   04              DO
     C                   EXSR      SØKKD
     C                   GOTO      STCPY
     C                   END
     C     *IN12         IFEQ      '1'
     C                   Z-ADD     XSISTE        ZRECNO
     C                   EXSR      MOVB2
     C                   ELSE
     C                   EXSR      TESTB1
     C   30              GOTO      STCPY
     C                   END
     C     XLIN          IFEQ      0
     C                   Z-ADD     15            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     DLTBV         BEGSR
     C***********************************************
     C                   EXSR      MOVBVW
     C                   SETON                                        99
     C                   EXFMT     VAREL3
     C   12              Z-ADD     XSISTE        ZRECNO
     C  N12              DO                                                     1<-B
     C                   EXSR      DLTOLD
     C                   DO        4             YLN                             2<-B
     C     BLV2K         CHAIN     BLV2                               33
     C  N33              DELETE    BLV2R
     C                   END                                                     2<-E
     C                   SUB       4             XANTL
     C     XANTL         COMP      10                                 29
     C                   ADD       1             YLN
     C     YLN           IFEQ      XNESTE                                        2<-B
     C                   SUB       1             XNESTE
     C                   END                                                     2<-E
     C                   EXSR      MOVB2
     C                   END                                                    1<-E
     C*
     C     XLIN          IFEQ      0                                            1<-B
     C                   Z-ADD     15            LINNBR
     C                   ELSE                                                   1
     C                   Z-ADD     XLIN          LINNBR
     C                   END                                                    1<-E
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     DLTOLD        BEGSR
     C***********************************************
     C                   SETON                                        95
     C                   MOVE      XSISTE        ZRECNO
     C     STDLTO        TAG
     C     ZRECNO        IFLE      MAXRCN
     C     ZRECNO        ANDNE     0
     C     ZRECNO        CHAIN     SUBFIL                             33
     C  N33YLI           IFNE      BVLI
     C   40              SETOFF                                       95
     C                   UPDATE    SUBFIL
     C                   GOTO      SLDLTO
     C                   END
     C  N33              DO
     C                   EXSR      MOVB1
     C                   UPDATE    SUBFIL
     C                   ADD       1             ZRECNO
     C                   GOTO      STDLTO
     C                   END
     C                   ELSE
     C                   MOVE      MAXRCN        ZRECNO
     C                   END
     C*
     C     SLDLTO        ENDSR
     C*
     C***********************************************
     C     MOVWBV        BEGSR
     C***********************************************
     C                   MOVE      UAVD          BVAVD
     C                   MOVE      UOPD          BVOPD
     C                   MOVE      YLI           BVLI
     C                   MOVE      YLN           BVLI2
     C                   MOVE      WSSORT        BVSORT
     C                   MOVE      MN(YLN)       BVMN
     C                   MOVE      FT(YLN)       BVFT
     C                   MOVE      WSKD          BVKD
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVBVW        BEGSR
     C***********************************************
     C                   MOVE      BVLI          YLI
     C                   MOVE      BVSORT        WSSORT
     C     STMBVW        TAG
     C     ZRECNO        IFGT      1
     C                   SUB       1             ZRECNO
     C     ZRECNO        CHAIN     SUBFIL                             33
     C  N33BVLI          COMP      YLI                                3333
     C  N33              GOTO      STMBVW
     C     ZRECNO        ADD       1             XSISTE            4 0
     C                   ELSE
     C                   Z-ADD     1             XSISTE
     C                   END
     C*
     C                   DO        4             YLN
     C     BLV2K         CHAIN     BLV2                               33
     C  N33              UPDATE    BLV2R
     C  N33              MOVE      BVMN          MN(YLN)
     C  N33              MOVE      BVFT          FT(YLN)
     C                   END
     C                   SETON                                        40
     C                   MOVE      BVKD          WSKD
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVB1         BEGSR
     C***********************************************
     C                   MOVE      *ZEROS        BVLI
     C                   MOVE      *ZEROS        BVSORT
     C                   MOVE      *BLANKS       BVMN
     C                   MOVE      '*DELETE'     XDLT
     C                   MOVE      *BLANKS       BVKD
     C                   MOVE      *BLANKS       BVFT
     C                   MOVE      *BLANKS       WSNR
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVB2         BEGSR
     C***********************************************
     C                   SETOFF                                       40
     C     XNESTE        IFLE      900
     C     XNESTE        MULT      10            WSSORT
     C                   ELSE
     C                   MOVE      XNESTE        WSSORT
     C                   END
     C                   MOVE      *BLANKS       MN
     C                   MOVE      *BLANKS       WSKD
     C                   MOVE      *BLANKS       WSKDF
     C                   MOVE      *BLANKS       FT
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     SØKKD         BEGSR
     C***********************************************
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C                   Z-ADD     WSSUBN        XRECNO            4 0
      *
     C                   Z-ADD     1             UFLAGG
     C     LINNBR        IFEQ      19
     C     POSNBR        ANDGE     22
     C     POSNBR        ANDLE     23
     C                   CALL      'SYFI10I'
     C                   PARM                    WSKDF
     C     WSKDF         IFNE      *BLANKS
     C                   DO        4             YLN
     C     KODFTK        CHAIN     KODTFT                             33
     C  N33              MOVEL     KFTFTX        FT(YLN)
     C   33              MOVE      *BLANKS       FT(YLN)
     C                   END
     C                   MOVE      *BLANKS       WSKDF
     C                   END
     C                   Z-ADD     21            LINNBR
     C                   Z-ADD     22            POSNBR
     C                   GOTO      SLSØK
     C                   END
      *
     C     LINNBR        IFEQ      21
     C     POSNBR        ANDEQ     22
     C                   MOVEL     'SEASYS'      UTYP             10
     C                   CALL      'SYFAKDA'
     C                   PARM                    UTYP
     C                   PARM                    UUPDAT            1
     C     WSKD          PARM      WSKD          UKD               5
     C                   Z-ADD     19            LINNBR
     C                   Z-ADD     2             POSNBR
     C                   GOTO      SLSØK
     C                   END
     C*
     C                   MOVE      'DIV1037'     MSGNR
     C                   SETON                                        30
     C*
     C     SLSØK         ENDSR
     C*
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     BLK           KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    UOPD
     C     BLV2K         KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    UOPD
     C                   KFLD                    YLI
     C                   KFLD                    YLN
     C     BLV2K1        KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    UOPD
     C     BLV1K         KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    UOPD
     C                   KFLD                    YLI
     C     KODFTK        KLIST
     C                   KFLD                    WSKDF
     C                   KFLD                    YLN
     C     KOFASK        KLIST
     C                   KFLD                    KFTYP
     C                   KFLD                    KFKOD
     C*
     C     *ENTRY        PLIST
     C                   PARM                    UAVD              4 0
     C                   PARM                    UOPD              7 0
     C                   PARM                    UDTR              6 0
     C                   PARM                    USG               3
     C                   PARM                    UUKE              2 0
     C                   PARM                    UAAR              2 0
     C                   PARM                    UFLAGG            1 0
     C*** UFLAGG = 0  -->  FRA LOKAL B/L
     C*** UFLAGG = 1  -->  FRA B/L (ÅPNE MULIGHET Å GÅ TIL OPPDRAG)
     C     UFLAGG        COMP      1                                      27
     C*
     C                   MOVE      *ZEROS        YLI               3 0
     C                   MOVE      *ZEROS        YLN               1 0
     C                   MOVEL     'SEASYS'      KFTYP
     C*
     C*** TELLER ANTALL LINJER AV TYPE 1
     C     BLV1K         SETLL     BLV1
     C     STINI         TAG
     C     UAVD          READE     BLV1                                   33
     C  N33UOPD          COMP      BVOPD                              3333
     C  N33              ADD       1             XANTL1            3 0
     C  N33              GOTO      STINI
     C*
     C                   ENDSR
     C*
