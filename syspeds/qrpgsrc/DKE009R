     FSADV      IF   E           K DISK
     FTARIEU    IF   E           K DISK
     FHEADF     IF   E           K DISK
     FDKEV      O  A E             DISK
     D                 DS
     D  SVVNT                  1      8S 0
     D  SVVNT6                 1      6
      *//////////////
      *  Hodelogikk /
      *//////////////
     C                   EXSR      SRA
     C                   EXSR      SRB
     C                   MOVE      '1'           *INLR
      *////////////////////////////////////////////////////////////
      *  Initiering                                           SRA /
      *////////////////////////////////////////////////////////////
     C     SRA           BEGSR
      *_________________
      * Input parametrer
      *"""""""""""""""""
     C     *ENTRY        PLIST
     C                   PARM                    UVEV_SYAV
     C                   PARM                    UVEV_SYOP
      *___________________
      * Definiere Workfelt
      *"""""""""""""""""""
     C     *LIKE         DEFINE    DKEV_SYAV     UVEV_SYAV
     C     *LIKE         DEFINE    DKEV_SYOP     UVEV_SYOP
     C     *LIKE         DEFINE    SVAVD         USVAVD
     C     *LIKE         DEFINE    SVTDN         USVTDN
     C                   EVAL      USVAVD=UVEV_SYAV
     C                   EVAL      USVTDN=UVEV_SYOP
      *__________________
      * Definiere Nøkkler
      *""""""""""""""""""
     C     SVKEY         KLIST
     C                   KFLD                    USVAVD
     C                   KFLD                    USVTDN
      *_________________________________
      * Hæmta dagens datum och klockslag
      *"""""""""""""""""""""""""""""""""
N01  C                   CALL      'SYGE15C'
N01  C                   PARM                    WDT               8
N01  C                   PARM                    WTM               6
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Bearbeide Formater                                   SRB /
      *////////////////////////////////////////////////////////////
     C     SRB           BEGSR
      * Oppdrag
     C     SVKEY         CHAIN     HEADF                              55
      * Varuposter
     C     SVKEY         SETLL     SADV
     C     SVKEY         READE     SADV                                   53
     C     *IN53         DOWEQ     *OFF
     C                   IF        SVVKTB > 0
     C                   CLEAR                   DKEVR
      *
     C                   EVAL      DKEV_SYAV=SVAVD
     C                   EVAL      DKEV_SYOP=SVTDN
     C                   EVAL      DKEV_SYLI=SVLI
     C                   EVAL      DKEV_313=1
     C                   EVAL      DKEV_314='PA'
     C                   EVAL      DKEV_315=SVVT
     C                   EVAL      DKEV_32=SVLI
      * De 6 första tecknen är lika i EU
     C                   MOVEL     SVVNT6        DKEV_331
      * Finne x-ref ?
     C     SVVNT         CHAIN     TARIEU                             54
     C     *IN54         IFEQ      *OFF
     C     TARIEUE       ANDNE     *BLANKS
     C                   EVAL      DKEV_331=TARIEUE
     C                   ENDIF
     C                   EVAL      DKEV_34A=SVLK
     C                   EVAL      DKEV_37='1000000'
     C                   EVAL      DKEV_35=SVVKTB
     C                   EVAL      DKEV_38=SVVKTN
     C                   EVAL      DKEV_412=SVNTM
     C                   EVAL      DKEV_42=SVBELT
     C                   EVAL      DKEV_46=SVBELT
      *
     C                   WRITE     DKEVR
     C                   ENDIF
     C     SVKEY         READE     SADV                                   53
     C                   ENDDO
     C                   ENDSR
