     FFREETUR   UF   E           K DISK    USROPN
     FHEAD11    IF   E           K DISK
     FISOFL7    UF   E           K DISK    USROPN
     FISOLFL1   UF   E           K DISK    USROPN
     FKOSBU13   UF   E           K DISK    USROPN
     FREISL01   UF   E           K DISK    USROPN
     FSTSTUR1   UF   E           K DISK    USROPN
     FTILLL02   UF   E           K DISK    USROPN
     FTRAVH4    UF   E           K DISK    USROPN
     FTRAVV4    UF   E           K DISK    USROPN
     FTUFE5     UF   E           K DISK    USROPN
     FTUFET     UF   E           K DISK    USROPN
     FTURER     UP   E             DISK
     FFREETUROLDO  A E           K DISK    USROPN
     F                                     RENAME(FREETURR:FREETURRO)
     FISOFOLD   O  A E           K DISK    USROPN
     F                                     RENAME(ISOFR:ISOFRO)
     FISOLFOLD  O  A E           K DISK    USROPN
     F                                     RENAME(ISOLFR:ISOLFRO)
     FKOSBUOLD  O  A E           K DISK    USROPN
     F                                     RENAME(KOSBUR:KOSBURO)
     FREISOLD   O  A E           K DISK    USROPN
     F                                     RENAME(REISR:REISRO)
     FSTSTUROLD O  A E           K DISK    USROPN
     F                                     RENAME(STSTURR:STSTURRO)
     FTILLOLD   O  A E           K DISK    USROPN
     F                                     RENAME(TILLR:TILLRO)
     FTRAVHOLD  O  A E           K DISK    USROPN
     F                                     RENAME(TRAVHR:TRAVHRO)
     FTRAVVOLD  O  A E           K DISK    USROPN
     F                                     RENAME(TRAVVR:TRAVVRO)
     FTUFEOLD   O  A E           K DISK    USROPN
     F                                     RENAME(TUFER:TUFERO)
     FTUFETOLD  O  A E           K DISK    USROPN
     F                                     RENAME(TUFETR:TUFETRO)
     FTUREROLD  O  A E           K DISK    USROPN
     F                                     RENAME(TURERR:TURERRO)
     FREOPDAFM  CF   E             WORKSTN MAXDEV(*FILE)
     F                                     INFDS(FEEDBK)
      *
     D FEEDBK          DS
     D  STATUS           *STATUS
     D                 DS
     D  WSDT                   1      6  0
     D  WSDTD                  1      2  0
     D  WSDTM                  3      4  0
     D  WSDTÅ                  5      6  0
     D                UDS
     D  DT                   101    106  0
     D  DTÅ                  101    102  0
     D  DTM                  103    104  0
     D  DTD                  105    106  0
     D  UIND26               107    107
     D  UIND27               108    108
     D  UIND28               109    109
     D  UIND29               110    110
      * HVIS F3 I FORIGE - HOPP UT.
     C     DT            IFEQ      *ZEROS
     C                   SETON                                        LR
     C                   RETURN
     C                   END
     C                   MOVE      DT            DT8               8 0
     C                   IF        DTÅ > 50
     C                   MOVEL     '19'          DT8
     C                   ELSE
     C                   MOVEL     '20'          DT8
     C                   END
      *
     C                   IF        *IN(98) = *OFF
     C                   MOVE      UIND26        *IN(26)
     C                   MOVE      UIND27        *IN(27)
     C                   MOVE      UIND28        *IN(28)
     C                   MOVE      UIND29        *IN(29)
     C                   MOVE      DTD           WSDTD
     C                   MOVE      DTM           WSDTM
     C                   MOVE      DTÅ           WSDTÅ
     C                   MOVE      '1'           *IN(98)
     C                   TIME                    WSTDST
     C                   EXSR      KDTFMT
     C                   WRITE     BILDE
     C                   READ      REOPDAFM                             3333
     C                   END
      **
     C     TURK          KLIST
     C                   KFLD                    TUAVD
     C                   KFLD                    TUPRO
      *
     C     *ENTRY        PLIST
     C                   PARM                    UDELETE           1
      * ÅPNE FILER
     C     XOPEN         IFNE      'J'
     C                   EXSR      OPENFILES
     C                   END
      * For turer med dato (avg.dato>0) t.o.m "slettedato"
      *  hvis ingen oppdrag slett (hvis minst ett finnes - behold)
      *
     C                   IF        ((TUDT > 0) AND (TUDT <= DT8))
     C     TUPRO         CHAIN     HEAD11                             31
      * Finnes ikke i HEADF   - slett i TURER/FREETUR
     C                   IF        *IN(31)
      *
     C                   IF        XFREETUR = 'Y'
     C     TURK          SETLL     FREETUR                                32
     C                   IF        *IN(32)
     C     TURK          READE     FREETUR                                33
     C                   DOW       *IN(33) = *OFF
     C  N50              WRITE     FREETURRO
     C                   DELETE    FREETURR
     C     TURK          READE     FREETUR                                33
     C                   END
     C                   END
     C                   END
      *
     C                   IF        XISOF = 'Y'
     C     HEPRO         SETLL     ISOFL7                                 32
     C                   IF        *IN(32)
     C     HEPRO         READE     ISOFL7                                 33
     C                   DOW       *IN(33) = *OFF
     C     ISSAKN        SETLL     ISOLFL1
     C     ISSAKN        READE     ISOLFL1                                33
     C                   DOW       *IN(33) = *OFF
     C  N50              WRITE     ISOLFRO
     C                   DELETE    ISOLFR
     C     ISSAKN        READE     ISOLFL1                                33
     C                   ENDDO
     C  N50              WRITE     ISOFRO
     C                   DELETE    ISOFR
     C     HEPRO         READE     ISOFL7                                 33
     C                   ENDDO
     C                   END
     C                   END
      *
     C                   IF        XKOSBU13 = 'Y'
     C     TUPRO         SETLL     KOSBU13                                32
     C                   IF        *IN(32)
     C     TUPRO         READE     KOSBU13                                33
     C                   DOW       *IN(33) = *OFF
     C  N50              WRITE     KOSBURO
     C                   DELETE    KOSBUR
     C     TUPRO         READE     KOSBU13                                33
     C                   END
     C                   END
     C                   END
      *
     C                   IF        XREISL01 = 'Y'
     C     TUPRO         SETLL     REISL01                                32
     C                   IF        *IN(32)
     C     TUPRO         READE     REISL01                                33
     C                   DOW       *IN(33) = *OFF
     C  N50              WRITE     REISRO
     C                   DELETE    REISR
     C     TUPRO         READE     REISL01                                33
     C                   END
     C                   END
     C                   END
      *
     C                   IF        XSTSTUR1 = 'Y'
     C     TUPRO         SETLL     STSTUR1                                32
     C                   IF        *IN(32)
     C     TUPRO         READE     STSTUR1                                33
     C                   DOW       *IN(33) = *OFF
     C  N50              WRITE     STSTURRO
     C                   DELETE    STSTURR
     C     TUPRO         READE     STSTUR1                                33
     C                   END
     C                   END
     C                   END
      *
     C                   IF        XTILLL02 = 'Y'
     C     TUPRO         SETLL     TILLL02                                32
     C                   IF        *IN(32)
     C     TUPRO         READE     TILLL02                                33
     C                   DOW       *IN(33) = *OFF
     C  N50              WRITE     TILLRO
     C                   DELETE    TILLR
     C     TUPRO         READE     TILLL02                                33
     C                   END
     C                   END
     C                   END
      *
     C                   IF        XTRAVH4 = 'Y'
     C     TUPRO         SETLL     TRAVH4                                 32
     C                   IF        *IN(32)
     C     TUPRO         READE     TRAVH4                                 33
     C                   DOW       *IN(33) = *OFF
     C  N50              WRITE     TRAVHRO
     C                   DELETE    TRAVHR
     C     TUPRO         READE     TRAVH4                                 33
     C                   END
     C                   END
     C                   END
      *
     C                   IF        XTRAVV4 = 'Y'
     C     TUPRO         SETLL     TRAVV4                                 32
     C                   IF        *IN(32)
     C     TUPRO         READE     TRAVV4                                 33
     C                   DOW       *IN(33) = *OFF
     C  N50              WRITE     TRAVVRO
     C                   DELETE    TRAVVR
     C     TUPRO         READE     TRAVV4                                 33
     C                   END
     C                   END
     C                   END
      *
     C                   IF        XTUFE5 = 'Y'
     C     TUPRO         SETLL     TUFE5                                  32
     C                   IF        *IN(32)
     C     TUPRO         READE     TUFE5                                  33
     C                   DOW       *IN(33) = *OFF
     C  N50              WRITE     TUFERO
     C                   DELETE    TUFER
     C     FETREF        SETLL     TUFET
     C     FETREF        READE     TUFET                                  33
     C                   DOW       *IN(33) = *OFF
     C  N50              WRITE     TUFETRO
     C                   DELETE    TUFETR
     C     FETREF        READE     TUFET                                  33
     C                   ENDDO
     C     TUPRO         READE     TUFE5                                  33
     C                   END
     C                   END
     C                   END
      *
     C  N50              WRITE     TURERRO
     C                   DELETE    TURERR
      *
     C                   ADD       1             WSANT
     C                   ADD       1             XANT              3 0
     C                   IF        WSANT <= 1000
     C                   MOVEA     '01'          *IN(92)
     C     XANT          COMP      100                                    96
     C                   ELSE
     C                   MOVEA     '10'          *IN(92)
     C     XANT          COMP      1000                                   96
     C                   END
     C                   IF        *IN96
     C                   MOVE      *ZEROS        XANT
     C                   TIME                    WSTDNÅ
     C                   WRITE     BILDE
     C                   READ      REOPDAFM                             3333
     C                   END
      *
     C                   END
     C                   END
      *
     CLR                 EXSR      CLOSEFILES
      *
     C***********************************************
     C     OPENFILES     BEGSR
     C***********************************************
     C                   MOVE      'J'           XOPEN             1
      *
     C                   IF        UDELETE = 'N'
     C                   OPEN      TUREROLD
     C                   ELSE
     C                   MOVE      '1'           *IN50
     C                   END
      *
     C                   OPEN      FREETUR
     C                   READ(N)   FREETUR                                33
     C                   IF        *IN(33) = *OFF
     C                   MOVE      'Y'           XFREETUR          1
     C  N50              OPEN      FREETUROLD
     C                   ELSE
     C                   CLOSE     FREETUR
     C                   END
      *
     C                   OPEN      ISOFL7
     C                   READ(N)   ISOFL7                                 33
     C                   IF        *IN(33) = *OFF
     C                   MOVE      'Y'           XISOF             1
     C  N50              OPEN      ISOFOLD
     C                   OPEN      ISOLFL1
     C  N50              OPEN      ISOLFOLD
     C                   ELSE
     C                   CLOSE     ISOFL7
     C                   END
      *
     C                   OPEN      KOSBU13
     C                   READ(N)   KOSBU13                                33
     C                   IF        *IN(33) = *OFF
     C                   MOVE      'Y'           XKOSBU13          1
     C  N50              OPEN      KOSBUOLD
     C                   ELSE
     C                   CLOSE     KOSBU13
     C                   END
      *
     C                   OPEN      REISL01
     C                   READ(N)   REISL01                                33
     C                   IF        *IN(33) = *OFF
     C                   MOVE      'Y'           XREISL01          1
     C  N50              OPEN      REISOLD
     C                   ELSE
     C                   CLOSE     REISL01
     C                   END
      *
     C                   OPEN      STSTUR1
     C                   READ(N)   STSTUR1                                33
     C                   IF        *IN(33) = *OFF
     C                   MOVE      'Y'           XSTSTUR1          1
     C  N50              OPEN      STSTUROLD
     C                   ELSE
     C                   CLOSE     STSTUR1
     C                   END
      *
     C                   OPEN      TILLL02
     C                   READ(N)   TILLL02                                33
     C                   IF        *IN(33) = *OFF
     C                   MOVE      'Y'           XTILLL02          1
     C  N50              OPEN      TILLOLD
     C                   ELSE
     C                   CLOSE     TILLL02
     C                   END
      *
     C                   OPEN      TRAVH4
     C                   READ(N)   TRAVH4                                 33
     C                   IF        *IN(33) = *OFF
     C                   MOVE      'Y'           XTRAVH4           1
     C  N50              OPEN      TRAVHOLD
     C                   ELSE
     C                   CLOSE     TRAVH4
     C                   END
      *
     C                   OPEN      TRAVV4
     C                   READ(N)   TRAVV4                                 33
     C                   IF        *IN(33) = *OFF
     C                   MOVE      'Y'           XTRAVV4           1
     C  N50              OPEN      TRAVVOLD
     C                   ELSE
     C                   CLOSE     TRAVV4
     C                   END
      *
     C                   OPEN      TUFE5
     C                   READ(N)   TUFE5                                  33
     C                   IF        *IN(33) = *OFF
     C                   MOVE      'Y'           XTUFE5            1
     C  N50              OPEN      TUFEOLD
     C                   OPEN      TUFET
     C  N50              OPEN      TUFETOLD
     C                   ELSE
     C                   CLOSE     TUFE5
     C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     CLOSEFILES    BEGSR
     C***********************************************
     C                   IF        XFREETUR = 'Y'
     C  N50              CLOSE     FREETUROLD
     C                   CLOSE     FREETUR
     C                   END
      *
     C                   IF        XISOF = 'Y'
     C  N50              CLOSE     ISOLFOLD
     C                   CLOSE     ISOLFL1
     C  N50              CLOSE     ISOFOLD
     C                   CLOSE     ISOFL7
     C                   END
      *
     C                   IF        XKOSBU13 = 'Y'
     C  N50              CLOSE     KOSBUOLD
     C                   CLOSE     KOSBU13
     C                   END
      *
     C                   IF        XREISL01 = 'Y'
     C  N50              CLOSE     REISOLD
     C                   CLOSE     REISL01
     C                   END
      *
     C                   IF        XSTSTUR1 = 'Y'
     C  N50              CLOSE     STSTUROLD
     C                   CLOSE     STSTUR1
     C                   END
      *
     C                   IF        XTILLL02 = 'Y'
     C  N50              CLOSE     TILLOLD
     C                   CLOSE     TILLL02
     C                   END
      *
     C                   IF        XTRAVH4 = 'Y'
     C  N50              CLOSE     TRAVHOLD
     C                   CLOSE     TRAVH4
     C                   END
      *
     C                   IF        XTRAVV4 = 'Y'
     C  N50              CLOSE     TRAVVOLD
     C                   CLOSE     TRAVV4
     C                   END
      *
     C                   IF        XTUFE5 = 'Y'
     C  N50              CLOSE     TUFEOLD
     C  N50              CLOSE     TUFETOLD
     C                   CLOSE     TUFE5
     C                   CLOSE     TUFET
     C                   END
      *
     C                   IF        UDELETE = 'N'
     C                   CLOSE     TUREROLD
     C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     KDTFMT        BEGSR
     C***********************************************
     C                   MOVE      WSDTD         UDT1              2 0
     C                   MOVE      WSDTM         UDT2              2 0
     C                   MOVE      WSDTÅ         UDT3              2 0
     C                   CALL      'DATOFMT'
     C                   PARM                    UDT1
     C                   PARM                    UDT2
     C                   PARM                    UDT3
     C                   MOVEL     UDT1          WSDTD
     C                   MOVEL     UDT2          WSDTM
     C                   MOVE      UDT3          WSDTÅ
      *
     C                   ENDSR
      *
