     A*%%TS  SD  20180615  054248  SVEIN       REL-V7R1M0  5770-WDS
*****A**************************************************************
     A* RETTINGER I DETTE PROGRAM:
PTFnrA*Sek Sig Vers Beskrivelse...........................
"""""A*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
P8068A* 01 TH PTF8 NY HELP - FUNKSJON
*****A**************************************************************
     A*%%EC
     A                                      DSPSIZ(27 132 *DS4)
     A                                      PRINT
     A                                      HLPPNLGRP(HLPSCR SYFA14PNL)
     A                                      HLPTITLE('OPPRETTE NYE AVD.')
     A                                      HELP
     A                                      ALTHELP
     A          R SUBFIL                    SFL
     A*%%TS  SD  20180614  133833  SVEIN       REL-V7R1M0  5770-WDS
     A            WSNR           1A  I  6  2
     A  98                                  DSPATR(RI)
     A  98                                  DSPATR(PC)
     A                                      VALUES(' ' '2' '4' '6' '9')
     A            KOAAVD         4Y 0O  6  4EDTCDE(4)
     A                                      COLOR(WHT)
     A            KOANVN        33A  O  6  9
     A            KOAFIR         2A  O  6 43
     A            KOAKNR         8Y 0O  6 46EDTCDE(4)
     A            KOABÆR         4S 0O  6 55
     A            KOAKON         4S 0O  6 60
     A            KOAIAT         7Y 0O  6 66EDTWRD('  & &    ')
     A            KOAIA2         4S 0O  6 76
     A            KOAPOS         1A  O  7 16
     A            KSIDNR        20A  O  7 18
     A            KOAIE          1A  O  7 43
     A            KOALK          2A  O  7 58
     A            NAVSG          3A  O  7 68
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A*%%TS  SD  20180615  054248  SVEIN       REL-V7R1M0  5770-WDS
     A                                      SFLSIZ(0008)
     A                                      SFLPAG(0007)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                  1  2'SYSPED/SYFA14'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(040 DIV0583 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                  1 70TIME
     A N50                              2  2MSGCON(010 SHU1843 *LIBL/SYMSGF)
     A                                      COLOR(RED)
     A  50                              2  2MSGCON(010 SHU1844 *LIBL/SYMSGF)
     A                                      COLOR(RED)
     A N50                              2 34MSGCON(018 SHU1840 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  50                              2 34MSGCON(018 SHU1846 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A            ZRECNO         4S 0O  2 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  3  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 34MSGCON(018 DIV0017 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 53MSGCON(027 JOV0212 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(002 DIV0025 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                  4  5MSGCON(075 DIV0584 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  5  5MSGCON(075 JOV0075 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A          R BUNN
     A*%%TS  SD  20180615  053212  SVEIN       REL-V7R1M0  5770-WDS
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF03(03 'AVSLUTT')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                      CF20(20 'Inaktive')
     A                                      SLNO(23)
     A                                      CLRL(02)
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  1 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  1 42MSGCON(018 DIV0022 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  1 62MSGCON(018 DIV0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  2  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                              2 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A N50                              1 81MSGCON(018 SHU1841 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  50                              1 81MSGCON(018 SHU1842 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A          R VARELI
     A*%%TS  SD  20160303  145013  SVEIN       REL-V7R1M0  5770-WDS
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF12(12 'Forrige')
     A                                      SLNO(19)
     A                                      CLRL(06)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1  5MSGCON(075 DIV0584 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  2  5MSGCON(075 JOV0075 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            WSAAVD         4Y 0B  3  4DSPATR(UL)
     A  40                                  DSPATR(HI)
     A  40                                  DSPATR(PR)
     A  89                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A            WSANVN        33A  B  3  9DSPATR(UL)
     A  88                                  DSPATR(RI)
     A            FIFIRM         2A  O  3 43DSPATR(UL)
     A            WSAKNR         8Y 0B  3 46DSPATR(UL)
     A  87                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A            WSABÆR         4Y 0B  3 55DSPATR(UL)
     A                                      EDTCDE(4)
     A            WSAKON         4Y 0B  3 60DSPATR(UL)
     A                                      EDTCDE(4)
     A            WSAIAT         7Y 0B  3 66DSPATR(UL)
     A                                      EDTWRD('  & &    ')
     A            WSAIA2         4Y 0B  3 76DSPATR(UL)
     A                                      EDTCDE(4)
     A            WSAPOS         1A  B  4 16
     A                                  4 18'SID'
     A                                      DSPATR(HI)
     A            WSIDNR        20A  B  4 22
     A            WSAIE          1A  B  4 43
     A            WSALK          2A  B  4 58
     A            WSSG           3   B  4 68
     A  86                                  DSPATR(RI)
     A                                  5  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  6  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                              6 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A          R VAREL2
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A                                  2  2MSGCON(012 DIV0026 *LIBL/SYMSGF)
     A            W2AAVD         4Y 0I  2 16DSPATR(UL)
     A                                      DSPATR(PC)
     A                                  4  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A          R VAREL3
     A                                      CF12(12 'Forrige')
     A                                      SLNO(19)
     A                                      CLRL(06)
     A                                      OVERLAY
     A                                  1  5MSGCON(075 DIV0584 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  2  5MSGCON(075 JOV0075 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            KOAAVD         4Y 0O  3  4DSPATR(UL)
     A                                      DSPATR(PC)
     A                                      DSPATR(HI)
     A                                      EDTCDE(4)
     A            KOANVN        33A  O  3  9DSPATR(UL)
     A                                      DSPATR(HI)
     A            KOAFIR         2A  O  3 43DSPATR(UL)
     A                                      DSPATR(HI)
     A            KOAKNR         8Y 0O  3 46DSPATR(UL)
     A                                      DSPATR(HI)
     A                                      EDTCDE(4)
     A            KOABÆR         4Y 0O  3 55DSPATR(UL)
     A                                      DSPATR(HI)
     A                                      EDTCDE(4)
     A            KOAKON         4Y 0O  3 60DSPATR(UL)
     A                                      DSPATR(HI)
     A                                      EDTCDE(4)
     A            KOAIAT         7Y 0O  3 66DSPATR(UL)
     A                                      DSPATR(HI)
     A                                      EDTWRD('  & &    ')
     A            KOAIA2         4Y 0O  3 76DSPATR(UL)
     A                                      DSPATR(HI)
     A                                      EDTCDE(4)
     A            KOAPOS         1A  O  4 16COLOR(WHT)
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            KOAIE          1A  O  4 43COLOR(WHT)
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            KOALK          2A  O  4 62COLOR(WHT)
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  5  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  6 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A                                  4 18'SID'
     A                                      DSPATR(HI)
     A            KSIDNR        20A  O  4 22COLOR(WHT)
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A          R VAREL4
     A                                      CF12(12 'Forrige')
     A                                      SLNO(19)
     A                                      CLRL(06)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  2  2MSGCON(078 JOV0211 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            WSAAVD         4Y 0O  3  4COLOR(WHT)
     A                                      EDTCDE(4)
     A            WSDUS1        10A  B  3  9DSPATR(UL)
     A            WSDUS2        10A  B  3 20DSPATR(UL)
     A            WSDUS3        10A  B  3 31DSPATR(UL)
     A            WSDUS4        10A  B  3 42DSPATR(UL)
     A            WSDUS5        10A  B  3 53DSPATR(UL)
     A            WSDUS6        10A  B  3 64DSPATR(UL)
     A                                  5  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  6 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A          R NYVARE
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF03(03 'AVSLUTT')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                      CF20(20 'Inaktive')
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/SYFA14'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(040 DIV0583 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                  1 69TIME
     A                                  3  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 34MSGCON(030 DIV0017 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(002 DIV0025 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                  4  5MSGCON(075 DIV0584 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 42MSGCON(018 DIV0022 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 62MSGCON(018 DIV0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N50                             23 81MSGCON(018 SHU1841 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  50                             23 81MSGCON(018 SHU1842 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                             24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A          R NYWRK
