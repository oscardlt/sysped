      *
      ***********************************************************
      *** PROGRAM SKAL KONVERTER XML-FIL TIL LOGISTIKKSYSTEM  ***
      *** KOMPLETT.NO Tollager,                               ***
      ***    D=VareUTGANG EKSPORT   (plukkliste samlet  )     ***
      ***********************************************************
      *
     FXMLRCVF   IF   E           K DISK
      *
     FFIRM      IF   E           K DISK
     FLLHEAD    UF A E           K DISK
     FLLLINE    IF A E             DISK
NXX  FLLVARE01  IF   E           K DISK
     FLLTELL    UF   E             DISK
      * Div arrays for flere invoice nummer+pakksnr(30+17)/ llhead/linjenr
     D INPA            S             47    DIM(2000)
     D LMN             S              7  0 DIM(2000)
     D LVN             S              3  0 DIM(2000)
N02  D                 DS
N02  D  INVPAK                 1     47
N02  D   INVNO                 1     30
N02  D   XxORDR               31     47
      *
     C                   EXSR      INIT
      *
     C                   EXSR      SRXML1
      *
     C                   EVAL      *INLR = *ON
     C                   RETURN
      *
      ***********************************************
     C     SRXML1        BEGSR
      ***********************************************
      * Les EN OG EN TAG ("Nøstede tager" kommer nå i "rett rekkefølge" )
     C     XRSN          setll     XMLRCVF
     C     XRSN          reade     XMLRCVF                                94
     C     *in94         doweq     '0'
     C                   EXSR      SRXML2
     C     XRSN          reade     XMLRCVF                                94
     C                   enddo
      *
     C                   ENDSR
      *
      ***********************************************
     C     SRXML2        BEGSR
      ***********************************************
      * Handling utfra tag-navn
      *    ( ekstra tester dersom samme tagnavn fins på flere nivå                     )
      *    ( les i filene XMLRCAF (attributt-verdier)+ XMLRCLF ("Extras") om nødvendig )
     C                   SELECT
      * Start Pakseddel-header
      * DIV REFFELT:
      *  LHFLN           LISTE NR(SAMLE-REF)    - EDI-NR INN (KEY FOR XMLRCVF )
      *  LHORDR          KUNDES ORDRENR         - ORDRENR DEN ENKELT DEL-ORDRE
      *  LHPAK           PAKKSEDDELNR           - FAKTURANUMMER
      *
      * pakkseddelnummer
     C                   WHEN      XRNV = 'VBELN' and XRLEV6 = 0
     c                   eval      xxORDR = XRVERD
      * Dato er siste element i header
     C                   WHEN      XRNV = 'ISDD'
     c                   eval      xxTDT  = %int(XRVERD)
      ***
      * Start Pakseddel-linjer (Kun tollager-linjer skrives til LLINE )
     C                   WHEN      XRNV = 'E1EDL24' and XRLEV6 = 0
     c                   move      'J'           OKlinje           1
     c                   move      *blanks       xLGORT            3
     c                   clear                   LLLINER
     C
      * artnr
     C                   WHEN      XRNV = 'MATNR' and XRLEV6 = 0
     c                   eval      LLANR  = XRVERD
NXX  C     LLVARE01K     CHAIN     LLVARE01                           33
NXX  C  N33              EVAL      LLVB   = LVVB
NXX  C   33              EVAL      LLVB   = *BLANKS
      * lagersted  (JA = tollager=OKlinje )
      * det viser seg at mange varelinjer mangler <LGORT>
      * selve testen er derfor flyttet lenger ned (til tag LGMNG som alltid kommer)
     C                   WHEN      XRNV = 'LGORT' and XRLEV6 = 0
     c                   eval      xLGORT = XRVERD
      * batch
     C                   WHEN      XRNV = 'CHARG'  and XRLEV6 = 0
     c                   eval      LLSER  = XRVERD                              Batchnr
      * mengde (dersom 0 - skipp )
     C                   WHEN      XRNV = 'LGMNG' and XRLEV6 = 0
     c                   eval(H)   LLANT0 = %float(XRVERD)
     c     LLANT0        ifeq      0
     c                   move      'N'           OKlinje
     c                   end
     c     xLGORT        IFne      'JA'
     c                   move      'N'           OKlinje
     c                   end
      * MOTTAKER ......
     C                   WHEN      XRNV = 'VKORG'
     c                   eval      xxMOT  = XRVERD
      * AVSENDER  .....
     C                   WHEN      XRNV = 'WERKS'
     c                   eval      xxAVS  = XRVERD
      * FAKTURANr......
     C                   WHEN      XRNV = 'INVNO'
     c                   eval      INVNO  = XRVERD
      *  <STAWN> =tariffnr. Svensk eller NORSK,      Norsk = siste tag - skriv linje !!!!
     C                   WHEN      XRNV = 'STAWN'
     c                   eval      TstPath='/ZSHPMNT02/IDOC/E1EDT20/E1EDL20/'
     c                                    +'E1EDL24/ZE1EDL24'                   UTENlandsk
     c     XRPATH        ifne      TstPath                                      Norsk=siste
      * i Front av hver Linje - enten lag ny header/hent eksisterende
     c                   exsr      SRhead
      * kun tollager-linjer forsøkes allokert fra lager
     c     OKlinje       ifeq      'J'
     C                   exsr      SRLine
     C                   end
     C                   end
      *
     C                   ENDSL
      *
     C                   ENDSR
      ***********************************************
     C     SRhead        BEGSR
      ***********************************************
      * 3100=Komplett Services 3300=Komplett Distribution
     c     xxAVS         ifeq      '3100'
     C                   Z-ADD     10            XPNR              8 0
     c                   else
     C                   Z-ADD     20            XPNR              8 0
     c                   endif
      * legg til invoice i array om det ikek finnes/finn arraynr
     c     1             do        2000          Ni                5 0
     c     InPa(Ni)      ifeq      InvPak
     c     InPa(Ni)      oreq      *blanks
     c                   leave
     c                   endif
     c                   enddo
      * Enten funnnet en eksisterende elle må skape/legge til en ny
     c     InPa(Ni)      ifeq      *blanks
     c                   clear                   LLHEADR
     C                   MOVE      XKJED         LHGRP
     C                   MOVE      XPNR          LHPNR
      * LØPENR HEADER
     C     1             CHAIN     LLTELL                             33
     C                   ADD       1             LLREC1
     C                   UPDATE    LLTELLR
     C                   MOVE      LLREC1        LHMN
      * PLUKKLISTENR TILDELES UMIDDELBART
     C     2             CHAIN     LLTELL                             33
     C                   ADD       1             LLREC1
     C                   UPDATE    LLTELLR
     C                   MOVE      LLREC1        LHALN
      * sett plukkstatus overført umiddelbart (Syxm17Cx kjøres rett etterpå og tar ut..)
     C                   MOVE      'O'           LHSTA
     C                   MOVE      'E'           LHOPR
     C                   MOVE      xxTDT         LHTDT
      * navn / adresser  hardkodet.
      * 2000=Komplett Services Sweeden AB 2300=Komplett Distribution SE AB
     c     xxMOT         ifeq      '2000'
     c                   eval      LHBNA  = 'Komplett Services Sweden AB'
     c                   else
     c                   eval      LHBNA  = 'Komplett Distribution SE AB'
     c                   endif
     c                   eval      LHBA1  = 'Kruthusgatan 19'
     c                   eval      LHBPU  = '411 04'
     c                   eval      LHBA3  = 'Göteborg'
     c                   eval      LHBLK  = 'SE'
     c     xxMOT         ifeq      '6100'
     c                   eval      LHBNA  = 'Komplett Finland'
     c                   eval      LHBA1  = 'xx           '
     c                   eval      LHBPU  = 'xxxxxx'
     c                   eval      LHBA3  = 'Helsinki ?'
     c                   eval      LHBLK  = 'FI'
     c                   endif
      *Lagrer EDIsendnr i LHFLN  på paskseddelhaeder
     c                   eval      LHFLN  = USN
      *Lagrer FAKTURANR i LHPAK  på paskseddelhaeder
     c                   eval      LHPAK  = INVNO
      *Lagrer pakkseddelnr i LHORDR på paskseddelhaeder
     C                   MOVE      xxORDR        LHORDR
      *
     C                   MOVEL     'SAPXML'      LHUSR
     C                   MOVE      WDT           LHRDT
     C                   MOVE      WTM           LHRTD
     C                   WRITE     LLHEADR
      *
     c                   movel     InvNo         InPa(Ni)
     c                   move      LHORDR        InPa(Ni)
     C                   MOVE      LHMN          LMN(Ni)
     c                   move      *zeros        LVN(Ni)
     c                   endif
      * FELT SOM TRENGS I SRLINE
     C                   MOVE      LMN(Ni)       LHMN
     c                   move      LVN(Ni)       Linnr
     C                   MOVE      XKJED         LHGRP
     C                   MOVE      XPNR          LHPNR
     C                   ENDSR
      ***********************************************
     C     SRLine        BEGSR
      ***********************************************
      *
     C                   MOVE      LHMN          LLMN
     c                   add       1             Linnr
     c                   z-add     Linnr         LLVN
      * bevar sist brukte linjenr denne Header..
     c                   move      Linnr         LVN(Ni)
      * Allokere vare...
     C                   Z-ADD     9             UFLAGG            1 0
     C                   MOVE      LLANT0        LLWNT0
     C                   MOVE      LLANT3        LLWNT3
     C                   MOVE      LLANT2        LLWNT2
     C                   MOVE      LLANT1        LLWNT1
     C                   MOVE      LLANTL        LLWNTL
     C                   MOVE      LLVEK         LLWVEK
     C                   MOVE      LLKUB         LLWKUB
     C                   MOVE      *BLANKS       LLLOK
      *
     C                   move      *zeros        UNTLK             3 0
     C     *LIKE         DEFINE    LHGRP         UGRP
     C     *LIKE         DEFINE    LLWNT0        UWNT0
     C     *LIKE         DEFINE    LLWNTL        UWNTL
     C     *LIKE         DEFINE    LLWNT1        UWNT1
     C     *LIKE         DEFINE    LLWNT2        UWNT2
     C     *LIKE         DEFINE    LLWNT3        UWNT3
     C                   CALL      'LL022R'
     C                   PARM      LHGRP         UGRP
     C                   PARM                    LHPNR
     C                   PARM                    LLANR
     C                   PARM                    LLSER
     C                   PARM                    UPRDT             8
     C                   PARM                    LLMN
     C                   PARM                    LLVN
     C                   PARM      LLWNT0        UWNT0
     C                   PARM                    UWNTL
     C                   PARM                    UWNT1
     C                   PARM                    UWNT2
     C                   PARM                    UWNT3
     C                   PARM                    UWVEK             7 2
     C                   PARM                    UWKUB             7 4
     C                   PARM                    LLLOK
     C                   PARM                    UNTLK             3 0
     C                   PARM                    UFLAGG
      *
     C     UFLAGG        IFEQ      0
     C                   MOVE      UWNT0         LLWNT0
     C                   MOVE      UWNTL         LLWNTL
     C                   MOVE      UWNT1         LLWNT1
     C                   MOVE      UWNT2         LLWNT2
     C                   MOVE      UWNT3         LLWNT3
     C                   MOVE      UNTLK         LLNTLK
     C                   MOVE      UWVEK         LLWVEK
     C                   MOVE      UWKUB         LLWKUB
     C                   ELSE
     C                   MOVE      *ZEROS        LLWNT0
     C                   MOVE      *ZEROS        LLWNT3
     C                   MOVE      *ZEROS        LLWNT2
     C                   MOVE      *ZEROS        LLWNT1
     C                   MOVE      *ZEROS        LLWNTL
     C                   MOVE      *ZEROS        LLWVEK
     C                   MOVE      *ZEROS        LLWKUB
     C                   END
      *
     C                   MOVE      'ESK'         LLAEH1
     C                   MOVE      'KRT'         LLAEH2
     C                   MOVE      'PAL'         LLAEH3
     C**                 MOVE      *BLANKS       LLSER
      *
     C                   WRITE     LLLINER
      *
     c     LHMN          chain     LLHEAD                             33
     c     *in33         ifeq      '0'
     c                   add       LLWNT0        LHKLL
     c                   add       LLWVEK        LHWVEK
     C                   UPDATE    LLHEADR
     c                   endif
     C                   ENDSR
      *
      ***********************************************
     C     INIT          BEGSR
      ***********************************************
     C     *ENTRY        PLIST
     C                   PARM                    USN               9
NXX  C     LLVARE01K     KLIST
NXX  C                   KFLD                    LHGRP
NXX  C                   KFLD                    LHPNR
NXX  C                   KFLD                    LLANR
      *
     C                   MOVE      USN           XRSN
      *
     C                   MOVE      *ZEROS        XKJED             8 0
     C                   Z-ADD     10            XPNR              8 0
      *
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVE      WDT           WDT08             8 0
      *
      *
     C     *LOVAL        SETLL     FIRM
     C                   READ      FIRM                                   33
      *
     c     *like         define    XRPATH        TstPath
     c     *like         define    LLVN          Linnr
     c     *like         define    LHORDR        xxORDR
     c     *like         define    LHTDT         xxTDT
     C                   move      *blanks       xxAVS             4
     c                   move      *blanks       xxMOT             4
     c                   move      *blanks       INVNO            30
     C                   ENDSR
      *
