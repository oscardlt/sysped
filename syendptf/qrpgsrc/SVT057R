********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
12XXX * 01 CB  PTF12 möjlighet att ändra slutdatum
********************************************************************
     FSVT057D   CF   E             WORKSTN
     F                                     SFILE(SVT057DA:ZRECNO)
     FSVTVK     UF A E           K DISK
     FSVTVK1    IF   E           K DISK
     F                                     RENAME(SVTVKR:SVTVKR1)
      *//////////////
      *  Hodelogikk /
      *//////////////
     C                   EXSR      SRA
     C                   EXSR      SRB
     C                   MOVE      '1'           *INLR
      *////////////////////////////////////////////////////////////
      *  Initiering                                           SRA /
      *////////////////////////////////////////////////////////////
     C     SRA           BEGSR
      *________________
      * Arbetsvariablar
      *""""""""""""""""
     C     *LIKE         DEFINE    ZRECNO        WRECNO
     C     *LIKE         DEFINE    *IN30         WIN30
      *__________________
      * Definiere Nøkkler
      *""""""""""""""""""
     C     VKKEY1        KLIST
     C                   KFLD                    SVVK_KD
     C                   KFLD                    SVVK_DTS
     C     VKKEY2        KLIST
     C                   KFLD                    ZVVK_KD
     C                   KFLD                    ZVVK_DTS
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Bearbeide Formater                                   SRB /
      *////////////////////////////////////////////////////////////
     C     SRB           BEGSR

     C     SRB1          TAG
      *______________
      * Tømme Subfile
      *""""""""""""""
     C                   EVAL      *IN93=*ON
     C                   WRITE     SVT057DB
     C                   MOVEA     '000'         *IN(93)
     C                   Z-ADD     *ZERO         ZRECNO
     C                   Z-ADD     *ZERO         WRECNO
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     *LOVAL        SETLL     SVTVK1
     C                   READ      SVTVK1                                 94

     C     *IN94         DOWEQ     '0'
     C                   ADD       1             ZRECNO
     C                   WRITE     SVT057DA
     C                   READ      SVTVK1                                 94
     C  N94              ENDDO

     C     ZRECNO        IFEQ      0
     C                   ADD       1             ZRECNO
     C                   WRITE     SVT057DA
     C                   ENDIF
     C                   EVAL      WRECNO=ZRECNO
     C                   EVAL      ZRECNO=1
      *_______________
      * Behandle bilde
      *"""""""""""""""
     C     SRB3          TAG

     C                   MOVEA     '11'          *IN(91)
     C  N30              EVAL      ZMSGNO='SY00000'
     C                   WRITE     SVT057DE
     C                   WRITE     SVT057DØ
     C                   WRITE     SVT057DC
     C                   EXFMT     SVT057DB

     C     *IN03         IFEQ      '0'
     C     *IN12         ANDEQ     '0'

     C                   MOVEA     '00'          *IN(91)
      *_____________
      * F5=Frisk opp
      *"""""""""""""
     C     *IN05         CABEQ     '1'           SRB1
      *___________
      * F6=Lage ny
      *"""""""""""
     C     *IN06         IFEQ      '1'
     C                   EXSR      SRBA
     C     *IN03         CABEQ     '1'           SRB9
     C                   GOTO      SRB3
     C                   ENDIF
      *_______
      * Valg ?
      *"""""""
     C                   EVAL      *IN52=*OFF
     C                   EVAL      *IN95=*OFF
     C     *IN52         DOWEQ     '0'

     C                   READC     SVT057DA                               52
     C     *IN52         IFEQ      '0'
     C     ZOP           CASEQ     '2'           SRBB                           Endre
     C     ZOP           CASEQ     '4'           SRBD                           Slette
     C                   ENDCS
     C                   ENDIF
      *
     C  N52              ENDDO

     C     *IN03         CABEQ     '1'           SRB9
     C     *IN12         CABEQ     '1'           SRB3
     C                   GOTO      SRB1

     C                   ENDIF

     C     SRB9          ENDSR
      *////////////////////////////////////////////////////////////
      *  Lage ny                                             SRBA /
      *////////////////////////////////////////////////////////////
     C     SRBA          BEGSR

     C                   CLEAR                   SVT057DD
     C                   MOVEA     '0000'        *IN(30)

     C     SRBA1         TAG

      *____________
      * Blanke felt
      *""""""""""""
     C  N30              EVAL      ZMSGNO='SY00000'
     C                   WRITE     SVT057DØ
     C                   EXFMT     SVT057DD
     C                   MOVEA     '000000'      *IN(30)
     C     *IN03         IFEQ      '0'
     C     *IN05         ANDEQ     '0'
     C     *IN12         ANDEQ     '0'
      *_______
      * F4=Søk
      *"""""""
     C     *IN04         IFEQ      '1'
     C                   ENDIF
      *____________
      * Teste input-1
      *""""""""""""
      * Kod må vare utfyllt
     C     ZVVK_KD       IFEQ      *BLANKS
     C                   EVAL      *IN30=*ON
     C                   EVAL      *IN31=*ON
     C                   MOVE      'SVT0025'     ZMSGNO
     C                   GOTO      SRBA1
     C                   ENDIF
      * Kurs
     C                   IF        ZVVK_KRS = 0
     C                   EVAL      *IN30=*ON
     C                   EVAL      *IN32=*ON
     C                   MOVE      'SVT0024'     ZMSGNO
     C                   GOTO      SRBA1
     C                   ENDIF
      * Omräkningsfaktor
     C     ZVVK_OMR      COMP      1                                  3030
     C   30ZVVK_OMR      COMP      100                                3030
     C                   IF        *IN30
     C                   EVAL      *IN33=*ON
     C                   MOVE      'SVT0026'     ZMSGNO
     C                   GOTO      SRBA1
     C                   ENDIF
      * Datum, från
     C                   IF        ZVVK_DTS = 0
     C                   EVAL      *IN30=*ON
     C                   EVAL      *IN34=*ON
     C                   MOVE      'SVT0028'     ZMSGNO
     C                   GOTO      SRBA1
     C                   ENDIF
      * Datum, till
     C                   IF        ZVVK_DTE = 0
     C                   EVAL      *IN30=*ON
     C                   EVAL      *IN35=*ON
     C                   MOVE      'SVT0029'     ZMSGNO
     C                   GOTO      SRBA1
     C                   ENDIF
      *___________________
      * Adda post til  FIL
      *"""""""""""""""""""
     C                   EVAL      SVVK_KD=ZVVK_KD
     C                   EVAL      SVVK_DTS=ZVVK_DTS
     C                   EVAL      SVVK_DTE=ZVVK_DTE
     C                   EVAL      SVVK_OMR=ZVVK_OMR
     C                   EVAL      SVVK_KRS=ZVVK_KRS
     C                   WRITE     SVTVKR
      *______________________
      * Adda post til  SUBFIL
      *""""""""""""""""""""""
     C                   ADD       1             WRECNO
     C                   EVAL      ZRECNO=WRECNO
     C                   WRITE     SVT057DA

     C                   GOTO      SRBA1

     C                   ENDIF

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Endre                                               SRBB /
      *////////////////////////////////////////////////////////////
     C     SRBB          BEGSR
      *_________________
      * Oppdatere SUBFIL
      *"""""""""""""""""
     C                   MOVE      *BLANKS       ZOP
     C                   UPDATE    SVT057DA
      *____________
      * Flytte felt
      *""""""""""""
     C     VKKEY1        CHAIN(N)  SVTVK                              53
     C                   MOVEA     '0000'        *IN(30)
     C                   EVAL      ZVVK_KD=SVVK_KD
     C                   EVAL      ZVVK_DTS=SVVK_DTS
     C                   EVAL      ZVVK_DTE=SVVK_DTE
     C                   EVAL      ZVVK_OMR=SVVK_OMR
     C                   EVAL      ZVVK_KRS=SVVK_KRS

     C     SRBB1         TAG

     C  N30              EVAL      ZMSGNO='SY00000'
     C                   WRITE     SVT057DØ
     C                   EXFMT     SVT057DD2
     C                   MOVEA     '000000'      *IN(30)
     C     *IN12         IFEQ      '0'
      *_______
      * F4=Søk
      *"""""""
     C     *IN04         IFEQ      '1'
     C                   ENDIF
      *____________
      * Teste input-1
      *""""""""""""
      * Kurs
     C                   IF        ZVVK_KRS = 0
     C                   EVAL      *IN30=*ON
     C                   EVAL      *IN32=*ON
     C                   MOVE      'SVT0024'     ZMSGNO
     C                   GOTO      SRBB1
     C                   ENDIF
      * Omräkningsfaktor
     C     ZVVK_OMR      COMP      1                                  3030
     C   30ZVVK_OMR      COMP      100                                3030
     C                   IF        *IN30
     C                   EVAL      *IN33=*ON
     C                   MOVE      'SVT0026'     ZMSGNO
     C                   GOTO      SRBB1
     C                   ENDIF
N01   * Datum, till
N01  C                   IF        ZVVK_DTE = 0
N01  C                   EVAL      *IN30=*ON
N01  C                   EVAL      *IN35=*ON
N01  C                   MOVE      'SVT0029'     ZMSGNO
N01  C                   GOTO      SRBB1
N01  C                   ENDIF
      *______________
      * Oppdatere FIL
      *""""""""""""""
     C     VKKEY2        CHAIN     SVTVK                              53
     C                   EVAL      SVVK_OMR=ZVVK_OMR
     C                   EVAL      SVVK_KRS=ZVVK_KRS
N01  C                   EVAL      SVVK_DTE=ZVVK_DTE
     C                   UPDATE    SVTVKR

     C                   ENDIF

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Slette                                              SRBD /
      *////////////////////////////////////////////////////////////
     C     SRBD          BEGSR
      *______________
      * Oppdatere FIL
      *""""""""""""""
     C     VKKEY1        CHAIN     SVTVK                              53
     C                   DELETE    SVTVKR
      *_________________
      * Oppdatere SUBFIL
      *"""""""""""""""""
     C                   CLEAR                   SVT057DA
     C                   UPDATE    SVT057DA
     C                   EVAL      *IN95=*ON

     C                   ENDSR
