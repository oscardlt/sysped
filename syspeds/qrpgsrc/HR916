      *   Tester ut kontrollsiffer for KID (MODULUS 10)
      *   KID i felt på 25 alfa kommer inn via LDA pos 701-725
      *   og returkode pos 726 settes lik 0 hvis OK og lik 1
      *   hvis ikke OK
      *   - Benytt FREE, av dette pgm. i det kallende program
      *     ved LR, da dette pgm. bare benytter return (*INRT)
      **********************************************************
      * Hjelpevariabler
     D WORK20          S              2  0 INZ(0)
     D WRK10V          S              1  0 INZ(0)
     D WRK10H          S              1  0 INZ(0)
     D IND             S              2  0 INZ(0)
     D X               S              2  0 INZ
     D Y               S              2  0 INZ
     D WKONTR          S                   LIKE(KONTRL)
      *
     D NN              S              1  0 INZ(0)
     D                                     DIM(25)
      *
     D                 DS
     D KONTRL                 25     25
     D WKID                    1     25
     D BN                      1     25  0 INZ(0)
     D                                     DIM(25)
      *
      * Parameterliste
      *
     D P_ENTRY         DS
     D  PLKID                        25
     D  PRC                           1
      *
     C     *ENTRY        PLIST
     C                   PARM                    P_ENTRY
      *
     C                   MOVE      *ZEROS        NN
      *
      * Kid høyrestilles med foranstilte nuller
      *
     C                   EVAL      WKID        = *ZEROS
     C                   EVAL      PLKID       = %TRIML(PLKID)
     C                   EVAL      X           = %SCAN(' ' : PLKID)
     C                   EVAL      Y           = 26 - X
     C                   IF        Y < 26
     C                   EVAL      WKID        = %SUBST(WKID:1:Y) +
     C                                           %SUBST(PLKID:1:X-1)
     C                   ELSE
     C                   EVAL      WKID        = PLKID
     C                   ENDIF
      *
      * Oppgitt kontrollsiffer spares, mens siste siffer i kid nullstilles
      *
     C                   EVAL      WKONTR      = KONTRL
     C                   EVAL      KONTRL      = *ZEROS
      *
      * Beregner kontrollsiffer på nytt
      *
     C                   EVAL      NN          = BN
     C     2             DO        24            IND
     C     BN(IND)       MULT      2             WORK20
     C                   MOVEL     WORK20        WRK10V
     C                   MOVE      WORK20        WRK10H
     C     WRK10V        ADD       WRK10H        NN(IND)
     C                   ENDDO     2
     C                   XFOOT     NN            WRK10V
     C     10            SUB       WRK10V        WRK10H
     C                   MOVE      WRK10H        KONTRL
      *
      * Test om beregnet kontrollsiffer er lik oppgitt kontrollsiffer
      *
     C                   IF        KONTRL <> WKONTR
     C                   EVAL      PRC         = '1'
     C                   ELSE
     C                   EVAL      PRC         = '0'
     C                   ENDIF
      *
     C                   EVAL      *INRT       = *ON
