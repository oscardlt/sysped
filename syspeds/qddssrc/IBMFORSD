     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
      *
     A          R BILDE01
     A                                      CF03(03 'avslutt program')
     A                                  1  3'IBMFORS'
     A                                      COLOR(BLU)
     A                                  1 16'Endre status på forslagsnummer sli-
     A                                      k at'
     A                                      COLOR(WHT)
     A                                  2 16'det kan arbeides med og sendes på -
     A                                      nytt'
     A                                      COLOR(WHT)
     A                                  7 16'Tast firmakode.......:'
     A            FIRMA     R        B  7 39REFFLD(FORSR/FIRMA L1DAT/FORSF)
     A                                  8 16'Tast forslagsnummer..:'
     A            FORSNR    R        B  8 39REFFLD(FORSR/FORSNR L1DAT/FORSF)
     A  30                                  DSPATR(RI)
     A                                 23  3'F3=avslutt program'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R BILDE02
     A                                      CF03(03 'avslutt program')
     A                                      CF05(05 'avslutt program')
     A                                  1  3'IBMFORS'
     A                                      COLOR(BLU)
     A                                  1 16'Endre status på forslagsnummer sli-
     A                                      k at'
     A                                      COLOR(WHT)
     A                                  2 16'det kan arbeides med og sendes på -
     A                                      nytt'
     A                                      COLOR(WHT)
     A                                  6 15'Firmakode.......:'
     A            FIRMA     R        O  6 34REFFLD(FORSR/FIRMA L1DAT/FORSF)
     A                                      COLOR(WHT)
     A                                  7 15'Forslagsnummer..:'
     A            FORSNR    R        O  7 34REFFLD(FORSR/FORSNR L1DAT/FORSF)
     A                                      COLOR(WHT)
     A                                  9 15'Valutakode......:'
     A            VALKOD    R        O  9 34REFFLD(FORSR/VALKOD L1DAT/FORSF)
     A                                      COLOR(WHT)
     A                                 10 15'Betalingsbeløp..:'
     A            FORBEL    R        O 10 34REFFLD(FORSR/FORBEL L1DAT/FORSF)
     A                                      COLOR(WHT)
     A                                 12 15'Forfallsdato....:'
     A            FFDDAG    R        O 12 34REFFLD(FORSR/FFDDAG L1DAT/FORSF)
     A                                      COLOR(WHT)
     A            FFDMND    R        O 12 37REFFLD(FORSR/FFDMND L1DAT/FORSF)
     A                                      COLOR(WHT)
     A            FFDAAR    R        O 12 40REFFLD(FORSR/FFDAAR L1DAT/FORSF)
     A                                      COLOR(WHT)
     A                                 13 15'Utbetalgsdato...:'
     A            UTDDAG    R        O 13 34REFFLD(FORSR/UTDDAG L1DAT/FORSF)
     A                                      COLOR(WHT)
     A            UTDMND    R        O 13 37REFFLD(FORSR/UTDMND L1DAT/FORSF)
     A                                      COLOR(WHT)
     A            UTDAAR    R        O 13 40REFFLD(FORSR/UTDAAR L1DAT/FORSF)
     A                                      COLOR(WHT)
     A                                 14 15'Fra til Levnr...:'
     A            FLEVE     R        O 14 34REFFLD(FORSR/FLEVE L1DAT/FORSF)
     A                                      COLOR(WHT)
     A            TLEVE     R        O 14 43REFFLD(FORSR/TLEVE L1DAT/FORSF)
     A                                      COLOR(WHT)
     A                                 23 25'F5=bekreft frigivelse forslag     '
     A                                      COLOR(BLU)
     A                                 23  3'F3=avslutt program'
     A                                      COLOR(BLU)
