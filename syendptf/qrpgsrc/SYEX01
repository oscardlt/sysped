********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 SH  PTF10 RECORDLÅS          ....
10XXX * 02 SH  PTF12 GI FEILMELDING VED DUPLIKAT
      ***********************************************************
      *** PROGRAM SKAL VEDLIKEHOLDE/SPØRRE SAMLASTAVGANGER    ***
      *****************************************************************
     FKODTW     IF   E           K DISK
     FTELLDE    UF A E           K DISK
     FTELL      UF   E           K DISK
     FSAMLA     UF A E           K DISK
     FCUNDL01   IF   E           K DISK
     FSTED2     IF   E           K DISK
     FKODTFS    IF   E           K DISK
     FFIRM      IF   E           K DISK
     FLOCL01    IF   E           K DISK    USROPN
     FISUL01    IF   E           K DISK    USROPN
     FSYEX01FM  CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     D NAV             S              3    DIM(12) CTDATA PERRCD(1)
     D                 DS
     D  WSSD                   1      3
     D  WSSD5                  1      5
     D                 DS
     D  WSSDF                  1      3
     D  WSSD5F                 1      5
     D                 DS
     D  WSROA1                 1      3
     D  XROA1                  1      5
     D                 DS
     D  WSROA2                 1      3
     D  XROA2                  1      5
     D                 DS
     D  WSROA3                 1      3
     D  XROA3                  1      5
     D                 DS
     D  SMDTÅ                  1      4  0
     D  SMDTM                  5      6  0
     D  SMDTD                  7      8  0
     D  SMDT                   1      8  0
     D                 DS
     D  WMDTD                  1      2  0
     D  WMDTM                  3      4  0
     D  WMDTÅ                  5      6  0
     D  WMDT                   1      6  0
     D                 DS
     D  WSDTD                  1      2  0
     D  WSDTM                  3      4  0
     D  WSDTÅ                  5      6  0
     D  WSDT                   1      6  0
     D                 DS
     D  WSDT8Å                 1      4  0
     D  WSDT8M                 5      6  0
     D  WSDT8D                 7      8  0
     D  WSDT8                  1      8  0
     D                 DS
     D  W2DTD                  1      2  0
     D  W2DTM                  3      4  0
     D  W2DTÅ                  5      6  0
     D  W2DT                   1      6  0
     D                 DS
     D  W2DT8Å                 1      4  0
     D  W2DT8M                 5      6  0
     D  W2DT8D                 7      8  0
     D  W2DT8                  1      8  0
      *____________________________________________________________
      * Definisjon av parameterlisten som brukes ved kall av TDV51C
      * (Schedule Request). TdV IS må være installert
      *""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
     D                 DS
     D  BUTTRM                 1      1
     D  BUTAOR                 2      4
     D  BUTADE                 5      7
     D  BUTDA1                 8      9  0
     D  BUTMN1                10     12
     D  BUTPID                13     29
     D  BUTRA1                30     32
     D  BUTRB1                33     39
     D  BUTRC1                40     41  0
     D  BUTRA2                42     44
     D  BUTRB2                45     51
     D  BUTRC2                52     53  0
     D  BUTRA3                54     56
     D  BUTRB3                57     63
     D  BUTRC3                64     65  0
     D  BUTYKL                66     69  0
     D  BUTYAI                70     72
     D  BUTINF                 1     72
      *___________________
      * DS for System Data
      *"""""""""""""""""""
     D                SDS
     D  WUSER                254    263
     D                UDS
     D  UDAVD                 15     18  0
      *
     C                   EXSR      INIT
      *
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
     C                   MOVE      W2SD          YSD               3
     C                   MOVE      W2DT8         YDT               8 0
     C                   MOVE      W2KL          YKL               4 0
     C                   MOVE      W2KNF         YKNF              3 0
     C                   MOVE      W2KNS         YKNS              8 0
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     SAMLA1        SETLL     SAMLA
     C     START1        TAG
S01  C*                  READ      SAMLA                                  94
N01  C                   READ(N)   SAMLA                                  94
     C  N94
     CANN99SMST          IFEQ      'U'
     C                   GOTO      START1
     C                   END
     C   94              MOVE      *BLANKS       SMSD
     C   94              MOVE      *BLANKS       SMROB1
     C   94              MOVE      *BLANKS       WSNR
S01  C* N94              UPDATE    SAMLAR
      *
     C     START2        TAG
      *
     C  N94              DO
     C                   DO        13
     C                   ADD       1             ZRECNO
     C                   EXSR      KDTFM3
     C                   WRITE     SUBFIL
     C     ST21          TAG
S01  C*                  READ      SAMLA                                  94
N01  C                   READ(N)   SAMLA                                  94
     C  N94
     CANN99SMST          IFEQ      'U'
     C                   GOTO      ST21
     C                   END
     C  N94              DO
S01  C*                  UPDATE    SAMLAR
     C                   MOVE      SMSD          YSD
     C                   MOVE      SMDT          YDT
     C                   MOVE      SMKL          YKL
     C                   MOVE      SMKNF         YKNF
     C                   MOVE      SMKNS         YKNS
     C                   END
     C  N94              END
     C                   END
      *
     C                   Z-ADD     ZRECNO        YRECNO            5 0
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C   98              WRITE     BUNN
     C  N98ZRECNO        IFEQ      0
     C                   EXFMT     NYWRK
     C                   GOTO      ST41
     C                   ELSE
     C                   WRITE     BUNN
     C                   END
     C                   SETOFF                                       3098
     C                   EXFMT     SUBCTL
      *
      *   AVSLUTT
      *
     C     ST41          TAG
     C   03              GOTO      SLUTT
      *   FRISK OPP
     C   05              GOTO      START
      *   LAGE NY SAMLASTAVGANG
     C   06              GOTO      START5
      *   SØK FRA SAMLASTAVGANG
     C   08              GOTO      START6
      *   BLA VIDERE
     C   25              DO
     C                   SETOFF                                       25
     C                   Z-ADD     YRECNO        ZRECNO
     C  N94SAMLA3        SETLL     SAMLA
S01  C* N94              READ      SAMLA                                  94
N01  C  N94              READ(N)   SAMLA                                  94
     C                   GOTO      START2
     C                   END
      *
     C     ZRECNO        IFEQ      0
     C                   GOTO      START3
     C                   ELSE
     C                   EXSR      HENT
     C   98              GOTO      START3
     C                   END
      *   SPØR  SAMLASTAVGANG
     C     WSNR          IFEQ      '5'
     C                   GOTO      START3
     C                   END
      *   SLETT SAMLASTAVGANG
     C     WSNR          IFEQ      '4'
     C                   MOVE      ' '           WSNR
     C                   EXFMT     VAREL3
     C  N12              DO
     C     SAMLA2        CHAIN     SAMLA                              33
     C  N33              DELETE    SAMLAR
     C                   EXSR      MOVB1
     C                   MOVEL     '*DL'         SMSD
     C                   MOVEL     '*DLT'        SMROB1
     C                   END
     C                   UPDATE    SUBFIL
     C                   GOTO      START3
     C                   END
      *
      *   ACTIVATE CONSOLLIDATION
     C     WSNR          IFEQ      '8'
     C                   MOVE      ' '           WSNR
     C                   EXFMT     VAREL3
     C  N12              DO
     C     SAMLA2        CHAIN     SAMLA                              33
     C  N33              EXSR      ACTSAM
     C  N33              UPDATE    SAMLAR
     C                   EXSR      MOVSMW
     C                   END
     C                   EXSR      KDTFM3
     C                   UPDATE    SUBFIL
     C                   GOTO      START3
     C                   END
      *   LAGE/ENDRE SAMLASTAVGANG
     C                   EXSR      KDTFM1
     C     START5        TAG
     C                   EXSR      KDTFM1
     C                   EXFMT     VARELI
     C                   EXSR      KDTFM1
     C                   SETOFF                                       30
     C                   SETOFF                                       706968
     C                   SETOFF                                       676665
     C                   SETOFF                                       646362
     C                   SETOFF                                       616059
     C                   SETOFF                                       585771
      *
     C   03              GOTO      SLUTT
      *
     C   12              DO
     C   40              UPDATE    SUBFIL
     C                   SETOFF                                       40
     C                   EXSR      MOVB2
     C                   GOTO      START3
     C                   END
      *
     C   04              DO
     C                   EXSR      SØKDTA
     C                   END
      *
      * KALL PÅ TDVIS SCHEDUEL REQUEST
      *
     C     *IN23         IFEQ      '1'
     C                   EXSR      SFIR
     C                   GOTO      START5
     C                   END
     C                   EXSR      TESTB1
     C   30              GOTO      START5
     C                   GOTO      START3
      *   SØK FRA UTSKRIFTTYPE
     C     START6        TAG
     C                   EXFMT     VAREL2
     C   03              GOTO      SLUTT
     C                   EXSR      KDTFM2
     C                   MOVE      W2DTÅ         W2DT8Å
     C                   MOVE      W2DTM         W2DT8M
     C                   MOVE      W2DTD         W2DT8D
     C     W2DT          IFNE      *ZEROS
     C     W2DTÅ         IFGT      50
     C                   MOVEL     '19'          W2DT8Å
     C                   ELSE
     C                   MOVEL     '20'          W2DT8Å
     C                   END
     C                   ELSE
     C                   MOVEL     '19'          W2DT8Å
     C                   END
     C                   GOTO      START
      *
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
     C*
      ***********************************************
     C     TESTB1        BEGSR
      ***********************************************
     C  N40              DO
     C     WSSD5F        CHAIN     STED2                              71
     C   71              MOVE      'SPS0012'     MSGNR
     C   71              SETON                                        30
     C   71              GOTO      SLTB
     C     WSSD5         CHAIN     STED2                              70
     C   70              MOVE      'SPS0030'     MSGNR
     C   70              SETON                                        30
     C   70              GOTO      SLTB
      *
     C                   MOVE      WSDTÅ         XÅR               2 0
     C                   MOVE      WSDTM         XMND              2 0
     C                   MOVE      WSDTD         XDAG              2 0
     C                   CALL      'TESTDATO'
     C                   PARM                    XDAG
     C                   PARM                    XMND
     C                   PARM                    XÅR
     C                   PARM                    UFLAGG            1 0
     C     UFLAGG        COMP      1                                      69
     C   69              SETON                                        30
     C   69              MOVE      'SPD0001'     MSGNR
     C   69              GOTO      SLTB
      *
     C     WSKL          COMP      *ZEROS                                 68
     C   68              SETON                                        30
     C   68              MOVE      'SPK0006'     MSGNR
     C   68              GOTO      SLTB
     C     KODFSK        CHAIN     KODTFS                             67
     C   67              SETON                                        30
     C   67              MOVE      'SPF0009'     MSGNR
     C   67              GOTO      SLTB
     C     KUND1K        CHAIN     CUNDL01                            66
     C   66              SETON                                        30
     C   66              MOVE      'SPK0028'     MSGNR
     C   66              GOTO      SLTB
     C                   END
      ***------------------------------------------------
      *
     C     XROA1         CHAIN     STED2                              65
     C     WSROB1        COMP      *BLANKS                                64
     C   64              SETON                                        30
     C   64              MOVE      'SPR0005'     MSGNR
     C   64              GOTO      SLTB
     C     WSROC1        COMP      0                                    6363
     C  N63WSROC1        COMP      31                                 63
     C   63              SETON                                        30
     C   63              MOVE      'SPR0006'     MSGNR
     C   63              GOTO      SLTB
     C     WSROA2        IFEQ      *BLANKS
     C                   MOVE      *BLANKS       WSROB2
     C                   MOVE      *ZEROS        WSROC2
     C                   ELSE
     C     XROA2         CHAIN     STED2                              62
     C     WSROB2        COMP      *BLANKS                                61
     C   61              SETON                                        30
     C   61              MOVE      'SPR0005'     MSGNR
     C   61              GOTO      SLTB
     C     WSROC2        COMP      0                                    6060
     C  N60WSROC2        COMP      31                                 60
     C   60              SETON                                        30
     C   60              MOVE      'SPR0006'     MSGNR
     C   60              GOTO      SLTB
     C                   END
     C     WSROA3        IFEQ      *BLANKS
     C                   MOVE      *BLANKS       WSROB3
     C                   MOVE      *ZEROS        WSROC3
     C                   ELSE
     C     XROA3         CHAIN     STED2                              59
     C     WSROB3        COMP      *BLANKS                              5858
     C   58              SETON                                        30
     C   58              MOVE      'SPR0005'     MSGNR
     C   58              GOTO      SLTB
     C     WSROC3        COMP      0                                    5757
     C  N57WSROC3        COMP      31                                 57
     C   57              MOVE      'SPR0006'     MSGNR
     C   57              SETON                                        30
     C   57              GOTO      SLTB
     C                   END
      *
     C                   MOVE      WSDTD         WSDT8D
     C                   MOVE      WSDTM         WSDT8M
     C                   MOVE      WSDTÅ         WSDT8Å
     C     WSDTÅ         IFGT      50
     C                   MOVEL     '19'          WSDT8Å
     C                   ELSE
     C                   MOVEL     '20'          WSDT8Å
     C                   END
      *   OPPDATERE PÅ REGISTER OG SUBFIL
     C     SAMLAK        CHAIN     SAMLA                              33
     C  N33
S02  C*NN40              GOTO      SLTB
N02  CANN40              DO
n02  C                   SETON                                        6830
n02  C                   MOVE      'SHU2044'     MSGNR
N02  C                   GOTO      SLTB
n02  C                   END
     C                   EXSR      MOVWSM
     C  N33              UPDATE    SAMLAR
     C   33              WRITE     SAMLAR
     C                   EXSR      KDTFM3
     C   40              UPDATE    SUBFIL
     C  N40              ADD       1             YRECNO
     C  N40              Z-ADD     YRECNO        ZRECNO
     C  N40              WRITE     SUBFIL
     C                   WRITE     SUBCTL
     C                   SETOFF                                       40
     C                   EXSR      MOVB2
      *
     C     SLTB          ENDSR
      *
      ***********************************************
     C     HENT          BEGSR
      ***********************************************
     C                   MOVEA     '00'          *IN(91)
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
      *
      * FEIL Å SLETTE / ENDRE VED SPØRRING
      *
     C   99
     CANN98WSNR          IFNE      '5'
     C     WSNR          ANDNE     '9'
     C     WSNR          IFNE      ' '
     C                   SETON                                            98
     C                   GOTO      SLHENT
     C                   END
     C                   END
     C  N98
     CANN99WSNR          IFEQ      '2'
     C     SMSD          COMP      '*DL'                                  98
     C   98              GOTO      SLHENT
     C                   EXSR      MOVSMW
     C                   MOVE      ' '           WSNR
     C                   SETON                                        40
     C                   GOTO      SLHENT
     C                   END
     C  N98
     CANN99WSNR          IFEQ      '4'
     C     SMSD          COMP      '*DL'                                  98
     C                   GOTO      SLHENT
     C                   END
      *
     C  N98WSNR          IFEQ      '5'
     C                   MOVE      ' '           WSNR
     C                   UPDATE    SUBFIL
     C                   CALL      'SYEF53'
     C                   PARM                    SMSD
     C                   PARM                    SMLNR
     C                   PARM                    AVD               4 0
     C                   MOVE      ' '           WSNR
     C                   END
      *
      * ACTIVATE CONSOLLIDATION
      *
     C  N98
     CANN99WSNR          IFEQ      '8'
     C     SMSD          COMP      '*DL'                                  98
     C   98              GOTO      SLHENT
     C                   EXSR      MOVSMW
     C                   GOTO      SLHENT
     C                   END
      * SEND AGENT TIL AGENT MELDING VIA TDVIS
     C  N98WSNR          IFEQ      '9'
     C                   MOVE      ' '           WSNR
     C                   UPDATE    SUBFIL
     C                   EXSR      WRKTDV
     C                   END
     C                   END
     C     SLHENT        ENDSR
      *
      ***********************************************
     C     MOVB1         BEGSR
      ***********************************************
     C                   MOVE      *BLANKS       SMSDF
     C                   MOVE      *BLANKS       SMSD
     C                   MOVE      *ZEROS        SMDT
     C                   MOVE      *ZEROS        WMDT
     C                   MOVE      *ZEROS        SMKL
     C                   MOVE      *ZEROS        SMKNF
     C                   MOVE      *ZEROS        SMKNS
     C                   MOVE      *BLANKS       SMROA1
     C                   MOVE      *BLANKS       SMROB1
     C                   MOVE      *ZEROS        SMROC1
     C                   MOVE      *BLANKS       SMROA2
     C                   MOVE      *BLANKS       SMROB2
     C                   MOVE      *ZEROS        SMROC2
     C                   MOVE      *BLANKS       SMROA3
     C                   MOVE      *BLANKS       SMROB3
     C                   MOVE      *ZEROS        SMROC3
     C                   MOVE      *ZEROS        SMMAWB
     C                   MOVE      *ZEROS        SMVKT1
      *
     C                   ENDSR
      *
      ***********************************************
     C     MOVB2         BEGSR
      ***********************************************
     C                   MOVE      *BLANKS       WSSDF
     C                   MOVE      *BLANKS       WSSD
     C                   MOVE      *ZEROS        WSDT
     C                   MOVE      *ZEROS        WSKL
     C                   MOVE      *ZEROS        WSKNF
     C                   MOVE      *ZEROS        WSKNS
     C                   MOVE      *BLANKS       WSROA1
     C                   MOVE      *BLANKS       WSROB1
     C                   MOVE      *ZEROS        WSROC1
     C                   MOVE      *BLANKS       WSROA2
     C                   MOVE      *BLANKS       WSROB2
     C                   MOVE      *ZEROS        WSROC2
     C                   MOVE      *BLANKS       WSROA3
     C                   MOVE      *BLANKS       WSROB3
     C                   MOVE      *ZEROS        WSROC3
     C                   MOVE      *ZEROS        WSMAWB
     C                   MOVE      *ZEROS        WSVKT1
     C                   MOVE      *ZEROS        WSETD1
     C                   MOVE      *ZEROS        WSETA1
     C                   MOVE      *ZEROS        WSEDA1
     C                   MOVE      *ZEROS        WSETD2
     C                   MOVE      *ZEROS        WSETA2
     C                   MOVE      *ZEROS        WSEDA2
     C                   MOVE      *ZEROS        WSETD3
     C                   MOVE      *ZEROS        WSETA3
     C                   MOVE      *ZEROS        WSEDA3
      *
     C                   ENDSR
      *
      ***********************************************
     C     MOVSMW        BEGSR
      ***********************************************
     C                   MOVE      SMSD          WSSD
     C                   MOVE      SMSDF         WSSDF
     C                   MOVE      SMDTÅ         WSDTÅ
     C                   MOVE      SMDTM         WSDTM
     C                   MOVE      SMDTD         WSDTD
     C                   MOVE      SMKL          WSKL
     C                   MOVE      SMKNF         WSKNF
     C                   MOVE      SMKNS         WSKNS
     C                   MOVE      SMROA1        WSROA1
     C                   MOVE      SMROB1        WSROB1
     C                   MOVE      SMROC1        WSROC1
     C                   MOVE      SMROA2        WSROA2
     C                   MOVE      SMROB2        WSROB2
     C                   MOVE      SMROC2        WSROC2
     C                   MOVE      SMROA3        WSROA3
     C                   MOVE      SMROB3        WSROB3
     C                   MOVE      SMROC3        WSROC3
     C                   MOVE      SMMAWB        WSMAWB
     C                   MOVE      SMVKT1        WSVKT1
     C                   MOVE      SMETD1        WSETD1
     C                   MOVE      SMETA1        WSETA1
     C                   MOVE      SMEDA1        WSEDA1
     C                   MOVE      SMETD2        WSETD2
     C                   MOVE      SMETA2        WSETA2
     C                   MOVE      SMEDA2        WSEDA2
     C                   MOVE      SMETD3        WSETD3
     C                   MOVE      SMETA3        WSETA3
     C                   MOVE      SMEDA3        WSEDA3
      *
     C                   ENDSR
      *
      ***********************************************
     C     MOVWSM        BEGSR
      ***********************************************
     C   33              DO
     C                   MOVE      ' '           SMST
     C                   MOVE      WSSD          SMSD
     C                   MOVE      WSDT8         SMDT
     C                   MOVE      WSKL          SMKL
     C                   MOVE      WSKNF         SMKNF
     C                   MOVE      WSKNS         SMKNS
     C                   MOVE      *ZEROS        SMLNR
     C                   MOVE      *ZEROS        SMNT
     C                   MOVE      *BLANKS       SMXXX
     C                   MOVE      *ZEROS        SMPRO
     C                   END
      *
     C                   MOVE      WSSDF         SMSDF
     C                   MOVE      WSROA1        SMROA1
     C                   MOVE      WSROB1        SMROB1
     C                   MOVE      WSROC1        SMROC1
     C                   MOVE      WSROA2        SMROA2
     C                   MOVE      WSROB2        SMROB2
     C                   MOVE      WSROC2        SMROC2
     C                   MOVE      WSROA3        SMROA3
     C                   MOVE      WSROB3        SMROB3
     C                   MOVE      WSROC3        SMROC3
     C                   MOVE      WSMAWB        SMMAWB
     C                   MOVE      WSVKT1        SMVKT1
     C                   MOVE      WSETD1        SMETD1
     C                   MOVE      WSETA1        SMETA1
     C                   MOVE      WSEDA1        SMEDA1
     C                   MOVE      WSETD2        SMETD2
     C                   MOVE      WSETA2        SMETA2
     C                   MOVE      WSEDA2        SMEDA2
     C                   MOVE      WSETD3        SMETD3
     C                   MOVE      WSETA3        SMETA3
     C                   MOVE      WSEDA3        SMEDA3
      *
     C                   ENDSR
      *
      ***********************************************
     C     ACTSAM        BEGSR
      ***********************************************
      *
      *
      * GETS NEXT MAWB-NUMBER
      *
     C     SMMAWB        IFEQ      *ZEROS
     C                   Z-ADD     2             IN10CP            1 0
     C     KODTWK        CHAIN     KODTW                              29
     C  N29              DO
     C                   MOVE      'E'           UPROD
     C                   CALL      'SYIN10C'
     C                   PARM                    SMKNF
     C                   PARM                    SMMAWB
     C                   PARM                    IN10CP
     C                   PARM                    KOWLAS
     C                   PARM                    UPROD             1
     C                   END
     C                   END
      *
      * GETS NEXT CONSOLLIDATIONNUMBER FOR DESTINATION
      *
     C     SMLNR         IFEQ      *ZEROS
     C     SMSD          CHAIN     TELLDE                             28
     C   28              DO
     C                   MOVE      SMSD          TEDSD
     C                   Z-ADD     1             TEDNR
     C                   WRITE     TELLDER
     C                   END
     C  N28              ADD       1             TEDNR
     C  N28              UPDATE    TELLDER
     C                   Z-ADD     TEDNR         SMLNR
     C                   END
      *
      * GETS NEXT PROJECTNUMBER
      *
     C     SMPRO         IFEQ      *ZEROS
     C     UDAVD         CHAIN     TELL                               27
     C   27AVDZER        CHAIN     TELL                               27
     C  N27              Z-ADD     TETURN        SMPRO
     C  N27              ADD       1             TETURN
     C  N27              UPDATE    TELLR
     C                   END
      *
     C                   ENDSR
      ***********************************************
     C     INIT          BEGSR
      ***********************************************
     C     KODTWK        KLIST
     C                   KFLD                    WSWUNI
     C                   KFLD                    UDAVD
     C     SAMLAK        KLIST
     C                   KFLD                    WSSD
     C                   KFLD                    WSDT8
     C                   KFLD                    WSKL
     C                   KFLD                    WSKNF
     C                   KFLD                    WSKNS
     C     SAMLA1        KLIST
     C                   KFLD                    W2SD
     C                   KFLD                    W2DT8
     C                   KFLD                    W2KL
     C                   KFLD                    W2KNF
     C                   KFLD                    W2KNS
     C     SAMLA2        KLIST
     C                   KFLD                    SMSD
     C                   KFLD                    SMDT
     C                   KFLD                    SMKL
     C                   KFLD                    SMKNF
     C                   KFLD                    SMKNS
     C     SAMLA3        KLIST
     C                   KFLD                    YSD
     C                   KFLD                    YDT
     C                   KFLD                    YKL
     C                   KFLD                    YKNF
     C                   KFLD                    YKNS
     C     KUND1K        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    WSKNS
     C     KODFSK        KLIST
     C                   KFLD                    YFSUNI
     C                   KFLD                    WSKNF
      *
     C     *ENTRY        PLIST
     C                   PARM                    UIN01             1
      *   UIN01 = 1   -->   SPØRRING (SYEF01CL)
      *   UIN01 = 0   -->   VEDLIKEHOLD (SYEX01CL)
     C     UIN01         COMP      '1'                                    99
      *
      *
      *
     C   99              MOVE      'DIV0578'     XMSGNR            7            MELDING-ID
     C  N99              MOVE      'DIV0582'     XMSGNR                         MELDING-ID
     C                   Z-ADD     3             XLEVEL            1 0          NIVÅ
     C                   Z-ADD     0             XRETUR            1 0          RETURKODE
     C                   Z-ADD     34            XMSGL             4 0          LENGDE PÅ MELDING
     C                   CALL      'SUBR23R3'
     C                   PARM                    XMSGNR
     C                   PARM                    WSMSG
     C                   PARM                    XLEVEL
     C                   PARM                    XRETUR
     C                   PARM                    XMSGL
      *
     C                   MOVE      'FS'          YFSUNI            2
     C                   MOVE      'W'           WSWUNI            1
     C                   MOVE      *ZEROS        WSDT
     C                   MOVE      *ZEROS        WSDT8
     C                   MOVE      *ZEROS        SMDT
     C                   MOVE      *ZEROS        W2DT
     C                   MOVE      *ZEROS        W2DT8
     C     *LIKE         DEFINE    UDAVD         AVDZER
      *
     C                   MOVE      *BLANKS       FIFIRM
     C     FIFIRM        SETLL     FIRM
     C                   SETOFF                                       33
     C                   READ      FIRM                                   33
      *
     C                   ENDSR
      *
      ***********************************************
     C     KDTFM1        BEGSR
      ***********************************************
     C                   MOVE      WSDTD         UDT1              2 0
     C                   MOVE      WSDTM         UDT2              2 0
     C                   MOVE      WSDTÅ         UDT3              2 0
     C                   CALL      'DATOFMT'
     C                   PARM                    UDT1
     C                   PARM                    UDT2
     C                   PARM                    UDT3
     C                   MOVE      UDT1          WSDTD
     C                   MOVE      UDT2          WSDTM
     C                   MOVE      UDT3          WSDTÅ
      *
     C                   ENDSR
      *
      ***********************************************
     C     KDTFM2        BEGSR
      ***********************************************
     C                   MOVE      W2DTD         UDT1
     C                   MOVE      W2DTM         UDT2
     C                   MOVE      W2DTÅ         UDT3
     C                   CALL      'DATOFMT'
     C                   PARM                    UDT1
     C                   PARM                    UDT2
     C                   PARM                    UDT3
     C                   MOVE      UDT1          W2DTD
     C                   MOVE      UDT2          W2DTM
     C                   MOVE      UDT3          W2DTÅ
      *
     C                   ENDSR
      *
      ***********************************************
     C     KDTFM3        BEGSR
      ***********************************************
     C                   MOVE      SMDTD         UDT1
     C                   MOVE      SMDTM         UDT2
     C                   MOVE      SMDTÅ         UDT3
     C                   CALL      'DATOFMT'
     C                   PARM                    UDT1
     C                   PARM                    UDT2
     C                   PARM                    UDT3
     C                   MOVE      UDT1          WMDTD
     C                   MOVE      UDT2          WMDTM
     C                   MOVE      UDT3          WMDTÅ
      *
     C                   ENDSR
      *
      ***********************************************
     C     SØKDTA        BEGSR
      ***********************************************
     C                   MOVE      *BLANKS       USØKKN           30
     C                   CALL      'SOEKKUN'
     C                   PARM                    WSKNS
     C                   PARM                    FIFIRM
     C                   PARM                    USØKKN           30
      *
      *
     C                   ENDSR
      *
      ***********************************************
     C     SFIR          BEGSR
      ***********************************************
     C                   OPEN      LOCL01                               91
     C  N91              OPEN      ISUL01                               91
     C     *IN91         IFEQ      '1'
     C                   MOVEL     'BBA0035'     MSGNR
     C                   MOVE      *ON           *IN30
     C                   GOTO      SFIR99
     C                   ENDIF
      * Blanker hele parameterstrengen
     C                   MOVEL     *BLANKS       BUTINF
     C                   MOVEL     WUSER         WWUSER           17
     C     WWUSER        CHAIN     LOCL01                             90
     C     *IN90         IFEQ      '0'
     C                   MOVEL     LODEFA        BUTPID                         <ADRESSE
     C                   ELSE
     C                   MOVEL     *BLANKS       BUTPID                         <ADRESSE
     C                   ENDIF
     C                   MOVEL     WSSDF         BUTAOR                         <ORIGIN AIRPORT
     C                   MOVEL     WSSD          BUTADE                         <DEST. AIRPORT
     C                   Z-ADD     WSDTD         BUTDA1
     C                   Z-ADD     WSDTM         ZZ                3 0
     C     ZZ            IFGT      0
     C     ZZ            ANDLE     12
     C                   MOVEA     NAV(ZZ)       BUTMN1                         <FLIGHT MND
     C                   ENDIF
     C                   Z-ADD     WSKL          BUTYKL
     C     WSKNF         IFNE      *ZEROS
     C     WSKNF         CHAIN     ISUL01                             90
     C     *IN90         IFEQ      '0'
     C                   MOVEL     ISCODE        BUTYAI
     C                   ENDIF
     C                   ENDIF
      * Kaller på program for å sende og motta Schedule Request
      * Parameter inneholder routing hvis plukket av bruker
     C                   CALL      'TDV51C'
     C                   PARM                    BUTINF
      *
      * Routingen på fraktbrevbildet skal oppdateres etter den
      * routing som bruker har valgt i TDV51R
      *
     C     BUTRA1        IFNE      *BLANKS
     C     BUTRA2        ORNE      *BLANKS
     C     BUTRA3        ORNE      *BLANKS
     C                   MOVEL     'BBA0036'     MSGNR
     C                   MOVE      *ON           *IN30
     C     BUTRA1        IFNE      *BLANKS
     C                   MOVEL     BUTRA1        WSROA1
     C                   MOVEL     BUTRB1        WSROB1
     C                   Z-ADD     BUTRC1        WSROC1
     C                   ENDIF                                                  <BUTRA1
     C     BUTRA2        IFNE      *BLANKS
     C                   MOVEL     BUTRA2        WSROA2
     C                   MOVEL     BUTRB2        WSROB2
     C                   Z-ADD     BUTRC2        WSROC2
     C                   ENDIF                                                  <BUTRA2
     C     BUTRA3        IFNE      *BLANKS
     C                   MOVEL     BUTRA3        WSROA3
     C                   MOVEL     BUTRB3        WSROB3
     C                   Z-ADD     BUTRC3        WSROC3
     C                   ENDIF                                                  <BUTRA3
     C                   ENDIF                                                  <BUTRA1
      *
     C     SFIR99        TAG
     C                   CLOSE     LOCL01                               91
     C                   CLOSE     ISUL01                               91
      *
     C                   ENDSR
      ***********************************************
     C     WRKTDV        BEGSR
      ***********************************************
      * Sending av master informasjon via TdV IS.
      * Det kan sendes MAWB (FWB), manifest (AZF) og HAWB (AZB)
     C                   Z-ADD     SMLNR         PMLNR             3 0
     C                   CALL      'TDV20R'
     C                   PARM      *ZEROS        PAVD              4 0
     C                   PARM      *ZEROS        POPD              7 0
     C                   PARM                    SMSD
     C                   PARM                    PMLNR
     C                   ENDSR
**    TABELL  (NAV)  ENDRING N02BB
JAN
FEB
MAR
APR
MAY
JUN
JUL
AUG
SEP
OCT
NOV
DEC
