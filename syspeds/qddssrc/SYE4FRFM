     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPDOC('STD' 'HELPNU' 'HLP400')
     A                                      HELP
     A                                      ALTHELP
     A          R BILDE
     A                                      CF03(03 'Avslutt')
     A            MSG001        22A  O  2 58MSGID(DIV0047 SYMSGF)
     A                                      COLOR(TRQ)
     A            MSG002        40A  O  3 17MSGID(DIV1835 SYMSGF)
     A                                      COLOR(WHT)
     A            MSG003        40A  O  4 17MSGID(DIV1836 SYMSGF)
     A                                      COLOR(WHT)
     A            MSG004        45A  O  5 17MSGID(DIV1837 SYMSGF)
     A                                      COLOR(WHT)
     A            MSG005        24A  O  8 17MSGID(DIV1497 SYMSGF)
     A            WSAVD          4Y 0B  8 44DSPATR(UL)
     A  99                                  DSPATR(RI)
     A  99                                  DSPATR(PC)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            MSG006        24A  O 10 17MSGID(DIV1498 SYMSGF)
     A            WSSG           3A  B 10 44DSPATR(UL)
     A  98                                  DSPATR(RI)
     A  98                                  DSPATR(PC)
     A                                      COLOR(YLW)
     A            MSG007        12A  O 10 57MSGID(DIV1513 SYMSGF)
     A            MSG008        24A  O 12 17MSGID(DIV1838 SYMSGF)
     A            WSSD           3A  B 12 44DSPATR(UL)
     A  97                                  DSPATR(RI)
     A  97                                  DSPATR(PC)
     A                                      COLOR(YLW)
     A            WSLNR          3Y 0B 12 48DSPATR(UL)
     A  97                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            MSG009        24A  O 15 17MSGID(DIV1509 SYMSGF)
     A            WSJBKØ         1A  B 15 44DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'J' 'N')
     A            MSG010        10A  O 15 48MSGID(DIV1839 SYMSGF)
     A            MSG011        24A  O 16 17MSGID(DIV1510 SYMSGF)
     A            WSPRT         10A  B 16 44DSPATR(UL)
     A                                      COLOR(YLW)
     A            MSG012        18A  O 23 17MSGID(DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
