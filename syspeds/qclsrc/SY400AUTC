/*  N01 REORG  IFS-MAPPER AV TYPE BACKUP 7 DAGER GAMLE */
/*  N02 REORG  ADOBE TEMP AV TYPE BACKUP 7 DAGER GAMLE */
/*  N03 REORG  EDI-REOG KUN PÅ LØR/SØN */
/*  N04 NYTT PJ, DANSK TOLD */
/*  N05 NYTT PJ, DANSK TOLD */
/*  N06 NY DTAQ, DANSK TOLD */
/*  N07 SAD61PQ ENDRET DTAQ */
/*  N07 NY DTAQ, DANSK TOLD */
/*  N08 07:00 -> 08:00      */
/*  N09 XML REORG 10 DAGER  */
/*  N10 TATT BORT FAX *DIR* */
/*  N11 NY DTAQ, EPOSTDIR   */
             PGM
/*___________*/
/* VARIABLER */
/*"""""""""""*/
             DCL        VAR(&TIME) TYPE(*CHAR) LEN(6)
             DCL        VAR(&SYSPED) TYPE(*CHAR) LEN(1) VALUE('Y')
             DCL        VAR(&FILIBF) TYPE(*CHAR) LEN(10)
             DCL        VAR(&FILIBO) TYPE(*CHAR) LEN(10)
             DCL        VAR(&FITRAN) TYPE(*CHAR) LEN(1)
             DCL        VAR(&FILE11) TYPE(*CHAR) LEN(1)
             DCL        VAR(&FILE21) TYPE(*CHAR) LEN(1)
             DCL        VAR(&IN51) TYPE(*LGL) LEN(1)
             DCL        VAR(&UESYSP) TYPE(*CHAR) LEN(1)
             DCL        VAR(&SYFLIBE) TYPE(*CHAR) LEN(10)
             DCL        VAR(&SYSBS) TYPE(*CHAR) LEN(10)
             DCL        VAR(&SYSBS) TYPE(*CHAR) LEN(10)
/*N03*/      DCL        VAR(&DAG) TYPE(*CHAR) LEN(4)
/*______________*/
/* LES FIRMAFIL */
/*""""""""""""""*/
             DCLF       FILE(FIRM) RCDFMT(*ALL)
             RCVF       DEV(*FILE) RCDFMT(*FILE)
/*________________________________*/
/* HENT INSTALLASJONSOPPLYSNINGER */
/*""""""""""""""""""""""""""""""""*/
             CALL       PGM(GETFIRMF1) PARM(&FIFIRM &SYFLIBE &SYSBS)
/*N03_____________________*/
/*N03 HENT NAVN PÅ UKEDAG */
/*N03"""""""""""""""""""""*/
/*N03*/      RTVSYSVAL  SYSVAL(QDAYOFWEEK) RTNVAR(&DAG)
/*_________________________________________*/
/* SLETTE OG LAG NYE DATAKØER KUN OM NATTA */
/*"""""""""""""""""""""""""""""""""""""""""*/

             RTVSYSVAL  SYSVAL(QTIME) RTNVAR(&TIME)
             IF         COND(&TIME *GT '000000') THEN(DO)
             IF         COND(&TIME *LT '080001') THEN(DO)

             CALL       PGM(SYTR94CL) /* N02 */
             DLTDTAQ    DTAQ(&FILIBF/SAD61PQ)
             MONMSG     MSGID(CPF0000)

             DLTDTAQ    DTAQ(&FILIBF/SAD52PQ)
             MONMSG     MSGID(CPF0000)


             DLTDTAQ    DTAQ(&FILIBF/EPOSTDTAQ)
             MONMSG     MSGID(CPF0000)

/*N11*/      DLTDTAQ    DTAQ(&FILIBF/EPOSTDIR)
/*N11*/      MONMSG     MSGID(CPF0000)


             DLTDTAQ    DTAQ(&FILIBF/SAD41PQ)
             MONMSG     MSGID(CPF0000)

             DLTDTAQ    DTAQ(&FILIBF/TR014RQ)
             MONMSG     MSGID(CPF0000)

             DLTDTAQ    DTAQ(&FILIBF/SYFI10XQ)
             MONMSG     MSGID(CPF0000)

             DLTDTAQ    DTAQ(&FILIBF/SYEX88Q)
             MONMSG     MSGID(CPF0000)

             DLTDTAQ    DTAQ(SV*)
             MONMSG     MSGID(CPF0000)

             DLTDTAQ    DTAQ(&FILIBF/E081Q)
             MONMSG     MSGID(CPF0000)

/*N06*/      DLTDTAQ    DTAQ(&FILIBF/DKDTAQ)
/*N06*/      MONMSG     MSGID(CPF0000)

             CRTDTAQ    DTAQ(&FILIBF/SAD61PQ) MAXLEN(128) +
                          FORCE(*YES) TEXT('SAD Import, Dataq for +
                          utskriftsprogram') AUT(*ALL)
             MONMSG     MSGID(CPF0000)

             CRTDTAQ    DTAQ(&FILIBF/SAD52PQ) MAXLEN(128) +
                          TEXT('SAD Export, Dataq for +
                          utskriftsprogram') AUT(*ALL)
             MONMSG     MSGID(CPF0000)
             CRTDTAQ    DTAQ(&FILIBF/EPOSTDTAQ) MAXLEN(132) +
                          TEXT('EPOSTDRIVER') AUT(*ALL) /*N15*/
             MONMSG     MSGID(CPF0000)

/*N11*/      CRTDTAQ    DTAQ(&FILIBF/EPOSTDIR) MAXLEN(132) +
                          TEXT('EPOSTDRIVER') AUT(*ALL) /*N15*/
/*N11*/      MONMSG     MSGID(CPF0000)

             CRTDTAQ    DTAQ(&FILIBF/SAD41PQ) MAXLEN(30) +
                          TEXT('SAD transiit, Dataq for +
                          utskriftsprogram') AUT(*ALL)
             MONMSG     MSGID(CPF0000)

             CRTDTAQ    DTAQ(&FILIBF/TR014RQ) MAXLEN(128) +
                          TEXT('El. transiit, Dataq for +
                          utskriftsprogram') AUT(*ALL)
             MONMSG     MSGID(CPF0000)

             CRTDTAQ    DTAQ(&FILIBF/SYFI10XQ) MAXLEN(109) +
                          TEXT('Dataq for invoice-calcu +
                          lation') AUT(*ALL)
             MONMSG     MSGID(CPF0000)

             CRTDTAQ    DTAQ(&FILIBF/SYEX88Q) MAXLEN(12) +
                          TEXT('Datakø for oppstart av AVR +
                          beregning') AUT(*ALL)
             MONMSG     MSGID(CPF0000)

             CRTDTAQ    DTAQ(&FILIBF/SVGSRQ) MAXLEN(11) TEXT('TDS  +
                          Dokumentsigill, Bestilling') AUT(*ALL)
             MONMSG     MSGID(CPF0000)
             CRTDTAQ    DTAQ(&FILIBF/SVGSSQ) MAXLEN(6) SEQ(*KEYED) +
                          KEYLEN(11) TEXT('TDS  Dokumentsigill, +
                          resultat') AUT(*ALL)
             MONMSG     MSGID(CPF0000)
             CRTDTAQ    DTAQ(&FILIBF/SVX014RQ) MAXLEN(128) TEXT(' +
                          SVENSK NCTS') AUT(*ALL)
             MONMSG     MSGID(CPF0000)
/*N07*/      CRTDTAQ    DTAQ(&FILIBF/DKX014RQ) MAXLEN(128) TEXT(' +
                          DANSK NCTS') AUT(*ALL)
/*N07*/      MONMSG     MSGID(CPF0000)
             CRTDTAQ    DTAQ(&FILIBF/E081Q) MAXLEN(30) TEXT('EDI +
                         EDIfact RCV, dataq for convert') AUT(*ALL)
             MONMSG     MSGID(CPF0000)
/*N06*/      CRTDTAQ    DTAQ(&FILIBF/DKDTAQ) MAXLEN(1) FORCE(*YES) +
                          TEXT('SKAT  Dataq for SND/RCV SKAT/TOLD IMP/EKS') +
                          AUT(*ALL)
/*N06*/      MONMSG     MSGID(CPF0000)
/*___________________________________________________________________*/
/* REORGANISERINGSRUTINER                                            */
/* FØLGENDE KUN LØRDAG/SØNDAG                                        */
/*___________________________________________________________________*/
             /*"""""""""""""""""""*/
/*N03*/      IF         COND((&DAG *EQ '*SAT') *OR (&DAG *EQ '*SUN')) +
                          THEN(DO)
             /*_________________________*/
             /* EDI-FILER               */
             /*"""""""""""""""""""""""""*/
             CALL       PGM(EDI09C)
             /*_________________________*/
             /* REORG AV DIV FILER      */
             /*"""""""""""""""""""""""""*/
             CALL       PGM(REOFILER)
             MONMSG     MSGID(CPF0000)
             /*_______________________*/
             /* REORG AV Z-FILER-LOGG */
             /*"""""""""""""""""""""""*/
             CALL       PGM(Z500C)
/*N03*/      ENDDO
/*___________________________________________________________________*/
/* REORGANISERINGSRUTINER                                            */
/* KJØRES DAGLIG                                                     */
/*___________________________________________________________________*/
             /*_________________________*/
             /* REORG AV IFS CGI ETC    */
             /*"""""""""""""""""""""""""*/
             /* FJERN FILER ELDRE ENN 7 DGR FRA DIVERSE IFS-MAPPER*/
             /* MAPPER SOM ER FELLES  KUN VED OPPSTART AV HOVEDFIRMA */
             CALL       PGM(REOIFSCL)
             MONMSG     MSGID(CPF0000)
             /*______________________________*/
             /* REORG XML ELDRE ENN 10 DAGER */
             /*______________________________*/
/* N09 */    CALL       PGM(XMLREOC)
/* N09 */    MONMSG     MSGID(CPF0000)
             /*______________________________*/
             /* REORG ADOBE/IFS TEMP  7DG    */
             /*______________________________*/
             CALL       PGM(EDI47C2)
             MONMSG     MSGID(CPF0000)
             /*______________________________*/
             /* REORG EDI/IFS BACKUP 7DG     */
             /* I EDIRE/RE1K/SE/SE1K         */
             /*______________________________*/
             CALL       PGM(EDI47C)
             MONMSG     MSGID(CPF0000)
             /*______________________________*/
             /* REORG AV ARKIV               */
             /*______________________________*/
             CALL       PGM(ARC042)

             ENDDO            /* ENDDO TIME > 000000 */
             ENDDO            /* ENDDO TIME < 080001 */
             /*______________________*/
             /* START AV DELSYSTEMER */
             /*""""""""""""""""""""""*/
             STRSBS     SBSD(QSNADS)
             MONMSG     MSGID(CPF0000)

/*______________________*/
/* START AV PROGRAMMER  */
/*""""""""""""""""""""""*/

/* TRANSP.MODUL SJEKK TURSTATUSER A - B (FERDI/UNDERVEIS) MOT UDATE */
             IF         COND(&FITRAN = 'J') THEN(DO)
             SBMJOB     CMD(CALL PGM(SYTR50AU)) +
                          JOB(TURSTATUS) JOBQ(SYJOBQNMAX)
             ENDDO

/* AUTOMATISK OVERFØRING TIL REGNSKAP ????? */
/* MEN UTFØRES KUN NÅR SYSTEMET STARTES OM NATTA */

             RTVSYSVAL  SYSVAL(QTIME) RTNVAR(&TIME)
             IF         COND(&TIME *GT '000000') THEN(DO)
             IF         COND(&TIME *LT '080001') THEN(DO)

/* START EVT. OVERFØRING AV FAKTURA  */

             IF         COND(&FILE11 = 'J') THEN(DO)
             SBMJOB     CMD(CALL PGM(SYFA35CLN)) JOB(FAKTURA) +
                          JOBD(SY400JOBD) +
                          JOBQ(SY400JOBQ)
             ENDDO
/* START EVT. OVERFØRING AV KOSTNADSBILAG  */
             IF         COND(&FILE21 = 'J') THEN(DO)
             SBMJOB     CMD(CALL PGM(SYKS35CLN)) +
                          JOB(KOSTBILAG) JOBD(SY400JOBD) +
                          JOBQ(SY400JOBQ)
             ENDDO

             ENDDO            /* ENDDO TIME > 000000 */
             ENDDO            /* ENDDO TIME < 080001 */

/*_______________________*/
/* START PRESTARTED JOBS */
/*"""""""""""""""""""""""*/
             STRPJ      SBS(&SYSBS) PGM(MAILGETP)
             MONMSG     MSGID(CPF0000)
/*N11*/      STRPJ      SBS(&SYSBS) PGM(EPOSTDIR1)
             MONMSG     MSGID(CPF0000)
             STRPJ      SBS(&SYSBS) PGM(EDI02C)
             MONMSG     MSGID(CPF0000)
             STRPJ      SBS(&SYSBS) PGM(EDI10C)
             MONMSG     MSGID(CPF0000)
             STRPJ      SBS(&SYSBS) PGM(E081C)
             MONMSG     MSGID(CPF0000)
             CALL       PGM(EDI01R) PARM('E' '3' &IN51)
             IF         COND(&IN51 *EQ '0') THEN(DO)
             STRPJ      SBS(&SYSBS) PGM(EDI07C3)
             MONMSG     MSGID(CPF0000)
             ENDDO
             CALL       PGM(EDI01R) PARM('E' '6' &IN51)
             IF         COND(&IN51 *EQ '0') THEN(DO)
             STRPJ      SBS(&SYSBS) PGM(EDI07C6)
             MONMSG     MSGID(CPF0000)
             ENDDO
             CALL       PGM(EDI01R) PARM('E' '7' &IN51)
             IF         COND(&IN51 *EQ '0') THEN(DO)
             STRPJ      SBS(&SYSBS) PGM(EDI07C7)
             MONMSG     MSGID(CPF0000)
             ENDDO
             CALL       PGM(EDI01R) PARM('E' '8' &IN51)
             IF         COND(&IN51 *EQ '0') THEN(DO)
             STRPJ      SBS(&SYSBS) PGM(EDI07C8)
             MONMSG     MSGID(CPF0000)
             ENDDO
             CALL       PGM(EDI01R) PARM('E' '9' &IN51)
             IF         COND(&IN51 *EQ '0') THEN(DO)
             STRPJ      SBS(&SYSBS) PGM(EDI07C9)
             MONMSG     MSGID(CPF0000)
             ENDDO
             CALL       PGM(EDI01R) PARM('E' 'A' &IN51)
             IF         COND(&IN51 *EQ '0') THEN(DO)
             STRPJ      SBS(&SYSBS) PGM(EDI07CA)
             MONMSG     MSGID(CPF0000)
             ENDDO
             CALL       PGM(EDI01R) PARM('E' 'B' &IN51)
             IF         COND(&IN51 *EQ '0') THEN(DO)
             STRPJ      SBS(&SYSBS) PGM(EDI07CB)
             MONMSG     MSGID(CPF0000)
             ENDDO
             CALL       PGM(EDI01R) PARM('E' 'E' &IN51)
             IF         COND(&IN51 *EQ '0') THEN(DO)
             STRPJ      SBS(&SYSBS) PGM(EDI07CE)
             MONMSG     MSGID(CPF0000)
             ENDDO
/*N04*/      CALL       PGM(EDI01R) PARM('E' '0' &IN51)
/*N04*/      IF         COND(&IN51 *EQ '0') THEN(DO)
/*N04*/      STRPJ      SBS(&SYSBS) PGM(EDI07C0)
/*N04*/      MONMSG     MSGID(CPF0000)
/*N04*/      ENDDO
             STRPJ      SBS(&SYSBS) PGM(SAD061CLQ) /* Print Custom +
                          declarations */
             MONMSG     MSGID(CPF0000)
             STRPJ      SBS(&SYSBS) PGM(SAD052CLQ) /* Print Custom +
                          declarations */
             MONMSG     MSGID(CPF0000)
             STRPJ      SBS(&SYSBS) PGM(SAD041CLQ) /* Print Custom +
                          declarations */
             MONMSG     MSGID(CPF0000)
             STRPJ      SBS(&SYSBS) PGM(TR014CQ) /* Print Custom +
                          declarations */
             MONMSG     MSGID(CPF0000)
             STRPJ      SBS(&SYSBS) PGM(SYFI10XQ) /* Invoice +
                          calculation */
             MONMSG     MSGID(CPF0000)
             STRPJ      SBS(&SYSBS) PGM(TVI02C) /* Invoice +
                          calculation */
             MONMSG     MSGID(CPF0000)
             STRPJ      SBS(&SYSBS) PGM(TR101C)
             MONMSG     MSGID(CPF0000)
             STRPJ      SBS(&SYSBS) PGM(SVG161C)
             MONMSG     MSGID(CPF0000)
             STRPJ      SBS(&SYSBS) PGM(SVX014CQ)
             MONMSG     MSGID(CPF0000)
             STRPJ      SBS(&SYSBS) PGM(SVX101C)
             MONMSG     MSGID(CPF0000)
/*N05*/      STRPJ      SBS(&SYSBS) PGM(DKG161C)
/*N05*/      MONMSG     MSGID(CPF0000)
/*________________________________*/
/* DIVERSE ANDRE (IKKE PJ-)JOBBER */
/*""""""""""""""""""""""""""""""""*/
/* SJEKKE "SECUR" PÅ ORDRE I SHIP-SYS  - DLYJOB/LOOP */
             SBMJOB     CMD(CALL PGM(GSS037C)) JOB(SECURORDER) +
                          JOBD(SY400JOBD) JOBQ(SYJOBQNMAX)
/* ARKIV-AGENT, LETE ETTER DUKUMENT I MAPPER-> INN I ARKIV - DLYJOB/LOOP */
             SBMJOB     CMD(CALL PGM(ARCA001CL)) JOB(ARKSKANAGE) +
                          JOBD(SY400JOBD) JOBQ(SYJOBQNMAX)
/*START IFTSTA(MM.)-SENDING FRA T&T-LOGG */
/*INNTIL VIDERE STJERNET UT I STANDARD - AKTIVERES VED BEHOV */
/*           SBMJOB     CMD(CALL PGM(ESTSTTC)) JOB(EDI_TT_STA) +
                          JOBD(SY400JOBD) JOBQ(SYJOBQNMAX)  */
/* PURRING (MAIL) PÅ RYCKATTEST - KJØRES KUN EN GANG/SÅ DØR DEN */
             SBMJOB     CMD(CALL PGM(SYRU01C)) JOB(PURRUCK) +
                          JOBD(SY400JOBD) JOBQ(SYJOBQNMAX)
             ENDPGM
