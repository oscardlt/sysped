********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10261 * 01 YBC PTF10 VEKT/KUBIKK I TURER ER ENDRET TIL 9.0 OG 7.3
10XXX * 02 YBC PTF10 SJEKK OM DET ER LOV Å PLUKKE HELE PO
********************************************************************
     FEPOPOS    IF A E           K DISK
     FEPOTT     O  A E             DISK
     FEPOH      UF   E           K DISK
     FEPOL      UF   E           K DISK
     FHEADF     UF   E           K DISK
     FTURER14   UF   E           K DISK
      *
     C                   EXSR      INIT
      *
     C                   IF        EPLIN = 0
     C                   EXSR      SRPLPO
     C                   ELSE
     C                   EXSR      SRPLLIN
     C                   END
      *
     C                   eval      *inlr = *on
     C                   return
      *
      *=====================================================================******
      *  PLUKK HELE PO
      *=====================================================================******
     C     SRPLPO        BEGSR
N02   *
N02  C     EPOHK         SETLL     EPOL
N02  C     EPOHK         READE     EPOL                                   33
N02  C                   DOW       *IN33 = *OFF
N02   * FINNES AVVIKENDE FRA/TIL STED --> IKKE PLUKK HELE PO
N02  C                   IF        (ELFSTE <> *BLANKS AND ELFSTE <> EHFSTE) OR
N02  C                             (ELTSTE <> *BLANKS AND ELTSTE <> EHTSTE)
N02  C                   GOTO      SLPLPO
N02  C                   END
N02  C     EPOHK         READE     EPOL                                   33
N02  C                   ENDDO
      *
      * PLUKK HELE BESTILLING
     C     EPOHK         CHAIN     EPOH                               33
     C                   IF        *IN33 = *OFF                                 1<-B
     C                   IF        EHAVD = 0                                     2<-B
     C                   MOVE      UAVD          EHAVD
     C                   MOVE      UOPD          EHOPD
     C     HEPRO         CHAIN(N)  TURER14                            33
     C                   EVAL      EHSTA = '2'                                  Booked
     c                   IF        TUTST4= 'X'                                    3<-B
     C                   EVAL      EHSTA = '3'                                  Confirmed
     c                   endif                                                    3<-E
     C                   EVAL      EHSTA2= 'S'                                  SEND
     C                   UPDATE    EPOHR
      * Update på linjenivå,
     C     EPOHK         SETLL     EPOL
     C     EPOHK         READE     EPOL                                   33
     C                   DOW       *IN33 = *OFF                                   3<-B
     C                   EVAL      ELSTA = EHSTA
     C                   EVAL      ELSTA2= 'S'                                  SEND
     C                   UPDATE    EPOLR
     C     EPOHK         READE     EPOL                                   33
     C                   ENDDO                                                    3<-E
      * T&T
     C                   EVAL      ETKUND = EHKUND
     C                   EVAL      ETBES  = EHBES
     C                   EVAL      ETACTI = 'PIC'
     C                   EVAL      ETTEXT = 'PO is picked up to order '
     C                             + %TRIM(%EDITC(EHAVD:'Z')) + '/'
     C                             + %TRIM(%EDITC(EHOPD:'Z'))
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVE      WDT           ETDATE
     C                   MOVE      WTM           ETTIME
     C                   MOVE      UUSER         ETUSER
     C                   WRITE     EPOTTR
      * Skriv x-ref ubetinget - sats 100% på at det fjernes ved evt remove fra oppdrag
      * finn høyeste eksisterende POSnr - add 10
     c     PosMax        setgt     EPOPOS
     c     HEADFK        readpe    EPOPOS
     C                   eval      EPAVD = EHAVD
     C                   eval      EPOPD = EHOPD
      * STUF.SEKVENSNR PARAMETERSTYRES SENERE ?
     C*                  eval      EPPOS = EPPOS + 10
     C                   move      *ZEROS        EPPOS
     C                   eval      EPKUND= EHKUND
     C                   eval      EPBES = EHBES
     C                   eval      EPLIN = *ZEROS
     C                   eval      EPULIN= *ZEROS
     C                   write     EPOPOSR
      *
     C     HEADFK        CHAIN     HEADF                              33
     C                   IF        *IN33 = *OFF                                   3<-B
     C                   ADD(H)    EHANTK        HENT
     C                   ADD(H)    EHVKT         HEVKT
     C     HEM3          ADD(H)    EHM3          BigNum           19 9
     C     BigNum        ifgt      9999.999                                        4<-B
     C                   z-add     9999.999      HEM3
     C                   else                                                      4
     C                   z-add     BigNum        HEM3
     C                   endif                                                     4<-E
     C                   UPDATE    HEADFR
     C     HEPRO         CHAIN     TURER14                            33
     C                   IF        *IN33 = *OFF                                    4<-B
     C                   ADD(H)    EHANTK        TUTS
     C                   ADD(H)    EHVKT         TUTVKT
     C     TUTM3         ADD(H)    EHM3          BigNum
S01  C*    BigNum        ifgt      999.99                                           5<-B
S01  C*                  z-add     999.99        TUTM3
N01  C     BigNum        ifgt      9999.999                                         5<-B
N01  C                   z-add     9999.999      TUTM3
     C                   else                                                       5
     C                   z-add     BigNum        TUTM3
     C                   endif                                                      5<-E
     C                   UPDATE    TURERR
     C                   END                                                       4<-E
     C                   END                                                      3<-E
      *
     C                   END                                                     2<-E
     C                   END                                                    1<-E
      *
N02  C     SLPLPO        TAG
     C                   ENDSR
      *
      *=====================================================================******
      *  PLUKK PO-LINE
      *=====================================================================******
     C     SRPLLIN       begsr
      *
     C                   MOVE      'J'           XALLPLK           1
     C     EPOHK         SETLL     EPOL
     C     EPOHK         READE(N)  EPOL                                   33
     C                   DOW       *IN33 = *OFF                                 1<-B
     C                   IF        ELAVD = 0 AND                                 2<-B
     C                             (EPLIN <> ELLIN OR EPULIN <> ELULIN OR
     C                             EPSTA3 <> ELSTA3)
     C                   MOVE      'N'           XALLPLK
     C                   LEAVE
     C                   END                                                     2<-E
     C     EPOHK         READE(N)  EPOL                                   33
     C                   ENDDO                                                  1<-E
      * PLUKK LINJE
     C     EPOLK         CHAIN     EPOL                               33
     C                   DOW       *IN33 = *OFF                                 1<-B
     C                   IF        ELSTA3 <> 'R'                                 2<-B
     C                   LEAVE
     C                   END                                                     2<-E
     C                   UPDATE    EPOLR
     C     EPOLK         READE     EPOL                                   33
     C                   ENDDO                                                  1<-E
      *
     C                   IF        *IN33 = *OFF                                 1<-B
     C                   IF        ELAVD = 0 AND EHAVD = 0                       2<-B
     C                   MOVE      UAVD          ELAVD
     C                   MOVE      UOPD          ELOPD
     C     HEPRO         CHAIN(N)  TURER14                            33
     C                   EVAL      ELSTA = '2'                                  Booked
     c                   IF        TUTST4= 'X'                                    3<-B
     C                   EVAL      ELSTA = '3'                                  Confirmed
     c                   endif                                                    3<-E
     C                   EVAL      ELSTA2= 'S'                                  SEND
     C                   UPDATE    EPOLR
      * SJEKK OM ALLE LINJER ER PLUKKET. I SÅ FALL SETT 9999/9999999 PÅ EPOH
     C                   IF        XALLPLK = 'J'                                  3<-B
     C     EPOHK         CHAIN     EPOH                               33
     C                   IF        *IN33 = *OFF                                    4<-B
     C                   EVAL      EHAVD = 9999
     C                   EVAL      EHOPD = 9999999
     C                   UPDATE    EPOHR
     C                   END                                                       4<-E
     C                   END                                                      3<-E
      * T&T
     C                   EVAL      ETKUND = ELKUND
     C                   EVAL      ETBES  = ELBES
     C                   EVAL      ETLIN  = ELLIN
     C                   EVAL      ETULIN = ELULIN
     C                   EVAL      ETACTI = 'PIC'
     C                   EVAL      ETTEXT = 'PO line is picked up to order '
     C                             + %TRIM(%EDITC(ELAVD:'Z')) + '/'
     C                             + %TRIM(%EDITC(ELOPD:'Z'))
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVE      WDT           ETDATE
     C                   MOVE      WTM           ETTIME
     C                   MOVE      UUSER         ETUSER
     C                   WRITE     EPOTTR
      * Skriv x-ref ubetinget - sats 100% på at det fjernes ved evt remove fra oppdrag
      * finn høyeste eksisterende POSnr - add 10
     c                   move      *zeros        EPPOS
     c     PosMax        setgt     EPOPOS
     c     HEADFK        readpe    EPOPOS
     C                   eval      EPAVD = ELAVD
     C                   eval      EPOPD = ELOPD
      * STUF.SEKVENSNR PARAMETERSTYRES SENERE ?
     C*                  eval      EPPOS = EPPOS + 10
     C                   move      *ZEROS        EPPOS
     C                   eval      EPKUND= ELKUND
     C                   eval      EPBES = ELBES
     C                   eval      EPLIN = ELLIN
     C                   eval      EPULIN= ELULIN
     C                   write     EPOPOSR
      *
     C     HEADFK        CHAIN     HEADF                              33
     C                   IF        *IN33 = *OFF                                   3<-B
     C                   ADD(H)    ELNTSL        HENT
     C                   ADD(H)    ELVKTL        HEVKT
     c     HEM3          ADD(H)    ELM3L         BigNum
     c     BigNum        ifgt      9999.999                                        4<-B
     c                   z-add     9999.999      HEM3
     c                   else                                                      4
     c                   z-add     BigNum        HEM3
     c                   endif                                                     4<-E
     C                   UPDATE    HEADFR
     C     HEPRO         CHAIN     TURER14                            33
     C                   IF        *IN33 = *OFF                                    4<-B
     C                   ADD(H)    ELNTSL        TUTS
     C                   ADD(H)    ELVKTL        TUTVKT
     c     TUTM3         ADD(H)    ELM3L         BigNum
S01  c*    BigNum        ifgt      999.99                                           5<-B
S01  c*                  z-add     999.99        TUTM3
N01  c     BigNum        ifgt      9999.999                                         5<-B
N01  c                   z-add     9999.999      TUTM3
     c                   else                                                       5
     c                   z-add     BigNum        TUTM3
     c                   endif                                                      5<-E
     C                   UPDATE    TURERR
     C                   END                                                       4<-E
     C                   END                                                      3<-E
     C                   ELSE                                                    2
     C                   EVAL      UFEILPLK = 'Y'
     C                   IF        EHAVD <> 0                                     3<-B
     C                   EVAL      UFEILPLKT = 'PO (' + %TRIM(EPBES)
     C                   IF        EHAVD > 0                                       4<-B
     C                   EVAL      UFEILPLKT = %TRIM(UFEILPLKT)
     C                             + ') is already picked up by another user.'
     C                   ELSE                                                      4
     C                   EVAL      UFEILPLKT = %TRIM(UFEILPLKT)
     C                             + ') is already deleted by another user.'
     C                   END                                                       4<-E
     C                   ELSE                                                     3
     C                   EVAL      UFEILPLKT = 'PO-line (' + %TRIM(EPBES)
     C                             + '/' + %TRIM(%EDITC(EPLIN:'Z'))
     C                             + '/' + %TRIM(%EDITC(EPULIN:'Z'))
     C                   IF        ELAVD > 0                                       4<-B
     C                   EVAL      UFEILPLKT = %TRIM(UFEILPLKT)
     C                             + ') is already picked up by another user.'
     C                   ELSE                                                      4
     C                   EVAL      UFEILPLKT = %TRIM(UFEILPLKT)
     C                             + ') is already deleted by another user.'
     C                   END                                                       4<-E
     C                   END                                                      3<-E
      *
     C                   END                                                     2<-E
     C                   END                                                    1<-E
      *
     C                   ENDSR
      *
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
     C     *ENTRY        PLIST
     C                   PARM                    UAVD              4 0
     C                   PARM                    UOPD              7 0
     C                   PARM                    UUSER            10
     C                   PARM                    EPKUND            8 0
     C                   PARM                    EPBES            10
     C                   PARM                    EPLIN             7 0
     C                   PARM                    EPULIN            5 0
     C                   PARM                    EPSTA3            1
     C                   PARM                    UFEILPLK          1
     C                   PARM                    UFEILPLKT       100
      *
     C     HEADFK        klist
     C                   kfld                    UAVD
     C                   kfld                    UOPD
     C     EPOHK         klist
     C                   kfld                    EPKUND
     c                   kfld                    EPBES
     C     EPOLK         klist
     C                   kfld                    EPKUND
     C                   kfld                    EPBES
     C                   kfld                    EPLIN
     C                   kfld                    EPULIN
     C     PosMax        klist
     C                   kfld                    UAVD
     C                   kfld                    UOPD
     C                   kfld                    MAX5
     C                   MOVE      99999         MAX5              5 0
      *
     C     HEADFK        CHAIN(N)  HEADF                              33
      *
     C                   endsr
      *
