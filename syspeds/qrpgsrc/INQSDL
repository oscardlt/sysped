      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/inqsdl MODULE(*LIBL/inqsdl *LIBL/INCGI)
CRTPGM*         ACTGRP(CGI) AUT(*USE)
      *
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
      * 01 jov PTF11 Nytt prog
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FKODTERM   IF   E           K DISK    USROPN
     FKODTERLF  IF   E           K DISK    USROPN
     F                                     RENAME(KOTMR:KOTMRLF)
      *********************************************************************
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
     D IfsMultIndicators...
     d                 ds
     D  NoErrors                       n
     D  NameTooLong                    n
     D  NotAccessible                  n
     D  NoFilesUsable                  n
     D  DupSections                    n
     D  FileIsEmpty                    n
      *
      *
     D Lib             s             10
     D Fn              s             10
     D Mbr             s             10
OddEvD OddEven         s             10
      * Client input variables
     D WSVARNV         s             20
     D WSFORMS         s             20
     D WSREFR          s              1
     D REFRESH         s             50
     D SOKKOD          s              1
     D SOKNVN          s             10    varying
     D SOKNVN2         s             20
     D user            s             10
     D MaxV            s              5
     D MaxVis          s              5  0
     D TSTSCAN40       s             35
      * Other variables
      *
     D Spaces          s             12a   inz('&nbsp;&nbsp;')
      *
     D UPC             C                   CONST('ABCDEFGHIJKLMNOPQRST-
     D                                     UVWXYZÆØÅ')
     D LO              C                   CONST('abcdefghijklmnopqrst-
     D                                     uvwxyzæøå')
      *
      *
      /copy syspeds/qrpgsrcpy,prolog3
      *
      * Get program start time for calculating execution time
     C                   time                    timedata1
      * Parse input / cvt to numeric
     C                   exsr      Parse
      * File open / keys
     C                   EXSR      INIT
      * Read output skeleton html member into memory
     C                   exsr      LoadHtml
      * Set section variables
     C                   exsr      SetTop
      * Send section top
     C                   callp     wrtsection('stdtopTab')
     C                   callp     wrtsection('top')
      * Find/send lines
     C                   exsr      LINES
      * Send section bot
      *
     C                   callp     wrtsection('bot')
     C                   callp     wrtsection('botStd')
      * Quit
     C                   exsr      Exit
      *
     C                   eval      *inlr = *on
     C                   return
      *
      *
      *=====================================================================
      * Parse input / cvt to numeric
      *=====================================================================
     C     Parse         begsr
      * Parse variables from QUERY_STRING environment variable
     C                   eval      SOKKOD = zhbgetvar('SOKKOD')
     C                   eval      SOKNVN  = zhbgetvar('SOKNVN')
     C*    LO:UPC        XLATE     SOKNVN        SOKNVN
     C                   eval      WSVARNV = zhbgetvar('WSVARNV')
     C                   IF        WSVARNV = *BLANKS
     C                   EVAL      WSVARNV = 'WSSDL'
     C                   END
     C                   eval      WSFORMS = zhbgetvar('WSFORMS')
     C                   IF        WSFORMS = *BLANKS
     C                   EVAL      WSFORMS = 'forms[0]'
     C                   END
     C                   eval      WSREFR  = zhbgetvar('WSREFR')
     C                   eval      REFRESH = ''
     C                   IF        ((((WSREFR = 'J') OR
     C                                (WSREFR = 'j')) OR
     C                                (WSREFR = 'Y')) OR
     C                                (WSREFR = 'y'))
     C                   eval      REFRESH = 'self.opener.document.'
     C                             + %trim(WSFORMS)
     C                             + '.submit();'
     C                   END
     C                   eval      MaxV    = zhbgetvar('MaxV')
     C                   eval      MaxVis  = c2n2(MaxV)
     c     MaxVis        ifeq      *zeros
     c                   z-add     50            MaxVis
     c                   end
      * Userid.....
     C                   eval      user = zhbgetvar('user')
     C                   endsr
      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   CLOSE     BRIDF
     C                   CLOSE     KODTERM
     C                   CLOSE     KODTERLF
     C                   return
     C                   endsr
      *=====================================================================
      * Read output skeleton html member into memory
      *=====================================================================
     C     LoadHtml      begsr
     C                   IF        XSPRÅK = 'EN'
     C                   eval      IfsMultIndicators = gethtmlifsmult(
     C                             '/syspeddev/html/eSpedTop.html -
     C                             /syspeddev/html/INQSDLEN.html -
     C                             /syspeddev/html/eSpedBot.html':
     C                             '/CGITAG/')
     C                   ELSE
     C                   eval      IfsMultIndicators = gethtmlifsmult(
     C                             '/syspeddev/html/eSpedTop.html -
     C                             /syspeddev/html/INQSDL.html -
     C                             /syspeddev/html/eSpedBot.html':
     C                             '/CGITAG/')
     C                   END
     C                   endsr
      *=====================================================================
      *  Set variable data for section /$top
      *=====================================================================
     C     SetTop        begsr
      *
     C                   IF        XSPRÅK = 'EN'
     C                   callp     updhtmlvar('TITLE':'Search load/unload')
     C                   ELSE
     C                   callp     updhtmlvar('TITLE':'Søk laste/lossested')
     C                   END
      *
BODY C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updHTMLvar('USER':USER)
N02  C                   callp     updHTMLvar('wsvarnv':WSVARNV)
N02  C                   callp     updHTMLvar('wsforms':WSFORMS)
N02  C                   callp     updHTMLvar('wsrefr':WSREFR)
N02  C                   callp     updHTMLvar('refresh':REFRESH)
      *
     C                   endsr
      *
      *=====================================================================
      * Fine lins - send to browser
      *=====================================================================
     C     Lines         begsr
     c                   eval      SOKNVN2   = SOKNVN
     c                   if        soknvn = '*ALL'
     c                             or soknvn = '*All'
     c                             or soknvn = '*all'
     c                   eval      SOKNVN2   = *blanks
     c                   endif
     c                   Move      *zeros        AntVist           5 0
     C     SOKKOD        IFEQ      *BLANKS
     C     SOKNVN        ANDEQ     *BLANKS
     C                   GOTO      UTLES
     C                   END
      *
     C     SOKKOD        IFNE      *BLANKS
     C     NRKEY         setll     KODTERM
     C                   read      KODTERM                                33
     C                   ELSE
     C     NVNKEY        setll     KODTERLF
     C                   read      KODTERLF                               33
     C                   END
     C     *in33         DOWEQ     *OFF
     C                   exsr      exclude
     c     EXCL          IFEQ      'N'
     C* klargjøre HTMLvariable - SEND ROW
     C                   callp     updHTMLvar('KOTMKO':KOTMKO)
     C                   callp     updHTMLvar('KOTMNV':KOTMNV)
     C                   callp     wrtsection('row')
     c                   Add       1             AntVist           5 0
     c     AntVist       cabge     MaxVis        UtLes
     C                   END
     C     SOKKOD        IFNE      *BLANKS
     C                   read      KODTERM                                33
     C                   ELSE
     C                   read      KODTERLF                               33
     C                   END
     C                   ENDDO
      *
     C     UTLES         TAG
      *
      * Compute run time
     C                   time                    timedata2
     C     timedata2     subdur    timedata1     ms:*ms
     C                   eval      sec = ms / 1000000
     C                   callp     updhtmlvar('runtime':
     C                             %trim(%editw(sec:'     0 .   ')))
      *
     C                   endsr
      *=====================================================================******
      *  Eksluder post basert på...
      *=====================================================================******
     C     EXCLUDE       begsr
     C                   Move      'J'           EXCL              1
      * diverse tester - om ikke bestått hopp ut (med J i EXCL)
      * .... Ingen slike her..
      * Posten skal med på liste
     C                   Move      'N'           EXCL              1
      *
     C     UtExcl        endsr
      *
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
     C                   OPEN      BRIDF
     C     USER          CHAIN     BRIDF                              50
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
      *
OddEv *      Odd/Even/Rowhead-farger,Logobilde,Språk,Intern/Ekstern bruker,  mm
OddEvc                   call      'ESGETPAR'
OddEvc                   parm                    BIBRID
OddEvc                   parm                    BIFIRM
OddEvc                   parm                    BIKUNR
OddEvc                   parm                    BIKNG
OddEvc                   parm                    RowHead          10
OddEvc                   parm                    Odd              10
OddEvc                   parm                    Even             10
OddEvc                   parm                    LogoImg          50
OddEvc                   parm                    XSPRÅK            2
OddEvc                   parm                    XINTERN           1
OddEvc                   parm                    ParmDS1        1024
OddEvc                   parm                    ParmDS2        1024
OddEvc                   parm                    ParmDS3        1024
      *
     C                   OPEN      KODTERM
     C                   OPEN      KODTERLF
      * Key for KODTERM
     C     NRkey         KLIST
     C                   KFLD                    KOTMUN
     C                   KFLD                    SOKKOD
     C                   movel     'TERM'        KOTMUN
      * Key for KODTERM
     C     NVNKEY        KLIST
     C                   KFLD                    SOKNVN2
      *
      *
     C                   endsr
