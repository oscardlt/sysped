      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/ESTK14 MODULE(*LIBL/ESTK14 *LIBL/INCGI)
CRTPGM*         ACTGRP(*NEW) AUT(*USE)
      *
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
********************************************************************
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FTURER2    IF   E           K DISK    USROPN
      *********************************************************************
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      /copy syspeds/qrpgsrcpy,variables3
      *
      * Client input variables
     D user            s             10
     D UnitID          s             10
     D ParcelID        s             11
N02  D CallBack        s           1000
      *
     D LinTxt          s           1000
     D ErrTxt          s            100
     D Spaces          s             12a   inz('&nbsp;&nbsp;')
     D ItemCount       s             10i 0
     D i               s             10i 0
     D                 DS
     D  AvdOpd                 1     11
     D   Avd                   1      4
     D   Opd                   5     11
      /copy syspeds/qrpgsrcpy,prolog3
      *
      * Get program start time for calculating execution time
     C                   time                    timedata1
      * Parse input / cvt to numeric
     C                   exsr      Parse
      * File open / keys
     C                   EXSR      INIT
      * Read output skeleton html member into memory
     C                   exsr      LoadHtml
      * Set section variables
     C                   exsr      SetAll
      * Quit
     C                   exsr      Exit
      *
     C                   eval      *inlr = *on
     C                   return
      *
      *
      *=====================================================================
      * Parse input / cvt to numeric
      *=====================================================================
     C     Parse         begsr
      * Parse variables from QUERY_STRING environment variable
     C                   eval      user = zhbgetvar('user')
     C                   eval      UnitID=zhbgetvar('UnitID')
     C                   eval      ParcelID=zhbgetvar('ParcelID')
     c                   move      ParcelID      AvdOpd
N02  C                   eval      CallBack = zhbgetvar('CallBack')
     C                   endsr
      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   CLOSE     BRIDF
     C                   CLOSE     TURER2
     C                   endsr
      *=====================================================================
      * Read output skeleton html member into memory
      *=====================================================================
     C     LoadHtml      begsr
N01  C                   callp     gethtml('HTMLSRC':
N01  C                             '*LIBL':
N01  C                             'ESTK11G':'/CGITAG/')
     C                   endsr
      *=====================================================================
      *  Set variable data for section /$top , lin , Bot
      *=====================================================================
     C     SetAll        begsr
      * Send section top
     C                   callp     wrtsection('top')
      *
      * cross domain oppkall ??
N02  c     CallBack      ifne      *blanks
N02  c                   eval      LinTxt=%trim(CallBack)+'('
N02  C                   callp     updHTMLvar('LINTXT':Lintxt)
N02  C                   callp     wrtsection('lin')
N02  C                   end
     c                   move      *blanks       ErrTxt
      * Bilen/Bilnummer må ha ÅPEN tur (ved flere plasseres gods på den siste )
     c     BilKey        setgt     TURER2
     c     BilKey        readpe    TURER2                                 33
     c     *in33         ifeq      '1'
     c                   eval      ErrTxt='Det finnes ingen tur med dagens dato'
     c                   else
     c                   move      avd           avdN              4 0
     c                   move      opd           opdN              7 0
     c                   call      'SYGE21'
     c                   parm                    avdN
     c                   parm                    opdN
     c                   parm                    TUPRO
     c                   parm                    IN30              1
     c     In30          ifeq      '1'
     c                   eval      ErrTxt='Det lyktes ikke å sette oppdrag '
     c*                            +ParcelID + ' på bil ' + UnitID
     c                             +%trim(%editc(AVDN:'Z'))+'/'
     c                             +%trim(%editc(OPDN:'Z'))
     c                             + ' på bil ' + UnitID
     c                             + ' / tur ' +%trim(%editc(TUPRO:'Z'))
     c                   endif
     c                   endif
      * Resultat
     c     ErrTxt        ifeq      *blanks
N02  c                   eval      LinTxt='{"status": "OK", '
     c                             +'"msg": "Oppdraget vellykket lagt '
     c                             +'på tur '+%trim(%editc(TUPRO:'Z'))+'"}'
     c                   else
N02  c                   eval      LinTxt='{"status": "ERROR", '
     c                             +'"msg": "'+%trim(errTxt)+'"}'
     C                   END
N02  C                   callp     updHTMLvar('LINTXT':Lintxt)
     C                   callp     wrtsection('lin')
      * cross domain oppkall ??
N02  c     CallBack      ifne      *blanks
N02  C                   callp     updHTMLvar('LINTXT':')')
N02  C                   callp     wrtsection('lin')
N02  C                   end
     C
     C                   endsr
     C
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
     C                   OPEN      BRIDF
     C     USER          CHAIN     BRIDF                              50
      * Ugyldig userID
NUSR c     *in50         ifeq      '1'
NUSR C                   callp     gethtml('HTMLSRC':'*LIBL':'ERRUSR':
NUSR c                             '/CGITAG/')
NUSR C                   callp     updhtmlvar('User':USER)
NUSR C                   callp     wrtsection('all')
NUSR C                   callp     wrtsection('*fini')
NUSR C                   close     BRIDF
NUSR C                   eval      *inlr = *on
NUSR C                   return
NUSR c                   endif
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
     C                   OPEN      Turer2
     c     BilKey        Klist
     c                   kfld                    UnitID
     c                   kfld                    Idag
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   move      WDT           Idag              8 0
     C     UtInit        endsr
