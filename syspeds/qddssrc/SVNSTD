      *__________________________________
      * SYSPED / Tolldekl. TRANSITT
      *""""""""""""""""""""""""""""""""""
     A                                      REF(SR)
     A          R SVNSTDR
     A            TIST      R               REFFLD(ST)
      * THST = ' ' --> ÅPEN TRANSITTERING
      *        'A' --> HOLD PÅ Å SENDE UT
      *        'B' --> SENDT TIL IBM
      *        'C' --> SENDT TIL TOLLVESENET
      *        'D' --> KANSELLERT
      *        'E' --> HOLD PÅ Å BLI ENDRET
      *        'F' --> MELDING OM INGEN FRIGIVELSE
      *        'K' --> VENT PÅ Å BLI UTSKREVET
      *        'M' --> FEILMELDING FRA TOLLVESENET
      *        'P' --> UTSKREVET
      *        'S' --> MELDING OM AVVIST FRIGIVELSE
      *        'U' --> HAR FÅTT MRN-NUMMER
      *        'V' --> VAREKONTROLL
     A            TIAVD          4S 0       COLHDG('AVDELING')
     A            TITDN          7S 0       COLHDG('OPPDRAGSNR')
     A            TISG      R               REFFLD(SG)
     A            TIDT      R               REFFLD(DT)
     A            TIENKL         1A         COLHDG('FORENKLET PROSEDYRE')
     A            TITRNR        18A         COLHDG('MRN-NR')
     A            TIGN          35A         COLHDG('GODSNR')
     A            TIGNSK         2A         COLHDG('SPRÅKKODE GODSNR')
     A            TIALSK        17A         COLHDG('KODE AVT. LAG.STED')
     A            TIALS         35A         COLHDG('AVTALT LAGRINGSSTED')
     A            TIALSS         2A         COLHDG('SPRÅKKODE AVT.LAG.ST')
     A            TIGLSK        17A         COLHDG('KODE GODKJ.LAG.STED')
     A            TIACTS        17A         COLHDG('KODE AVT.CONTR.STED')
     A            TISKB          2A         COLHDG('SPRÅKKODE BEST.STED')
     A            TITSB          8A         COLHDG('FREMLEG. TOLLSTED')
     A            TITARF        16          COLHDG('TARIFFØRSNAVN')
     A            TIWS      R               REFFLD(WS)
      * NÄRINGSLIV
     A            TIKN      R               REFFLD(KN)
     A                                      COLHDG('Kundenr')
     A            TINA      R               REFFLD(NA)
     A                                      COLHDG('Navn')
     A            TIAD1     R               REFFLD(AD)
     A                                      COLHDG('Adresse-1')
     A            TIPN           9A         COLHDG('POSTNR')
     A            TIPS          24A         COLHDG('POSTSTED')
     A            TILK           2A         COLHDG('LANDKODE')
     A            TISK           2A         COLHDG('SPRÅKKODE')
     A            TITIN         17A         COLHDG('TIN-KODE')
      * TVINN - VARIABLER
     A            S0004     R               REFFLD(N0004)
     A            S0010     R               REFFLD(N0010)
     A            S0035     R               REFFLD(N0035)
     A            S0026     R               REFFLD(N0026)
      *
      *================================================================
      * FRA IE43 LOSSETILLATELSE
      *================================================================
     A            TIDK           5A         COLHDG('DEKLARASJONSKODE')
      * HOVEDANSVARLIG
     A            TIKNA     R               REFFLD(KN)
     A                                      COLHDG('HOVEDANSVARLIG')
     A            TINAA     R               REFFLD(NA)
     A                                      COLHDG('Navn ANSVARLIG')
     A            TIADA1    R               REFFLD(AD)
     A                                      COLHDG('Adresse-1 ANSVARLIG')
     A            TIPNA          9A         COLHDG('POSTNR ANSVARLIG')
     A            TIPSA         24A         COLHDG('POSTSTED ANSVARLIG')
     A            TILKA          2A         COLHDG('LANDKODE ANSVARLIG')
     A            TISKA          2A         COLHDG('SPRÅKKODE ANSVARLIG')
     A            TITINA        17A         COLHDG('TIN-KODE ANSVARLIG')
      * SELGER
     A            TIKNS     R               REFFLD(KN)
     A                                      COLHDG('Kundenr Selger')
     A            TINAS     R               REFFLD(NA)
     A                                      COLHDG('Navn Selger')
     A            TIADS1    R               REFFLD(AD)
     A                                      COLHDG('Adresse-1 Selger')
     A            TIPNS          9A         COLHDG('POSTNR SELGER')
     A            TIPSS         24A         COLHDG('POSTSTED SELGER')
     A            TILKS          2A         COLHDG('LANDKODE SELGER')
     A            TISKS          2A         COLHDG('SPRÅKKODE SELGER')
     A            TITINS        17A         COLHDG('TIN-KODE SELGER')
      * KJØPER
     A            TIKNK     R               REFFLD(KN)
     A                                      COLHDG('Kundenr Kjøper')
     A            TINAK     R               REFFLD(NA)
     A                                      COLHDG('Navn Kjøper')
     A            TIADK1    R               REFFLD(AD)
     A                                      COLHDG('Adresse-1 Kjøper')
     A            TIPNK          9A         COLHDG('POSTNR KJØPER')
     A            TIPSK         24A         COLHDG('POSTSTED KJØPER')
     A            TILKK          2A         COLHDG('LANDKODE KJØPER')
     A            TISKK          2A         COLHDG('SPRÅKKODE KJØPER')
     A            TITINK        17A         COLHDG('TIN-KODE KJØPER')
      * BESTEMMELSES LAND  RUBR. 17A
     A            TIBLK     R               REFFLD(LKA)
     A                                      COLHDG('LANDK TIL B.LAND')
      * AVSENDELAND  RUBR. 15A
     A            TIALK     R               REFFLD(LKA)
     A                                      COLHDG('LANDK TIL A.LAND')
      * TRANSPORTMIDDEL VED AVGANG  RUBR. 18
     A            TITAID        27A         COLHDG('IDENTITET')
     A            TITASK         2A         COLHDG('SPRÅK')
     A            TITALK    R               REFFLD(LKA)
     A                                      COLHDG('Landk Tr.middel')
      * CONTAINER  RUBR. 19
     A            TIKDC     R               REFFLD(KDC)
      *
     A            TITRDT         8S 0       COLHDG('GODKJENNINGSDATO')
     A            TILSTL         4S 0       COLHDG('ANTALL LASTELISTE')
     A            TIVPOS         5S 0       COLHDG('VAREPOST')
     A            TINTK     R               REFFLD(NTK)
     A            TIVKB         11S 3       COLHDG('Bruttovekt')
     A            TICATS         8A         COLHDG('AVGANGS TOLLSTED')
      * CTL. KOONTROLL
     A            TICTL          1A         COLHDG('FORTSETTE LOSSING')
      * FORSEGLING
     A            TIDANT         4S 0       COLHDG('ANTALL FORSEGLING')
     A            TIDFKD        20A         COLHDG('FORSEGLING-ID KODE')
     A            TIDFSK         2A         COLHDG('FORSEGLING-ID SPRÅK')
      *================================================================
      * EKSTRA FELTER FOR SVENSK NCTS
     A            TITUID        14A         COLHDG('TULLID')
     A            TISGDK         6A         COLHDG('DOKUMENTSIGILL')
     A            TISGME         3A         COLHDG('SIGILLMETODE')
     A            TISGUT         6A         COLHDG('UTSTÆLLARSIGILL')
     A            TISGID        10A         COLHDG('ID PÅ UTSTÆLLARE')
     A            TISGDT        12A         COLHDG('DAUM FØR SIGILL')
     A            TIBYTE         7S 0       COLHDG('ANTALL BYTE EDI')
      *
     A          K TIAVD
     A          K TITDN
