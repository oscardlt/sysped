      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/SYXML35 MODULE(*LIBL/SYXML35 *LIBL/INCGI)
CRTPGM*        ACTGRP(CGI) AUT(*USE)
      *
      *********************************************************************
      /copy SYSPEDS/qrpgsrcpy,hspecs
      /copy SYSPEDS/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FFIRM      IF   E           K DISK    USROPN
     FCUNDL01   IF   E           K DISK    USROPN
     FCUNDL02   IF   E           K DISK    USROPN
     F                                     RENAME(KUNDR:KUNDR2)
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy SYSPEDS/qrpgsrcpy,prototypeb
      /copy SYSPEDS/qrpgsrcpy,usec
      /copy SYSPEDS/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
     D WSVARNV         S             20
     D SOKKNR          S              8
     D SOKKNRN         S              8  0
     D SOKNVN          S             10    varying
     D SOKNVN2         S             10
     D KunPnSted       S             10    varying
     D USER            S             10
     D MaxV            S              5
     D MaxVis          S              5  0
     D TSTSCAN40       S             35
     D UPC             C                   CONST('ABCDEFGHIJKLMNOPQRST-
     D                                     UVWXYZÆØÅ')
     D LO              C                   CONST('abcdefghijklmnopqrst-
     D                                     uvwxyzæøå')
     D Error           S            150
     D JSONstring      s          32000

      *get input-string
      /copy syspeds/qrpgsrcpy,prolog3

     C                   EXSR      PARSE
     C                   EXSR      INIT

     C                   CALLP     gethtmlifs(
     C                             '/syspeddev/html/JSON.html':
     C                             '/CGITAG/')
     C                   CALLP     wrtsection('top')
      *
      * ............................................................................................
      * skriv JSON-Object start..(alltid '{' + x ant param. m/komma mellom (IKKE komma etter siste))
      * ............................................................................................
      *
      /free
        JSONstring='{'
        +'¬user¬ : ¬'+%trim(USER)+'¬, '
        +'¬sokknr¬ : ¬'+%trim(SOKKNR)+'¬, '
        +'¬soknvn¬ : ¬'+%trim(SOKNVN)+'¬, '
        +'¬kunpnsted¬ : ¬'+%trim(KUNPNSTED)+'¬, '
        +'¬wsvarnv¬ : ¬'+%trim(WSVARNV)+'¬, '
        +'¬maxv¬ : ¬'+%trim(MAXV)+'¬, '
        +'¬errMsg¬ : ¬'+%trim(Error)+'¬, ';
      /end-free
      * JSONfix escaper " i tekst,  for deretter å bytte ¬->"
     c                   call      'JSONFIX'
     c                   parm                    JSONstring
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      *
      * ............................................................................................
      * Skriv JSON-LISTE Start (en liste er EN parameter(fra [ til   )
      * ............................................................................................
     c                   eval      JSONstring ='"inqcustomer" : ['
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      *
     C     ERROR         IFEQ      *blanks
     C                   EXSR      LINES
     C                   ENDIF
      * Skriv JSON-Liste/Array  Avslutning
     C                   EVAL      JSONstring =']'
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%ADDr(JSONstring)
     C                                         :%len(JSONstring))
     C                   CALLP     wrtsection('JSONlin')
      * Skriv JSON-string Avsluttning
     C                   EVAL      JSONstring ='}'
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%ADDr(JSONstring)
     C                                         :%len(JSONstring))
     C                   CALLP     wrtsection('JSONlin')

     C                   EXSR      EXIT
      *=====================================================================
      * Parse input / cvt to numeric
      *=====================================================================
     C     PARSE         BEGSR
     C                   EVAL      USER    = zhbgetvar('USER')
     C                   EVAL      SOKKNR  = zhbgetvar('SOKKNR')
     C                   EVAL      SOKKNRN = c2n2(SOKKNR)
     C                   EVAL      SOKNVN  = zhbgetvar('SOKNVN')
     C     LO:UPC        XLATE     SOKNVN        SOKNVN
     C                   EVAL      KunPnSted = zhbgetvar('KunPnSted')
     C                   EVAL      KunPnSted = uppify(KunPnSted)
     C                   EVAL      WSVARNV = zhbgetvar('WSVARNV')
     C                   IF        WSVARNV = *BLANKS
     C                   EVAL      WSVARNV = 'WSKNS'
     C                   END
     C                   EVAL      MaxV    = zhbgetvar('MaxV')
     C                   EVAL      MaxVis  = c2n2(MaxV)
     C     MaxVis        IFEQ      *ZEROS
     C                   Z-ADD     50            MaxVis
     C                   ENDIF
     C                   ENDSR
      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          BEGSR
     C                   callp     wrtsection('*fini')
     C                   CLOSE     BRIDF
     C  N50              CLOSE     FIRM
     C  N50              CLOSE     CUNDL01
     C  N50              CLOSE     CUNDL02
     C                   MOVE      *ON           *INLR
     C                   RETURN
     C                   ENDSR
      *=====================================================================
      * Fine lins - send to browser
      *=====================================================================
     C     Lines         BEGSR
     C                   EVAL      SOKNVN2   = SOKNVN
     C                   MOVE      *ZEROS        AntVist           5 0
     C     SOKKNRN       IFEQ      *ZEROS
     C     SOKNVN        ANDEQ     *BLANKS
     C                   GOTO      UTLES
     C                   ENDIF

     C     SOKKNRN       IFNE      *ZEROS
     C     KNRKEY        SETLL     CUNDL01
     C*    BIFIRM        READE     CUNDL01                                33
     C     KNRKEY        READE     CUNDL01                                33
     C                   ELSE
     C     NVNKEY        SETLL     CUNDL02
     C     BIFIRM        READE     CUNDL02                                33
     C     SOKNVN        SCAN      SONAVN                                 34
     C     *IN34         CABEQ     *OFF          UtLes
     C                   ENDIF

     C     *IN33         DOWEQ     *OFF
     C                   EXSR      EXCLUDE
      * klargjøre HTMLvariable - SEND ROW
     C     EXCL          IFEQ      'N'
     c                   if        SYLAND = *blanks
     c                   eval      SYLAND = FILAND
     c                   endif
     C                   MOVEL     *BLANKS       ADRESSE          35
     C     SYPOGE        IFNE      *BLANKS
     C                   EVAL      ADRESSE =  SYPOGE
     C                   ELSE
     C                   MOVEL     POSTNR        POSTNRA           4
     C                   EVAL      ADRESSE =  POSTNRA
     C                   ENDIF
     C                   CAT       ADR3:1        ADRESSE
     C*                  CAT       SYLAND:2      ADRESSE
     c                   IF        FMOT = 0
     c                   eval      FMOT = KUNDNR
     c                   endif

     C                   ADD       1             AntVist           5 0
      /free
       if AntVist=1;
          JSONstring=*blanks;
          else;
          JSONstring=', ';
          endif;
       JSONstring=%trim(JSONstring)+'{'
          +'¬kundnr¬ : ¬'+%trim(%editc(KUNDNR:'3'))+'¬, '
          +'¬navn¬ : ¬'+%trim(KNAVN)+'¬, '
          +'¬adr1¬ : ¬'+%trim(ADR1)+'¬, '
          +'¬adr2¬ : ¬'+%trim(ADR2)+'¬, '
          +'¬adresse¬ : ¬'+%trim(ADRESSE)+'¬,'
          +'¬land¬ : ¬'+%trim(SYLAND)+'¬,'
          +'¬fakknr¬ : ¬'+%trim(%editc(FMOT:'3'))+'¬}';
      /end-free
     C                   call      'JSONFIX'
     C                   parm                    JSONstring
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')

     C     AntVist       CABGE     MaxVis        UtLes
     C                   ENDIF

     C     SOKKNRN       IFNE      *ZEROS
     C*    BIFIRM        READE     CUNDL01                                33
     C     KNRKEY        READE     CUNDL01                                33
     C                   ELSE
     C     BIFIRM        READE     CUNDL02                                33
     C     SOKNVN        SCAN      SONAVN                                 34
     C     *IN34         CABEQ     *OFF          UTLES
     C                   ENDIF
     C                   ENDDO

     C     UTLES         TAG

     C                   ENDSR
      *=====================================================================******
      *  Eksluder post basert på...
      *=====================================================================******
     C     EXCLUDE       BEGSR
     C                   MOVE      'J'           EXCL              1
      * Diverse tester - om ikke bestått hopp ut (med J i EXCL)
     C     KunPnSted     IFNE      *BLANKS
     C     SYLAND        IFEQ      *BLANKS
     C                   MOVE      'NO'          SYLAND
     C                   ENDIF
     C     SYPOGE        IFEQ      *BLANKS
     C                   MOVEL     POSTNR        SYPOGE
     C                   ENDIF
     C                   EVAL      TSTSCAN40  = SYLAND+%trim(SYPOGE)+ADR3
     C                   EVAL      TSTSCAN40  = uppify(TSTSCAN40)
     C     KunPnSted     SCAN      TSTSCAN40                              33
     C     *IN33         CaBEQ     *OFF          UtExcl
     C                   ENDIF
      * Posten skal med på liste
     C                   MOVE      'N'           EXCL              1

     C     UtExcl        ENDSR
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          BEGSR
     C                   OPEN      BRIDF
     C     USER          CHAIN     BRIDF                              50
      * En "standardvariant" libl: CALL bound (INCGI=*MODULE) med parameter.
     C  N50BILIBL        IFEQ      *BLANKS
     C                   CALLB     'INCGI'
     C                   PARM                    BISTDL
     C                   else
      * Helt egen bibl.liste satt på USER - normal CALL (BILIBL er "*pgm")
     C                   CALL      BILIBL
     C                   ENDIF

     C   50              eval      Error = 'Invalid userID'
     C  N50              OPEN      FIRM
     C  N50              OPEN      CUNDL01
     C  N50              OPEN      CUNDL02
      * Key for CUNDL01
     C     KNRKEY        KLIST
     C                   KFLD                    BIFIRM
     C                   KFLD                    SOKKNRN
      * Key for CUNDL01
     C     NVNKEY        KLIST
     C                   KFLD                    BIFIRM
     C                   KFLD                    SOKNVN2
     C                   IF        *IN50=*OFF
     C     *loval        setll     FIRM
     C                   READ      FIRM
     C                   ENDIF

     C                   ENDSR
