      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/STS624 MODULE(*LIBL/STS624 *LIBL/INCGI)
CRTPGM*         ACTGRP(*NEW) AUT(*USE)
      *
      *********************************************************************
      *
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
      *--------------------------------------------------------------------
      * Data base files
      *--------------------------------------------------------------------
     FBRIDF     IF   E           K DISK    usropn
     FGSSOLL05  UF   E           K DISK    usropn
     FGSSORF    IF   E           K DISK    usropn
     FGSSLGF    O  A E             DISK    usropn
      *--------------------------------------------------------------------
      * Prototype defintions and standard system API error structure
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      *--------------------------------------------------------------------
      * Variables common to CGI programs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
      * Variables received from the input string
     D lng             s              2
     D BckGnd          s             10
     D XXUSER          S             10
     D XXLSTED         S              5
     D XXANTLK         S              7
     D XXANTLK2        S              7  0
     D FEILKODE        s              1
     D lstbrd          s              1
      * andre variabler
     D Fn              s             10
     D Lib             s             10
     D Mbr             s             10
     D i               s             10i 0
      *
     D                 DS
     D  XXLKID20               4     23
     D  XXLKID                 1      9
     D  XXLKID2                4      9
     D                 DS
     D  XXNYLKID               1      9
     D  XXNYPRE                1      3
     D  XXNYLKID2              4      9
      *
      *--------------------------------------------------------------------
      * Prolog common to CGI programs
      * -Receive input from the remote browser
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prolog3
      *=====================================================================******
      * MAIN PROCESS LINE
      *=====================================================================******
      * Parse input string
     C                   EXSR      PARSE
      *
     C                   EXSR      INIT
      *
     C                   SELECT
     C                   WHEN      *IN60
     C                   MOVE      '1'           FEILKODE
     C                   exsr      SetSkl621B
     C                   WHEN      BIINN = ' '
     C                   MOVE      '1'           FEILKODE
     C                   exsr      SetSkl621B
      * Blank, ikke gjør noe..(vis bilde på ny)
     C                   WHEN      XXNYLKID = *blanks
     C                   EXSR      SetSkl624
     C                   OTHER
     C                   EXSR      SRBYTT
     C                   exsr      SetSkl623
     C                   ENDSL
      * Read skeleton output html, etc.
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
      * 1-Section /$top
     C                   exsr      SetTop
     C                   SELECT
     C                   WHEN      *IN60
     C                   callp     wrtsection('all')
     C                   WHEN      BIINN = ' '
     C                   callp     wrtsection('all')
     C                   OTHER
     C                   callp     wrtsection('top')
     C                   ENDSL
      *
     C                   callp     wrtsection('endhtml')
     C                   callp     wrtsection('*fini')
      *
      *
     C                   close     BRIDF
     C                   close     GSSOLL05
     C                   close     GSSORF
     C                   close     GSSLGF
     C                   eval      *inlr = *on
     C                   RETURN
      *
      *=====================================================================******
      * Parse input
      *=====================================================================******
     C     Parse         begsr
      *
     C                   MOVE      *BLANKS       XXLKID20
     C                   eval      XXUSER  = zhbgetvar('USER')
     C                   eval      XXNYLKID  = zhbgetvar('NYLKID')
     C                   eval      XXLSTED = zhbgetvar('LSTED')
     C                   eval      XXLKID  = zhbgetvar('LKID')
     C                   eval      XXANTLK = zhbgetvar('ANTLK')
     C                   EVAL      XXANTLK2 = c2n2(XXANTLK)
      *
     C                   endsr
      *
      *=====================================================================******
      * BYTT LOKASJON
      *=====================================================================******
     C     SRBYTT        begsr
      *
     C     GSSOLL05K     SETLL     GSSOLL05
     C     GSSOLL05K     READE     GSSOLL05                               33
     C                   DOW       *IN33 = *OFF
     C     OLONO         CHAIN     GSSORF                             34
     C* N34ORCNO         COMP      BIKUNR                             3434
     C  N34              IF        ((ORST >= '0') AND (ORST <= '3'))
     C                   EXSR      OPDLOGG
     C                   EVAL      OLLC = XXNYLKID2
     C                   END
     C                   UPDATE    GSSOLFR
     C     GSSOLL05K     READE     GSSOLL05                               33
     C  N33              ENDDO
      *
     C                   endsr
      *
      *=====================================================================
      *  Oppdater loggen
      *=====================================================================
     C     OPDLOGG       begsr
      *
     C                   MOVE      XXUSER        LGUSER
     C                   MOVE      WDT           LGDT
     C                   MOVE      WTM           LGTID
     C                   Z-ADD     1             LGLN
     C                   MOVEL     'STS624'      LGPGM
     C                   Z-ADD     9             LGNIVÅ
     C                   MOVE      WDT           LGHDT
     C                   MOVEL     WTM           LGHTID
     C                   MOVEL     'MAIN'        LGFTYP
     C                   MOVE      OLONO         LGONO
     C                   MOVE      OLLNO         LGLNO
     C                   EVAL      LGTXT = 'BYTT LOKASJON'
     C                   EVAL      LGAGM = 'GAMMEL LOKASJON:'
     C                   CAT       XXLKID2:1     LGAGM
     C                   EVAL      LGANY = 'NY LOKASJON:'
     C                   CAT       XXNYLKID2:1   LGANY
     C                   WRITE     GSSLGFR
      *
     C                   endsr
      *=====================================================================******
      * Set html output skeleton member before its read into memory
      *=====================================================================******
     C     SetSkl621B    begsr
     C                   eval      lng = uppify(lng)
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC' + lng
     C                   eval      Mbr = 'STS621B'
      *
     C                   endsr
      *=====================================================================******
      * Set html output skeleton member before its read into memory
      *=====================================================================******
     C     SetSkl623     begsr
     C                   eval      lng = uppify(lng)
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC' + lng
     C                   eval      Mbr = 'STS623'
      *
     C                   endsr
      *=====================================================================******
      * Set html output skeleton member before its read into memory
      *=====================================================================******
     C     SetSkl624     begsr
     C                   eval      lng = uppify(lng)
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC' + lng
     C                   eval      Mbr = 'STS624'
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$top
      *  (search criteria)
      *=====================================================================******
     C     SetTop        begsr
      * Repeat national language for the next invocation
     C                   callp     updhtmlvar('LNG':lng:
     C                             InitHTMLVars)
      * Repeat background color for the next invocation
     C                   callp     updhtmlvar('BCKGND':bckgnd)
      * Color for text, background, links, visited links, active links
     C                   exsr      setcolor
BODY C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updhtmlvar('USER':XXUSER)
     C                   callp     updhtmlvar('LKID':XXLKID)
     C                   callp     updhtmlvar('LSTED':XXLSTED)
     C                   callp     updHTMLvar('ANTLK':
     C                             %trim(%editc(XXANTLK2:'Z')))
     C                   callp     updHTMLvar('FEILKODE':FEILKODE)
      *
     C                   endsr
      *=====================================================================******
      * Color for font, background, links, visited links, active links
      *=====================================================================******
     C     SetColor      begsr
     C                   eval      BckGnd = uppify(BckGnd)
     C                   callp     updhtmlvar('TXTCOLOR':'black')
     C                   callp     updhtmlvar('BOLD':' ')
      * BGCOLOR white, LINK blue, VLINK red, ALINK green
     C     bckgnd        ifeq      'WHITE'
     C     bckgnd        oreq      *blank
     C                   callp     updhtmlvar('BGCOLOR':'FFFFFF')
     C                   callp     updhtmlvar('LINK':'0000FF')
     C                   callp     updhtmlvar('VLINK':'FF0000')
     C                   callp     updhtmlvar('ALINK':'00FF00')
     C                   endif
      * BGCOLOR gray, LINK blue, VLINK red, ALINK green
     C     bckgnd        ifeq      'GRAY'
     C                   callp     updhtmlvar('BGCOLOR':'CCCCCC')
     C                   callp     updhtmlvar('LINK':'0000FF')
     C                   callp     updhtmlvar('VLINK':'FF0000')
     C                   callp     updhtmlvar('ALINK':'00FF00')
     C                   endif
      * BGCOLOR light blue, LINK blue, VLINK red, ALINK green
     C     bckgnd        ifeq      'LBLUE'
     C                   callp     updhtmlvar('BGCOLOR':'5BE6F3')
     C                   callp     updhtmlvar('LINK':'0000FF')
     C                   callp     updhtmlvar('VLINK':'FF0000')
     C                   callp     updhtmlvar('ALINK':'00FF00')
     C                   endif
      * BGCOLOR black, LINK green, VLINK red, ALINK white
     C     bckgnd        ifeq      'BLACK'
     C                   callp     updhtmlvar('TXTCOLOR':'white')
     C                   callp     updhtmlvar('BGCOLOR':'000000')
     C                   callp     updhtmlvar('LINK':'00FF00')
     C                   callp     updhtmlvar('VLINK':'FF0000')
     C                   callp     updhtmlvar('ALINK':'FFFFFF')
     C                   callp     updhtmlvar('BOLD':'<b>')
     C                   endif
     C                   endsr
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     GSSOLL05K     KLIST
     C                   KFLD                    XXLSTED
     C                   KFLD                    XXLKID20
      *
     C                   open      BRIDF
     C     XXUSER        CHAIN     BRIDF                              60
     C                   IF        *IN60 = *OFF
      * En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   CALLB     'INCGI'
     C                   PARM                    BISTDL
     C                   else
      * Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
      *
     C                   open      GSSOLL05
     C                   open      GSSORF
     C                   open      GSSLGF
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   END
     C                   MOVE      *BLANKS       FEILKODE
      *
     C                   ENDSR
