     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      REF(*LIBL/PALLD)
     A                                      PRINT
     A                                      CF03(03 'Exit')
     A                                      ALTHELP
     A                                      HELP
     A          R NYVARE
     A                                      TEXT('Select')
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A                                      CF12(03)
     A                                      CF06(06 'LAG NY')
     A                                      CF16(16 'STANDARD SØK')
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/SYPA01'
     A                                      COLOR(BLU)
     A                                  1 20MSGCON(045 JOV0582 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 73DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  2  2MSGCON(040 DIV2057 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  2 52'Brukerid:'
     A            WSUSER    R        B  2 62REFFLD(PALLDR/PAUSER *LIBL/PALLD)
     A                                  2 73TIME
     A                                      COLOR(BLU)
     A                                  3  2MSGCON(078 JOV0893 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  1MSGCON(079 JOV0956 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            WSBUNR    R        B  5  4REFFLD(PALLDR/PALØNR *LIBL/PALLD)
     A                                      EDTCDE(4)
     A            WSKVNR    R        B  5 12REFFLD(PALLDR/PAKVNR *LIBL/PALLD)
     A                                      EDTCDE(4)
     A            WSLØNR    R        B  5 22REFFLD(PALLDR/PALØNR *LIBL/PALLD)
     A                                      EDTCDE(4)
     A            WSPPNR    R        B  5 30REFFLD(PALLDR/PAPPNR *LIBL/PALLD)
     A                                      EDTCDE(4)
     A            WSSTAT    R        B  5 73REFFLD(PALLDR/PASTAT *LIBL/PALLD)
     A            WSDATO         6Y 0B  5 75EDTCDE(4)
     A                                 23  2MSGCON(010 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 42MSGCON(018 DIV0022 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 62MSGCON(018 JOV0958 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A            ZMSGTX        79A  O 24  2MSGID(&ZMSGNO SYMSGF)
     A                                      COLOR(WHT)
     A            ZMSGNO         7A  P
     A          R SUBFIL                    SFL
     A                                      TEXT('SUBFILE')
     A            PAAB      R        H
     A            PAKVDT    R        H      REFFLD(PALLDR/PAKVDT *LIBL/PALLD)
     A            WSNR           1A  B  6  2VALUES(' ' '2' '4' '5')
     A            PABUNR    R        O  6  4REFFLD(PALLDR/PABUNR *LIBL/PALLD)
     A                                      EDTCDE(4)
     A  66                                  COLOR(WHT)
     A            PAKVNR    R        O  6 12REFFLD(PALLDR/PAKVNR *LIBL/PALLD)
     A                                      EDTCDE(4)
     A            PALØNR    R        O  6 22REFFLD(PALLDR/PALØNR *LIBL/PALLD)
     A                                      EDTCDE(4)
     A            PAPPNR    R        O  6 30REFFLD(PALLDR/PAPPNR *LIBL/PALLD)
     A                                      EDTCDE(4)
     A            XXNAVN        13A  O  6 39
     A            PAANT     R        O  6 53REFFLD(PALLDR/PAANT *LIBL/PALLD)
     A                                      EDTCDE(L)
     A            PABILN    R        O  6 60REFFLD(PALLDR/PABILN *LIBL/PALLD)
     A            PABEIE         1A  O  6 71
     A            PASTAT    R        O  6 73REFFLD(PALLDR/PASTAT *LIBL/PALLD)
     A            WXKVDT         6Y 0O  6 75
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0012)
     A                                      TEXT('SUBCTL')
     A                                      CF05(05 'Forny')
     A                                      CF06(06 'LAG NY')
     A                                      CF12(12 'Annuller')
     A                                      CF16(16 'STANDARD SØK')
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/SYPA01'
     A                                      COLOR(BLU)
     A                                  1 20MSGCON(045 JOV0582 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            ZRECNO         4S 0O  1 67SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1 73DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  2  2MSGCON(040 DIV2057 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  2 52'Brukerid:'
     A            WSUSER    R        B  2 62REFFLD(PALLDR/PAUSER *LIBL/PALLD)
     A  65                                  DSPATR(RI)
     A                                  2 73TIME
     A                                      COLOR(BLU)
     A                                  3  2MSGCON(078 JOV0893 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  1MSGCON(079 JOV0956 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            WSBUNR    R        B  5  4REFFLD(PALLDR/PALØNR *LIBL/PALLD)
     A                                      EDTCDE(4)
     A            WSKVNR    R        B  5 12REFFLD(PALLDR/PAKVNR *LIBL/PALLD)
     A                                      EDTCDE(4)
     A            WSLØNR    R        B  5 22REFFLD(PALLDR/PALØNR *LIBL/PALLD)
     A                                      EDTCDE(4)
     A            WSPPNR    R        B  5 30REFFLD(PALLDR/PAPPNR *LIBL/PALLD)
     A                                      EDTCDE(4)
     A            WSSTAT    R        B  5 73REFFLD(PALLDR/PASTAT *LIBL/PALLD)
     A            WSDATO         6Y 0B  5 75EDTCDE(4)
     A          R BUNN
     A                                      TEXT('INFO')
     A                                      SLNO(18)
     A                                      CLRL(06)
     A                                      OVERLAY
     A                                  6  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  6 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  6 42MSGCON(018 DIV0022 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  6 62MSGCON(018 JOV0958 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A            ZMSGTX        79A  O  7  2MSGID(&ZMSGNO SYMSGF)
     A                                      COLOR(WHT)
     A            ZMSGNO         7A  P
     A          R VARELI
     A                                      TEXT('INFO')
     A                                      CF04(04 'SØK')
     A                                      CF09(09 'NY BUNT')
     A                                      CF12(12 'Annuller')
     A                                      CF23(23 'BEKREFT LIK')
     A                                      SLNO(19)
     A                                      CLRL(05)
     A                                      OVERLAY
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            ZMSGNO         7A  P
     A                                  1  1MSGCON(070 JOV0835 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  2  1MSGCON(004 JOV0836 *LIBL/SYMSGF)
     A            PALØNR    R        O  2  6REFFLD(PALLDR/PALØNR *LIBL/PALLD)
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A                                      DSPATR(UL)
     A                                  2 14MSGCON(007 JOV0586 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            WAPPNR    R        B  2 22REFFLD(PALLDR/PAPPNR *LIBL/PALLD)
     A  71                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A            WANAVN        10A  B  2 31
     A            WAKVDT         6Y 0B  2 42EDTCDE(4)
     A  72                                  DSPATR(RI)
     A            WAANT     R        B  2 49REFFLD(PALLDR/PAANT *LIBL/PALLD)
     A                                      EDTCDE(L)
     A  73                                  DSPATR(RI)
     A            WATXT     R        B  2 56REFFLD(PALLDR/PATXT *LIBL/PALLD)
     A                                  3  1MSGCON(004 JOV0837 *LIBL/SYMSGF)
     A            WAKVNR         9Y 0B  3  6EDTCDE(4)
     A  76                                  DSPATR(RI)
     A                                  3 14MSGCON(007 JOV0587 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            WBPPNR    R        B  3 22REFFLD(PALLDR/PAPPNR *LIBL/PALLD)
     A  74                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A            WBNAVN        10A  B  3 31
     A            WBANT     R        O  3 49REFFLD(PALLDR/PAANT *LIBL/PALLD)
     A  75                                  DSPATR(RI)
     A                                      EDTCDE(L)
     A            WBTXT     R        B  3 56REFFLD(PALLDR/PATXT *LIBL/PALLD)
     A                                  4  1MSGCON(004 JOV0838 *LIBL/SYMSGF)
     A            WABUNR         7Y 0B  4  6EDTCDE(4)
     A                                      DSPATR(PR)
     A                                  4 22MSGCON(008 JOV0894 *LIBL/SYMSGF)
     A            WABILN    R        B  4 31REFFLD(PALLDR/PABILN *LIBL/PALLD)
     A  77                                  DSPATR(RI)
     A                                  4 42MSGCON(008 JOV0953 *LIBL/SYMSGF)
     A            WSBIAB         1   B  4 51DSPATR(PR)
     A                                  4 53'(A/B)'
     A                                  5 22MSGCON(018 JOV0834 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5 42MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        ZMSGTX        79A  O  6  2MSGID(&ZMSGNO SYMSGF)
     A                                      COLOR(WHT)
     A          R VAREL2
     A                                      TEXT('INFO')
     A                                      CF12(12 'Annuller')
     A  63                                  CF06(06 'Se A-Oppdrag')
     A  63                                  CF07(07 'Se A-Faktura')
     A  64                                  CF08(08 'Se B-Oppdrag')
     A  64                                  CF09(09 'Se B-Faktura')
     A                                      SLNO(16)
     A                                      CLRL(08)
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            PAKVDT    R        H      REFFLD(PALLDR/PAKVDT *LIBL/PALLD)
     A            PADATO    R        H      REFFLD(PALLDR/PADATO *LIBL/PALLD)
     A                                  1  2MSGCON(016 JOV0890 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(008 JOV0908 *LIBL/SYMSGF)
     A            PABUNR    R        O  1 28REFFLD(PALLDR/PABUNR *LIBL/PALLD)
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  1 37MSGCON(008 JOV0891 *LIBL/SYMSGF)
     A            PAKVNR    R        O  1 46REFFLD(PALLDR/PAKVNR *LIBL/PALLD)
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  2 19MSGCON(008 JOV0892 *LIBL/SYMSGF)
     A            PALØNR    R        O  2 28REFFLD(PALLDR/PALØNR *LIBL/PALLD)
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  2 37MSGCON(008 JOV0616 *LIBL/SYMSGF)
     A            WXKVDT         6Y 0O  2 46EDTWRD('  .  .  ')
     A                                      COLOR(WHT)
     A                                  2 60MSGCON(008 JOV0894 *LIBL/SYMSGF)
     A            PABILN    R        O  2 69REFFLD(PALLDR/PABILN *LIBL/PALLD)
     A                                      COLOR(WHT)
     A            WSBIAB         1   O  2 80COLOR(WHT)
     A                                  3  2MSGCON(079 JOV0618 *LIBL/SYMSGF)
     A            PAAB      R        O  4  3REFFLD(PALLDR/PAAB *LIBL/PALLD)
     A                                      COLOR(WHT)
     A            PASTAT    R        O  4  7REFFLD(PALLDR/PASTAT *LIBL/PALLD)
     A                                      COLOR(WHT)
     A            PAPPNR    R        O  4 10REFFLD(PALLDR/PAPPNR *LIBL/PALLD)
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A            WXNAVA        20A  O  4 19COLOR(WHT)
     A            PAANT     R        O  4 40REFFLD(PALLDR/PAANT *LIBL/PALLD)
     A                                      EDTCDE(L)
     A                                      COLOR(WHT)
     A            PATXT     R        O  4 47REFFLD(PALLDR/PATXT *LIBL/PALLD)
     A                                      COLOR(WHT)
     A            PAUSER    R        O  4 69REFFLD(PALLDR/PAUSER *LIBL/PALLD)
     A                                      COLOR(WHT)
     A  61                              5 19MSGCON(007 JOV0614 *LIBL/SYMSGF)
     A  61        PAPUNR    R        O  5 27REFFLD(PALLDR/PAPUNR *LIBL/PALLD)
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A  63                              5 35MSGCON(015 JOV0612 *LIBL/SYMSGF)
     A  63        PAAVD     R        O  5 51REFFLD(PALLDR/PAAVD *LIBL/PALLD)
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A  63        PAOPD     R        O  5 56REFFLD(PALLDR/PAOPD *LIBL/PALLD)
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A            WADATO         6Y 0O  5 69EDTWRD('  .  .  ')
     A                                      COLOR(WHT)
     A            WXAB      R        O  6  3REFFLD(PALLDR/PAAB *LIBL/PALLD)
     A                                      COLOR(WHT)
     A            WXSTAT    R        O  6  7REFFLD(PALLDR/PASTAT *LIBL/PALLD)
     A                                      COLOR(WHT)
     A            WXPPNR    R        O  6 10REFFLD(PALLDR/PAPPNR *LIBL/PALLD)
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A            WXNAVB        20A  O  6 19COLOR(WHT)
     A            WXANT     R        O  6 40REFFLD(PALLDR/PAANT *LIBL/PALLD)
     A                                      EDTCDE(L)
     A                                      COLOR(WHT)
     A            WXTXT     R        O  6 47REFFLD(PALLDR/PATXT *LIBL/PALLD)
     A                                      COLOR(WHT)
     A            WXUSER    R        O  6 69REFFLD(PALLDR/PAUSER *LIBL/PALLD)
     A                                      COLOR(WHT)
     A  62                              7 19MSGCON(007 JOV0614 *LIBL/SYMSGF)
     A  62        WXPUNR    R        O  7 27REFFLD(PALLDR/PAPUNR *LIBL/PALLD)
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A  64                              7 35MSGCON(015 JOV0612 *LIBL/SYMSGF)
     A  64        WXAVD     R        O  7 51REFFLD(PALLDR/PAAVD *LIBL/PALLD)
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A  64        WXOPD     R        O  7 56REFFLD(PALLDR/PAOPD *LIBL/PALLD)
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A            WXDATO         6Y 0O  7 69EDTWRD('  .  .  ')
     A                                      COLOR(WHT)
     A  63                              8  2MSGCON(030 JOV0620 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  64                              8 34MSGCON(030 JOV0621 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  8 66MSGCON(015 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A          R BUNT
     A                                      WINDOW(*DFT 12 70)
     A                                      CF12(12 'Returner')
     A                                      OVERLAY
     A                                      PUTOVR
     A                                      OVRDTA
     A                                      OVRATR
     A                                  1  2'SYSPED/SYPA01'
     A                                      COLOR(BLU)
     A                                  1 20MSGCON(045 JOV0923 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  3  2MSGCON(060 JOV0918 *LIBL/SYMSGF)
     A                                  5  2MSGCON(040 JOV0919 *LIBL/SYMSGF)
     A            WSBUNR         7Y 0O  5 44EDTCDE(4)
     A                                  7  2MSGCON(030 JOV0920 *LIBL/SYMSGF)
     A            WSPRIN        10A  B  7 34
     A                                  8  2MSGCON(030 JOV0921 *LIBL/SYMSGF)
     A            WSFORM         1A  B  8 34
     A                                  8 36MSGCON(030 JOV0922 *LIBL/SYMSGF)
     A                                  9  2MSGCON(030 JOV0924 *LIBL/SYMSGF)
     A            WSARB          1A  B  9 34
     A                                  9 36MSGCON(035 JOV0925 *LIBL/SYMSGF)
     A                                 11  2MSGCON(065 JOV0917 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A          R ASSUME
     A                                      ASSUME
     A                                  1  4' '
