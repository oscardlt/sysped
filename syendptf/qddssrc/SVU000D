********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
12266 * 01 YBC PTF12 DOC_1001 er endret fra 3A til 5A OG NYTT FELT MRN
********************************************************************
      *
     A                                      DSPSIZ(27 132 *DS4)
     A                                      PRINT
     A                                      ALTHELP
     A                                      HELP
     A                                      HLPPNLGRP(HLPSCR SVU000PNL)
     A                                      HLPTITLE('ED För Import')
      *
     A          R SVU000DA                  SFL
     A            ZOP            1A  I  7  2
     A  31                                  DSPATR(HI)
     A  31                                  DSPATR(PC)
     A                                      VALUES(' ' '5' 'P' 'A')
     A            DOC_1004      10A  O  7  4
     A            DTM_2380B      8A  O  7 15
S01  A*           DOC_1001       3A  O  7 24
N01  A            DOC_1001       5A  O  7 24
E01  A            NAD_3207E      2A  O  7 30
E01  A            NAD_3036D     35A  O  7 33
E01  A            NAD_3036E     35A  O  7 69
N01  A            MRN           35A  H
N01  A            MRN_20        20A  O  7105
      *
     A          R SVU000DB                  SFLCTL(SVU000DA)
     A                                      SFLSIZ(0017)
     A                                      SFLPAG(0016)
     A  91N93N94                            ROLLUP(25 'Rollup')
     A  91N93N94                            ROLLDOWN(26 'Rolldown')
     A                                      CF03(03 'Gå ut')
     A                                      CF05(05 'Refresh')
     A                                      CF08(08 'Ny avd/sign')
     A                                      CF12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/SVU000'
     A                                      COLOR(BLU)
     A                                  1 16'DA'
     A                                      COLOR(BLU)
     A                                  1 26'Underhåll av Utl.sedel Översikt'
     A                                      COLOR(WHT)
     A                                  1 64DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  1 73TIME
     A                                      COLOR(BLU)
     A            ZRECNO         4S 0O  2 75SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  3  2'Välj option och tryck Enter.  '
     A                                      COLOR(BLU)
     A                                  4  4'5=Visa   P=Skriv ut   A=Arkiv'
     A                                      COLOR(BLU)
     A                                  6  4'Tullid'
     A                                      COLOR(WHT)
     A                                  6 15'Datum'
     A                                      COLOR(WHT)
     A                                  6 24'Typ'
     A                                      COLOR(WHT)
S01  A*                                 6 28'Avsändare'
N01  A                                  6 30'Avsändare'
     A                                      COLOR(WHT)
S01  A*                                 6 67'Mottagare'
N01  A                                  6 69'Mottagare'
     A                                      COLOR(WHT)
N01  A                                  6105'MRN'
N01  A                                      COLOR(WHT)
      *
     A          R SVU000DC
     A                                      OVERLAY
     A                                 26  2'F3=Avsluta'
     A                                      COLOR(BLU)
     A                                 26 15'F5=Uppdatera'
     A                                      COLOR(BLU)
     A                                 26 30'F8=Selektera'
     A                                      COLOR(BLU)
     A                                 26 45'F12=Avbryt'
     A                                      COLOR(BLU)
      *
     A          R SVU000DD
     A                                      CF03(03 'Avslutt program')
     A                                      CF04(04 'Sök kunde/leverandör')
     A                                      OVERLAY
     A            MSGNR          7A  P
     A                                  1  2'SYSPED/SVU000'
     A                                      COLOR(BLU)
     A                                  1 16'DD'
     A                                      COLOR(BLU)
     A                                  1 25'Underhåll av Utl.sedel Selektion'
     A                                      COLOR(WHT)
     A                                  1 64DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  1 73TIME
     A                                      COLOR(BLU)
     A                                  5  4'Välj sökväg:'
     A                                      COLOR(WHT)
     A                                  7  4'Tullid . :'
     A            ZS_1004       10A  B  7 17TEXT('Toll-ID')
     A                                      DSPATR(UL)
     A  46                                  DSPATR(PC)
     A                                      COLOR(YLW)
N01  A                                  9  4'MRN:'
N01  A            ZS_MRN        35A  B  9 17TEXT('MRN')
N01  A                                      DSPATR(UL)
N01  A                                      COLOR(YLW)
     A                                 26  2'F3=Avsluta'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 27  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
      *
     A          R SVU000DE
     A                                      CF03(03 'Avslutt program')
     A                                  1  2'SYSPED/SVU000'
     A                                      COLOR(BLU)
     A                                  1 16'DE'
     A                                      COLOR(BLU)
     A                                  1 26'Underhåll av Utl.sedel Översikt'
     A                                      COLOR(WHT)
     A                                  1 64DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  1 73TIME
     A                                      COLOR(BLU)
     A                                  2 59'Utl.datum'
     A                                      COLOR(WHT)
     A            DTM_2380B      8A  O  2 69
N01  A                                  2 78'MRN'
N01  A                                      COLOR(WHT)
N01  A            MRN           35A  O  2 82
     A                                  3  2'Status'
     A                                      COLOR(WHT)
     A            SVUH_ST        1A  O  3  9
     A                                  3 13'Typ'
     A                                      COLOR(WHT)
S01  A*           DOC_1001       3A  O  3 17
N01  A            DOC_1001       5A  O  3 17
     A                                  3 23'Godsl.kod'
     A                                      COLOR(WHT)
     A            LOC_3225A      3A  O  3 33
     A                                  3 39'Tullid'
     A                                      COLOR(WHT)
     A            DOC_1004      10A  O  3 46
     A                                  3 59'Tax.datum'
     A                                      COLOR(WHT)
     A            DTM_2380A      8A  O  3 69
     A                                  3 80'Brutt.vkt'
     A                                      COLOR(WHT)
     A            MEA_6314      15A  O  3 90
     A            MEA_6314B     15A  O  3106
     A                                  4  2'Dekl.ref'
     A                                      COLOR(WHT)
     A            RFF_1154A     35A  O  4 11
     A                                  4 59'Kollital'
     A                                      COLOR(WHT)
     A            CNT_6066       6A  O  4 69
     A            MEA_6314C     15A  O  4 90
     A            MEA_6314D     15A  O  4106
     A                                  5  2'Utl.tullkontor'
     A                                      COLOR(WHT)
     A            LOC_3225B      5A  O  5 17
     A            NAD_3036C     35A  O  5 23
     A            CTA_3412C     35A  O  5 59
     A                                  7  2'Ombud'
     A                                      COLOR(WHT)
     A            NAD_1131A      2A  O  7  8
     A            NAD_3039A     12A  O  7 11
     A            NAD_3036A     35A  O  7 24
     A            NAD_3042AA    35A  O  7 60
     A            NAD_3042BA    35A  O  7 96
     A            NAD_3251A     15A  O  8  8
     A            NAD_3164A     35A  O  8 24
     A            NAD_3207A      2A  O  8 60
     A            CTA_3412A     35A  O  8 63
     A            COM_3148A     35A  O  8 99
     A                                 10  2'Dekl'
     A                                      COLOR(WHT)
     A            NAD_1131B      2A  O 10  8
     A            NAD_3039B     12A  O 10 11
     A            NAD_3036B     35A  O 10 24
     A            NAD_3042AB    35A  O 10 60
     A            NAD_3042BB    35A  O 10 96
     A            NAD_3251B     15A  O 11  8
     A            NAD_3164B     35A  O 11 24
     A            NAD_3207B      2A  O 11 60
     A            CTA_3412B     35A  O 11 63
     A            COM_3148B     35A  O 11 99
     A                                 13  2'Avs'
     A                                      COLOR(WHT)
     A            NAD_3036D     35A  O 13  8
     A            NAD_3042AD    35A  O 13 44
     A            NAD_3042BD    35A  O 13 80
     A            NAD_3207D      2A  O 14  8
     A            NAD_3164D     35A  O 14 11
     A                                 16  2'Mot'
     A                                      COLOR(WHT)
     A            NAD_1131E      2A  O 16  8
     A            NAD_3039E     12A  O 16 11
     A            NAD_3036E     35A  O 16 24
     A            NAD_3042AE    35A  O 16 60
     A            NAD_3042BE    35A  O 16 96
     A            NAD_3251E     15A  O 17  8
     A            NAD_3164E     35A  O 17 24
     A            NAD_3207E      2A  O 17 60
     A                                 19  2'Vara'
     A                                      COLOR(WHT)
     A            FTX_4440A     70A  O 19  8
     A            FTX_4440B     70A  O 19 79
     A            FTX_4440C     70A  O 20 18
     A                                 22  2'Gods'
     A                                      COLOR(WHT)
     A            PCI_7102A     50A  O 22  8
     A            PCI_7102B     50A  O 22 79
     A            PCI_7102C     50A  O 23 18
     A                                 25  2'Y.ref'
     A                                      COLOR(WHT)
     A            RFF_1154B     35A  O 25  8
     A            RFF_1154C     35A  O 25 44
     A            RFF_1154D     35A  O 25 80
     A                                 26  2'F3=Avsluta'
     A                                      COLOR(BLU)
      *
     A          R SVU000DØ
     A                                      OVERLAY
     A            ZMSGNO         7A  P
     A            ZMSGTX        79   O 27  2MSGID(&ZMSGNO SYMSGF)
     A                                      COLOR(WHT)
