             PGM        PARM(&INTCLL &INTCLF)
/* OBS! FOR KOMPILERING MÅ FILEN E084C  FINNES I LIBL                */
/* TAST(KLIPP/LIM) F.EKS:                                            */
/* DSPFD FIRM TYPE(*ATR) OUTPUT(*OUTFILE) OUTFILE(QTEMP/E084C)       */
/*       FILEATR(*PF)                                                */
/*                                                                   */
/* EN BØR!!! DA BYTTE UT E084C MED DEN EN SELV HAR SKAPT I QTEMP     */
/* SLIK AT EN ER SIKKER PÅ AT FILEN ER PÅ RETT NIVÅ !!!!!!           */
/* FEIL KAN ELLERS GJØR AT EN HOPPER UT VED FORSØK PÅ RCVF           */
/* SIDEN ALLE FEIL MONITORES -> EXIT VIL EN DA IKKE OPPDAGE          */
/* HVORFOR EN IKKE FÅR LEST FILENE/MEMBERNE I E084C......            */
        /*___________________*/
        /* Beskriv variabler */
        /*"""""""""""""""""""*/
             DCL        VAR(&INTCLL) TYPE(*CHAR) LEN(10)
             DCL        VAR(&INTCLF) TYPE(*CHAR) LEN(10)
             DCLF       FILE(E084C) RCDFMT(QWHFDPHY)
        /*____________*/
        /* Hent liste */
        /*""""""""""""*/
        /*___________________*/
        /* ALLE FILER I BIBL */
        /*"""""""""""""""""""*/
             IF         COND(&INTCLF = '          ') THEN(DO)
             DSPFD      FILE(&INTCLL/*ALL) TYPE(*ATR) +
                          OUTPUT(*OUTFILE) FILEATR(*PF) +
                          OUTFILE(E084C)
             MONMSG     MSGID(CPF0000)
             GOTO       CMDLBL(READ)
             ENDDO
        /*___________________*/
        /* SPESIFIKK FIL     */
        /*"""""""""""""""""""*/
             DSPFD      FILE(&INTCLL/&INTCLF) TYPE(*ATR) +
                          OUTPUT(*OUTFILE) FILEATR(*PF) +
                          OUTFILE(E084C)
             MONMSG     MSGID(CPF0000)
        /*_____*/
        /* Les */
        /*"""""*/
 READ:       RCVF       RCDFMT(*FILE)
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(EXIT))
        /*_______________________*/
        /* Er filnavnet blankt ? */
        /*"""""""""""""""""""""""*/
             IF         COND(&PHFILE = '          ') THEN(GOTO +
                          CMDLBL(READ))
        /*___________________*/
        /* Hente komponenter */
        /*"""""""""""""""""""*/
             IF         COND(&PHNOMB *GT 0) THEN(DO)
             IF         COND(&PHMXRL *EQ 80) THEN(CALL PGM(E084C2) +
                          PARM(&INTCLL &PHFILE))
             ELSE       CMD(CALL PGM(E084C3) PARM(&INTCLL &PHFILE))
             ENDDO
        /*_______________*/
        /* SLETT FILEN ? */
        /*"""""""""""""""*/
             IF         COND(&INTCLF = '          ') THEN(DO)
             DLTF       FILE(&INTCLL/&PHFILE)
             MONMSG     MSGID(CPF0000)
             ENDDO
        /*__________________*/
        /* Les neste record */
        /*""""""""""""""""""*/
             GOTO       CMDLBL(READ)
        /*_____*/
        /* End */
        /*"""""*/
 EXIT:       ENDPGM
