     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HELP
      * "NOERASE" MÅ HA FOR Å IKKE TØMME BILDE SOM MAN KOMME FRA
     A          R NOERASE
     A                                      ASSUME
     A                                  1  2' '
      * MAN KUN DEFINNERE VINDU PÅ REKORDNIVÅ
     A          R WINDOWSZ
     A                                      OVERLAY
     A                                      WINDOW(*DFT 14 55 *NOMSGLIN)
      *
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  5  2VALUES(' ' '2' '5')
     A            S3LKF          2A  H
     A            S3SDF          5A  H
     A            S3LKT          2A  H
     A            S3SDT          5A  H
     A            S3AVD          4Y 0O  5  5EDTCDE(4)
     A            S3OPD          7Y 0O  5 10EDTCDE(4)
     A            S3ATAD         8Y 0H
     A            XATAD          6Y 0O  5 18EDTCDE(4)
     A            S3ATAK         4Y 0O  5 25EDTWRD('  :  ')
     A            S3ATDD         8Y 0H
     A            XATDD          6Y 0O  5 31EDTCDE(4)
     A            S3ATDK         4Y 0O  5 37EDTWRD('  :  ')
     A            S3ETAD         8Y 0H
     A            S3ETAK         4Y 0H
     A            S3ETDD         8Y 0H
     A            S3ETDK         4Y 0H
     A            S3EX1D         8Y 0H
     A            S3EX1K         4Y 0H
     A            S3EX2D         8Y 0H
     A            S3EX2K         4Y 0H
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0009)
     A                                      SFLPAG(0008)
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                  1  2'SYSPED/SYTT04'
     A                                      COLOR(BLU)
     A                                  1 20'Sett dato på oppdragsnivå'
     A                                      COLOR(WHT)
     A                                  2  2MSGCON(008 YBC0639 *LIBL/SYMSGF)
     A            UPRO2          8  0O  2 12EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  2 22'Fra:'
     A            ULKF           2   O  2 27COLOR(WHT)
     A            USDF           5   O  2 30COLOR(WHT)
     A                                  2 37'Til:'
     A            ULKT           2   O  2 42COLOR(WHT)
     A            USDT           5   O  2 45COLOR(WHT)
     A            ZRECNO         4S 0O  2 51SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  3  2'Valg opsjon:   2=Sett dato   5=Vis-
     A                                       oppdrag'
     A                                      COLOR(BLU)
     A                                  4  2'Op Avd. Opd.nr. ATA dato/tid ATD d-
     A                                      ato/tid'
     A                                      COLOR(WHT)
      *
     A          R BUNN
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A                                      CA03(03 'AVSLUTT')
     A                                      CA05(05 'Frisk')
     A                                      CA12(12 'AVSLUTT')
     A                                      SLNO(13)
     A                                      CLRL(02)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'F3/F12=Avbryt   F5=Forny'
     A                                      COLOR(BLU)
     A  30        MSGTXT        50A  O  2  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                              2  2'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R VARELI
     A                                      WINDOW(*DFT 8 62 *NOMSGLIN)
     A                                      OVERLAY
     A                                      CA12(12 'Forrige')
     A            MSGNR          7A  P
     A                                  1  2'SYSPED/SYTT04'
     A                                      COLOR(BLU)
     A                                  1 20'Sett dato på oppdragsnivå'
     A                                      DSPATR(HI)
     A                                  2  2'Avd/opd:'
     A            S3AVD          4  0O  2 11EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  2 16'/'
     A                                      COLOR(WHT)
     A            S3OPD          7  0O  2 18EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  2 26MSGCON(011 YBC0644 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  2 43MSGCON(011 YBC0644 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  3  2MSGCON(010 YBC0633 *LIBL/SYMSGF)
     A                                  3 21'ATA'
     A            WSATAD         6Y 0B  3 25DSPATR(UL)
     A  90
     AON30                                  DSPATR(PC)
     A  90                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A            WSATAK         4Y 0B  3 32DSPATR(UL)
     A  89                                  DSPATR(PC)
     A  89                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                  3 39'ATD'
     A            WSATDD         6Y 0B  3 43DSPATR(UL)
     A  88                                  DSPATR(PC)
     A  88                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A            WSATDK         4Y 0B  3 50DSPATR(UL)
     A  87                                  DSPATR(PC)
     A  87                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                  4  2MSGCON(010 YBC0630 *LIBL/SYMSGF)
     A                                  4 21'ETA'
     A            WSETAD         6Y 0B  4 25DSPATR(UL)
     A  86                                  DSPATR(PC)
     A  86                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A            WSETAK         4Y 0B  4 32DSPATR(UL)
     A  85                                  DSPATR(PC)
     A  85                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                  4 39'ETD'
     A            WSETDD         6Y 0B  4 43DSPATR(UL)
     A  84                                  DSPATR(PC)
     A  84                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A            WSETDK         4Y 0B  4 50DSPATR(UL)
     A  83                                  DSPATR(PC)
     A  83                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                  5  2MSGCON(010 YBC0634 *LIBL/SYMSGF)
     A                                  5 21'EX1'
     A            WSEX1D         6Y 0B  5 25DSPATR(UL)
     A  82                                  DSPATR(PC)
     A  82                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A            WSEX1K         4Y 0B  5 32DSPATR(UL)
     A  81                                  DSPATR(PC)
     A  81                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                  5 39'EX2'
     A            WSEX2D         6Y 0B  5 43DSPATR(UL)
     A  80                                  DSPATR(PC)
     A  80                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A            WSEX2K         4Y 0B  5 50DSPATR(UL)
     A  79                                  DSPATR(PC)
     A  79                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                  6  2MSGCON(022 YBC0648 *LIBL/SYMSGF)
     A                                  6 25MSGCON(025 YBC0649 *LIBL/SYMSGF)
     A                                  7  2MSGCON(060 YBC0645 *LIBL/SYMSGF)
     A N30                              8  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        55A  O  8  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
