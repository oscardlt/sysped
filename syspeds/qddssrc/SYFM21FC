********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 YBC PTF10 TOLLSTEDSKODE ER ENDRET TIL 4.0
********************************************************************
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPDOC('STD' 'HELPNU' 'HLP400')
     A                                      HELP
     A          R BILDE01
     A                                      CF03(03 'Avslutt')
     A                                      CF12(12 'Gå til rollbilde')
     A                                      CF22(22 'SE EDI LOGG')
     A                                      CF23(23 'bekreft sletting')
     A                                      CF10(10 'Gå til varelinjer')
     A                                      CF17(17 'Rubrikk 36')
     A                                      CF18(18 'Rubrikk 29')
     A                                      CF13(13 'SE KOLLIID')
     A                                      CF20(20 'ANENN FRAKTBET')
     A                                      OVERLAY
     A          H                           HLPDOC('SYFI10A010' 'SYFI10A' 'SY40-
     A                                      0LO/FELLES')
     A                                      HLPARA(01 001 02 080)
     A          H                           HLPDOC('SYFI10A020' 'SYFI10A' 'SY40-
     A                                      0LO/FELLES')
     A                                      HLPARA(03 001 03 009)
     A          H                           HLPDOC('SYFI10A030' 'SYFI10A' 'SY40-
     A                                      0LO/FELLES')
     A                                      HLPARA(03 010 03 028)
     A          H                           HLPDOC('SYFI10A040' 'SYFI10A' 'SY40-
     A                                      0LO/FELLES')
     A                                      HLPARA(03 029 03 080)
     A          H                           HLPDOC('SYFI10A050' 'SYFI10A' 'SY40-
     A                                      0LO/FELLES')
     A                                      HLPARA(04 001 06 080)
     A          H                           HLPDOC('SYFI10A060' 'SYFI10A' 'SY40-
     A                                      0LO/FELLES')
     A                                      HLPARA(07 001 08 080)
     A          H                           HLPDOC('SYFI10A070' 'SYFI10A' 'SY40-
     A                                      0LO/FELLES')
     A                                      HLPARA(09 001 09 080)
     A          H                           HLPDOC('SYFI10A080' 'SYFI10A' 'SY40-
     A                                      0LO/FELLES')
     A                                      HLPARA(10 001 10 080)
     A          H                           HLPDOC('SYFI10A090' 'SYFI10A' 'SY40-
     A                                      0LO/FELLES')
     A                                      HLPARA(11 001 11 080)
     A          H                           HLPDOC('SYFI10A100' 'SYFI10A' 'SY40-
     A                                      0LO/FELLES')
     A                                      HLPARA(12 001 12 048)
     A          H                           HLPDOC('SYFI10A110' 'SYFI10A' 'SY40-
     A                                      0LO/FELLES')
     A                                      HLPARA(12 049 12 080)
     A          H                           HLPDOC('SYFI10A120' 'SYFI10A' 'SY40-
     A                                      0LO/FELLES')
     A                                      HLPARA(13 001 13 033)
     A          H                           HLPDOC('SYFI10A130' 'SYFI10A' 'SY40-
     A                                      0LO/FELLES')
     A                                      HLPARA(13 034 13 080)
     A          H                           HLPDOC('SYFI10A140' 'SYFI10A' 'SY40-
     A                                      0LO/FELLES')
     A                                      HLPARA(14 001 16 080)
     A          H                           HLPDOC('SYFI10A150' 'SYFI10A' 'SY40-
     A                                      0LO/FELLES')
     A                                      HLPARA(17 001 17 080)
     A          H                           HLPDOC('SYFI10A160' 'SYFI10A' 'SY40-
     A                                      0LO/FELLES')
     A                                      HLPARA(18 001 18 080)
     A          H                           HLPDOC('SYFI10A170' 'SYFI10A' 'SY40-
     A                                      0LO/FELLES')
     A                                      HLPARA(19 001 20 080)
     A          H                           HLPDOC('SYFI10A190' 'SYFI10A' 'SY40-
     A                                      0LO/FELLES')
     A                                      HLPARA(21 001 21 048)
     A          H                           HLPDOC('SYFI10A180' 'SYFI10A' 'SY40-
     A                                      0LO/FELLES')
     A                                      HLPARA(21 049 21 080)
     A          H                           HLPDOC('SYFI10A999' 'SYFI10A' 'SY40-
     A                                      0LO/FELLES')
     A                                      HLPARA(22 001 24 080)
     A                                  1  2'SYSPED/SYFM21C'
     A                                      COLOR(BLU)
     A                                  1 20MSGCON(013 SHU0441 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            WSSNDN        17A  O  1 36COLOR(RED)
     A            DFAVD          4Y 0O  1 54EDTCDE(4)
     A                                      COLOR(WHT)
     A            DFOPD          7Y 0O  1 59EDTCDE(4)
     A                                      COLOR(WHT)
     A            DFFBNR         3Y 0O  1 67EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  1 71DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  2  2MSGCON(006 DIV0151 *LIBL/SYMSGF)
     A            DFST           1A  B  2  9TEXT('Status')
     A                                      DSPATR(PR)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  2 11MSGCON(010 DIV0152 *LIBL/SYMSGF)
     A            DFKDME         1A  O  2 22TEXT('Merkelapp  /X')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            DFMANT         4Y 0O  2 24EDTCDE(4)
     A                                      COLOR(YLW)
     A                                  2 30MSGCON(012 DIV0153 *LIBL/SYMSGF)
     A            DFKDE          1A  O  2 43TEXT('Etterkrav  /X')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  2 46MSGCON(008 DIV0131 *LIBL/SYMSGF)
     A            DFSIGN         3A  O  2 55COLOR(YLW)
S01  A*                                 2 60'Tollst'
S01  A*           DFTOLL         2Y 0O  2 67TEXT('Tollsted')
N01  A                                  2 60'T.std'
N01  A            DFTOLL         4Y 0O  2 66TEXT('Tollsted')
     A                                      EDTCDE(4)
     A                                      COLOR(RED)
     A                                  2 71TIME
     A                                      COLOR(BLU)
     A                                  3 11'EDIfact ?'
     A            DFCMN     R        O  3 22REFFLD(DOKUFR/DFCMN *LIBL/DOKUF)
     A                                      COLOR(YLW)
     A                                  3 24'Y/N'
     A                                  3 30'Ventekode'
     A            DFVEN     R        O  3 43REFFLD(DOKUFR/DFVEN *LIBL/DOKUF)
     A                                      COLOR(YLW)
     A                                  3 58'17.Utst.dato'
     A            WSDTU          6Y 0O  3 71COLOR(WHT)
     A                                      EDTWRD('  .  .  ')
     A                                  4  2'01.Sender'
     A            DFKNSS         8Y 0O  4 12TEXT('Kundenr Sender')
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(YLW)
     A                                  4 21'15.Knr hos tr.'
     A            DFKNS         12A  O  4 36TEXT('Kundenr Selger')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  4 49'16.Senders ref'
     A            DFSREF        17A  O  4 64TEXT('Senders ref')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            DFNAVS        30A  O  5  2COLOR(YLW)
     A                                  5 49'18.Booking ref'
     A            DFBREF        17A  O  5 64TEXT('Booking ref')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            DFAD1S        30A  O  6  2COLOR(YLW)
     A                                  6 36'Oppr. avsender/Fakturasender'
     A            DFPNLS    R        O  7  2REFFLD(DOKUFR/DFPNLS *LIBL/DOKUF)
     A                                      COLOR(YLW)
     A                                      EDTWRD('    ')
     A            WSAD3S        25A  O  7  7COLOR(YLW)
     A            DFFASE        25A  O  7 36COLOR(YLW)
     A                                  8  2'04.Mottak'
     A            DFKNSM         8Y 0O  8 12TEXT('Kundenr Sender')
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(YLW)
     A                                  8 21'23.Knr hos tr.'
     A            DFKNM         12A  O  8 36TEXT('Kundenr Mottaker')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  8 49'25.Mottak. ref'
     A            DFMREF        17A  O  8 64TEXT('Mottak. ref')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            DFNAVM        30A  O  9  2TEXT('Navn Mottaker')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  9 41'07.Lever.'
     A            DFNAVL        30A  O  9 51TEXT('Navn Mottaker')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            DFAD1M        30A  O 10  2TEXT('Adresse Mottaker')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            DFAD1L        30A  O 10 51TEXT('Leveringsadresse')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            DFAD3M        30A  O 11  2TEXT('P-adr Mottaker')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 11 41'09.UTL.st'
     A            DFPOUL    R        O 11 51REFFLD(DOKUFR/DFPOUL *LIBL/DOKUF)
     A                                      COLOR(YLW)
     A                                      EDTWRD('    ')
     A            WSADUL        24A  O 11 56TEXT('Utleveringssted')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 12  2'28.Belast'
     A            DFBELA         1A  O 12 12TEXT('Sender bet henting')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 12 14'S/M/A'
     A                                 13  2'10.Transp'
     A            DFTRAN    R     Y  O 13 12REFFLD(DOKUFR/DFTRAN *LIBL/DOKUF)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            DFNAT         20A  O 13 21TEXT('Navn Transportør')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 13 42'14.Prod.'
     A            DFPROK         5A  O 13 51COLOR(YLW)
     A            PRODNA        24A  O 13 57COLOR(WHT)
     A                                 14  2'11.Over'
     A            DFOVER        17A  O 14 12TEXT('Over Tollsted')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            DFPRO1        30A  O 14 51TEXT('Over Tollsted')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            DFPRO2        30A  O 15 51TEXT('Over Tollsted')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 16  2'24.Varefors.verdi'
     A            DFFVCU    R        O 16 20REFFLD(DOKUFR/DFFVCU *LIBL/DOKUF)
     A                                      COLOR(YLW)
     A            DFFVBL    R     Y  O 16 24REFFLD(DOKUFR/DFFVBL *LIBL/DOKUF)
     A                                      COLOR(YLW)
     A                                      EDTCDE(M)
     A                                 16 36'Kateg'
     A            DFFKAT         4A  O 16 42COLOR(YLW)
     A                                 16 47'26.Polise/SKAL for'
     A            DFPOLI        13A  O 16 66COLOR(YLW)
     A            DFSKFO         1A  O 16 80COLOR(YLW)
     A                                 17  2'27.Le.bet'
     A            DFLEVB         3A  O 17 12COLOR(YLW)
     A            DFLEVT        20A  O 17 16COLOR(YLW)
     A                                 17 37'30.Lev.instr.'
     A            DFLEVI        30A  O 17 51COLOR(YLW)
     A                                 18  2MSGCON(020 SHU0524 *LIBL/SYMSGF)
     A            DFVALK    R        O 18 23REFFLD(DOKUFR/DFFVCU *LIBL/DOKUF)
     A                                      COLOR(YLW)
     A            DFBELV    R     Y  O 18 27REFFLD(DOKUFR/DFFVBL *LIBL/DOKUF)
     A                                      EDTCDE(M)
     A                                      COLOR(YLW)
     A                                 18 49'12.Utv.pll'
     A            DFNTEU         2Y 0O 18 60TEXT('Ant EURO-plr utveksl')
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(YLW)
     A                                 18 63'13.Andre pll'
     A            DFNTAU         2Y 0O 18 76TEXT('Ant andr-plr utveksl')
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(YLW)
     A                                 19  2'33.Merking   34.Ant. 35.Type pakn.-
     A                                      /godsslag 36.Br.vkt/39.Fr.v  37.M3 -
     A                                        37.Lm '
     A            DFGM          12A  O 20  2TEXT('Godsmerking')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            DFNT           7Y 0O 20 15TEXT('Antall')
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(YLW)
     A            DFVS          25A  O 20 23TEXT('Vareslag')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            DFVKT          9Y 0O 20 49TEXT('Vekt')
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(YLW)
     A            DFM3           7Y 3O 20 63TEXT('Kubikk')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            DFLM           4Y 2O 20 73TEXT('Lastem.')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            DFGM2         12A  O 21  2TEXT('Godsmerking')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            DFVS2         25A  O 21 23TEXT('Vareslag')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            DFVKTF         9Y 0O 21 52TEXT('Vekt')
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(YLW)
     A  88                             22  4'*** S L E T T I N G   V A L G T   -
     A                                      B E K R E F T   M E D   F23'
     A                                      COLOR(RED)
     A  88                                  DSPATR(BL)
     A                                 23  1MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 20MSGCON(018 TRO0001 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 39MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 59MSGCON(007 BBA0145 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 73MSGCON(007 BBA0146 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 24  1MSGCON(018 JOV1014 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 24 20MSGCON(018 JOV1117 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A N88 51                          22  2'Kvit.mott:'
     A N88 51     DFSGM          5   O 22 14COLOR(WHT)
     A N88 51     DFDTMO         8   O 22 21COLOR(WHT)
     A N88 51     DFKLMO         4   O 22 31COLOR(WHT)
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  7  2
     A  98                                  DSPATR(RI)
     A  98                                  DSPATR(PC)
     A                                      VALUES(' ' '5' '4')
     A            DFFBNR    R        O  7  4REFFLD(DOKUFR/DFFBNR *LIBL/DOKUF)
     A                                      DSPATR(UL)
     A            DFNAMW        29A  O  6 20
     A            DFNT      R        O  7 11REFFLD(DOKUFR/DFNT *LIBL/DOKUF)
     A                                      EDTCDE(4)
     A                                      DSPATR(UL)
     A            DFVS      R        O  7 19REFFLD(DOKUFR/DFVS *LIBL/DOKUF)
     A                                      DSPATR(UL)
     A            DFVKT     R        O  7 45REFFLD(DOKUFR/DFVKT *LIBL/DOKUF)
     A                                      EDTCDE(4)
     A                                      DSPATR(UL)
     A            WSSNDN        17A  O  6  2COLOR(YLW)
     A            DFPOUL    R        O  6 50REFFLD(DOKUFR/DFPOUL *LIBL/DOKUF)
     A            WSADUL        24A  O  6 55
     A            DFNAT     R        O  7 55REFFLD(DOKUFR/DFNAT *LIBL/DOKUF)
     A                                      DSPATR(UL)
     A            DFSGM          5   O  7 76COLOR(WHT)
     A*
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0009)
     A                                      SFLPAG(0008)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                  1  2'SYSPED/SYFM21C'
     A                                      COLOR(BLU)
     A                                  1 20MSGCON(030 DIV0378 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            UAVD           4Y 0O  1 52EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  1 57'/'
     A            UOPD           7Y 0O  1 59EDTCDE(4)
     A                                      COLOR(WHT)
     A            ZRECNO         4S 0O  1 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  2 65TIME
     A                                  3  2MSGCON(020 SHU0158 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  1MSGCON(078 SHU0156 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  5 11MSGCON(069 SHU0157 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  5  1'op no'
     A*
     A          R BUNN
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF03(03 'Avslutt')
     A                                      CF12(12)
     A                                      SLNO(22)
     A                                      CLRL(03)
     A                                      OVERLAY
     A                                  2  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  2 22MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A            MSGNR          7A  P
     A  30        MSGTXT        78A  O  3  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A*
     A          R NYVARE
     A                                      OVERLAY
     A                                  1  2'SYSPED/SYFI10A'
     A                                      COLOR(BLU)
     A                                  1 20MSGCON(030 DIV0378 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            UAVD           4Y 0O  1 52EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  1 57'/'
     A            UOPD           7Y 0O  1 59EDTCDE(4)
     A                                      COLOR(WHT)
     A            ZRECNO         4S 0O  1 76DSPATR(ND)
     A                                  2 65TIME
     A                                  4  3MSGCON(078 SHU0157 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  5 12MSGCON(069 SHU0156 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A          R POSTEN
     A                                      WINDOW(11 3 7 44)
     A                                  1  8'*** POSTENS BEDRIFTSPAKKE ***'
     A                                      DSPATR(HI)
     A            DFVT1         40   O  2  2COLOR(YLW)
     A            DFVT2         40   O  3  2COLOR(YLW)
     A            DFVT3         40   O  4  2COLOR(YLW)
     A            DFVT4         40   O  5  2COLOR(YLW)
     A                                  6  2MSGCON(030 BBA0138 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A          R ANNEN
     A                                      WINDOW(9 20 10 42)
     A                                  1  8'Annen Fraktbetaler    '
     A                                      DSPATR(HI)
     A                                  3  2'19.Annen'
     A            DFKNSX         8Y 0O  3 12TEXT('Kundenr ANNEN')
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(YLW)
     A                                  4  2'20.Knr hos tr.'
     A            DFKNX         12A  O  4 20TEXT('Kundenr ANNEN')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            DFNAVX        30A  O  5  2TEXT('Navn Annen')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            DFAD1X        30A  O  6  2TEXT('Adresse Annen')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            DFAD3X        30A  O  7  2TEXT('Adr.3   Annen')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
