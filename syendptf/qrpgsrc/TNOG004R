      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/TNOG004R MODULE(*LIBL/TNOG004R *LIBL/INCGI)
CRTPGM*        ACTGRP(*NEW) AUT(*USE)
      *
      *********************************************************************
      /copy SYSPEDS/qrpgsrcpy,hspecs
      /copy SYSPEDS/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy SYSPEDS/qrpgsrcpy,prototypeb
      /copy SYSPEDS/qrpgsrcpy,usec
      /copy SYSPEDS/qrpgsrcpy,variables3
     ‚*  Prototype for 'JSONFIX'
     d jsonfix         pr                  extpgm('JSONFIX')
     d jsonstring_                         like(jsonstring)
     ‚*  Prototype for 'INCGI'
     d incgi           pr                  extproc('INCGI')
     d bistdl_                             like(bistdl)
     ‚*  Prototype for variabel program
     D Varpgm          Pr                  ExtPgm(BILIBL)
     d WhereC          s            512a
     d FirstW          s              1A
     d cQuote          c                   const('''')
     d SqlStm          s            512a
     d myrows          s             10i 0
     D WSUSER          S             10
     D WSONAVN         S             10
     D WKOD            S             10
     D WAVG            S              1
     D WANK            S              1
     D WTRS            S              1
     D JSONstring      s          32000
     D Error           S            150
     D AntLest         S              9  0
     d X               S             10  0
     d MyPoDS          ds                  Qualified  dim(999)
     d  TKUNIK                        3
     d  TKKODE                       10
     d  TKTXTN                       35
     d  TKTXTE                       35
     d  TKAVG                         1
     d  TKANK                         1
     d  TKTRS                         1
      *get input-string
      /copy syspeds/qrpgsrcpy,prolog3

      /free
         exsr ParseSr;
         exsr init;

       gethtmlifs(
       '/syspeddev/html/JSON.html':
       '/CGITAG/');
       wrtsection('top');

       //...........................................................................................
       //skriv JSON-Object start (alltid '{' + x ant param. m/komma mellom (IKKE komma etter siste))
       //...........................................................................................

        JSONstring='{'
        +'¬user¬ : ¬'+%trim(WSUSER)+'¬, '
        +'¬sonavn¬ : ¬'+%trim(WSONAVN)+'¬, '
        +'¬kod¬ : ¬'+%trim(WKOD)+'¬, '
        +'¬avg¬ : ¬'+%trim(WAVG)+'¬, '
        +'¬ank¬ : ¬'+%trim(WANK)+'¬, '
        +'¬trs¬ : ¬'+%trim(WTRS)+'¬, '
        +'¬errMsg¬ : ¬'+%trim(Error)+'¬, ';

       //JSONfix escaper " i tekst,  for deretter å bytte ¬->"

       jsonfix ( jsonstring );

       updhtmlvar2('JSONstring'
                  :%addr(jsonstring)
                  :%len(jsonstring));
       wrtsection('JSONlin');

       //...........................................................................................
       // Skriv JSON-LISTE Start (en liste er EN parameter(fra [ til   )
       //..........................................................................................
       jsonstring ='"customslist" : [';

       updhtmlvar2('JSONstring'
                  :%addr(jsonstring)
                  :%len(jsonstring));
       wrtsection('JSONlin');

       if error = *blanks;
         exsr SelSr;
         if myrows > 0;
           exsr LesSr;
         endif;
       endif;
       // ..........................................................................................
       // Skriv JSON-Liste/Array  Avslutning
       // ..........................................................................................
       jsonstring =']';
       updhtmlvar2('JSONstring'
                  :%addr(jsonstring)
                  :%len(jsonstring));
       wrtsection('JSONlin');
       // ..........................................................................................
       // Skriv JSON-string Avsluttning
       // ..........................................................................................
       jsonstring ='}';
       updhtmlvar2('JSONstring'
                  :%addr(jsonstring)
                  :%len(jsonstring));
       wrtsection('JSONlin');
         exsr Exit;

       //-----------------------------------------------------------------
       // Select fra fil via SQL
       //-----------------------------------------------------------------
       begsr     SelSr;

         Exsr  AddAnd;
         Eval  WhereC =  %trim(WhereC) +
         ' TKUNIK = ' + cQuote + '106' + cQuote +' ';

       If    WSONAVN  <> *blanks;
         Exsr  AddAnd;
         Eval  WhereC =  %trim(WhereC)  +
         ' UPPER(TKTXTN) LIKE UPPER(' + cQuote + '%' +
         %trim(WSONAVN) + '%' + cQuote + ')';
       EndIf;

       If    WKOD  <> *blanks;
         Exsr  AddAnd;
         Eval  WhereC =  %trim(WhereC)  +
         ' UPPER(TKKODE) LIKE UPPER(' + cQuote + '%' +
         %trim(WKOD) + '%' + cQuote + ')';
       EndIf;

       If    WAVG  = 'J';
         Exsr  AddAnd;
         Eval  WhereC =  %trim(WhereC)  +
         ' TKAVG = ' + cQuote + 'J' + cQuote +' ';
       EndIf;

       If    WANK  = 'J';
         Exsr  AddAnd;
         Eval  WhereC =  %trim(WhereC)  +
         ' TKANK = ' + cQuote + 'J' + cQuote +' ';
       EndIf;

       If    WTRS  = 'J';
         Exsr  AddAnd;
         Eval  WhereC =  %trim(WhereC)  +
         ' TKTRS = ' + cQuote + 'J' + cQuote +' ';
       EndIf;

         Eval      WhereC =  ' Where ' + %trim(WhereC);

         Eval       SqlStm = 'Select TKUNIK, TKKODE, TKTXTN, TKTXTE, ' +
                             'TKAVG, TKANK, TKTRS ' +
                             'from TRKODF ' +
                              %trim(WhereC)  +
                             ' Order by '       +
                             'TKKODE desc ' +
                             'FOR READ ONLY ';
         Exec SQL PREPARE DynSel from :SqlStm;
         EXEC SQL DECLARE  A33  cursor for DynSel;
         Exec SQL OPEN A33;
         Myrows = %elem(MyPoDS);
         exec sql FETCH A33   for :MyRows rows
                  INTO :MyPoDs;
       endsr;
       //-----------------------------------------------------------------
       // Set inn "AND" mellom where settninger
       //-----------------------------------------------------------------
       begsr     AddAnd;
       If        FirstW =   'N';
         Eval      WhereC =  %trim(WhereC) + ' and ';
       EndIf;
         Eval      FirstW =   'N';
       endsr;
       //-----------------------------------------------------------------
       // hent input
       //-----------------------------------------------------------------
       begsr     ParseSr;
         wsuser  = zhbgetvar('USER');
         wsonavn = zhbgetvar('SONAVN');
         wkod    = zhbgetvar('KOD');
         wavg    = zhbgetvar('AVG');
         wank    = zhbgetvar('ANK');
         wtrs    = zhbgetvar('TRS');

        endsr;
        // __________________________________________________
        // Læs array                                    LESSR
        // """"""""""""""""""""""""""""""""""""""""""""""""""
           begsr LesSr;
       for x = 1 to myrows;

       if mypods(x).tkkode = *blanks;
         leave;
       endif;

           if error = *blanks;

        // Lægg ut Json stræng
           AntLest = AntLest + 1;

        // Skriv en JSON-Array-linje pr menypunkt - komma før hvert nytt element

       if AntLest=1;
          JSONstring=*blanks;
          else;
          JSONstring=', ';
          endif;
       JSONstring=%trim(JSONstring)+'{'
          +'¬tkkode¬ : ¬'+%trim(mypods(x).TKKODE)+'¬, '
          +'¬tktxtn¬ : ¬'+%trim(mypods(x).TKTXTN)+'¬, '
          +'¬tkavg¬ : ¬'+%trim(mypods(x).TKAVG)+'¬, '
          +'¬tkank¬ : ¬'+%trim(mypods(x).TKANK)+'¬, '
          +'¬tktrs¬ : ¬'+%trim(mypods(x).TKTRS)+'¬}';
         jsonfix (JSONstring);
       updhtmlvar2('JSONstring'
                  :%addr(jsonstring)
                  :%len(jsonstring));
         wrtsection ('JSONlin');

        endif;

        endfor;

       endsr;
       //********************************************************
       begsr init;
       //********************************************************
         open bridf;
         // Test innlogging
         chain wsuser bridf;

         if %found;
           if bilibl = *blanks;
             incgi ( bistdl );
           else;

             Monitor;
                   Varpgm();
                 On-Error;
                   //
                 EndMon;

           endif;
         else;

           error = 'Invalid userID';
         endif;

       endsr;
       //=====================================================================
       // Close output html and quit
       //=====================================================================
       begsr exit;
         // Lukk fil
         close bridf;

         wrtsection('*fini');
         *inlr = *on;
         return;
       endsr;
      /end-free
