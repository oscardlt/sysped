      *
      **************************************************************
      *** PROGRAM FOR VEDLIKEHOLD AV FERJEPRISER                 ***
      **************************************************************
      *
     FFERR      UF A E           K DISK
     FLEVEL01   IF   E           K DISK
     FFIRM      IF   E           K DISK
     FSTED2     IF   E           K DISK
     FVALUL01   IF   E           K DISK
     FSYTR01FM  CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D                 DS
     D  FEFROM                 1      5
     D  XDLT                   1      5
     D                 DS
     D  FEDATE                 1      8  0
     D  FEDTÅ                  1      4  0
     D  FEDTM                  5      6  0
     D  FEDTD                  7      8  0
     D                 DS
     D  WEDATE                 1      6  0
     D  WEDTD                  1      2  0
     D  WEDTM                  3      4  0
     D  WEDTÅ                  5      6  0
     D                 DS
     D  WSDATE                 1      6  0
     D  WSDTD                  1      2  0
     D  WSDTM                  3      4  0
     D  WSDTÅ                  5      6  0
     D                 DS
     D  W2DATE                 1      6  0
     D  W2DTD                  1      2  0
     D  W2DTM                  3      4  0
     D  W2DTÅ                  5      6  0
     D                 DS
     D  YSDATE                 1      8  0
     D  YSDTÅ                  1      4  0
     D  YSDTM                  5      6  0
     D  YSDTD                  7      8  0
     C*
     C     STINI         TAG
     C                   EXSR      INIT
     C*
     C*** AVSLUTT
     C   03              GOTO      SLUTT
     C*
     C*
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
     C                   MOVE      W2FROM        YSFROM
     C                   MOVE      W2TO          YSTO
     C                   MOVE      W2LENG        YSLENG
     C                   MOVE      W2DTD         YSDTD
     C                   MOVE      W2DTM         YSDTM
     C     W2DTÅ         IFEQ      0
     C     W2DTÅ         ADD       1900          YSDTÅ
     C                   ELSE
     C     W2DTÅ         ADD       2000          YSDTÅ
     C                   END
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     KODSI1        SETLL     FERR
     C     START1        TAG
     C     WSFIRM        READE(N)  FERR                                   94
     C     *IN94         IFEQ      '0'
     C                   MOVE      FEFROM        YSFROM
     C                   MOVE      FETO          YSTO
     C                   MOVE      FELENG        YSLENG
     C                   MOVE      FEDATE        YSDATE
     C                   END
      *
     C     START2        TAG
      *
     C  N94              DO
     C                   DO        14
     C                   ADD       1             ZRECNO
     C                   MOVE      FEDTD         WEDTD
     C                   MOVE      FEDTM         WEDTM
     C                   MOVE      FEDTÅ         WEDTÅ
     C                   WRITE     SUBFIL
     C     WSFIRM        READE(N)  FERR                                   94
     C     *IN94         IFEQ      '0'
     C                   MOVE      FEFROM        YSFROM
     C                   MOVE      FETO          YSTO
     C                   MOVE      FELENG        YSLENG
     C                   MOVE      FEDATE        YSDATE
     C                   END
     C  N94              END
     C                   END
     C*
     C                   Z-ADD     ZRECNO        YRECNO            5 0
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     2             POSNBR
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   Z-ADD     4             POSNBR
     C                   EXFMT     NYWRK
     C                   ELSE
     C                   WRITE     SUBCTL
     C                   EXFMT     BUNN
     C                   END
     C*
     C***
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C                   Z-ADD     WSSUBN        XRECNO            4 0
     C     XRECNO        IFEQ      0
     C                   Z-ADD     ZRECNO        XRECNO            4 0
     C                   END
     C***
     C*
     C                   SETOFF                                       9899
     C                   MOVEA     '00000000'    *IN(70)
     C*
     C*** AVSLUTT
     C   03              GOTO      STINI
     C*
     C*** FRISK OPP
     C   05              GOTO      START
     C*
     C*** LAGE NY
     C   06              DO
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     04            POSNBR
     C                   EXSR      MOVB2
     C                   GOTO      START5
     C                   END
     C*** SØK FRA
     C   08              GOTO      START6
     C*
     C*** BLA VIDERE
     C   25              DO
     C                   SETOFF                                       25
     C                   Z-ADD     YRECNO        ZRECNO
     C  N94KODSI3        SETLL     FERR
     C  N94WSFIRM        READE(N)  FERR                                   94
     C     *IN94         IFEQ      '0'
     C                   MOVE      FEFROM        YSFROM
     C                   MOVE      FETO          YSTO
     C                   MOVE      FELENG        YSLENG
     C                   MOVE      FEDATE        YSDATE
     C                   END
     C                   GOTO      START2
     C                   END
     C*
     C     ZRECNO        IFNE      0
     C                   EXSR      HENT
     C                   END
     C   98XRECNO        IFNE      0
     C                   Z-ADD     XRECNO        ZRECNO
     C                   END
     C                   GOTO      START3
     C*
     C*** LAGE NY
     C     START5        TAG
     C                   EXFMT     VARELI
     C                   SETOFF                                       309089
     C   12              EXSR      MOVB2
     C  N12              EXSR      TESTB1
     C  N12
     CAN 30              GOTO      START5
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     2             POSNBR
     C                   GOTO      START3
     C*
     C*** SØK FRA
     C     START6        TAG
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     04            POSNBR
     C                   EXFMT     VAREL2
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     2             POSNBR
     C   12              GOTO      START3
     C                   GOTO      START
     C*
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
     C*
     C***********************************************
     C     TESTB1        BEGSR
     C***********************************************
     C  N40              DO
     C* TEST FROM PLACE
     C     WSFROM        CHAIN     STED2                              70
     C   70              MOVE      'SPS0012'     MSGNR
     C   70              SETON                                        30
     C*
     C* TEST TO PLACE
     C     WSTO          CHAIN     STED2                              71
     C   71
     CANN30              MOVE      'SPS0013'     MSGNR
     C   71
     CANN30              Z-ADD     10            POSNBR
     C   71              SETON                                        30
     C*
     C     WSLENG        COMP      *BLANKS                                74
     C   74
     CANN30              Z-ADD     16            POSNBR
     C   74              SETON                                        30
     C*
     C* TESTER DATO
     C*
     C                   MOVE      WSDTD         UDD               2 0
     C                   MOVE      WSDTM         UMM               2 0
     C                   MOVE      WSDTÅ         UÅÅ               2 0
     C                   CALL      'TESTDATO'
     C                   PARM                    UDD
     C                   PARM                    UMM
     C                   PARM                    UÅÅ
     C                   PARM                    UFLAGG            1 0
     C     UFLAGG        COMP      1                                      75
     C*
     C   75
     CANN30              MOVE      'SPG0011'     MSGNR
     C   75
     CANN30              Z-ADD     22            POSNBR
     C   75              SETON                                        30
     C*
     C*
     C*  TESTER OM RECORD FINNES FRA FØR
     C     *IN30         IFEQ      '0'
     C                   MOVE      WSDTD         FEDTD
     C                   MOVE      WSDTM         FEDTM
     C     WSDTÅ         IFGT      50
     C     WSDTÅ         ADD       1900          FEDTÅ
     C                   ELSE
     C     WSDTÅ         ADD       2000          FEDTÅ
     C                   END
     C                   END
     C*
     C  N30KODSIK        CHAIN(N)  FERR                               33
     C  N30
     CANN33              DO
     C                   SETON                                        90
     C   90
     CANN30              Z-ADD     4             POSNBR
     C   90
     CANN30              MOVE      'SPF0108'     MSGNR
     C   90              SETON                                        30
     C                   END
     C                   END
     C*
     C*  TESTER VALUTAKODE
     C*
     C     VALKEY        CHAIN     VALUL01                            72
     C   72
     CANN30              MOVE      'SPV0001'     MSGNR
     C   72
     CANN30              Z-ADD     31            POSNBR
     C   72              SETON                                        30
     C*
     C*
     C*  TESTER BELØP1
     C*
     C     WSPRI1        COMP      *ZEROS                                 76
     C   76
     CANN30              MOVE      'SPB0007'     MSGNR
     C   76
     CANN30              Z-ADD     36            POSNBR
     C   76              SETON                                        30
     C*
     C*
     C*  TESTER BELØP2
     C*
     C     WSPRI2        COMP      *ZEROS                                 77
     C   77
     CANN30              MOVE      'SPB0007'     MSGNR
     C   77
     CANN30              Z-ADD     60            POSNBR
     C   77              SETON                                        30
     C*
     C*
     C* SLUTT PÅ TESTER
     C*
     C   30              Z-ADD     22            LINNBR
     C*
     C   30              GOTO      SLTB
     C*
     C*
     C*** OPPDATERE PÅ REGISTER OG SUBFIL
     C     KODSIK        CHAIN     FERR                               33
     C                   EXSR      MOVWSI
     C  N33              UPDATE    FERRR
     C   33              WRITE     FERRR
     C   40              UPDATE    SUBFIL
     C  N40              ADD       1             YRECNO
     C  N40              Z-ADD     YRECNO        ZRECNO
     C  N40              WRITE     SUBFIL
     C                   WRITE     SUBCTL
     C                   EXSR      MOVB2
     C*
     C     SLTB          ENDSR
     C*
     C***********************************************
     C     HENT          BEGSR
     C***********************************************
     C                   Z-ADD     0             LINNBR
     C                   SETOFF                                       919299
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C   98              DO
     C   99              SETOFF                                       98
     C                   Z-ADD     1             LINNBR
     C                   GOTO      SLHENT
     C                   END
     C     ZRECNO        DIV       14            LINNBR
     C                   MVR                     XLIN              2 0
     C     XLIN          IFEQ      0
     C                   Z-ADD     14            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C     WSNR          IFEQ      '2'                                          ENDRE
     C                   MOVE      ' '           WSNR
     C     XDLT          COMP      '*DEL*'                                98
     C   98              GOTO      SLHENT
     C                   EXSR      CHGSI
     C   12              GOTO      SLHENT
     C                   END
     C     WSNR          IFEQ      '4'                                          SLETTE
     C                   MOVE      ' '           WSNR
     C     XDLT          COMP      '*DEL*'                                98
     C   98              GOTO      SLHENT
     C                   EXSR      DLTSI
     C   12              GOTO      SLHENT
     C                   END
     C                   END
     C*
     C     SLHENT        TAG
     C                   ADD       5             LINNBR
     C                   Z-ADD     2             POSNBR
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     DLTSI         BEGSR
     C***********************************************
     C                   SETON                                        99
     C                   MOVE      FEFROM        WSFROM
     C                   MOVE      FETO          WSTO
     C                   MOVE      FELENG        WSLENG
     C                   MOVE      FEDTD         WSDTD
     C                   MOVE      FEDTM         WSDTM
     C                   MOVE      FEDTÅ         WSDTÅ
     C                   MOVE      FECURR        WSCURR
     C                   MOVE      FEPRI1        WSPRI1
     C                   MOVE      FEPRI2        WSPRI2
     C                   EXFMT     VAREL3
     C  N12              DO
     C     KODSIK        CHAIN     FERR                               33
     C  N33              DELETE    FERRR
     C                   EXSR      MOVB1
     C                   END
     C     XLIN          IFEQ      0
     C                   Z-ADD     14            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C                   UPDATE    SUBFIL
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     CHGSI         BEGSR
     C***********************************************
     C                   EXSR      MOVSIW
     C                   SETON                                        99
     C     STCHG         TAG
     C                   EXFMT     VARELI
     C                   SETOFF                                       309089
     C   12              DO
     C                   UPDATE    SUBFIL
     C                   EXSR      MOVB2
     C                   END
     C  N12              EXSR      TESTB1
     C  N12
     CAN 30              GOTO      STCHG
     C     XLIN          IFEQ      0
     C                   Z-ADD     14            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVB1         BEGSR
     C***********************************************
     C                   MOVE      *BLANKS       FEFROM
     C                   MOVE      *BLANKS       FETO
     C                   MOVE      *BLANKS       FELENG
     C                   MOVE      *ZEROS        FEDATE
     C                   MOVE      *ZEROS        WEDATE
     C                   MOVE      *BLANKS       FECURR
     C                   MOVE      *ZEROS        FEPRI1
     C                   MOVE      *ZEROS        FEPRI2
     C*
     C                   MOVEL     '*DEL*'       XDLT
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVB2         BEGSR
     C***********************************************
     C                   SETOFF                                       40
     C                   MOVE      *BLANKS       WSFROM
     C                   MOVE      *BLANKS       WSTO
     C                   MOVE      *BLANKS       WSLENG
     C                   MOVE      *ZEROS        WSDATE
     C                   MOVE      *BLANKS       WSCURR
     C                   MOVE      *ZEROS        WSPRI1
     C                   MOVE      *ZEROS        WSPRI2
     C*
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVSIW        BEGSR
     C***********************************************
     C                   MOVE      FEFROM        WSFROM
     C                   MOVE      FETO          WSTO
     C                   MOVE      FELENG        WSLENG
     C                   MOVE      FEDTD         WSDTD
     C                   MOVE      FEDTM         WSDTM
     C                   MOVE      FEDTÅ         WSDTÅ
     C                   MOVE      FECURR        WSCURR
     C                   MOVE      FEPRI1        WSPRI1
     C                   MOVE      FEPRI2        WSPRI2
     C*
     C                   SETON                                        40
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     31            POSNBR
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVWSI        BEGSR
     C***********************************************
     C* FIX KEY IF NOT FOUND
     C   33              DO
     C                   MOVE      WSFROM        FEFROM
     C                   MOVE      WSTO          FETO
     C                   MOVE      WSLENG        FELENG
     C                   END
     C*
     C* FIXES VARIABLES
     C                   MOVE      WSCURR        FECURR
     C                   MOVE      WSPRI1        FEPRI1
     C                   MOVE      WSPRI2        FEPRI2
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     *LIKE         DEFINE    FEFROM        YSFROM
     C     *LIKE         DEFINE    FETO          YSTO
     C     *LIKE         DEFINE    FELENG        YSLENG
     C     KODSIK        KLIST
     C                   KFLD                    WSFIRM
     C                   KFLD                    WSFROM
     C                   KFLD                    WSTO
     C                   KFLD                    WSLENG
     C                   KFLD                    FEDATE
     C     KODSI1        KLIST
     C                   KFLD                    WSFIRM
     C                   KFLD                    W2FROM
     C                   KFLD                    W2TO
     C                   KFLD                    W2LENG
     C                   KFLD                    YSDATE
     C     KODSI3        KLIST
     C                   KFLD                    WSFIRM
     C                   KFLD                    YSFROM
     C                   KFLD                    YSTO
     C                   KFLD                    YSLENG
     C                   KFLD                    YSDATE
     C*
     C     LEVEK         KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    WSFIRM
     C*
     C     VALKEY        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    WSCURR
     C*
     C                   READ      FIRM                                   33
     C*
     C     STI           TAG
     C                   EXFMT     BILDE1
     C   03              GOTO      UTI
     C   04              DO
     C                   MOVE      *BLANKS       UUSNAV           30
     C                   CALL      'SO1LEV'
     C                   PARM                    WSFIRM
     C                   PARM                    FIFIRM
     C                   PARM                    UUSNAV
     C                   END
     C     LEVEK         CHAIN     LEVEL01                            44
     C   44              MOVE      'SPM0007'     MSGNR
     C   44              GOTO      STI
     C                   MOVE      WSFIRM        FEFIRM
     C                   MOVE      LNAVN         WSNAVN
     C*
     C     UTI           ENDSR
     C*
