      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/TJTL06 MODULE(*LIBL/TJTL06 *LIBL/INCGI)
CRTPGM*        ACTGRP(*NEW) AUT(*USE)
      *
      *********************************************************************
      /copy SYSPEDS/qrpgsrcpy,hspecs
      /copy SYSPEDS/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FMERKNF    IF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy SYSPEDS/qrpgsrcpy,prototypeb
      /copy SYSPEDS/qrpgsrcpy,usec
      /copy SYSPEDS/qrpgsrcpy,variables3
      *
      * Varible for
     D WSUSER          S             10
     D WSGN            S             15
     D WSTYPE          S              1
     D WSLENKE         S             50
     D TstGn           S             15
     D JSONstring      s          32000
     D Error           S            150

      *get input-string
      /copy syspeds/qrpgsrcpy,prolog3

      * Parse input
     C                   exsr      Parse
      * Open files etc
     C                   EXSR      INIT
      *
     c                   callp     gethtmlifs(
     C                             '/syspeddev/html/JSON.html':
     C                             '/CGITAG/')
     C                   callp     wrtsection('top')
      *
     c                   if        Error = *blanks
      *
      * må finnes minst EN merknad
     C                   if        wstype = 'P'
      *P=posisjon/på dette ene godsnummeret
     C     WSGN          chain     MERKNF
     C                   if        not %found
     c                   eval      Error = 'Det finnes ingen merknader '
     c                             + 'på godsnummer '
     c                             + %trim(WSGN)
     C                   endif
     C                   else
      * D=Dag - alle denne dag / på et godsnr med dette dagnummer(12 første)
     c                   eval      TstGN = %subst(WSGN:1:12)
     C     TstGn         setll     MERKNF
     C                   read      MERKNF
     C                   if        %eof or
     c                             TstGN <> %subst(GOGN:1:12)
     c                   eval      Error = 'Det finnes ingen merknader '
     c                             + 'på godsnummer '
     c                             + %trim(TstGN)+'001 - '
     c                             + %trim(TstGN)+'999'
     C                   endif
     C                   endif
      *
     C                   endif
      *
     c                   if        Error = *blanks
     c                   call      'TJTL06CL'
     c                   parm                    WSGN
     c                   parm                    WSTYPE
     c                   parm                    WSlenke
      *
     C                   endif
      * ............................................................................................
      * skriv JSON-Object start..(alltid '{' + x ant param. m/komma mellom (IKKE komma etter siste))
      * ............................................................................................
      *
      /free
        JSONstring='{'
        +'¬user¬ : ¬'+%trim(WSUSER)+'¬, '
        +'¬gogn¬ : ¬'+%trim(WSGN)+'¬, '
        +'¬type¬ : ¬'+%trim(WSTYPE)+'¬, '
        +'¬lenke¬ : ¬'+%trim(WSLENKE)+'¬, '
        +'¬errMsg¬ : ¬'+%trim(Error)+'¬}';
      /end-free
      * JSONfix escaper " i tekst,  for deretter å bytte ¬->"
     c                   call      'JSONFIX'
     c                   parm                    JSONstring
     C                   callp     updHTMLvar('JSONstring':JSONstring)
     C                   callp     wrtsection('JSONlin')
      *
      * Quit
     C                   exsr      Exit


      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Lukk fil
     C                   CLOSE     BRIDF
     C  N50              CLOSE     MERKNF

      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   MOVE      *ON           *INLR
     C                   return
     C                   endsr
      *
      *********************************************************
     C     Parse         Begsr
      *********************************************************
      * Get input
     C                   EVAL      WSUSER  = zhbgetvar('USER')
     C                   EVAL      WSGN = zhbgetvar('GOGN')
      * P= ETT godsnummer (P i grønnskjerm)
      * D= ALLE en dag (D i grønnskjerm)
     C                   EVAL      WSTYPE = zhbgetvar('TYPE')
     C
     c                   endsr
      *
      *********************************************************
     C     INIT          Begsr
      *********************************************************
     C                   OPEN      BRIDF
      * Test innlogging
     C     WSUSER        CHAIN     BRIDF                              50
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C                   call      BILIBL
     C                   end
      *
     C   50              eval      Error = 'Invalid userID'
     C  N50              OPEN      MERKNF

     c                   endsr
      *
