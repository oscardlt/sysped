     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A          R BILDE
     A                                      CF03(03 'Avslutt')
     A                                  1  2'SYSPED/SYSJ40'
     A                                      COLOR(BLU)
     A                                  1 62DATE
     A                                      EDTCDE(Y)
     A                                  1 73TIME
     A                                  1 20MSGCON(40 DIV1402 SYMSGF)
     A                                      COLOR(WHT)
     A                                  2 20MSGCON(40 DIV1616 SYMSGF)
     A                                      COLOR(WHT)
     A                                  6  2MSGCON(26 DIV1404 SYMSGF)
     A            WSAVD          4Y 0B  6 32DSPATR(UL)
     A                                      EDTCDE(4)
     A  99                                  DSPATR(RI)
     A  99                                  DSPATR(PC)
     A                                  8  2MSGCON(26 DIV1405 SYMSGF)
     A            WSSG           3A  B  8 32DSPATR(UL)
     A  98                                  DSPATR(RI)
     A  98                                  DSPATR(PC)
     A                                 10  2MSGCON(26 DIV1615 SYMSGF)
     A            WSAO           1A  B 10 32DSPATR(UL)
     A N99N98                               DSPATR(PC)
     A                                      VALUES(' ' 'N' 'J')
     A            WSDT           6Y 0B 10 34DSPATR(UL)
     A                                      EDTCDE(4)
     A                                 10 48MSGCON(10 DIV0259 SYMSGF)
     A                                 12  2MSGCON(26 DIV1407 SYMSGF)
     A            WSFAK          1A  B 12 32DSPATR(UL)
     A                                      VALUES(' ' 'J' 'N')
     A                                 12 48MSGCON(10 DIV0259 SYMSGF)
     A                                 14  2MSGCON(26 DIV3893 SYMSGF)
     A            WSCMR          1A  B 14 32DSPATR(UL)
     A                                      VALUES(' ' 'J' 'N')
     A                                 14 48MSGCON(10 DIV0259 SYMSGF)
     A                                 15  2MSGCON(026 DIV1502 *LIBL/SYMSGF)
     A            WSVF           1A  B 15 32DSPATR(UL)
     A                                      VALUES(' ' 'J' 'N')
     A                                 15 48MSGCON(010 DIV0259 *LIBL/SYMSGF)
     A                                 17  2MSGCON(26 DIV1408 SYMSGF)
     A            WSJBKØ         1A  B 17 32DSPATR(UL)
     A                                      VALUES(' ' 'J' 'N')
     A                                 17 48MSGCON(10 DIV0259 SYMSGF)
     A                                 19  2MSGCON(26 DIV1409 SYMSGF)
     A            WSPRT         10A  B 19 32DSPATR(UL)
     A                                      COLOR(GRN)
     A                                 23  2MSGCON(18 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                             24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
