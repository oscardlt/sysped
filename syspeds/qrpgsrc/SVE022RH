     FSVEH      IF   E           K DISK
     FSVEZ      IF   E           K DISK
     FSVEV1     UF   E           K DISK
     FSVEHE     O  A E             DISK
      *____________________
      * DS for input string
      *""""""""""""""""""""
     D                 DS
     D  A                      1     70
     D                                     DIM(70)                              Input string
     D  ADS                    1     70
      *_____________________
      * DS for output string
      *"""""""""""""""""""""
     D                 DS
     D  B                      1     80
     D                                     DIM(80)                              Output string
     D  SVEHE_DTA              1     80
      *________________________________________
      * DS for å venstrestille numeriske felter
      *""""""""""""""""""""""""""""""""""""""""
     D                 DS
     D  ØN154                  1     15  4
     D  ØA154H                 1     11
     D  ØA154D                12     15
     D                 DS
     D  ØA16                   1     16
     D  Ø16                    1     16
     D                                     DIM(16)                              Num edit
      *____________
      * DS for EORI
      *""""""""""""
     D                 DS
     D  WEORI                  1     17
     D  WEORIA                 1      2
     D  WEORIB                 3     17
      *//////////////
      *  Hodelogikk /
      *//////////////
     C                   EXSR      SRA
     C                   EXSR      SRB
     C                   MOVE      '1'           *INLR
      *////////////////////////////////////////////////////////////
      *  Initiering                                           SRA /
      *////////////////////////////////////////////////////////////
     C     SRA           BEGSR
      *___________________
      * Definiere Workfelt
      *"""""""""""""""""""
     C     *LIKE         DEFINE    SVEH_SYAV     UVEH_SYAV
     C     *LIKE         DEFINE    SVEH_SYOP     UVEH_SYOP
     C     *LIKE         DEFINE    SVEV_VANO     UVEV_VANO
      *
     C                   Z-ADD     0             ØX                2 0
     C                   MOVE      *BLANKS       ØAN              12
     C                   Z-ADD     0             W0074             7 0
     C                   Z-ADD     80            LEN               5 0
     C                   MOVEL     'EUNOAO  '    TABLE            10
     C                   MOVEL     '*LIBL   '    LIB              10
      *__________________
      * Definiere Nøkkler
      *""""""""""""""""""
     C     KEYA          KLIST
     C                   KFLD                    UVEH_SYAV
     C                   KFLD                    UVEH_SYOP
     C     KEYB          KLIST
     C                   KFLD                    SVEV_SYAV
     C                   KFLD                    SVEV_SYOP
     C                   KFLD                    SVEV_SYLI
      *_____________
      * Init av felt
      *"""""""""""""
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVE      WDT           WDT80             8 0
      * Workfields for Array's
     C                   Z-ADD     0             AX                2 0          Index for A
     C                   Z-ADD     1             BX                2 0          Index for B
     C                   Z-ADD     0             AXV               2 0          Leftindx A
     C                   Z-ADD     0             AXH               2 0          Rightindx A
     C                   MOVE      *BLANKS       WCHAR             1            Test character
     C                   MOVE      'Y'           WCHART            1            Test character
      * Workfields for separation character  '
     C                   BITON     '123457'      WAPSTR            1
     C                   BITOFF    '06'          WAPSTR
     C                   EVAL      SVEHE_SYA=UVEH_SYAV
     C                   EVAL      SVEHE_SYO=UVEH_SYOP
      *_________________
      * Input parametrer
      *"""""""""""""""""
     C     *ENTRY        PLIST
     C                   PARM                    UVEH_SYAV
     C                   PARM                    UVEH_SYOP
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Start av file                                        SRB /
      *////////////////////////////////////////////////////////////
     C     SRB           BEGSR
     C     KEYA          CHAIN     SVEH

      *_____________
      * Huvudsektion
      *"""""""""""""
      * Meddelandeingress
     C                   EXSR      UNH00
      * Meddelandebørjan
     C                   EXSR      BGM00
      * Plats lokaliseringsidentifikation
     C                   EXSR      LOC00
      * Generell indikator
     C                   EXSR      GIS00
      * Referenser
     C                   EXSR      RFF01
      * Transportdetaljer
     C                   EXSR      TDT04
      * Namm och adress, Avsændare
     C                   EXSR      NAD06C
      * Seksjonskille
     C                   EXSR      UNSD
      *______________
      * Detaljsektion
      *""""""""""""""
      * Skrive ut meldingsvarelinjer til EDIFACT-fil
     C     SVEH_MTYP     IFEQ      'UGO'
     C                   EXSR      CST30
     C                   ENDIF
      * Seksjonskille
     C                   EXSR      UNSS
      *_____________
      * Summasektion
      *"""""""""""""
      * Meddelandeslut
     C                   EXSR      UNT00
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR for output string, read input from left              SRÅ
      *////////////////////////////////////////////////////////////
     C     SRÅ           BEGSR
      *_________________
      * Trim left blanks
      *"""""""""""""""""
     C                   MOVEL(P)  ADS           ADS2             70
     C                   EVAL      ADS=*BLANKS
     C                   EVAL      ADS=%TRIML(ADS2)
      *_______________
      * Find positions
      *""""""""""""""""
     C                   EVAL      AXV=1
     C     ' '           CHECKR    ADS           AXH
      *________________________________________________
      * Read input string and place it in output string
      *""""""""""""""""""""""""""""""""""""""""""""""""
     C     AXV           DO        AXH           AXV                            <-------------I
     C                   MOVE      A(AXV)        WCHAR                                        I
      * If invalid character (' + : ?) then replace with 'Ø'             I
     C     WCHART        IFEQ      'Y'                                           <-----------II
     C     WCHAR         IFEQ      ';'                                                      III
     C                   MOVE      ':'           WCHAR
     C                   ENDIF                                                    <---------III
     C     WCHAR         COMP      WAPSTR                                 83                III
     C  N83WCHAR         COMP      '+'                                    83                III
     C  N83WCHAR         COMP      ':'                                    83                III
     C  N83WCHAR         COMP      '?'                                    83                III
     C   83              EXSR      SRÅA                                                     III
     C                   END                                                     <-----------II
      * Move to output string                                            I
     C                   MOVE      WCHAR         B(BX)                                        I
     C                   ADD       1             BX                                           I
      * If arrayindex for output array is over 80 Then add a record      I
     C     BX            IFGT      80                                            <-----------II
     C                   WRITE     SVEHER                                                    II
     C                   Z-ADD     1             BX                                          II
     C                   MOVE      *BLANKS       SVEHE_DTA                                   II
     C                   END                                                     <-----------II
      *                                                                  I
     C                   END                                                    <-------------I
      *
     C                   MOVE      'Y'           WCHART
     C                   EVAL      ADS=*BLANKS
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR for replace control characters                      SRÅA
      *////////////////////////////////////////////////////////////
     C     SRÅA          BEGSR
      * Move '?' before the character
     C                   MOVE      '?'           B(BX)
     C                   ADD       1             BX
     C     BX            IFGT      80                                           <-------------I
     C                   WRITE     SVEHER                                                     I
     C                   Z-ADD     1             BX                                           I
     C                   MOVE      *BLANKS       SVEHE_DTA                                    I
     C                   END                                                    <-------------I
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Venstrestill numeriske verdier                      SRNE /
      *////////////////////////////////////////////////////////////
     C     SRNE          BEGSR
      * Flytte in rett verdi i Alfa 12-felt
     C                   MOVE      *ZEROS        ØA16
     C     ØA154D        IFEQ      '0000'                                       <-------------I
     C                   MOVE      ØA154H        ØA16                                         I
     C                   ELSE                                                   <-------------I
     C                   MOVEL     ØA154H        ØA16                                         I
     C                   MOVE      '.    '       ØA16                                         I
     C                   MOVE      ØA154D        ØA16                                         I
     C     Ø16(16)       IFEQ      '0'
     C                   MOVE      ' '           Ø16(16)
     C     Ø16(15)       IFEQ      '0'
     C                   MOVE      ' '           Ø16(15)
     C                   END
     C                   END
     C                   END                                                    <-------------I
      * lete fra venstre og finn foerste siffer over 0
     C     1             DO        15            ØX                             <-------------I
     C     Ø16(ØX)       IFNE      '0'                                           <-----------II
     C                   GOTO      SRNE1                                                     II
     C                   END                                                     <-----------II
     C                   END                                                    <-------------I
     C     SRNE1         TAG
      * Venstrestill til et nytt felt
     C                   MOVE      *BLANKS       ØAN
     C                   MOVEA     Ø16(ØX)       ØAN
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Meddelandeingress                                  UNH00 /
      *////////////////////////////////////////////////////////////
     C     UNH00         BEGSR
      * Segmentnavn
      * W0074  --->  Teller for antall segment
     C                   Z-ADD     1             W0074
     C                   MOVEL     'UNH+'        ADS
     C                   MOVE      'N'           WCHART
     C                   EXSR      SRÅ
      * Meldingsreferanse
     C                   MOVEL     SVEH_SYAV     W0062            11
     C                   MOVE      SVEH_SYOP     W0062
     C                   MOVEL     W0062         ADS
     C                   EXSR      SRÅ
      *
     C                   MOVEL     '+'           ADS
     C                   MOVE      'N'           WCHART
     C                   EXSR      SRÅ
      * Meldingstype  (= CUSDEC)
     C                   MOVEL     'CUSDEC'      ADS
     C                   EXSR      SRÅ
      *
     C                   MOVEA     ':D:96B:U'    A(1)
     C                   MOVEA     'N:145050'    A(9)
     C                   MOVE      'N'           WCHART
     C                   EXSR      SRÅ
      * Slutt segment
     C                   MOVEL(P)  WAPSTR        ADS
     C                   MOVE      'N'           WCHART
     C                   EXSR      SRÅ
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Meddelandebørjan                                   BGM00 /
      *////////////////////////////////////////////////////////////
     C     BGM00         BEGSR
      * Segmentnavn
     C                   ADD       1             W0074
     C                   EVAL      ADS='BGM+'+SVEH_MTYP+':::'+SVEH_DEK1+'++'
     C                             + SVEH_DEK2+WAPSTR
     C                   MOVE      'N'           WCHART
     C                   EXSR      SRÅ
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Varupostnivå                                       CST30 /
      *////////////////////////////////////////////////////////////
     C     CST30         BEGSR

     C                   EVAL      *IN20=*OFF
     C                   EVAL      UVEV_VANO=0
     C     KEYA          SETLL     SVEV1
     C     KEYA          READE     SVEV1                                  20
     C     *IN20         DOWEQ     *OFF
     C                   ADD       1             UVEV_VANO
     C                   EVAL      SVEV_VANO=UVEV_VANO
     C                   UPDATE    SVEVR

     C                   ADD       1             W0074
     C                   EVAL      ØN154=SVEV_VANO
     C                   EXSR      SRNE
     C                   EVAL      ADS='CST+'+%trimr(ØAN)+'+'+SVEV_VATA+WAPSTR
     C                   MOVE      'N'           WCHART
     C                   EXSR      SRÅ
      * Andra segment under CST
     C                   EXSR      RFF35

      * Læs næsta varulinje
     C     KEYA          READE     SVEV1                                  20
     C  N20              ENDDO
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Generell indikator                                 GIS00 /
      *////////////////////////////////////////////////////////////
     C     GIS00         BEGSR
      *__________
      * Container
      *""""""""""
     C     SVEH_CONT     IFNE      *BLANKS
     C                   ADD       1             W0074
     C                   EVAL      ADS='GIS+'+%trimr(SVEH_CONT)+':109'+WAPSTR
     C                   MOVE      'N'           WCHART
     C                   EXSR      SRÅ
     C                   ENDIF
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Plats/lokaliseringsidentifikation                  LOC00 /
      *////////////////////////////////////////////////////////////
     C     LOC00         BEGSR
      *__________________
      * Utfartstullkontor
      *""""""""""""""""""
     C     SVEH_UTFA     IFNE      *BLANKS
     C                   ADD       1             W0074
     C                   EVAL      ADS='LOC+17+'+SVEH_UTFA+WAPSTR
     C                   MOVE      'N'           WCHART
     C                   EXSR      SRÅ
     C                   ENDIF
      *______________
      * Lastplatskode
      *""""""""""""""
     C     SVEH_LAST     IFNE      *BLANKS
     C                   ADD       1             W0074
     C                   EVAL      ADS='LOC+9+'+SVEH_LAST+WAPSTR
     C                   MOVE      'N'           WCHART
     C                   EXSR      SRÅ
     C                   ENDIF

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Namn och adress, Avsændare                        NAD06C /
      *////////////////////////////////////////////////////////////
     C     NAD06C        BEGSR
     C                   EVAL      WEORI=SVEH_AVEO
      * Segmentnavn
     C                   ADD       1             W0074
     C                   EVAL      ADS='NAD+EX+'+%trimr(WEORIB)
     C                             +':'+%trimr(WEORIA)
     C                             +WAPSTR
     C                   MOVE      'N'           WCHART
     C                   EXSR      SRÅ
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Seksjonskille                                       UNSD /
      *////////////////////////////////////////////////////////////
     C     UNSD          BEGSR
      * Segmentnavn
      * W0074  --->  Teller for antall segment
     C                   ADD       1             W0074
     C                   MOVEL     'UNS+D'       ADS
     C                   MOVE      'N'           WCHART
     C                   EXSR      SRÅ
      * Slutt segment
     C                   MOVEL     WAPSTR        ADS
     C                   MOVE      'N'           WCHART
     C                   EXSR      SRÅ
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Seksjonskille                                       UNSS /
      *////////////////////////////////////////////////////////////
     C     UNSS          BEGSR
      * Segmentnavn
      * W0074  --->  Teller for antall segment
     C                   ADD       1             W0074
     C                   MOVEL     'UNS+S'       ADS
     C                   MOVE      'N'           WCHART
     C                   EXSR      SRÅ
      * Slutt segment
     C                   MOVEL     WAPSTR        ADS
     C                   MOVE      'N'           WCHART
     C                   EXSR      SRÅ
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Referens                                           RFF01 /
      *////////////////////////////////////////////////////////////
     C     RFF01         BEGSR
      *_______
      * Tullid
      *"""""""
     C                   ADD       1             W0074
     C                   EVAL      ADS='RFF+ZTI:'+%trimr(SVEH_TUID)+WAPSTR
     C                   MOVE      'N'           WCHART
     C                   EXSR      SRÅ
      *_____________________
      * Klareringsenhets id.
      *"""""""""""""""""""""
     C     SVEH_KENH     IFNE      *BLANKS
     C                   ADD       1             W0074
     C                   EVAL      ADS='RFF+CLU:'+%trimr(SVEH_KENH)+WAPSTR
     C                   MOVE      'N'           WCHART
     C                   EXSR      SRÅ
     C                   ENDIF
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Referens                                           RFF35 /
      *////////////////////////////////////////////////////////////
     C     RFF35         BEGSR
      *__________
      * Container
      *""""""""""
     C     SVEH_MTYP     IFNE      'TNU'
     C     SVEH_CONT     IFEQ      '1'
     C                   ADD       1             W0074
     C                   EVAL      ADS='RFF+AAQ:'+%trimr(SVEH_CONN)+WAPSTR
     C                   MOVE      'N'           WCHART
     C                   EXSR      SRÅ
      * Finns det fler containernr ?
     C                   EXSR      RFF35Z
     C                   ENDIF
     C                   ENDIF
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Referens x-tra contanernr                         RFF35Z /
      *////////////////////////////////////////////////////////////
     C     RFF35Z        BEGSR
     C     KEYA          CHAIN     SVEZ                               22
     C     *IN22         IFEQ      *OFF
     C     SVEZ_CON2     IFNE      *BLANKS
     C                   ADD       1             W0074
     C                   EVAL      ADS='RFF+AAQ:'+%trimr(SVEZ_CON2)+WAPSTR
     C                   MOVE      'N'           WCHART
     C                   EXSR      SRÅ
     C                   ENDIF
     C     SVEZ_CON3     IFNE      *BLANKS
     C                   ADD       1             W0074
     C                   EVAL      ADS='RFF+AAQ:'+%trimr(SVEZ_CON3)+WAPSTR
     C                   MOVE      'N'           WCHART
     C                   EXSR      SRÅ
     C                   ENDIF
     C     SVEZ_CON4     IFNE      *BLANKS
     C                   ADD       1             W0074
     C                   EVAL      ADS='RFF+AAQ:'+%trimr(SVEZ_CON4)+WAPSTR
     C                   MOVE      'N'           WCHART
     C                   EXSR      SRÅ
     C                   ENDIF
     C     SVEZ_CON5     IFNE      *BLANKS
     C                   ADD       1             W0074
     C                   EVAL      ADS='RFF+AAQ:'+%trimr(SVEZ_CON5)+WAPSTR
     C                   MOVE      'N'           WCHART
     C                   EXSR      SRÅ
     C                   ENDIF
     C     SVEZ_CON6     IFNE      *BLANKS
     C                   ADD       1             W0074
     C                   EVAL      ADS='RFF+AAQ:'+%trimr(SVEZ_CON6)+WAPSTR
     C                   MOVE      'N'           WCHART
     C                   EXSR      SRÅ
     C                   ENDIF
     C     SVEZ_CON7     IFNE      *BLANKS
     C                   ADD       1             W0074
     C                   EVAL      ADS='RFF+AAQ:'+%trimr(SVEZ_CON7)+WAPSTR
     C                   MOVE      'N'           WCHART
     C                   EXSR      SRÅ
     C                   ENDIF
     C     SVEZ_CON8     IFNE      *BLANKS
     C                   ADD       1             W0074
     C                   EVAL      ADS='RFF+AAQ:'+%trimr(SVEZ_CON8)+WAPSTR
     C                   MOVE      'N'           WCHART
     C                   EXSR      SRÅ
     C                   ENDIF
     C     SVEZ_CON9     IFNE      *BLANKS
     C                   ADD       1             W0074
     C                   EVAL      ADS='RFF+AAQ:'+%trimr(SVEZ_CON9)+WAPSTR
     C                   MOVE      'N'           WCHART
     C                   EXSR      SRÅ
     C                   ENDIF
     C                   ENDIF
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Transportdetaljer                                  TDT04 /
      *////////////////////////////////////////////////////////////
     C     TDT04         BEGSR
     C     SVEH_MTYP     IFNE      'TNU'
      *______________________________
      * Typ av inrikes transportmedel
      *""""""""""""""""""""""""""""""
     C     SVEH_TRIN     IFNE      *BLANKS
     C                   ADD       1             W0074
     C                   EVAL      ADS='TDT+1++'+%trimr(SVEH_TRIN)+WAPSTR
     C                   MOVE      'N'           WCHART
     C                   EXSR      SRÅ
     C                   ENDIF
      *____________________________
      * Typ av græns transportmedel
      *""""""""""""""""""""""""""""
     C     SVEH_TRGR     IFNE      *BLANKS
     C                   ADD       1             W0074
     C                   EVAL      ADS='TDT+11++'+%trimr(SVEH_TRGR)
     C                             +'+++++:::'
     C                             +%trimr(SVEH_TRAI)+':'+%trimr(SVEH_TRAL)
     C                             +WAPSTR
     C                   MOVE      'N'           WCHART
     C                   EXSR      SRÅ
     C                   ENDIF
      *__________________________
      * 18. Tr.medl.id vid avgång
      *""""""""""""""""""""""""""
     C     SVEH_TRID     IFNE      *BLANKS
     C                   ADD       1             W0074
     C                   EVAL      ADS='TDT+12+++++++:::'
     C                             + %trimr(SVEH_TRID)+WAPSTR
     C                   MOVE      'N'           WCHART
     C                   EXSR      SRÅ
     C                   ENDIF
     C                   ENDIF
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Meldingsslutt                                      UNT00 /
      *////////////////////////////////////////////////////////////
     C     UNT00         BEGSR
      * Segmentnavn
     C                   ADD       1             W0074
     C                   MOVEL     'UNT+'        ADS
     C                   MOVE      'N'           WCHART
     C                   EXSR      SRÅ
      * Antall segmenter i meldingen (inkludert UNH og UNT)
     C                   EVAL      ØN154=W0074
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           ADS
     C                   EXSR      SRÅ
      *
     C                   MOVEL     '+'           ADS
     C                   MOVE      'N'           WCHART
     C                   EXSR      SRÅ
      * Meldingsreferanse
     C                   MOVEL     W0062         ADS
     C                   EXSR      SRÅ
      * Slutt segment
     C                   MOVEL     WAPSTR        ADS
     C                   MOVE      'N'           WCHART
     C                   EXSR      SRÅ
      *
     C     BX            IFGT      1
     C                   WRITE     SVEHER
     C                   ENDIF
      *
     C                   ENDSR
