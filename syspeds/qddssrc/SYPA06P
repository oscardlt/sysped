     A*****************************************************************
     A*   KONTOUTDRAG (TIDLIGERE PURREDOKUMENT)                       *
     A*****************************************************************
     A          R LASER1
     A            KFMT1         75A     1  1
     A          R LASER2
     A            KFMT2         75A     1  1
     A          R HEAD                      SKIPB(1)
     A            PFHT1         79A        2HIGHLIGHT
     A            PFHT2         79A        2HIGHLIGHT
     A                                      SPACEB(1)
     A            PFHT3         79A        2HIGHLIGHT
     A                                      SPACEB(1)
     A            PFHT4         79A        2HIGHLIGHT
     A                                      SPACEB(1)
     A            PFHT5         79A        2HIGHLIGHT
     A                                      SPACEB(1)
     A            PFHT6         79A        2HIGHLIGHT
     A                                      SPACEB(1)
     A            PFHT7         79A        2HIGHLIGHT
     A                                      SPACEB(2)
      *
     A            XKNAVN        30A       11
     A                                      SPACEB(1)
     A                                    54'************************'
     A                                      HIGHLIGHT
      *
     A            XADR1         30A       11
     A                                      SPACEB(1)
     A                                    54MSGCON(24 JOV0990 SYMSGF)
     A                                      HIGHLIGHT
      *
     A            XADR2         30A       11
     A                                      SPACEB(1)
     A                                    54MSGCON(24 JOV0970 SYMSGF)
     A                                      HIGHLIGHT
     A            SIDE           4  0     71EDTCDE(Z) HIGHLIGHT
      *
     A            XPOSTN         4  0     11
     A                                      SPACEB(1)
     A            XADR3         24A       17
      *
     A                                    54MSGCON(12 JOV0653 SYMSGF)
     A            XXNR           8  0     70EDTCDE(3) HIGHLIGHT
      *
     A                                    54MSGCON(12 DIV0199 SYMSGF)
     A                                      SPACEB(1)
     A            XXKUNR         8  0     70EDTCDE(3) HIGHLIGHT
      *
     A  70                                54MSGCON(12 JOV0967 SYMSGF)
     A                                      SPACEB(1)
     A  70        XXBILN        10        68HIGHLIGHT
      *
     A  70                                54MSGCON(12 JOV0971 SYMSGF)
     A                                      SPACEB(1)
     A  70        BISIDE         4  0     68EDTCDE(Z) HIGHLIGHT
      *
     A                                    54MSGCON(12 JOV0991 SYMSGF)
     A                                      SPACEB(1)
     A            FOMDAT         6  0     70EDTCDE(Y) HIGHLIGHT
      *
     A                                    54MSGCON(12 JOV0652 SYMSGF)
     A                                      SPACEB(1)
     A            TOMDAT         6  0     70EDTCDE(Y) HIGHLIGHT
      *
     A                                    54MSGCON(12 JOV0651 SYMSGF)
     A                                      SPACEB(1)
     A                                    70DATE EDTCDE(Y) HIGHLIGHT
      *
     A                                     1MSGCON(79 JOV0994 SYMSGF)
     A                                      SPACEB(2)
      *
     A                                     1' -----------------------'
     A                                      SPACEB(1)
     A                                    25'------------------------'
     A                                    49'------------------------'
     A                                    73'------'
     A
     A          R DET                       SPACEB(1)
     A            PAKVNR         9  0      1EDTCDE(Z)
     A            PALØNR         7  0     11EDTCDE(Z)
     A            PAKVDT         8  0     19EDTCDE(Z)
     A            PABILN        10A       28
     A  62                                  HIGHLIGHT
     A            PATXT         20A       38
     A            XXMANT         5  0     59EDTCDE(Z)
     A            XXLANT         5  0     66EDTCDE(Z)
     A            PAPUNR         7  0     72EDTCDE(Z)
     A          R PINNSA                    SPACEB(1)
     A                                     2MSGCON(20 SHU0737 SYMSGF)
     A            XXSALD         5  0     25EDTCDE(Z)
     A            XXMANT         5  0     59EDTCDE(Z)
     A            XXLANT         5  0     66EDTCDE(Z)
     A          R TOTTXT                    SPACEB(5)
     A                                     2MSGCON(75 JOV0968 SYMSGF)
     A                                      SPACEA(2)
     A                                     2MSGCON(75 JOV0993 SYMSGF)
     A                                      SPACEA(7)
     A          R TOT                       SPACEB(2) HIGHLIGHT
     A                                     2MSGCON(30 JOV0932 SYMSGF)
     A            TOTMAN         5  0     59EDTCDE(Z)
     A            TOTLAN         5  0     66EDTCDE(Z)
     A                                     2MSGCON(10 SHU0738 SYMSGF)
     A                                      SPACEB(2)
     A            TOMDAT         6  0     14EDTCDE(Y) HIGHLIGHT
      * REDUSERT MED 25
     A            TOTANT         5  0     25EDTCDE(Z)
     A  60                                33MSGCON(16 JOV0928 SYMSGF)
     A N60                                33MSGCON(16 JOV0929 SYMSGF)
     A                                      HIGHLIGHT
