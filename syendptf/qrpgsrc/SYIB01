      ***********************************************************
      *** PROGRAM SKAL VEDLIKEHOLDE betalingsbetingelser      ***
      ***********************************************************
      *
     FFIRM      IF   E           K DISK
     FKODAL01   UF A E           K DISK
     FSYib01fm  CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D                 DS
     D  BETTXT                 1     30
     D  XDLT                   1      7
     C*
     C                   EXSR      INIT
     C*
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     keysok        SETLL     KODAL01
     C     START1        TAG
     C     keysok        READE(N)  KODAL01                                94
      *
     C     START2        TAG
      *
     C  N94              DO
     C                   DO        15
     C                   ADD       1             ZRECNO
     C                   WRITE     SUBFIL
     C     KEYSOK        READE(N)  KODAL01                                94
     C  N94              END
     C                   END
     C                   Z-ADD     ZRECNO        YRECNO            4 0
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     3             POSNBR
     C*
     C     START3        TAG
     C                   SETOFF                                       9998
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYVARE
     C                   ELSE
     C                   WRITE     SUBCTL
     C                   EXFMT     BUNN
     C                   END
     C*
     C***
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C                   Z-ADD     WSSUBN        XRECNO            4 0
     C***
     C*
     C                   SETOFF                                       9830
     C*** AVSLUTT
     C   03              GOTO      SLUTT
     C*** FRISK OPP
     C   05              GOTO      START
     C*** LAGE NY
     C   06              DO
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     6             POSNBR
     C                   GOTO      START5
     C                   END
     C*
     C*** BLA VIDERE
     C   25              DO
     C                   Z-ADD     YRECNO        ZRECNO
     C                   SETOFF                                       25
     C                   GOTO      START2
     C                   END
     C*
     C     ZRECNO        IFNE      0
     C                   EXSR      HENT
     C                   END
     C   98XRECNO        IFNE      0
     C                   Z-ADD     XRECNO        ZRECNO
     C                   END
     C                   GOTO      START3
     C*
     C*** LAGE NY
     C*
     C     START5        TAG
     C                   EXFMT     VARELI
     C                   MOVEA     '000'         *IN(88)
     C                   SETOFF                                           30
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C*
      *
      *
     C   12              EXSR      MOVB2
     C  N12              EXSR      TESTB1
     C  N12
     CAN 30              GOTO      START5
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     3             POSNBR
     C                   GOTO      START3
     C                   GOTO      START
     C*
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
     C*
     C***********************************************
     C     TESTB1        BEGSR
     C***********************************************
     C  N40              DO
     c     wskey         chain(n)  KODAL01                            33
     C  N33              MOVE      'SHU1898'     MSGNR
     C  N33              SETON                                        3090
     C                   END
     C*
     C   30              GOTO      SLTB1
     C*
     C*
     C*** OPPDATERE PÅ REGISTER OG SUBFIL
     c     wskey         chain     KODAL01                            33
     C                   EXSR      MOVWCE
     C  N33              UPDATE    KODAR
     C   33              WRITE     KODAR
     C   40              UPDATE    SUBFIL
     C  N40              ADD       1             YRECNO
     C  N40              Z-ADD     YRECNO        ZRECNO
     C  N40              WRITE     SUBFIL
     C                   WRITE     SUBCTL
     C                   EXSR      MOVB2
     C*
     C     SLTB1         ENDSR
     C*
     C***********************************************
     C     HENT          BEGSR
     C***********************************************
     C                   Z-ADD     0             LINNBR
     C                   SETOFF                                       919299
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C   98              DO
     C   99              SETOFF                                       98
     C                   Z-ADD     1             LINNBR
     C                   GOTO      SLHENT
     C                   END
     C     ZRECNO        DIV       15            LINNBR
     C                   MVR                     XLIN              2 0
     C     XLIN          IFEQ      0
     C                   Z-ADD     15            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C     WSNR          IFEQ      '2'
     C                   MOVE      ' '           WSNR
     C     XDLT          COMP      '*DELETE'                              98
     C   98              GOTO      SLHENT
     C                   EXSR      CHGCE
     C   12              GOTO      SLHENT
     C                   END
     C     WSNR          IFEQ      '4'
     C                   MOVE      ' '           WSNR
     C     XDLT          COMP      '*DELETE'                              98
     C   98              GOTO      SLHENT
     C                   EXSR      DLTCE
     C   12              GOTO      SLHENT
     C                   END
     C                   END
     C*
     C     SLHENT        TAG
     C                   ADD       4             LINNBR
     C                   Z-ADD     3             POSNBR
     C                   SETOFF                                       99
     C   98              DO
     C                   MOVE      'DIV0012'     MSGNR
     C                   SETON                                            30
     C                   END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     CHGCE         BEGSR
     C***********************************************
     C                   EXSR      MOVCEW
     C                   SETON                                        99
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     11            POSNBR
     C     STCHG         TAG
     C                   EXFMT     VARELI
     C                   MOVEA     '000'         *IN(88)
     C                   SETOFF                                         30
     C*
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C*
     C   12              DO
     C                   UPDATE    SUBFIL
     C                   EXSR      MOVB2
     C                   END
     C  N12              EXSR      TESTB1
     C  N12
     CAN 30              GOTO      STCHG
     C     XLIN          IFEQ      0
     C                   Z-ADD     15            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     DLTCE         BEGSR
     C***********************************************
     C                   SETON                                        99
     C                   EXFMT     VAREL3
     C  N12              DO
     C     keydel        CHAIN     KODAL01                            33
     C  N33              DELETE    KODAR
     C                   EXSR      MOVB1
     C                   END
     C     XLIN          IFEQ      0
     C                   Z-ADD     15            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C                   UPDATE    SUBFIL
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVWCE        BEGSR
     C***********************************************
     C                   MOVE      WSTBET        BETBET
     C                   move      WSTTXT        BETTXT
     C                   move      WSILEV        FRILEV
N01  C                   move      WSDBER        TIDBER
N01  C                   move      WSTTID        BETTID
N01  C                   move      WSTKOD        AKTKOD
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVCEW        BEGSR
     C***********************************************
     C                   MOVE      BETBET        WSTBET
     C                   MOVE      BETTXT        WSTTXT
     C                   MOVE      FRILEV        WSILEV
N01  C                   MOVE      TIDBER        WSDBER
N01  C                   MOVE      BETTID        WSTTID
     C                   MOVE      AKTKOD        WSTKOD
     C                   SETON                                        40
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     12            POSNBR
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVB1         BEGSR
     C***********************************************
     C                   MOVE      *BLANKS       WSTBET
     C                   MOVE      *BLANKS       WSTTXT
     C                   MOVE      *BLANKS       WSILEV
     C                   MOVE      *BLANKS       WSDBER
     C                   MOVE      *ZEROS        WSTTID
     C                   MOVEL     '*DELETE'     XDLT
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVB2         BEGSR
     C***********************************************
     C                   SETOFF                                       40
     C                   MOVE      *BLANKS       WSTBET
     C                   MOVE      *BLANKS       WSTTXT
     C                   MOVE      *BLANKS       WSILEV
     C                   MOVE      *BLANKS       WSDBER
     C                   MOVE      *ZEROS        WSTTID
     C                   MOVE      'A'           WStkod
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     KEYSOK        KLIST
     C                   KFLD                    FIRMA
     C     WSKEY         KLIST
     C                   KFLD                    FIRMA
     C                   KFLD                    WStbet
     C     KEYDEL        KLIST
     C                   KFLD                    FIRMA
     C                   KFLD                    BEtbet
     C     FIFIRM        SETLL     FIRM
     C                   READ      FIRM                                   33
     c                   move      fifirm        firma
     C*
     C                   ENDSR
     C*
