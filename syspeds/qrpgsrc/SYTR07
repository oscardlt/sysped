      *
      **************************************************************
      *** PROGRAM SKAL VEDLIKEHOLDE TILLATELSER                  ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER       ***
      *** LEDIGE INDIKATORER 01 02 04 07 09-11 13-24             ***
      *** LEDIGE INDIKATORER 27-29 31 32 34-39 41-88 96-97       ***
      **************************************************************
      *
     FTILL      UF A E           K DISK
     FTILLL01   IF   E           K DISK
     F                                     RENAME(TILLR:TILLR2)
     FTURER     IF   E           K DISK
     FFIRM      IF   E           K DISK
     FSYTR07FM  CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D                 DS
     D  TINR                   1     15
     D  XDLT                   1      8
     D                 DS
     D  TUDATF                 1      6  0
     D  WFDTD                  1      2  0
     D  WFDTM                  3      4  0
     D  WFDTÅ                  5      6  0
     D                 DS
     D  TUDT                   1      8  0
     D  TUDTÅ                  1      4  0
     D  TUDTM                  5      6  0
     D  TUDTD                  7      8  0
     D                 DS
     D  TUDATT                 1      6  0
     D  WTDTD                  1      2  0
     D  WTDTM                  3      4  0
     D  WTDTÅ                  5      6  0
     D                 DS
     D  TUDTT                  1      8  0
     D  TUDTTÅ                 1      4  0
     D  TUDTTM                 5      6  0
     D  TUDTTD                 7      8  0
     D                 DS
     D  TIDATE                 1      8  0
     D  TIYEAR                 1      4  0
     D  TIMONT                 5      6  0
     D  TIDAY                  7      8  0
     D                 DS
     D  WIDATE                 1      6  0
     D  WIDAY                  1      2  0
     D  WIMONT                 3      4  0
     D  WIYEAR                 5      6  0
     D                 DS
     D  WSDATE                 1      6  0
     D  WSDAY                  1      2  0
     D  WSMONT                 3      4  0
     D  WSYEAR                 5      6  0
     ITILLR2
     I              TIAVD                       XIAVD
     I              TIPRO                       XIPRO
     C*
     C                   MOVE      TIAVD         UUAVD             4 0
     C                   MOVE      TIPRO         UUPRO             8 0
     C*
     C     STINI         TAG
     C                   EXSR      INIT
     C*
     C*** AVSLUTT
     C   03              GOTO      SLUTT
     C*
     C*
     C     START         TAG
     C                   SETOFF                                       56
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
     C                   MOVE      W2NR          YSNR
     C                   MOVE      W2TYPE        YSTYPE
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C  N55KODSI1        SETLL     TILL
     C   55XODSI1        SETLL     TILLL01
     C     START1        TAG
     C  N55KODRED        READE(N)  TILL                                   94
     C   55              READ      TILLL01                                94
     C  N94              MOVE      TINR          YSNR
     C  N94              MOVE      TITYPE        YSTYPE
      *
     C     START2        TAG
      *
     C  N94              DO
     C                   DO        14
     C                   ADD       1             ZRECNO
     C   55XIAVD         COMP      *ZEROS                             56
     C   56TURKEY        CHAIN     TURER                              59
     C     *IN56         IFEQ      '1'
     C     *IN59         ANDEQ     '0'
     C                   MOVE      TUDTD         WFDTD
     C                   MOVE      TUDTM         WFDTM
     C                   MOVE      TUDTÅ         WFDTÅ
     C                   MOVE      TUDTTD        WTDTD
     C                   MOVE      TUDTTM        WTDTM
     C                   MOVE      TUDTTÅ        WTDTÅ
     C                   ELSE
     C                   MOVE      *ZEROS        TUDATF
     C                   MOVE      *ZEROS        TUDATT
     C                   END
     C                   MOVE      TIYEAR        WIYEAR
     C                   MOVE      TIMONT        WIMONT
     C                   MOVE      TIDAY         WIDAY
     C                   WRITE     SUBFIL
     C  N55KODRED        READE(N)  TILL                                   94
     C   55              READ      TILLL01                                94
     C  N94              MOVE      TINR          YSNR
     C  N94              MOVE      TITYPE        YSTYPE
     C  N94              END
     C                   END
     C*
     C                   Z-ADD     ZRECNO        YRECNO            5 0
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     2             POSNBR
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   Z-ADD     4             POSNBR
     C                   EXFMT     NYWRK
     C                   ELSE
     C                   WRITE     SUBCTL
     C*
     C     NY            TAG
     C*
     C                   EXFMT     BUNN
     C                   END
     C*
     C***
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C                   Z-ADD     WSSUBN        XRECNO            4 0
     C     XRECNO        IFEQ      0
     C                   Z-ADD     ZRECNO        XRECNO            4 0
     C                   END
     C***
     C*
     C                   SETOFF                                       9899
     C*
     C*** CHANGE MODE
     C   10              DO
     C     TIAVD         IFNE      *ZEROS
     C                   MOVE      TIAVD         GMAVD
     C                   MOVE      TIPRO         GMPRO
     C                   MOVE      *ZEROS        TIAVD
     C                   MOVE      *ZEROS        TIPRO
     C                   SETON                                        55
     C                   GOTO      START
     C                   ELSE
     C                   MOVE      GMAVD         TIAVD
     C                   MOVE      GMPRO         TIPRO
     C                   MOVE      *ZEROS        GMAVD
     C                   MOVE      *ZEROS        GMPRO
     C                   SETOFF                                       55
     C                   GOTO      START
     C                   END
     C                   END
     C   03KALL          IFNE      'J'
     C                   MOVE      *ZEROS        TIAVD
     C                   MOVE      *ZEROS        TIPRO
     C                   GOTO      STINI
     C                   ELSE
     C                   GOTO      SLUTT
     C                   END
     C*
     C*** FRISK OPP
     C   05              GOTO      START
     C*
     C*** LAGE NY
     C   06              DO
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     04            POSNBR
     C                   EXSR      MOVB2
     C                   GOTO      START5
     C                   END
     C*** SØK FRA
     C   08              GOTO      START6
     C*
     C*** BLA VIDERE
     C   25              DO
     C                   SETOFF                                       25
     C                   Z-ADD     YRECNO        ZRECNO
     C  N94
     CANN55KODSI3        SETLL     TILL
     C  N94
     CAN 55XODSI3        SETLL     TILLL01
     C  N94
     CANN55KODRED        READE(N)  TILL                                   94
     C  N94
     CAN 55              READ      TILLL01                                94
     C                   GOTO      START2
     C                   END
     C*
     C     ZRECNO        IFNE      0
     C                   EXSR      HENT
     C                   END
     C   98XRECNO        IFNE      0
     C                   Z-ADD     XRECNO        ZRECNO
     C                   END
     C                   GOTO      START3
     C*
     C*** LAGE NY
     C     START5        TAG
     C                   EXFMT     VARELI
     C                   SETOFF                                       309089
     C   12              EXSR      MOVB2
     C  N12              EXSR      TESTB1
     C  N12
     CAN 30              GOTO      START5
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     2             POSNBR
     C                   GOTO      NY
     C*
     C*** SØK FRA
     C     START6        TAG
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     04            POSNBR
     C                   EXFMT     VAREL2
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     2             POSNBR
     C   12              GOTO      START3
     C                   GOTO      START
     C*
     C     SLUTT         TAG
     C                   MOVE      UUAVD         TIAVD             4 0
     C                   MOVE      UUPRO         TIPRO             8 0
     C                   SETON                                        LR
     C                   RETURN
     C*
     C***********************************************
     C     TESTB1        BEGSR
     C***********************************************
     C*
     C     WSNR          COMP      *BLANKS                                90
     C  N90WSTYPE        COMP      *BLANKS                                90
     C  N90WSDATE        COMP      *ZEROS                                 90
     C   90              MOVE      'SPG0011'     MSGNR
     C  N40              DO
     C  N90KODSIK        CHAIN(N)  TILL                               33
     C  N90
     CANN33              DO
     C                   SETON                                        90
     C                   MOVE      'SPT0131'     MSGNR
     C                   END
     C                   END
     C   90              SETON                                        30
     C   90
     CAN 40              Z-ADD     021           POSNBR
     C   90
     CANN40              Z-ADD     04            POSNBR
     C*
     C   30              GOTO      SLTB
     C*
     C*** OPPDATERE PÅ REGISTER OG SUBFIL
     C     KODSIK        CHAIN     TILL                               33
     C                   EXSR      MOVWSI
     C  N33              UPDATE    TILLR
     C   33              WRITE     TILLR
     C   40              UPDATE    SUBFIL
     C  N40              ADD       1             YRECNO
     C  N40              Z-ADD     YRECNO        ZRECNO
     C  N40              WRITE     SUBFIL
     C                   WRITE     SUBCTL
     C                   EXSR      MOVB2
     C*
     C     SLTB          ENDSR
     C*
     C***********************************************
     C     HENT          BEGSR
     C***********************************************
     C                   Z-ADD     0             LINNBR
     C                   SETOFF                                       919299
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C   98              DO
     C   99              SETOFF                                       98
     C                   Z-ADD     1             LINNBR
     C                   GOTO      SLHENT
     C                   END
     C     ZRECNO        DIV       14            LINNBR
     C                   MVR                     XLIN              2 0
     C     XLIN          IFEQ      0
     C                   Z-ADD     14            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C   55WSNRO         IFEQ      '1'                                          ENDRE
     C                   MOVE      ' '           WSNRO
     C     XDLT          COMP      '*PLUKKE*'                             98
     C   98              GOTO      SLHENT
     C                   EXSR      CHGFRI
     C   12              GOTO      SLHENT
     C                   END
     C     WSNRO         IFEQ      '2'                                          ENDRE
     C                   MOVE      ' '           WSNRO
     C     XDLT          COMP      '*DELETE*'                             98
     C   98              GOTO      SLHENT
     C                   EXSR      CHGSI
     C   12              GOTO      SLHENT
     C                   END
     C     WSNRO         IFEQ      '4'                                          SLETTE
     C                   MOVE      ' '           WSNRO
     C     XDLT          COMP      '*DELETE*'                             98
     C   98              GOTO      SLHENT
     C                   EXSR      DLTSI
     C   12              GOTO      SLHENT
     C                   END
     C                   END
     C*
     C     SLHENT        TAG
     C                   ADD       5             LINNBR
     C                   Z-ADD     2             POSNBR
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     DLTSI         BEGSR
     C***********************************************
     C                   SETON                                        99
     C                   EXFMT     VAREL3
     C  N12              DO
     C     KODSDE        CHAIN     TILL                               33
     C  N33              DELETE    TILLR
     C                   EXSR      MOVB1
     C                   END
     C     XLIN          IFEQ      0
     C                   Z-ADD     14            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C                   UPDATE    SUBFIL
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     CHGSI         BEGSR
     C***********************************************
     C                   EXSR      MOVSIW
     C                   SETON                                        99
     C     STCHG         TAG
     C                   EXFMT     VARELI
     C                   SETOFF                                       309089
     C   12              DO
     C                   UPDATE    SUBFIL
     C                   EXSR      MOVB2
     C                   END
     C  N12              EXSR      TESTB1
     C  N12
     CAN 30              GOTO      STCHG
     C     XLIN          IFEQ      0
     C                   Z-ADD     14            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     CHGFRI        BEGSR
     C***********************************************
     C                   SETON                                        99
     C     KODSDE        CHAIN     TILL                               33
     C* N33                DELETTILLR
     C                   MOVE      GMAVD         TIAVD
     C                   MOVE      GMPRO         TIPRO
     C     KODSDE        CHAIN     TILL                               33
     C   33              WRITE     TILLR
     C                   MOVE      *ZEROS        TIAVD
     C                   MOVE      *ZEROS        TIPRO
     C                   EXSR      MOVB9
     C     XLIN          IFEQ      0
     C                   Z-ADD     14            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C                   UPDATE    SUBFIL
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVB1         BEGSR
     C***********************************************
     C                   MOVE      *BLANKS       TINR
     C                   MOVE      *BLANKS       TITYPE
     C                   MOVE      *ZEROS        TIDATE
     C                   MOVE      *ZEROS        WIDATE
     C*
     C                   MOVEL     '*DELETE*'    XDLT
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVB9         BEGSR
     C***********************************************
     C                   MOVEL     '*PLUKKE*'    XDLT
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVB2         BEGSR
     C***********************************************
     C                   SETOFF                                       40
     C                   MOVE      *BLANKS       WSNR
     C                   MOVE      *BLANKS       WSTYPE
     C                   MOVE      *ZEROS        WSDATE
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVSIW        BEGSR
     C***********************************************
     C                   MOVE      TINR          WSNR
     C                   MOVE      TITYPE        WSTYPE
     C                   MOVE      TIYEAR        WSYEAR
     C                   MOVE      TIMONT        WSMONT
     C                   MOVE      TIDAY         WSDAY
     C*
     C                   SETON                                        40
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     21            POSNBR
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVWSI        BEGSR
     C***********************************************
     C* FIX KEY IF NOT FOUND
     C   33              DO
     C                   MOVE      WSNR          TINR
     C                   END
     C*
     C* FIXES VARIABLES
     C                   MOVE      WSTYPE        TITYPE
     C     WSYEAR        IFGT      50
     C     WSYEAR        ADD       1900          TIYEAR
     C                   ELSE
     C     WSYEAR        ADD       2000          TIYEAR
     C                   END
     C                   MOVE      WSMONT        TIMONT
     C                   MOVE      WSDAY         TIDAY
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     *LIKE         DEFINE    TINR          YSNR
     C     *LIKE         DEFINE    TITYPE        YSTYPE
     C     KODSIK        KLIST
     C                   KFLD                    TIAVD
     C                   KFLD                    TIPRO
     C                   KFLD                    WSNR
     C     KODSDE        KLIST
     C                   KFLD                    TIAVD
     C                   KFLD                    TIPRO
     C                   KFLD                    TINR
     C     KODSI1        KLIST
     C                   KFLD                    TIAVD
     C                   KFLD                    TIPRO
     C                   KFLD                    W2NR
     C     XODSI1        KLIST
     C                   KFLD                    W2TYPE
     C     KODSI3        KLIST
     C                   KFLD                    TIAVD
     C                   KFLD                    TIPRO
     C                   KFLD                    YSNR
     C     XODSI3        KLIST
     C                   KFLD                    YSTYPE
     C*
     C     KODRED        KLIST
     C                   KFLD                    TIAVD
     C                   KFLD                    TIPRO
     C     TURKEY        KLIST
     C                   KFLD                    XIAVD
     C                   KFLD                    XIPRO
     C     LEVEK         KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    WSFIRM
     C                   READ      FIRM                                   33
     C                   MOVE      *ZEROS        TUDATF
     C                   MOVE      *ZEROS        TUDATT
     C                   MOVE      *ZEROS        WSDATE
     C*
     C     *ENTRY        PLIST
     C                   PARM                    TIAVD
     C                   PARM                    TIPRO
     C     KODRED        CHAIN     TURER                              33
     C  N33              MOVE      'J'           KALL              1
     C  N33              GOTO      UTI
     C** 95 ER PÅ VED VEDLIKEHOLD
     C                   SETON                                        95
     C*
     C     UTI           ENDSR
     C*
