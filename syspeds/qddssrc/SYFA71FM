     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPPNLGRP(HLPSCR SYFA71PNL)
     A                                      HLPTITLE('VEDLIKEHOLD AV UTLEVERING-
     A                                      SFORBEHOLD')
     A                                      HELP
     A                                      ALTHELP
     A          R SUBFIL                    SFL
     A            WSNR           1A  B  6  3VALUES(' ' '1' '2' '4')
     A            KUKD1     R        O  6  8REFFLD(KODTUR/KUKD1 *LIBL/KODTU)
     A            KUTX      R        O  6 16REFFLD(KODTUR/KUTX *LIBL/KODTU)
     A            KUKD2     R        O  6 52REFFLD(KODTUR/KUKD2 *LIBL/KODTU)
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A            ZRECNO         4S 0O  3 75SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYSPED/SYFA71'
     A                                      COLOR(BLU)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                  2 70TIME
     A N27                              1 19MSGCON(050 SHU0152 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A  27                              1 19MSGCON(050 SHU0155 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A N27                              3  2MSGCON(40 DIV1033 SYMSGF)
     A                                      COLOR(BLU)
     A  27                              3  2MSGCON(25 DIV1034 SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  2MSGCON(078 SHU0151 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A          R BUNN
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF03(03 'EXIT')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  3  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 42MSGCON(018 DIV0022 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 62MSGCON(018 DIV0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  98                              4  2MSGCON(078 DIV0012 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A N98                              4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A          R VARELI
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1  2MSGCON(078 SHU0151 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            WSKD1     R        B  2  8REFFLD(KODTUR/KUKD1 *LIBL/KODTU)
     A  40                                  DSPATR(HI)
     A  90                                  DSPATR(RI)
     A  40                                  DSPATR(PR)
     A                                      DSPATR(UL)
     A            WSTX      R        B  2 16REFFLD(KODTUR/KUTX *LIBL/KODTU)
     A  89                                  DSPATR(RI)
     A                                      DSPATR(UL)
     A                                      CHECK(LC)
     A            WSKD2     R        B  2 52REFFLD(KODTUR/KUKD2 *LIBL/KODTU)
     A                                  3  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  4  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                              4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A          R VAREL2
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A                                  2  2MSGCON(012 DIV1216 *LIBL/SYMSGF)
     A            W2KD1     R        I  2 16REFFLD(KODTUR/KUKD1 *LIBL/KODTU)
     A                                  3  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A          R VAREL3
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A                                  1  2MSGCON(078 SHU0151 *LIBL/SYMSGF)
     A            KUKD1     R        O  2  8REFFLD(KODTUR/KUKD1 *LIBL/KODTU)
     A                                      COLOR(WHT)
     A            KUTX      R        O  2 16REFFLD(KODTUR/KUTX *LIBL/KODTU)
     A                                      COLOR(WHT)
     A            KUKD2     R        O  2 52REFFLD(KODTUR/KUKD2 *LIBL/KODTU)
     A                                      COLOR(WHT)
     A                                  3  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A          R NYWRK
     A                                      CF03(03 'EXIT')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                  1  2'SYSPED/SYFA71'
     A                                      COLOR(BLU)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                  2 70TIME
     A N27                              1 19MSGCON(050 SHU0152 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A  27                              1 19MSGCON(050 SHU0155 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A N27                              3  2MSGCON(40 DIV1033 SYMSGF)
     A                                      COLOR(BLU)
     A  27                              3  2MSGCON(25 DIV1034 SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  2MSGCON(078 SHU0151 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 42MSGCON(018 DIV0022 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 62MSGCON(018 DIV0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
