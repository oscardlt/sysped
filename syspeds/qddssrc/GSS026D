********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 YBC PTF10 F10 --> HENT VEKT FRA ORDRE TIL BOKS
********************************************************************
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
      *
     A          R BILDE
N01  A N40                                  CF10(10 'HENT VEKT FRA ORDRE')
     A                                  1  2'SHIPSYS/GSS026D'
     A                                      COLOR(BLU)
     A                                  1 21'Utskrift av merkelapp for box'
     A                                      DSPATR(HI)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                  1 70TIME
     A                                  8  2'Box-nr:'
     A            OLBOX          3Y 0O  8 22DSPATR(HI)
     A                                      EDTCDE(4)
     A                                  8 27'av'
     A            TOTBOX         3Y 0O  8 31DSPATR(HI)
     A                                      EDTCDE(4)
     A                                 10  2'Vekt       :'
     A            WSVKT          7Y 1B 10 22DSPATR(UL)
     A  98                                  DSPATR(RI)
     A  98                                  DSPATR(PC)
     A                                      EDTCDE(4)
     A  60                             11  2'Cube mtr   :'
     A  60        WSM3           6Y 3B 11 22DSPATR(UL)
     A                                      EDTCDE(4)
     A  60                             12  2'Loading mtr:'
     A N60                             12  2'Mål (L/B/H):'
     A  60        WSLM           4Y 2B 12 22DSPATR(UL)
     A                                      EDTCDE(4)
     A N60        WSMÅLL         4Y 0B 12 22DSPATR(UL)
     A  97                                  DSPATR(RI)
     A  97                                  DSPATR(PC)
     A                                      EDTCDE(4)
     A N60        WSMÅLB         4Y 0B 12 27DSPATR(UL)
     A  96                                  DSPATR(RI)
     A  96                                  DSPATR(PC)
     A                                      EDTCDE(4)
     A N60        WSMÅLH         4Y 0B 12 32DSPATR(UL)
     A  95                                  DSPATR(RI)
     A  95                                  DSPATR(PC)
     A                                      EDTCDE(4)
     A  60                             14  2'Unit  id.  :'
     A N60                             14  2'Cont. id.  :'
     A            WSCID         17A  B 14 22DSPATR(UL)
     A  94                                  DSPATR(RI)
     A  94                                  DSPATR(PC)
N01  A N40                             23  2'F10=Use orders weight'
N01  A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A            MSGNR          7A  P
      *
     A          R WINDOW1
     A                                      OVERLAY
     A                                      WINDOW(*DFT 10 50 *NOMSGLIN)
     A                                  1  2'Utskrift pakkseddel - feilmelding '
     A                                      COLOR(WHT)
     A                                  3  2'Det finnes ett eller flere kolli s-
     A                                      om ikke er   '
     A                                  4  2'tilordnet kasse.                  -
     A                                                   '
     A                                  5  2'Kjøring av pakkseddel med kasse-me-
     A                                      rkelapp kan  '
     A                                  6  2'ikke tillates.                    -
     A                                                   '
     A                                  8  2'Kjør plukkliste, gjerne med box-so-
     A                                      rtering for  '
     A                                  9  2'å finne hvilke kolli det gjelder. -
     A                                                   '
     A                                 10 36'Tast Enter'
     A                                      COLOR(WHT)
      *
     A          R WINDOW2
     A                                      WINDOW(*DFT 10 50 *NOMSGLIN)
     A                                      OVERLAY
     A                                  1  2'Utskrift pakkseddel - feilmelding '
     A                                      COLOR(WHT)
     A                                  3  2'Det finnes IKKE logg (i rett antal-
     A                                      l) som viser '
     A                                  4  2'at packinglist av type P (pr box m-
     A                                      /merkelapp)  '
     A                                  5  2'er skrevet.                       -
     A                                                   '
     A                                  7  2'Kjør Packing list av type P først.-
     A                                                   '
     A                                 10 36'Tast Enter'
     A                                      COLOR(WHT)
      *
     A          R WINDOW3
     A                                      WINDOW(*DFT 10 50 *NOMSGLIN)
     A                                      OVERLAY
     A                                  1  2'Confirm of packinglist - error mes-
     A                                      sage'
     A                                      COLOR(WHT)
     A                                  3  2'This trip is NOT a CONTAIMER trip '
     A                                  4  2'or NOT ALL COLLIES are scaned in c-
     A                                      ontainer.'
     A                                  7  2'Make a packinglist with type L fir-
     A                                      st.          '
     A                                 10 36'Press Enter'
     A                                      COLOR(WHT)
      *
     A          R WINDOW4
     A                                      WINDOW(*DFT 04 30 *NOMSGLIN)
     A                                      OVERLAY
     A                                  1  2'Print packinglist'
     A                                      COLOR(WHT)
     A                                  3  2'Container ?'
     A            WSCONT         1   B  3 14DSPATR(UL)
     A                                      VALUES(' ' 'Y' 'N')
     A                                  3 18'(Y/N)'
      *
     A          R WINDOW5
     A                                      WINDOW(*DFT 04 50 *NOMSGLIN)
     A                                      OVERLAY
     A                                  1  2'Print packinglist'
     A                                      COLOR(WHT)
     A                                  3  2'Container No.'
     A            WSCNR         17A  B  3 16DSPATR(UL)
      *
     A          R WINDOW6
     A                                      WINDOW(*DFT 10 73 *NOMSGLIN)
     A                                      CF04(04 'Søking')
     A                                      OVERLAY
     A                                  1  2'Print proforma invoice'
     A                                      COLOR(WHT)
     A                                  2  2'HEADING TEXT'
     A            WSFT1         70A  B  3  2DSPATR(UL)
     A N98N99                               DSPATR(PC)
     A            WSFT2         70A  B  4  2DSPATR(UL)
     A                                  5  2'Printer name :'
     A            WSPRT         10A  B  5 17DSPATR(UL)
     A  99                                  DSPATR(HI)
     A  99                                  DSPATR(PC)
     A                                  6  2'No. of copies:'
     A            WSCPY          1  0B  6 17DSPATR(UL)
     A                                  7  2'Curr. code   :'
     A            WSCUR          3A  B  7 17DSPATR(UL)
     A  98                                  DSPATR(HI)
     A  98                                  DSPATR(PC)
     A N27                              8  2'Free text    :'
     A N27        WSFTXT        30A  B  8 17DSPATR(UL)
     A                                  9  2'Comm.invoice?:'
     A            WSCOMM         1A  B  9 17DSPATR(UL)
     A                                      VALUES('Y' 'N')
     A                                  9 22'Clientsnr:'
     A            WSKNC          8  0B  9 34DSPATR(UL)
     A  97                                  DSPATR(HI)
     A  97                                  DSPATR(PC)
     A                                      EDTCDE(4)
     A N99N98N97                       10  2'F4=Søk'
     A                                      COLOR(BLU)
     A  99                             10  2MSGCON(25 GSS0042 SYMSGF)
     A                                      COLOR(WHT)
     A  98                             10  2MSGCON(25 GSS0066 SYMSGF)
     A                                      COLOR(WHT)
     A  97                             10  2MSGCON(25 SPK0001 SYMSGF)
     A                                      COLOR(WHT)
