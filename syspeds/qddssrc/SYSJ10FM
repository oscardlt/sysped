********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 YBC PTF10 TOLLSTEDSKODE ER ENDRET TIL 4.0
********************************************************************
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPPNLGRP(HLPSCR SYSJ10PNL)
     A                                      HLPTITLE('OPPDRAGSREGISTRERING SJØ -
     A                                      IMPORT')
     A                                      HELP
     A                                      ALTHELP
     A          R BILDE01
     A                                      CF12(12 'Avslutt program')
     A                                      CF05(05 'Gå direkte til faktura')
     A                                      CF07(07 'SPØRRIND OPPDRAG')
     A                                      CF09(09 'Bytt signatur')
     A                                  1  3'SYSPED/SYSJ10'
     A                                      COLOR(BLU)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(GRN)
     A                                  1 70TIME
     A                                      COLOR(GRN)
     A                                  2  3MSGCON(12 DIV1451 SYMSGF)
     A                                      COLOR(TRQ)
     A            UFIRM          2A  O  2 16TEXT('FIRMAKODE')
     A                                      COLOR(TRQ)
     A                                  2 58MSGCON(022 DIV0047 *LIBL/SYMSGF)
     A                                      COLOR(TRQ)
     A                                  3 15MSGCON(045 DIV0596 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  4 15MSGCON(045 DIV0597 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  5 15MSGCON(045 DIV3283 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  9 15MSGCON(018 DIV0599 *LIBL/SYMSGF)
     A            WSSG           3A  B  9 35TEXT('Signatur')
     A                                      DSPATR(UL)
     A  99                                  DSPATR(PC)
     A  99                                  DSPATR(RI)
     A                                 11 15MSGCON(018 DIV0600 *LIBL/SYMSGF)
     A            WSAVD          4Y 0B 11 35TEXT('Avdeling')
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
     A  98                                  DSPATR(PC)
     A  98                                  DSPATR(RI)
     A                                 13 15MSGCON(018 DIV0601 *LIBL/SYMSGF)
     A            WSOPD          7Y 0B 13 35TEXT('Oppdragsnummer')
     A                                      DSPATR(UL)
     A N96                                  DSPATR(PC)
     A  97                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                 13 50MSGCON(020 DIV0604 *LIBL/SYMSGF)
     A                                 15 15MSGCON(018 DIV0602 *LIBL/SYMSGF)
     A            HEAVD          4Y 0B 15 35TEXT('Avdeling')
     A                                      DSPATR(UL)
     A  96                                  DSPATR(PC)
     A  96                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A            HEOPD          7Y 0B 15 41TEXT('Oppdragsnummer')
     A                                      DSPATR(UL)
     A  96                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                 15 50MSGCON(020 DIV0036 *LIBL/SYMSGF)
     A                                 18 15MSGCON(018 DIV0603 *LIBL/SYMSGF)
     A            WS7PDG         5Y 0O 18 35COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 23  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(018 DIV0605 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 42MSGCON(018 DIV0606 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 62MSGCON(018 DIV0033 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A          R BILDE02
     A N31
     AO 31N32N34                            CF03(03 'Avslutt registrering av op-
     A                                      pdrag')
     A                                      CF04(04 'Søke')
     A N31
     AO 31N32N34                            CF05(05 'Gå til faktura')
     A                                      CF07(07 'SPØRRING PÅ OPPDRAG')
     A N31
     AO 31N32N34                            CF09(09 'Ny tur')
     A                                      CF11(11 'DUP-OPPDRAG')
     A                                      CF12(12 'Avbryt registrering av opp-
     A                                      drag')
     A                                      CF14(14 'Vise opplysning til kjøper-
     A                                       / SELGER')
     A                                      CF15(15 'FrI SØK')
     A N31                                  CF16(16 'Neste')
     A                                      CF24(24 'FLERE TASTER')
     A                                      CF18(18 'Freetext')
     A  27                                  ALARM
     A                                      CSRLOC(LINNBR     POSNBR)
     A          H                           HLPPNLGRP(SYSJ10010 SYSJ10PNL)
     A                                      HLPARA(01 001 01 080)
     A          H                           HLPPNLGRP(SYSJ10010 SYSJ10PNL)
     A                                      HLPARA(02 001 02 018)
     A          H                           HLPPNLGRP(SYSJ10015 SYSJ10PNL)
     A                                      HLPARA(02 019 02 042)
     A          H                           HLPPNLGRP(SYSJ10020 SYSJ10PNL)
     A                                      HLPARA(02 043 02 080)
     A          H                           HLPPNLGRP(SYSJ10025 SYSJ10PNL)
     A                                      HLPARA(03 001 03 022)
     A          H                           HLPPNLGRP(SYFA10035 SYSJ10PNL)
     A                                      HLPARA(03 023 03 031)
     A          H                           HLPPNLGRP(SYSJ10030 SYSJ10PNL)
     A                                      HLPARA(03 034 03 056)
     A          H                           HLPPNLGRP(SYSJ10035 SYSJ10PNL)
     A                                      HLPARA(03 057 03 080)
     A          H                           HLPPNLGRP(SYSJ10040 SYSJ10PNL)
     A                                      HLPARA(04 001 04 080)
     A          H                           HLPPNLGRP(SYSJ10045 SYSJ10PNL)
     A                                      HLPARA(05 001 05 024)
     A          H                           HLPPNLGRP(SYSJ10050 SYSJ10PNL)
     A                                      HLPARA(05 025 05 080)
     A          H                           HLPPNLGRP(SYSJ10055 SYSJ10PNL)
     A                                      HLPARA(07 001 07 023)
     A          H                           HLPPNLGRP(SYSJ10060 SYSJ10PNL)
     A                                      HLPARA(07 024 07 035)
     A          H                           HLPPNLGRP(SYSJ10065 SYSJ10PNL)
     A                                      HLPARA(07 037 07 080)
     A          H                           HLPPNLGRP(SYSJ10070 SYSJ10PNL)
     A                                      HLPARA(08 001 10 080)
     A          H                           HLPPNLGRP(SYSJ10075 SYSJ10PNL)
     A                                      HLPARA(11 001 14 080)
     A          H                           HLPPNLGRP(SYSJ10080 SYSJ10PNL)
     A                                      HLPARA(15 001 15 024)
     A          H                           HLPPNLGRP(SYSJ10080 SYSJ10PNL)
     A                                      HLPARA(16 001 16 024)
     A          H                           HLPPNLGRP(SYSJ10080 SYSJ10PNL)
     A                                      HLPARA(17 001 17 024)
     A          H                           HLPPNLGRP(SYSJ10085 SYSJ10PNL)
     A                                      HLPARA(15 025 15 059)
     A          H                           HLPPNLGRP(SYSJ10085 SYSJ10PNL)
     A                                      HLPARA(16 025 16 059)
     A          H                           HLPPNLGRP(SYSJ10085 SYSJ10PNL)
     A                                      HLPARA(17 025 17 059)
     A          H                           HLPPNLGRP(SYSJ10090 SYSJ10PNL)
     A                                      HLPARA(15 060 15 080)
     A          H                           HLPPNLGRP(SYSJ10090 SYSJ10PNL)
     A                                      HLPARA(16 060 16 080)
     A          H                           HLPPNLGRP(SYSJ10090 SYSJ10PNL)
     A                                      HLPARA(17 060 17 080)
     A          H                           HLPPNLGRP(SYSJ10095 SYSJ10PNL)
     A                                      HLPARA(18 001 18 021)
     A          H                           HLPPNLGRP(SYSJ10100 SYSJ10PNL)
     A                                      HLPARA(18 022 18 044)
     A          H                           HLPPNLGRP(SYSJ10105 SYSJ10PNL)
     A                                      HLPARA(18 045 18 080)
     A          H                           HLPPNLGRP(SYSJ10110 SYSJ10PNL)
     A                                      HLPARA(19 001 20 080)
     A          H                           HLPPNLGRP(SYSJ10115 SYSJ10PNL)
     A                                      HLPARA(21 001 21 080)
     A          H                           HLPPNLGRP(SYSJ10120 SYSJ10PNL)
     A                                      HLPARA(22 001 22 080)
     A          H                           HLPPNLGRP(SYSJ10999 SYSJ10PNL)
     A                                      HLPARA(23 001 24 080)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGLN1         7A  P
     A            MSGLN2         7A  P
     A            MSGLN3         7A  P
     A            MSGLN4         7A  P
     A            MSGNR          7A  P
     A                                  1  2MSGCON(020 DIV3285 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 23MSGCON(004 DIV0071 *LIBL/SYMSGF)
     A            WSAVD          4Y 0B  1 28TEXT('Avdeling')
     A N58                                  DSPATR(PR)
     A N58                                  COLOR(WHT)
     A  58                                  COLOR(YLW)
     A                                      EDTCDE(4)
     A  58                              1 33MSGCON(005 YBC0035 *LIBL/SYMSGF)
     A                                      DSPATR(RI)
     A                                      COLOR(YLW)
     A N58        WSOPD          7Y 0O  1 33TEXT('Oppdragsnummer')
     A N29                                  DSPATR(BL)
     A N29                                  COLOR(RED)
     A  29                                  COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  1 41MSGCON(003 DIV0072 *LIBL/SYMSGF)
     A            WSSG           3A  B  1 45TEXT('Signatur')
     A                                      COLOR(YLW)
     A                                  1 49MSGCON(004 DIV0073 *LIBL/SYMSGF)
     A            WSSTD          4Y 0B  1 54TEXT('Kostnadssted')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  1 61MSGCON(004 DIV5019 *LIBL/SYMSGF)
     A            WSRFA         15A  B  1 66TEXT('Referanse Agent')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  2  2MSGCON(005 DIV3249 *LIBL/SYMSGF)
     A            WSPLAN         1A  B  2  8TEXT('Planleggingskode')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A  97                                  DSPATR(RI)
     A                                      VALUES(' ' 'X' 'R')
     A            WSPRO          8Y 0B  2 10TEXT('Prosjektnummer')
     A                                      DSPATR(UL)
     A  97                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  2 19'P'
     A            WSPOS1         7A  B  2 21TEXT('Posisjon-1')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSPOS2         4Y 0B  2 29TEXT('Posisjon-2')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  2 34'D'
     A            WSDT           6Y 0B  2 36DSPATR(UL)
     A  96                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  2 43MSGCON(005 DIV0587 *LIBL/SYMSGF)
     A            WSGNA          4A  B  2 49TEXT('Godsnummer')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSGNB          5A  B  2 54TEXT('Godsnummer')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSGNC          6A  B  2 60TEXT('Godsnummer')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  3  2MSGCON(004 DIV0589 *LIBL/SYMSGF)
     A            WSSDF          5A  B  3  8TEXT('Frakt fra')
     A                                      DSPATR(UL)
     A  95                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                  3 14'-'
     A            WSSDT          5A  B  3 16TEXT('Frakt til')
     A                                      DSPATR(UL)
     A  94                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSVALT         3A  B  3 23TEXT('V.kode tillegg')
     A                                      DSPATR(UL)
     A  76                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSVALP         3Y 1B  3 28DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  3 34MSGCON(005 DIV3200 *LIBL/SYMSGF)
     A            WSLB           5A  B  3 40TEXT('LOKAL BÅT')
     A                                      DSPATR(UL)
     A  74                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                  3 48MSGCON(004 DIV0082 *LIBL/SYMSGF)
     A            WSLKA          2A  B  3 53TEXT('Landkode')
     A                                      DSPATR(UL)
     A  93                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                  3 57MSGCON(003 DIV0083 *LIBL/SYMSGF)
     A            WSSDL         20A  B  3 61TEXT('Lossested')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A                                  4  2MSGCON(005 DIV3284 *LIBL/SYMSGF)
     A            WSKNA          8Y 0B  4  8TEXT('Kundenr Agent')
     A                                      DSPATR(UL)
     A  70                                  DSPATR(RI)
     A  73                                  DSPATR(BL)
     A  73                                  COLOR(RED)
     A N73                                  COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSNAA         30A  O  4 17TEXT('Navn Agent')
     A  73                                  DSPATR(BL)
     A  73                                  COLOR(RED)
     A N73                                  COLOR(WHT)
     A                                  4 48MSGCON(005 DIV0085 *LIBL/SYMSGF)
     A            WSKNT          8Y 0B  4 54TEXT('Kundenr Transportør')
     A                                      DSPATR(UL)
     A  69                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSTRNV        18A  O  4 63COLOR(WHT)
     A                                  5  2MSGCON(005 DIV0086 *LIBL/SYMSGF)
     A            WSTRM          2Y 0B  5  8TEXT('Transportmåte')
     A                                      DSPATR(UL)
     A  92                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  5 12MSGCON(008 DIV0087 *LIBL/SYMSGF)
     A            WSTRI          2A  B  5 21TEXT('Lankode Transport')
     A                                      DSPATR(UL)
     A  91                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                  5 25MSGCON(005 DIV0088 *LIBL/SYMSGF)
     A            WSTRC          1Y 0B  5 31TEXT('Transport cont')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                      VALUES(0 1)
     A                                  5 34MSGCON(015 DIV5020 *LIBL/SYMSGF)
     A            WSTRCN        17A  B  5 50TEXT('Transport cnr')
     A                                      DSPATR(UL)
     A  90                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                  7  2MSGCON(004 DIV0090 *LIBL/SYMSGF)
     A            WSKNS          8Y 0B  7  8TEXT('Kundenr Selger')
     A                                      DSPATR(UL)
     A  67
     AO 68                                  DSPATR(RI)
     A  34                                  DSPATR(PR)
     A  72                                  DSPATR(BL)
     A  72                                  COLOR(RED)
     A N72                                  COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  7 18MSGCON(003 DIV0091 *LIBL/SYMSGF)
     A            WSANS          1Y 0B  7 22TEXT('Avtalenr Selger')
     A                                      DSPATR(UL)
     A  34                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  7 24MSGCON(002 DIV0092 *LIBL/SYMSGF)
     A            WSKDFS         1A  B  7 27TEXT('Fakturakode Selger')
     A                                      DSPATR(UL)
     A  34                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'X' 'B' 'E')
     A                                  7 29MSGCON(003 DIV0093 *LIBL/SYMSGF)
     A            WSVALS         3A  B  7 33TEXT('Valutakode Selger')
     A                                      DSPATR(UL)
     A  66                                  DSPATR(RI)
     A  34                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                  7 37MSGCON(004 DIV0094 *LIBL/SYMSGF)
     A            WSKNSF         8Y 0B  7 42TEXT('Kundenr Selg.fakm')
     A                                      DSPATR(UL)
     A  65                                  DSPATR(RI)
     A  34                                  DSPATR(PR)
     A  99                                  DSPATR(BL)
     A  99                                  COLOR(RED)
     A N99                                  COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSNASF        30A  O  7 51TEXT('Navn Selg.fakm')
     A                                      DSPATR(UL)
     A  99                                  DSPATR(BL)
     A  99                                  COLOR(RED)
     A N99                                  COLOR(WHT)
     A                                  8  2MSGCON(004 DIV0095 *LIBL/SYMSGF)
     A            WSNAS         30A  B  8  8TEXT('Navn Selger')
     A                                      DSPATR(UL)
     A  67                                  DSPATR(RI)
     A  34                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A            WSADS1        30A  B  8 42TEXT('Adresse-1 Selger')
     A                                      DSPATR(UL)
     A  34                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A            WSADS2        30A  B  9  8TEXT('Adresse-3 Selger')
     A                                      DSPATR(UL)
     A  34                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A            WSADS3        30A  B  9 42TEXT('Adresse-2 Selger')
     A                                      DSPATR(UL)
     A  34                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                 11  2MSGCON(004 DIV0096 *LIBL/SYMSGF)
     A            WSKNK          8Y 0B 11  8TEXT('Kundenr Kjøper')
     A                                      DSPATR(UL)
     A  62
     AO 61                                  DSPATR(RI)
     A  32                                  DSPATR(PR)
     A  71                                  DSPATR(BL)
     A  71                                  COLOR(RED)
     A N71                                  COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 11 18MSGCON(003 DIV0091 *LIBL/SYMSGF)
     A            WSANK          1Y 0B 11 22TEXT('Avtalenr Kjøper')
     A                                      DSPATR(UL)
     A  32                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 11 24MSGCON(002 DIV0092 *LIBL/SYMSGF)
     A            WSKDFK         1A  B 11 27TEXT('Fakturakode Kjøper')
     A                                      DSPATR(UL)
     A  32                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'X' 'B' 'E')
     A                                 11 29MSGCON(003 DIV0093 *LIBL/SYMSGF)
     A            WSVALK         3A  B 11 33TEXT('Valutakode Kjøper')
     A                                      DSPATR(UL)
     A  60                                  DSPATR(RI)
     A  32                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                 11 37MSGCON(004 DIV0094 *LIBL/SYMSGF)
     A            WSKNKF         8Y 0B 11 42TEXT('Kundenr Kjøp.fakm')
     A                                      DSPATR(UL)
     A  59                                  DSPATR(RI)
     A  32                                  DSPATR(PR)
     A  98                                  DSPATR(BL)
     A  98                                  COLOR(RED)
     A N98                                  COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSNAKF        30A  O 11 51TEXT('Navn Kjøp.fakm')
     A                                      DSPATR(UL)
     A  98                                  DSPATR(BL)
     A  98                                  COLOR(RED)
     A N98                                  COLOR(WHT)
     A                                 12  2MSGCON(004 DIV0097 *LIBL/SYMSGF)
     A            WSNAK         30A  B 12  8TEXT('Navn Kjøper')
     A                                      DSPATR(UL)
     A  62                                  DSPATR(RI)
     A  32                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A            WSADK1        30A  B 12 42TEXT('Adresse-1 Kjøper')
     A                                      DSPATR(UL)
     A  32                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A            WSADK2        30A  B 13  8TEXT('Adresse-2 Kjøper')
     A                                      DSPATR(UL)
     A  32                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A            WSADK3        30A  B 13 42TEXT('Adresse-3 Kjøper')
     A                                      DSPATR(UL)
     A  32                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                 14 38MSGCON(005 DIV3209 *LIBL/SYMSGF)
     A            BLLSTY         3A  O 14 44DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 14 48MSGCON(005 DIV3240 *LIBL/SYMSGF)
     A            BLOB           5A  O 14 54DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 14 60MSGCON(005 DIV3192 *LIBL/SYMSGF)
     A            BLREIS        14A  O 14 66DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 15  2MSGCON(078 DIV3282 *LIBL/SYMSGF)
     A            WSOT           2A  B 16  2TEXT('Oppdragstype')
     A                                      DSPATR(UL)
     A  89                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSFR           3A  B 16  5TEXT('Frankatur')
     A                                      DSPATR(UL)
     A  88                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSGM1         15A  B 16  9TEXT('Godsmerking-1')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSNT           7Y 0B 16 25TEXT('Antall')
     A                                      DSPATR(UL)
     A  87                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSVS1         25A  B 16 33TEXT('Vareslag-1')
     A                                      DSPATR(UL)
     A  86                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSVKT          9Y 0B 16 60TEXT('Vekt')
     A                                      DSPATR(UL)
     A  85                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSM3           7Y 3B 16 70TEXT('Kubikk')
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                      DSPATR(UL)
     A            WSGM2         15A  B 17  9TEXT('Godsmerking-2')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSVS2         25A  B 17 33TEXT('Vareslag-2')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 18  2MSGCON(006 SHU0150 *LIBL/SYMSGF)
     A            WSKF           2A  B 18  9DSPATR(UL)
     A  56                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSKFT1         1A  B 18 12DSPATR(UL)
     A  55                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSFT1A        30A  B 18 15DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSFT1B        15A  B 18 46DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSUSR         10A  O 18 62COLOR(PNK)
     A                                 19  2MSGCON(012 DIV0595 *LIBL/SYMSGF)
     A                                 19 16MSGCON(009 DIV0592 *LIBL/SYMSGF)
     A            WSLKS          2   O 19 26COLOR(YLW)
     A            WSSDFF         5A  B 19 29TEXT('Forfrakt fra')
     A                                      DSPATR(UL)
     A  84                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 19 35MSGCON(011 DIV0593 *LIBL/SYMSGF)
     A            WSLKK          2   O 19 48COLOR(YLW)
     A            WSSDVT         5A  B 19 51TEXT('Viderefrakt til')
     A                                      DSPATR(UL)
     A  83                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 19 57MSGCON(005 DIV0100 *LIBL/SYMSGF)
     A            WSKDL         15A  B 19 63TEXT('Lagerkode')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSPK1          1A  B 20  2TEXT('Printerkode-1')
     A                                      DSPATR(UL)
     A  82                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'J' 'N' 'P')
     A            WSPK2          1A  B 20  6TEXT('Printerkode-2')
     A                                      DSPATR(UL)
     A  81                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'J' 'N' 'P')
     A            WSPK3          1A  B 20  9TEXT('Printerkode-3')
     A                                      DSPATR(UL)
     A  80                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'J' 'N' 'P')
     A            WSPK4          1A  B 20 12TEXT('Printerkode-4')
     A                                      DSPATR(UL)
     A  79                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'J' 'N' 'P')
     A            WSMSG         62A  O 20 19COLOR(RED)
     A                                      DSPATR(BL)
S01  A*           W2TOLL         2Y 0B 21  2TEXT('Tollsted')
N01  A            W2TOLL         4Y 0B 21  2TEXT('Tollsted')
     A                                      DSPATR(UL)
     A  78                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
S01  A*                                21  5MSGCON(011 DIV0104 *LIBL/SYMSGF)
N01  A                                 21  7MSGCON(011 DIV0104 *LIBL/SYMSGF)
     A            W2KDME         1A  B 21 20TEXT('Merkelapp  /X')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'X' 'L')
     A                                 21 22MSGCON(011 DIV0105 *LIBL/SYMSGF)
     A            W2KDE          1A  B 21 40TEXT('Etterkrav  /X')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'X')
     A                                 21 42MSGCON(011 DIV0106 *LIBL/SYMSGF)
     A            WSNTVS         3Y 0B 21 56TEXT('Antall varepos SAD')
     A                                      DSPATR(UL)
     A  77                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 21 60MSGCON(011 DIV0107 *LIBL/SYMSGF)
     A                                 22  2MSGCON(009 DIV0607 *LIBL/SYMSGF)
     A            WSOPDG         7Y 0O 22 12DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            MSGL1         18A  O 23  2MSGID(&MSGLN1 SYMSGF)
     A                                      COLOR(BLU)
     A            MSGL2         18A  O 23 22MSGID(&MSGLN2 SYMSGF)
     A                                      COLOR(BLU)
     A            MSGL3         18A  O 23 42MSGID(&MSGLN3 SYMSGF)
     A                                      COLOR(BLU)
     A            MSGL4         18A  O 23 62MSGID(&MSGLN4 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
