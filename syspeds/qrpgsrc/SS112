      *********************************************************************
      * TRANSP.PLANLEGGING   - Opprette tur
      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/ss112 MODULE(*LIBL/ss112
CRTPGM*        *LIBL/CHECKUSR *LIBL/INCGI) ACTGRP(CGI) AUT(*USE)
      *
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10172 * 01 SH  PTF10 norsk text til Track and trace
********************************************************************
      *********************************************************************
      * MAIN PROGRAM FLOW
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
     FBRIDF     UF   E           K DISK    USROPN
     FGSSTRL01  UF A E           k disk    usropn
     FTRACKF    O    E             DISK    USROPN
     FTELL      UF   E           k disk    usropn
     FHEADF     O  A E           k disk    usropn
     FDOKUF     UF A E           k disk    usropn
     FBRPARF    IF   E           k disk    usropn
     FCundL01   IF   E           k disk    usropn
     FVadr      IF   E           k disk    usropn
     FVadr1     IF   E           k disk    usropn
     F                                     RENAME(VADRR:VARDRR1)
     FKODTLK    IF   E           k disk    usropn
     FTRAN      IF   E           k disk    usropn
     FGSSCGF    IF   E           k disk    usropn
      *--------------------------------------------------------------------
      * Prototype defintions and standard system API error structure
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      *--------------------------------------------------------------------
      * Variables common to CGI programs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
      * Variables received from the input string
     D lng             s              2
     D UsrSpcName      s             10
     D WSUSER          S             10
     D CURTUR          S             11
     D CURTURN         S             11  0
     D KUNDE           S              8
     D KUNDEN          S              8  0
     D ADRNR           S              5
     D ADRNRN          S              5  0
     D TRANSP          S              8  0
     D DATO            S             10
     D WSTRAD1         S             30
     D WSTRAD2         S             30
     D WSTRAD3         S             30
     D WSTRAD6         S             30
     D NavnAdr         S             60
     D LAGRETUR        S              1
     D PGM             C                   CONST('SYSPED/CHECKUSR')
     D XXTXT01         C                   CONST('Trip is created.')
     D XXTXT02         C                   CONST('Trip is changed.')
     D XXTXT01L        C                   CONST('Tur er opprettet')
     D XXTXT02L        C                   CONST('Tur er endret.')
     D                 DS
     D  VADRN4                 1     30
     D  VADRN4PNU              1      9
     D  VADRN4410             10     10
     D  VADRN4STU1            10     30
     D  VADRN4STU2            11     30
     D                 DS
     D  WSTRAD4                1     30
     D  WSTRADPNU              1      9
     D  WSTRAD410             10     10
     D  WSTRADSTU1            10     30
     D  WSTRADSTU2            11     30
     D                 DS
     D  WSTRAD5                1     30
     D  WSTRADLK               1      2
     D  WSTRADSTED             4     30
     D                 DS
     D  SLLOC                  1     13
     D  SLLOCA                 1      8
     D  SLLOCB                 9     13
     D                 DS
     D  ShipTO                 1     13
     D  ShipTOA                1      8
     D  ShipTOB                9     13
     D                 DS
     D  SLTNR                  1     13
     D  SLTNRA                 1      8
     D  SLTNRB                 9     13
     D                 DS
     D  ShipBY                 1     13
     D  ShipBYA                1      8
     D  ShipBYB                9     13
     D                 DS
     D  WTIME                  1     14  0
     D  WTID                   1      6  0
     D  WDD                    7      8  0
     D  WMM                    9     10  0
     D  WÅÅ                   11     14  0
     D                 DS
     D DATON                   1      8  0
     D WSDTÅÅ                  1      4  0
     D WSDTMM                  5      6  0
     D WSDTDD                  7      8  0
     D                 DS
     D DT8                     1      8  0
     D DT8Å                    1      4  0
     D DT8M                    5      6  0
     D DT8D                    7      8  0
     D FMELDNR         S              2
     D FEILMELDING     S             60
     D FEIL01          C                   CONST('Given date is invalid. -
     D                                      (format is HHYYMMDD)')
     D FEIL02          C                   CONST('Customer must be selected.')
     D FEIL03          C                   CONST('Ship to  must be selected.')
     D FEIL04          C                   CONST('Consignee / reciever must b-
     D                                     e filled in.')
     D FEIL05          C                   CONST('User has not param. (SSAVD) -
     D                                     for creating new shipping lists.')
      *
     D Lib             s             10
     D Fn              s             10
     D Mbr             s             10
      *
     D Spaces          s             12a   inz('&nbsp;&nbsp;')
      *
      *--------------------------------------------------------------------
      * Prolog common to CGI programs
      * -Receive input from the remote browser
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prolog3
      *=====================================================================******
      * MAIN PROCESS LINE
      *=====================================================================******
      * Get program start time for calculating execution time
     C                   time                    timedata1
      * Parse input
     C                   exsr      Parse
      *
     C                   EXSR      INIT
     C                   CALLB     PGM
     C                   PARM                    BIBRID
     C                   PARM                    UsrSpcName
     C                   PARM                    UINN              1
     C                   IF        UINN = 'N'
     C                   GOTO      SLUTT
     C                   END
     C     WSUSER        CHAIN     BRIDF                              50
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVE      WDT           BIDTF
     C                   MOVE      WTM           BITIDF
     C                   UPDATE    BRIDFR
      * "Programsyklus:"
      * Ved første gangs kall kalles pgm med parm User /turnr/lagretur=N
      * Videre kaller programmet('s HTML - OK ->) seg selv med lagretur=Y
      * (samt alle andre input-parametre fra skjermbildet...)
      * Dette skjer på ny og på ny inntil det (ved load av html) finner
      * at det er ingen feilmeldinger - da closer vinduet seg selv.
     c     curturn       IFNE      *zeros
     c     LAGRETUR      ANDNE     'Y'
     C                   EXSR      GETTUR
     c                   END
      * hvis en har tastet submit fra vedlikehodsbildet, test/lagre
      * (men bildet vises på ny dersom det er feil ...)
     c     LAGRETUR      IFEQ      'Y'
     c                   exsr      tstfeil
     c                   end
      *
      * Set output skeleton html member name
     C                   exsr      SetSkl
      * Read skeleton output html, etc.
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
      *
      * 1-Section /$top
     C                   exsr      SetTop
     C                   callp     updhtmlvar('FEILMELDING':
     C                             FEILMELDING)
     C                   callp     updhtmlvar('FMELDNR':FMELDNR)
     C                   callp     wrtsection('top')
      * 2-Kunder -pulldown
     C                   exsr      DspKund
      * 2-Kunder -pulldown
     C                   exsr      DspTran
      * 2-Ship to-pulldown
     C                   exsr      DspShipTo
      * 2-Adr.nr  pulldown
     C                   exsr      DspAdr
      *
     C     SLUTT         TAG
     C                   EXSR      EXIT
     C                   return
      *=====================================================================******
      * Test skjermbilde
      *=====================================================================******
     C     TstFeil       begsr
      * Er avd-parameter satt (nødv. for å lage ny..)
     c     CurTurN       IFEQ      *ZEROS
     c     XXavd         andeq     *ZEROS
     C                   MOVEL     FEIL05        FEILMELDING
     C                   MOVE      '05'          FMELDNR
     C                   GOTO      UtTest
     C                   ENDIF
      * TEST DATO
     C                   MOVE      WSDTDD        UDD               2 0
     C                   MOVE      WSDTMM        UMM               2 0
     C                   MOVE      WSDTÅÅ        UÅÅ               2 0
     C                   CALL      'TESTDATO'
     C                   PARM                    UDD
     C                   PARM                    UMM
     C                   PARM                    UÅÅ
     C                   PARM                    UFLAGG            1 0
     C     UFLAGG        IFEQ      1
     C                   MOVEL     FEIL01        FEILMELDING
     C                   MOVE      '01'          FMELDNR
     C                   GOTO      UtTest
     C                   ENDIF
      * Test kunde
     c     KUNDEN        Ifeq      *zeros
     C                   MOVEL     FEIL02        FEILMELDING
     C                   MOVE      '02'          FMELDNR
     C                   GOTO      UtTest
     C                   ENDIF
      *
      * Test ShipTo
     c     SHIPTOB       Ifeq      *blanks
     C                   MOVEL     FEIL03        FEILMELDING
     C                   MOVE      '03'          FMELDNR
     C                   GOTO      UtTest
     C                   ENDIF
      *
      * Test mottaker
     c     WSTRAD1       Ifeq      *blanks
     c     SHIPTOB       AndEQ     'DIREC'
     c     ADRNRN        AndEQ     *zeros
     C                   MOVEL     FEIL04        FEILMELDING
     C                   MOVE      '04'          FMELDNR
     C                   GOTO      UtTest
     C                   ELSE
     c     SHIPTOB       IFEQ      'DIREC'
     c     ADRNRN        ANDNE     *zeros
     c     VADRKEY       Chain     VADR                               34
     C     *IN34         IFEQ      '0'
     C                   MOVEL     VADRNA        WSTRAD1
     C                   MOVEL     VADRN1        WSTRAD2
     C                   MOVEL     VADRN2        WSTRAD3
     C                   MOVEL     VADRN3        WSTRAD4
     C                   MOVE      *BLANKS       WSTRAD5
     C     VALAND        IFNE      '  '
     C                   MOVE      VALAND        KLKLK
     C     LANDKEY       CHAIN     KODTLK                             33
     C                   EVAL      WSTRAD5 =  KLKLK + ' ' + KLKNVN
     C                   END
     C                   MOVE      *BLANKS       WSTRAD6
     c                   move      ADRNRN        BAATCODE          5
     c                   movel     BPREFIX       BAATCODE          5
     C                   ENDIF
     C                   ENDIF
     C                   ENDIF
      *
      * ALT OK - eksisterend tur eller lag ny - Deretter avslutt
     C                   MOVE      'Y'           AVSLUTT
     c     CurTurN       IFNE      *ZEROS
      * Hvis endring..
     c     CurTurN       chain     GSSTRL01                           55
     c                   else
      * Hvis ny..
     c     xxavd         cabeq     *zeros        UtTest
     C                   MOVE      XXAVD         TEAVD
     C                   CLEAR                   HEADFR
      * Lag oppdrag
     C                   MOVE      TEAVD         HEAVD
     C     TEAVD         CHAIN     TELL                               33
     C  N33              DO
     C                   MOVE      TEOPDN        HEOPD
     C                   ADD       1             TEOPDN
     C                   UPDATE    TELLR
     C                   MOVE      'U'           HEST
     C                   MOVE      'WEB'         HESG
     C                   MOVE      'S'           HEUR
     C                   MOVE      DATON         HEDTOP
     C                   MOVE      DT8           HEDTR
     C                   MOVE      *ZEROS        HEHAWB
     C                   WRITE     HEADFR
      * Lag fraktbrev
     C                   CLEAR                   DOKUFR
     c                   move      'F'           DFRI
     c                   move      HEAVD         DFAVD
     c                   move      HEOPD         DFOPD
     c                   z-add     1             DFFBNR
     C                   MOVE      HESG          DFSG
     C                   WRITE     DOKUFR
      * Lag Trip header i SS
     C                   CLEAR                   GSSTRFR
     C                   END
     c                   movel     heavd         TRTNO
     c                   move      heopd         TRTNO
     c                   end
     c     CurTurN       IFNE      *ZEROS
     c  N55              update    gsstrfr
     c                   else
     c                   move      TRTNO         CurTurN
     c                   move      TRTNO         CurTur
     c                   move      'Y'           NYTUR
     c                   write     gsstrfr
     C                   EXSR      OPDLOGG1
     c                   end
      ******
      ******  ER NÅ SIKKER PÅ AT BÅDE GSSTRF / HEADF / DOKUF  FINNES
      ******  OPPDATER AVSENDER MOTTAKER MM
      ******  (Dette KUNNE vært gjort uten ekstra chain, men det ble
      ******    greier å forenkle OPPRETTING..)
      * Oppdater fraktbrev
      * OBS!!! Endelig oppdatering må skje i det en
      *        kjører liste - oppdaterer antall kll mm /skriver fraktbrevet
     c                   Movel     curturn       DFAVD
     c                   Move      curturn       DFOPD
     c     DokufKey      chain     Dokuf                              33
     c     *in33         IFeq      *OFF
      * setter stauskode "N" (skal Ikke skrives..) Rutine for utskrift
      * er uansett kall på syfa12 som om mail/kopi-skrives
     c                   move      'N'           DFST
     c                   move      'S'           DFKDME
      * Avsender
     c                   movel     SHIPFROM      DFNAVS
     c     AvsKey        Chain     Cundl01                            34
     c     *in34         ifeq      '0'
     C                   MOVE      KNAVN         DFNAVS
     C                   MOVE      ADR1          DFAD1S
     C                   MOVE      ADR2          DFAD2S
     C                   MOVE      POSTNR        DFPNLS
     C                   MOVEL     ADR3          DFAD3S
     c                   end
      * Mottaker
     c     ShipToB       ifeq      'DIREC'
      * Direkte mot sluttkunde
     c                   movel     WSTRAD1       DFNAVM
     c                   movel     WSTRAD2       DFAD1M
     c                   movel     WSTRAD3       DFAD2M
      * Postnr / Sted-linje avhenfig av norsk/utenlandsk
     C     WSTRADLK      IFEQ      '  '
     C     WSTRADLK      OREQ      'NO'
     c                   movel     WSTRAD4       DFAD3M
     c                   move      *Blanks       DFPNUM
     c                   move      *Blanks       DFLKM
     C                   ELSE
      * Status U/V = utenlands layout..
     c                   move      'V'           DFST
     c                   movel     WSTRADPNU     DFPNUM
     C     WSTRAD410     IFNE      ' '
     c                   movel     WSTRADSTU1    DFAD3M
     C                   ELSE
     c                   movel     WSTRADSTU2    DFAD3M
     C                   END
     c                   movel     WSTRADLK      DFLKM
     C                   END
     c     VADRKEY       Chain     VADR                               34
     c     *in34         ifeq      '0'
     c                   movel     VADRNA        DFNAVM
     c                   movel     VADRN1        DFAD1M
     c                   movel     VADRN2        DFAD2M
      * Postnr / Sted-linje avhenfig av norsk/utenlandsk
     C     VALAND        IFEQ      '  '
     C     VALAND        OREQ      'NO'
     c                   movel     VADRN3        DFAD3M
     c                   move      *Blanks       DFPNUM
     c                   move      *Blanks       DFLKM
     c                   else
     c                   move      'V'           DFST
     c                   movel     VADRN4PNU     DFPNUM
     C     VADRN4410     IFNE      ' '
     c                   movel     VADRN4STU1    DFAD3M
     C                   ELSE
     c                   movel     VADRN4STU2    DFAD3M
     C                   END
     c                   movel     VALAND        DFLKM
     c                   end
     c                   end
     c                   else
      * Til hub
     c                   movel     SHIPTOB       DFNAVM
     c     MotKey        Chain     Cundl01                            34
     c     *in34         ifeq      '0'
     C                   MOVE      KNAVN         DFNAVM
     C                   MOVE      ADR1          DFAD1M
     C                   MOVE      ADR2          DFAD2M
      * Postnr / Sted-linje avhenfig av norsk/utenlandsk
     C     SYLAND        IFEQ      '  '
     C     SYLAND        OREQ      'NO'
     C     POSTNR        IFNE      *ZEROS
     C                   MOVEL     POSTNR        TMPPNR
     c                   eval      DFAD3M = TMPPNR + ' ' + ADR3
     c                   else
     c                   eval      DFAD3M = ADR3
     c                   end
     c                   move      *Blanks       DFPNUM
     c                   move      *Blanks       DFLKM
     c                   else
     c                   move      'V'           DFST
     c                   movel     SYPOGE        DFPNUM
     c                   movel     ADR3          DFAD3M
     c                   movel     SYLAND        DFLKM
     c                   end
      *
     c                   end
     c                   end
      * Levadr etter mottaker
     c                   movel     DFNAVM        DFNAVL
     c                   movel     DFAD1M        DFAD1L
     c                   movel     DFAD2M        DFAD2L
     C     DFLKM         IFEQ      '  '
      *NORSK
     c                   movel     DFAD3M        DFADUL
     C                   MOVE      *zeros        DFPOUL
     c                   movel     DFAD3M        TMPPNR            4
     C                   TESTN                   tmpPNR               34
     C     *IN34         IFEQ      '1'
     C                   MOVE      TMPPNR        DFPOUL
     C                   MOVE      ADR3          XXAD25           25
     C                   MOVE      *BLANKS       DFADUL
     C                   MOVEL     XXAD25        DFADUL
     C                   END
     C                   ELSE
      *UTENLANDSK
     c                   movel     DFPNUM        DFPNUL
     c                   movel     DFAD3M        DFADUL
     c                   movel     DFLKM         DFLKL
     C                   END
      *
     C                   MOVE      TRANSP        TNR
     c     TranKey       Chain     TRAN                               34
     C     *in34         ifeq      '0'
     C                   MOVEL     TRANSP        DFTRAN
     C                   MOVEL     VMNAVN        DFNAT
      * FRAKTBREV IKKE TILDELT - TRANSP.KATEG. 'X' - TILDEL NÅ
     C     VMTKAT        IFEQ      'X'
     c     DF1004        ANDEQ     *blanks
     C                   call      'SYGE15'
     c                   parm                    XXsndn           20
     c                   move      xxsndn        SNDNR17          17
     c                   movel     SNDNR17       DF1004
     C                   END
     C                   END
     C                   UPDATE    DOKUFR
     c                   end
      *
      * Oppdater GSSTRL01
     c     CurTurN       chain     GSSTRL01                           55
     c     *in55         ifeq      *OFF
     C                   EXSR      OPDLOGG2
     c                   move      KUNDEN        TRCNO
     c                   move      SHIPFROM      TRLOCF
      * TIL HUB ..
     c     SHIPTOB       IFNE      'DIREC'
     C                   move      *blanks       TRBCD
     c                   move      SHIPTOB       TRLOCT
     c                   eval      WSTRAD1 =  DFNAVM
     c                   eval      WSTRAD2 =  DFAD1M
     c                   eval      WSTRAD3 =  DFAD2M
     c                   eval      WSTRAD4 =  DFAD3M
     C     DFLKM         IFEQ      '  '
     c                   move      *BLANKS       WSTRAD5
     c                   else
     C                   MOVE      DFLKM         KLKLK
     C     LANDKEY       CHAIN     KODTLK                             33
     C                   EVAL      WSTRAD5 =  KLKLK + ' ' + KLKNVN
     C                   END
      * TIL SLUTTKUNDE..
     c                   else
     C                   move      BAATCODE      TRBCD
     c                   move      *BLANKS       TRLOCT
     c                   end
      *
     c                   move      WSTRAD1       TRAD1
     c                   move      WSTRAD2       TRAD2
     c                   move      WSTRAD3       TRAD3
     c                   move      WSTRAD4       TRAD4
     c                   move      WSTRAD5       TRAD5
     c                   move      WSTRAD6       TRAD6
     c                   move      DATON         TRDTS
      * forutsette foreløpig samme mottaksdato
     c                   move      DATON         TRDTL
     c                   move      SHIPFIRM      TRFC
     c                   update    gsstrfr
     c                   end

      *
     C     UtTest        endsr
      *=====================================================================
      *  Oppdater loggen
      *=====================================================================
     C     OPDLOGG1      begsr
      *
     C                   MOVE      HEAVD         TTAVD
     C                   MOVE      HEOPD         TTOPD
     C                   MOVE      WDT           TTDATE
     C                   MOVE      WTM           TTTIME
     C                   MOVE      WDT           TTDATL
     C                   MOVE      WTM           TTTIML
     C                   MOVE      'STR'         TTACTI
     C                   MOVE      WSUSER        TTUSER
     C                   EVAL      TTTEXT = XXTXT01
n01  C                   EVAL      TTTEXL = XXTXT01L
     C                   WRITE     TRACKFR
      *
     C                   endsr
      *=====================================================================
      *  Oppdater loggen
      *=====================================================================
     C     OPDLOGG2      begsr
      *
     C                   MOVE      HEAVD         TTAVD
     C                   MOVE      HEOPD         TTOPD
     C                   MOVE      DATON         TTDATE
     C                   MOVE      *ZEROS        TTTIME
     C                   MOVE      WDT           TTDATL
     C                   MOVE      WTM           TTTIML
     C                   MOVE      'STR'         TTACTI
     C                   MOVE      WSUSER        TTUSER
     C                   EVAL      TTTEXT = XXTXT02
     C                   CAT       'From loc.':5 TTTEXT
     C                   CAT       SHIPFROM:1    TTTEXT
     C                   CAT       'to loc.':1   TTTEXT
     C                   CAT       SHIPTO:1      TTTEXT
     C                   CAT       'Reciever:':5 TTTEXT
     c                   CAT       WSTRAD1:1     TTTEXT
n01  C                   EVAL      TTTEXL = XXTXT02L
n01  C                   CAT       'Fra loc.':5  TTTEXL
n01  C                   CAT       SHIPFROM:1    TTTEXL
n01  C                   CAT       'til loc.':1  TTTEXL
n01  C                   CAT       SHIPTO:1      TTTEXL
n01  C                   CAT       'Mottager:':5 TTTEXL
n01  c                   CAT       WSTRAD1:1     TTTEXL
     C                   WRITE     TRACKFR
      *
     C                   endsr
      *************************************
     C     GETTUR        BEGSR
      *************************************
     c     CurTurN       chain(N)  GSSTRL01                           55
      * Uavh. om endring eller ny...
     c     *in55         ifeq      *OFF
     c                   move      TRCNO         KUNDEN
     C                   move      TRBCD         BAATCODE
     c                   move      TRAD1         WSTRAD1
     c                   move      TRAD2         WSTRAD2
     c                   move      TRAD3         WSTRAD3
     c                   move      TRAD4         WSTRAD4
     c                   move      TRAD5         WSTRAD5
     c                   move      TRAD6         WSTRAD6
     c                   move      TRDTS         DATON
     c                   move      TRLOCT        SHIPTOB
     c     shiptoB       ifeq      *blanks
     c                   movel     'DIREC'       SHIPTOB
     c                   end
     c                   move      CurTurN       TRTNO
     c                   end
      * visse verdier fra DOKUF
     c                   Movel     CurturN       DFAVD
     c                   Move      CurturN       DFOPD
     c     DokufKey      chain     Dokuf                              33
     c     *in33         IFeq      *OFF
     C                   movel     dftran        ShipByA
     c                   end
      *
     C                   endsr
      *=====================================================================******
      * Parse input
      *=====================================================================******
     C     Parse         begsr
     C                   eval      LAGRETUR = zhbgetvar('LAGRETUR')
     C                   eval      lng     = zhbgetvar('lng')
     C                   eval      DATO    = zhbgetvar('DATO')
     C                   eval      DATON   = c2n2(DATO)
     C                   eval      WSUSER  = zhbgetvar('USER')
     C                   EVAL      UsrSpcName  = zhbgetvar('UsrSpcName')
     C                   eval      CURTUR  = zhbgetvar('CURTUR')
     C                   eval      CURTURN = c2n2(CURTUR)
     C                   eval      KUNDE   = zhbgetvar('KUNDE')
     C                   eval      KUNDEN  = c2n2(KUNDE)
     C                   eval      WSTRAD1 = zhbgetvar('WSTRAD1')
     C                   eval      WSTRAD2 = zhbgetvar('WSTRAD2')
     C                   eval      WSTRAD3 = zhbgetvar('WSTRAD3')
     C                   eval      WSTRAD4 = zhbgetvar('WSTRAD4')
     C                   eval      WSTRAD5 = zhbgetvar('WSTRAD5')
     C                   eval      WSTRAD6 = zhbgetvar('WSTRAD6')
     C                   eval      SHIPTO  = zhbgetvar('SHIPTO')
     C                   eval      MOTKNR  = c2n2(SHIPTOA)
     C                   eval      SHIPBY  = zhbgetvar('SHIPBY')
     C                   eval      TRANSP  = c2n2(SHIPBYA)
     C                   eval      ADRNR   = zhbgetvar('ADRNR')
     C                   eval      ADRNRN  = c2n2(ADRNR)
     C                   eval      BAATCODE   = zhbgetvar('BAATCODE')
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$top
      *  (search criteria)
      *=====================================================================******
     C     SetTop        begsr
      * Repeat national language for the next invocation
     C                   callp     updhtmlvar('LNG':lng:
     C                             InitHTMLVars)
      * Color for text, background, links, visited links, active links
     C                   callp     updhtmlvar('TXTCOLOR':'black')
     C                   callp     updhtmlvar('BOLD':' ')
     C                   callp     updhtmlvar('BGCOLOR':BIFARG)
     C                   callp     updhtmlvar('LINK':'0000FF')
     C                   callp     updhtmlvar('VLINK':'FF0000')
     C                   callp     updhtmlvar('ALINK':'00FF00')
     C                   callp     updHTMLvar('DATO':
     C                             %trim(%editw(DATON:'    .  .  ')))
     C                   callp     updhtmlvar('CURTUR':
     C                             %trim(%editc(CURTURN:'3')))
     C                   callp     updhtmlvar('NYTUR':NYTUR)
     C                   callp     updhtmlvar('WSTRAD1':WSTRAD1)
     C                   callp     updhtmlvar('WSTRAD2':WSTRAD2)
     C                   callp     updhtmlvar('WSTRAD3':WSTRAD3)
     C                   callp     updhtmlvar('WSTRAD4':WSTRAD4)
     C                   callp     updhtmlvar('WSTRAD5':WSTRAD5)
     C                   callp     updhtmlvar('WSTRAD6':WSTRAD6)
     C                   callp     updhtmlvar('SHIPTO':SHIPTO)
     C                   callp     updhtmlvar('SENDNR':DF1004)
BODY C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updHtmlVar('USER':WSUSER)
     C                   callp     updHtmlVar('UsrSpcName':UsrSpcName)
     C                   callp     updHtmlVar('BAATCODE':BAATCODE)
     C                   callp     updHTMLvar('sdato':
     C                             %trim(%editw(BIDTV:'    .  .  ')))
     C                   callp     updHTMLvar('stid':
     C                             %trim(%editw(BITIDV:'  .  .  ')))
     C                   callp     updHtmlVar('AVSLUTT':AVSLUTT)
      *
     C                   endsr
      *=====================================================================******
      * Set html output skeleton member before its read into memory
      *=====================================================================******
     C     SetSkl        begsr
      *------------------
      * Set html output skeleton member
     C                   eval      lng = uppify(lng)
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC' + lng
     C                   eval      Mbr = 'SS112'
      *
     C                   endsr
      *=====================================================================******
      *  Display Kunder        to select from
      *=====================================================================******
     C     DspKund       begsr
      *
     C                   MOVE      'SSCUS'       PAID
     C     BrParKey      setll     BRPARF
     C     BrParKey      reade     BRPARF                                 40
     C     *in40         doweq     '0'
     C                   exsr      SetSltRow
     C                   callp     wrtsection('typsltrow')
     C     BrParKey      reade     BRPARF                                 40
     C                   enddo
      *
     C                   callp     wrtsection('typsltend')
      *
     C                   endsr
      *=====================================================================******
      *  Display Ship by       to select from
      *=====================================================================******
     C     DspTran       begsr
      *
     C                   MOVE      'TRANS'       PAID
     C     BrParKey      setll     BRPARF
     C     BrParKey      reade     BRPARF                                 40
     C     *in40         doweq     '0'
     C                   exsr      SetSltRow2
     C                   callp     wrtsection('typsltrow2')
     C     BrParKey      reade     BRPARF                                 40
     C                   enddo
      *
     C                   callp     wrtsection('typsltend2')
      *
     C                   endsr
      *=====================================================================******
      *  Display Ship To       to select from
      *=====================================================================******
     C     DspShipTo     begsr
      *
     C                   MOVE      'SSHUB'       PAID
     C     BrParKey      setll     BRPARF
     C     BrParKey      reade     BRPARF                                 40
     C     *in40         doweq     '0'
     C                   exsr      SetSltRow3
     C                   callp     wrtsection('typsltrow3')
     C     BrParKey      reade     BRPARF                                 40
     C                   enddo
      *
     C                   callp     wrtsection('typsltend3')
      *
     C                   endsr
      *=====================================================================******
      *  Display end Customer Adress to select from
      *=====================================================================******
     C     DspAdr        begsr
      *
     C     Vadr1Key      setll     VADR1
     C     Vadr1Key      reade     VADR1                                  40
     C     *in40         doweq     '0'
     C                   exsr      SetSltRow4
     C                   callp     wrtsection('typsltrow4')
     C     Vadr1Key      reade     VADR1                                  40
     C                   enddo
      *
     C                   callp     wrtsection('typsltend4')
      *
     C                   endsr
      *=====================================================================******
      *  Set variable data for section /$typsltrow
      *=====================================================================******
     C     SetSltRow     begsr
     C                   Z-ADD     PAVRDN        KUNRN             8 0
     C                   callp     updhtmlvar('SLKNR':
     C                             %trim(%editc(KUNRN:'Z')))
     C                   callp     updhtmlvar('SLNAVN':PAVRDA)
      * Når kundenr alt er valgt (inp.param.) vis posten også i pulldown
     C     KUNDEN        IFNE      *ZEROS
     C     KUNDEN        ANDEQ     KUNRN
     C                   callp     updhtmlvar('KSEL':'SELECTED')
     c                   else
     C                   callp     updhtmlvar('KSEL':spaces)
     c                   end
     C                   endsr
      *=====================================================================******
      *  Set variable data for section /$typsltrow2
      *=====================================================================******
     C     SetSltRow2    begsr
     C                   z-add     PAVRDN        TNR               8 0
     C                   MOVEL     TNR           SLTNRA
     C                   MOVEL     PAVRDA        SLTNRB
     c     TranKey       Chain     TRAN                               33
     c   33              movel     '*UNKNOWN*'   VMNAVN
     C                   MOVEL     VMNAVN        SLTNAV           45
     C                   callp     updhtmlvar('SLTNR':SLTNR)
     C                   callp     updhtmlvar('SLTNAV':SLTNAV)
      * Når shipto  alt er valgt (inp.param.) vis posten også i pulldown
     C                   callp     updhtmlvar('SBSEL':spaces)
     C     SHIPBYA       IFEQ      SLTNRA
     C                   callp     updhtmlvar('SBSEL':'SELECTED')
     c                   else
     C                   callp     updhtmlvar('SBSEL':spaces)
     c                   end
     C                   endsr
      *=====================================================================******
      *  Set variable data for section /$typsltrow3
      *=====================================================================******
     C     SetSltRow3    begsr
     C                   z-add     PAVRDN        KNR               8 0
     C                   MOVEL     KNR           SLLOCA
     C                   MOVEL     PAVRDA        SLLOCB
     C                   MOVEL     PAVRDA        SLLOCT           45
     C                   callp     updhtmlvar('SLLOC':SLLOC)
     C                   callp     updhtmlvar('SLLOCT':SLLOCT)
      * Når shipto  alt er valgt (inp.param.) vis posten også i pulldown
     C                   callp     updhtmlvar('STSEL':spaces)
     C     SHIPTOB       IFNE      *BLANKS
     C     SHIPTOB       ANDEQ     SLLOCB
     C                   callp     updhtmlvar('STSEL':'SELECTED')
     c                   else
     C                   callp     updhtmlvar('STSEL':spaces)
     c                   end
     C                   endsr
      *=====================================================================******
      *  Set variable data for section /$typsltrow4
      *=====================================================================******
     C     SetSltRow4    begsr
     c                   eval      NavnAdr = VADRNA + ' ' + VADRN3
     C                   callp     updhtmlvar('SLADR':
     C                             %trim(%editc(VADRNR:'Z')))
     C                   callp     updhtmlvar('SLADRT':NavnAdr)
     C                   endsr
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
     C                   OPEN      BRIDF
     C     WSUSER        CHAIN(N)  BRIDF                              50
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
     C                   OPEN      GSSTRL01
     C                   open      TRACKF
     C                   OPEN      BRPARF
     C                   OPEN      TELL
     C                   OPEN      HEADF
     C                   OPEN      DOKUF
     C                   OPEN      CundL01
     C                   OPEN      Vadr
     C                   OPEN      Vadr1
     C                   OPEN      KODTLK
     C                   OPEN      Tran
     C                   OPEN      GSSCGF
      *
     C     DokufKey      klist
     C                   kfld                    DFRI
     C                   kfld                    DFAVD
     C                   kfld                    DFOPD
     C                   kfld                    DFFBNR
     c                   move      'F'           DFRI
     c                   z-add     1             DFFBNR
      *
     C     AvsKey        klist
     C                   kfld                    BIFIRM
     C                   kfld                    AVSKNR            8 0
     C     MotKey        klist
     C                   kfld                    BIFIRM
     C                   kfld                    MOTKNR            8 0
     C     TranKey       klist
     C                   kfld                    BIFIRM
     C                   kfld                    TNR               8 0
     C     Vadr1Key      klist
     C                   kfld                    BIFIRM
     C                   kfld                    KUNDEN
     C     VadrKey       klist
     C                   kfld                    BIFIRM
     C                   kfld                    KUNDEN
     C                   kfld                    ADRNRN
     C     LandKey       klist
     C                   kfld                    KLKUNI
     C                   kfld                    KLKLK
     C                   MOVEL     'LK '         KLKUNI
      *
     C     BrParKey      klist
     C                   kfld                    PABRID
     C                   kfld                    PAKUNR
     C                   kfld                    PAID
     C                   MOVE      BIBRID        PABRID
     C                   MOVE      *zeros        PAKUNR
      * Hent avdeling for å lage turnr
     c                   Move      *zeros        XXAVD             4 0
     C                   MOVE      'SSAVD'       PAID
     C     BrParKey      Chain     BRPARF                             33
     c  N33              z-add     PAVRDN        XXAVD
      * Hent Firmakode shipssystem
     c                   Move      *blanks       SHIPFIRM          3
     C                   MOVE      'SSFIR'       PAID
     C     BrParKey      Chain     BRPARF                             33
     c  n33              MoveL     PAVRDA        SHIPFIRM
      * Hent fabrikk kode (Ship From) / Navn
     c                   Move      *blanks       SHIPFROM          5
     C                   MOVE      'SSFAB'       PAID
     C     BrParKey      Chain     BRPARF                             33
     c  n33              MoveL     PAVRDA        SHIPFROM
     c  N33              z-add     PAVRDN        AVSKNR
      * Hent clients båt-prefix -kode
     c                   Move      *blanks       BPREFIX           1
     C     KUNDEN        IFNE      *zeros
     C     KUNDEN        setll     GSSCGF
     C     KUNDEN        READE     GSSCGF                                 33
     c     *IN33         doweq     '0'
     c                   move      CGCG          TSTCG             1
     c     TSTCG         ifeq      '*'
     c                   movel     CGCG          BPREFIX
     c                   leave
     c                   end
     C     KUNDEN        READE     GSSCGF                                 33
     c                   end
     c                   end
      *
     C                   TIME                    WTIME
     C                   MOVE      WÅÅ           DT8Å
     C                   MOVE      WMM           DT8M
     C                   MOVE      WDD           DT8D
     C     DATON         IFEQ      *ZEROS
     C                   MOVE      DT8           DATON
     C                   END
     c                   move      *blanks       feilmelding
     c                   move      *blanks       fmeldnr
     C                   MOVE      'N'           AVSLUTT           1
     C                   MOVE      'N'           NYTUR             1
      *
     C                   endsr
      *=====================================================================******
      *  AVSLUTT
      *=====================================================================******
     C     EXIT          begsr
     C                   callp     wrtsection('endhtml')
     C                   callp     wrtsection('*fini')
     C                   close     BRIDF
     C                   close     GSSTRL01
     C                   close     TRACKF
     C                   close     BRPARF
     C                   close     TELL
     C                   close     HEADF
     C                   close     DOKUF
     C                   close     CundL01
     C                   close     vadr
     C                   close     vadr1
     C                   close     KODTLK
     C                   close     Tran
     C                   close     GSSCGF
     C                   eval      *inlr = *on
      *
     C                   endsr
