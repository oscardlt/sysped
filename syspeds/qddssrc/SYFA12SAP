********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 YBC PTF10 FEIL STARTPOSISJON FOR REFERANSE
10XXX * 05 JOV PTF11 FNC1 INN I FRONT AV ALLE BARCODES (UNC/GS1-128)
********************************************************************
      *  PDF/AFP (*AFPDS) BARCODE DIREKTE I DDS
      *  PRINT HUMAN READABLE DOBBELT OPP VED AFP - ENKELTE PRINTERE TAKLER IKKE
     A          R LABEL
     A                                  3  5'Fra/from'
     A                                      FNTCHRSET(C0D0GC15 T1D0BASE)
     A                                  3 20'Utskr.dato'
     A                                      FNTCHRSET(C0D0GC15 T1D0BASE)
     A            WDATO          8  0   3 27EDTWRD('  /  /    ')
     A                                      FNTCHRSET(C0D0GC15 T1D0BASE)
     A            WTID4          4  0   3 35EDTWRD('  :  ')
     A                                      FNTCHRSET(C0D0GC15 T1D0BASE)
     A            DFNAVS        30A     4  9FNTCHRSET(C0S0EBTR T1D0BASE)
     A            DFAD1S        30A     5  9FNTCHRSET(C0S0EBTR T1D0BASE)
     A            AVSPNS        30A     6  9FNTCHRSET(C0S0EBTR T1D0BASE)
      *
     A                                  6  5'__________________________________'
     A                                      FNTCHRSET(C0S0OB10 T1D0BASE)
      *
     A                                  7  5'Til/to'
     A                                      FNTCHRSET(C0D0GC15 T1D0BASE)
     A            MOTNAV        30A     8  9FNTCHRSET(C0S0OB10 T1D0BASE)
     A            MOTADR        30A    10  9FNTCHRSET(C0S0OB10 T1D0BASE)
     A            MOTPNR         5A    12  9FONT(2305 (*POINTSIZE 25))
     A            MOTSTE        25A    12 19FONT(2305 (*POINTSIZE 15))
      *
     A            VIASTREK      35A    12  8
     A            VIALED        35A    13  5FNTCHRSET(C0D0GC15 T1D0BASE)
     A            VIATXT1       35A    14  9FNTCHRSET(C0S0EBTR T1D0BASE)
     A            VIATXT2       35A    15  9FNTCHRSET(C0S0EBTR T1D0BASE)
     A            VIATXT3       35A    16  9FNTCHRSET(C0S0EBTR T1D0BASE)
      *
     A                                 16  5'__________________________________'
     A                                      FNTCHRSET(C0S0OB10 T1D0BASE)
      *
     A                                 17  5'Transportor/Carrier'
     A                                      FNTCHRSET(C0D0GC15 T1D0BASE)
     A  62                             17 27' TOLLPASS ! '
     A                                      FNTCHRSET(C0D0GR10 T1D0BASE)
     A            TRANAVN       30A    18  9FNTCHRSET(C0S0EBTR T1D0BASE)
     A  60        HEFT1A        30A    20  5FNTCHRSET(C0D0GR10 T1D0BASE)
     A  61                             21  5'Mottaker (MyPack):'
N05  A                                      FNTCHRSET(C0D0GC15 T1D0BASE)
     A  61        MYPMOT        50A    22  7FNTCHRSET(C0D0GB12 T1D0BASE)
     A  61        MYPADR        50A    23  7FNTCHRSET(C0D0GB12 T1D0BASE)
     A N61                             21  5'Produkt:'
     A                                      FNTCHRSET(C0D0GC15 T1D0BASE)
     A N61        PRODUKT       50A    22  7FNTCHRSET(C0S0EBTR T1D0BASE)
     A                                 24  5'Merke/marks:'
     A                                      FNTCHRSET(C0D0GC15 T1D0BASE)
     A            MARKS         30A    24 14FNTCHRSET(C0D0GB10 T1D0BASE)
     A N63                             25  5'A-ref:'
N05  A                                      FNTCHRSET(C0D0GC15 T1D0BASE)
     A  63                             25  5'Oppd.gi.ref:'
     A                                      FNTCHRSET(C0D0GC15 T1D0BASE)
     A*           HERFA         15A    25 14FNTCHRSET(C0D0GB10 T1D0BASE)
S01  A*           HERFA15       15A    25 14FNTCHRSET(C0D0GB10 T1D0BASE)
N01  A**          HERFA15       15A    25 18FNTCHRSET(C0D0GB10 T1D0BASE)
N01  A            HERFA         15A    25 18FNTCHRSET(C0D0GB10 T1D0BASE)
     A  63                             26  5'Transp. ref:'
     A                                      FNTCHRSET(C0D0GC15 T1D0BASE)
S01  A*           SENDREF       30A    26 14FNTCHRSET(C0D0GB10 T1D0BASE)
S01  A*           DUPAV         20     26 30FNTCHRSET(C0D0GC15 T1D0BASE)
N01  A            SENDREF       30A    26 18FNTCHRSET(C0D0GB10 T1D0BASE)
N01  A            DUPAV         20     26 34FNTCHRSET(C0D0GC15 T1D0BASE)
     A                                 29  5'Kolli'
     A                                      FNTCHRSET(C0D0GC15 T1D0BASE)
     A            KLLTXT        20A    29 14FNTCHRSET(C0S0OB10 T1D0BASE)
      *
      *
      *
     A                                 30  5'__________________________________'
     A                                      FNTCHRSET(C0S0OB10 T1D0BASE)
      *
     A                                 31  5'Sendings-ID/Consignment'
     A                                      FNTCHRSET(C0D0GC15 T1D0BASE)
S05  A*           SNDN20        20     32 10BARCODE(CODE128 5 *HRI *HRZ +
N05  A            SNDN21        21     32 10BARCODE(CODE128 5 *NOHRI +
     A                                      (*WIDTH 0.018) (*RATIO 3) X'02')
N05  A            HRI_SNDN      25A    38 12
      *
     A                                 39  5'__________________________________'
     A                                      FNTCHRSET(C0S0OB10 T1D0BASE)
      * (51=NORSK, 54-59 = UTENLANDSK 4-9 LANGT)
     A                                 40  5'Til/To'
     A                                      FNTCHRSET(C0D0GC15 T1D0BASE)
S05  A* 51N63     PONR10        11     41 10BARCODE(CODE128 5 *HRI *HRZ +
N05  A  51N63     PONRNO        11     41 10BARCODE(CODE128 5 *NOHRI +
     A                                      (*WIDTH 0.018) (*RATIO 3) X'02')
N05   * ALLE BARCODES-LENGDE ØKT MED EN FOR Å FÅ PLASS TIL FNC1 (X'F8') I FRONT
E05  A  54N63     PONRUL4       11     41 10BARCODE(CODE128 5 *NOHRI +
     A                                      (*WIDTH 0.018) (*RATIO 3) X'02')
E05  A  55N63     PONRUL5       12     41 10BARCODE(CODE128 5 *NOHRI +
     A                                      (*WIDTH 0.018) (*RATIO 3) X'02')
E05  A  56N63     PONRUL6       13     41 10BARCODE(CODE128 5 *NOHRI +
     A                                      (*WIDTH 0.018) (*RATIO 3) X'02')
E05  A  57N63     PONRUL7       14     41 10BARCODE(CODE128 5 *NOHRI +
     A                                      (*WIDTH 0.018) (*RATIO 3) X'02')
E05  A  58N63     PONRUL8       15     41 10BARCODE(CODE128 5 *NOHRI +
     A                                      (*WIDTH 0.018) (*RATIO 3) X'02')
E05  A  59N63     PONRUL9       16     41 10BARCODE(CODE128 5 *NOHRI +
     A                                      (*WIDTH 0.018) (*RATIO 3) X'02')
     A  63N64     MOTPNRB        5A    46 10FONT(2305 (*POINTSIZE 70))
     A  63 64                          44  5'IKKE UTKJØRING'
     A                                      FONT(2305 (*POINTSIZE 25))
     A  63 64                          46  5'(Hentes på terminal el. eksport.)'
     A                                      FONT(2305 (*POINTSIZE 15))
N05  A N63        HRI_PONR      25A    47 12
      *
     A                                 48  5'__________________________________'
     A                                      FNTCHRSET(C0S0OB10 T1D0BASE)
      *
     A                                 49  5'Kolli-ID/Licence plate'
     A                                      FNTCHRSET(C0D0GC15 T1D0BASE)
S05  A*           KOID20        20     50 10BARCODE(CODE128 5 *HRI *HRZ +
N05  A            KOID21        21     50 10BARCODE(CODE128 5 *NOHRI +
     A                                      (*WIDTH 0.018) (*RATIO 3) X'02')
N05  A            HRI_KOID      25A    56 12
