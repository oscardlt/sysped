      ***********************************************************
      *** PROGRAM SKAL SENDE XML-OPPDRAG TIL AMPHORA          ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01-26                            ***
      *** LEDIGE INDIKATORER 27-32 34-49 51-99                ***
      ***********************************************************
     FXMLTILP1K O  A E           K DISK    USROPN
     FHEADF     IF   E           K DISK
     FFRISOK    IF   E           K DISK
     FTRACKT    IF   E           K DISK
     FFIRM      IF   E           K DISK
     FCUNDL01   IF   E           K DISK
     FKODTFR    IF   E           K DISK
     FEDIC      UF A E             DISK
     FEDIS      O  A E             DISK
     FEDIM      O  A E             DISK
     FEDISE1K   O  A E             DISK
     D                 DS
     D  XTXT801                1     80
     D  XT801                  1     80    DIM(80)
     D                 DS
     D  XTXT802                1     80
     D  XT802                  1     80    DIM(80)
     D                 DS
     D  XMBR                   1     10
     D  XMBRA                  1      1
     D  XMBRB                  2     10S 0
     D  XMBRC                  2     10
     D                 DS
     D  WDT                    1      8
     D  WDTÅ                   1      4
     D  WDTM                   5      6
     D  WDTD                   7      8
     D                 DS
     D  WTM                    1      6
     D  WTMH                   1      2
     D  WTMM                   3      4
     D  WTMS                   5      6
     D                 DS
     D  HEDTOP                 1      8  0
     D  HEDTOPÅ                1      4
     D  HEDTOPM                5      6
     D  HEDTOPD                7      8
     D                 DS
     D  HEAVD                  1      4  0
     D  HEOPD                  5     11  0
     D  HEAVDOPD               1     11
     D  HESG                  12     14
     D  SYSPEDREF              1     14
     D                 DS
     D  TXETDD                 1      8  0
     D  TXETDDÅ                1      4
     D  TXETDDM                5      6
     D  TXETDDD                7      8
     D                 DS
     D  TXETAD                 1      8  0
     D  TXETADÅ                1      4
     D  TXETADM                5      6
     D  TXETADD                7      8
     D                 DS
     D  TXATAD                 1      8  0
     D  TXATADÅ                1      4
     D  TXATADM                5      6
     D  TXATADD                7      8
      *
     C                   EXSR      INIT
      *
     C                   EXSR      SRINIT
     C                   EXSR      SRBYGGF
      *
     C                   EXSR      SRSTART
      *
     C                   EXSR      SRMSGDET
      *
     C                   EXSR      SRCONSTR
      *
     C                   EXSR      SRSLUTT
     C                   EXSR      SRCLOSE
     C                   EXSR      SRSEND
      *
     C                   MOVE      *ON           *INLR
     C                   RETURN
      *
      ***********************************************
     C     SRINIT        BEGSR
      ***********************************************
      *
     C     1             CHAIN     EDIC                               33
     C                   ADD       1             CSN
     C                   ADD       1             CMN
     C                   UPDATE    EDICR
      *
     C                   MOVE      *BLANKS       XMBR
     C                   MOVE      'F'           XMBRA
     C                   MOVE      CSN           XMBRB
     C                   MOVE      XMBR          SEMBR
     C                   MOVE      *BLANKS       SDATA
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT
     C                   PARM                    WTM
      *
     C                   EVAL      S0004 = 'SYSPEDNO'
     C                   EVAL      S0010 = 'AMPHORA'
     C                   EVAL      S0020 = FSSOK
     C                   EVAL      S0026 = 'XML-ORDREDATA'
     C                   EVAL      SSN   = CSN
     C                   EVAL      SSR   = 'S'
     C                   EVAL      SST   = 'C'
     C                   MOVE      WDT           SDT
     C                   MOVE      WTM           STM
     C                   EVAL      SLIB  = '*LIBL'
     C                   EVAL      SFILE = 'EDISE1K'
     C                   EVAL      SMBR  = SEMBR
     C                   EVAL      SMBRS = 0
     C                   EVAL      S0036 = 1
      *
     C                   ENDSR
      *
      ***********************************************
     C     SRBYGGF       BEGSR
      ***********************************************
     C                   CALL      'SYXML21B'
     C                   OPEN      XMLTILP1k
      *
     C                   ENDSR
      *
      ***********************************************
     C     SRSTART       BEGSR
      ***********************************************
     C                   EVAL      SDATA = '<?xml version="1.0" '
     C                             + 'encoding="ISO-8859-1" ?>'
     C                   EXSR      TILFIL
     C                   EVAL      SDATA = '<Transport collaboration="JobOrder"'
     C                             + ' messageFunction="JobOrder"'
     C                             + ' versionBasedOn="3.2"'
     C                             + ' language="no" domain="Transport"'
     C                             + ' profile="BringInhouse" definedBy="BIG"'
     C                             + ' messageId="' + %trim(SMBR) +'"'
     C                             + ' updateIndicator="Original" version="1.1"'
     C                             + ' xmlns:xsi="http://www.w3.org/2001'
     C                             + '/XMLSchema-instance">'
     C                   EXSR      TILFIL
      *
     C                   ENDSR
      *
      ***********************************************
     C     SRMSGDET      BEGSR
      ***********************************************
     C                   EVAL      SDATA = '<MessageDetails>'
     C                   EXSR      TILFIL
     C                   EVAL      SDATA = '<MessageSender partyId="'
     C                             + %trim(S0004) + '" idType="14">'
     C                             + '</MessageSender>'
     C                   EXSR      TILFIL
     C                   EVAL      SDATA = '<MessageRecipient partyId="'
     C                             + %trim(S0010) + '" idType="14">'
     C                             + '</MessageRecipient>'
     C                   EXSR      TILFIL
     C                   EVAL      SDATA = '<MessageDate timeZone="+01:00"'
     C                             + ' dstIndicator="YES">'
     C                             + WDTÅ + '-' + WDTM + '-' + WDTD + 'T'
     C                             + WTMH + ':' + WTMM + ':' + WTMS
     C                             + '</MessageDate>'
     C                   EXSR      TILFIL
     C                   EVAL      SDATA = '</MessageDetails>'
     C                   EXSR      TILFIL
      *
     C                   ENDSR
      *
      ***********************************************
     C     SRCONSTR      BEGSR
      ***********************************************
     C                   EVAL      SDATA = '<ConsignmentStructure'
     C                             + ' structureType="Single">'
     C                   EXSR      TILFIL
     C                   EVAL      SDATA = '<ConsignmentSet>'
     C                   EXSR      TILFIL
      * Consignment
     C                   EXSR      SRCONSIG
      * ConsignmentGoodsItemSet
     C                   EXSR      SRCONGIT
      * Party
     C                   EXSR      SRPARTY
      * TransportLeg
     C                   EXSR      SRTRSLEG
      * TermsOfDelivery
     C                   EXSR      SRTERMS
      * Service
     C                   EXSR      SRSERVIC
      * DateAndTimes
     C                   EXSR      SRDATTIM
      *
     C                   EVAL      SDATA = '</ConsignmentSet>'
     C                   EXSR      TILFIL
     C                   EVAL      SDATA = '</ConsignmentStructure>'
     C                   EXSR      TILFIL
      *
     C                   ENDSR
      *
      ***********************************************
     C     SRCONSIG      BEGSR
      ***********************************************
     C                   EVAL      SDATA = '<Consignment consignmentId="'
     C                             + %trim(HEAVDOPD)
     C                             + '" idType="XXX">'
     C                   EXSR      TILFIL
      * Total
     C                   EVAL      SDATA = '<TotalGrossWeight unitCode="KGM">'
     C                             + %trim(%editc(HEVKT:'Z'))
     C                             + '</TotalGrossWeight>'
     C                   EXSR      TILFIL
     C                   EVAL      SDATA = '<TotalVolume unitCode="MTQ">'
     C                             + %trim(%editc(HEM3:'3'))
     C                             + '</TotalVolume>'
     C                   EXSR      TILFIL
     C                   EVAL      SDATA = '<LoadingMetres unitCode="MTR">'
     C                             + %trim(%editc(HELM:'3'))
     C                             + '</LoadingMetres>'
     C                   EXSR      TILFIL
     C                   EVAL      SDATA = '<NumberOfPackages unitCode="PCE">'
     C                             + %trim(%editc(HENT:'Z'))
     C                             + '</NumberOfPackages>'
     C                   EXSR      TILFIL
      * Reference
     C                   EVAL      SDATA = '<Reference>'
     C                             + '<ReferenceNo>' + %trim(SYSPEDREF)
     C                             + '</ReferenceNo>'
     C                             + '<ReferenceType>AAS</ReferenceType>'
     C                             + '</Reference>'
     C                   EXSR      TILFIL
     C                   EVAL      SDATA = '<Reference>'
     C                             + '<ReferenceNo>' + %trim(HERFA)
     C                             + '</ReferenceNo>'
     C                             + '<ReferenceType>CU</ReferenceType>'
     C                             + '</Reference>'
     C                   EXSR      TILFIL
      *
     C                   EVAL      SDATA = '</Consignment>'
     C                   EXSR      TILFIL
      *
     C                   ENDSR
      *
      ***********************************************
     C     SRCONGIT      BEGSR
      ***********************************************
     C                   EVAL      SDATA = '<ConsignmentGoodsItemSet>'
     C                   EXSR      TILFIL
      *
     C                   EVAL      SDATA = '<GoodsItem goodsItemId="1">'
     C                   EXSR      TILFIL
     C                   EVAL      SDATA = '<GoodsDescription>'
     C                             + %TRIM(HEVS1) + ' ' + %TRIM(HEVS2)
     C                             + '</GoodsDescription>'
     C                   EXSR      TILFIL
     C                   EVAL      SDATA = '<NumberOfPackages>'
     C                             + %trim(%editc(HENT:'Z'))
     C                             + '</NumberOfPackages>'
     C                   EXSR      TILFIL
     C                   EVAL      SDATA = '<PackageTypeCode>PK'
     C                             + '</PackageTypeCode>'
     C                   EXSR      TILFIL
     C                   EVAL      SDATA = '<GrossWeight unitCode="KGM">'
     C                             + %trim(%editc(HEVKT:'Z'))
     C                             + '</GrossWeight>'
     C                   EXSR      TILFIL
     C                   EVAL      SDATA = '<NetWeight unitCode="KGM">'
     C                             + %trim(%editc(HEFBV:'Z'))
     C                             + '</NetWeight>'
     C                   EXSR      TILFIL
     C                   EVAL      SDATA = '<LoadingMetres unitCode="MTR">'
     C                             + %trim(%editc(HELM:'3'))
     C                             + '</LoadingMetres>'
     C                   EXSR      TILFIL
     C                   EVAL      SDATA = '<Volume unitCode="DMQ">'
     C                             + %trim(%editc(HEM3:'3'))
     C                             + '</Volume>'
     C                   EXSR      TILFIL
     C                   EVAL      SDATA = '</GoodsItem>'
     C                   EXSR      TILFIL
      *
     C                   EVAL      SDATA = '<Package>'  + '<Marking>'
     C                             + %TRIM(HEGM1) + ' ' + %TRIM(HEGM2)
     C                             + '</Marking>' + '</Package>'
     C                   EXSR      TILFIL
      *
     C                   EVAL      SDATA = '</ConsignmentGoodsItemSet>'
     C                   EXSR      TILFIL
      *
     C                   ENDSR
      *
      ***********************************************
     C     SRPARTY       BEGSR
      ***********************************************
      * Consignor
     C                   EVAL      KUNDNR = HEKNS
     C     CUNDL01K      CHAIN     CUNDL01                            33
     C                   EVAL      SDATA = '<Party subClass="Consignor" '
     C                             + 'partyId="' + %trim(%EDITC(HEKNS:'Z'))
     C                             + '" idType="86">'
     C                   EXSR      TILFIL
     C                   EVAL      XTXT801 = KNAVN
     C                   Z-ADD     35            XN53              3 0
     C                   EXSR      KNVST
     C                   EVAL      SDATA = '<Name>' + %trim(XTXT802) + '</Name>'
     C                   EXSR      TILFIL
     C                   EVAL      SDATA ='<Address subClass="PhysicalAddress">'
     C                   EXSR      TILFIL
     C                   MOVE      POSTNR        XPOSTNR           4
     C                   EVAL      SDATA = '<PostalCode>'
     C                             + %trim(XPOSTNR)
     C                             + '</PostalCode>'
     C                   EXSR      TILFIL
     C                   EVAL      XTXT801 = ADR3
     C                   Z-ADD     35            XN53              3 0
     C                   EXSR      KNVST
     C                   EVAL      SDATA = '<City>' + %trim(XTXT802) + '</City>'
     C                   EXSR      TILFIL
     C                   IF        SYLAND = *BLANKS
     C                   EVAL      SYLAND = FILAND
     C                   END
     C                   EVAL      SDATA = '<CountryCode>'
     C                             + %trim(SYLAND)
     C                             + '</CountryCode>'
     C                   EXSR      TILFIL
     C                   EVAL      XTXT801 = ADR1
     C                   Z-ADD     35            XN53              3 0
     C                   EXSR      KNVST
     C                   EVAL      SDATA = '<Street>' + %trim(XTXT802)
     C                             + '</Street>'
     C                   EXSR      TILFIL
     C                   EVAL      SDATA ='</Address>'
     C                   EXSR      TILFIL
     C                   EVAL      SDATA = '</Party>'
     C                   EXSR      TILFIL
      * DespatchingParty (henteadresse)
     C                   EVAL      SDATA = '<Party subClass="DespatchingParty" '
     C                             + 'partyId="" idType="86">'
     C                   EXSR      TILFIL
     C                   EVAL      XTXT801 = HENAS
     C                   Z-ADD     35            XN53              3 0
     C                   EXSR      KNVST
     C                   EVAL      SDATA = '<Name>' + %trim(XTXT802) + '</Name>'
     C                   EXSR      TILFIL
     C                   EVAL      SDATA ='<Address subClass="PhysicalAddress">'
     C                   EXSR      TILFIL
     C     ' '           SCAN      HEADS3        XPOS              3 0
     C                   IF        XPOS <= 1
     C                   Z-ADD     1             XPOS
     C                   END
     C                   EVAL      SDATA = '<PostalCode>'
     C                             + %subst(HEADS3:1:XPOS-1)
     C                             + '</PostalCode>'
     C                   EXSR      TILFIL
     C                   IF        XPOS = 1
     C                   Z-ADD     0             XPOS
     C                   END
     C                   EVAL      XTXT801 = %subst(HEADS3:XPOS+1:30-XPOS)
     C                   Z-ADD     35            XN53              3 0
     C                   EXSR      KNVST
     C                   EVAL      SDATA = '<City>' + %trim(XTXT802) + '</City>'
     C                   EXSR      TILFIL
     C                   IF        HELKS = *BLANKS
     C                   EVAL      HELKS = FILAND
     C                   END
     C                   EVAL      SDATA = '<CountryCode>'
     C                             + %trim(HELKS)
     C                             + '</CountryCode>'
     C                   EXSR      TILFIL
     C                   EVAL      XTXT801 = HEADS1
     C                   Z-ADD     35            XN53              3 0
     C                   EXSR      KNVST
     C                   EVAL      SDATA = '<Street>' + %trim(XTXT802)
     C                             + '</Street>'
     C                   EXSR      TILFIL
     C                   EVAL      SDATA ='</Address>'
     C                   EXSR      TILFIL
     C                   EVAL      SDATA = '</Party>'
     C                   EXSR      TILFIL
      * Consignee
     C                   EVAL      KUNDNR = HEKNK
     C     CUNDL01K      CHAIN     CUNDL01                            33
     C                   EVAL      SDATA = '<Party subClass="Consignee" '
     C                             + 'partyId="' + %trim(%EDITC(HEKNK:'Z'))
     C                             + '" idType="86">'
     C                   EXSR      TILFIL
     C                   EVAL      XTXT801 = KNAVN
     C                   Z-ADD     35            XN53              3 0
     C                   EXSR      KNVST
     C                   EVAL      SDATA = '<Name>' + %trim(XTXT802) + '</Name>'
     C                   EXSR      TILFIL
     C                   EVAL      SDATA ='<Address subClass="PhysicalAddress">'
     C                   EXSR      TILFIL
     C                   MOVE      POSTNR        XPOSTNR           4
     C                   EVAL      SDATA = '<PostalCode>'
     C                             + %trim(XPOSTNR)
     C                             + '</PostalCode>'
     C                   EXSR      TILFIL
     C                   EVAL      XTXT801 = ADR3
     C                   Z-ADD     35            XN53              3 0
     C                   EXSR      KNVST
     C                   EVAL      SDATA = '<City>' + %trim(XTXT802) + '</City>'
     C                   EXSR      TILFIL
     C                   IF        SYLAND = *BLANKS
     C                   EVAL      SYLAND = FILAND
     C                   END
     C                   EVAL      SDATA = '<CountryCode>'
     C                             + %trim(SYLAND)
     C                             + '</CountryCode>'
     C                   EXSR      TILFIL
     C                   EVAL      XTXT801 = ADR1
     C                   Z-ADD     35            XN53              3 0
     C                   EXSR      KNVST
     C                   EVAL      SDATA = '<Street>' + %trim(XTXT802)
     C                             + '</Street>'
     C                   EXSR      TILFIL
     C                   EVAL      SDATA ='</Address>'
     C                   EXSR      TILFIL
     C                   EVAL      SDATA = '</Party>'
     C                   EXSR      TILFIL
      * DeliveryParty (leveringsadresse)
     C                   EVAL      SDATA = '<Party subClass="DeliveryParty" '
     C                             + 'partyId="" idType="86">'
     C                   EXSR      TILFIL
     C                   EVAL      XTXT801 = HENAK
     C                   Z-ADD     35            XN53              3 0
     C                   EXSR      KNVST
     C                   EVAL      SDATA = '<Name>' + %trim(XTXT802) + '</Name>'
     C                   EXSR      TILFIL
     C                   EVAL      SDATA ='<Address subClass="PhysicalAddress">'
     C                   EXSR      TILFIL
     C     ' '           SCAN      HEADK3        XPOS              3 0
     C                   IF        XPOS <= 1
     C                   Z-ADD     1             XPOS
     C                   END
     C                   EVAL      SDATA = '<PostalCode>'
     C                             + %subst(HEADK3:1:XPOS-1)
     C                             + '</PostalCode>'
     C                   EXSR      TILFIL
     C                   IF        XPOS = 1
     C                   Z-ADD     0             XPOS
     C                   END
     C                   EVAL      XTXT801 = %subst(HEADK3:XPOS+1:30-XPOS)
     C                   Z-ADD     35            XN53              3 0
     C                   EXSR      KNVST
     C                   EVAL      SDATA = '<City>' + %trim(XTXT802) + '</City>'
     C                   EXSR      TILFIL
     C                   IF        HELKK = *BLANKS
     C                   EVAL      HELKK = FILAND
     C                   END
     C                   EVAL      SDATA = '<CountryCode>'
     C                             + %trim(HELKK)
     C                             + '</CountryCode>'
     C                   EXSR      TILFIL
     C                   EVAL      XTXT801 = HEADK1
     C                   Z-ADD     35            XN53              3 0
     C                   EXSR      KNVST
     C                   EVAL      SDATA = '<Street>' + %trim(XTXT802)
     C                             + '</Street>'
     C                   EXSR      TILFIL
     C                   EVAL      SDATA ='</Address>'
     C                   EXSR      TILFIL
     C                   EVAL      SDATA = '</Party>'
     C                   EXSR      TILFIL
      *
     C                   ENDSR
      *
      ***********************************************
     C     SRTRSLEG      BEGSR
      ***********************************************
      * TransportLegType 10
     C                   EVAL      SDATA = '<TransportLeg>'
     C                   EXSR      TILFIL
     C                   EVAL      SDATA = '<TransportLegType>10'
     C                             + '</TransportLegType>'
     C                   EXSR      TILFIL
     C                   EVAL      SDATA = '<Location subClass="StartLocation">'
     C                   EXSR      TILFIL
     C                   EVAL      SDATA = '<LocationName>'
     C                             + %trim(HESDFF)
     C                             + '</LocationName>'
     C                   EXSR      TILFIL
     C                   EVAL      SDATA = '</Location>'
     C                   EXSR      TILFIL
     C                   EVAL      SDATA = '<CountryCode>NO</CountryCode>'
     C                   EXSR      TILFIL
     C                   EVAL      SDATA = '</TransportLeg>'
     C                   EXSR      TILFIL
      * TransportLegType 20
     C                   EVAL      SDATA = '<TransportLeg>'
     C                   EXSR      TILFIL
     C                   EVAL      SDATA = '<TransportLegType>20'
     C                             + '</TransportLegType>'
     C                   EXSR      TILFIL
     C                   EVAL      SDATA = '<Location subClass="StartLocation">'
     C                   EXSR      TILFIL
     C                   EVAL      SDATA = '<LocationName>'
     C                             + %trim(HESDF)
     C                             + '</LocationName>'
     C                   EXSR      TILFIL
     C                   EVAL      SDATA = '</Location>'
     C                   EXSR      TILFIL
     C                   EVAL      SDATA = '<CountryCode>NO</CountryCode>'
     C                   EXSR      TILFIL
     C                   EVAL      SDATA = '</TransportLeg>'
     C                   EXSR      TILFIL
      * TransportLegType 30
     C                   EVAL      SDATA = '<TransportLeg>'
     C                   EXSR      TILFIL
     C                   EVAL      SDATA = '<TransportLegType>30'
     C                             + '</TransportLegType>'
     C                   EXSR      TILFIL
     C                   EVAL      SDATA = '<Location subClass="EndLocation">'
     C                   EXSR      TILFIL
     C                   EVAL      SDATA = '<LocationName>'
     C                             + %trim(HESDVT)
     C                             + '</LocationName>'
     C                   EXSR      TILFIL
     C                   EVAL      SDATA = '</Location>'
     C                   EXSR      TILFIL
     C                   EVAL      SDATA = '<CountryCode>NO</CountryCode>'
     C                   EXSR      TILFIL
     C                   EVAL      SDATA = '</TransportLeg>'
     C                   EXSR      TILFIL
      *
     C                   ENDSR
      *
      ***********************************************
     C     SRTERMS       BEGSR
      ***********************************************
      *
     C                   EVAL      SDATA = '<TermsOfDelivery>'
     C                   EXSR      TILFIL
     C                   EVAL      SDATA = '<TODConditionCode>'
     C                             + %trim(HEFR)
     C                             + '</TODConditionCode>'
     C                   EXSR      TILFIL
     C     KODTFRK       CHAIN     KODTFR                             33
     C                   EVAL      SDATA = '<TODConditionCodeList>'
     C                             + %trim(KFRNTX)
     C                             + '</TODConditionCodeList>'
     C                   EXSR      TILFIL
     C                   EVAL      SDATA = '</TermsOfDelivery>'
     C                   EXSR      TILFIL
      *
     C                   ENDSR
      *
      ***********************************************
     C     SRSERVIC      BEGSR
      ***********************************************
      *
     C                   EVAL      SDATA = '<Service>'
     C                   EXSR      TILFIL
     C                   EVAL      SDATA = '<BasicServiceCode>STD'
     C                             + '</BasicServiceCode>'
     C                   EXSR      TILFIL
     C                   EVAL      SDATA = '</Service>'
     C                   EXSR      TILFIL
      *
     C                   ENDSR
      *
      ***********************************************
     C     SRDATTIM      BEGSR
      ***********************************************
      * TransportOrderDate
     C                   EVAL      SDATA = '<DateAndTimes '
     C                             + ' subClass="TransportOrderDate">'
     C                   EXSR      TILFIL
     C                   EVAL      SDATA = '<DateTime>'
     C                             + HEDTOPÅ + '-' + HEDTOPM + '-' + HEDTOPD
     C                             + 'T00:00:00'
     C                             + '</DateTime>'
     C                   EXSR      TILFIL
     C                   EVAL      SDATA = '</DateAndTimes>'
     C                   EXSR      TILFIL
      * DepartureDateEstimated
     C                   IF        TXETDD <> 0
     C                   EVAL      SDATA = '<DateAndTimes '
     C                             + ' subClass="DepartureDateEstimated">'
     C                   EXSR      TILFIL
     C                   EVAL      SDATA = '<DateTime>'
     C                             + TXETDDÅ + '-' + TXETDDM + '-' + TXETDDD
     C                             + 'T00:00:00'
     C                             + '</DateTime>'
     C                   EXSR      TILFIL
     C                   EVAL      SDATA = '</DateAndTimes>'
     C                   EXSR      TILFIL
     C                   END
      * ArrivalDateEstimated
     C                   IF        TXETDD <> 0
     C                   EVAL      SDATA = '<DateAndTimes '
     C                             + ' subClass="ArrivalDateEstimated">'
     C                   EXSR      TILFIL
     C                   EVAL      SDATA = '<DateTime>'
     C                             + TXETADÅ + '-' + TXETADM + '-' + TXETADD
     C                             + 'T00:00:00'
     C                             + '</DateTime>'
     C                   EXSR      TILFIL
     C                   EVAL      SDATA = '</DateAndTimes>'
     C                   EXSR      TILFIL
     C                   END
      * ArrivalDateActual
     C                   IF        TXETDD <> 0
     C                   EVAL      SDATA = '<DateAndTimes '
     C                             + ' subClass="ArrivalDateActual">'
     C                   EXSR      TILFIL
     C                   EVAL      SDATA = '<DateTime>'
     C                             + TXATADÅ + '-' + TXATADM + '-' + TXATADD
     C                             + 'T00:00:00'
     C                             + '</DateTime>'
     C                   EXSR      TILFIL
     C                   EVAL      SDATA = '</DateAndTimes>'
     C                   EXSR      TILFIL
     C                   END
      *
     C                   ENDSR
      *
      ***********************************************
     C     SRSLUTT       BEGSR
      ***********************************************
      *
     C                   EVAL      SDATA = '</Transport>'
     C                   EXSR      TILFIL
      *
      * Oppdater EDIS
     C                   WRITE     EDISR
      * Oppdater EDIM
     C                   EXSR      OPDEDIM
      *
     C                   ENDSR
      *
      ***********************************************
     C     SRCLOSE       BEGSR
      ***********************************************
      *
     C                   ENDSR
      *
      ***********************************************
     C     SRSEND        BEGSR
      ***********************************************
     C                   CLOSE     XMLTILP1k
     C                   EVAL      XFILNVN = 'XXXX'
     C                   CAT       XMBRC:0       XFILNVN          16
     C                   CALL      'SYXML21S'
     C                   PARM                    XFILNVN
      *
     C                   ENDSR
      *
      ***********************************************
     C     OPDEDIM       BEGSR
      ***********************************************
     C     1             CHAIN     EDIC                               33
     C                   ADD       1             CMN
     C                   UPDATE    EDICR
      *
     C                   EVAL      M0004 = S0004
     C                   EVAL      M0010 = S0010
     C                   MOVEL     CMN           M0062
     C                   EVAL      M0065 = 'XMLORD'
     C                   EVAL      M0068 = FSSOK
     C                   EVAL      M1001 = 'XML'
     C                   EVAL      M1004 = SYSPEDREF
     C                   EVAL      M1225 = 'ORD'
     C                   EVAL      MSN = SSN
     C                   EVAL      MMN = CMN
     C                   EVAL      MSR = 'S'
     C                   EVAL      MST = 'C'
     C                   MOVE      WDT           MDT
     C                   MOVE      WTM           MTM
     C                   MOVE      UAVD          MAVD
     C                   MOVE      UOPD          MTDN
     C                   WRITE     EDIMR
      *
     C                   ENDSR
      *
      ***********************************************
     C     TILFIL        BEGSR
      ***********************************************
     C                   WRITE     EDISER
      *
     C                   MOVE      SDATA         XTPDTA
     C                   WRITE     XMLTILPR
      *
     C                   ENDSR
      *
      ***********************************************
     C     KNVST         BEGSR
      ***********************************************
     C     XN53          SUB       4             XN54              3 0
     C     XN53          SUB       5             XN55              3 0
     C     XN53          SUB       6             XN56              3 0
     C                   Z-ADD     0             XN52              3 0
     C                   MOVE      *BLANKS       XTXT802
     C     1             DO        80            XN51              3 0
     C                   IF        (((((XT801(XN51) = '&') OR
     C                                 (XT801(XN51) = '"')) OR
     C                                 (XT801(XN51) = XAPS)) OR
     C                                 (XT801(XN51) = '<')) OR
     C                                 (XT801(XN51) = '>'))
     C                   IF        XN52 < XN53
     C                   ADD       1             XN52
     C                   SELECT
     C                   WHEN      XT801(XN51) = '&'
     C                   IF        XN51 < XN55
     C                   MOVEA     '&amp;'       XT802(XN52)
     C                   ADD       4             XN52
     C                   ELSE
     C                   SUB       1             XN52
     C                   END
     C                   WHEN      XT801(XN51) = '"'
     C                   IF        XN51 < 75
     C                   MOVEA     '&quot;'      XT802(XN52)
     C                   ADD       5             XN52
     C                   ELSE
     C                   SUB       1             XN52
     C                   END
     C                   WHEN      XT801(XN51) = XAPS
     C                   IF        XN51 < XN56
     C                   MOVEA     '&apos;'      XT802(XN52)
     C                   ADD       5             XN52
     C                   ELSE
     C                   SUB       1             XN52
     C                   END
     C                   WHEN      XT801(XN51) = '>'
     C                   IF        XN51 < XN54
     C                   MOVEA     '&gt;'        XT802(XN52)
     C                   ADD       3             XN52
     C                   ELSE
     C                   SUB       1             XN52
     C                   END
     C                   WHEN      XT801(XN51) = '<'
     C                   IF        XN51 < XN54
     C                   MOVEA     '&lt;'        XT802(XN52)
     C                   ADD       3             XN52
     C                   ELSE
     C                   SUB       1             XN52
     C                   END
     C                   ENDSL
     C                   END
     C                   ELSE
     C                   IF        XN52 < XN53
     C                   ADD       1             XN52
     C                   MOVE      XT801(XN51)   XT802(XN52)
     C                   ELSE
     C                   LEAVE
     C                   END
     C                   END
     C                   ENDDO
      *
     C                   ENDSR
      *
      ***********************************************
     C     INIT          BEGSR
      ***********************************************
     C     HEADFK        KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C     FRISOKK       KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C                   KFLD                    FSKODE
     C     CUNDL01K      KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    KUNDNR
     C     KODTFRK       KLIST
     C                   KFLD                    KFRUNI
     C                   KFLD                    HEFR
      *
     C     *ENTRY        PLIST
     C                   PARM                    UAVD              4
     C                   PARM                    UOPD              7
      *
     C                   MOVE      UAVD          HEAVD
     C                   MOVE      UOPD          HEOPD
     C                   BITON     '123457'      XAPS              1
     C                   BITOFF    '06'          XAPS
     C                   MOVE      'ORD'         FSKODE
     C                   MOVE      'FR'          KFRUNI
      *
     C     *LOVAL        SETLL     FIRM
     C                   READ      FIRM                                   33
     C     HEADFK        CHAIN     HEADF                              33
     C     HEADFK        CHAIN     TRACKT                             33
     C                   IF        *IN33
     C                   EVAL      TXETDD = 0
     C                   EVAL      TXETAD = 0
     C                   EVAL      TXATAD = 0
     C                   END
     C     FRISOKK       CHAIN     FRISOK                             33
      *
     C                   ENDSR
      *
