      *__________________________________
      * SYSPED / Tolldekl. TRANSITT
      *""""""""""""""""""""""""""""""""""
     A                                      REF(SR)
     A          R TRIHR
     A            TIST      R               REFFLD(ST)
      * THST = ' ' --> ÅPEN TRANSITTERING
      *        'E' --> HOLD PÅ Å BLI ENDRET
      *        'Q' --> I UTGÅENDE POSTKASSE
      *        '+' --> MERKET FOR Å SENDE UT
      *        'A' --> HOLD PÅ Å SENDE UT
      *        'B' --> MELDING ER SENDT TIL IBM
      *        'C' --> IE07 SENDT (ANKOMST MELDING)
      *        'U' --> IE43 MOTTATT (LOSSETILLATELSE)
      *        'V' --> IE43 MOTTATT (LOSSETILLATELSE) PAPIRLASTLISTE
      *        'F' --> IE44 SENDT (LOSSEMERKNAD)
      *        'P' --> IE25 MOTTATT (TRANSITTERING ER SLUTTET)
      *        'M' --> FEILMELDING FRA TOLLVESENET (IE08 ELLER CONTRL)
      *        'N' --> FEILMELDING FRA TOLLVESENET (IE58 ELLER CONTRL)
      *        'K' --> VENT PÅ AT LOSSETILLATELSE BLIR UTSKREVET
      *        'Å' --> MERKET FEILMELIDNG FØR DET SKRIVES UT
     A            TIAVD          4S 0       COLHDG('AVDELING')
     A            TITDN     R               REFFLD(TDN)
     A            TISG      R               REFFLD(SG)
     A            TIDT      R               REFFLD(DT)
     A                                      COLHDG('REGISTRERINGSDATO')
     A            TIDTF     R               REFFLD(DT)
     A                                      COLHDG('FRIGIVELSESDATO')
     A            TIENKL         1A         COLHDG('FORENKLET PROSEDYRE')
     A            TITRNR        18A         COLHDG('MRN-NR')
     A            TIGN          35A         COLHDG('GODSNR')
     A            TIGNSK         2A         COLHDG('SPRÅKKODE GODSNR')
     A            TIALSK        17A         COLHDG('KODE AVT. LAG.STED')
     A            TIALS         35A         COLHDG('AVTALT LAGRINGSSTED')
     A            TIALSS         2A         COLHDG('SPRÅKKODE AVT.LAG.ST')
     A            TIGLSK        17A         COLHDG('KODE GODKJ.LAG.STED')
     A            TIACTS        17A         COLHDG('KODE AVT.CONTR.STED')
     A            TISKB          2A         COLHDG('SPRÅKKODE BEST.STED')
     A            TITSB          8A         COLHDG('FREMLEG. TOLLSTED')
     A            TITARF        16          COLHDG('TARIFFØRSNAVN')
     A            TIWS      R               REFFLD(WS)
      * NÄRINGSLIV
     A            TIKN      R               REFFLD(KN)
     A                                      COLHDG('Kundenr')
     A            TINA      R               REFFLD(NA)
     A                                      COLHDG('Navn')
     A            TIAD1     R               REFFLD(AD)
     A                                      COLHDG('Adresse-1')
     A            TIPN           9A         COLHDG('POSTNR')
     A            TIPS          24A         COLHDG('POSTSTED')
     A            TILK           2A         COLHDG('LANDKODE')
     A            TISK           2A         COLHDG('SPRÅKKODE')
     A            TITIN         17A         COLHDG('TIN-KODE')
      * TVINN - VARIABLER
     A            TIMF      R               REFFLD(MF)
     A            TIMP      R               REFFLD(MP)
     A            TIKDV     R               REFFLD(KDV)
     A            TI0035    R               REFFLD(N0035)
      *
      *================================================================
      * FRA IE43 LOSSETILLATELSE
      *================================================================
     A            TIDK           5A         COLHDG('DEKLARASJONSKODE')
      * HOVEDANSVARLIG
     A            TIKNA     R               REFFLD(KN)
     A                                      COLHDG('HOVEDANSVARLIG')
     A            TINAA     R               REFFLD(NA)
     A                                      COLHDG('Navn ANSVARLIG')
     A            TIADA1    R               REFFLD(AD)
     A                                      COLHDG('Adresse-1 ANSVARLIG')
     A            TIPNA          9A         COLHDG('POSTNR ANSVARLIG')
     A            TIPSA         24A         COLHDG('POSTSTED ANSVARLIG')
     A            TILKA          2A         COLHDG('LANDKODE ANSVARLIG')
     A            TISKA          2A         COLHDG('SPRÅKKODE ANSVARLIG')
     A            TITINA        17A         COLHDG('TIN-KODE ANSVARLIG')
      * SELGER
     A            TIKNS     R               REFFLD(KN)
     A                                      COLHDG('Kundenr Selger')
     A            TINAS     R               REFFLD(NA)
     A                                      COLHDG('Navn Selger')
     A            TIADS1    R               REFFLD(AD)
     A                                      COLHDG('Adresse-1 Selger')
     A            TIPNS          9A         COLHDG('POSTNR SELGER')
     A            TIPSS         24A         COLHDG('POSTSTED SELGER')
     A            TILKS          2A         COLHDG('LANDKODE SELGER')
     A            TISKS          2A         COLHDG('SPRÅKKODE SELGER')
     A            TITINS        17A         COLHDG('TIN-KODE SELGER')
      * KJØPER
     A            TIKNK     R               REFFLD(KN)
     A                                      COLHDG('Kundenr Kjøper')
     A            TINAK     R               REFFLD(NA)
     A                                      COLHDG('Navn Kjøper')
     A            TIADK1    R               REFFLD(AD)
     A                                      COLHDG('Adresse-1 Kjøper')
     A            TIPNK          9A         COLHDG('POSTNR KJØPER')
     A            TIPSK         24A         COLHDG('POSTSTED KJØPER')
     A            TILKK          2A         COLHDG('LANDKODE KJØPER')
     A            TISKK          2A         COLHDG('SPRÅKKODE KJØPER')
     A            TITINK        17A         COLHDG('TIN-KODE KJØPER')
      * BESTEMMELSES LAND  RUBR. 17A
     A            TIBLK     R               REFFLD(LKA)
     A                                      COLHDG('LANDK TIL B.LAND')
      * AVSENDELAND  RUBR. 15A
     A            TIALK     R               REFFLD(LKA)
     A                                      COLHDG('LANDK TIL A.LAND')
      * TRANSPORTMIDDEL VED AVGANG  RUBR. 18
     A            TITAID        27A         COLHDG('IDENTITET')
     A            TITASK         2A         COLHDG('SPRÅK')
     A            TITALK    R               REFFLD(LKA)
     A                                      COLHDG('Landk Tr.middel')
      * CONTAINER  RUBR. 19
     A            TIKDC     R               REFFLD(KDC)
      *
     A            TITRDT         8S 0       COLHDG('GODKJENNINGSDATO')
     A            TILSTL         4S 0       COLHDG('ANTALL LASTELISTE')
     A            TIVPOS         5S 0       COLHDG('VAREPOST')
     A            TINTK     R               REFFLD(NTK)
     A            TIVKB         11S 3       COLHDG('Bruttovekt')
     A            TICATS         8A         COLHDG('AVGANGS TOLLSTED')
      * CTL. KOONTROLL
     A            TICTL          1A         COLHDG('FORTSETTE LOSSING')
      * FORSEGLING
     A            TIDANT         4S 0       COLHDG('ANTALL FORSEGLING')
     A            TIDFKD        20A         COLHDG('FORSEGLING-ID KODE')
     A            TIDFSK         2A         COLHDG('FORSEGLING-ID SPRÅK')
      *================================================================
      *
      *================================================================
      * FOR IE44 LOSSEMERKNADER
      *================================================================
     A            NIVPOS         5S 0       COLHDG('VAREPOST')
     A            NINTK     R               REFFLD(NTK)
     A            NIVKB         11S 3       COLHDG('Bruttovekt')
      * FORSEGLING
     A            NIDFST         1A         COLHDG('FORSEGLING STATUS')
     A            NIDANT         4S 0       COLHDG('ANTALL FORSEGLING')
     A            NIDFKD        20A         COLHDG('FORSEGLING-ID KODE')
     A            NIDFSK         2A         COLHDG('FORSEGLING-ID SPRÅK')
      * MERKNAD
     A            NIMN1         70A         COLHDG('LOSSEMERKNADER 1')
     A            NIMN2         70A         COLHDG('LOSSEMERKNADER 2')
     A            NIMNSK         2A         COLHDG('MERKNAD SPRÅK')
      * KONFORM OG LOSSE
     A            NIKONF         1S 0       COLHDG('KONFORM')
     A            NIFULF         1S 0       COLHDG('LOSSE FULLFØRT')
     A            NIDTL          8S 0       COLHDG('LOSSE DATO')
      * KONTROLL RESULTAT PÅ HODENIVÅ
     A            NICTB         70A         COLHDG('BESKRIVELSE')
     A            NICTB2        70A         COLHDG('BESKRIVELSE')
     A            NICTSK         2A         COLHDG('SPRÅKKODE')
     A            NICTP         35A         COLHDG('PEKER TIL ATTR.')
     A            NICTNV        15A         COLHDG('NY VERDI')
      *================================================================
      *
     A          K TIAVD
     A          K TITDN
