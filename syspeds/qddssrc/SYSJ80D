     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      ALTHELP
     A                                      HELP
     A          R BILDE
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CA03(03 'Avslutt')
     A                                      CF04(04 'SØKING')
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/SYSJ80R'
     A                                      COLOR(BLU)
     A                                  1 61DATE
     A                                      EDTCDE(Y)
     A                                  1 70TIME
     A                                  1 20MSGCON(40 YBC0729 SYMSGF)
     A                                      DSPATR(HI)
     A                                  3  2MSGCON(24 DIV3439 SYMSGF)
     A            WSAVD          4Y 0B  3 28DSPATR(UL)
     A  90                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                  3 45MSGCON(24 DIV0457 SYMSGF)
     A                                  5  2MSGCON(30 DIV3440 SYMSGF)
     A                                  7  2MSGCON(10 DIV3441 SYMSGF)
     A            WSDT           6Y 0B  7 28DSPATR(UL)
     A  89                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                  8  2MSGCON(24 DIV3442 SYMSGF)
     A            WSTYPE         2Y 0B  8 28DSPATR(UL)
     A  88                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                 10  2MSGCON(24 DIV3478 SYMSGF)
     A            WSBLNR        16   B 10 28DSPATR(UL)
     A  87                                  DSPATR(RI)
     A                                 11  2MSGCON(24 DIV3479 SYMSGF)
     A            WSREIS        14   B 11 28DSPATR(UL)
     A  86                                  DSPATR(RI)
     A                                 12  2MSGCON(24 DIV3480 SYMSGF)
     A            WSOB           5   B 12 28DSPATR(UL)
     A  85                                  DSPATR(RI)
     A                                 13  2MSGCON(24 DIV3481 SYMSGF)
     A            WSLB           5   B 13 28DSPATR(UL)
     A  84                                  DSPATR(RI)
     A                                 14  2MSGCON(24 DIV3482 SYMSGF)
     A            WSKNR          8Y 0B 14 28DSPATR(UL)
     A  83                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                 15  2MSGCON(24 DIV3483 SYMSGF)
     A                                 15 28MSGCON(4 DIV3444 SYMSGF)
     A            WSÅR           2Y 0B 15 33DSPATR(UL)
     A                                      EDTCDE(4)
     A                                 15 37MSGCON(4 DIV3445 SYMSGF)
     A            WSUKE          2Y 0B 15 42DSPATR(UL)
     A  82                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                 15 48MSGCON(10 DIV3446 SYMSGF)
     A            WSUKET         2Y 0B 15 59DSPATR(UL)
     A  81                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                 16  2MSGCON(24 DIV3484 SYMSGF)
     A            WSTR           4   B 16 28DSPATR(UL)
     A  80                                  DSPATR(RI)
     A                                 17  2MSGCON(24 DIV3485 SYMSGF)
     A            WSOPD          7Y 0B 17 28DSPATR(UL)
     A  79                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                 18  2MSGCON(24 YBC0702 SYMSGF)
     A            WSSG           3A  B 18 28DSPATR(UL)
     A  78                                  DSPATR(RI)
     A                                 19  2MSGCON(24 YBC0720 SYMSGF)
     A            WSMN          17A  B 19 28DSPATR(UL)
     A                                 23  2MSGCON(18 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(18 DIV0032 SYMSGF)
     A                                      COLOR(BLU)
     A N30                             24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R SUBFIL                    SFL
     A            WSNR           2Y 0B  7  2EDTCDE(4)
     A            HEUR           1A  H
     A            HEST           1A  H
     A            BLST           1A  H
     A            BLUKE          2  0O  7  5EDTCDE(1)
     A  46                                  COLOR(WHT)
     A            XDTA           6  0O  7  8EDTCDE(4)
     A            BLLB           5   O  7 15
     A  44                                  COLOR(WHT)
     A            BLLOH          5   O  7 21
     A            BLOB           5   O  7 27
     A  43                                  COLOR(WHT)
     A            BLBLNR        16   O  7 33
     A  41                                  COLOR(WHT)
     A            BLREIS        14   O  7 50
     A  42                                  COLOR(WHT)
     A            BLAVD          4  0O  7 65EDTCDE(4)
     A            BLOPD          7  0O  7 70EDTCDE(4)
     A  48                                  COLOR(WHT)
     A            BLSG           3   O  7 78
     A  49                                  COLOR(WHT)
     A            HESG           3   H
     A            BLKNR          8  0H
     A            XNAR          15   O  8  5DSPATR(UL)
     A  45                                  COLOR(WHT)
     A            BLTR           4   O  8 21DSPATR(UL)
     A  47                                  COLOR(WHT)
     A            BLPRO          8  0O  8 26DSPATR(UL)
     A                                      EDTCDE(4)
     A            HENT           7  0O  8 35DSPATR(UL)
     A                                      EDTCDE(4)
     A            HEVKT          9  0O  8 43DSPATR(UL)
     A                                      EDTCDE(4)
     A            BLMN          17   O  8 53DSPATR(UL)
     A  50                                  COLOR(WHT)
     A            HEOT           2   O  8 71DSPATR(UL)
     A            HEFR           3   O  8 74DSPATR(UL)
     A            BLUR           1A  O  8 78DSPATR(UL)
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      CA03(03 'AVSLUTT')
     A                                      CF04(04 'SØK KODE')
     A                                      CA05(05 'Frisk')
     A                                      CA06(06 'LAGE EN NY')
     A                                      CA08(08 'SØK')
     A                                      CA12(12 'FORRIGE')
     A                                      CA13(13 'TIL OPPDRAG')
     A                                      CA15(15 'TIL BOOKING')
     A                                      CA20(20 'TIL PLUKKING')
     A                                      CA23(23 'MER OPSJONER')
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A                                      SFLSIZ(0008)
     A                                      SFLPAG(0007)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            ZRECNO         4S 0O  1 77SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYSPED/SYSJ80R'
     A                                      COLOR(BLU)
     A                                  1 61DATE
     A                                      EDTCDE(Y)
     A                                  1 70TIME
     A                                  1 20MSGCON(40 YBC0729 SYMSGF)
     A                                      COLOR(WHT)
     A                                  2  2MSGCON(010 YBC0065 *LIBL/SYMSGF)
     A            WSAVDA         4Y 0B  2 13EDTCDE(4)
     A  96                                  DSPATR(HI)
     A            WSSGA          3A  B  2 19DSPATR(UL)
     A  95                                  DSPATR(HI)
     A  41                              2 24MSGCON(24 DIV3478 SYMSGF)
     A  41        WSBLNR        16   O  2 49DSPATR(UL)
     A                                      COLOR(WHT)
     A  42                              2 24MSGCON(24 DIV3479 SYMSGF)
     A  42        WSREIS        14   O  2 49DSPATR(UL)
     A                                      COLOR(WHT)
     A  43                              2 24MSGCON(24 DIV3480 SYMSGF)
     A  44                              2 24MSGCON(24 DIV3481 SYMSGF)
     A  43
     AO 44        XCBNAB        30   O  2 49DSPATR(UL)
     A                                      COLOR(WHT)
     A  45                              2 24MSGCON(24 DIV3482 SYMSGF)
     A  45        XKNAVN        30   O  2 49DSPATR(UL)
     A                                      COLOR(WHT)
     A  46                              2 24MSGCON(24 DIV3483 SYMSGF)
     A  46                              2 49MSGCON(4 DIV3444 SYMSGF)
     A  46        WSÅR           2Y 0O  2 54DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(1)
     A  46                              2 57MSGCON(4 DIV3445 SYMSGF)
     A  46        WSUKE          2Y 0O  2 62DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(1)
     A  46                              2 65MSGCON(10 DIV3446 SYMSGF)
     A  46        WSUKET         2Y 0O  2 76DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(1)
     A  47                              2 24MSGCON(24 DIV3484 SYMSGF)
     A  47        WSTR           4   O  2 49DSPATR(UL)
     A                                      COLOR(WHT)
     A  48                              2 24MSGCON(24 DIV3485 SYMSGF)
     A  48        WSOPD          7Y 0O  2 49DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A  49                              2 24MSGCON(24 YBC0702 SYMSGF)
     A  49        WSSG           3A  O  2 49DSPATR(UL)
     A  50                              2 24MSGCON(24 YBC0720 SYMSGF)
     A  50        WSMN          17A  O  2 49DSPATR(UL)
     A                                      COLOR(WHT)
     A            XOPTXT        78A  O  3  2MSGID(&XOPNR SYMSGF)
     A                                      COLOR(BLU)
     A            XOPNR          7A  P
     A                                  4  2MSGCON(78 YBC0717 SYMSGF)
     A                                      COLOR(WHT)
     A                                  5  2MSGCON(78 YBC0718 SYMSGF)
     A                                      COLOR(WHT)
     A            W2NR           2Y 0B  6  2EDTCDE(4)
     A                                      DSPATR(UL)
     A            W2UKE          2  0B  6  5EDTCDE(4)
     A            W2DT           6  0B  6  8EDTCDE(4)
     A            W2LB           5   B  6 15
     A            W2OB           5   B  6 27
     A            W2BLNR        16   B  6 33
     A            W2REIS        14   B  6 50
     A            W2AVD          4  0B  6 65EDTCDE(4)
     A            W2OPD          7  0B  6 70EDTCDE(4)
     A            W2SG           3   B  6 78
      *
     A          R BUNN
     A                                      SLNO(22)
     A                                      CLRL(03)
     A                                      OVERLAY
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2MSGCON(78 YBC0723 SYMSGF)
     A                                      COLOR(BLU)
     A                                  2  2MSGCON(78 YBC0721 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  3  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                              3 40'(C) COPYRIGHT  SYSTEMA A/S  1999  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R NYVARE
     A                                      CA03(03 'AVSLUTT')
     A                                      CF04(04 'SØK KODE')
     A                                      CA06(06 'LAGE EN NY')
     A                                      CA08(08 'SØK')
     A                                      CA12(12 'FORRIGE')
     A                                      CA13(13 'TIL OPPDRAG')
     A                                      CA15(15 'TIL BOOKING')
     A                                      CA20(20 'TIL PLUKKING')
     A                                      CA23(23 'MER OPSJONER')
     A                                  1  2'SYSPED/SYSJ80R'
     A                                      COLOR(BLU)
     A                                  1 61DATE
     A                                      EDTCDE(Y)
     A                                  1 70TIME
     A                                  1 20MSGCON(40 YBC0729 SYMSGF)
     A                                      COLOR(WHT)
     A                                  2  2MSGCON(010 YBC0065 *LIBL/SYMSGF)
     A            WSAVDA         4Y 0B  2 13EDTCDE(4)
     A  96                                  DSPATR(HI)
     A            WSSGA          3A  B  2 19DSPATR(UL)
     A  95                                  DSPATR(HI)
     A  41                              2 24MSGCON(24 DIV3478 SYMSGF)
     A  41        WSBLNR        16   O  2 49DSPATR(UL)
     A                                      COLOR(WHT)
     A  42                              2 24MSGCON(24 DIV3479 SYMSGF)
     A  42        WSREIS        14   O  2 49DSPATR(UL)
     A                                      COLOR(WHT)
     A  43                              2 24MSGCON(24 DIV3480 SYMSGF)
     A  44                              2 24MSGCON(24 DIV3481 SYMSGF)
     A  43
     AO 44        XCBNAB        30   O  2 49DSPATR(UL)
     A                                      COLOR(WHT)
     A  45                              2 24MSGCON(24 DIV3482 SYMSGF)
     A  45        XKNAVN        30   O  2 49DSPATR(UL)
     A                                      COLOR(WHT)
     A  46                              2 24MSGCON(24 DIV3483 SYMSGF)
     A  46                              2 49MSGCON(4 DIV3444 SYMSGF)
     A  46        WSÅR           2Y 0O  2 54DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(1)
     A  46                              2 57MSGCON(4 DIV3445 SYMSGF)
     A  46        WSUKE          2Y 0O  2 62DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(1)
     A  46                              2 65MSGCON(10 DIV3446 SYMSGF)
     A  46        WSUKET         2Y 0O  2 76DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(1)
     A  47                              2 24MSGCON(24 DIV3484 SYMSGF)
     A  47        WSTR           4   O  2 49DSPATR(UL)
     A                                      COLOR(WHT)
     A  48                              2 24MSGCON(24 DIV3485 SYMSGF)
     A  48        WSOPD          7Y 0O  2 49DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A  49                              2 24MSGCON(24 YBC0702 SYMSGF)
     A  49        WSSG           3A  O  2 49DSPATR(UL)
     A                                      COLOR(WHT)
     A  50                              2 24MSGCON(24 YBC0720 SYMSGF)
     A  50        WSMN          17A  O  2 49DSPATR(UL)
     A                                      COLOR(WHT)
     A            XOPTXT        78A  O  3  2MSGID(&XOPNR SYMSGF)
     A                                      COLOR(BLU)
     A            XOPNR          7A  P
     A                                  4  2MSGCON(78 YBC0717 SYMSGF)
     A                                      COLOR(WHT)
     A                                  5  2MSGCON(78 YBC0718 SYMSGF)
     A                                      COLOR(WHT)
     A            W2NR           2Y 0B  6  2EDTCDE(4)
     A                                      DSPATR(UL)
     A            W2UKE          2  0B  6  5EDTCDE(4)
     A            W2DT           6  0B  6  8EDTCDE(4)
     A            W2LB           5   B  6 15
     A            W2OB           5   B  6 27
     A            W2BLNR        16   B  6 33
     A            W2REIS        14   B  6 50
     A            W2AVD          4  0B  6 65EDTCDE(4)
     A            W2OPD          7  0B  6 70EDTCDE(4)
     A            W2SG           3   B  6 78
     A                                 22  2MSGCON(78 YBC0723 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23  2MSGCON(78 YBC0721 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                             24 40'(C) COPYRIGHT  SYSTEMA A/S  1999  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
