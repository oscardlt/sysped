********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10005 * 01 YBC PTF10 TOLLSTEDSKODE ER ENDRE FRA 2.0 TIL 4.0
12248 * 02 YBC PTF12 VEKT, KOLLI ER UTVIDET.
12282 * 03 YBC PTF12 4 SIFFER PROSEDYRE OG 2 SIFFER TRANSAKSJONSTYPE
********************************************************************
     A*__________________________________
     A* SYSPED / Tolldeklarasjoner EKSPORT
     A*""""""""""""""""""""""""""""""""""
     A                                      REF(SR)
     A          R SAEHR
     A            SEST      R               REFFLD(ST)
     A            SEAVD          4S 0       COLHDG('AVDELING')
     A            SETDN     R               REFFLD(TDN)
      * SEUR = 'G' --> SJØ-OPPD.  SEUR = ' ' --> LAND/FLY-OPPD.
     A            SEUR      R               REFFLD(UR)
     A                                      COLHDG('Oppr.kode')
     A            SEDTY     R               REFFLD(DTY)
     A            SEDP      R               REFFLD(DP)
N03  A            SEDP2          2S 0       COLHDG('Siste 2 siffer pros')
     A            SEKNS     R               REFFLD(KN)
     A                                      COLHDG('Kundenr Selger')
     A            SENAS     R               REFFLD(NA)
     A                                      COLHDG('Navn Selger')
     A            SEADS1    R               REFFLD(AD)
     A                                      COLHDG('Adresse-1 Selger')
     A            SEADS2    R               REFFLD(AD)
     A                                      COLHDG('Adresse-2 Selger')
     A            SEADS3    R               REFFLD(AD)
     A                                      COLHDG('Adresse-3 Selger')
S02  A*           SENTK     R               REFFLD(NTK)
S02  A*           SEVKB     R               REFFLD(VKT9)
S02  A*                                     COLHDG('Bruttovekt')
N02  A            SENTK         10S 0       COLHDG('KOLLI')
N02  A            SEVKB         11S 0       COLHDG('Vekt Brutto')
     A            SEKDC     R               REFFLD(KDC)
     A            SESG      R               REFFLD(SG)
     A            SEKNK     R               REFFLD(KN)
     A                                      COLHDG('Kundenr Kjøper')
     A            SERG          11A         COLHDG('FORETAKSNR.')
     A            SENAK     R               REFFLD(NA)
     A                                      COLHDG('Navn Kjøper')
     A            SEADK1    R               REFFLD(AD)
     A                                      COLHDG('Adresse-1 Kjøper')
     A            SEADK2    R               REFFLD(AD)
     A                                      COLHDG('Adresse-2 Kjøper')
     A            SEADK3    R               REFFLD(AD)
     A                                      COLHDG('Adresse-3 Kjøper')
     A            SELKA     R               REFFLD(LKA)
     A                                      COLHDG('Landk avs/utf-land')
     A            SETLF     R               REFFLD(TLF)
     A                                      COLHDG('Telefon Deklarant')
     A            SENAD     R               REFFLD(NA)
     A                                      COLHDG('Navn Deklarant')
     A            SELV      R               REFFLD(LV)
     A            SELVT     R               REFFLD(LVT)
     A            SETRID    R               REFFLD(TRID)
     A            SELKT     R               REFFLD(LKA)
     A                                      COLHDG('Landk Tr.middel')
     A            SEVAL1    R               REFFLD(VAL)
     A                                      COLHDG('Val.kode Faktsum')
     A            SEBEL1    R               REFFLD(BEL13)
     A                                      COLHDG('Beløp Faktsum')
     A            SEVKU     R               REFFLD(VKU)
S01  A*           SEKDH     R               REFFLD(KDH)
N01  A            SEKDH          4S 0       COLHDG('TOLLSTEDSKODE')
S03  A*           SETST     R               REFFLD(TST)
N03  A            SETST          2S 0       COLHDG('Transaksjonstype')
     A            SEBEL2    R               REFFLD(BEL11)
     A                                      COLHDG('Beløp A. kost')
     A            SEFTG2    R               REFFLD(KDFT)
     A                                      COLHDG('Fortegn +/- a.kost')
     A            SETRM     R               REFFLD(TRM)
     A            SEFIF     R               REFFLD(FIF)
     A            SEFID     R               REFFLD(DT)
     A                                      COLHDG('Finans oppl.fakdat')
     A            SEKTA     R               REFFLD(KTA)
     A            SEKTB     R               REFFLD(KTB)
     A            SEGN          15          COLHDG('GODSNR.')
     A            SEFT1     R               REFFLD(FT45)
     A                                      COLHDG('Fritekst-1')
     A            SEFT2     R               REFFLD(FT45)
     A                                      COLHDG('Fritekst-2')
     A            SEFT3     R               REFFLD(FT45)
     A                                      COLHDG('Fritekst-3')
     A            SEDST     R               REFFLD(DST)
     A            SEDTG     R               REFFLD(DTG)
     A            SETLL     R               REFFLD(TLL)
     A            SETLE     R               REFFLD(TLE)
     A            SESKI     R               REFFLD(KDSKI)
     A            SELS      R               REFFLD(LS)
     A            SEKDLS         2A         COLHDG('Lagringssted kode')
     A            SEDT      R               REFFLD(DT)
     A                                      COLHDG('Registreringsdato')
     A            SELV2     R               REFFLD(LV)
     A            SEKDDK    R               REFFLD(KDDK)
     A            SEGKD          1S 0       COLHDG('GARATIKODE')
     A            SEGFT1        30          COLHDG('GARATI FRITEKST-1')
     A            SEGFT2        30          COLHDG('GARATI FRITEKST-2')
     A            SEPOS          8          COLHDG('POSISJON')
     A            SEFTB         17          COLHDG('FRITEKST TIL B.LAND')
     A            SELKB     R               REFFLD(LKA)
     A                                      COLHDG('LANDK TIL B.LAND')
     A            SEFTM         17          COLHDG('F.T. MIDLETS ID LAND')
     A            SELKM     R               REFFLD(LKA)
     A                                      COLHDG('LK MIDLETS ID LAND')
     A            SEKDFT        17          COLHDG('FRITEKST TOLLSTED')
     A            SETARF        16          COLHDG('TARIFFØRSNAVN')
     A            SELKAT        17          COLHDG('F.T. AVS/UTF.LAND')
     A            SEVAL2    R               REFFLD(VAL)
     A                                      COLHDG('Valutakode A.KOST')
     A            SEBEL3    R               REFFLD(BEL9)
     A                                      COLHDG('Andre kost.omber')
     A            SEWS      R               REFFLD(WS)
      *
      * TVINN - VARIABLER
     A            SEMF      R               REFFLD(MF)
     A            SEMP      R               REFFLD(MP)
     A            SEDTØ     R               REFFLD(DT)
     A                                      COLHDG('Ønsket Beh.dato')
     A            SEBELD    R               REFFLD(BEL10S)
     A                                      COLHDG('Beløp Depositum')
     A            SEPKL     R               REFFLD(PK)
     A                                      COLHDG('Print ved linje ?')
     A            SEKDV     R               REFFLD(KDV)
      * SE0035 --> 1. OM DET ER EN TEST SENDING VED UTGÅENDE
      *            2. OM DET ER DOKUMENT/VAREKONTROLL VED INNGÅENDE
     A            SE0035    R               REFFLD(N0035)
     A            SE3039    R               REFFLD(N3039E)
     A            SEMI      R               REFFLD(MI)
     A            SEKTC          1A         COLHDG('IKKE TOLLKREDITT')
     A            SEDTPL    R               REFFLD(DT)
     A                                      COLHDG('PLUKKINGSDATO')
     A            SEUNIK         9  0       COLHDG('RECORDNR')
     A            SEREFF        17          COLHDG('REFERANSE')
     A          K SEREFF
