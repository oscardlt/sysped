     A*****************************************************************
     A*   UTLISTING AV FORSLAG PERIODEJUSTERING                       *
     A* ANTALL LINJE=48,  ANTALL TEGN PR. LINJE=132, OVERFLOW=44      *
     A*****************************************************************
     A**
     A          R HEAD0D                    SKIPB(2)
     A                                      HIGHLIGHT
     A                                     1MSGCON(132 JOV0754 SYMSGF)
     A                                      SPACEB(1)
     A                                   109DATE EDTCDE(Y)
     A                                   118TIME EDTCDE(Y)
     A  50                                 1MSGCON(132 JOV0755 SYMSGF)
     A                                      SPACEB(1)
     A N50                                 1MSGCON(132 JOV0757 SYMSGF)
     A                                      SPACEB(1)
     A            UPERF          4  0     16
     A            UPERT          4  0     34
     A            UAVD           4  0     64EDTCDE(Z)
     A                                   122PAGNBR EDTCDE(Z)
     A                                     1MSGCON(132 JOV0756 SYMSGF)
     A                                      SPACEB(2)
     A                                     1MSGCON(132 PRI9999 SYMSGF)
     A          R DET                       SPACEB(1)
     A            AVFPEÅ         2  0      3
     A            AVFPEM         2  0      6
     A            AVPERÅ         2  0     12
     A            AVPERM         2  0     15
     A            AVKONT         5  0     22
     A            AVKSTE         4  0     28EDTCDE(Z)
     A  61                                  HIGHLIGHT
     A            AVKBÆR         4  0     34EDTCDE(Z)
     A            AVKOIN         1A       42
     A            AVTEXT        30A       46
     A            AVOAVD         4  0     79EDTCDE(Z)
     A                                    83'/'
     A            AVOPD          7  0     84EDTCDE(Z)
     A            AVTUR          8  0     92EDTCDE(Z)
     A            AVBINR         7  0    105EDTCDE(Z)
     A            AVBELL        11S 2    112EDTCDE(J)
      * IB= FØRT I LAVERE PERIODE ENN OPPDRAGET..
      * UB= FØRT I HØYERE PERIODE ENN OPPDRAGET..
     A  51                               127'IB'
     A N51                               130'UB'
     A          R DETT                      SPACEB(1)
     A  50                                  HIGHLIGHT
     A            GMFPEÅ         2  0      3
     A            GMFPEM         2  0      6
     A            GMPERÅ         2  0     12
     A            GMPERM         2  0     15
     A            GMKONT         5  0     22
     A            GMKSTE         4  0     28EDTCDE(Z)
     A N50 61                               HIGHLIGHT
     A            GMKBÆR         4  0     34EDTCDE(Z)
     A                                    42MSGCON(30 JOV0236 SYMSGF)
     A            DETTOT        13S 2    109EDTCDE(J)
      * IB= FØRT I LAVERE PERIODE ENN OPPDRAGET..
      * UB= FØRT I HØYERE PERIODE ENN OPPDRAGET..
     A  51                               127'IB'
     A N51                               130'UB'
     A          R GRAT                      HIGHLIGHT
     A                                      SPACEB(1)
     A N50                                  SPACEA(1)
     A            GMFPEÅ         2  0      3
     A            GMFPEM         2  0      6
     A            GMPERÅ         2  0     12
     A            GMPERM         2  0     15
     A                                    22MSGCON(80 JOV0758 SYMSGF)
     A            GRATOT        13S 2    109EDTCDE(J)
     A**
     A**
