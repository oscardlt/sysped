     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HELP
     A          R BILDE
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF03(03 'Avslutt registrering av BI-
     A                                      LL OF LADING')
     A                                      CF04(04 'Søke')
     A                                      CF12(12 'FORRIGE')
     A                                      CF14(14 'Vise opplysning til AVSEND-
     A                                      ER/MOTTAKER/NOTIFY')
     A                                      CF15(15 'BE UTSKRIFT')
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/SYSJ27B'
     A                                      COLOR(BLU)
     A                                  1 61DATE
     A                                      EDTCDE(Y)
     A                                  1 70TIME
     A                                  1 20MSGCON(40 DIV3624 SYMSGF)
     A                                      DSPATR(HI)
     A                                  2  2MSGCON(4 DIV3163 SYMSGF)
     A            UAVD           4Y 0O  2  7DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  2 12MSGCON(6 DIV3164 SYMSGF)
     A            UOPD           7Y 0O  2 19DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  2 27MSGCON(5 DIV3165 SYMSGF)
     A            BLSG           3A  O  2 33DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  2 38MSGCON(5 DIV3185 SYMSGF)
     A            BLPRO          8Y 0O  2 44DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  2 54MSGCON(4 DIV3186 SYMSGF)
     A            BLOB           5A  O  2 59DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  2 66MSGCON(3 DIV3166 SYMSGF)
     A            BLUKE          2Y 0O  2 70DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  2 74MSGCON(4 DIV3167 SYMSGF)
     A            BLAAR          2Y 0O  2 79DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  3  2MSGCON(3 DIV3190 SYMSGF)
     A            BLKNR          8Y 0O  3  6DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            WSNAR         18A  O  3 15DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  3 34MSGCON(2 DIV3191 SYMSGF)
     A            BLTR           4A  O  3 37DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  3 42MSGCON(4 YBC0112 SYMSGF)
     A            BLREIS        14A  O  3 47DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  3 62MSGCON(2 YBC0113 SYMSGF)
     A            WSBLNR        16A  B  3 65DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  4  2MSGCON(3 DIV3142 SYMSGF)
     A            WSKNA          8Y 0B  4  6DSPATR(UL)
     A  99                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            BLDKA          1A  O  4 16DSPATR(UL)
     A                                      COLOR(WHT)
     A            WSNAA         30A  B  4 19DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSAD1A        30A  B  4 51DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSAD2A        30A  B  5 19DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSAD3A        30A  B  5 51DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  6  2MSGCON(3 DIV3143 SYMSGF)
     A            WSKNM          8Y 0B  6  6DSPATR(UL)
     A  98                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSNAM         30A  B  6 19DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSAD1M        30A  B  6 51DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSAD2M        30A  B  7 19DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSAD3M        30A  B  7 51DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  8  2MSGCON(3 DIV3144 SYMSGF)
     A            WSKNN          8Y 0B  8  6DSPATR(UL)
     A  97                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSNAN         30A  B  8 19DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSAD1N        30A  B  8 51DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSAD2N        30A  B  9 19DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSAD3N        30A  B  9 51DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 10  2MSGCON(8 DIV3194 SYMSGF)
     A            BLKNSA         8Y 0O 10 11DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            BLDKSA         1A  O 10 20DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 10 22MSGCON(8 DIV1062 SYMSGF)
     A            BLKNK          8Y 0O 10 31DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            BLDKK          1A  O 10 40DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 10 42MSGCON(8 DIV3196 SYMSGF)
     A            BLKNKA         8Y 0O 10 51DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            BLDKKA         1A  O 10 60DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 10 62MSGCON(8 YBC0198 SYMSGF)
     A            BLKNRA         8Y 0O 10 71DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            BLDKRA         1A  O 10 80DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 11  2MSGCON(5 DIV3198 SYMSGF)
     A            BLLSS          5A  O 11  8DSPATR(UL)
     A                                      COLOR(WHT)
     A            BLLSS2         4A  O 11 14DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 11 20MSGCON(5 DIV3199 SYMSGF)
     A            WSLSH          5A  B 11 26DSPATR(UL)
     A  96                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 11 32MSGCON(5 DIV3200 SYMSGF)
     A            BLLB           5A  O 11 38DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 11 44MSGCON(5 DIV3201 SYMSGF)
     A            BLVH           5A  O 11 50DSPATR(UL)
     A                                      COLOR(WHT)
     A            WSDTVH         6Y 0O 11 56DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 11 63MSGCON(005 YBC0273 *LIBL/SYMSGF)
     A            BLVH2          5A  O 11 69DSPATR(UL)
     A                                      COLOR(WHT)
     A            WSDVH2         6Y 0O 11 75DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 12  2MSGCON(5 DIV3203 SYMSGF)
     A            WSLOH          5A  B 12  8DSPATR(UL)
     A  95                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 12 20MSGCON(5 DIV3204 SYMSGF)
     A            BLLOS          5A  O 12 26DSPATR(UL)
     A                                      COLOR(WHT)
     A            BLLOS2         4A  O 12 32DSPATR(UL)
     A                                      COLOR(WHT)
     A N27                             12 38MSGCON(10 DIV3205 SYMSGF)
     A                                      COLOR(TRQ)
     A  27                             12 38MSGCON(10 DIV3928 SYMSGF)
     A                                      COLOR(TRQ)
     A            WSDTA          6Y 0O 12 49DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 12 58MSGCON(10 DIV3206 SYMSGF)
     A            BLKDP          5A  O 12 69DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 12 76MSGCON(3 DIV3207 SYMSGF)
     A            BLCH           1A  O 12 80DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 13  2MSGCON(8 DIV3208 SYMSGF)
     A            BLMM          15A  O 13 11DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 13 28MSGCON(10 DIV3146 SYMSGF)
     A            BLMN          17A  O 13 39DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 13 63MSGCON(5 DIV3209 SYMSGF)
     A            BLLSTY         3A  O 13 69DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 13 76MSGCON(3 DIV3210 SYMSGF)
     A            BLMH           1A  O 13 80DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 14  2MSGCON(8 DIV3220 SYMSGF)
     A            WSCP           1   B 14 11TEXT('COLL/PPD')
     A                                      DSPATR(UL)
     A  94                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 14 13MSGCON(10 DIV3153 SYMSGF)
     A            BLNTBS        15   O 14 24DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 14 40MSGCON(5 DIV3154 SYMSGF)
     A            BLSD           5   O 14 46DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 14 52MSGCON(5 DIV3155 SYMSGF)
     A            WSDTR          6Y 0O 14 58DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 15 23MSGCON(7 DIV3211 SYMSGF)
     A                                 15 41MSGCON(8 DIV3222 SYMSGF)
     A            BLKNG          8Y 0O 15 50DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 16  2MSGCON(20 DIV3212 SYMSGF)
     A            BLOLP          1   O 16 24TEXT('PRE. ORIG. LANDFR.')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            BLOLC          1   O 16 28TEXT('COL. ORIG. LANDFR.')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            BLNAG         30A  O 16 41DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 17  2MSGCON(20 DIV3213 SYMSGF)
     A            BLOTP          1   O 17 24TEXT('PRE. ORIG. TOC')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            BLOTC          1   O 17 28TEXT('COL. ORIG. TOC')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            BLAD1G        30A  O 17 41DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 18  2MSGCON(20 DIV3214 SYMSGF)
     A            BLSFP          1   O 18 24TEXT('PRE. SEAFREIGHT')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            BLSFC          1   O 18 28TEXT('COL. SEAFREIGHT')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            BLAD2G        30A  O 18 41DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 19  2MSGCON(20 DIV3217 SYMSGF)
     A            BLDTP          1   O 19 24TEXT('PRE. DESTINATION TOC')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            BLDTC          1   O 19 28TEXT('COL. DESTINATION TOC')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            BLAD3G        30A  O 19 41DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 20  2MSGCON(20 DIV3218 SYMSGF)
     A            BLDLP          1   O 20 24TEXT('PRE. DEST. LANDFR.')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            BLDLC          1   O 20 28TEXT('COL. DEST. LANDFR.')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                 21  2MSGCON(20 DIV3219 SYMSGF)
     A            BLMFP          1   O 21 24TEXT('PRE. MULTIMODAL FR.')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            BLMFC          1   O 21 28TEXT('COL. MULTIMODAL FR.')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A N27                             22 62MSGCON(15 DIV3215 SYMSGF)
     A                                      COLOR(RED)
     A  27                             22 62MSGCON(15 DIV3216 SYMSGF)
     A                                      COLOR(RED)
     A                                 23  2MSGCON(78 DIV3634 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                             24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
