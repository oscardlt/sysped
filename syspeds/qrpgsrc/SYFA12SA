********************************************************************
      * Print av label men på AFP /Laser
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 JOV PTF10
10XXX * 02 YBC PTF10 WEB-OPPDRAG HAR IKKE AVDELING OG OPPDRAGSNR
10XXX * 03 JOV PTF10 Feil Postnr/sted ved DFST=U
      *%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
10XXX * 04 JOV PTF11 IKKE forsøk å chaine DOKUF mm når layout er HZ - booking Z-filer
10XXX * 05 JOV PTF11 forsøk med FNC1 i front av stekkode
      *%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
10XXX * 06 JOV PTF12 test frapostnr før det legges i DFPLNLS
********************************************************************
     FDOKUF     IF   E           K DISK
     FDOKUFE    IF   E           K DISK
     FHEADF     IF   E           K DISK
     FKOFAST    IF   E           K DISK
     FFIRFB     UF   E           K DISK
     FDOKUFM    UF A E           K DISK
     FZOKUFM1   UF A E           K DISK
     F                                     RENAME(dokufmr:zokufmr)
     FZEADL02   UF   E           K DISK
     F                                     RENAME(headfr:zeadfr)
     FKODFRI    IF   E           K DISK
     FFRISOK    IF A E           K DISK
     FZRISOL01  IF A E           K DISK
     F                                     RENAME(frisokr:zrisokr)
     FGSSORF    IF   E           K DISK
     FGSSORL21  IF   E           K DISK
     F                                     RENAME(gssorfr:gssorf21R)
     FGSSPOF    IF   E           K DISK
     FGSSRFF    IF   E           K DISK
     FVADR      IF   E           K DISK
     FGSSOLF    UF   E           K DISK
     FKODTLK    IF   E           K DISK
N01  FKODTA     IF   E           K DISK
N01  FFIRM      IF   E           K DISK
     FSYFA12SAP O    E             PRINTER
NRFA D                 DS
NRFA D  HERFA                  1     35
NRFA D  HERFA15                1     15
     D                 DS
     D  HEFT1                  1     51
     D  HEFT1A                 1     30
     D  HEFT1B                31     45
     D                 DS
     D  WS1004                 1     35
     D  WSSNDA                 1      2
     D  WSSNDB                 3      7
     D  WSSNDC                 8     17
     D  WSSNDI                 1     17
     D                 DS
     D  DF1004                 1     35
     D  WSSN17                 1     17
     D  DF1004A               17     17
     D  DF100416               1     16
     D                 DS
     D  KOID                   1     18
     D  DSIDPK                 1      1
     D  DSIDLA                 2      3
     D  DSIDLE                 4      8
     D  DSIDNR                 9     18
      * Norsk GS1-nr skal være (00+) 18 lang - 18 blank eller 19 ikke blank er FEIL
     D                 DS
     D  FMMRK1                 1     35
     D    FMMRK18_18          18     18
     D    FMMRK19_19          19     19
     D                 DS
     D  KLKNVN                 1     30
     D  DsLkNr                28     30
     D                 DS
     D  DFPNUL                 1      9
     D   DFPNUL5               5      5
     D   DFPNUL6               6      6
     D   DFPNUL7               7      7
     D   DFPNUL8               8      8
     D   DFPNUL9               9      9
     D                 DS
N05  D* PONRUL                 1     15
N05  D*  PONRUL4               1     10
N05  D*  PONRUL5               1     11
N05  D*  PONRUL6               1     12
N05  D*  PONRUL7               1     13
N05  D*  PONRUL8               1     14
N05  D*  PONRUL9               1     15
N05  D  PONRUL                 1     16
N05  D   FNC1                  1      1
N05  D   PONRUL4               1     11
N05  D   PONRUL5               1     12
N05  D   PONRUL6               1     13
N05  D   PONRUL7               1     14
N05  D   PONRUL8               1     15
N05  D   PONRUL9               1     16
     D                 DS
     D  WTIME                  1     14  0
     D    WTID4                1      4  0
     D    WDATO                7     14  0
N01   * dobeltdefinert for PRTF
      * konvertering særtegn til OKTegn
     D SÆRT            C                   CONST('ÆØÅæøå')
     D OKT             C                   CONST('EOAeoa')
      *
     C                   EXSR      SRINIT
      *
      * Loop number of pieces
     C                   Z-ADD     1             WPCN              5 0
     C     WPCN          DO        DFNTLA        WPCN
      *
     C     WMLAYO        IFNE      'W2'
      * Hent cll-ID, enten eksisternde eller NY
     c                   EXSR      SR0001K
     c                   end
N02  C                   eval      KLLTXT= %trim(%editc(WPCN:'Z'))
N02  c                             +' AV '+%trim(%editc(DFNTLA:'Z'))
     c*                  eval      KOID20='00'+KOID
     c                   eval      KOID21=X'8F'+'00'+KOID
N05  c                   eval      HRI_koid='(00)'+KOID
     c                   write     LABEL
      *
     C                   ENDDO
      *
     C                   seton                                        LR
     C                   RETURN
      *
      *
      *_____________________________________________________
      * Hent data fra HEADF / ZEADF
      *"""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     GetHead       BEGSR
     C     WMlayo        IFEQ      'HZ'
      * Heunik = 9 lang.
     c                   Movel     WFAVD         TMP11            11
     c                   Move      WFOPD         TMP11
     c                   Move      Tmp11         HEUNIK
     C     HEUNIK        CHAIN     ZEADL02                            33
     C     *in33         ifeq      '0'
     c                   move      'P'           HEPK3
     C                   update    zeadfr
     c                   end
     c                   end
      * merkelapper lagres på fraktbrev 1
     c                   z-add     1             DFFBNR
      *
     c                   MOVE      ' '           DFST
     c                   select
     c     heur          wheneq    'B'
     c                   MOVE      'U'           DFST
     c     heur          wheneq    'H'
     C     HETRI         IFNE      'NO'
     c                   MOVE      'U'           DFST
     c                   end
     c                   endsl
      * Flytt HE-felt inn i DF felt..
     C                   MOVE      'F'           DFRI
     C                   MOVE      HEavd         DFavd
     C                   MOVE      HEopd         DFopd
     C                   MOVE      HESG          DFSG
     C                   MOVEL     HENAS         DFNAVS
     C                   MOVEL     HEADS1        DFAD1S
     C                   MOVEL     HEADS2        DFAD2S
S06  C*                  MOVEL     HEADS3        DFPNLS
N06  C                   MOVE      *ZEROS        DFPNLS                          hen.POSTNR
N06  C                   MOVEL     HEADS3        TMPPUL            4
N06  C                   TESTN                   TMPPUL               34
N06  C     *IN34         IFEQ      '1'
N06  C                   MOVE      TMPPUL        DFPOUL                          UTL.POSTNR
     C                   MOVE      HEADS3        DFAD3S25         25
     C                   MOVEL     DFAD3s25      DFAD3S
N06  C                   else
N06  C                   MOVEL     HEADS3        DFAD3S
N06  C                   end
     C                   MOVEL     HENAK         DFNAVM
     C                   MOVEL     HEADK1        DFAD1M
     C                   MOVEL     HEADK2        DFAD2M
     C                   MOVEL     HEADK3        DFAD3M
     C     DFST          IFEQ      'U'
      * scan finner første blank for å skille Postnr /sted
     C                   MOVE      HETRI         DFLKM
     C                   MOVE      HETRI         DFLKL
     C     ' '           SCAN      HEADK3        pos               2 0    33
     c     pos           ifgt      3
     c     pos           andlt     10
     C                   EVAL      DFPNUM      = %SUBST(HEADK3:1:pos-1)
     C                   EVAL      DFAD3M      = %SUBST(HEADK3:pos+1)
     C                   MOVE      DFPNUM        DFPNUL
     C                   end
     C                   else
     C                   MOVE      *ZEROS        DFPOUL                          UTL.POSTNR
     C                   MOVEL     HEADK3        TMPPUL            4
     C                   TESTN                   TMPPUL               34
     C     *IN34         IFEQ      '1'
     C                   MOVE      TMPPUL        DFPOUL                          UTL.POSTNR
     C                   end
     C                   end
     C                   Z-ADD     HENT          DFNTLA
     C                   Z-ADD     HENT          DFNT
     C                   MOVEL     HEGM1         DFGM
     C                   MOVEL     HEGM2         DFGM2
      * sendingsnr - hent fra FRISOK - finnes ikke / skap...
     c     wmlayo        IFEQ      'HZ'
      * På ZEADF
     c     HEUNIK        setll     Zrisol01
     c     HEUNIK        Reade     Zrisol01                               33
     c     *in33         doweq     '0'
     c     FSKODE        IFEQ      'IFB'
     c                   movel     FSSOK         DF1004
     c                   goto      FbnrOK
     c                   end
     c     HEUNIK        Reade     Zrisol01                               33
     c                   end
     c                   else
      * På HEADF
      * innland..
     c                   IF        Heur='H' and HXSNDN<>*blanks
     c                   IF        %subst(HXSNDN:20:1)<>*blanks
     c                   eval      DF1004= %subst(HXSNDN:4:17)
     c                   else
     c                   eval      DF1004=HXSNDN
     c                   endif
     c                   goto      FbnrOK
     c                   endif
     C     FRISOKKEY     CHAIN     FRISOK                             33
     c     *IN33         IFEQ      '0'
     c                   movel     FSSOK         DF1004
     c                   goto      FbnrOK
     c                   end
     c                   end
      * Fant ikke nummer - tildel..
      * TAR HENSYN KUN TIL EAN...
     C     WFIRM         CHAIN     FIRFB                              33
     C     *IN33         IFEQ      '0'
     C     FIRECN        IFNE      *ZEROS
     C                   ADD       1             FIRECN
     C                   MOVE      FISNLA        WSSNDA
     C                   MOVE      FISNLE        WSSNDB
     C                   MOVEL     FIRECN        WSSNDC
     C                   END
     C                   UPDATE    FIRFBR
     C                   MOVE      *ZEROS        UAN22            22
     C                   MOVE      WSSNDI        UAN22
     C                   CALL      'SYGE08'
     C                   PARM                    UAN22
     C                   MOVE      UAN22         WSSNDI
     C                   MOVEL     WS1004        DF1004
     C                   END
      *
     c                   move      HEAVD         FSAVD
     c                   move      HEOPD         FSOPD
     c                   move      'IFB'         FSKODE
     C                   MOVEL     DF1004        FSSOK
     c                   move      HEDTOP        FSDTOP
     c                   move      *BLANKS       FSDOKK
     C     FSKODE        CHAIN     KODFRI                             33
     c                   IF        *IN33
     c                   MOVE      *BLANKS       FSDOKK
     c                   ELSE
     c                   MOVE      KFSODK        FSDOKK
     c                   END
     c     wmlayo        IFEQ      'HZ'
     c                   move      HEUNIK        FSUNIK
     c                   move      HEREFF        FSREFF
     C                   write     ZRISOKR
     C                   else
     C                   write     FRISOKR
     C                   end
      *
     c     FbnrOK        tag
      *
     C                   endsr
      *_____________________________________________________
      * Execute Label Init
      *"""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     SR0001        BEGSR
      *
     C                   MOVE      *ZEROS        FMLONR
     C                   MOVE      *BLANKS       KOID
      * Hent kolli-id
     C     WFIRM         CHAIN     FIRFB                              33
     C                   ADD       1             FIIDNR
     C                   UPDATE    FIRFBR
     C                   MOVE      '3'           DSIDPK
     C                   MOVE      FIIDLA        DSIDLA
     C                   MOVE      FIIDLE        DSIDLE
     C                   MOVEL     FIIDNR        DSIDNR
     C                   MOVE      *ZEROS        UAN22            22
     C                   MOVE      KOID          UAN22
     C                   CALL      'SYGE08'
     C                   PARM                    UAN22
     C                   MOVE      UAN22         KOID
     C                   ADD       1             FMMKNR
     C                   eval      FMMRK1    =   KOID
     C                   MOVE      'I'           FMST
     C                   clear                   FMITER
     C                   clear                   FMUTER
     C                   clear                   FMLEVE
     C                   clear                   FMLEDI
     C                   clear                   FMVKT
     C                   clear                   FMVOL
     C                   clear                   FMLM
     C                   clear                   FMLEN
     C                   clear                   FMBRD
     C                   clear                   FMHOY
     C                   clear                   FMPAID
     C                   clear                   FMITDT
     C                   clear                   FMITTM
     C                   clear                   FMITUS
     C                   clear                   FMUTDT
     C                   clear                   FMUTTM
     C                   clear                   FMUTUS
     C                   clear                   FMPODT
     C                   clear                   FMPOTM
     C                   clear                   FMPOUS
     C     WMLAYO        IFNE      'HZ'
     C                   WRITE     DOKUFMR
     C                   ELSE
     C                   MOVE      HEUNIK        FMUNIK
     C                   MOVE      HEREFF        FMREFF
     C                   WRITE     ZOKUFMR
     C                   END
      *
     C                   ENDSR
      *_____________________________________________________
      * Execute Label Init copyprint
      *"""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     SR0001K       BEGSR
      * SJEKK OM GAMMEL KOLLI ID NR XX (WPCN) FINNES SOM SKAL GJENBRUKES
      *
      ** Les og finn intern/ekstern med løpenr som samsvarer xx AV Tot
     C                   MOVE      *BLANKS       KOID
     c                   move      *zeros        LnrIE             4 0
     c                   move      *zeros        FMMKNR
     C     DOKUFMKEY     SETLL     DOKUFM
     C     DOKUFMKEY     READE(N)  DOKUFM                                 33
     C     *IN33         DOWEQ     '0'
     C     FMST          ifeq      'I'
     C     FMST          oreq      'E'
     c                   add       1             LnrIE
     c     LnrIE         ifeq      wpcn
     C                   MOVEL     FMMRK1        KOID
     C                   endif
     C                   endif
     C     DOKUFMKEY     READE(N)  DOKUFM                                 33
     C                   END
      * Hvis ikke finner kolliid på posisjonen HENT NY (FØRSTE KJØRING ELLER ANTALL ØKT..)
     C     KOID          IFEQ      *blanks
     C                   exsr      sr0001
     C                   ENDIF
     C                   ENDSR
      *_____________________________________________________
      * Initialise subroutine, set character set
      *"""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     SRINIT        BEGSR
      *
      * Input Parameter
     C     *ENTRY        PLIST
     C                   PARM                    WFIRM             2
     C                   PARM                    WMLAYO            2
     C                   PARM                    WFAVD
     C                   PARM                    WFOPD
     C                   PARM                    WFFBNR
     C                   PARM                    uukopi            1
      * Define workfields
     C     *LIKE         DEFINE    DFRI          WFRI
     C     *LIKE         DEFINE    DFAVD         WFAVD
     C     *LIKE         DEFINE    DFOPD         WFOPD
     C     *LIKE         DEFINE    DFFBNR        WFFBNR
     C     *LIKE         DEFINE    KFTYP         WFTYP
     C     *LIKE         DEFINE    KFKOD         WFKOD
     C                   MOVEL     'F'           WFRI
     C                   MOVEL     'FRBREVPRO'   WFTYP
     C     DOKKEY        KLIST
     C                   KFLD                    WFRI
     C                   KFLD                    WFAVD
     C                   KFLD                    WFOPD
     C                   KFLD                    WFFBNR
     C     DOKUFEKEY     KLIST
     C                   KFLD                    WFAVD
     C                   KFLD                    WFOPD
     C                   KFLD                    WFFBNR
     C                   KFLD                    WE_N3035
     C     *LIKE         DEFINE    FE_N3035      WE_N3035
     C                   EVAL      WE_N3035='CN '
     C     KOKEY         KLIST
     C                   KFLD                    WFTYP
     C                   KFLD                    WFKOD
     C     LKKEY         KLIST
     C                   KFLD                    KLKUNI
     C                   KFLD                    DFLKL
     C                   MOVEL     'LK '         KLKUNI
     C     DOKUFMKEY     KLIST
     C                   KFLD                    FMRI
     C                   KFLD                    FMAVD
     C                   KFLD                    FMOPD
     C                   KFLD                    FMFBNR
     C                   KFLD                    FMLINR
     C     DOKUFMKE2     KLIST
     C                   KFLD                    FMRI
     C                   KFLD                    FMAVD
     C                   KFLD                    FMOPD
     C                   KFLD                    FMFBNR
     C                   KFLD                    FMLINR
     c                   KFLD                    FMMKNR
     C     HEAKEY        KLIST
     C                   KFLD                    WFAVD
     C                   KFLD                    WFOPD
     C     DUAKEY        KLIST
     C                   KFLD                    TRAVD0
     C                   KFLD                    TROPD0
     C     FRISOKKEY     KLIST
     C                   KFLD                    WFAVD
     C                   KFLD                    WFOPD
     C                   KFLD                    XXSOK
     C                   move      'IFB'         XXSOK             3
     C     GSSOLKey      KLIST
     C                   KFLD                    ORONO
     C                   KFLD                    OLLNO
     C     VadrKey       KLIST
     C                   KFLD                    WFIRM
     C                   KFLD                    ORCNO
     C                   KFLD                    VADRNR
N01  C     KODTAKEY      KLIST
N01  C                   KFLD                    XKAUNI
N01  C                   KFLD                    WFAVD
N01  C                   MOVE      'A'           XKAUNI            1
N01  C     KODTAKEY      CHAIN     KODTA                              33
N01   *
N01  C     WFIRM         CHAIN     FIRM                               33
      *
      * 77 ON = FRA WEB-LØSNING, SKAL SKRIVES UT VIA HTML
      * WW - ordinært knyttet til gitt fraktbrev, W2=Frie lapper..
     C     WMLAYO        COMP      'WW'                                   77
     C  N77WMLAYO        COMP      'W2'                                   77
     C  N77WMLAYO        COMP      'HZ'                                   77
      * Execute init routine only once
      *
     C     WMLAYO        IFNE      'W2'
N04  C     WMLAYO        Ifne      'HZ'
     C     DOKKEY        CHAIN     DOKUF                              55
     c                   move      *blanks       TLFnr            15
     C     DOKUFEKEY     CHAIN     DOKUFE                             33
     c     *in33         ifeq      '0'
     c                   movel     FE_C3148D     TLFnr                          Sms
     c     TLFnr         ifeq      *blanks
     c                   movel     FE_C3148B     TLFnr                          Mob
     c                   end
     c     TLFnr         ifeq      *blanks
     c                   movel     FE_C3148A     TLFnr                          tlf
     c                   end
     c                   end
     C     HEAKEY        CHAIN     HEADF                              33
     C                   MOVE      ' '           EXPORT            1
     c     HEUR          ifeq      'B'
     c     HEUR          oreq      'D'
     C                   MOVE      'J'           EXPORT
     C                   ENDIF
     c     heur          IFEQ      'H'
     C     HETRI         ANDNE     'NO'
     C                   MOVE      'J'           EXPORT
     C                   ENDIF
      * DERSOM DUP AV.. hent evt Gnr/turnr/oppr.avsender fra "moroppdrag"
     C                   Move      *BLANKS       DUAavs           25
     C                   Move      *BLANKS       DUAGn            15
     C                   Move      *Blanks       DUATur            8
     C     *in33         IFEQ      *OFF
     C     TROPD0        andne     *zeros
     C     DUAKEY        CHAIN     HEADF                              33
     C     *in33         IFEQ      *OFF
     C                   Move      HENAS         DUAavs
     C                   Move      HEGN          DUAGn
     C     Hepro         ifne      *zeros
     C                   Move      HEPRO         DUATur
     C                   end
     C                   end
     C     HEAKEY        CHAIN     HEADF                              33
     C                   end
N04  C                   endif
      * Basert på headf eller ZEADF - fyll DF..-variable med HE..-data
     c     WMLAYO        IFeq      'HE'
     c     WMLAYO        OREQ      'HZ'
     c                   EXSR      GetHead
     c                   end
      ** vanlige (ikke W2)
     C                   EXSR      SRINKOID
     c                   else
     C* KODE W2 - "Løse lapper" knyttet til skipssystem ("på fabrikk")
     C     WFOPD         CHAIN     GSSORF                             55
     C                   Z-ADD     ORCLI         DFNTLA
     C                   MOVEL     ORSNM         DFNAVS
     C                   MOVEL     ORBCD         DFNAVM
      * Kolli-ID som skal benyttes finnes alt i GSSOLF
     c                   end
      * EDI ventekode telle som utlev.forbehold
     c     DFVEN         Ifne      ' '
     c     HEFT1A        ifeq      *blanks                                      Ikke utlev.forb.h.
     c     HEKFT1        orne      *blanks                                      eller er utkvittert
     c                   eval      HEFT1a='Ventekode '
     c                             +DFVEN+' på fr.br'
     c                   eval      HEKFT1=' '
     c                   endif
     c                   endif
     C     WMLAYO        IFEQ      'WW'
     C                   MOVEL     WFAVD         TRIP
     C                   MOVE      WFOPD         TRIP             11 0
     C     TRIP          SETLL     GSSORL21
     C                   END
      *
      * Klargjør div felt for output
     c*                  eval      SNDN20='401'+DF1004
     c                   eval      SNDN21=X'8F'+'401'+DF1004
N05  c                   eval      HRI_sndn='(401)'+DF1004
      * Text to be printed (421)= AI internasj. nr / 578= Norge
     C                   MOVE      DFPOUL        XXPOUL            4
S05  C*                  move      *blanks       PONR10           10
S05  C*                  Eval      PONR10 = '421578'+XXPOUL
N05  C                   move      *blanks       PONRNO
N05  C                   Eval      PONRNO =  X'8F'+'421578'+XXPOUL
N05  c                   eval      HRI_ponr='(421)578'+XXPOUL
     C                   MOVE      '1'           *IN51                          51 = NO
     C                   MOVEA     '000000'      *IN(54)
     C     DFLKL         IFNE      *blanks
     c     LKKEY         Chain     KODTLK                             33
     c     *in33         Ifeq      *off
     c     DsLKnr        ANDGE     '000'
     c     DsLKnr        ANDLE     '999'
     C                   MOVE      '0'           *IN51                          51 OFF = UTLAND
     C                   Eval      PONRUL = X'8F'+'421'+DsLKnr+DFPNUL
N05  c                   eval      HRI_ponr='(421)'+DsLKnr+DFPNUL
     C                   select
     c     DFPNUL9       whenne    ' '
     C                   MOVEA     '000001'      *IN(54)                        59 = 9 lang UL
     c     DFPNUL8       whenne    ' '
     C                   MOVEA     '000010'      *IN(54)                        58 = 8 lang UL
     c     DFPNUL7       whenne    ' '
     C                   MOVEA     '000100'      *IN(54)                        57 = 7 lang UL
     c     DFPNUL6       whenne    ' '
     C                   MOVEA     '001000'      *IN(54)                        56 = 6 lang UL
     c     DFPNUL5       whenne    ' '
     C                   MOVEA     '010000'      *IN(54)                        55 = 5 lang UL
     c                   other
     C                   MOVEA     '100000'      *IN(54)                        54 = 4 lang UL
     c                   endsl
     c                   end
     c                   end
      *
     C                   MOVEL     DFPNLS        XXAD3L           35
     C                   MOVE      DFAD3S        XXAD3L
     C                   MOVEL     XXAD3L        AVSPNS
      *
     C     DFNAVL        IFNE      *BLANKS
     C     EXPORT        ANDNE     'J'
     C                   MOVEL     DFNAVL        MOTNAV
     C                   ELSE
     C                   MOVEL     DFNAVM        MOTNAV
     C                   ENDIF
     C     DFNAVL        IFNE      *BLANKS
     C     EXPORT        ANDNE     'J'
     C                   MOVEL     DFAD1L        MOTADR
     C                   ELSE
     C                   MOVEL     DFAD1M        MOTADR
     C                   ENDIF
     C     DFNAVL        IFNE      *BLANKS
     C     EXPORT        ANDNE     'J'
S01  C*                  MOVEL     DFPOUL        XXAD3L           35
S01  C*                  MOVE      DFADUL        XXAD3L
S01  C*                  MOVEL     XXAD3L        MOTPNS
     C                   MOVEL     DFPOUL        MOTPNR
N02  C                   MOVEL     DFPOUL        MOTPNRB           5
     C                   MOVEL     DFADUL        MOTSTE
     C                   ELSE
N03  c                   if        DFST = 'U'
N03  C                   EVAL      MOTPNR = DFPNUM
N03  C                   EVAL      MOTPNRB= DFPNUM
N03  C                   EVAL      MOTSTE = DFAD3M
N03  C                   else
S01  C*                  MOVEL     DFAD3M        MOTPNS
     C                   MOVEL     DFAD3M        MOTSTE
N01  C     ' '           SCAN      DFAD3M:1      Y                 3 0
N01  c                   if        Y<>0
N01  C                   EVAL      MOTPNR = %SUBST(DFAD3M:1:Y)
N03  C                   EVAL      MOTPNRB= MOTPNR
N01  C                   EVAL      MOTSTE = %SUBST(DFAD3M:Y+1:30-Y-1)
N03  c                   endif
N01  c                   endif
     c                   ENDIF
      *
     C                   MOVEL     DFNAT         TRANAVN          30
     C     EXPORT        IFEQ      'J'
     c                   eval      TRANAVN=%trim(TRANAVN)+' (Henting)'
     c                   ENDIF
N01
N01   * Innland henting ??
N01  c                   setoff                                       6364
N01  c     HESDVT        ifne      *blanks
N01  c     HESDT         oreq      '1000'
N01  c     HESDT         oreq      '3000'
N01  c     HESDT         oreq      '1405'
N01  C     KOAIE         IFEQ      '2'                                          INNGÅENDE INNLAND
N01  c                   seton                                        63
N01  c*                  eval      TRANAVN=%trim(FIKRTN)+' (Henting)'
N01  c                   eval      TRANAVN=%trim(FIKRTN)
N01  c***  Viakey        Chain     SYPARL01 ???
N01  c     HESDVT        ifeq      '3000'
N01  c     HESDT         oreq      '3000'
N01  c                   eval      ViaStrek='__________________________'
N01  c                   eval      ViaLed ='Via'
N01  c                   eval      ViaTxt1= %subst(HENAA:1:9)
N01  c                                    + ' C/O RAMBERG TERMINAL'
N01  c                   eval      ViaTxt2='SVEND HAUGS GT 11'
N01  c                   eval      ViaTxt3='3013 DRAMMEN'
N01  c                   endif
N01  c     HESDVT        ifeq      '1000'
N01  c     HESDT         oreq      '1000'
N01  c                   eval      ViaStrek='__________________________'
N01  c                   eval      ViaLed ='Via'
N01  c                   eval      ViaTxt1= %subst(HENAA:1:9)
N01  c                                    + ' C/O RAMBERG TERMINAL'
N01  c                   eval      ViaTxt2='GNEISV. 4 '
N01  c                   eval      ViaTxt3='2020 SKEDSMOKORSET'
N01  c                   endif
N01  c     HESDVT        ifeq      '1405'
N01  c     HESDT         oreq      '1405'
V01  c     HESDT         oreq      '1418'                                       fra 09.12.2014
N01  c                   eval      ViaStrek='__________________________'
N01  c                   eval      ViaLed ='Via'
N01  c                   eval      ViaTxt1= %subst(HENAA:1:9)
N01  c                                    + ' C/O RAMBERG TERMINAL'
N01  c                   eval      ViaTxt2='SNIPTJERNVEIEN 2'
N01  c                   eval      ViaTxt3='1405 LANGHUS'
N01  c                   endif
N01  c     HESDT         ifeq      '1000'
N01  c     HESDT         oreq      '3000'
N01  c     HESDT         oreq      '1405'
N02   * ved 64 ON skrives  IKKE UTKJØRING mm på label i stedet for postnr
N02   * ved 64 OFF skriv  terminalens postnr
N02  c**                 seton                                        64
N02  C                   MOVEL     HESDT         MOTPNRB
     c                   endif
N01  c                   endif
N01  c                   endif
      * Produktkode
     c                   setoff                                       61
     c     DFPROK        ifeq      'TG9M'
     c                   eval      TRANAVN=%trim(TRANAVN)+'    MyPack'
     c                   eval      MypMot=%trim(DFNAVM)+'     Tlf:'
     c                             +TlfNr
     c                   eval      MypAdr= %trim(DFAD1M)+ ',   '
     c                             +DFAD3M
     c                   seton                                        61
     c                   else
     C     DFPROK        IFNE      *BLANKS
     C                   MOVEL     DFPROK        WFKOD
     C     KOKEY         CHAIN     KOFAST                             33
     C  N33              eval      PRODUKT = KFTXT
     c                   endif
     c                   endif
      *
     c                   setoff                                       60
     C     HEFT1A        IFNE      *blanks                                       UTLforbehold
     C     HEKFT1        ANDEQ     *blanks                                       IKKE KVITTE
     C                   seton                                        60
     c                   ENDIF
      *Tollpass
     c     DFTOLL        COMP      *Zeros                             62
      *
     c                   eval      MARKS = %trim(DFGM)+' '+DFGM2
      *
N02  C                   IF        WMlayo = 'HZ'
N02  C                   eval      SENDREF = *BLANKS
N02  C                   ELSE
     c                   eval      SENDREF=%trim(%editc(DFAVD:'Z'))
     c                             +'/'+%trim(%editc(DFOPD:'Z'))
     c                             +'/'+%trim(%editc(DFFBNR:'Z'))
     c                             +'/'+DFSG
N02  C                   END
     C     TRopd0        IFNE      *zeros
     c                   eval      DupAv='(Op:'+%trim(%editc(TRAVD0:'Z'))
     c                             +'/'+%trim(%editc(TROPD0:'Z'))+')'
     C                   endif
      *
     c     SÆRT:OKT      XLATE     DFNAVS        DFNAVS
     c     SÆRT:OKT      XLATE     DFAD1S        DFAD1S
     c     SÆRT:OKT      XLATE     AVSPNS        AVSPNS
     c     SÆRT:OKT      XLATE     MOTNAV        MOTNAV
     c     SÆRT:OKT      XLATE     MOTADR        MOTADR
S01  c*    SÆRT:OKT      XLATE     MOTPNS        MOTPNS
     c     SÆRT:OKT      XLATE     TRANAVN       TRANAVN
     c     SÆRT:OKT      XLATE     HEFT1A        HEFT1A
     c     SÆRT:OKT      XLATE     MYPMOT        MYPMOT
     c     SÆRT:OKT      XLATE     MYPADR        MYPADR
     c     SÆRT:OKT      XLATE     MARKS         MARKS
     c     SÆRT:OKT      XLATE     HERFA         HERFA
      * HENTE DAGENS DATO OG TID
     C                   TIME                    WTIME
      *
     C                   ENDSR
      *_____________________________________________________
      * Subroutine to INIT Colli_ID handling
      *"""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     SRINKOID      BEGSR
      * Sjekk evt tidligere tildelte kolli-IDs
     C                   MOVE      DFRI          FMRI
     C                   MOVE      DFAVD         FMAVD
     C                   MOVE      DFOPD         FMOPD
     C                   MOVE      DFFBNR        FMFBNR
     C                   MOVE      *ZEROS        FMLINR
      * MERK UGYLDIGE,(ELLER ANTALL HØYERE ENN FRAKTBREVETS..) MED =O=ORIGINAL
     c                   move      *zeros        LnrIE             4 0
      *
     c     wmlayo        IFNE      'HZ'
     C     DOKUFMKEY     SETLL     DOKUFM
     C     DOKUFMKEY     READE     DOKUFM                                 33
     C     *IN33         DOWEQ     '0'
      * feil format ??
     C     FMST          ifeq      'E'
     C     FMMRK18_18    ifeq      ' '
     C     FMMRK19_19    orne      ' '
     C                   move      'O'           FMST                           O=Original/Ompakket
     c                   endif
     c                   endif
      * for mange kolli-IDs finnes (antall er redusert i fraktbrevet)
     C     FMST          IFEQ      'E'
     C     FMST          OREQ      'I'
     C                   ADD       1             LnrIE
     C     LNRIE         ifgt      DFNTLA
     C                   move      'O'           FMST                           O=Original/Ompakket
     c                   endif
     C                   ENDIF
     C                   UPDATE    DOKUFMR
     C     DOKUFMKEY     READE     DOKUFM                                 33
     C                   END
      *
     C                   else
      * Type HZ = fra WEB oppdragsbooking (slett/tildel nye alltid...)
     C     HEUNIK        SETLL     ZOKUFM1
     C     HEUNIK        READE     ZOKUFM1                                33
     C     *IN33         DOWEQ     '0'
     C                   DELETE    ZOKUFMR
     C     HEUNIK        READE     ZOKUFM1                                33
     C                   END
     C                   endif
      *
     C                   ENDSR
      *_____________________________________________________
      * ved WEB / shipssystem, hent neste ORD / PO
      *"""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     GetNextSS     BEGSR
      * Denne rutine kalles KUN ved layout WW (alle PO's på trip/fr.br)
      * eller W2 (skriv "frie" labels for EN PO, ennå ikke på trip/fbr)
      *
     C     AntSS         IFEQ      AvSS
      * Neste PO/ordre  på trippen
     C     WMLAYO        IFEQ      'WW'
     c     TRIP          reade     GSSORL21                               33
     C                   ELSE
     C                   MOVE      *OFF          *IN33
     C                   END
     c     *in33         IFEQ      *ON
     c                   Move      *zeros        AntSS
     c                   Move      *zeros        AvSS
     c                   else
     c                   z-add     1             AntSS             4 0
     c                   z-add     ORCLI         AvSS              4 0
      * Hent Customer PO samt  internal (fabrikkens) PO.
     C     ORONO         CHAIN     GSSPOF                             33
     C   33              MOVE      *BLANKS       POCONO
     C     ORONO         SETLL     GSSRFF
     C     ORONO         READE     GSSRFF                                 33
     C     *IN33         DOWEQ     '0'
     C     RFKD          IFEQ      'CPO'
     C                   LEAVE
     C                   ENDIF
     C     ORONO         READE     GSSRFF                                 33
     C  N33              ENDDO
     C   33              MOVE      *BLANKS       RFREF
      * Areacode = 2 førset av siste FT
     c                   movel     ORFT4         AREACODE          2
      * Hent Navn/adr (postCode.)  sluttkunde via Båtkode..
     C                   Move      ORBCD         TSTNUM            4
     C                   TESTN                   TSTNUM               33
     c     *in33         IFEQ      *ON
     c                   move      tstnum        VADRNR
     C     VADRKey       CHAIN     VADR                               33
     c   33              Move      *blanks       POSTCODE         10
     c  N33              MoveL     VADRN3        POSTCODE
     c  N33              MoveL     VALAND        POSTCTRY          2
     C     *IN33         IFEQ      *OFF
     C     WMLAYO        ANDEQ     'W2'
     C     POSTCTRY      ANDNE     '  '
     c                   MoveL     VADRN3        DFPNUL
     c                   MoveL     VALAND        DFLKL
     c                   end
     c                   end
      *
     c                   end                                                    End READE ....
     c                   else
      * Neste kolli på samme PO...
     c                   Add       1             AntSS
     c                   end
      *
      * Oppdater EAN kolli-ID på PO-ens kolli-post.
      * (Når det er original knyttet til Shipssyst-trip/fraktbr.(fra FABR))
     c     UUKOPI        Ifne      'J'
     c     AntSS         Andne     *zeros
     c                   z-add     AntSS         OLLNO
     C     GSSOLKey      CHAIN     GSSOLF                             33
     c     *in33         IFEQ      *OFF
     c                   move      ORTNO         OLTNO
     c                   move      ORPOS1        OLSTD
     c                   Eval      OLMN  = '00'+KOID
     c                   update    GSSOLFR
     c                   end
     C                   end
      *
      * Normal trip/fraktbrev UT FRA HUB (da skal kolli-ID finens fra før)
      * har forprogrammet lagt ut også dokufm (basert på ID i GSSOLF)
      * Kjøres som ren kopi.
      *
      * Hent EAN-nr fra GSSOLF når det er "Fri merkelapp" fra SS-ordre
      * (Da finnes ikke DOKUF/DOKUFM) - Kjøres som KOPI.
     C     WMLAYO        IFEQ      'W2'
     c     AntSS         Andne     *zeros
     c                   z-add     AntSS         OLLNO
     C     GSSOLKey      CHAIN(N)  GSSOLF                             33
     c     *in33         IFEQ      *OFF
     c                   movel     OLMN          olmn20           20
     c                   move      OLMN20        KOID
     c                   end
     c                   end
      *
     C                   ENDSR
      *
      *_____________________________________________________
      * ved WEB / shipssystem, Print PO-referanset
      *"""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     PrtNextSS     BEGSR
      * Inern/ekstern PO ledetekst
     C                   ENDSR

      *_____________________________________________________
      * Execute MyPack Mottaker navn / adresse.
      *"""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     MYPMott       BEGSR
      *
     C                   ENDSR
