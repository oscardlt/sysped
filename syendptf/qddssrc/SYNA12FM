     A*%%TS  SD  20171211  101635  SVEIN       REL-V7R1M0  5770-WDS
     A*********************************************************************
*****A**************************************************************
     A* RETTINGER I DETTE PROGRAM:
PTFnrA*Sek Sig Vers Beskrivelse...........................
"""""A*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
P8068A* 01 TH  PTF8 NY HELPFUNKSJON  KUN DDS
*****A**************************************************************
     A*%%EC
     A                                      DSPSIZ(27 132 *DS4)
     A                                      CF03(03 'Avslutt')
     A                                      PRINT
     A                                      HLPPNLGRP(HLPSCR SYEX05PNL)
     A                                      HLPTITLE('VEDLIKEHOLD AV FLYFRAKTRA-
     A                                      TER')
     A                                      HELP
     A                                      ALTHELP
     A          R SUBFIL                    SFL
     A*%%TS  SD  20171211  101635  SVEIN       REL-V7R1M0  5770-WDS
     A            WSNR           1A  I  7  3
     A  98                                  DSPATR(RI)
     A  98                                  DSPATR(PC)
     A                                      VALUES(' ' '4')
     A            ATTAVD         4Y 0O  7  8
     A            KOANVN        33A  O  7 13
     A            ATTSG          3A  O  7 48
     A            KSINAV        16A  O  7 52
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A*%%TS  SD  20171211  101635  SVEIN       REL-V7R1M0  5770-WDS
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A  91N93                               CF05(05 'Frisk')
     A  91N93                               CF06(06 'LAGE NY')
     A  91N93                               CF08(08 'SØK')
     A                                  1  3'SYSPED/SYNA12 '
     A                                      COLOR(BLU)
     A                                  1 57'Dato:'
     A                                      COLOR(BLU)
     A                                  1 63DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  2 57'Kl. :'
     A                                      COLOR(BLU)
     A                                  2 63TIME
     A                                      COLOR(BLU)
     A            ZRECNO         4S 0O  2 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  4  3'Velg funksjon, trykk Enter'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(035 SHU1740 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  6  2MSGCON(080 SHU1741 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  4 40'4=Slette'
     A                                      COLOR(BLU)
     A          R BUNN
     A                                 23  2MSGCON(18 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 21MSGCON(18 DIV0021 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 40MSGCON(18 DIV0022 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 60MSGCON(18 DIV0023 SYMSGF)
     A                                      COLOR(BLU)
     A  98                             24  2'INVALID OPTION'
     A                                      COLOR(WHT)
     A N98                             24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A          R VARELI
     A*%%TS  SD  20130806  161236  SVEIN       REL-V7R1M0  5770-WDS
     A                                      CF12(12 'Forrige')
     A                                      SLNO(20)
     A                                      CLRL(05)
     A                                  1  2MSGCON(080 SHU1741 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            WTTAVD         4S 0B  7  8
     A  70                                  DSPATR(PC)
     A  70                                  DSPATR(RI)
     A            KOANVN        33   O  7 13
     A            WTTSG          3   B  7 48
     A  71                                  DSPATR(PC)
     A  71                                  DSPATR(RI)
     A            KSINAV        16   O  7 52
     A  30        MSGTXT        78A  O  5  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                              5 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A                                  3  1MSGCON(013 SHU1394 *LIBL/SYMSGF)
     A          R VAREL2
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                  1  2MSGCON(080 SHU1741 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            WTTAVD         4Y 0B  2 21DSPATR(PC)
     A                                  2  2MSGCON(12 DIV0026 SYMSGF)
     A                                  4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A          R VAREL3
     A*%%TS  SD  20171211  083252  SVEIN       REL-V7R1M0  5770-WDS
     A                                      CF12(12 'Forrige')
     A                                      SLNO(20)
     A                                      CLRL(05)
     A                                  1  2MSGCON(080 SHU1741 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            ATTAVD         4Y 0O  2  8
     A            KOANVN        33   O  2 13
     A            ATTSG          3   O  2 45
     A            KSINAV        16   O  2 49
     A                                  3  2MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 25MSGCON(18 SHU1743 SYMSGF)
     A                                      COLOR(RED)
     A                                  4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A          R NYWRK
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                  1  3'SYSPED/SYNA12 '
     A                                      COLOR(BLU)
     A                                  1 57'Dato:'
     A                                      COLOR(BLU)
     A                                  1 63DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  2 57'Kl. :'
     A                                      COLOR(BLU)
     A                                  2 63TIME
     A                                      COLOR(BLU)
     A                                  4  3'Velg funksjon, trykk Enter'
     A                                      COLOR(BLU)
     A                                  4 40'  2=Endre  4=Slette'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(035 SHU1740 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  6  2MSGCON(080 SHU1741 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                 23  2MSGCON(18 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 21MSGCON(18 DIV0021 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 40MSGCON(18 DIV0022 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 60MSGCON(18 DIV0023 SYMSGF)
     A                                      COLOR(BLU)
     A                                 24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
