      *
      ***********************************************************
      *** PROGRAM MAINTAINS DOKUFM FILE                       ***
      *** INDICATORS  01 - 26 FUNCTION  / ROLL KEYS           ***
      *** INDICATORS USED                                     ***
      ***********************************************************
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 JOV PTF10 ved Utgang sjekke om TEU / TEU skal oppdateres
10XXX * 02 SH  PTF11 vekt og lengde
10XXX * 03 SH  PTF11 paid
********************************************************************
      *
     FDOKUFM    UF A E           K DISK
N01  FDOKUF     IF   E           K DISK
     FSYFA10FB  CF   E             WORKSTN
     F                                     SFILE(FA10BH50:ZRECNO)
     F                                     INFDS(FBAREA)
     D TXT             S             15    DIM(1) CTDATA PERRCD(1)
     D EXTTXT          C                   CONST('** EKSTERNT TILDELT KOLLI-ID -
     D                                     **')
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
N01  D                 DS
N01  D  DFBREF                 1     17
N01  D  WSBRTX                 1      5
N01  D  WSBRAV                 6      9
N01  D  WSBRST                10     10
N01  D  WSBROP                11     17
      *
     C                   EXSR      INIT
      *
     C     START         TAG
      *______________
      * Clear Subfile
      *""""""""""""""
     C                   MOVE      '1'           *IN93
     C                   WRITE     FA10BK60
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
N01  c     DokKey        chain     DOKUF                              33
N01  C     *IN33         ifeq      *off
N01  C     WSBRTX        andeq     'U.op:'
N01  C                   move      'JOV1531'     MSGNR
N01  C                   move      *on           *in30
N01  C                   endif
N01  C     *in30         ifeq      *off
N01  C     DupMode       andeq     'J'
N01  C                   move      'JOV1532'     MSGNR
N01  C                   move      *on           *in30
N01  C                   endif
      *_____________
      * Fill Subfile
      *"""""""""""""
     C     DOKKY2        SETLL     DOKUFM
     C     DOKKY2        READE(N)  DOKUFM                                 94
      *
     C     *IN94         DOWEQ     '0'
     C                   MOVE      FMST          SFST
     C                   MOVE      FMMKNR        SFMKNR
     C                   MOVE      FMLONR        SFMLNR
     C                   MOVE      FMMRK1        SFMRK
     C                   MOVE      FMITER        SFITER
     C                   MOVE      FMUTER        SFUTER
     C                   MOVE      FMLEVE        SFLEVE
N02  C                   MOVE      FMVKT         SFVKT
N02  C                   MOVE      FMLEN         SFLEN
N02  C                   MOVE      FMPAID        SFPAID
     C                   ADD       1             ZRECNO
     C                   WRITE     FA10BH50
     C     DOKKY2        READE(N)  DOKUFM                                 94
     C  N94              ENDDO
      *
     C                   Z-ADD     ZRECNO        YRECNO            5 0
     C     ZRECNO        IFGT      0
     C                   Z-ADD     1             ZRECNO
     C                   END
     C                   Z-ADD     9             LINNBR
     C                   Z-ADD     3             POSNBR
      *________________
      * Display Subfile
      *""""""""""""""""
     C     START3        TAG
      *
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   WRITE     FA10BB20
     C                   ELSE
     C                   WRITE     FA10BK60
     C                   END
     C                   EXFMT     FA10BB40
      *
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C                   Z-ADD     WSSUBN        XRECNO
     C     XRECNO        IFEQ      0
     C                   Z-ADD     ZRECNO        XRECNO            4 0
     C                   END
      *
      *
     C                   SETOFF                                       999830
      *** F3=Exit
     C     *IN03         IFEQ      '1'
     C                   GOTO      ENDPGM
     C                   END                                                    <*IN03
      *** F5=Refresh
     C     *IN05         IFEQ      '1'
     C                   exsr      REGEN
     C                   GOTO      START
     C                   END                                                    <*IN05
      *** F6=New record
     C     *IN06         IFEQ      '1'
     C                   Z-ADD     10            LINNBR
     C                   Z-ADD     27            POSNBR
     C                   MOVEL     'Ny      '    WMODE
     C                   MOVE      *BLANKS       WMODE2           35
     C                   exsr      FinnMax
     C                   EXSR      CLRSCR
     C                   GOTO      START5
     C                   END                                                    <*IN06
N01  C     *IN10         IFEQ      '1'
N01  C     Dupmode       Ifeq      'N'
N01  C     WSBRTX        andeq     'U.op:'
N01  C                   move      'J'           DUPMODE
N01  C                   move      WSBRAV        UAVD
N01  C                   move      WSBROP        UOPD
N01  C                   z-add     1             UFBNR
N01  C                   else
N01  C                   move      'N'           DUPMODE
N01  C                   move      iAVD          UAVD
N01  C                   move      iOPD          UOPD
N01  C                   move      iFBNR         UFBNR
N01  C                   endif
N01  C                   GOTO      START
N01  C                   endif
N01   *
N01   * F11 = merk E til O på alle
N01  C     *IN11         IFEQ      '1'
N01  C                   exsr      MerkO
N01  C                   GOTO      START
N01  C                   END                                                    <*IN12
      *** F12=Cancel
     C     *IN12         IFEQ      '1'
     C                   MOVE      '0'           *IN12
     C                   GOTO      START
     C                   END                                                    <*IN12
      *** Roll
     C     *IN25         IFEQ      '1'
     C                   MOVE      '0'           *IN25
     C                   ADD       13            ZRECNO
     C     ZRECNO        IFGT      YRECNO
     C                   Z-ADD     YRECNO        ZRECNO
     C                   END
     C                   GOTO      START3
     C                   END                                                    <*IN25
      *** Get options from screen
     C     ZRECNO        IFNE      0
     C                   EXSR      GETOPT
     C                   END                                                    <*IN12
     C                   GOTO      START3
      *** New record
     C     START5        TAG
     C                   EXFMT     FA10BB10
      *** F12=Cancel
     C     *IN12         IFEQ      '0'
     C                   EXSR      TESTB1
     C     *IN30         CABEQ     '1'           START5
     C                   END                                                    <*IN12
     C                   Z-ADD     9             LINNBR
     C                   Z-ADD     3             POSNBR
     C                   GOTO      START3
      *
     C     ENDPGM        TAG
N01  C                   move      iAVD          UAVD
N01  C                   move      iOPD          UOPD
N01  C                   move      iFBNR         UFBNR
     C                   MOVE      '1'           *INLR
     C                   RETURN
      *
      *
      ***********************************************
     C     REGEN         BEGSR
      ***********************************************
      *______________________
      * Regenerer kollinummer
      *""""""""""""""""""""""
     C                   Z-ADD     0             XMAXLN            7 0
     C     DOKKY2        SETLL     DOKUFM
     C     DOKKY2        READE     DOKUFM                                 94
     C     *IN94         DOWEQ     *OFF
     C                   ADD       1             XMAXLN
     C                   MOVE      *zeros        FMMKNR
     C                   UPDATE    DOKUFMR
     C     DOKKY2        READE     DOKUFM                                 94
     C                   ENDDO
      *
     C     DOKKY2        SETLL     DOKUFM
     C     1             do        XMAXLN        XXMKNR            7 0
     C     DOKKY2        READE     DOKUFM                                 94
     C  N94              MOVE      XXMKNR        FMMKNR
     C  N94              UPDATE    DOKUFMR
     C                   ENDDO
      *
     C                   ENDSR
      *
      ***********************************************
     C     TESTB1        BEGSR
      ***********************************************
      *          N40 = NEW
      *           40 = CHANGE
      ***********************************************
     C                   EXSR      TSTSCR
     C     *IN30         IFEQ      '1'
     C                   GOTO      ENDTST
     C                   END                                                    <*IN30
     C     *IN40         IFEQ      '1'
     C                   UPDATE    DOKUFMR
     C                   ELSE
     C                   Z-ADD     1             FMLONR
     C                   WRITE     DOKUFMR
     C                   END                                                    <*IN40
      *
     C                   MOVE      FMMKNR        SFMKNR
     C                   MOVE      FMMRK1        SFMRK
N02  C                   MOVE      FMVKT         SFVKT
N02  C                   MOVE      FMLEN         SFLEN
N03  C                   MOVE      FMPAID        SFPAID
     C     *IN40         IFEQ      '1'
     C                   UPDATE    FA10BH50
     C                   ELSE
     C                   ADD       1             YRECNO
     C                   Z-ADD     YRECNO        ZRECNO
     C                   WRITE     FA10BH50
     C                   END                                                    <*IN40
      *
     C     ENDTST        ENDSR
      *
      ***********************************************************
     C     TSTSCR        BEGSR
      ***********************************************************
      *
     C     TSTEND        ENDSR
      *
      ***********************************************
     C     GETOPT        BEGSR
      ***********************************************
     C                   MOVEA     '00'          *IN(91)
     C                   MOVEA     '00'          *IN(98)
     C                   Z-ADD     1             XLIN              2 0
     C     *IN98         DOWEQ     '0'
     C                   READC     FA10BH50                               98
     C     *IN98         IFEQ      '1'
     C   99              MOVE      '0'           *IN98
     C                   GOTO      ENDGET
     C                   END                                                    <*IN98
      *
     C     ZRECNO        DIV       13            LINNBR
     C                   MVR                     XLIN              2 0
     C     SFMRK         IFEQ      TXT(1)
     C                   UPDATE    FA10BH50
     C                   GOTO      ENDGET
     C                   END
     C                   Z-ADD     10            LINNBR
     C                   Z-ADD     27            POSNBR
      *** 2=Change
     C  N69WSNR          IFEQ      '2'
     C                   MOVE      ' '           WSNR
     C                   MOVEL     'Endre   '    WMODE
     C                   MOVE      '1'           *IN40
     C                   EXSR      CHGST
      *** F12=Cancel
     C     *IN12         CABEQ     '1'           ENDGET
     C                   END                                                    <WSNR=2
      *** 3=Copy
     C  N69WSNR          IFEQ      '3'
     C                   MOVE      ' '           WSNR
     C                   UPDATE    FA10BH50
     C                   MOVEL     'Kopiere '    WMODE
     C                   EXSR      CPYST
      *** F12=Cancel
     C     *IN12         CABEQ     '1'           ENDGET
     C                   END                                                    <WSNR=3
      *** 4=Delete
     C  N69WSNR          IFEQ      '4'
     C                   MOVE      ' '           WSNR
     C                   MOVEL     'Slette  '    WMODE
     C                   EXSR      DLTST
      *** F12=Cancel
     C     *IN12         CABEQ     '1'           ENDGET
     C                   END                                                    <WSNR=4
      *** 5=Display
     C     WSNR          IFEQ      '5'
     C                   MOVE      ' '           WSNR
     C                   MOVEL     'Vise    '    WMODE
     C                   UPDATE    FA10BH50
     C     DOKKYM        CHAIN(N)  DOKUFM                             77
     C                   MOVE      '1'           *IN88
     C                   EXFMT     FA10BB10
     C                   MOVE      '0'           *IN88
      *** F12=Cancel
     C     *IN12         CABEQ     '1'           ENDGET
     C                   END                                                    <WSNR=5
      *** L=se kollilogg
     C     WSNR          IFEQ      'L'
     C                   call      'SYFA10B2'
     c                   parm      SFMRK         UUCLID           18
     C                   UPDATE    FA10BH50
     c                   endif                                                  <WSNR=L
     C                   ENDDO
      *
     C     ENDGET        TAG
     C     XLIN          IFEQ      0
     C                   Z-ADD     13            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C                   ADD       8             LINNBR
     C                   Z-ADD     3             POSNBR
      *
     C                   ENDSR
      *
      ***********************************************
     C     CHGST         BEGSR
      ***********************************************
     C     DOKKYM        CHAIN     DOKUFM                             77
     C                   MOVE      '1'           *IN99
     C                   MOVE      '1'           *IN40
     C                   MOVE      *BLANKS       WMODE2
     C     FMST          IFEQ      'E'
     C                   MOVEL     EXTTXT        WMODE2
     C                   END
     C     STCHG         TAG
     C                   EXFMT     FA10BB10
     C                   MOVE      '0'           *IN30
     C     *IN12         IFEQ      '1'
     C                   EXCEPT    DUMFM
     C                   UPDATE    FA10BH50
     C                   GOTO      CHGEND
     C                   END                                                    <*IN12
     C                   EXSR      TESTB1
     C     *IN30         CABEQ     '1'           STCHG
      *
     C     CHGEND        ENDSR
      *
      ***********************************************
     C     CPYST         BEGSR
      ***********************************************
     c                   exsr      FinnMax
     C     DOKKYM        CHAIN(N)  DOKUFM                             77
     C                   MOVE      '1'           *IN99
     C                   MOVE      '0'           *IN40
     C                   MOVE      *BLANKS       WMODE2
     C     FMST          IFEQ      'E'
     C                   MOVEL     EXTTXT        WMODE2
     C                   END
     C     XMAXLN        ADD       1             FMMKNR
     C     STCPY         TAG
     C                   EXFMT     FA10BB10
     C                   MOVE      '0'           *IN30
     C     *IN12         CABEQ     '1'           CPYEND
     C                   EXSR      TESTB1
     C     *IN30         CABEQ     '1'           STCPY
      *
     C     CPYEND        ENDSR
      *
      ***********************************************
     C     DLTST         BEGSR
      ***********************************************
     C                   MOVE      '1'           *IN99
     C                   MOVEA     '11'          *IN(88)
     C     DOKKYM        CHAIN     DOKUFM                             77
     C                   MOVE      *BLANKS       WMODE2
     C     FMST          IFEQ      'E'
     C                   MOVEL     EXTTXT        WMODE2
     C                   END
     C                   EXFMT     FA10BB10
     C                   MOVEA     '00'          *IN(88)
     C     *IN11         IFEQ      '1'
     C                   DELETE    DOKUFMR
     C                   CLEAR                   SFMRK
     C                   MOVEL     TXT(1)        SFMRK
     C                   END                                                    <*IN11
     C                   UPDATE    FA10BH50
      *
     C                   ENDSR
      ***********************************************
     C     CLRSCR        BEGSR
      ***********************************************
     C                   MOVE      '0'           *IN40
     C                   CLEAR                   DOKUFMR
     C                   MOVE      'F'           FMRI
     C                   MOVE      UAVD          FMAVD
     C                   MOVE      UOPD          FMOPD
     C                   MOVE      UFBNR         FMFBNR
     C                   MOVE      ULINR         FMLINR
     C     XMAXLN        ADD       1             FMMKNR
     C                   Z-ADD     1             FMLONR
      *
     C                   ENDSR
      *
      ***********************************************
     C     FinnMax       BEGSR
      ***********************************************
      *
     c                   move      *zeros        XMAXLN
     c     MaxKey        setgt     DOKUFM
     c     DOKKY2        readpe    DOKUFM                                 33
     c  N33              move      FMMKNR        XMAXLN            7 0
     C                   ENDSR
N01
N01   ***********************************************
N01  C     CHKTEI        BEGSR
N01   ***********************************************
      *
N01   * KAN HA SLETTET KOLLI-IDS.. ER ALLE KOLLI-IDS NÅ SKUTT INN / SAMSVAR MED FBR..
N01  c*    DFNTLA        ifeq      *zeros
N01  c*                  eval      DFNTLA=DFNT
N01  c*                  endif
N01  C*                  MOVE      *ZEROS        ANTCLID           7 0
N01  C*                  MOVE      *ZEROS        ANTITER           7 0
N01  C*                  MOVE      *ZEROS        ANTUTER           7 0
N01  C*    DOKKY2        SETLL     DOKUFM
N01  C*    DOKKY2        READE(N)  DOKUFM                                 94
N01  C*    *IN94         DOWEQ     *OFF
N01  C*    FMST          IFEQ      'I'
N01  C*    FMST          OREQ      'E'
N01  C*                  ADD       1             XMAXLN
N01  C*    FMITER        IFEQ      'X'
N01  C*                  ADD       1             ANTITE
N01  C*                  ENDIF
N01  C*    FMUTER        IFEQ      'X'
N01  C*                  ADD       1             ANTUTE
N01  C*                  ENDIF
N01  C*                  ENDIF
N01  C*    DOKKY2        READE(N)  DOKUFM                                 94
N01  C*                  ENDDO
N01   *
N01  C*    AntClid       ifeq      DFNTLA
N01  C*    AntIte        andeq     DFNTLA
N01  c*                  move      'TEI'         XXACTI
N01  c*                  endif
N01   *
N01  C                   ENDSR
N01   *
N01   ***********************************************
N01  C     MerkO         BEGSR
N01   ***********************************************
N01   * bevar originalene (for å frigi plass til internprint..)
N01  C     DOKKY2        SETLL     DOKUFM
N01  C     DOKKY2        READE     DOKUFM                                 94
N01  C     *IN94         DOWEQ     *OFF
N01  C     FMST          Ifeq      'E'
N01  C                   MOVE      'O'           FMST
N01  c                   endif
N01  C                   UPDATE    DOKUFMR
N01  C     DOKKY2        READE     DOKUFM                                 94
N01  C                   ENDDO
N01   *
N01  C                   ENDSR
N01   *
      ***********************************************
     C     INIT          BEGSR
      ***********************************************
     C     *ENTRY        PLIST
     C                   PARM                    UAVD              4 0
     C                   PARM                    UOPD              7 0
     C                   PARM                    UFBNR             3 0
     C                   PARM                    ULINR             2 0
     C                   PARM                    UVIS              1
      *
N01  C                   move      UAVD          iAVD              4 0
N01  C                   move      UOPD          iOPD              7 0
N01  C                   move      UFBNR         iFBNR             3 0
      *
     C     UVIS          COMP      'J'                                    69
     C     *LIKE         DEFINE    FMLONR        ULONR
     C     *LIKE         DEFINE    FMRI          URI
     C                   Z-ADD     1             ULONR
     C                   MOVE      'F'           URI
      *
      *--------------------------------------------------------------
      * DEFINE KEY FOR DOKUFM
      *--------------------------------------------------------------
      *
N01  C     DOKKEY        KLIST
N01  C                   KFLD                    URI
N01  C                   KFLD                    UAVD
N01  C                   KFLD                    UOPD
N01  C                   KFLD                    UFBNR
     C     DOKKYM        KLIST
     C                   KFLD                    URI
     C                   KFLD                    UAVD
     C                   KFLD                    UOPD
     C                   KFLD                    UFBNR
     C                   KFLD                    ULINR
     C                   KFLD                    SFMKNR
     C                   KFLD                    SFMLNR
     C     DOKKY2        KLIST
     C                   KFLD                    URI
     C                   KFLD                    UAVD
     C                   KFLD                    UOPD
     C                   KFLD                    UFBNR
     C                   KFLD                    ULINR
     C     MaxKey        KLIST
     C                   KFLD                    URI
     C                   KFLD                    UAVD
     C                   KFLD                    UOPD
     C                   KFLD                    UFBNR
     C                   KFLD                    ULINR
     C                   KFLD                    XXMAX
     C                   move      99999         XXMAX             5 0
      *
N01  C                   move      'N'           DUPMODE           1
      *
     C                   ENDSR
      *
     ODOKUFMR   E            DUMFM
**
*** SLETTET ***
