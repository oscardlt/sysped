SF7  A* FAKTNR 7
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPDOC('STD' 'HELPNU' 'HLP400')
     A                                      HELP
     A                                      ALTHELP
     A          R BILDE01
     A                                      CF03(03 'Avslutt')
     A                                  1  2'SYSPED/SYAM27'
     A                                      COLOR(BLU)
     A                                  1 27MSGCON(033 DIV0278 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 73DATE
     A                                      EDTCDE(Y)
     A                                  2 73TIME
     A                                  3  2'Velg og trykk Enter:'
     A                                      COLOR(BLU)
     A                                  6  2MSGCON(028 DIV0225 *LIBL/SYMSGF)
     A            WSAVD          4Y 0B  6 33DSPATR(UL)
     A  99                                  DSPATR(PC)
     A  99                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                  6 44MSGCON(024 DIV0279 *LIBL/SYMSGF)
     A                                  7  4MSGCON(024 DIV0280 *LIBL/SYMSGF)
     A            WSKN           8Y 0B  7 33DSPATR(UL)
     A  98                                  DSPATR(PC)
     A  98                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                  7 44MSGCON(024 DIV0279 *LIBL/SYMSGF)
     A                                  8  4MSGCON(028 DIV0281 *LIBL/SYMSGF)
     A            WSPRO          8Y 0B  8 33DSPATR(UL)
     A  97                                  DSPATR(PC)
     A  97                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                  8 44MSGCON(024 DIV0279 *LIBL/SYMSGF)
     A                                  9  4MSGCON(028 DIV3291 *LIBL/SYMSGF)
     A            WSDATE         6Y 0B  9 33EDTCDE(4)
     A                                  9 44MSGCON(024 DIV3292 *LIBL/SYMSGF)
     A                                 12  4MSGCON(024 DIV0282 *LIBL/SYMSGF)
     A            WSPER          4Y 0B 12 33DSPATR(UL)
     A  96                                  DSPATR(PC)
     A  96                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A          R BILDE02
     A                                      CF03(03 'Avslutt')
     A                                  2  2'SYSPED/SYAM27'
     A                                      COLOR(BLU)
     A            MSGX01        22A  O  2 58MSGID(DIV 0047 SYMSGF)
     A                                      COLOR(TRQ)
     A            MSGX02        33A  O  4 11MSGID(DIV 0278 SYMSGF)
     A                                      COLOR(WHT)
     A            MSGX03        24A  O  9 11MSGID(DIV 0283 SYMSGF)
SF7  A*           WSFN           6Y 0B  9 37DSPATR(UL)
NF7  A            WSFN           7Y 0B  9 37DSPATR(UL)
     A  99                                  DSPATR(PC)
     A  99                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            MSGX04        18A  O 23 11MSGID(DIV 0020 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
