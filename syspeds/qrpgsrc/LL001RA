      *
      ***********************************************************
      *** PROGRAM SKAL VEDLIKEHOLDE TOLL-TARIFF               ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01 02 07-11 13-24                ***
      *** LEDIGE INDIKATORER 27-29 31 32 34-39 41-84 95-97    ***
      ***********************************************************
      *
     FLLTARF    UF A E           K DISK
     FKODTS2    IF   E           K DISK
     FKODTSE    IF   E           K DISK
     FKODTSB    IF   E           K DISK
     FKODTSC    IF   E           K DISK
     FTARI      IF   E           K DISK
     FLL001DA   CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               382    383B 0
     D  WSSUBN               378    379B 0
      *
     C                   EXSR      INIT
      *
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     LTKEY1        SETLL     LLTARF
     C     LTKEY1        READE(N)  LLTARF                                 94
     C     *IN94         DOWEQ     '0'
     C                   ADD       1             ZRECNO
     C                   WRITE     SUBFIL
     C     LTKEY1        READE(N)  LLTARF                                 94
     C  N94              ENDDO
     C                   Z-ADD     ZRECNO        YRECNO            4 0
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     2             POSNBR
      *
     C     START3        TAG
     C                   SETOFF                                       9998
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYWRK
     C                   ELSE
     C                   WRITE     BUNN
     C                   EXFMT     SUBCTL
     C                   END
      *
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C                   Z-ADD     WSSUBN        XRECNO            4 0
      *
     C                   SETOFF                                       9830
      *** AVSLUTT
     C   03
     COR 12              GOTO      SLUTT
      *** FRISK OPP
     C   05              GOTO      START
      *** LAGE NY
     C   06              DO
     C                   EXSR      MOVB2
     C                   GOTO      START5
     C                   END
      *
     C     ZRECNO        IFNE      0
     C                   EXSR      HENT
     C                   END
     C   98XRECNO        IFNE      0
     C                   Z-ADD     XRECNO        ZRECNO
     C                   END
     C                   GOTO      START3
      *
     C     START5        TAG
     C                   EXFMT     BILDE01
     C                   MOVEA     '000000'      *IN(85)
     C                   MOVE      '0'           *IN30
      *
     C   04              DO
     C                   EXSR      SØKDTA
     C                   GOTO      START5
     C                   END
     C   12              EXSR      MOVB2
     C  N12              EXSR      TESTB1
     C  N12
     CAN 30              GOTO      START5
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     2             POSNBR
     C                   GOTO      START3
      *
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
      *
     C***********************************************
     C     TESTB1        BEGSR
     C***********************************************
     C  N40              DO
     C     KODS2K        CHAIN     KODTS2                             90
     C   90              MOVE      'SPL0001'     MSGNR
     C   90              SETON                                        30
     C  N90LTKEY2        CHAIN(N)  LLTARF                             33
     C  N90
     CANN33              DO
     C                   MOVE      'YBC1836'     MSGNR
     C                   SETON                                        3090
     C                   END
     C   90              Z-ADD     3             LINNBR
     C   90              Z-ADD     13            POSNBR
     C                   END
      *
     C     WSLK          IFEQ      'NO'
     C                   MOVEL     WSTANR        TATANR
     C     TATANR        SETLL     TARI
     C     TATANR        READE     TARI                                   89
     C                   ELSE
     C     WSTANR        COMP      *BLANKS                                89
     C                   END
     C  N30*IN89         IFEQ      '1'
     C                   MOVE      'SPT0033'     MSGNR
     C                   Z-ADD     4             LINNBR
     C                   Z-ADD     13            POSNBR
     C                   SETON                                        30
     C                   END
      *
     C     WSFYL         IFNE      *BLANKS
     C     WSFYL         CHAIN     KODTSE                             88
     C  N30*IN88         IFEQ      '1'
     C                   MOVE      'YBC0120'     MSGNR
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     13            POSNBR
     C                   SETON                                        30
     C                   END
     C                   END
      *
     C     WSCRE         IFNE      *BLANKS
     C     WSCRE         CHAIN     KODTSC                             87
     C  N30*IN87         IFEQ      '1'
     C                   MOVE      'SPC0006'     MSGNR
     C                   Z-ADD     7             LINNBR
     C                   Z-ADD     13            POSNBR
     C                   SETON                                        30
     C                   END
     C                   END
      *
     C     WSCRI         IFNE      *BLANKS
     C     WSCRI         CHAIN     KODTSB                             86
     C  N30*IN86         IFEQ      '1'
     C                   MOVE      'SPC0006'     MSGNR
     C                   Z-ADD     7             LINNBR
     C                   Z-ADD     28            POSNBR
     C                   SETON                                        30
     C                   END
     C                   END
      *
     C     WSLKOP        IFNE      *BLANKS
     C     KODS2O        CHAIN     KODTS2                             85
     C  N30*IN85         IFEQ      '1'
     C                   MOVE      'SPL0001'     MSGNR
     C                   Z-ADD     8             LINNBR
     C                   Z-ADD     13            POSNBR
     C                   SETON                                        30
     C                   END
     C                   END
      *
     C   30              GOTO      SLTB1
      *
      *
      *** OPPDATERE PÅ REGISTER OG SUBFIL
     C     LTKEY2        CHAIN     LLTARF                             33
     C  N33
     CANN40              GOTO      SLTB1
      *
     C                   EXSR      MOVWLT
     C  N33              UPDATE    LLTARFR
     C   33              WRITE     LLTARFR
      *
     C   40              UPDATE    SUBFIL
     C  N40              ADD       1             YRECNO
     C  N40              Z-ADD     YRECNO        ZRECNO
     C  N40              WRITE     SUBFIL
     C                   WRITE     SUBCTL
     C                   EXSR      MOVB2
      *
     C     SLTB1         ENDSR
      *
     C***********************************************
     C     HENT          BEGSR
     C***********************************************
     C                   Z-ADD     0             LINNBR
     C                   SETOFF                                       919299
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C   98              DO
     C   99              SETOFF                                       98
     C                   Z-ADD     1             LINNBR
     C                   GOTO      SLHENT
     C                   END
      *
     C     ZRECNO        DIV       15            LINNBR
     C                   MVR                     XLIN              2 0
     C     XLIN          IFEQ      0
     C                   Z-ADD     15            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
      *
     C     WSNR          IFEQ      '2'
     C                   MOVE      ' '           WSNR
     C                   EXSR      CHGTAR
     C   12              GOTO      SLHENT
     C                   END
      *
     C                   END
      *
     C     SLHENT        TAG
     C                   ADD       4             LINNBR
     C                   Z-ADD     2             POSNBR
     C                   SETOFF                                       99
     C   98              DO
     C                   MOVE      'DIV0012'     MSGNR
     C                   SETON                                            30
     C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     CHGTAR        BEGSR
     C***********************************************
     C                   EXSR      MOVLTW
     C                   SETON                                        99
     C     STCHG         TAG
     C                   EXFMT     BILDE01
     C                   MOVEA     '000000'      *IN(85)
     C                   MOVE      '0'           *IN30
      *
     C   04              DO
     C                   EXSR      SØKDTA
     C                   GOTO      STCHG
     C                   END
     C   12              DO
     C                   UPDATE    SUBFIL
     C                   EXSR      MOVB2
     C                   END
     C  N12              EXSR      TESTB1
     C  N12
     CAN 30              GOTO      STCHG
     C     XLIN          IFEQ      0
     C                   Z-ADD     9             LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     MOVWLT        BEGSR
     C***********************************************
     C                   MOVE      UGRUP         LTGRUP
     C                   MOVE      UPNR          LTPNR
     C                   MOVE      UANR          LTANR
     C                   MOVE      WSLK          LTLK
     C                   MOVE      WSTANR        LTTANR
     C                   MOVE      WSFYL         LTFYL
     C                   MOVE      WSTO          LTTO
     C                   MOVE      WSCRI         LTCRI
     C                   MOVE      WSCRE         LTCRE
     C                   MOVE      WSLKOP        LTLKOP
     C                   MOVE      WSMOT         LTMOT
      *
     C                   ENDSR
      *
     C***********************************************
     C     MOVLTW        BEGSR
     C***********************************************
     C                   MOVE      LTLK          WSLK
     C                   MOVE      LTTANR        WSTANR
     C                   MOVE      LTFYL         WSFYL
     C                   MOVE      LTTO          WSTO
     C                   MOVE      LTCRI         WSCRI
     C                   MOVE      LTCRE         WSCRE
     C                   MOVE      LTLKOP        WSLKOP
     C                   MOVE      LTMOT         WSMOT
     C                   SETON                                        40
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     13            POSNBR
      *
     C                   ENDSR
      *
     C***********************************************
     C     MOVB2         BEGSR
     C***********************************************
     C                   SETOFF                                       40
     C                   MOVE      *BLANKS       WSLK
     C                   MOVE      *BLANKS       WSTANR
     C                   MOVE      *BLANKS       WSFYL
     C                   MOVE      *BLANKS       WSTO
     C                   MOVE      *BLANKS       WSCRI
     C                   MOVE      *BLANKS       WSCRE
     C                   MOVE      *BLANKS       WSLKOP
     C                   MOVE      *BLANKS       WSMOT
     C                   Z-ADD     3             LINNBR
     C                   Z-ADD     13            POSNBR
      *
     C                   ENDSR
      *
     C***********************************************
     C     SØKDTA        BEGSR
     C***********************************************
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
      *
     C     LINNBR        IFEQ      3
     C     POSNBR        ANDGE     13
     C     POSNBR        ANDLE     14
     C                   CALL      'SYFI10L'
     C                   PARM                    WSLK
     C                   PARM                    USPED
     C                   Z-ADD     4             LINNBR
     C                   Z-ADD     13            POSNBR
     C                   GOTO      SLSØK
     C                   END
      *
     C     LINNBR        IFEQ      5
     C     POSNBR        ANDGE     13
     C     POSNBR        ANDLE     14
     C                   CALL      'SAD052H'
     C                   PARM                    WSFYL
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     13            POSNBR
     C                   GOTO      SLSØK
     C                   END
      *
     C     LINNBR        IFEQ      7
     C     POSNBR        ANDGE     13
     C     POSNBR        ANDLE     15
     C                   MOVE      'N'           UIMP              1
     C                   CALL      'SAD061K'
     C                   PARM                    WSCRE
     C                   PARM                    UIMP
     C                   Z-ADD     7             LINNBR
     C                   Z-ADD     28            POSNBR
     C                   GOTO      SLSØK
     C                   END
      *
     C     LINNBR        IFEQ      7
     C     POSNBR        ANDGE     28
     C     POSNBR        ANDLE     30
     C                   MOVE      'J'           UIMP              1
     C                   CALL      'SAD061K'
     C                   PARM                    WSCRI
     C                   PARM                    UIMP
     C                   Z-ADD     8             LINNBR
     C                   Z-ADD     13            POSNBR
     C                   GOTO      SLSØK
     C                   END
      *
     C     LINNBR        IFEQ      8
     C     POSNBR        ANDGE     13
     C     POSNBR        ANDLE     14
     C                   CALL      'SYFI10L'
     C                   PARM                    WSLKOP
     C                   PARM                    USPED
     C                   GOTO      SLSØK
     C                   END
      *
     C                   MOVE      'DIV1037'     MSGNR
     C                   SETON                                        30
      *
     C     SLSØK         ENDSR
      *
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     LTKEY1        KLIST
     C                   KFLD                    UGRUP
     C                   KFLD                    UPNR
     C                   KFLD                    UANR
     C     LTKEY2        KLIST
     C                   KFLD                    UGRUP
     C                   KFLD                    UPNR
     C                   KFLD                    UANR
     C                   KFLD                    WSLK
     C     KODS2K        KLIST
     C                   KFLD                    KS2UNI
     C                   KFLD                    WSLK
     C     KODS2O        KLIST
     C                   KFLD                    KS2UNI
     C                   KFLD                    WSLKOP
      *
     C     *ENTRY        PLIST
     C                   PARM                    UGRUP             8 0
     C                   PARM                    UPNR              8 0
     C                   PARM                    UANR             15
      *
     C                   MOVEL     'S2'          KS2UNI
     C                   MOVE      'N'           USPED             1
     C                   WRITE     WINDOWSZ
      *
     C                   ENDSR
      *
