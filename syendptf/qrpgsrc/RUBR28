********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10205 * 01 YBC PTF10 INNPARAMETER ULAG = 5 --> IMPORT OG IKKE SUMMER BELØP
10XXX * 02 YBC PTF10 SETT CURSOR PÅ VALUTAKODE ETTER SYGE11
xxxxxx*xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
11XXX * 03 YBC PTF11 TILBAKESTILLE EDI-FAKTURA VED SLETTING
11132 * 04 YBC PTF11 3 DESIMALER PÅ KALKULATOR
12XXX * 05 YBC PTF12 TEST FAKTURANR
12XXX * 06 SH  PTF12 kasn ta F2 umiddelbart
12158 * 07 YBC PTF12 Sjekk om første versjon skal brukes
12318 * 08 CB  PTF12 FAKTURANUMMER ØKES FRA 17 TIL 21 TEGN
12318 * 09 CB  PTF12 vis/ikke vis 21 langt fakturanummer
12364 * 10 CB  PTF12 Bedre duplikattest på 21 lange fakturanummer Import
12365 * 11 CB  PTF12 Faktura duplikatkontroll eksport
12368 * 12 JOV PTF12 ved NY: Første 17 av fakt-nr (mm) være unikt/hindre duplikat key ..
********************************************************************
      *
      ***********************************************************
      *** PROGRAM SKAL REGISTRERE RUBR.28 PÅ SAD-IMPORT       ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01-04 07 09-11 13-24             ***
      *** LEDIGE INDIKATORER 28 29 31 32 34-39 41-86 95-97    ***
      ***********************************************************
      *
     FSADF      UF A E           K DISK
N08  FSADFFA    UF A E           K DISK
N10  FSADFFA1   IF   E           K DISK    RENAME(SADFFAR:SADFFAR1)
N10  F                                     PREFIX(X_)
     FZADHL02   IF   E           K DISK
     FZAEHL02   IF   E           K DISK
     FZADFL02   UF   E           K DISK    RENAME(SADFR:ZADFR)
N03  FZADVL03   UF   E           K DISK    RENAME(SADVR:ZADVR3)
     FKODTVAL2  IF   E           K DISK
     FFIRM      IF   E           K DISK
     FRUBR28FM  CF   E             WORKSTN SFILE(SUBFIL:ZRECNO)
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSSUBN               378    379B 0
     D  WSCLOC               382    383B 0
     D                 DS
     D  SFDTÅ                  1      4  0
     D  SFDTM                  5      6  0
     D  SFDTD                  7      8  0
     D  SFDT                   1      8  0
     D                 DS
     D  WSDTD                  1      2  0
     D  WSDTM                  3      4  0
     D  WSDTÅ                  5      6  0
     D  WSDT                   1      6  0
     D                 DS
     D  W2DTD                  1      2  0
     D  W2DTM                  3      4  0
     D  W2DTÅ                  5      6  0
     D  W2DT                   1      6  0
     D                 DS
     D  YDTÅ                   1      4  0
     D  YDTM                   5      6  0
     D  YDTD                   7      8  0
     D  YDT                    1      8  0
     D                 DS
     D  W28DTD                 1      2  0
     D  W28DTM                 3      4  0
     D  W28DTÅ                 5      6  0
     D  W28DT                  1      6  0
     D                 DS
     D  Y28DTÅ                 1      4  0
     D  Y28DTM                 5      6  0
     D  Y28DTD                 7      8  0
     D  Y28DT                  1      8  0
     D                 DS
     D  SFTXT                  1     17
     D  XDLT                   1      7
N05  D SAVUDS         UDS
N05  D  UUDSDTA                1   1024
N05  D  UUDSRG               301    317
     IZADFR
     I              SFAVD                       ZFAVD
     I              SFOPDN                      ZFOPDN
     I              SFTXT                       ZFTXT
     I              SFDT                        ZFDT
     I              SFBL28                      ZFBL28
     I              SFVK28                      ZFVK28
     I              SFOM28                      ZFOM28
     I              SFXXX                       ZFXXX
     I              SFUNIK                      ZFUNIK
     I              SFREFF                      ZFREFF
     C*
     C                   EXSR      INIT
     C*
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
     C                   MOVE      W2TXT         YTXT             17
     C                   MOVE      W2DTD         YDTD
     C                   MOVE      W2DTM         YDTM
     C                   MOVE      W2DTÅ         YDTÅ
     C     W2DTÅ         IFGT      50
     C                   MOVEL     '19'          YDTÅ
     C                   ELSE
     C                   MOVEL     '20'          YDTÅ
     C                   END
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     SADF2K        SETLL     SADF
     C     START1        TAG
     C     SAD21K        READE(N)  SADF                                   94
     C  N94              DO
     C                   MOVE      SFTXT         YTXT
     C                   MOVE      SFDT          YDT
     C                   END
     C   94              DO
     C                   MOVE      *BLANKS       SFTXT
N08  C                   MOVE      *BLANKS       SFAFTXT21
     C                   MOVE      *ZEROS        SFDT
     C                   MOVE      *BLANKS       WSNR
     C                   END
      *
     C     START2        TAG
      *
     C  N94              DO
     C                   DO        6
     C                   ADD       1             ZRECNO
     C                   MOVE      SFVK28        WSVK28
     C                   MOVE      SFDTD         WSDTD
     C                   MOVE      SFDTM         WSDTM
     C                   MOVE      SFDTÅ         WSDTÅ
N08  C                   IF        UFLAG = 0
N08  C     SADFKFA       CHAIN(N)  SADFFA
N08  C                   IF        %FOUND
N08  C                   ELSE
N08  C                   EVAL      SFAFTXT21=*BLANKS
N08  C                   ENDIF
N08  C                   ENDIF
     C                   WRITE     SUBFIL
     C     SAD21K        READE(N)  SADF                                   94
     C  N94              DO
     C                   MOVE      SFTXT         YTXT
     C                   MOVE      SFDT          YDT
     C                   END
     C   94              DO
     C                   MOVE      *BLANKS       SFTXT
N08  C                   MOVE      *BLANKS       SFAFTXT21
     C                   MOVE      *ZEROS        SFDT
     C                   MOVE      *BLANKS       WSNR
     C                   END
     C  N94              END
     C                   END
     C*
     C                   Z-ADD     ZRECNO        YRECNO            5 0
     C     ZRECNO        IFEQ      0
     C                   WRITE     NYWRK
     C                   Z-ADD     13            LINNBR
     C                   Z-ADD     5             POSNBR
     C                   MOVE      *BLANKS       W28TXT
N08  C                   MOVE      *BLANKS       W28TXT21
     C                   MOVE      *ZEROS        W28DT
N09  C                   EVAL      *IN32=*OFF
     C                   GOTO      START5
     C                   ELSE
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     2             POSNBR
     C                   END
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYWRK
     C                   ELSE
     C                   WRITE     BUNN
     C                   EXFMT     SUBCTL
     C                   END
     C*
     C***
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C                   Z-ADD     WSSUBN        XRECNO            4 0
     C     XRECNO        IFEQ      0
     C                   Z-ADD     ZRECNO        XRECNO            4 0
     C                   END
     C***
     C*
     C                   SETOFF                                       989930
      *
N06  C     startf2       tag
     C   02              DO
     C                   CALL      'SAD031'
     C                   PARM                    UAVD
     C                   PARM                    UTDN
     C                   PARM                    UIMP
     C                   GOTO      START
     C                   END
     C*
     C   12              GOTO      SLUTT
     C*** FRISK OPP
     C   05              GOTO      START
     C*** LAGE NY
     C   06              DO
     C                   Z-ADD     13            LINNBR
     C                   Z-ADD     5             POSNBR
     C                   MOVE      *BLANKS       W28TXT
N08  C                   MOVE      *BLANKS       W28TXT21
     C                   MOVE      *ZEROS        W28DT
N09  C                   EVAL      *IN32=*OFF
     C                   GOTO      START5
     C                   END
      *
     C   07              DO
     C                   EXFMT     BEKREFT
     C   12              GOTO      START3
N07   * SJEKK OM FØRSTE VERSJON HAR FÅTT TILBAKE MELDING. I SÅ FALL AVBRYTE JOBB
N07  C                   EXSR      SRTSTOPD
N07   *
     C     ZADF2K        SETLL     SADF
     C     ZADF2K        READE     SADF                                   33
     C     *IN33         DOWEQ     '0'
     C                   DELETE    SADFR
     C     ZADF2K        READE     SADF                                   33
     C  N33              ENDDO
     C     ZADF2K        SETLL     ZADFL02
     C     ZADF2K        READE(N)  ZADFL02                                33
     C     *IN33         DOWEQ     '0'
     C                   MOVE      ZFAVD         SFAVD
     C                   MOVE      ZFOPDN        SFOPDN
     C                   MOVE      ZFTXT         SFTXT
     C                   MOVE      ZFDT          SFDT
     C                   MOVE      ZFBL28        SFBL28
     C                   MOVE      ZFVK28        SFVK28
     C                   MOVE      ZFOM28        SFOM28
     C                   MOVE      ZFXXX         SFXXX
     C                   WRITE     SADFR
     C     ZADF2K        READE(N)  ZADFL02                                33
     C  N33              ENDDO
     C                   GOTO      START
     C                   ENDDO
     C*
     C*** BLA VIDERE
     C   25              DO
     C                   Z-ADD     YRECNO        ZRECNO
     C                   SETOFF                                       25
     C  N94SADF3K        SETLL     SADF
     C  N94SAD21K        READE(N)  SADF                                   94
     C                   GOTO      START2
     C                   END
      *
     C     W2NR          IFEQ      '1'
     C                   Z-ADD     13            LINNBR
     C                   Z-ADD     5             POSNBR
     C                   MOVE      *BLANKS       W28TXT
N08  C                   MOVE      *BLANKS       W28TXT21
     C                   MOVE      *ZEROS        W28DT
N09  C                   EVAL      *IN32=*OFF
     C                   GOTO      START5
     C                   ELSE
     C     W2TXT         CABNE     *BLANKS       START
     C                   END
      *
     C     ZRECNO        IFNE      0
     C                   EXSR      HENT
     C                   END
     C   98XRECNO        IFNE      0
     C                   Z-ADD     XRECNO        ZRECNO
     C                   END
     C                   GOTO      START3
     C*
     C*** LAGE NY
     C     START5        TAG
     C                   EXFMT     VARELI
N09  C                   IF        *IN08
N09  C                   IF        *IN32
N09  C                   EVAL      *IN32=*OFF
N09  C                   ELSE
N09  C                   EVAL      *IN32=*ON
N09  C                   ENDIF
N09  C                   GOTO      START5
N09  C                   ENDIF
N06  C   02              goto      startf2
     C                   MOVEA     '0000'        *IN(87)
     C                   SETOFF                                       30
     C     *IN04         IFEQ      '1'
S04  C*                  Z-ADD     WSBL28        UNU132           13 2
N04  C                   Z-ADD     WSBL28        UNU133           13 3
     C                   CALL      'SYGE11'
S04  C*                  PARM                    UNU132
N04  C                   PARM                    UNU133
S04  C*                  Z-ADD     UNU132        WSBL28
N04  C                   Z-ADD(H)  UNU133        WSBL28
N02  C                   Z-ADD     13            LINNBR
N02  C                   Z-ADD     47            POSNBR
     C                   GOTO      START5
     C                   END
     C     *IN12         IFEQ      '1'
     C                   EXSR      MOVB2
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     2             POSNBR
     C                   GOTO      START3
     C                   ELSE
     C                   EXSR      TESTB1
     C     *IN30         IFEQ      '0'
     C                   EXSR      OPDSDF
     C                   MOVEA     '11'          *IN(91)
     C                   WRITE     SUBCTL
     C                   Z-ADD     13            LINNBR
     C                   Z-ADD     5             POSNBR
     C                   END
     C                   GOTO      START5
     C                   END
     C*
     C     SLUTT         TAG
     C                   EXSR      SUM28
     C                   SETON                                        LR
     C                   RETURN
     C*
     C***********************************************
     C     TESTB1        BEGSR
     C***********************************************
     C     W28TXT        IFEQ      *BLANKS
     C                   MOVE      'SPF0001'     MSGNR
     C                   Z-ADD     5             POSNBR
     C                   SETON                                        9030
N05  C                   ELSE
N05  C                   IF        UFLAG = 0
N10  C                   IF        W28TXT21=*BLANKS
N05  C                   CALL      'SAD061W'
N05  C                   PARM      UAVD          XAVD              4 0
N05  C                   PARM      UTDN          XTDN              7 0
N05  C                   PARM      UUDSRG        URG              17
N05  C                   PARM                    W28TXT
N05  C                   PARM      W28DT         XFID              6 0
N05  C                   PARM                    UFLAGG            1 0
N05  C                   IF        UFLAGG <> 0
N05  C                   Z-ADD     5             POSNBR
N05  C                   SETON                                        9030
N05  C                   END
N10  C                   ELSE
N10  C                   CALL      'SAD061W21'
N10  C                   PARM      UAVD          XAVD              4 0
N10  C                   PARM      UTDN          XTDN              7 0
N10  C                   PARM      UUDSRG        URG              17
N10  C                   PARM                    W28TXT
N10  C                   PARM                    W28TXT21
N10  C                   PARM      W28DT         XFID              6 0
N10  C                   PARM                    UFLAGG            1 0
N10  C                   IF        UFLAGG <> 0
N10  C                   Z-ADD     5             POSNBR
N10  C                   SETON                                        9030
N10  C                   END
N10  C                   ENDIF
N05  C                   END
N11  C                   IF        UIMP='N'
N11  C                   CALL      'SAD052W'
N11  C                   PARM      UAVD          XAVD              4 0
N11  C                   PARM      UTDN          XTDN              7 0
N11  C                   PARM      UUDSRG        URG              17
N11  C                   PARM                    W28TXT
N11  C                   PARM      W28DT         XFID              6 0
N11  C                   PARM                    UFLAGG            1 0
N11  C                   IF        UFLAGG <> 0
N11  C                   Z-ADD     5             POSNBR
N11  C                   SETON                                        9030
N11  C                   END
N11  C                   ENDIF
     C                   END
     C*
     C     W28DT         IFEQ      0
     C                   SETON                                        89
     C                   ELSE
     C                   MOVE      W28DTD        XDAG              2 0
     C                   MOVE      W28DTM        XMND              2 0
     C                   MOVE      W28DTÅ        XÅR               2 0
     C                   CALL      'TESTDATO'
     C                   PARM                    XDAG
     C                   PARM                    XMND
     C                   PARM                    XÅR
     C                   PARM                    UFLAG
     C     UFLAG         COMP      1                                      89
     C                   END
     C     *IN89         IFEQ      '0'
     C                   MOVE      W28DTD        Y28DTD
     C                   MOVE      W28DTM        Y28DTM
     C                   MOVE      W28DTÅ        Y28DTÅ
     C     W28DTÅ        IFGT      50
     C                   MOVEL     '19'          Y28DTÅ
     C                   ELSE
     C                   MOVEL     '20'          Y28DTÅ
     C                   END
     C     Y28DT         COMP      WDT80                              89
     C                   END
     C   89
     CANN30              DO
     C                   MOVE      'SPD0001'     MSGNR
S08  C*                  Z-ADD     23            POSNBR
N08  C                   Z-ADD     28            POSNBR
     C                   SETON                                        30
     C                   END
      *
N12   * Ved NY (N40) gjenninnfør feilmeldingen (også ved 4 ekstra) men med mer presis tekst
     C  N40*IN30         IFEQ      '0'
S12  C*    W28TXT21      ANDEQ     *BLANKS
     C     SADFK         CHAIN(N)  SADF                               33
     C     *IN33         IFEQ      '0'
s10  C*                  MOVE      'YBC0466'     MSGNR
N12  C                   MOVE      'JOV1806'     MSGNR
     C                   Z-ADD     5             POSNBR
     C                   SETON                                        908930
N10  C*    UFLAG         IFEQ      0
N10  C*    W28TXT21      IFNE      *BLANKS
N10  C*    SADFKFA1      CHAIN     SADFFA1
N10  C*                  IF        %FOUND
N10  C*                  SETON                                        908930
N10  C*                  MOVE      'YBC0466'     MSGNR
N10  C*                  ELSE
N10  C*                  SETOFF                                       908930
N10  C*                  MOVE      *BLANKS       MSGNR
N10  C*                  ENDIF
N10  C*                  ENDIF
N10  C*                  ENDIF
     C                   END
     C                   END
     C   40
     CANN30W28TXT        IFNE      SFTXT
     C     Y28DT         ORNE      SFDT
     C                   MOVE      SFTXT         XXTXT            17
     C                   MOVE      SFDT          XXDT              8 0
     C     SADFK         CHAIN(N)  SADF                               33
     C                   MOVE      XXTXT         SFTXT
     C                   MOVE      XXDT          SFDT
     C     *IN33         IFEQ      '0'
     C                   MOVE      'YBC0466'     MSGNR
     C                   Z-ADD     5             POSNBR
     C                   SETON                                        908930
     C                   END
     C                   END
     C*
     C     WSVK28        IFEQ      *BLANKS
     C     WSBL28        ANDNE     0
     C                   MOVE      FICURR        WSVK28
     C                   END
     C     WSVK28        IFNE      *BLANKS
     C     KODVAK        SETGT     KODTVAL2
     C     WSVK28        READPE    KODTVAL2                               88
     C   88
     CANN30              DO
     C                   MOVE      'SPV0001'     MSGNR
S08  C*                  Z-ADD     46            POSNBR
N08  C                   Z-ADD     52            POSNBR
     C                   SETON                                        30
     C                   END
     C   30              GOTO      SLTB1
     C                   MOVE      KVAOMR        XOM28             3 0
     C     WSKR28        IFEQ      0
     C                   MOVE      KVAKRS        WSKR28
     C                   END
     C                   END
      *
     C  N30              IF        WSBL28 = 0
S08  C*                  Z-ADD     32            POSNBR
N08  C                   Z-ADD     37            POSNBR
     C                   MOVE      'SPB0007'     MSGNR
     C                   SETON                                        3087
     C                   END
      *
     C*
     C     SLTB1         ENDSR
     C*
     C***********************************************
     C     HENT          BEGSR
     C***********************************************
     C                   SETOFF                                       919299
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C   98              DO
     C   99              SETOFF                                       98
     C   98              MOVE      'DIV0012'     MSGNR
     C   98              SETON                                            30
     C                   Z-ADD     1             LINNBR
     C                   GOTO      SLHENT
     C                   END
     C     ZRECNO        DIV       6             LINNBR
     C                   MVR                     LINNBR
     C                   Z-ADD     LINNBR        XLIN              2 0
     C     XLIN          IFEQ      0
     C                   Z-ADD     6             LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C     WSNR          IFEQ      '2'
     C                   MOVE      ' '           WSNR
     C     XDLT          COMP      '*DELETE'                              98
     C   98              MOVE      'SPO0020'     MSGNR
     C   98              SETON                                            30
     C   98              GOTO      SLHENT
     C                   EXSR      CHGSDF
     C   12              GOTO      SLHENT
     C                   END
     C     WSNR          IFEQ      '4'
     C                   MOVE      ' '           WSNR
     C     XDLT          COMP      '*DELETE'                              98
     C   98              MOVE      'SPO0020'     MSGNR
     C   98              SETON                                            30
     C   98              GOTO      SLHENT
     C                   EXSR      DLTSDF
     C   12              GOTO      SLHENT
     C                   END
     C                   END
     C*
     C     SLHENT        TAG
     C                   ADD       5             LINNBR
     C                   Z-ADD     2             POSNBR
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     DLTSDF        BEGSR
     C***********************************************
     C                   SETON                                        99
     C                   MOVE      SFTXT         W28TXT
     C                   MOVE      SFDTÅ         W28DTÅ
     C                   MOVE      SFDTM         W28DTM
     C                   MOVE      SFDTD         W28DTD
     C                   EXFMT     VAREL3
     C  N12              DO
N07   * SJEKK OM FØRSTE VERSJON HAR FÅTT TILBAKE MELDING. I SÅ FALL AVBRYTE JOBB
N07  C                   EXSR      SRTSTOPD
N07   *
     C                   MOVE      SFDT          Y28DT
     C     SADFK         CHAIN     SADF                               33
     C  N33              DELETE    SADFR
     C     *IN28         IFEQ      '1'
     C     ZADF2K        SETLL     ZADFL02
     C     ZADF2K        READE     ZADFL02                                33
     C     *IN33         DOWEQ     '0'
     C     SFTXT         IFEQ      ZFTXT
     C                   MOVE      *ZEROS        ZFAVD
     C                   MOVE      *ZEROS        ZFOPDN
N03   * TILBAKESTILLE VARELINJE
N03  C     ZADF2K        SETLL     ZADVL03
N03  C     ZADF2K        READE     ZADVL03                                33
N03  C                   DOW       *IN33 = *OFF
N03  C                   IF        SFTXT = SVREFF
N03  C                   MOVE      *ZEROS        SVAVD
N03  C                   MOVE      *ZEROS        SVTDN
N03  C                   END
N03  C                   UPDATE    ZADVR3
N03  C     ZADF2K        READE     ZADVL03                                33
N03  C                   ENDDO
     C                   END
     C                   UPDATE    ZADFR
     C     ZFAVD         IFEQ      0
     C                   LEAVE
     C                   END
     C     ZADF2K        READE     ZADFL02                                33
     C  N33              ENDDO
     C                   END
     C                   EXSR      MOVB1
     C                   END
     C     XLIN          IFEQ      0
     C                   Z-ADD     6             LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C                   UPDATE    SUBFIL
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     CHGSDF        BEGSR
     C***********************************************
     C                   EXSR      MOVSDW
     C                   SETON                                        99
     C     STCHG         TAG
     C                   EXFMT     VARELI
     C                   MOVEA     '0000'        *IN(87)
     C                   SETOFF                                       30
N09  C                   IF        *IN08
N09  C                   IF        *IN32
N09  C                   EVAL      *IN32=*OFF
N09  C                   ELSE
N09  C                   EVAL      *IN32=*ON
N09  C                   ENDIF
N09  C                   GOTO      STCHG
N09  C                   ENDIF
     C     *IN04         IFEQ      '1'
S04  C*                  Z-ADD     WSBL28        UNU132           13 2
N04  C                   Z-ADD     WSBL28        UNU133           13 3
     C                   CALL      'SYGE11'
S04  C*                  PARM                    UNU132
N04  C                   PARM                    UNU133
S04  C*                  Z-ADD     UNU132        WSBL28
N04  C                   Z-ADD(H)  UNU133        WSBL28
N02  C                   Z-ADD     13            LINNBR
N02  C                   Z-ADD     47            POSNBR
     C                   GOTO      STCHG
     C                   END
     C   12              DO
     C                   UPDATE    SUBFIL
     C                   EXSR      MOVB2
     C                   END
     C  N12              EXSR      TESTB1
     C  N12
     CAN 30              GOTO      STCHG
     C  N12              EXSR      OPDSDF
     C     XLIN          IFEQ      0
     C                   Z-ADD     6             LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     OPDSDF        BEGSR
     C***********************************************
N07   * SJEKK OM FØRSTE VERSJON HAR FÅTT TILBAKE MELDING. I SÅ FALL AVBRYTE JOBB
N07  C                   EXSR      SRTSTOPD
N07   *
     C  N40              MOVE      '1'           *IN33
     C   40SADFKE        CHAIN     SADF                               33
     C                   EXSR      MOVWSD
     C  N33              UPDATE    SADFR
     C   33              WRITE     SADFR
N08  C                   IF        UFLAG = 0
N08  C                   IF        W28TXT21 <> *BLANKS
N08  C     SADFKFA       CHAIN     SADFFA
N08  C                   IF        %FOUND
N08  C                   EVAL      SFAFTXT21=W28TXT21
N08  C                   UPDATE    SADFFAR
N08  C                   ELSE
N08  C                   EVAL      SFAFAVD=SFAVD
N08  C                   EVAL      SFAFOPDN=SFOPDN
N08  C                   EVAL      SFAFTXT=SFTXT
N08  C                   EVAL      SFAFDT=SFDT
N08  C                   EVAL      SFAFTXT21=W28TXT21
N08  C                   WRITE     SADFFAR
N08  C                   ENDIF
N08  C                   ENDIF
N08  C                   ENDIF
     C*
     C                   MOVE      SFDTD         WSDTD
     C                   MOVE      SFDTM         WSDTM
     C                   MOVE      SFDTÅ         WSDTÅ
     C   40              UPDATE    SUBFIL
     C  N40              ADD       1             YRECNO
     C  N40              Z-ADD     YRECNO        ZRECNO
     C  N40              WRITE     SUBFIL
     C                   EXSR      MOVB2
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVSDW        BEGSR
     C***********************************************
     C                   MOVE      SFTXT         W28TXT
     C                   MOVE      SFDTÅ         W28DTÅ
     C                   MOVE      SFDTM         W28DTM
     C                   MOVE      SFDTD         W28DTD
     C                   MOVE      SFBL28        WSBL28
     C                   MOVE      SFVK28        WSVK28
     C                   MOVE      SFKR28        WSKR28
N09  C                   IF        UFLAG = 0
N09  C     SADFKFA       CHAIN(N)  SADFFA
N09  C                   IF        %FOUND
N09  C                   EVAL      W28TXT21=SFAFTXT21
N09  C                   EVAL      *IN32=*ON
N09  C                   ELSE
N09  C                   EVAL      W28TXT21=*BLANKS
N09  C                   EVAL      *IN32=*OFF
N09  C                   ENDIF
N09  C                   ENDIF
     C                   SETON                                        40
     C                   Z-ADD     13            LINNBR
S02  C*                  Z-ADD     32            POSNBR
N02  C                   Z-ADD     37            POSNBR
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVWSD        BEGSR
     C***********************************************
     C                   MOVE      UAVD          SFAVD
     C                   MOVE      UTDN          SFOPDN
     C   40W28TXT        IFNE      SFTXT
     C     Y28DT         ORNE      SFDT
     C                   DELETE    SADFR
     C                   MOVE      '1'           *IN33
     C                   END
     C                   MOVE      W28TXT        SFTXT
     C                   MOVE      Y28DT         SFDT
     C                   MOVE      WSBL28        SFBL28
     C                   MOVE      WSVK28        SFVK28
     C                   MOVE      WSKR28        SFKR28
     C                   MOVE      XOM28         SFOM28
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVB1         BEGSR
     C***********************************************
     C                   MOVE      *BLANKS       SFTXT
N08  C                   MOVE      *BLANKS       SFAFTXT21
     C                   MOVE      '*DELETE'     XDLT
     C                   MOVE      *ZEROS        SFDT
     C                   MOVE      *ZEROS        SFBL28
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVB2         BEGSR
     C***********************************************
     C                   SETOFF                                       40
     C                   MOVE      *BLANKS       W28TXT
N08  C                   MOVE      *BLANKS       W28TXT21
     C                   MOVE      *ZEROS        WSBL28
     C                   MOVE      *ZEROS        WSKR28
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     SUM28         BEGSR
     C***********************************************
N01  C     XIKKEOPD      CABEQ     'J'           SLS28
     C                   MOVE      *ZEROS        XNOK             13 2
     C                   MOVE      *ZEROS        XNOKI            13 2
     C                   MOVE      *BLANKS       XVAL              3
     C                   MOVE      *BLANKS       W28TXT
N08  C                   MOVE      *BLANKS       W28TXT21
     C                   MOVE      *ZEROS        Y28DT
     C     SADFK         SETLL     SADF
     C                   SETOFF                                       9089
     C*
     C     STS28         TAG
     C*          UAVD      READESADF                     90
     C* N90                UPDATSADFR
     C* N90      UTDN      COMP SFOPDN               9090
     C     SAD21K        READE(N)  SADF                                   90
     C  N90              DO
     C                   Z-ADD     1             UFLAG
     C     SFVK28        IFNE      *BLANKS
     C  N89XVAL          IFEQ      *BLANKS
     C                   MOVE      SFVK28        XVAL
     C                   MOVE      SFKR28        UKURS
     C                   ELSE
     C     XVAL          COMP      SFVK28                             8989
     C                   END
     C                   ADD       SFBL28        XNOKI
     C     SFBL28        DIV       SFOM28        XWORK            17 4
     C     XWORK         MULT(H)   SFKR28        XWORK
     C                   ADD(H)    XWORK         XNOK
     C                   END
     C                   GOTO      STS28
     C                   END
      *
     C     XNOK          IFNE      *ZEROS
     C   89              MOVE      FICURR        UVAL
     C   89              Z-ADD     100           UKURS
     C   89              Z-ADD     XNOK          USUM
     C                   END
      *
     C  N89XVAL          IFNE      *BLANKS
     C     XNOKI         ANDNE     *ZEROS
     C                   MOVE      XVAL          UVAL
     C                   Z-ADD     XNOKI         USUM
     C                   END
     C*
     C     SLS28         ENDSR
     C*
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     SADFK         KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    UTDN
     C                   KFLD                    W28TXT
     C                   KFLD                    Y28DT
     C     SADFKE        KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    UTDN
     C                   KFLD                    SFTXT
     C                   KFLD                    SFDT
     C     SADF2K        KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    UTDN
     C                   KFLD                    W2TXT
     C                   KFLD                    YDT
     C     SAD21K        KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    UTDN
     C     SADF3K        KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    UTDN
     C                   KFLD                    YTXT
     C                   KFLD                    YDT
     C     ZADF2K        KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    UTDN
     C     KODVAK        KLIST
     C                   KFLD                    WSVK28
     C                   KFLD                    UDT
N08  C     SADFKFA       KLIST
N08  C                   KFLD                    SFAVD
N08  C                   KFLD                    SFOPDN
N08  C                   KFLD                    SFTXT
N08  C                   KFLD                    SFDT
N10  C     SADFKFA1      KLIST
N10  C                   KFLD                    W28TXT
N10  C                   KFLD                    Y28DT
N10  C                   KFLD                    W28TXT21
     C*
     C     *ENTRY        PLIST
     C                   PARM                    UAVD              4 0
     C                   PARM                    UTDN              7 0
     C                   PARM                    USUM             13 2
     C                   PARM                    UVAL              3
     C                   PARM                    UKURS             7 3
     C                   PARM                    UFLAG             1 0
     C                   PARM                    UDT2              8
     C                   MOVE      UDT2          UDT               8 0
      *
N05  C     *DTAARA       DEFINE    *LDA          SAVUDS
N05  C                   OUT       SAVUDS
N01  C                   IF        UFLAG = 5
N01  C                   MOVE      'J'           XIKKEOPD          1
N01  C                   EVAL      UFLAG = 1
N01  C                   END
     C     UFLAG         IFEQ      0
     C                   MOVE      'J'           UIMP              1
     C     ZADF2K        CHAIN     ZADHL02                            28
     C                   ELSE
     C                   MOVE      'N'           UIMP
     C     ZADF2K        CHAIN     ZAEHL02                            28
     C                   END
     C                   MOVE      *ZEROS        UFLAG
     C                   MOVE      *ZEROS        UKURS
     C*
     C                   MOVE      *ZEROS        SFDT
     C                   MOVE      *ZEROS        WSDT
     C                   MOVE      *ZEROS        W2DT
     C                   MOVE      *ZEROS        YDT
     C                   MOVE      *ZEROS        W28DT
     C                   MOVE      *ZEROS        Y28DT
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVE      WDT           WDT80             8 0
     C*
     C     FIFIRM        SETLL     FIRM
     C                   READ      FIRM                                   90
     C                   SETOFF                                       90
      *
     C     ZADF2K        SETLL     ZADFL02
     C     ZADF2K        READE(N)  ZADFL02                                33
     C  N33              MOVE      '1'           *IN27
      *
     C                   WRITE     WINDOWSZ
     C*
     C                   ENDSR
     C*
N07   ***********************************************
N07  C     SRTSTOPD      BEGSR
N07   ***********************************************
N07  C                   IF        UTDN > 0
N07  C                   MOVE      UAVD          UAVD2             4
N07  C                   MOVE      UTDN          UTDN2             7
N07  C                   MOVE      '0'           UFLAG2            1
N07  C                   IF        UFLAG = 0
N07  C                   CALL      'SAD061Y'
N07  C                   PARM                    UAVD2
N07  C                   PARM                    UTDN2
N07  C                   PARM                    UFLAG2
N07  C                   ELSE
N07  C*                  CALL      'SAD052Y'
N07  C*                  PARM                    UAVD2
N07  C*                  PARM                    UTDN2
N07  C*                  PARM                    UFLAG2
N07  C                   END
N07  C                   MOVE      UFLAG2        UFLAGG
N07  C                   IF        UFLAGG = 9
N07  C                   EVAL      UFLAG = 9
N07  C                   EVAL      *INLR = *ON
N07  C                   RETURN
N07  C                   END
N07  C                   END
N07   *
N07  C                   ENDSR
N07   *
