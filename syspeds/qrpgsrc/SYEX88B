      *
      ****************************************************************
      *** PROGRAM SKAL VEDLI./SPØRRE PÅ TURENS INNHOLD             ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER         ***
      *** LEDIGE INDIKATORER 02 04 05 08-11 13-24                  ***
      *** LEDIGE INDIKATORER 28 29 34-61 64-90 95-97 99            ***
      *** INDIKATOR 27 PÅ --> KALLE FRA SYEX89   (SPØRRING PÅ TUR) ***
      ***           27 AV --> KALLE FRA SYEX88   (VEDL. TUR)       ***
      ****************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 JOV PTF10 ved visning kun import skip i TOT-telling / ikke oppdater TUR
10XXX * 02 sh  PTF10 ikke flytte bilnummer hvis blank
********************************************************************
      *
      *
     FTURER     UF   E           K DISK
     FHEADF     UF   E           K DISK
     FHEAD9     IF   E           K DISK
     F                                     RENAME(HEADFR:HEAD9R)
     FFRISOK    IF   E           K DISK
     FFORDEL    IF   E           K DISK
     FSYEX88FB  CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     D                 DS
     D  DSSOK                  1     35
     D  DSAVD                  1      4  0
     D  DSOPD                  5     11  0
     D  DSREST                12     35
     D                 DS
     D  DSNAAD                 1     30
     D  DSNAVN                 1     15
     D  DSSK                  16     16
     D  DSSTPO                17     30
     D                 DS
     D  TUBILN                 1     10
     D  TUBIL9                 1      9
     D                 DS
     D  HEVKT                  1      9  0
     D  W2VKT                  5      9  0
     D                 DS
     D  HEM3                   1      7  3
     D  W2M3                   3      6  2
     C*
     C                   EXSR      INIT
     C*
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '000'         *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     HEAD9K        SETLL     HEAD9
     C     START1        TAG
     C                   READ      HEAD9                                  94
     C  N94              DO                                                     1<-B
     C     HEPRO         IFLT      UPRO                                          2<-B
     C                   MOVE      HEAVD         YAVD
     C     HEAD9K        SETLL     HEAD9
     C                   GOTO      START1
     C                   END                                                     2<-E
     C     HEPRO         IFGT      UPRO                                          2<-B
     C     HEAVD         COMP      9999                                   94
     C   94              GOTO      START2
     C     HEAVD         ADD       1             YAVD
     C     HEAD9K        SETLL     HEAD9
     C                   GOTO      START1
     C                   END                                                     2<-E
     C   27HEUR          IFEQ      'A'                                           2<-B
     C     UIMPL         ANDNE     'X'
     C                   GOTO      START1
     C                   END                                                     2<-E
     C   27HEUR          IFEQ      'B'                                           2<-B
     C     UEXPL         ANDNE     'X'
     C                   GOTO      START1
     C                   END                                                     2<-E
     C   27HEUR          IFEQ      'C'                                           2<-B
     C     UIMPF         ANDNE     'X'
     C                   GOTO      START1
     C                   END                                                     2<-E
     C   27HEUR          IFEQ      'D'                                           2<-B
     C     UEXPF         ANDNE     'X'
     C                   GOTO      START1
     C                   END                                                     2<-E
     C                   END                                                    1<-E
      *
     C     START2        TAG
      *
     C  N94              DO                                                     1<-B
     C                   DO        7                                             2<-B
     C                   ADD       1             ZRECNO
     C*
     C*  SETT SAMMEN NAVN/ POSTNR STED
     C*
     C                   MOVE      *BLANKS       DSNAAD
     C                   MOVEL     HENAK         DSNAVN
     C                   MOVE      '/'           DSSK
     C                   MOVEL     HEADK3        DSSTPO
     C                   MOVEL     DSNAAD        SFNAVN
     C                   WRITE     SUBFIL
     C     ST21          TAG
     C                   READ      HEAD9                                  94
     C  N94              DO                                                       3<-B
     C     HEPRO         IFLT      UPRO                                            4<-B
     C                   MOVE      HEAVD         YAVD
     C     HEAD9K        SETLL     HEAD9
     C                   GOTO      ST21
     C                   END                                                       4<-E
     C     HEPRO         IFGT      UPRO                                            4<-B
     C     HEAVD         COMP      9999                                   94
     C   94              GOTO      ST22
     C     HEAVD         ADD       1             YAVD
     C     HEAD9K        SETLL     HEAD9
     C                   GOTO      ST21
     C                   END                                                       4<-E
     C   27HEUR          IFEQ      'A'                                             4<-B
     C     UIMPL         ANDNE     'X'
     C                   GOTO      ST21
     C                   END                                                       4<-E
     C   27HEUR          IFEQ      'B'                                             4<-B
     C     UEXPL         ANDNE     'X'
     C                   GOTO      ST21
     C                   END                                                       4<-E
     C   27HEUR          IFEQ      'C'                                             4<-B
     C     UIMPF         ANDNE     'X'
     C                   GOTO      ST21
     C                   END                                                       4<-E
     C   27HEUR          IFEQ      'D'                                             4<-B
     C     UEXPF         ANDNE     'X'
     C                   GOTO      ST21
     C                   END                                                       4<-E
     C                   END                                                      3<-E
     C  N94              END                                                     2<-E
     C                   END                                                    1<-E
     C     ST22          TAG
     C*
     C                   MOVE      ZRECNO        XRECNO            5 0
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYWRK
     C                   ELSE
     C                   WRITE     SUBCTL
     C                   EXFMT     BUNN
     C                   END
     C*
     C                   SETOFF                                       9830
     C   06              DO
     C                   EXSR      PLUKKE
     C  N01
     CANN03              Z-ADD     1             YAVD
     C  N01
     CANN03              GOTO      START
     C                   END
     C     *IN16         IFEQ      *ON
     C                   EXSR      HPLUKK
     C                   Z-ADD     1             YAVD
     C  N03              GOTO      START
     C                   ENDIF                                                  <*IN16
     C   01              Z-ADD     1             UFLAGG
     C   01              GOTO      SLUTT
     C   03              Z-ADD     1             UFLAGG
     C   03              GOTO      SLUTT
     C   12              Z-ADD     9             UFLAGG
     C   12              GOTO      SLUTT
     C*
     C*** BLA VIDERE
     C   25              DO
     C                   Z-ADD     XRECNO        ZRECNO
     C                   SETOFF                                       25
     C                   GOTO      START2
     C                   END
     C*
     C     ZRECNO        IFNE      0
     C                   EXSR      TESTIN
     C                   END
     C                   GOTO      START3
     C*
     C     SLUTT         TAG
     C                   EXSR      OPDPOS
     C                   SETON                                        LR
     C                   RETURN
     C*
     C***********************************************
     C     CALTOT        BEGSR
     C***********************************************
     C     HEAD9K        SETLL     HEAD9
     C                   SETOFF                                       33
     C     STCAL         TAG
     C                   READ      HEAD9                                  33
     C  N33              DO
     C     HEPRO         IFLT      UPRO
     C                   MOVE      HEAVD         YAVD
     C     HEAD9K        SETLL     HEAD9
     C                   GOTO      STCAL
     C                   END
     C     HEPRO         IFGT      UPRO
     C     HEAVD         IFEQ      9999
     C                   GOTO      SLCAL
     C                   END
     C     HEAVD         ADD       1             YAVD
     C     HEAD9K        SETLL     HEAD9
     C                   GOTO      STCAL
     C                   END
N01  C* skip i telling dersom spørring / aktuell type ikke med
N01  C   27HEUR          IFEQ      'A'                                           2<-B
N01  C     UIMPL         ANDNE     'X'
N01  C                   GOTO      Skipp
N01  C                   END                                                     2<-E
N01  C   27HEUR          IFEQ      'B'                                           2<-B
N01  C     UEXPL         ANDNE     'X'
N01  C                   GOTO      Skipp
N01  C                   END                                                     2<-E
N01  C   27HEUR          IFEQ      'C'                                           2<-B
N01  C     UIMPF         ANDNE     'X'
N01  C                   GOTO      Skipp
N01  C                   END                                                     2<-E
N01  C   27HEUR          IFEQ      'D'                                           2<-B
N01  C     UEXPF         ANDNE     'X'
N01  C                   GOTO      Skipp
N01  C                   END                                                     2<-E
     C                   ADD       HENT          WSNT
     C                   ADD       HEVKT         WSVKT
     C                   ADD(H)    HEM3          WSM3
     C                   ADD       HELM          WSLM
     C                   ADD       1             WSNTO
N01  c     skipp         Tag
N02  C     TUBIL9        ifne      *blanks
     C     HEHAWB        IFNE      TUBIL9
     C     HEADFK        CHAIN     HEADF                              33
     C                   MOVEL     TUBILN        HEHAWB
     C  N33              UPDATE    HEADFR
     C                   END
N02  C                   END
     C                   GOTO      STCAL
     C                   END
     C*
     C     SLCAL         TAG
N01  c     *in27         ifne      '1'
     C     TURERK        CHAIN     TURER                              33
     C                   Z-ADD     WSNTO         TUAO
     C                   Z-ADD     WSNT          TUTS
     C                   Z-ADD     WSVKT         TUTVKT
     C                   Z-ADD     WSM3          TUTM3
     C                   Z-ADD     WSLM          TUTLM
     C                   UPDATE    TURERR
N01  c                   end
     C                   Z-ADD     1             YAVD
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     TESTIN        BEGSR
     C***********************************************
     C                   MOVEA     '00'          *IN(91)
     C     *IN98         DOWEQ     '0'                                          1<-B
     C                   READC     SUBFIL                                 98
     C  N98WSNR          IFEQ      '4'                                           2<-B
     C   27              DO                                                       3<-B
     C                   MOVE      'SPO0008'     MSGNR
     C                   SETON                                        9830
     C                   END                                                      3<-E
     C  N98HENAS         IFEQ      XSLETT                                         3<-B
     C                   MOVE      'SPO0009'     MSGNR
     C                   SETON                                        9830
     C                   END                                                      3<-E
     C  N98              EXSR      SLETT
     C                   UPDATE    SUBFIL
     C                   GOTO      SLTST
     C                   END                                                     2<-E
     C*
     C  N98WSNR          IFEQ      '5'                                           2<-B
     C     HENAS         IFEQ      XSLETT                                         3<-B
     C                   MOVE      'SPO0009'     MSGNR
     C                   SETON                                        9830
     C                   END                                                      3<-E
     C                   UPDATE    SUBFIL
     C  N98              EXSR      VISOPD
     C                   GOTO      SLTST
     C                   END                                                     2<-E
     C                   END                                                    1<-E
     C*
     C   98              DO
     C                   MOVE      'DIV0012'     MSGNR
     C                   SETON                                            30
     C                   END
     C*
     C     SLTST         ENDSR
     C*
     C***********************************************
     C     SLETT         BEGSR
     C***********************************************
     C     HEADFK        CHAIN     HEADF                              33
     C     HEPRO         IFEQ      UPRO
     C                   Z-ADD     0             HEPRO
     C                   END
     C                   UPDATE    HEADFR
     C     HEPRO         IFEQ      0
     C                   SUB       HENT          WSNT
     C                   SUB       HEVKT         WSVKT
     C                   SUB(H)    HEM3          WSM3
     C                   SUB       HELM          WSLM
     C                   SUB       1             WSNTO
     C                   MOVE      XSLETT        HENAS
     C                   MOVE      *BLANKS       HENAK
     C                   MOVE      *BLANKS       SFNAVN
     C                   MOVE      *ZEROS        HENT
     C                   MOVE      *BLANKS       HEVS1
     C                   MOVE      *BLANKS       HESDF
     C                   MOVE      *BLANKS       HESDT
     C                   MOVE      *ZEROS        HEVKT
     C                   MOVE      *ZEROS        HEM3
     C                   MOVE      *ZEROS        HELM
     C                   MOVE      *BLANKS       HEOT
     C                   MOVE      *BLANKS       HEFR
     C     TURERK        CHAIN     TURER                              33
     C                   Z-ADD     WSNTO         TUAO
     C                   Z-ADD     WSNT          TUTS
     C                   Z-ADD     WSVKT         TUTVKT
     C                   Z-ADD     WSM3          TUTM3
     C                   Z-ADD     WSLM          TUTLM
     C                   UPDATE    TURERR
     C                   END
     C*
     C                   ENDSR
     C*
     C******************************************
     C     OPDPOS        BEGSR
     C******************************************
     C*
     C* OPPDATERER POSISJON NULL PÅ ALLE OPPDRAG
     C                   SETOFF                                       94
     C                   Z-ADD     1             YAVD
     C     HEAD9K        SETLL     HEAD9
     C     STPOS         TAG
     C                   READ      HEAD9                                  94
     C  N94              DO                                                     1<-B
     C     HEPRO         IFLT      UPRO                                          2<-B
     C                   MOVE      HEAVD         YAVD
     C     HEAD9K        SETLL     HEAD9
     C                   GOTO      STPOS
     C                   END                                                     2<-E
     C     HEPRO         IFGT      UPRO                                          2<-B
     C     HEAVD         COMP      9999                                   94
     C   94              GOTO      SLPOS
     C     HEAVD         ADD       1             YAVD
     C     HEAD9K        SETLL     HEAD9
     C                   GOTO      STPOS
     C                   END                                                     2<-E
     C     HEADFK        CHAIN     HEADF                              93
     C                   MOVE      *BLANKS       HEPOS1
     C  N93              UPDATE    HEADFR
     C                   END                                                    1<-E
     C*
     C     SLPOS         ENDSR
     C*
     C***********************************************
     C     PLUKKE        BEGSR
     C***********************************************
     C                   CALL      'SYEX88C'
     C                   PARM                    UFIRM
     C                   PARM                    UAVD
     C                   PARM                    UPRO
     C                   PARM                    UIMP
     C                   PARM                    UFLAGG            1 0
     C*
     C     TURERK        CHAIN     TURER                              33
     C                   UPDATE    TURERR
     C                   Z-ADD     TUAO          WSNTO
     C                   Z-ADD     TUTS          WSNT
     C                   Z-ADD     TUTVKT        WSVKT
     C                   Z-ADD     TUTM3         WSM3
     C                   Z-ADD     TUTLM         WSLM
     C     UFLAGG        COMP      1                                      01
     C     UFLAGG        COMP      3                                      03
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     HPLUKK        BEGSR
     C***********************************************
     C                   MOVE      *OFF          *IN30
     C                   MOVE      *ZEROS        WFOAVD
     C                   MOVE      *ZEROS        WFOOPD
     C*
     C     HP01          TAG
     C                   EXFMT     BILDE01
     C                   MOVE      *OFF          *IN30
     C     *IN01         IFEQ      *OFF
     C     *IN03         ANDEQ     *OFF
     C     *IN12         ANDEQ     *OFF
     C     WFOFBN        IFEQ      *ZEROS
     C                   MOVE      'GIN'         YKODE
     C     FRISKK        CHAIN     FRISOK                             63
     C     *IN63         IFEQ      *ON
     C                   MOVE      *ON           *IN30
     C                   MOVE      'TRO0202'     MSGNR
     C                   GOTO      HP01
     C                   ELSE
     C                   MOVE      FSSOK         DSSOK
     C                   MOVE      DSAVD         HEAVD
     C                   MOVE      DSOPD         HEOPD
     C                   ENDIF                                                  <*IN63
     C                   ELSE
     C                   Z-ADD     WFOFBN        FOLOP
     C     FORDK         CHAIN     FORDEL                             63
     C     *IN63         IFEQ      *ON
     C                   MOVE      *ON           *IN30
     C                   MOVE      'JOV0799'     MSGNR
     C                   GOTO      HP01
     C                   ELSE
     C                   MOVE      FOAVDV        HEAVD
     C                   MOVE      FOOPDV        HEOPD
     C                   ENDIF                                                  <*IN63
     C                   ENDIF                                                  <WFOFBN
     C     HEADFK        CHAIN     HEADF                              62
     C     *IN62         IFEQ      *ON
     C                   MOVE      *ON           *IN30
     C                   MOVE      'TRO0202'     MSGNR
     C                   GOTO      HP01
     C                   ELSE
     C     HEPRO         IFNE      *ZEROS
     C                   UPDATE    HEADFR
     C                   MOVE      *ON           *IN30
     C                   MOVE      'TRO0203'     MSGNR
     C                   GOTO      HP01
     C                   ENDIF                                                  <HEAVD
     C     *IN32         IFEQ      *OFF
     C     HEAVD         ANDEQ     9999
     C                   MOVE      *ON           *IN32
     C                   MOVE      *ON           *IN30
     C                   MOVE      'TRO0204'     MSGNR
     C                   GOTO      HP01
     C                   ENDIF                                                  <*IN32
     C                   MOVE      TUPRO         HEPRO
     C                   UPDATE    HEADFR
     C                   ENDIF                                                  <*IN62
     C                   MOVE      *OFF          *IN32
     C                   Z-ADD     13            LINNBR
     C                   Z-ADD     31            POSNBR
     C     WFOFBN        IFEQ      *ZEROS
     C                   MOVE      *ZEROS        WFOOPD
     C                   ELSE
     C                   MOVE      *ZEROS        WFOFBN
     C                   Z-ADD     17            LINNBR
     C                   ENDIF
     C                   GOTO      HP01
     C                   ENDIF                                                  <*IN01
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     VISOPD        BEGSR
     C***********************************************
     C                   MOVE      9             UFLAGG            1 0
     C                   CALL      'SYFM21A'
     C                   PARM                    UFIRM
     C                   PARM                    HEAVD
     C                   PARM                    HEOPD
     C                   PARM                    UFLAGG
     C                   MOVE      *ZEROS        UFLAGG
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     TURERK        KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    UPRO
     C     HEADFK        KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C     HEAD9K        KLIST
     C                   KFLD                    YAVD
     C                   KFLD                    UPRO
     C                   KFLD                    YKNA
     C     FRISKK        KLIST
     C                   KFLD                    WFOAVD
     C                   KFLD                    WFOOPD
     C                   KFLD                    YKODE             3
     C     FORDK         KLIST
     C                   KFLD                    WFOAVD
     C                   KFLD                    WFOOPD
     C                   KFLD                    FOLOP
     C*
     C     *ENTRY        PLIST
     C                   PARM                    UFIRM             2
     C                   PARM                    UAVD              4 0
     C                   PARM                    UPRO              8 0
     C                   PARM                    UIMPL             1
     C                   PARM                    UEXPL             1
     C                   PARM                    UIMPF             1
     C                   PARM                    UEXPF             1
     C                   PARM                    USPØR             1
     C                   PARM                    UIMP              1
     C                   PARM                    UFLAGG            1 0
     C     USPØR         COMP      'J'                                    27
     C     UIMP          COMP      'J'                                    31
     C*
     C                   Z-ADD     1             YAVD              4 0
     C                   MOVE      *BLANKS       YPOS1             8
     C                   MOVE      *ZEROS        YKNA              8 0
     C                   MOVE      *ZEROS        HEVKT
     C                   MOVE      *ZEROS        HEM3
     C*
     C                   MOVE      'DIV0698'     XMSGNR            7            MELDING-ID
     C                   Z-ADD     3             XLEVEL            1 0          NIVÅ
     C                   Z-ADD     0             XRETUR            1 0          RETURKODE
     C                   Z-ADD     30            XMSGL             4 0          LENGDE PÅ MELDING
     C                   CALL      'SUBR23R3'
     C                   PARM                    XMSGNR
     C                   PARM                    XSLETT           30
     C                   PARM                    XLEVEL
     C                   PARM                    XRETUR
     C                   PARM                    XMSGL
     C*
     C     TURERK        CHAIN(N)  TURER                              33
     C                   EXSR      CALTOT
     C*
     C                   ENDSR
     C*
