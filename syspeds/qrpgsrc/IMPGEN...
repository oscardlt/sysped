     FNAVAUTK   IF   E           K DISK
     FFIRM      IF   E           K DISK
     FEDIRE1K   IF   E           K DISK
     FZTELL     UF   E           K DISK
     FSADVARE   UF A E           K DISK    PREFIX(SV_)
     FTARI      IF   E           K DISK
     FKODTVAL2  IF   E           K DISK
     FKODTS2    IF   E           K DISK
     FZADF      IF A E             DISK
     FZADV      O    E             DISK
     FZADVL04   IF   E           K DISK    PREFIX(Z_)
     F                                     RENAME(SADVR:SADVR4)
     FFRISOL01  IF A E           K DISK
     D                 DS
     D  dtafld                 1  18000
     D                                     DIM(60)
     D                 DS
     D  RDATA                  1   1024
     D  RTYP                   1      6
     D  RTYP4                  1      4
     D                 DS
     D  FE                     1    256
     D                                     DIM(256)
     D  FELT                   1    256
     D                 DS
     D  AA                     1   1024
     D                                     DIM(1024)
     D  AADS                   1   1024
     D                 DS
     D  ZDEC                   1      7  6
     D  ZDEC6                  2      7
     D  ZHEL                  11     22  0
     D                 DS
     D  A                      1     79
     D                                     DIM(79)
     D                 DS
     D  B                      1     18
     D                                     DIM(18)
      */////////////
      * MAIN LOGIC /
      */////////////
     C                   EXSR      INIT
      *
     C     UMBR          SETLL     EDIRE1K
     C     UMBR          READE     EDIRE1K                                91
     C     *IN91         DOWEQ     *OFF

     C     RTYP4         IFEQ      'Impo'
     C                   EXSR      SRFAK
     C                   ELSE
     C                   EXSR      SRVAR
     C                   end

     C     UMBR          READE     EDIRE1K                                91
     C                   ENDDO


     C     LR            TAG
     C                   MOVE      '1'           *INLR
      *
      *****************************************************************
     C     INIT          BEGSR
      *****************************************************************
      *
     C     *LIKE         DEFINE    KVADT         WKVADT
     C     *LIKE         DEFINE    SVLI          WVLI
     C     *LIKE         DEFINE    KS2UNI        WS2UNI
      *
     C     *ENTRY        PLIST
     C                   PARM                    UMBR             10
      * Hente dagens dato og tid
     C                   CALL      'SYGE15C'
     C                   PARM                    WDATO             8
     C                   PARM                    WTID              6
      * Init av arbetsfelt
     C                   MOVEL     WDATO         WKVADT
     C                   MOVEL     'S2'          WS2UNI
     C     SADVAREK      KLIST
     C                   KFLD                    SV_LEVENR
     C                   KFLD                    SV_VARENR
      *
      *
      * tariff
     C     TARIK         KLIST
     C                   KFLD                    SVVNT
     C     KODVAK        KLIST
     C                   KFLD                    SFVK28
     C                   KFLD                    WKVADT
     C     KODTS2K       KLIST
     C                   KFLD                    WS2UNI
     C                   KFLD                    SVLK
     C     FRISOKK       KLIST
     C                   KFLD                    FSKODE
     C                   KFLD                    FSSOK
     C                   MOVE      '*CR'         FSKODE
     c                   read      firm                                   33
      * Hent unikt nummer for Z-filer
     C     FIFIRM        CHAIN     ZTELL
     C                   ADD       1             ZUNIK
     C                   UPDATE    ZTELLR
     C     ENDINI        ENDSR
      *
      **************************************************************
     C     SRFAK         BEGSR
      **************************************************************
      *
      * Kaller eksternt program og legger felt i array
      *
     C                   MOVEL     RDATA         UDATA          1024
     C                   MOVEL     *BLANKS       UARRA         18000
     C                   MOVE      ';'           SKILLET           1
     C                   CALL      'SYGE111'
     C                   PARM                    SKILLET
     C                   PARM                    UDATA
     C                   PARM                    UARRA
     c                   movea     uarra         dtafld
      *
      * leser fakturatotaler linje 1
      *
     C                   CLEAR                   SADFR
      *
      *
      *
      * 1. fast tekst Imp...
      *
      *
      * 2. kundenummer
      *
     C                   MOVEA     dtafld(2)     A
     C                   EXSR      GETNUM
     C                   Z-ADD     ZNUM          SV_LEVENR         8 0
     C     SV_LEVENR     CHAIN     NAVAUTK                            33
      *
      * 3. Fakturanummer
      *
      *
      *
     C                   EVAL      SFTXT=dtafld(3)
     C                   EVAL      SFTXT = %TRIM(SFTXT)
      *
      * finnes samme nummer uplukket
      *
     c                   setoff                                       33
     c                   z-add     0             nr                2 0
     c                   MOVE      sftxt         sftxtt           17
     c                   MOVE      sftxt         sftxti           17
     c     *in33         doweq     '0'
     c     SFTXTT        chain     ZADVL04                            33
     c     *in33         ifeq      '0'
     c                   add       1             nr
     c                   eval      sftxtt=sftxti
     c     SFTXTT        CAT       '-':0         SFTXTT
     c                   move      nr            nra               2
     c     SFTXTT        CAT       NRA:0         SFTXTT
     c                   end
     c                   enddo
     c
     c                   move      sftxtt        sftxt
     c
     C                   EVAL      SFREFF=SFTXT
      *
      * 4. Fakturadato
      *
     C                   MOVEA     dtafld(4)     A
     C                   EXSR      GETNUM
     C                   Z-ADD     ZNUM          SFDT
      *
      * 5. Valutakode
      *
     C                   EVAL      SFVK28=dtafld(5)
     C     SFVK28        IFEQ      *blanks
     C                   MOVE      'NOK'         SFVK28
     c                   end
      *
      * 6. valutabeløp
      *
     C                   MOVEA     dtafld(6)     A
     C                   EXSR      GETNUM
     C                   Z-ADD     ZNUM          SFBL28
      *
      * 7. Unik Dachserreferanse 11 numerisk
      *
     C                   MOVEA     dtafld(7)     A
     C                   EXSR      GETNUM
     C                   Z-ADD     ZNUM          DACREF           11 0
     C                   MOVEL     DACREF        FSSOK
     C     FRISOKK       CHAIN     FRISOL01                           33
     C  N33              EXSR      FRISR
      *
      * Overfør variabler til SADF
      *
     C                   EVAL      SFUNIK=ZUNIK
     C                   EVAL      SFTXT = %TRIM(SFTXT)
     C                   EVAL      SFREFF=SFTXT
      *
      * hent omregningskurser
      *
     C     KODVAK        SETGT     KODTVAL2
     C     SFVK28        READPE    KODTVAL2                               33
     C     *IN33         IFEQ      *OFF
     C                   EVAL      SFKR28=KVAKRS
     C                   EVAL      SFOM28=KVAOMR
     C                   ENDIF
     C                   WRITE     SADFR
     C                   ENDSR
      *
      **************************************************************
     C     SRVAR         BEGSR
      **************************************************************
      *
      * SAD Varelinjer                                      SRVAR /
      *
      * Kaller eksternt program og legger felt i array
      *
     C                   MOVEL     RDATA         UDATA          1024
     C                   MOVEL     *BLANKS       UARRA         18000
     C                   MOVE      ';'           SKILLET           1
     C                   CALL      'SYGE111'
     C                   PARM                    SKILLET
     C                   PARM                    UDATA
     C                   PARM                    UARRA
     c                   movea     uarra         dtafld
      *
      *
     C                   EVAL      SVUNIK = SFUNIK
     C                   EVAL      SVREFF = SFREFF
     C                   EVAL      SVPRE = 'N'
      *
      *
      * 1. opprinnelseskode
     C                   MOVEL     dtafld(1)     SVLK
      * 2. Tariffnummer
     C                   MOVEL     dtafld(2)     SVVNT
      *
      * 3. tollverdi
      *
     C                   MOVEA     dtafld(3)     A
     C                   EXSR      GETNUM
     C                   Z-ADD     ZNUM          SVBELT
      *
      * 4. Bruttovekt KG
     C                   MOVEA     dtafld(4)     A
     C                   EXSR      GETNUM
     C                   Z-ADD     ZNUM          SVVKTB
      * 5. Nettovekt
     C                   MOVEA     dtafld(5)     A
     C                   EXSR      GETNUM
     C                   Z-ADD     ZNUM          SVVKTN
      *
      * hvis verdi vekt netto brutta alle er 0 skipp linjen
      *
     c     svbelt        ifeq      0
     c     svvktb        andeq     0
     c     svvktn        andeq     0
     c                   goto      feilimp
     c                   end
      * 6. Antall
     C                   MOVEA     dtafld(6)     A
     C                   EXSR      GETNUM
     C                   Z-ADD     ZNUM          SVNTM
      * 7. Artikkenummer
     C                   EVAL      SV_VARENR=dtafld(7)
     C                   EVAL      SVEXR03=dtafld(7)
     c     SV_VARENR     ifeq      *blanks
     C                   MOVEL     dtafld(2)     SV_VARENR
     C                   EVAL      SVEXR03=dtafld(2)
     C                   END
      *
      * evt landkode i artikkelnummer
      *
     c     navlka        ifeq      'J'
     c                   move      svlk          SV_VARENR
     c                   end
     c
      * 8. Varebeskrivelse
     C                   MOVEL     dtafld(8)     SVVT01
      *
     C                   ADD       1             WVLI
     c                   z-add     wvli          svli
     c                   z-add     wvli          svlN
      *
      * sjekker tariffnummer før vi skriver til fil
      *
      * sjekker tariff ved norskt tariffnr
      *
     c     NAVNTA        IFEQ      'J'
     c     tarik         chain     tari                               33
     c                   end
      * feil
     c     SV_VARENR     ifne      *blanks
     c     SV_LEVENR     andne     0
     c                   exsr      tarisr
     c                   end
     c*
     c*
     c                   EVAL      SVVF   = '1'
      * Skriv post til Arbetsfil
     C                   WRITE     sadvr
     C     feilimp       ENDSR
      *
      *************************************************************
     C     GETNUM        BEGSR
      *************************************************************
      *
     C                   MOVE      *ZEROS        ZHEL
     C                   MOVE      *ZEROS        ZDEC
     C                   MOVE      *ZEROS        B
     C                   Z-ADD     1             AX                2 0
     C                   Z-ADD     1             BX                2 0
      * Get Integer
     C     A(AX)         DOWGE     '0'
     C     ZHEL          MULT      10            ZHEL
     C                   MOVE      A(AX)         ZHEL
     C                   ADD       1             AX
     C                   ENDDO
      * Decimal point ?
     C     A(AX)         IFEQ      '.'
     C     A(AX)         OREQ      ','
     C                   ADD       1             AX
     C     A(AX)         DOWGE     '0'
     C                   MOVE      A(AX)         B(BX)
     C                   ADD       1             AX
     C                   ADD       1             BX
     C                   ENDDO
     C                   MOVEA     B             ZDEC6
     C                   ENDIF
      * Concatenate
     C                   Z-ADD     ZHEL          ZNUM             18 6
     C                   ADD       ZDEC          ZNUM
     C                   ENDSR
      **************************************************************************
     c     frisr         begsr
      **************************************************************************
      * Skriv till Frie søkeveier
     C                   CLEAR                   FRISOKR
     c                   MOVE      'HFI'         FSKODE
     C                   EVAL      FSSOK=SFTXT
     C                   EVAL      FSDTOP=SFDT
     C                   WRITE     FRISOKR
     C                   ENDSR
      ********************************************************************
     c     tarisr        begsr
      ********************************************************************
      *
      * HENTER NORSK FRA VAREREGISTER
      *
     C     SADVAREK      CHAIN     SADVARE                            33
      * ikke finnes fra før
     C                   IF        *IN33
     C                   EVAL      SV_VAREBE = SVVT01
     C                   EVAL      SV_W2LK   = SVLK
     C                   MOVE      SVVNT         SV_W2VNTI
     C                   WRITE     SADVARER
      * slutt ny post
     C                   ELSE
     C                   IF        SVLK = *BLANKS
     C                   EVAL      SVLK = SV_W2LK
     C                   ELSE
     C                   EVAL      SV_W2LK = SVLK
     C                   END
     C                   MOVE      SV_W2VNTI     SVVNT
     C                   EVAL      SVAKD1 = SV_W2AKD1
     C                   EVAL      SVASV1 = SV_W2ASV1
     c     SV_W2PRE      ifne      ' '
     c                   eval      svpre=SV_W2PRE
     c                   end
     C                   UPDATE    SADVARER
     C                   END
     c                   endsr
     c
      *
