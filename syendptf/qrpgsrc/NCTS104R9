      *****************************************************************
      *** PROGRAM SKAL KONVERTERE INNGÅENDE IE928                   ***
      *** Positive Acknowledge                                      ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER          ***
      *** LEDIGE INDIKATORER 01-26                                  ***
      *** LEDIGE INDIKATORER 28-32 34-97                            ***
      *****************************************************************
     H DFTACTGRP(*NO)
     FXMLRCVF   IF   E           K DISK
     FXMLRCLF   IF   E           K DISK
     FTRACKF    O  A E           K DISK
     FNCTSEH    UF   E           K DISK
     FTVINF     O  A E             DISK
     FEDIC      UF   E             DISK
     FEDIM      O  A E             DISK
     FEDIM5     IF   E           K DISK    RENAME(EDIMR:EDIMR5)
     D                sds
     D ZUSER                 254    263
      *  Prototype for 'SYGE15C'
     d syge15c         pr                  extpgm('SYGE15C')
     d wdt_                                like(wdt)
     d wtm_                                like(wtm)
      *  Prototype for ncts104r9
     d ncts104r9       pr
     d usn_                                like(usn)
      *  *ENTRY Interface for Main Procedure
     d ncts104r9       pi
     d usn                            9
      *----------------------------------------------------------------------
      * Stand Alone Fields - TOP
      *----------------------------------------------------------------------
     d x1_xrlev1       s                   like(xrlev1)
     d x1_xrlev2       s                   like(xrlev2)
     d x2_xrlev1       s                   like(xrlev1)
     d x2_xrlev2       s                   like(xrlev2)
     d x2_xrlev3       s                   like(xrlev3)
     d x3_xrlev1       s                   like(xrlev1)
     d x3_xrlev2       s                   like(xrlev2)
     d x3_xrlev3       s                   like(xrlev3)
     d x3_xrlev4       s                   like(xrlev4)
     d x_thtrdt        s              8
     d x_thtrnr        s                   like(thtrnr)
     d x_tinh          s                   like(thtinh)
     d x_nah           s                   like(thnah)
     d x_ad1h          s                   like(thad1h)
     d x_pnh           s                   like(thpnh)
     d x_psh           s                   like(thpsh)
     d x_lkh           s                   like(thlkh)
     d x_tinr          s                   like(thtinh)
     d x_IE928         s              1a
     d x_ref           s              8a
     d x_TIR           s             17a
     d x_rtyp          s              3a
     d x_rcod          s              2a
     d x_str           s              1a
     d x_ecod          s              2a
     d x_erea          s              7a
     d xn              s              3s 0
     d wdt             s              8
     d wtm             s              6
     d                 ds
     d x_rdt                   1      8  0
     d x_rdta                  1      8
     d                 ds
     d x_rtm                   1      6  0
     d x_rtma                  1      6
     d                 ds
     d m1004_ref               1     11
     d m1004_avd               1      4  0
     d m1004_tdn               5     11  0
     d                 ds
     d f0078a                  1     70a
     d f0078b                 71    140a
     d f0078c                141    210a
     d f0078d                211    280a
     d f0078e                281    350a
     d ft                      1    350a   dim(5)
      *
     D UP              C                   CONST('ABCDEFGHIJKLMNOPQRST-
     D                                     UVWXYZÆØÅ')
     D LO              C                   CONST('abcdefghijklmnopqrst-
     D                                     uvwxyzæøå')
      *----------------------------------------------------------------------
     IEDIMR5
     I              M0004                       X0004
     I              M0010                       X0010
     I              M0035                       X0035
     I              M0062                       X0062
     I              M0065                       X0065
     I              M0068                       X0068
     I              M0068A                      X0068A
     I              M0068B                      X0068B
     I              M0068C                      X0068C
     I              M0068D                      X0068D
     I              M0068E                      X0068E
     I              M0068F                      X0068F
     I              M1001                       X1001
     I              M1004                       X1004
     I              M1225                       X1225
     I              M2005B                      X2005B
     I              M3039D                      X3039D
     I              M3039E                      X3039E
     I              M5004D                      X5004D
     I              M1N07                       X1N07
     I              M1N08                       X1N08
     I              M9N01                       X9N01
     I              MSN                         XSN
     I              MMN                         XMN
     I              MSR                         XSR
     I              MST                         XST
     I              MDT                         XDT
     I              MTM                         XTM
     I              MVEN                        XVEN
     I              MAVD                        XAVD
     I              MTDN                        XTDN
      /free

       exsr init;

       exsr srxml;

       *inlr = *on;
       return;

       //**********************************************
       begsr srxml;
       //**********************************************

         x1_xrlev1 = 0;
         x1_xrlev2 = 0;

         chain (xrsn:x1_xrlev1:x1_xrlev2) xmlrcvf;
         dow %found;
           select;
           when xrnv = 'messageSender';
             m0004  = xrverd;
           when xrnv = 'messageRecipient';
             m0010  = xrverd;
           when xrnv = 'preparationDateAndTime';
             x_rdta = %subst(xrverd:1:4) + %subst(xrverd:6:2)
                   + %subst(xrverd:9:2);
             x_rtma = %subst(xrverd:12:2) + %subst(xrverd:15:2)
                   + %subst(xrverd:18:2);
           when xrnv = 'messageIdentification';
             m0062  = xrverd;
             m1004  = xrverd;
           when xrnv = 'messageType' and xrverd = 'CC928C';
             x_ie928 = 'J';
             chain 1 edic;
             cmn = cmn + 1;
             update edicr;
             fmn = cmn;
             mmn = cmn;
           when xrnv = 'correlationIdentifier';
           when xrnv = 'TransitOperation';
             exsr srxml1;
           when xrnv = 'CustomsOfficeOfDeparture';
             exsr srxml2;
           when xrnv = 'HolderOfTheTransitProcedure';
             exsr srxml3;
           endsl;

           x1_xrlev1 = x1_xrlev1 + 1;
           chain (xrsn:x1_xrlev1:x1_xrlev2) xmlrcvf;
         enddo;

         if x_ie928 = 'J';
           exsr opdopd;
           exsr ttsr;
         endif;

         endsr;

       //**********************************************
       begsr srxml1;
       //**********************************************

         x2_xrlev1 = xrlev1;
         x2_xrlev2 = 1;
         x2_xrlev3 = 0;

         chain (xrsn:x2_xrlev1:x2_xrlev2:x2_xrlev3) xmlrcvf;
         dow %found;
           select;
           when xrnv = 'LRN';
             x0068 = xrverd;
           endsl;

           x2_xrlev2 = x2_xrlev2 + 1;
           chain (xrsn:x2_xrlev1:x2_xrlev2:x2_xrlev3) xmlrcvf;
         enddo;

       endsr;

       //**********************************************
       begsr srxml2;
       //**********************************************

         x2_xrlev1 = xrlev1;
         x2_xrlev2 = 1;
         x2_xrlev3 = 0;

         chain (xrsn:x2_xrlev1:x2_xrlev2:x2_xrlev3) xmlrcvf;
         dow %found;
           select;
           when xrnv = 'referenceNumber';
             x_ref = xrverd;
             ft     = *blanks;
             xn     = 0;
             fln    = fln + 1;
             f0077  = *blanks;
             ft(1)  = 'Customs office of departure: ' + xrverd;
             //write tvinff;
           endsl;

           x2_xrlev2 = x2_xrlev2 + 1;
           chain (xrsn:x2_xrlev1:x2_xrlev2:x2_xrlev3) xmlrcvf;
         enddo;

       endsr;

       //**********************************************
       begsr srxml3;
       //**********************************************

         x2_xrlev1 = xrlev1;
         x2_xrlev2 = 1;
         x2_xrlev3 = 0;

         chain (xrsn:x2_xrlev1:x2_xrlev2:x2_xrlev3) xmlrcvf;
         dow %found;
           select;
           when xrnv = 'identificationNumber';
             x_tinh = xrverd;
           when xrnv = 'TIRHolderIdentificationNumber';
             x_TIR = xrverd;
           when xrnv = 'name';
             x_nah = xrverd;
             //ft     = *blanks;
             xn     = 0;
             //fln    = fln + 1;
             f0077  = *blanks;
             ft(2)  = 'Holder of the transit procedure: ' + %trim(x_tinh)
                     + ' ' + %trim(x_nah);
             write tvinff;
           when xrnv = 'Address';
             exsr srxml31;
           endsl;

           x2_xrlev2 = x2_xrlev2 + 1;
           chain (xrsn:x2_xrlev1:x2_xrlev2:x2_xrlev3) xmlrcvf;
         enddo;

       endsr;

       //**********************************************
       begsr srxml31;
       //**********************************************

         x3_xrlev1 = xrlev1;
         x3_xrlev2 = xrlev2;
         x3_xrlev3 = 1;
         x3_xrlev4 = 0;

         chain (xrsn:x3_xrlev1:x3_xrlev2:x3_xrlev3:x3_xrlev4) xmlrcvf;
         dow %found;
           select;
           when xrnv = 'streetAndNumber';
             x_ad1h = xrverd;
           when xrnv = 'postcode';
             x_pnh = xrverd;
           when xrnv = 'city';
             x_psh = xrverd;
           when xrnv = 'country';
             x_lkh = xrverd;
           endsl;

           x3_xrlev3 = x3_xrlev3 + 1;
           chain (xrsn:x3_xrlev1:x3_xrlev2:x3_xrlev3:x3_xrlev4) xmlrcvf;
         enddo;

       endsr;

       //**********************************************
       begsr opdopd;
       //**********************************************

         chain x0068 edim5;
         if %found;
           chain (xavd: xtdn) nctseh;
           if %found;
             if thst = 'C';
               thst    = 'c';
             endif;
             thst928 = 'J';
             update nctsehr;
           endif;
         endif;

         if m0004 = *blanks;
           if xsr = 'S';
             m0004 = x0010;
           else;
             m0004 = x0004;
           endif;
         endif;
         m0065  = 'XMLRES';
         m0068  = x0068;
         m1001  = 'TET';
         m1004_avd = thavd;
         m1004_tdn = thtdn;
         m1004  = m1004_ref;
         m1225  = '928';
         msn    = xrsn;
         msr    = 'R';
         mst    = 'C';
         mdt    = %float(wdt);
         mtm    = %float(wtm);
         m0068a = x0068a;
         m0068b = x0068b;
         m1n07  = '928';
         mavd   = thavd;
         mtdn   = thtdn;
         write edimr;

       endsr;

       //**********************************************
       begsr ttsr;
       //**********************************************

         ttavd  = thavd;
         ttopd  = thtdn;
         ttacti = '928';
         ttdate = %float(wdt);
         tttime = %float(wtm);
         ttuser = zuser;
         tttext = 'Positive Acknowledge from NCTS: '
                  + %trim(%editw(x_rdt:'    .  .  '))
                  + ' ' + %trim(%editw(x_rtm:'  :  :  '));
         tttexl = 'Positiv anerkjennelse fra NCTS: '
                  + %trim(%editw(x_rdt:'    .  .  '))
                  + ' ' + %trim(%editw(x_rtm:'  :  :  '));
         write trackfr;

       endsr;

       //**********************************************
       begsr init;
       //**********************************************

         xrsn = %dec(usn:9:0);
         syge15c ( wdt : wtm );

       endsr;

      /end-free
