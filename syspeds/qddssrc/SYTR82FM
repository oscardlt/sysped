     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      REF(*LIBL/KODTRI)
     A                                      CF03(03 'Avslutt')
     A                                      PRINT
     A                                      HELP
     A                                      ALTHELP
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  6  3VALUES(' ' '2' '4')
     A  98                                  DSPATR(RI)
     A  98                                  DSPATR(PC)
     A            RITERM    R        O  6  6REFFLD(KODTRIR/RITERM *LIBL/KODTRI)
     A            RIPNRF    R        O  6 13REFFLD(KODTRIR/RIPNRF *LIBL/KODTRI)
     A                                  6 19'-'
     A            RIPNRT    R        O  6 22REFFLD(KODTRIR/RIPNRT *LIBL/KODTRI)
     A            RIAVDU    R        O  6 35REFFLD(KODTRIR/RIAVDU *LIBL/KODTRI)
     A            RIAVDI    R        O  6 43REFFLD(KODTRIR/RIAVDI *LIBL/KODTRI)
     A            RIAVDH    R        O  6 51REFFLD(KODTRIR/RIAVDI *LIBL/KODTRI)
     A            SFDEL         10A  O  6 66
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A  91N93                               CF05(05 'Frisk')
     A  91N93                               CF06(06 'LAGE NY')
     A  91N93                               CF08(08 'SØK')
     A                                  1  2'SYSPED/SYTR82'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(040 TRO0149 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                      COLOR(WHT)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  1 69TIME
     A                                      COLOR(BLU)
     A            ZRECNO         4S 0O  2 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  3  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 33MSGCON(030 DIV0017 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2'Op  TERM   Postnr.område         A-
     A                                      vdeling ved                        -
     A                                           '
     A                                      COLOR(WHT)
     A                                  5  2'           Fra   -  Til       UTGÅ-
     A                                      ENDE/INNGÅENDE/HENTE               -
     A                                           '
     A                                      COLOR(WHT)
     A          R BUNN
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 21MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 40MSGCON(018 DIV0022 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 60MSGCON(018 DIV0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  98                             24  2'INVALID OPTION'
     A                                      COLOR(WHT)
     A          R VARELI
     A                                      CF12(12 'Forrige')
     A                                      SLNO(19)
     A                                      CLRL(06)
     A            MSGNR          7A  P
     A                                  2  6'TERM   Fra   -  Til       UTGÅENDE-
     A                                      /INNGÅENDE/HENTE                   -
     A                                       '
     A                                      COLOR(WHT)
     A            WSTERM         4Y 0B  3  6DSPATR(UL)
     A                                      EDTCDE(4)
     A  40                                  DSPATR(PR)
     A            WSPNRF         4Y 0B  3 13DSPATR(UL)
     A                                      EDTCDE(4)
     A  40                                  DSPATR(PR)
     A                                  3 19'-'
     A            WSPNRT         4Y 0B  3 22DSPATR(UL)
     A                                      EDTCDE(4)
     A  40                                  DSPATR(PR)
     A            WSAVDU         4Y 0B  3 35DSPATR(UL)
     A                                      EDTCDE(4)
     A            WSAVDI         4Y 0B  3 43DSPATR(UL)
     A                                      EDTCDE(4)
     A            WSAVDH         4Y 0B  3 51DSPATR(UL)
     A                                      EDTCDE(4)
     A                                  4  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 21MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  5  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A          R VAREL2
     A*%%TS  SD  20011002  104059  JOVO        REL-V4R4M0  5769-PW1
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                  2  2MSGCON(012 DIV0026 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A            W2TERM         4Y 0B  3  6DSPATR(PC)
     A                                      EDTCDE(4)
     A            W2PNRF         4Y 0B  3 13DSPATR(PC)
     A                                      EDTCDE(4)
     A          R VAREL3
     A                                      CF12(12 'Forrige')
     A                                      SLNO(19)
     A                                      CLRL(06)
     A            MSGNR          7A  P
     A                                  2  6'Tast ENTER for å slette posten / F-
     A                                      12 for å avbryte'
     A                                      COLOR(WHT)
     A            RITERM         4Y 0O  3  6DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      DSPATR(HI)
     A            RIPNRF         4Y 0O  3 13DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      DSPATR(HI)
     A            RIPNRT         4Y 0O  3 22DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      DSPATR(HI)
     A            RIAVDU         4Y 0O  3 35DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      DSPATR(HI)
     A            RIAVDI         4Y 0O  3 43DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      DSPATR(HI)
     A            RIAVDH         4Y 0O  3 51DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      DSPATR(HI)
     A                                  4  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 21MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  5  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A          R NYWRK
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                  1  2'SYSPED/SYTR82'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(040 TRO0149 *LIBL/SYMSGF)
     A                                      DSPATR(HI)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                  1 69TIME
     A                                  3  2MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 33MSGCON(030 DIV0017 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2'Op  TERM   Postnr.område         A-
     A                                      vdeling ved                        -
     A                                           '
     A                                      COLOR(WHT)
     A                                  5  2'           Fra   -  Til       UTGÅ-
     A                                      ENDE/INNGÅENDE/HENTE               -
     A                                           '
     A                                      COLOR(WHT)
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 21MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 40MSGCON(018 DIV0022 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 60MSGCON(018 DIV0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
