     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      REF(*LIBL/EXRSTC)
     A                                      PRINT
     A                                      CF03(03 'Gå ut')
     A                                      CF12(12 'Forrige')
      *
     A          R E004DA
     A                                      CF04(04)
     A                                      OVERLAY
     A                                  1  2'EDI'
     A                                      COLOR(BLU)
     A                                  1 22'Arbeide med X-ref for koder'
     A                                      COLOR(WHT)
     A                                  1 76'E004D'
     A                                  3  2MSGCON(026 EDI0602 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  2'Edifact tag . . . . . . . . . . :'
     A            WXTAG          4A  B  5 38
     A  31                                  DSPATR(PC)
     A  31                                  COLOR(RED)
     A                                  6  4'Intern kode . . . . . . . . . :'
     A            WXKODI         6A  B  6 38
     A  32                                  DSPATR(PC)
     A  32                                  COLOR(RED)
     A                                 22  3MSGCON(010 EDI0803 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 22 16MSGCON(014 EDI0804 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
      *
     A          R E004DB                    SFL
     A            ZOP            1A  I  9  3VALUES(' ' '1' '2' '3' '4' '5')
     A  41                                  DSPATR(ND)
     A                                      DSPATR(PC)
     A            EXKNR     R        O  9  6EDTCDE(Z)
     A            KNAVN         30A  O  9 15
     A            EXKODE    R        O  9 46
     A            EXTX28        28A  O  9 53
      *
     A          R E004DC                    SFLCTL(E004DB)
     A                                      SFLSIZ(0013)
     A                                      SFLPAG(0012)
     A  91N93N94                            ROLLUP(25 'Rollup')
     A  91N93N94                            ROLLDOWN(26 'Rolldown')
     A                                      CF05(05 'Refresh')
     A                                      CF06(06 'Create')
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A                                  1  2'EDI    '
     A                                      COLOR(BLU)
     A                                  1 22'Arbeide med X-ref for koder'
     A                                      COLOR(WHT)
     A            ZRECNO         4S 0O  1 60SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1 76'E004D'
     A                                  3  2'Edifact tag..:'
     A            WXTAG          4A  O  3 18
     A            WTAGTX        35A  O  3 25
     A                                  4  4'Intern kode:'
     A            WXKODI         6A  O  4 18
     A            WUALTX        35A  O  4 25
     A                                  5  2MSGCON(026 EDI0602 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  6  4MSGCON(006 EDI0701 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  6 13MSGCON(007 EDI0702 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  6 23MSGCON(008 EDI0703 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  6 34MSGCON(008 EDI0704 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  6 45MSGCON(009 EDI0705 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  8  2MSGCON(003 EDI0609 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  8  7'Kundenr'
     A                                      COLOR(WHT)
     A                                  8 15'Navn'
     A                                      COLOR(WHT)
     A                                  8 46'Extern kode'
     A                                      COLOR(WHT)
      *
     A          R E004DD
     A                                      OVERLAY
     A                                 22  2MSGCON(010 EDI0803 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 22 15MSGCON(010 EDI0805 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 22 28MSGCON(010 EDI0806 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 22 41MSGCON(010 EDI0812 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
      *
     A          R E004DE
     A                                      OVERLAY
     A                                      CF04(04)
     A                                  1  2'EDI    '
     A                                      COLOR(BLU)
     A                                  1 22'Arbeide med X-ref for koder'
     A                                      COLOR(WHT)
     A                                  1 76'E004D'
     A                                  3  2'Edifact tag..:'
     A            WXTAG          4A  O  3 18
     A            WTAGTX        35A  O  3 25
     A                                  4  4'Intern kode:'
     A            WXKODI         6A  O  4 18
     A            WUALTX        35A  O  4 25
     A                                  6  2'Velg og trykk Enter.'
     A                                      COLOR(BLU)
     A                                  8  2'Kundenummer . . . . . . . . . . .:'
     A            ÅXKNR          8S 0B  8 39
     A  31                                  COLOR(RED)
     A  61                                  DSPATR(PR)
     A                                  9  4'Extern kode . . . . . . . . . .:'
     A            ÅXKODE         6A  B  9 39CHECK(LC)
     A  62                                  DSPATR(PR)
     A                                 10  6'Beskrivelse . . . . . . . . .:'
     A            ÅXTX          60A  B 11  6CHECK(LC)
     A  62                                  DSPATR(PR)
     A                                 22  2'F3=Avslutt'
     A                                      COLOR(BLU)
     A                                 22 15'F4=Søk kunde'
     A                                      COLOR(BLU)
     A                                 22 30'F12=Avbryt'
     A                                      COLOR(BLU)
      *
     A          R E004DØ
     A                                      OVERLAY
     A            ZMSGTX        79A  O 23  2MSGID(&ZMSGNO SYMSGF)
     A N30                                  COLOR(WHT)
     A  30                                  COLOR(RED)
     A            ZMSGNO         7A  P
