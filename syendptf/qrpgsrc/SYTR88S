********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 sh  PTF10 ny parameter syge13
10XXX * 02 sh  PTF10 hvis linje 0 er kreditert flyttes denne til
      *              et annet linjenummer
10XXX * 03 sh  PTF11 kun førhøye ved lokal valuta
      * %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
10XXX * 04 sh  PTF12 feil i avtalekundnummer
      * 05 JOV PTF12 UTYPE='S'/ fra splittet oppdrag - frisok. eks  = SPO 00000125
********************************************************************
      *
      * SAMTAKSERING...
      *
      * OBS! I INITRUTINE KAN SETTES PARAMETER FOR OM HELE NAVN/ADR..
      *      MÅ MATCHE FÅR Å FORESLÅ SAMTAKSERING...
      *      + EVT EVT. KR.BELØP SOM SKAL PLUSSES PÅ PR, OPPDRAG
      *
      ************************************************************************
     FFIRM      IF   E           K DISK
     FKODTA     IF   E           K DISK
     FHEADF     UF   E           K DISK
     FHEAD14    IF   E           K DISK
     F                                     RENAME(HEADFR:HEAD14R)
s02  F*AKT      UF   E           K DISK
N02  FFAKT      UF A E           K DISK
     FTURER14   IF   E           K DISK
     FCUNDL01   IF   E           K DISK
n05  FFRISOL01  IF   E           K DISK
     FSYTR88S   UF A E           K DISK
     FSYTR88S2  IF   E           K DISK
     F                                     RENAME(SYTR88SR:SYTR882R)
     FSYTR88FS  CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               376    377B 0
     D*                                   B 378 3790WSSUBN
     D                 DS
     D  STREKN                 1     15
     D  STLKF                  1      2
     D  STSDF                  3      7
     D  STLKT                  8      9
     D  STSDT                 10     14
     D  STKDPL                15     15
N05  D                 DS
N05  D  TRSDFD                 1      8  0
N05  D  TRSDFDÅ                1      4  0
N05  D    TRSDFDCC             1      2  0
N05  D    TRSDFDAA             3      4  0
N05  D  TRSDFDM                5      6  0
N05  D  TRSDFDD                7      8  0
N05  D                 DS
N05  D  TRSDTD                 1      8  0
N05  D  TRSDTDÅ                1      4  0
N05  D  TRSDTDM                5      6  0
N05  D  TRSDTDD                7      8  0
     D                 DS
     D  TUDT                   1      8  0
     D  TUDTÅ                  1      4  0
     D  TUDTM                  5      6  0
     D  TUDTD                  7      8  0
     D                 DS
     D  WSDT                   1      6  0
     D  WSDTD                  1      2  0
     D  WSDTM                  3      4  0
     D  WSDTÅ                  5      6  0
     D                 DS
     D  TUDTT                  1      8  0
     D  TUDTTÅ                 1      4  0
     D  TUDTTM                 5      6  0
     D  TUDTTD                 7      8  0
     D                 DS
     D  WSDTT                  1      6  0
     D  WSDTTD                 1      2  0
     D  WSDTTM                 3      4  0
     D  WSDTTÅ                 5      6  0
     D                 DS
     D  HXHEAD                 1    256
     D  XXUR                   1      1
     D  WSFRB                  2      4
     D  WSKDPB                 5      5
     D  WSLKFB                 6      7
     D  WSSDFB                 8     12
     D  WSLKTB                13     14
     D  WSSDTB                15     19
     D  XXLKS                 20     21
     D  XXSDFF                22     26
     D  XXLKK                 27     28
     D  XXSDVT                29     33
     D  WSVKTS                34     42  0
     D  WSFBVS                43     51  0
     D  WSM3S                 52     58  3
     D  WSLMS                 59     62  2
     D  WSNTS                 63     69  0
     D  XXVERV                70     72
     D  XXVERB                73     75  2
     D                 DS
     D  SYFR05                 1      3
     D  SYSAMT                 1      1
     D                 DS
     D  STAMNA                 1     30
N05  D* STAVMO                 1     10
N05  D  STAVMO                 1     20
     D  STAMA1                31     60
     D  STAMA2                61     90
     D  STAMA3                91    120
     D  AVSMOT                 1    120
      *
     C                   EXSR      INIT
      *
     C     START         TAG
      *
     C                   EXSR      LESOPD
      *
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
     C                   MOVEA     '000000'      *IN(51)
     C                   MOVE      'N'           SKUMJN            1
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     UPRO2         SETLL     SYTR88S2
     C     UPRO2         READE     SYTR88S2                               94
      *
     C     *IN94         DOWEQ     '0'
     C                   MOVE      STKNR         KUAVTA
     C     KUNKEY        CHAIN     CUNDL01                            33
     C     SYSAMT        COMP      'J'                                    55
     C  N33AKNRKU        IFNE      *ZEROS
     C                   MOVE      AKNRKU        KUAVTA            8 0
     C                   END
     C     STNR          IFEQ      0
     C                   EXSR      FARGE
      * er ennå ikke samtaksert
     C                   MOVE      *ZEROS        XXFRA
     C                   MOVE      *OFF          *IN51
     C     STOPKO        COMP      'C'                                54         ER FAKT!!
     C                   ELSE
     C                   MOVE      *OFF          *IN54
      * er samtaksert
     C     FAKT0K        CHAIN(N)  FAKT                               35
     C   35              MOVE      *ZEROS        FABELN
     C                   Z-ADD     FABELN        XXFRA
     C                   Z-ADD(H)  STBEFD        YYFRA            11 0
     C     XXFRA         COMP      YYFRA                              5151       ER JUSTERT!
     C                   END
      * MODUS VIS REN FRA ELLER KUN FORDELT....
     C     *IN50         IFEQ      '1'
     C                   Z-ADD     STBEFD        XXBEL
     C                   ELSE
     C                   Z-ADD     XXFRA         XXBEL
     C                   END
     C     UUSPØR        IFEQ      'U'
     C     UUSPØR        OREQ      'I'
     C                   EXSR      SKUMM
     C                   END
      *
     C                   ADD       1             ZRECNO
     C                   WRITE     SUBFIL
     C                   MOVE      KUAVTA        GMAVTA            8 0
     C                   MOVE      STREKN        GMSTRE           15
     C                   MOVE      AVSMOT        GMAVMO          120
     C                   MOVE      STNR          GMSTNR            2 0
     C                   MOVE      *IN52         GM52              1
     C                   MOVE      *IN53         GM53              1
     C     UPRO2         READE     SYTR88S2                               94
     C  N94              END
      *
      * Hvis en kom inn Automatisk og IKKE fant noe - hopp ut.
     C     UTYPE         CABEQ     'A'           SLUTT
      *
     C                   Z-ADD     ZRECNO        YRECNO            5 0
     C     ZRECNO        IFGT      0
     C                   Z-ADD     1             ZRECNO
     C                   END
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     2             POSNBR
      *
     C     SKUMJN        IFEQ      'J'
     C                   MOVE      *ON           *IN30
     C                   MOVE      'JOV1307'     MSGNR
     C                   END
     C     START3        TAG
     C                   SETOFF                                       9998
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYVARE
     C                   ELSE
     C                   WRITE     SUBCTL
     C                   EXFMT     BUNN
     C                   END
      *
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C                   Z-ADD     WSSUBN        XRECNO            4 0
      *
     C                   SETOFF                                       9830
     C*** AVSLUTT
     C   03              GOTO      SLUTT
      *** FRISK OPP
     C   05              GOTO      START
      *** MODUS
     C     *IN07         IFEQ      '1'
     C     *IN50         IFEQ      '1'
     C                   MOVE      '0'           *IN50
     C                   ELSE
     C                   MOVE      '1'           *IN50
     C                   END
     C                   GOTO      START
     C                   END
      *
     C   12              GOTO      SLUTT
      *
      *** BLA VIDERE
     C   25              DO
     C                   ADD       11            ZRECNO
     C     ZRECNO        IFGT      YRECNO
     C                   Z-ADD     YRECNO        ZRECNO
     C                   END
     C                   SETOFF                                       25
     C                   GOTO      START3
     C                   END
      *
     C     ZRECNO        IFNE      0
     C                   EXSR      HENTSR
     C   98              Z-ADD     XRECNO        ZRECNO
     C                   END
     C     SAMTAK        IFEQ      'J'
     C     XCHG          OREQ      'J'
     C                   MOVE      'N'           SAMTAK
     C                   MOVE      'N'           XCHG
     C                   GOTO      START
     C                   END
     C                   GOTO      START3
      *
     C     SLUTT         TAG
      *
      *
      * Fjerne fra "Workfil" de som ikke er samtaksert.
      * N01 - SKILT UT SOM EGEN SR.
     C                   EXSR      SLETTS
      *
     C                   SETON                                        LR
     C                   RETURN
      *
     C***********************************************
     C     SKUMM         BEGSR
     C***********************************************
     C                   SETOFF                                       56
     C     FAKT1K        SETLL     FAKT
     C     HEADFK        READE(N)  FAKT                                   33
     C     *IN33         DOWEQ     '0'
     C     TRSLAG        IFEQ      'HO'
     C     FASK          ANDEQ     'I'
     C     FAKDA         ANDNE     'I'
     C                   SETON                                        56
     C                   MOVE      'J'           SKUMJN
     C                   GOTO      UTSKUM
     C                   END
     C     HEADFK        READE(N)  FAKT                                   33
     C                   END
     C     UTSKUM        ENDSR
      *
      *
     C***********************************************
     C     FARGE         BEGSR
     C***********************************************
      *
      * Indiker med farge om den bør slås sammen med foregående
      * (V/lik kunde / strekning / prod-kode )
     C     KUAVTA        IFEQ      GMAVTA
     C     STREKN        ANDEQ     GMSTRE
     C     GMSTNR        ANDEQ     *ZEROS
     C     TSTADR        IFEQ      'J'
     C     AVSMOT        ANDNE     GMAVMO
     C                   GOTO      UTADR
     C                   END
     C                   MOVE      *ON           *IN52                           VIS FAGE
      * Oppdater farge på forige (sist skrevne record) når den...
     C     GM52          IFEQ      '0'
     C     ZRECNO        CHAIN     SUBFIL                             33
     C     *IN33         IFEQ      '0'
     C                   UPDATE    SUBFIL
      * Hent på ny sist leste
     C     UPRO2         READPE    SYTR88S2                               94
     C     UPRO2         READE     SYTR88S2                               94
     C                   END
     C                   END
      * Kommet inn via auto - kunde m/samtax-avtale - sett flagg B=BLI!
     C     UTYPE         IFEQ      'A'
     C     *IN55         ANDEQ     *ON
     C                   MOVE      'B'           UTYPE
     C                   END
     C                   ELSE
     C     UTADR         TAG
      *IKKE lik sist leste
     C                   MOVE      *OFF          *IN52
      * Switch farge-indikator (N53 Hvit / 53 Gul) - Klar for neste...
     C     GM52          IFEQ      '1'
     C     GM53          IFEQ      '0'
     C                   MOVE      *ON           *IN53
     C                   ELSE
     C                   MOVE      *OFF          *IN53
     C                   END
     C                   END
     C                   END
      *
     C                   ENDSR
      *
     C***********************************************
     C     HENTSR        BEGSR
     C***********************************************
     C                   CLEAR                   VARELI
     C                   MOVE      'N'           SAMTAK            1
     C                   MOVE      'N'           XCHG              1
     C                   Z-ADD     0             LINNBR
     C                   SETOFF                                       919299
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C   98              DO
     C   99              SETOFF                                       98
     C                   Z-ADD     1             LINNBR
     C                   GOTO      SLHENT
     C                   END
      *
     C     ZRECNO        DIV       11            LINNBR
     C                   MVR                     XLIN              2 0
     C     XLIN          IFEQ      0
     C                   Z-ADD     11            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
      * 1=UTFØR SAMTAKSERING
     C     WSNR          IFEQ      '1'
     C                   EXSR      ADDSAM
     C                   END
      * 5=SE OPPDRAG
     C     WSNR          IFEQ      '5'
     C                   EXSR      SEOPD
     C                   END
      * 6=SE FAKTURA
     C     WSNR          IFEQ      '6'
     C                   EXSR      SEFAK
     C                   END
      * 7=ARB. M/OPPD-FAKTURA
     C     WSNR          IFEQ      '7'
     C                   EXSR      AROPD
     C                   END
      * X=SLETT STATUS/LØPENR = ÅPNE IGJEN
     C     WSNR          IFEQ      'X'
     C                   EXSR      SLETT
     C                   END
     C                   END
      *
     C     SLHENT        TAG
      * ER DET BEDT OM SAMTAKSERING....
     C     SAMTAK        IFEQ      'J'
     C     WSANTO        IFNE      *ZEROS
     C                   EXSR      CHGDT
     C                   ELSE
     C                   MOVE      'JOV1309'     MSGNR
     C                   SETON                                            30
     C                   END
     C                   END
      *
     C                   ADD       4             LINNBR
     C                   Z-ADD     2             POSNBR
     C                   SETOFF                                       99
     C   98              DO
     C                   MOVE      'DIV0012'     MSGNR
     C                   SETON                                            30
     C                   END
      *
     C                   ENDSR
      *
     C*
      *//////////////////////////////////////////////////////////////////////
      * SLETT STATUS                                                 CHGKB //
      *//////////////////////////////////////////////////////////////////////
     C     SLETT         BEGSR
      *
     C                   MOVE      '1'           *IN99
     C     SYNCKE        CHAIN     SYTR88S                            34
     C     *IN34         IFEQ      '0'
     C                   MOVE      'J'           XCHG
      * LEGG TILBAKE OPPRINNELIG PRIS PÅ OPPDRAGET
     C     FAKT0K        CHAIN     FAKT                               35
     C     *IN35         IFEQ      '0'
     C     FAOPKO        IFLE      'C'
     C                   Z-ADD     STOPFR        FABELV
     C                   Z-ADD     STOPFR        FABELN
     C                   ADD       0.50          FABELN
     C                   MOVE      00            FABELN
     C                   MOVE      *ZEROS        XXBEL
     C                   END
     C                   UPDATE    FAKTR
     C     HEADFK        CHAIN     HEADF                              35
     C     *IN35         IFEQ      '0'
     C                   Z-ADD     FABELN        TRFRAB
     C                   MOVE      STFRKO        TRFRAK
     C                   UPDATE    HEADFR
     C                   END
     C                   END
      * RESETT VERDIER I WORKFILE
     C                   MOVE      *ZEROS        STNR
     C                   UPDATE    SYTR88SR
     C                   END
      *
     C                   MOVE      ' '           WSNR
     C                   UPDATE    SUBFIL
      *
     C                   ENDSR
      *//////////////////////////////////////////////////////////////////////
      * VIEW ORDER                                                   CHGKB //
      *//////////////////////////////////////////////////////////////////////
     C     SEOPD         BEGSR
      *
     C                   MOVE      '1'           *IN99
     C                   MOVE      *ZEROS        DUMFLA            1 0
     C                   CALL      'SYFM21A'
     C                   PARM                    FIFIRM
     C                   PARM                    STAVD
     C                   PARM                    STOPD
     C                   PARM                    DUMFLA
      *
     C                   MOVE      ' '           WSNR
     C                   UPDATE    SUBFIL
      *
     C                   ENDSR
      *//////////////////////////////////////////////////////////////////////
      * VIEW Invoice picture                                         CHGKB //
      *//////////////////////////////////////////////////////////////////////
     C     SEFAK         BEGSR
      *
     C                   MOVE      '1'           *IN99
     C                   MOVE      *ZEROS        DUMFLA            1 0
     C                   CALL      'SYFM21B'
     C                   PARM                    FIFIRM
     C                   PARM                    STAVD
     C                   PARM                    STOPD
     C                   PARM                    DUMFLA
      *
     C                   MOVE      ' '           WSNR
     C                   UPDATE    SUBFIL
      *
     C                   ENDSR
      *//////////////////////////////////////////////////////////////////////
      * WORK WITH ORDER/INVOICE                                      CHGKB //
      *//////////////////////////////////////////////////////////////////////
     C     AROPD         BEGSR
      *
     C                   MOVE      'J'           XCHG
     C                   SETON                                        99
     C                   MOVE      '0'           UUFUNK
     C                   MOVE      '2'           UURETU
     C                   MOVE      STAVD         UUAVD
     C                   MOVE      *ZEROS        UUAVDC
     C                   MOVE      STOPD         UUOPD
     C* T = KALLER SYTR10 FRA TURSYSTEMET
     C                   CALL      'SYTR10'
     C                   PARM                    UUAVD             4 0
     C                   PARM                    UUAVDC            4 0
     C                   PARM                    UUOPD             7 0
     C                   PARM                    UUSPØR            1
     C                   PARM                    UURETU            1
     C                   PARM                    UUFUNK            1
     C*
      *
     C                   MOVE      ' '           WSNR
     C                   UPDATE    SUBFIL
      *
     C                   ENDSR
     C*
     C***********************************************
     C     ADDSAM        BEGSR
     C***********************************************
      *
     C                   SETON                                        99
      * SKIPP OPPDRAG SOM ALT ER SAMTAKSERT ELLER ALT ER FAKTURERT
     C     STNR          IFGT      0
     C     STNR          ANDLT     99
     C                   ADD       1             WSANTF
     C                   GOTO      ENDADD
     C                   END
     C     STOPKO        IFGT      'C'
     C                   ADD       1             WSANTF
     C                   GOTO      ENDADD
     C                   END
      * SKAL INNGÅ I SAMTAKSERINGSGRUNNLAGET
     C                   ADD       1             WSANTO
     C                   ADD       STOPFR        WSOPFS
     C                   ADD       STVKT         WSVKTS
     C                   ADD       STFBV         WSFBVS
     C                   ADD       STM3          WSM3S
     C                   ADD       STLM          WSLMS
     C                   ADD       STNT          WSNTS
     C     WSANTO        IFEQ      1
      * HENT NESTE SAMTAKS.NR...
     C                   CALL      'SYTR88SB'
     C                   PARM                    STTUR
     C                   PARM                    XXNR              2 0
     C                   MOVE      STKNR         WSKNRB
     C                   MOVE      STLKF         WSLKF
     C                   MOVE      STSDF         WSSDF
     C                   MOVE      STLKT         WSLKT
     C                   MOVE      STSDT         WSSDT
     C                   MOVE      STLKF         WSLKFB
     C                   MOVE      STSDF         WSSDFB
     C                   MOVE      STLKT         WSLKTB
     C                   MOVE      STSDT         WSSDTB
     C                   MOVE      STFR          WSFRB
     C                   MOVE      STKDPL        WSKDPB
     C                   END
     C                   MOVE      XXNR          STNR
      *
      *
     C     ENDADD        TAG
     C                   MOVE      ' '           WSNR
     C                   UPDATE    SUBFIL
     C                   MOVE      'J'           SAMTAK
      *
     C                   ENDSR
      *
     C***********************************************
     C     CHGDT         BEGSR
     C***********************************************
      * FORDELINGSNØKKEL BURDE KUNNE HENTES FRA INNLANS STANDARDINFO
     C                   MOVE      '4'           NØKKEL
     C                   EXSR      BERFRA
     C     STCHG         TAG
     C                   EXFMT     VARELI
     C                   MOVEA     '00000000'    *IN(83)
     C                   MOVEA     '0000'        *IN(79)
     C                   SETOFF                                       30
      *
     C   12              GOTO      UTCH
      *
      * IND 10 =  FORDEL BELØP (UTFØR SAMTAKSERING)
     C     *IN10         IFEQ      '1'
     C                   EXSR      FORDEL
     C                   GOTO      UTCH
     C                   END
      *
      * ENTER ER TASTET - NY BEREGNING HVIS BLANK
      * REKALK
     C     WSBEFS        IFEQ      *ZEROS
     C                   EXSR      BERFRA
     C                   END
     C                   GOTO      STCHG
      *
     C     UTCH          ENDSR
     C***********************************************
     C     BERFRA        BEGSR
     C***********************************************
      *
     C                   MOVE      *BLANKS       UUVAL             3
     C                   MOVE      'FRA'         UGEB              3
     C                   MOVE      'FRA'         UGEBKO            3
     C                   MOVE      *ZEROS        UUBELØ           13 2
     C                   MOVE      ' '           USKA              1
     C                   Z-ADD     WSVKTS        GRIVKT            9 0
     C     WSFBVS        IFGT      WSVKTS
     C                   Z-ADD     WSFBVS        WSVKTS
     C                   END
     C                   MOVE      HXHEAD        UHEAD           256
     C                   CALL      'SYGE14'
     C                   PARM                    UGEBKO
     C                   PARM                    UGEB
     C                   PARM                    TUAVD
     C                   PARM                    XXOPD             7 0
     C                   PARM                    KUAVTA
     C                   PARM                    UUVAL
     C                   PARM                    UUBELØ
     C                   PARM                    UAVNR             9 0
     C                   PARM                    UAVMSG           50
     C                   PARM                    USKA
     C                   PARM                    UHEAD
     C                   Z-ADD     GRIVKT        WSVKTS
      * BRUTTO??
     C     UUBELØ        IFEQ      0
     C                   MOVE      *BLANKS       USONE
     C                   MOVE      *BLANKS       UUVAL
     C                   MOVE      *ZEROS        URABAT
     C                   Z-ADD     WSVKTS        GRIVKT            9 0
     C     WSFBVS        IFGT      WSVKTS
     C                   Z-ADD     WSFBVS        WSVKTS
     C                   END
     C                   MOVE      HXHEAD        UHEAD           256
     C                   CALL      'SYGE13'
     C                   PARM                    UGEB
     C                   PARM                    TUAVD
     C                   PARM                    XXOPD
     C                   PARM                    UUVAL
     C                   PARM                    UUBELØ
     C                   PARM                    USONE             5
     C                   PARM                    URABAT            5 2
     C                   PARM                    N5                5 0
     C                   PARM                    N5                5 0
     C                   PARM                    N5                5 0
     C                   PARM                    N5                5 0
     C                   PARM                    KUAVTA
     C                   PARM                    UHEAD
n01  C                   PARM                    atdum             1
     C                   Z-ADD     GRIVKT        WSVKTS
     C                   END
      * SAMTAKSERT FRAKTBELØP....
     C                   Z-ADD     UUBELØ        WSSAFS
      * HVIS IKKE MANUELT TASTET OVERSTYRING, SÅ BENYTTES SAMTAKS-BELØP
     C     WSBEFS        IFEQ      *ZEROS
     C                   Z-ADD     WSSAFS        WSBEFS
     C* FAST KRONETILLEGG PR OPPDRAG
     C     WSANTO        MULT(H)   KRTILL        XKRTIL            5 0
     C                   ADD       XKRTIL        WSBEFS
     C                   END
     C                   ENDSR
      *
     C***********************************************
     C     FORDEL        BEGSR
     C***********************************************
      *
     C     1             DO        9000          ZRECNO
     C     ZRECNO        CHAIN     SUBFIL                             34
     C   34              LEAVE
      *
     C     STNR          IFEQ      XXNR
     C     SYNCKE        CHAIN     SYTR88S                            34
     C     *IN34         IFEQ      '0'
     C                   MOVE      XXNR          STNR
     C                   MOVE      WSANTO        STANTO
     C                   MOVE      WSANTF        STANTF
     C                   MOVE      WSVKTS        STVKTS
     C                   MOVE      WSFBVS        STFBVS
     C                   MOVE      WSM3S         STM3S
     C                   MOVE      WSLMS         STLMS
     C                   MOVE      WSNTS         STNTS
     C                   MOVE      WSKNRB        STKNRB
     C                   MOVE      WSLKFB        STLKFB
     C                   MOVE      WSSDFB        STSDFB
     C                   MOVE      WSLKTB        STLKTB
     C                   MOVE      WSSDTB        STSDTB
     C                   MOVE      WSFRB         STFRB
     C                   MOVE      WSKDPB        STKDPB
     C                   MOVE      WSOPFS        STOPFS
     C                   MOVE      WSSAFS        STSAFS
     C                   MOVE      WSBEFS        STBEFS
     C                   MOVE      *ZEROS        FAKTOR           11 8
     C                   SELECT
     C     NØKKEL        WHENEQ    '4'                                           VERDI
     C     STOPFS        ifne      *ZEROS
     C     STOPFR        DIV       STOPFS        FAKTOR           11 8
     c                   end
     C     NØKKEL        WHENEQ    '2'                                           M3
     C     WSM3S         ifne      *ZEROS
     C     STM3          DIV       WSM3S         FAKTOR           11 8
     c                   end
     C     NØKKEL        WHENEQ    '3'                                           LM
     C     WSLMS         ifne      *ZEROS
     C     STLM          DIV       WSLMS         FAKTOR           11 8
     c                   end
     C                   OTHER                                                   F.VEKT
     C     WSFBVS        IFNE      *ZEROS
     C     STFBV         DIV       WSFBVS        FAKTOR           11 8
     C                   END
     C                   ENDSL
     c     Faktor        ifeq      *zeros
     C                   move      *zeros        STOAND
     C                   move      *zeros        STBEFD
     C                   else
     C     FAKTOR        MULT      100           STOAND
     C     WSBEFS        MULT      FAKTOR        STBEFD
     C                   end
     C                   UPDATE    SYTR88SR
      * oppdater kun på de oppdrag der beregnet beløp (faktor)  var ulikt 0
     c     STBEFD        ifne      *zeros
     C     FAKT0K        CHAIN     FAKT                               35
     C     *IN35         IFEQ      '0'
     C     FAOPKO        IFLE      'C'
     C                   Z-ADD     STBEFD        FABELV
     C                   Z-ADD     STBEFD        FABELN
N03  C     FACURI        ifeq      '   '
N03  C     FACURI        oreq      FICURR
N03  c     FIDECI        ifne      2
s03  C*                  ADD       0.50          FABELN
s03  C*                  MOVE      00            FABELN
N03  c                   exsr      deci
N03  C                   end
N03  C                   end
     C                   UPDATE    FAKTR
     C     HEADFK        CHAIN     HEADF                              35
     C     *IN35         IFEQ      '0'
     C                   Z-ADD     FABELN        TRFRAB
     C                   MOVE      'A'           TRFRAK
     C                   UPDATE    HEADFR
     C                   END
     C                   ELSE
     C                   UPDATE    FAKTR
     C                   END
     C                   END
     C                   END
     C                   END
     C                   END
      *
     C                   UPDATE    SUBFIL
      *
     C                   ENDDO
      *
     C                   ENDSR
      *
      *
     C***********************************************
     C     LESOPD        BEGSR
     C***********************************************
     C                   MOVE      UPRO          UPRO2
     C* T = KALLER SYTR10 FRA TURSYSTEMET / U = UTGÅENDE INNLAND..
N05   * S = KALL FRA SPLITTET OPPDRAG
N05  C                   IF        UTYPE = 'S'
N05   * lagres med nevgativt fortegn i SYTR88S for å hindre konflikt med turtelleverg
N05  C                   EVAL      UPRO2 = UPRO2 * -1
N05  C                   EVAL      FSKODE = 'SPO'
N05  C                   EVAL      FSSOK  = UPRO
N05  C     FRI01KEY      CHAIN     FRISOL01
N05  C                   IF        %found
N05  C     FSHEAKEY      CHAIN(N)  HEADF
N05  C                   ENDIF
N05  C                   IF        not %found(FRISOL01) OR
N05  C                             not %found(HEADF)
N05  c                   EVAL      *INLR = *ON
N05  c                   RETURN
N05  C                   ENDIF
N05  C                   MOVE      'S'           UUSPØR
N05  C                   EVAL      TUPRO  = UPRO2
N05  C                   EVAL      TURACC = TRSDFDCC
N05  C                   EVAL      TURAAR = TRSDFDAA
N05  C                   EVAL      TURMND = TRSDFDM
N05  C                   MOVE      TRSDFDD       WSDTD
N05  C                   MOVE      TRSDFDM       WSDTM
N05  C                   MOVE      TRSDFDÅ       WSDTÅ
N05  C                   MOVE      TRSDTDD       WSDTTD
N05  C                   MOVE      TRSDTDM       WSDTTM
N05  C                   MOVE      TRSDTDÅ       WSDTTÅ
N05  C                   ELSE
     C     UPRO2         CHAIN     TURER14                            33
     C                   MOVE      'T'           UUSPØR
     C     KODAK         CHAIN     KODTA                              33
     C     *IN33         IFEQ      '0'
     C     KOAIE         IFEQ      '1'
     C     KOAIE         OREQ      '2'
     C                   MOVE      'U'           UUSPØR
     C                   END
     C                   END
     C                   MOVE      TUDTD         WSDTD
     C                   MOVE      TUDTM         WSDTM
     C                   MOVE      TUDTÅ         WSDTÅ
     C                   MOVE      TUDTTD        WSDTTD
     C                   MOVE      TUDTTM        WSDTTM
     C                   MOVE      TUDTTÅ        WSDTTÅ
N05  C                   ENDIF
      * FJERN GAMLE POSTER SOM IKKE ALT ER SAMTAKSERT
     C                   EXSR      SLETTS
      *
      * AKKUMULER VERDIER FOR UFERDIGE SAMTAKSERINGER / LEGG UT POSTER
N05   * Setll
N05  C                   IF        UTYPE = 'S'
N05  C     FRI01KEY      SETLL     FRISOL01
N05  C                   ELSE
     C     UPRO2         SETLL     HEAD14
N05  C                   ENDIF
N05   * reade
N05  C                   IF        UTYPE = 'S'
N05  C     FRI01KEY      READE     FRISOL01                               33
N05  C                   ELSE
     C     UPRO2         READE     HEAD14                                 33
N05  C                   ENDIF
N05   *
     C     *IN33         DOWEQ     '0'
N05  C                   IF        UTYPE = 'S'
N05  C     FSHEAKEY      CHAIN(N)  HEADF
N05  C                   IF        not %found(HEADF)
N05  C                   GOTO      NXTPOS
N05  C                   ENDIF
N05  C                   ENDIF
     C                   CLEAR                   SYTR88SR
     C                   MOVE      TUPRO         STTUR
     C                   MOVE      HEAVD         STAVD
     C                   MOVE      HEOPD         STOPD
     C     SYNCKE        CHAIN     SYTR88S                            34
     C     *IN34         IFEQ      '1'
      * STKNR ER IKKE LENGER DEL AV KEY - FLYTTET INN I  *IN34 IFEQ '1'
S04  C*    HEKDFS        IFEQ      'X'
N04  C     TRKDFF        ifeq      'K'
     C                   MOVE      HEKNKF        STKNR
     C                   MOVEL     HENAS         STAMNA
     C                   MOVEL     HEADS1        STAMA1
     C                   MOVEL     HEADS2        STAMA2
     C                   MOVEL     HEADS3        STAMA3
     C                   ELSE
     C                   MOVE      HEKNSF        STKNR
     C                   MOVEL     HENAK         STAMNA
     C                   MOVEL     HEADK1        STAMA1
     C                   MOVEL     HEADK2        STAMA2
     C                   MOVEL     HEADK3        STAMA3
     C                   END
      *  ER DEN ALT FAKTURERT...
     C     FAKT0K        CHAIN(N)  FAKT                               35
      * hvis 0-linjen er kreditert flytt den
N02  C     *in35         ifeq      '0'
N02  C     faopko        andeq     'O'
N02  C     fakda         andeq     'C'
N02  C                   exsr      flytt0
N02  C                   end
      *
     C   35              MOVE      *ZEROS        FABELN
     C   35              MOVE      ' '           FAOPKO
     C                   Z-ADD     FABELN        STOPFR
     C                   MOVEL     FAOPKO        STOPKO
     C                   MOVEL     TRFRAK        STFRKO
     C                   Z-ADD     HEVKT         STVKT
     C                   Z-ADD     HEFBV         STFBV
     C                   Z-ADD     HEM3          STM3
     C                   Z-ADD     HELM          STLM
     C                   Z-ADD     HENT          STNT
     C                   MOVE      HELKA         STLKF
     C                   MOVE      HESDF         STSDF
     C                   MOVE      HETRI         STLKT
     C                   MOVE      HESDT         STSDT
     C                   MOVE      HEFR          STFR
     C                   MOVE      HEKDPL        STKDPL
     C                   MOVE      HEFR          STFR
     C                   MOVEL     TURACC        STTUÅR
     C                   MOVE      TURAAR        STTUÅR
     C                   MOVE      TURMND        STTUMN
     C                   WRITE     SYTR88SR
     C                   ELSE
     C                   UPDATE    SYTR88SR
     C                   END
      *
      *
     C     NXTPOS        TAG
N05   * reade
N05  C                   IF        UTYPE = 'S'
N05  C     FRI01KEY      READE     FRISOL01                               33
N05  C                   ELSE
     C     UPRO2         READE     HEAD14                                 33
N05  C                   ENDIF
     C  N33              ENDDO
      *
     C                   ENDSR
      *
     C***********************************************
     C     SLETTS        BEGSR
     C***********************************************
      * SLETT POSTER SOM IKKE ALT ER SAMTAKSERT (N01 SKILT UT SOM SR)
     C     UPRO2         SETLL     SYTR88S
     C     UPRO2         READE     SYTR88S                                33
     C     *IN33         DOWEQ     '0'
     C     STNR          IFEQ      0
     C     STNR          OREQ      99
     C                   DELETE    SYTR88SR
     C                   END
     C     UPRO2         READE     SYTR88S                                33
     C  N33              ENDDO
      *
     C                   ENDSR
      *
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     SYNCKE        KLIST
     C                   KFLD                    STTUR
     C                   KFLD                    STAVD
     C                   KFLD                    STOPD
     C*
     C     FAKT0K        KLIST
     C                   KFLD                    STAVD
     C                   KFLD                    STOPD
     C                   KFLD                    NULLIN            5 0
     C                   MOVE      *ZEROS        NULLIN            5 0
     C     FAKT1K        KLIST
     C                   KFLD                    STAVD
     C                   KFLD                    STOPD
     C                   KFLD                    ETTLIN            5 0
     C                   Z-ADD     1             ETTLIN            5 0
     C*
     C     HEADFK        KLIST
     C                   KFLD                    STAVD
     C                   KFLD                    STOPD
N05  C     FSHEAKEY      KLIST
N05  C                   KFLD                    FSAVD
N05  C                   KFLD                    FSOPD
     C*
     C     KODAK         KLIST
     C                   KFLD                    KOAUNI
     C                   KFLD                    TUAVD
     C                   MOVE      'A'           KOAUNI
     C     ST2KF         KLIST
     C                   KFLD                    WSLKFB
     C                   KFLD                    WSSDFB
     C     ST2KT         KLIST
     C                   KFLD                    WSLKTB
     C                   KFLD                    WSSDTB
     C*
     C     KUNKEY        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    STKNR
N05  C     Fri01Key      KLIST
N05  C                   KFLD                    FSKODE
N05  C                   KFLD                    FSSOK
      *
      * PARAMETRE I KALL TIL SYNC04 SOM HAR FASTE/DUMMY VERDIER
     C                   MOVE      'H'           XXUR
     C                   MOVE      *BLANKS       XXLKS
     C                   MOVE      *BLANKS       XXSDFF
     C                   MOVE      *BLANKS       XXLKK
     C                   MOVE      *BLANKS       XXSDVT
     C                   MOVE      *BLANKS       XXVERV
     C                   MOVE      *ZEROS        XXVERB
      *
     C     *ENTRY        PLIST
     C                   PARM                    UPRO              8
     C                   PARM                    UTYPE             1             A=AUTO
      *
     C                   MOVE      'N'           TSTADR            1
     C                   MOVE      *ZEROS        KRTILL            5 0
      *
     C     *LOVAL        SETLL     FIRM
     C                   READ      FIRM                                   33
      *
     C                   WRITE     WINDOWSZ
      *
     C                   ENDSR
      *                                                                         stb
      *
N02  C***********************************************
N02  C     FLYTT0        BEGSR
N02  C***********************************************
N02  C* finner ny fali og skriver til fil
N02  C                   CALL      'SYGE06'
N02  C                   PARM                    FAAVD
N02  C                   PARM                    FAOPD
N02  C                   PARM                    FALI
N02  C                   WRITE     faktr
N02  C* frigjør record nummer 0
N02  C     FAKT0K        CHAIN     FAKT                               35
N02  C     *IN35         ifeq      '0'
N02  C                   MOVE      *ZEROS        FAFAKT
N02  C                   MOVE      ' '           FAOPKO
N02  C                   MOVE      *ZEROS        FAJOUR
N02  C                   MOVE      *ZEROS        FACC
N02  C                   MOVE      *ZEROS        FAÅR
N02  C                   MOVE      *ZEROS        FAMND
N02  C                   MOVE      *ZEROS        FAFRBN
N02  C                   MOVE      *ZEROS        FADATO
N02  C                   MOVE      *ZEROS        FADAFF
N02  C                   MOVE      'M'           FAKDA
N02  C                   UPDATE    FAKTR
N02  C                   end
N02  C                   ENDSR
N03   ***********************************************
N03  C     DECI          BEGSR
N03   ***********************************************
N03   * TEST FIRMS NUMBER OF DECIMALS
N03   *
N03   * NO DECIMALS
N03  C     FIDECI        IFEQ      0
N03  C                   Z-ADD(H)  FABELN        FABEL0           11 0
N03  C                   Z-ADD     FABEL0        FABELN
N03  C                   ELSE
N03   * 1 DECIMAL
N03  C     FIDECI        IFEQ      1
N03  C                   Z-ADD(H)  FABELN        FABEL1           12 1
N03  C                   Z-ADD     FABEL1        FABELN
N03  C                   END
N03  C                   END
N03   *
N03  C                   ENDSR
N03   *
