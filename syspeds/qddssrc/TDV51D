     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      REF(*LIBL/FIR01L)
     A                                      PRINT
     A                                      HLPTITLE('General Help')
     A                                      HLPPNLGRP('TDV5110' TDV51PNL)
     A                                      ALTHELP
     A                                      HELP
     A*
     A          R TDV51B10
     A                                      CF03(03 'Exit')
     A                                      CF09(09 'Select new participant ID')
     A                                      CF10(10 'Send Request')
     A                                      OVERLAY
     A          H                           HLPPNLGRP('TDV5111' TDV51PNL)
     A                                      HLPARA(06 001 08 080)
     A          H                           HLPPNLGRP('TDV5112' TDV51PNL)
     A                                      HLPARA(09 001 11 080)
     A          H                           HLPPNLGRP('TDV5113' TDV51PNL)
     A                                      HLPARA(12 001 12 080)
     A          H                           HLPPNLGRP('TDV5114' TDV51PNL)
     A                                      HLPARA(13 001 13 080)
     A          H                           HLPPNLGRP('TDV5115' TDV51PNL)
     A                                      HLPARA(14 001 17 080)
     A          H                           HLPPNLGRP('TDV5116' TDV51PNL)
     A                                      HLPARA(18 001 20 080)
     A          H                           HLPPNLGRP('TDV5117' TDV51PNL)
     A                                      HLPARA(21 001 21 080)
     A          H                           HLPPNLGRP('TDV5110' TDV51PNL)
     A                                      HLPARA(01 001 22 080)
     A          H                           HLPPNLGRP('TDV5199' TDV51PNL)
     A                                      HLPARA(23 001 24 080)
     A            MSGNR          7A  P
     A                                  1  2'TDV51R'
     A                                  1 26'Schedule Information Request'
     A                                      DSPATR(HI)
     A                                  1 66'Date:'
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A                                  2  2USER
     A            DEFADR        17   O  4 53DSPATR(HI)
     A                                  2 36'Details'
     A                                      DSPATR(HI)
     A                                  2 66'Time:'
     A                                  2 72TIME
     A                                  4  2'Enter information and press F10 to-
     A                                       send Request to'
     A                                      COLOR(BLU)
     A                                  6  2'Requested Routing'
     A                                  7  4'Origin Airport/City Code . . . .'
     A            I0231     R        B  7 38DSPATR(HI)
     A  31                                  DSPATR(PC)
     A                                  8  4'Destination Airport/City Code  .'
     A            I0232     R        B  8 38DSPATR(HI)
     A  32                                  DSPATR(PC)
     A                                  9  2'Date or Month'
     A                                 10  4'Day of Month . . . . . . . . . .'
     A            I0251     R        B 10 38EDTCDE(4)
     A                                      RANGE(0 31)
     A  35                                  DSPATR(RI)
     A  35                                  DSPATR(PC)
     A                                 11  4'Month  . . . . . . . . . . . . .'
     A            I0252     R        B 11 38DSPATR(HI)
     A  33                                  DSPATR(PC)
     A                                 12  2'ICAO Carrier Code  . . . . . . . .'
     A            I027      R        B 12 38
     A                                 13  2'Earliest Departure Time  . . . . .'
     A            I0291     R        B 13 38EDTCDE(4)
     A                                      RANGE(0 2400)
     A                                 14  2'Latest Arrival Date/Time'
     A                                 15  4'Day of Month . . . . . . . . . .'
     A            I02111    R        B 15 38EDTCDE(4)
     A                                      RANGE(0 31)
     A  36                                  DSPATR(RI)
     A  36                                  DSPATR(PC)
     A                                 16  4'Month  . . . . . . . . . . . . .'
     A            I02112    R        B 16 38
     A  36                                  DSPATR(RI)
     A                                 17  4'Time . . . . . . . . . . . . . .'
     A            I02113    R        B 17 38EDTCDE(4)
     A                                 18  2'Connection Restriction'
     A                                 19  4'Indicator  . . . . . . . . . . .'
     A            I02131    R        B 19 38
     A  34                                  DSPATR(RI)
     A  34                                  DSPATR(PC)
     A                                 20  4'Airport/City Code  . . . . . . .'
     A            I02132    R        B 20 38
     A  34                                  DSPATR(RI)
     A                                 21  2'Aircraft Possibility Code  . . . .'
     A            I0215     R        B 21 38
     A                                 23  1'F3=Exit'
     A                                      COLOR(BLU)
     A                                 23 23'F9=Select new part.ID'
     A                                      COLOR(BLU)
     A                                 23 63'F10=Send Request'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  1MSGID(&MSGNR SMAMSGF)
     A                                      COLOR(WHT)
     A          R TDV51B20
     A                                      CF03(03 'Exit')
     A                                      CF09(09 'Select new participant ID')
     A                                      CF10(10 'Send Request')
     A                                      OVERLAY
     A          H                           HLPPNLGRP('TDV5199' TDV51PNL)
     A                                      HLPARA(23 001 24 080)
     A                                 23  1'F3=Exit'
     A                                      COLOR(BLU)
     A                                 23 65'F12=Cancel'
     A                                      COLOR(BLU)
     A            MSGNR          7A  P
     A  30        MSGTXT        78A  O 24  1MSGID(&MSGNR SMAMSGF)
     A                                      COLOR(WHT)
     A          R TDV51H30                  SFL
     A                                      SFLNXTCHG
     A            SFRRN         10S 0H
     A            RECNUM        10S 0H
     A            LGRECN         9S 0H
     A            DATA          69   O  7  2TEXT('Message text')
     A          R TDV51K40                  SFLCTL(TDV51H30)
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                      CF03(03 'Exit')
     A                                      CA12(12 'Cancel')
     A                                      OVERLAY
     A                                      PUTOVR
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A                                      SFLSIZ(0014)
     A                                      SFLPAG(0014)
     A  68                                  SFLMSGID(SMA0032 *LIBL/SMAMSGF)
     A  33                                  SFLMSGID(BBA0029 *LIBL/SYMSGF)
     A  34                                  SFLMSGID(BBA0030 *LIBL/SYMSGF)
     A  35                                  SFLMSGID(BBA0031 *LIBL/SYMSGF)
     A          H                           HLPPNLGRP('TDV5140' TDV51PNL)
     A                                      HLPARA(01 001 22 080)
     A          H                           HLPPNLGRP('TDV5199' TDV51PNL)
     A                                      HLPARA(23 001 24 080)
     A                                  1  3'TDV51'
     A                                  1 25'Schedule Information Request'
     A                                      DSPATR(HI)
     A                                  1 61'Date:'
     A                                  1 67DATE
     A                                      EDTCDE(Y)
     A                                  2  3USER
     A                                  2 31'Answer Header'
     A                                      DSPATR(HI)
     A                                  2 61'Time:'
     A                                  2 67TIME
     A            ZRECNO         4S 0O  2 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  4  2'No Answer Details can be found. Pl-
     A                                      ease read message received'
     A                                      COLOR(BLU)
     A                                  6  2'Message'
     A                                      DSPATR(HI)
     A          R TDV51H50                  SFL
     A                                      SFLNXTCHG
     A            SFRRN         10S 0H
     A            RECNUM        10S 0H
     A            LGRECN         9S 0H
     A            WSNR           1A  B 13  4VALUES(' ' '1' '2' '3')
     A            V043      R        O 13  7REFFLD(FIA04R/V043 FIA04F)
     A            V044      R        O 13 11REFFLD(FIA04R/V044 FIA04F)
     A            V046      R        O 13 18REFFLD(FIA04R/V046 *LIBL/FIA04F)
     A            V047      R        O 13 24REFFLD(FIA04R/V047 *LIBL/FIA04F)
     A            V049      R        O 13 31REFFLD(FIA04R/V049 *LIBL/FIA04F)
     A                                      EDTWRD('  .  ')
     A            V0411     R        O 13 43REFFLD(FIA04R/V0411 *LIBL/FIA04F)
     A            V0413     R        O 13 51REFFLD(FIA04R/V0413 *LIBL/FIA04F)
     A                                      EDTWRD('  .  ')
     A            V0415     R        O 13 62REFFLD(FIA04R/V0415 *LIBL/FIA04F)
     A            V0417     R        O 13 67REFFLD(FIA04R/V0417 *LIBL/FIA04F)
     A            V0419     R        O 13 73REFFLD(FIA04R/V0419 *LIBL/FIA04F)
     A          R TDV51K60                  SFLCTL(TDV51H50)
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                      CF03(03 'Exit')
     A                                      CA12(12 'Cancel')
     A                                      OVERLAY
     A                                      PUTOVR
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A                                      SFLSIZ(0010)
     A                                      SFLPAG(0009)
     A  68                                  SFLMSGID(SMA0032 *LIBL/SMAMSGF)
     A  33                                  SFLMSGID(BBA0029 *LIBL/SYMSGF)
     A  34                                  SFLMSGID(BBA0030 *LIBL/SYMSGF)
     A  35                                  SFLMSGID(BBA0031 *LIBL/SYMSGF)
     A          H                           HLPPNLGRP('TDV5160' TDV51PNL)
     A                                      HLPARA(01 001 22 080)
     A          H                           HLPPNLGRP('TDV5199' TDV51PNL)
     A                                      HLPARA(23 001 24 080)
     A                                  1  3'TDV51'
     A                                  1 25'Schedule Information Request'
     A                                      DSPATR(HI)
     A                                  1 61'Date:'
     A                                  1 67DATE
     A                                      EDTCDE(Y)
     A                                  2  3USER
     A                                  2 31'Request Details'
     A                                      DSPATR(HI)
     A                                  2 61'Time:'
     A                                  2 67TIME
     A            ZRECNO         4S 0O  2 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  4  3'Frm To   Depart.   Carr E.Dep   La-
     A                                      test Arrivial  Con.Rest Conect. Pos-
     A                                      sibi'
     A                                      DSPATR(HI)
     A                                  5 12'Day Mth   Code Time    Day Month  -
     A                                      Time   Code    Airport Code'
     A                                      DSPATR(HI)
     A            V0231     R        O  6  3REFFLD(FIA00R/V0231 *LIBL/FIA00F)
     A            V0232     R        O  6  7REFFLD(FIA00R/V0232 *LIBL/FIA00F)
     A            V0251     R        O  6 12REFFLD(FIA00R/V0251 *LIBL/FIA00F)
     A            V0252     R        O  6 16REFFLD(FIA00R/V0252 *LIBL/FIA00F)
     A            V027      R        O  6 22REFFLD(FIA00R/V027 *LIBL/FIA00F)
     A            V0291     R        O  6 27REFFLD(FIA00R/V0291 *LIBL/FIA00F)
     A            V02111    R        O  6 35REFFLD(FIA00R/V02111 *LIBL/FIA00F)
     A            V02112    R        O  6 40REFFLD(FIA00R/V02112 *LIBL/FIA00F)
     A            V02113    R        O  6 46REFFLD(FIA00R/V02113 *LIBL/FIA00F)
     A            V02131    R        O  6 55REFFLD(FIA00R/V02131 *LIBL/FIA00F)
     A            V02132    R        O  6 62REFFLD(FIA00R/V02132 *LIBL/FIA00F)
     A            V0215     R        O  6 70REFFLD(FIA00R/V0215 *LIBL/FIA00F)
     A                                  8  2'Select up to three routings with 1-
     A                                      ,2,3 and press Enter'
     A                                      COLOR(BLU)
     A                                 10 31'Answer Details'
     A                                      DSPATR(HI)
     A                                 11  3'Opt  Flight    From  To   Time of -
     A                                         Change   Time of   Change Type S-
     A                                      tops'
     A                                      DSPATR(HI)
     A                                 12 29'departure    Ind.   arrivial    In-
     A                                      d.'
     A                                      DSPATR(HI)
