     H DATEDIT(*YMD)
     FFIRM      IF   E           K DISK
     FEUNH      IF   E           K DISK
     FEDIM      UF   E           K DISK
     FZTELL     UF   E           K DISK
     FEDTM96A   IF   E           K DISK
     FELIN96A   IF   E           K DISK
     FELOC96A   IF   E           K DISK
     FEMEA96A   IF   E           K DISK
     FEMOA96A   IF   E           K DISK
     FEPIA96A   IF   E           K DISK
     FEQTY96A   IF   E           K DISK
     FERFF96A   IF   E           K DISK
     FKODTVAL2  IF   E           K DISK
     FZADF      O  A E             DISK
     FZADV      O  A E             DISK
     D A               S              1    DIM(79)
     D B               S              1    DIM(18)
      *________________________
      * DS for decimal handling
      *""""""""""""""""""""""""
     D                 DS
     D  ZDEC                   1      7  6
     D  ZDEC6                  2      7
     D  ZHEL                  11     22  0
     D                 DS
     D  SVREFF                 1     17
     D  SFREFF                 1     17
     D                 DS
     D  SVUNIK                 1      9  0
     D  SFUNIK                 1      9  0
      */////////////
      * MAIN LOGIC /
      */////////////
      *___________
      * Initiering
      *"""""""""""
     C                   EXSR      INIT
     C                   IF        *IN20 = *OFF
      *________
      * CONVERT
      *""""""""
     C                   EXSR      RTVD
      *___________
      * UPDATE LOG
      *"""""""""""
     C                   MOVE      'C'           MST
     C                   UPDATE    EDIMR
     C                   END
      *
     C                   SETON                                        LR
     C                   RETURN
      *
      *////////////////////////////////////////////////////////////
      *                                                      INIT /
      *////////////////////////////////////////////////////////////
     C     INIT          BEGSR
     C     KODTVAL2K     KLIST
     C                   KFLD                    SFVK28
     C                   KFLD                    XDTMAX
     C                   Z-ADD     99999999      XDTMAX            8 0
     C     KEYMS0        KLIST
     C                   KFLD                    ZMN
     C                   KFLD                    ZGN0
     C     KEYMS1        KLIST
     C                   KFLD                    ZMN
     C                   KFLD                    ZGN1
     C                   KFLD                    ZREPB0
     C     *LIKE         DEFINE    ZMN           ZMMN
     C     *LIKE         DEFINE    DMN           ZMN
     C     *LIKE         DEFINE    DGN           ZGN0
     C     *LIKE         DEFINE    DGN           ZGN1
     C     *LIKE         DEFINE    DREP0         ZREPB0
      *
     C     *ENTRY        PLIST
     C                   PARM                    UMNA              9
      *
     C                   MOVE      UMNA          ZMN
     C     ZMN           CHAIN     EUNH                               20
     C  N20              Z-ADD     ZMN           ZMMN
     C  N20ZMMN          CHAIN     EDIM                               20
      *
     C     *LOVAL        SETLL     FIRM
     C                   READ      FIRM                                   33
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      * RETREIVE INFO FROM EDIFACT DATABASE TO SYSPED400     RTVD /
      *////////////////////////////////////////////////////////////
     C     RTVD          BEGSR
      *
     C     FIFIRM        CHAIN     ZTELL                              33
     C                   Z-ADD     ZUNIK         SFUNIK
     c                   ADD       1             ZUNIK
     c                   UPDATE    ZTELLR
     C                   MOVEL     U1004         SFREFF
     C                   MOVEL     U1004         SFTXT                          FAKTURANR
      *___________________
      * READ MAIN SEGMENTS
      *"""""""""""""""""""
      * DTM00
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     0             ZGN0
     C     KEYMS0        SETLL     EDTM96A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS0        READE     EDTM96A                                51
     C     *IN51         DOWEQ     '0'
      *
     C                   MOVEA     D2380         A
     C                   EXSR      GETNUM
     C                   SELECT
     C     D2005         WHENEQ    '3  '                                        FAKTURADATO
     C                   Z-ADD     ZNUM          SFDT
     C                   LEAVE
     C                   ENDSL
      *
     C     KEYMS0        READE     EDTM96A                                51
     C  N51              ENDDO
     C                   ENDIF
      * RFF01
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     1             ZGN0
     C     KEYMS0        SETLL     ERFF96A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS0        READE     ERFF96A                                51
     C     *IN51         DOWEQ     '0'
      *
     C                   SELECT
     C     R1153         WHENEQ    'IV'
     C                   MOVEL     R1154         SFTXT
     C                   LEAVE
     C                   ENDSL
      *
     C     KEYMS0        READE     ERFF96A                                51
     C  N51              ENDDO
     C                   ENDIF
      *_________
      * READ LIN
      *"""""""""
     C                   MOVE      '0'           *IN51
     C                   Z-ADD     25            ZGN0
     C     KEYMS0        SETLL     ELIN96A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS0        READE     ELIN96A                                51
     C     *IN51         DOWEQ     '0'
      *
     C                   EXSR      RTVDB
      *
     C     KEYMS0        READE     ELIN96A                                51
     C  N51              ENDDO
     C                   ENDIF
      *
     C     KODTVAL2K     SETGT     KODTVAL2
     C     SFVK28        READPE    KODTVAL2                               33
     C  N33              Z-ADD     KVAOMR        SFOM28
     C  N33              Z-ADD     KVAKRS        SFKR28
     C                   WRITE     SADFR
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      * RETREIVE INFO FROM EDIFACT DATABASE TO SYSPED400    RTVDB /
      *////////////////////////////////////////////////////////////
     C     RTVDB         BEGSR
      * LINJENR
     C                   ADD       1             SVLI
     C                   MOVE      '1'           SVVF
      *__________________
      * READ ALL SEGMENTS
      *""""""""""""""""""
     C                   Z-ADD     LREP0         ZREPB0
     C                   EXSR      PIA25
     C                   EXSR      MEA25
     C                   EXSR      QTY25
     C                   EXSR      MOA26
     C                   EXSR      LOC32
      *
     C                   WRITE     SADVR
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      * MEA25                                               MEA25 /
      *////////////////////////////////////////////////////////////
     C     MEA25         BEGSR
     C                   MOVE      '0'           *IN52
     C                   Z-ADD     25            ZGN1
     C     KEYMS1        SETLL     EMEA96A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS1        READE     EMEA96A                                52
     C     *IN52         DOWEQ     '0'
      *
     C                   MOVEA     M6314         A
     C                   EXSR      GETNUM
     C                   IF        ZNUM < 1
     C                   Z-ADD     1             SVVKTN
     C                   ELSE
     C                   Z-ADD(H)  ZNUM          SVVKTN
     C                   END
     C                   EVAL(H)   SVVKTB = SVVKTN * 1,1
     C                   LEAVE
      *
     C     KEYMS1        READE     EMEA96A                                52
     C  N52              ENDDO
     C                   ENDIF
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      * PIA25                                               PIA25 /
      *////////////////////////////////////////////////////////////
     C     PIA25         BEGSR
     C                   MOVE      '0'           *IN52
     C                   Z-ADD     25            ZGN1
     C     KEYMS1        SETLL     EPIA96A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS1        READE     EPIA96A                                52
     C     *IN52         DOWEQ     '0'
      *
     C                   MOVEL     P7140B        SVVNT
     C                   LEAVE
      *
     C     KEYMS1        READE     EPIA96A                                52
     C  N52              ENDDO
     C                   ENDIF
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      * QTY25                                               QTY25 /
      *////////////////////////////////////////////////////////////
     C     QTY25         BEGSR
     C                   MOVE      '0'           *IN52
     C                   Z-ADD     25            ZGN1
     C     KEYMS1        SETLL     EQTY96A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS1        READE     EQTY96A                                52
     C     *IN52         DOWEQ     '0'
      *
     C                   SELECT
     C     Q6063         WHENEQ    '47'
     C                   MOVEA     Q6060         A
     C                   EXSR      GETNUM
     C                   Z-ADD     ZNUM          SVNTM
     C                   LEAVE
     C                   ENDSL
      *
     C     KEYMS1        READE     EQTY96A                                52
     C  N52              ENDDO
     C                   ENDIF
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      * MOA26                                               MOA26 /
      *////////////////////////////////////////////////////////////
     C     MOA26         BEGSR
     C                   MOVE      '0'           *IN52
     C                   Z-ADD     26            ZGN1
     C     KEYMS1        SETLL     EMOA96A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS1        READE     EMOA96A                                52
     C     *IN52         DOWEQ     '0'
      *
     C                   SELECT
     C     M5025         WHENEQ    '40'
     C                   MOVEA     M5004         A
     C                   EXSR      GETNUM
     C                   Z-ADD     ZNUM          SVBELT
     C                   ADD       ZNUM          SFBL28
     C                   MOVEL     M6345         SFVK28
     C                   LEAVE
     C                   ENDSL
      *
     C     KEYMS1        READE     EMOA96A                                52
     C  N52              ENDDO
     C                   ENDIF
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      * LOC32                                               LOC32 /
      *////////////////////////////////////////////////////////////
     C     LOC32         BEGSR
     C                   MOVE      '0'           *IN52
     C                   Z-ADD     32            ZGN1
     C     KEYMS1        SETLL     ELOC96A                                20
     C     *IN20         IFEQ      '1'
     C     KEYMS1        READE     ELOC96A                                52
     C     *IN52         DOWEQ     '0'
      *
      * TESTER PÅ FRAKTBREVNUMMER
      *
     C                   SELECT
     C     L3227         WHENEQ    '27'
     C                   MOVEL     L3225         SVLK
     C                   LEAVE
     C                   ENDSL
      *
     C     KEYMS1        READE     ELOC96A                                52
     C  N52              ENDDO
     C                   ENDIF
      *
     C                   ENDSR
      *
      *////////////////////////////////////////////////////////////
      *  Get numerical value                               GETNUM /
      *////////////////////////////////////////////////////////////
     C     GETNUM        BEGSR
     C                   MOVE      *ZEROS        ZHEL
     C                   MOVE      *ZEROS        ZDEC
     C                   MOVE      *ZEROS        B
     C                   IF        A(1) = '-'
     C                   Z-SUB     1             XFORTEGN
     C                   Z-ADD     2             AX
     C                   ELSE
     C                   Z-ADD     1             XFORTEGN          1 0
     C                   Z-ADD     1             AX                2 0
     C                   END
     C                   Z-ADD     1             BX                2 0
      * Get Integer
     C     A(AX)         DOWGE     '0'
     C     ZHEL          MULT      10            ZHEL
     C                   MOVE      A(AX)         ZHEL
     C                   ADD       1             AX
     C                   ENDDO
      * Decimal point ?
     C     A(AX)         IFEQ      '.'
     C     A(AX)         OREQ      ','
     C                   ADD       1             AX
     C     A(AX)         DOWGE     '0'
     C                   MOVE      A(AX)         B(BX)
     C                   ADD       1             AX
     C                   ADD       1             BX
     C                   ENDDO
     C                   MOVEA     B             ZDEC6
     C                   ENDIF
      * Concatenate
     C                   Z-ADD     ZHEL          ZNUM             18 6
     C                   ADD       ZDEC          ZNUM
     C                   EVAL      ZNUM = ZNUM * XFORTEGN
      *
     C                   ENDSR
      *
