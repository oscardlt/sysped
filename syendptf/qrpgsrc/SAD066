********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
12XXX * 01 YBC PTF12 REKORDLÅS
********************************************************************
      *
      ***********************************************************
      *** PROGRAM SKAL VEDLIKEHOLDE GEBYR FOR SPESIFIKASJON   ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01 02 07 09-11 13-24             ***
      *** LEDIGE INDIKATORER 27-29 31 32 34-39 41-87 95-97    ***
      ***********************************************************
      *
     FSADHSGEB  UF A E           K DISK
     FCUNDL01   IF   E           K DISK
     FFIRM      IF   E           K DISK
     FKODTGE    IF   E           K DISK
     FSAD066D   CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D                 DS
     D  KNAVN                  1     30
     D  XDLT                   1      7
      *
     C                   EXSR      INIT
      *
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     W2KN          SETLL     SADHSGEB
     C     START1        TAG
     C                   READ(N)   SADHSGEB                               94
      *
     C     START2        TAG
      *
     C  N94              DO
     C                   DO        15
     C                   ADD       1             ZRECNO
     C                   IF        SGKN = 0
     C                   EVAL      KNAVN = 'FOR ALLE KUNDER'
     C                   ELSE
     C     CUND0K        CHAIN     CUNDL01                            33
     C   33              MOVE      *BLANKS       KNAVN
     C                   END
     C                   WRITE     SUBFIL
S01  C*                  READ(N)   SADHSGEB                               94
N01  C                   READ      SADHSGEB                               94
     C  N94              END
     C                   END
     C                   Z-ADD     ZRECNO        YRECNO            4 0
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     2             POSNBR
      *
     C     START3        TAG
     C                   SETOFF                                       9998
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYVARE
     C                   ELSE
     C                   WRITE     SUBCTL
     C                   EXFMT     BUNN
     C                   END
      *
      ***
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C                   Z-ADD     WSSUBN        XRECNO            4 0
      ***
      *
     C                   SETOFF                                       9830
     C*** AVSLUTT
     C   03              GOTO      SLUTT
     C*** FRISK OPP
     C   05              GOTO      START
     C*** LAGE NY
     C   06              DO
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     5             POSNBR
     C                   MOVE      *BLANKS       KNAVN
     C                   GOTO      START5
     C                   END
      *** SØK FRA
     C   08              GOTO      START6
      *
      *** BLA VIDERE
     C   25              DO
     C                   Z-ADD     YRECNO        ZRECNO
     C                   SETOFF                                       25
     C                   GOTO      START2
     C                   END
      *
     C     ZRECNO        IFNE      0
     C                   EXSR      HENT
     C                   END
     C   98XRECNO        IFNE      0
     C                   Z-ADD     XRECNO        ZRECNO
     C                   END
     C                   GOTO      START3
      *
      *** LAGE
     C     START5        TAG
     C                   EXFMT     VARELI
     C                   MOVEA     '000'         *IN(88)
     C                   SETOFF                                           30
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
      *
     C   04              DO
     C                   EXSR      SØKDTA
     C                   GOTO      START5
     C                   END
      *
      *
     C   12              EXSR      MOVB2
     C  N12              EXSR      TESTB1
     C  N12
     CAN 30              GOTO      START5
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     2             POSNBR
     C                   GOTO      START3
      *
      *** SØK FRA
     C     START6        TAG
     C                   EXFMT     VAREL2
     C   04              DO
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C                   MOVE      W2KN          WSKN
     C                   EXSR      SØKDTA
     C                   MOVE      WSKN          W2KN
     C                   GOTO      START6
     C                   END
     C   12              Z-ADD     5             LINNBR
     C   12              Z-ADD     2             POSNBR
     C   12              GOTO      START3
     C                   GOTO      START
      *
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
      *
      ***********************************************
     C     TESTB1        BEGSR
      ***********************************************
     C  N40              DO
      * TEST KUNDENR.
     C     WSKN          CHAIN(N)  SADHSGEB                           33
     C                   IF        *IN33
     C                   IF        WSKN = 0
     C                   EVAL      KNAVN = 'FOR ALLE KUNDER'
     C                   ELSE
     C     CUND1K        CHAIN     CUNDL01                            90
     C                   IF        *IN90
     C                   MOVE      'SPK0001'     MSGNR
     C                   SETON                                        30
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     5             POSNBR
     C                   END
     C                   END
     C                   ELSE
     C                   MOVE      'SPK0024'     MSGNR
     C                   SETON                                        90
     C                   SETON                                        30
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     5             POSNBR
     C                   END
     C                   ENDDO
      *
     C     KODTGEK       CHAIN     KODTGE                             89
     C  N30              IF        *IN89
     C                   MOVE      'SPG0002'     MSGNR
     C                   SETON                                        30
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     45            POSNBR
     C                   END
      *
     C   30              GOTO      SLTB1
      *
      *
      *** OPPDATERE PÅ REGISTER OG SUBFIL
     c     WSKN          CHAIN     SADHSGEB                           33
     C                   EXSR      MOVWCE
     C  N33              UPDATE    SADHSGR
     C   33              WRITE     SADHSGR
     C   40              UPDATE    SUBFIL
     C  N40              ADD       1             YRECNO
     C  N40              Z-ADD     YRECNO        ZRECNO
     C  N40              WRITE     SUBFIL
     C                   WRITE     SUBCTL
     C                   EXSR      MOVB2
      *
     C     SLTB1         ENDSR
      *
      ***********************************************
     C     HENT          BEGSR
      ***********************************************
     C                   Z-ADD     0             LINNBR
     C                   SETOFF                                       919299
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C   98              DO
     C   99              SETOFF                                       98
     C                   Z-ADD     1             LINNBR
     C                   GOTO      SLHENT
     C                   END
     C     ZRECNO        DIV       15            LINNBR
     C                   MVR                     XLIN              2 0
     C     XLIN          IFEQ      0
     C                   Z-ADD     15            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C     WSNR          IFEQ      '2'
     C                   MOVE      ' '           WSNR
     C     XDLT          COMP      '*DELETE'                              98
     C   98              GOTO      SLHENT
     C                   EXSR      CHGCE
     C   12              GOTO      SLHENT
     C                   END
     C     WSNR          IFEQ      '4'
     C                   MOVE      ' '           WSNR
     C     XDLT          COMP      '*DELETE'                              98
     C   98              GOTO      SLHENT
     C                   EXSR      DLTCE
     C   12              GOTO      SLHENT
     C                   END
     C                   END
      *
     C     SLHENT        TAG
     C                   ADD       4             LINNBR
     C                   Z-ADD     2             POSNBR
     C                   SETOFF                                       99
     C   98              DO
     C                   MOVE      'DIV0012'     MSGNR
     C                   SETON                                            30
     C                   END
      *
     C                   ENDSR
      *
      ***********************************************
     C     CHGCE         BEGSR
      ***********************************************
     C                   EXSR      MOVCEW
     C                   SETON                                        99
     C     STCHG         TAG
     C                   EXFMT     VARELI
     C                   MOVEA     '000'         *IN(88)
     C                   SETOFF                                         30
      *
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
      *
     C   04              DO
     C                   EXSR      SØKDTA
     C                   GOTO      STCHG
     C                   END
     C   12              DO
     C                   UPDATE    SUBFIL
     C                   EXSR      MOVB2
     C                   END
     C  N12              EXSR      TESTB1
     C  N12
     CAN 30              GOTO      STCHG
     C     XLIN          IFEQ      0
     C                   Z-ADD     15            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
      *
     C                   ENDSR
      *
      ***********************************************
     C     DLTCE         BEGSR
      ***********************************************
     C                   SETON                                        99
     C                   EXFMT     VAREL3
     C  N12              DO
     C     SGKN          CHAIN     SADHSGEB                           33
     C  N33              DELETE    SADHSGR
     C                   EXSR      MOVB1
     C                   END
     C     XLIN          IFEQ      0
     C                   Z-ADD     15            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C                   UPDATE    SUBFIL
      *
     C                   ENDSR
      *
      ***********************************************
     C     MOVWCE        BEGSR
      ***********************************************
     C                   MOVE      WSKN          SGKN
     C                   MOVE      WSGKD         SGGKD
     C                   MOVE      WSBEL         SGBEL
     C                   MOVE      WSMV          SGMV
      *
     C                   ENDSR
      *
      ***********************************************
     C     MOVCEW        BEGSR
      ***********************************************
     C                   MOVE      SGKN          WSKN
     C                   MOVE      SGGKD         WSGKD
     C                   MOVE      SGBEL         WSBEL
     C                   MOVE      SGMV          WSMV
     C                   SETON                                        40
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     45            POSNBR
      *
     C                   ENDSR
      *
      ***********************************************
     C     MOVB1         BEGSR
      ***********************************************
     C                   EVAL      SGKN  = 0
     C                   EVAL      KNAVN = '*DELETE'
     C                   EVAL      SGGKD = *BLANKS
     C                   EVAL      SGBEL = 0
     C                   EVAL      SGMV  = *BLANKS
      *
     C                   ENDSR
      *
      ***********************************************
     C     MOVB2         BEGSR
      ***********************************************
     C                   SETOFF                                       40
     C                   EVAL      WSKN  = 0
     C*                  EVAL      WSGKD = *BLANKS
     C                   EVAL      WSBEL = 0
     C                   EVAL      WSMV  = 'J'
      *
     C                   ENDSR
      *
      ***********************************************
     C     SØKDTA        BEGSR
      ***********************************************
      *
     C     LINNBR        IFEQ      22
     C     POSNBR        ANDGE     5
     C     POSNBR        ANDLE     12
     C                   MOVE      *BLANKS       USØKKN           30
     C                   CALL      'SOEKKUN'
     C                   PARM                    WSKN
     C                   PARM                    FIFIRM
     C                   PARM                    USØKKN           30
     C                   Z-ADD     45            POSNBR
     C                   GOTO      SLSØK
     C                   END
      *
     C                   MOVE      'DIV1037'     MSGNR
     C                   SETON                                        30
      *
     C     SLSØK         ENDSR
      *
      ***********************************************
     C     INIT          BEGSR
      ***********************************************
     C     CUND1K        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    WSKN
     C     CUND0K        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    SGKN
     C     KODTGEK       KLIST
     C                   KFLD                    KGEUNI
     C                   KFLD                    WSGKD
      *
     C                   EVAL      WSMV  = 'J'
     C                   EVAL      KGEUNI = 'GE'
      *
     C     FIFIRM        SETLL     FIRM
     C                   READ      FIRM                                   33
      *
     C                   ENDSR
      *
