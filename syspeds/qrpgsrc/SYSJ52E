      *
      ***********************************************************
      *** PROGRAM SKAL VISE BILL OF LADING                    ***
      ***        VARELINJER (TYPE-1)                          ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 02 04 06-10 13-24                ***
      *** LEDIGE INDIKATORER 27-32 34-90 95-99                ***
      ***********************************************************
      *
     FBLV1LF    IF   E           K DISK
     FSYSJ52FE  CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     D                UDS
     D  UFIRM                  1      2
     C*
     C                   EXSR      INIT
     C*
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     BLV1K         SETLL     BLV1LF
     C     BLV1K         READE     BLV1LF                                 94
      *
     C     START2        TAG
     C  N94              DO
     C                   DO        15
     C                   ADD       1             ZRECNO
     C                   WRITE     SUBFIL
     C     BLV1K         READE     BLV1LF                                 94
     C  N94              END
     C                   END
     C*
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYVARE
     C                   ELSE
     C                   WRITE     SUBCTL
     C                   EXFMT     BUNN
     C                   END
     C*
     C*** AVSLUTT
     C   01
     COR 03
     COR 12              GOTO      SLUTT
     C*** FRISK OPP
     C   05              GOTO      START
     C*** TIL TYPE 2
     C   11              Z-ADD     6             UFLAGG
     C   11              GOTO      SLUTT
     C*** TIL SIDE 2
     C   13              DO
     C                   CALL      'SYSJ52G'
     C                   PARM                    UAVD
     C                   PARM                    UOPD
     C                   PARM                    UDTR
     C                   PARM                    USG
     C                   PARM                    UUKE
     C                   PARM                    UAAR
     C                   PARM                    UFLAGG
     C     UFLAGG        IFEQ      1
     C     UFLAGG        OREQ      3
     C                   GOTO      SLUTT
     C                   END
     C                   GOTO      START3
     C                   END
     C*
     C*** BLA VIDERE
     C   25              DO
     C                   SETOFF                                       25
     C                   GOTO      START2
     C                   END
     C*
     C                   GOTO      START3
     C*
     C     SLUTT         TAG
     C   01              Z-ADD     1             UFLAGG
     C   03              Z-ADD     3             UFLAGG
     C   12              Z-ADD     9             UFLAGG
     C                   SETON                                        LR
     C                   RETURN
     C*
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     BLV1K         KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    UOPD
     C*
     C     *ENTRY        PLIST
     C                   PARM                    UAVD              4 0
     C                   PARM                    UOPD              7 0
     C                   PARM                    UDTR              6 0
     C                   PARM                    USG               3
     C                   PARM                    UUKE              2 0
     C                   PARM                    UAAR              2 0
     C                   PARM                    UFLAGG            1 0
     C*
     C                   ENDSR
     C*
