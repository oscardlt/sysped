     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A          R SUBFIL                    SFL
     A            SFNR           1A  B  8  3VALUES(' ' '1' '2' '5' '8')
     A            KUNDNR    R        O  8  5REFFLD(KUNDR/KUNDNR *LIBL/CUNDF)
     A  50                                  COLOR(WHT)
     A            KNAVN     R        O  8 14REFFLD(KUNDR/KNAVN *LIBL/CUNDF)
     A  50                                  COLOR(WHT)
     A            ADR1      R        O  8 45REFFLD(KUNDR/ADR1 *LIBL/CUNDF)
     A  50                                  COLOR(WHT)
     A            ADR2      R        O  9 14REFFLD(KUNDR/ADR2 *LIBL/CUNDF)
     A  50                                  COLOR(WHT)
     A            POSTNR    R        O  9 45REFFLD(KUNDR/POSTNR *LIBL/CUNDF)
     A  50                                  COLOR(WHT)
     A            ADR3      R        O  9 51REFFLD(KUNDR/ADR3 *LIBL/CUNDF)
     A  50                                  COLOR(WHT)
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A                                      SFLSIZ(0008)
     A                                      SFLPAG(0007)
     A                                  1  3'SALG400/SYPO36  '
     A                                      COLOR(BLU)
     A                                  1 20MSGCON(040 SHU0285 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  2 70TIME
     A                                      COLOR(BLU)
     A            ZRECNO         4S 0O  3 76SFLRCDNBR
     A                                      COLOR(BLU)
     A                                  7  1MSGCON(078 SHU0286 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A          R BUNN
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF03(03 'AVSLUTT')
     A                                      CF05(05 'Frisk')
     A                                      CF08(08 'SØK')
     A                                      CF12(12 'tilbake')
     A                                      SLNO(22)
     A                                      CLRL(03)
     A                                      CSRLOC(LINNBR POSNBR)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  2  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  2 21MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  2 41MSGCON(018 DIV0023 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  2 61MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A          R BUNNS
     A                                      CF12(12 'SØK')
     A                                      SLNO(22)
     A                                      CLRL(03)
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            WSALFA        10   I  1  3DSPATR(UL)
     A                                  2  3MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
