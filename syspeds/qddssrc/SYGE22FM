     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      HELP
     A          R SUBFIL                    SFL
     A            WSNR           1A  B  6  2VALUES(' ' '1')
     A  20                                  COLOR(YLW)
     A            KUNDNR         8Y 0O  6  4EDTCDE(4)
     A  20                                  COLOR(YLW)
     A            KNAVN         30A  O  6 13
     A  20                                  COLOR(YLW)
     A            WSAD2         28A  O  6 53
     A  20                                  COLOR(YLW)
     A            KONTO          8A  O  7  4
     A  20                                  COLOR(YLW)
     A            FORET         11A  O  7 13
     A  20                                  COLOR(YLW)
     A            TLF           15A  O  7 25
     A  20                                  COLOR(YLW)
     A            WSAD3         28A  O  7 53
     A  20                                  COLOR(YLW)
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0008)
     A                                      SFLPAG(0007)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                  1  2'SYSPED/SYGE22'
     A                                      COLOR(BLU)
     A                                  1 68DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  2 68TIME
     A                                      COLOR(BLU)
     A            ZRECNO         4S 0O  3 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1 16MSGCON(030 SHU0865 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  4  1MSGCON(075 SHU0871 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  5  4MSGCON(073 SHU0872 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  3  2MSGCON(015 SHU0873 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A          R BUNN
     A                                      CF03(03 'EXIT')
     A                                      CF12(12 'EXIT')
     A                                      CF05(05 'Frisk')
     A                                      CF10(10 'EPOST')
     A                                      CF08(08 'SØK')
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A            MSGNR          7A  P
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            WSSOK         30A  I  2 14
     A                                  3  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  70        MSGTXT        78A  O  4  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A                                  2  2MSGCON(011 SHU0875 *LIBL/SYMSGF)
     A          R NYWRK
     A                                      CF03(03 'EXIT')
     A                                      CF08(08 'EXIT')
     A                                      CF12(03)
     A                                  1  2'SYSPED/SYGE22'
     A                                      COLOR(BLU)
     A                                  1 67DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  2 67TIME
     A                                      COLOR(BLU)
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 62MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  8  2'Søk:'
     A            MSGNR          7A  P
     A            WSSOK         30   I  8  8
     A                                  1 16MSGCON(030 SHU0865 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
