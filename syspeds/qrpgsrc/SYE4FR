      *
      ************************************************************
      *** PROGRAM SKAL LESE VALGBEGREPENE FOR Å SKRIVE         ***
      ***                   MANIFEST (OMKJØRING)               ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER     ***
      *** LEDIGE INDIKATORER 01 02 04-26                       ***
      *** LEDIGE INDIKATORER 27-29 31-96                       ***
      ************************************************************
      *
     FSAMLB     IF   E           K DISK
     FDOKEM     IF   E           K DISK
     FDOKUM     IF   E           K DISK
     FFIRM      IF   E           K DISK
     FKODTP     IF   E           K DISK
     FKODTV     IF   E           K DISK
     FKODTW     IF   E           K DISK
     FKODTSF    IF   E           K DISK
     FSYE4FRFM  CF   E             WORKSTN
     D                UDS
     D  UFIRM                  1      2
     D  UAVD                  15     18  0
     D  USG                   19     21
     D  UPRO                  22     29  0
     D  USD                   36     38
     D  ULNR                  39     41  0
     D  UFAK                  42     42
     D  UHOD                  47     47
     D  UBBS                  48     48
V10  D  UOPD                  82     88  0
     D*
     D* XBLA1/XBLA2 ER IKKE I BRUK. HVIS TAR NOEN POSISJONER TIL BRUK
     D* HUSK Å SYNKE STØRRELSE PÅ XBLA1/XBL2
     D  XBLA1                 70    256
     D  XBLA2                257    510
     D  UPRT18               681    690
     D  UPT18                691    700
     D  UPRT73               961    970
     D  UPT73                971    980
     I*
     C*
     C     SAMLBK        KLIST
     C                   KFLD                    WSSD
     C                   KFLD                    WSLNR
     C     DOKEMK        KLIST
     C                   KFLD                    YRI
     C                   KFLD                    WSSD
     C                   KFLD                    WSLNR
     C     DOKUMK        KLIST
     C                   KFLD                    WSSD
     C                   KFLD                    WSLNR
     C     KODTPK        KLIST
     C                   KFLD                    WSPUNI
     C                   KFLD                    WSAVD
     C                   KFLD                    WSPLNR
     C     KODTVK        KLIST
     C                   KFLD                    WSVUNI
     C                   KFLD                    WSAVD
     C     KODTWK        KLIST
     C                   KFLD                    WSWUNI
     C                   KFLD                    WSAVD
     C     KODSFK        KLIST
     C                   KFLD                    WSFUNI
     C                   KFLD                    WSSG
     C*
     C     *ENTRY        PLIST
     C                   PARM                    UJBKØ             1
     C                   PARM                    UIN01             1
     C                   MOVE      '0'           UIN01
     C                   MOVE      'J'           UJBKØ
     C                   MOVE      'J'           WSJBKØ
     C*
     C                   MOVE      'M'           YRI               1
     C                   MOVE      'P'           WSPUNI            1
     C                   MOVE      'V'           WSVUNI            1
     C                   MOVE      'W'           WSWUNI            1
     C                   MOVE      'SF'          WSFUNI            2
     C                   MOVE      UAVD          WSAVD
     C                   MOVE      USG           WSSG
     C* BLANKE DE FELTENE SOM IKKE ER I BRUK
     C                   MOVE      *BLANKS       XBLA1
     C                   MOVE      *BLANKS       XBLA2
     C*
     C     FIFIRM        CHAIN     FIRM                               99
     C                   READ      FIRM                                   99
     C                   MOVE      FIFIRM        UFIRM
     C*
     C     START         TAG
     C                   EXFMT     BILDE
     C                   MOVEA     '000'         *IN(97)
     C                   SETOFF                                       30
     C*
     C   03              MOVE      '1'           UIN01
     C   03              GOTO      SLUTT
     C*
     C     KODTVK        CHAIN     KODTV                              99
     C  N99KODTWK        CHAIN     KODTW                              99
     C  N99              Z-ADD     18            WSPLNR            3 0
     C  N99KODTPK        CHAIN     KODTP                              99
     C  N99              MOVE      KOPNVN        UPRT18
     C  N99              MOVE      KOPTY         UPT18
     C  N99              Z-ADD     73            WSPLNR            3 0
     C  N99KODTPK        CHAIN     KODTP                              99
     C  N99              MOVE      KOPNVN        UPRT73
     C  N99              MOVE      KOPTY         UPT73
     C   99              MOVE      'SPA0001'     MSGNR
     C   99              SETON                                        30
     C   99              GOTO      START
     C     KODSFK        CHAIN     KODTSF                             98
     C   98              MOVE      'SPS0001'     MSGNR
     C   98              SETON                                        30
     C   98              GOTO      START
     C     SAMLBK        CHAIN     SAMLB                              97
     C  N97SMST          COMP      'U'                                9797
     C  N97              DO
     C     DOKEMK        CHAIN     DOKEM                              97
     C   97DOKUMK        CHAIN     DOKUM                              97
     C                   END
     C   97              MOVE      'SPS0039'     MSGNR
     C   97              SETON                                        30
     C   97              GOTO      START
     C*
     C                   MOVE      WSAVD         UAVD
     C                   MOVE      WSSG          USG
     C                   MOVE      *ZEROS        UPRO
V10  C                   MOVE      *ZEROS        UOPD
     C                   MOVE      WSSD          USD
     C                   MOVE      WSLNR         ULNR
     C                   MOVE      WSJBKØ        UJBKØ
     C                   MOVE      KOWHOD        UHOD
     C                   MOVE      KOWBBS        UBBS
     C     WSPRT         IFNE      *BLANKS
     C                   MOVE      WSPRT         UPRT18
     C                   MOVE      WSPRT         UPRT73
     C                   END
     C     UPRT18        IFEQ      *BLANKS
     C                   MOVEL     'QPRINT '     UPRT18
     C                   END
     C     UPT18         IFEQ      *BLANKS
     C                   MOVEL     '*STD'        UPT18
     C                   END
     C     UPRT73        IFEQ      *BLANKS
     C                   MOVEL     'QPRINT '     UPRT73
     C                   END
     C     UPT73         IFEQ      *BLANKS
     C                   MOVEL     '*STD'        UPT73
     C                   END
     C*
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
     C*
