********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
12XXX * 01 SH  PTF12 sende epost til en selv
12XXX * 02 CB  PTF12 Øk arraystørrelse
12XXX * 03 SH  PTF12 tømmer workfile etter visning
********************************************************************
     H Option( *SrcStmt ) DftActGrp( *No ) BndDir( 'QC2LE' ) DEBUG(*YES)
     FEDI41P    O  A F  256        DISK    USROPN
      *__________
      * Variabler
      *""""""""""
     D FILE_i          s               *
     D FILE_o          s               *
     D String          s           1024a
     D rc              s             10i 0
     D Codepage        s              5a   Varying
      *___________________________
      * IFS Stream file funksjoner
      *"""""""""""""""""""""""""""
     Dopen             Pr              *   ExtProc( '_C_IFS_fopen' )
     D                                 *   Value Options( *String )
     D                                 *   Value Options( *String )

     Dfputs            Pr            10i 0 ExtProc( '_C_IFS_fputs' )
     D                                 *   Value Options( *String )
     D                                 *   Value

     Dclose            Pr            10i 0 ExtProc( '_C_IFS_fclose' )
     D                                 *   Value
     Dfgets            Pr              *   ExtProc( '_C_IFS_fgets' )
     D                                 *   Value
     D                               10i 0 Value
     D                                 *   Value
     D C               S              1    DIM(256)
     D                 DS
     D  FE                     1    256
     D                                     DIM(256)
     D  FELT                   1    256
     D                 DS
N02  D  AB                     1  45000
N02  D                                     DIM(45000)
N02  D  ABDS                   1  45000
S02  D* AB                     1  30000
S02  D*                                    DIM(30000)
S02  D* ABDS                   1  30000
      */////////////
      * MAIN LOGIC /
      */////////////
      *___________
      * Initiering
      *"""""""""""
     C                   EXSR      INIT
      *_______________
      * Clear Workfile
      *"""""""""""""""
     C                   CALL      'EDI41C'
      *_______________
      * OPEN  Workfile
      *"""""""""""""""
     C                   OPEN      EDI41P
      *______________
      * Fill Workfile
      *""""""""""""""
     C     AB(1)         IFEQ      'U'
     C     AB(2)         ANDEQ     'N'
     C     AB(3)         ANDEQ     'A'
     C                   MOVEA     *BLANKS       C
     C                   MOVE      AB(1)         C(1)
     C                   MOVE      AB(2)         C(2)
     C                   MOVE      AB(3)         C(3)
     C                   MOVE      AB(4)         C(4)
     C                   MOVE      AB(5)         C(5)
     C                   MOVE      AB(6)         C(6)
     C                   MOVE      AB(7)         C(7)
     C                   MOVE      AB(8)         C(8)
     C                   MOVE      AB(9)         C(9)
     C                   MOVEA     C             ZDATA           256
     C                   EXCEPT    EDIADD
     C                   ELSE
     C                   MOVE      GRAAPP        TEGN
     C                   ENDIF
     C     SEGNAM        DOUEQ     'UNZ'
     C                   EXSR      GETSEG
     C                   EXSR      RTVDB
     C                   ENDDO
     C     SLUTT         TAG
     C                   CLOSE     EDI41P
      *_________________
      * Display WORKFILE
      *"""""""""""""""""
N01  C     EPOST         IFNE      'F'
     C                   MOVEL(P)  'DSPPFM F'    QCMDA            64
     C     QCMDA         CAT       'ILE(':0      QCMDA
     C     QCMDA         CAT       'EDI41P':0    QCMDA
     C     QCMDA         CAT       ')':0         QCMDA
     C                   Z-ADD     64            QCMDL            15 5
     C                   CALL      'QCMDEXC'
     C                   PARM                    QCMDA
     C                   PARM                    QCMDL
N01  C                   ELSE
N01  C                   movel     'EDI41P'      EDI41PA          10
N01  C                   CALL      'SYGE55TXCL'
N01  C                   PARM                    EDI41PA
N01  C                   PARM                    EDI41PA
N01  C                   END
n02   *_______________
n02   * Clear Workfile
n02   *"""""""""""""""
n02  C                   CALL      'EDI41C'
n02  C                   EVAL      *INLR=*ON
      *////////////////////////////////////////////////////////////
      *                                                      INIT /
      *////////////////////////////////////////////////////////////
     C     INIT          BEGSR
     C                   MOVE      *ZEROS        NUTEGN            1 0          "TEGN" numerisk rep.
     C                   MOVE      *BLANKS       SEGNAM            3            Segment navn
     C                   Z-ADD     1             TN                5 0
     C                   MOVE      'NO '         EOF               3             * FLAGG FOR EOF
     C                   MOVE      ' '           TEGN              1
     C                   MOVE      ' '           LOKAHE            1
     C                   MOVE      '?'           GRASPØ            1
     C                   MOVE      '+'           GRAPLU            1
     C                   MOVE      ':'           GRAKOL            1
     C                   BITON     '123457'      GRAAPP            1
     C                   BITOFF    '06'          GRAAPP
     C                   MOVE      '0'           *IN99
      *_________________
      * Input parameters
      *"""""""""""""""""
     C     *ENTRY        PLIST
     C                   PARM                    UFILI           256
N01  C                   PARM                    EPOST             1
      *__________
      * Open file
      *""""""""""
     C                   EVAL      FILE_i = open( %TrimR( UFILI ) : 'r' )


     C                   DOW       fgets( %Addr( String ) : %Size( String )
     C                             : FILE_i ) <> *Null
     C                   EVAL      String2=*BLANKS
     C     1023          SUBST     String:1      String2        1023
     C     ABDS          CAT       String2:0     ABDS
     C                   ENDDO

     C                   EVAL      rc = close( FILE_i )
     C     ENDINI        ENDSR
      *////////////////////////////////////////////////////////////
      * RETREIVE INFO FROM EDIFACT DATABASE TO SFL          RTVDB /
      *////////////////////////////////////////////////////////////
     C     RTVDB         BEGSR
     C                   MOVEA     *BLANKS       C
     C                   Z-ADD     1             NR
     C                   SELECT
     C     SEGNAM        WHENEQ    'UNB'
     C                   Z-ADD     1             NR
     C     SEGNAM        WHENEQ    'UNH'
     C                   Z-ADD     4             NR
     C     SEGNAM        WHENEQ    'BGM'
     C                   Z-ADD     4             NR
     C     SEGNAM        WHENEQ    'UNT'
     C                   Z-ADD     4             NR
     C     SEGNAM        WHENEQ    'UNZ'
     C                   Z-ADD     1             NR
     C                   OTHER
     C                   Z-ADD     7             NR
     C                   ENDSL
     C                   MOVEA     SEGNAM        C(NR)
     C                   ADD       3             NR
     C                   MOVEA     TEGN          C(NR)
N01  C     TEGN          CABEQ     GRAAPP        ENDUNH
     C                   ADD       1             NR
     C     NR            DO        256           NR
     C                   EXSR      GETCH
     C                   MOVEA     TEGN          C(NR)
     C     TEGN          IFEQ      GRAAPP
     C     TEGNGM        ANDNE     '?'
     C                   GOTO      ENDUNH
     C                   ENDIF
     C                   MOVE      TEGN          TEGNGM            1
     C                   END
      *
     C     ENDUNH        TAG
      *
     C                   MOVEA     C             ZDATA
     C                   EXCEPT    EDIADD
     C                   ENDSR
     C*__________________________________________________________
     C* SR for å hente neste tegn i datastroem             GETCH
     C*""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     GETCH         BEGSR
     C* Hent det tegn som TN peker på - Tegnet etter er "Lookahead"
     C                   MOVE      AB(TN)        TEGN
     C                   ADD       1             TN
     C                   MOVE      AB(TN)        LOKAHE            1
     C     ENDCH         ENDSR
     C*__________________________________________________________
     C* SR for å hente neste Segment                       GETSEG
     C*""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     GETSEG        BEGSR
     C* "Spis tegn" til appostrof er lest
     C     TEGN          DOWNE     GRAAPP
     C                   EXSR      GETCH
     C                   ENDDO
     C* "Spis tegn" til første IKKE blanke
     C                   MOVEA     *BLANKS       FE
     C                   EXSR      GETCH
     C     TEGN          DOWEQ     ' '
     C                   EXSR      GETCH
     C                   ENDDO
     C                   MOVEA     TEGN          FE(1)
     C* Har nå lest appostrof og kansje blank- de 3 neste vil være SEGMENTNAVN
     C     2             DO        3             NR                3 0
     C                   EXSR      GETCH
     C                   MOVEA     TEGN          FE(NR)
     C                   ENDDO
     C                   MOVEL     FELT          SEGNAM
     C* La TEGN være '+' etter segmentnavnet ved utgang av SR
     C                   EXSR      GETCH
     C* Segmentnavn returneres nå i variabel 'SEGNAM'
     C     ENDSEG        ENDSR
     OEDI41P    EADD         EDIADD
     O                       ZDATA
