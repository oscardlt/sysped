      *
      ***************************************************************
      *** PROGRAM SKAL VEDL. FLYFRAKTBREV (MAWB-INNENLAND)        ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER        ***
      *** LEDIGE INDIKATORER 01 02 06-11 13-26                    ***
      *** LEDIGE INDIKATORER 27-29 31 32 35-40 45-51 55-95        ***
      ***************************************************************
      *
     FDOKUM     UF A E           K DISK
     FSAMLB     IF   E           K DISK
     FHEAD7     IF   E           K DISK
     FKODTINP   IF   E           K DISK
     FKODTA     IF   E           K DISK
     FKODTFS    IF   E           K DISK
     FKODTFC    IF   E           K DISK
     FSTED2     IF   E           K DISK
     FCUNDL01   IF   E           K DISK
     FSYIN15FM  CF   E             WORKSTN
     D                 DS
     D  HEGN                   1     15
     D  XGN3                   5      7
     D                 DS
     D* XRFA                   1     15
     D  XRFA                   1     35
     D  WSSD                   1      3
     D  WSLNR                  4      6  0
     D                 DS
     D  KIPB2                  1      5  2
     D  KIPB3                  6     10  2
     D  KIPB4                 11     15  2
     D  KIPB5                 16     20  2
     D  KIPB6                 21     25  2
     D  KIPB7                 26     30  2
     D  KIPB8                 31     35  2
     D  KIPB9                 36     40  2
     D  KIPB10                41     45  2
     D  KIPB11                46     50  2
     D  IP                     1     50  2
     D                                     DIM(10)
     D                 DS
     D  DSDAT                  1      8  0
     D  DSAAR                  1      4  0
     D  DSMND                  5      6  0
     D  DSDAG                  7      8  0
     D                 DS
     D  DSFUEL                 1      9
     D  DSF1                   1      1
     D  DSF2                   2      2
     D  DSF3                   3      3
     D  DSF4                   4      4
     D  DSF5                   5      5
     D  DSF6                   6      6
     D  DSF7                   7      7
     D  DSF8                   8      8
     D  DSF9                   9      9
     D  DSFHEL                 1      7
     D  DSFDES                 8      9
     D                UDS
     D  UFIRM                  1      2
     D  UAVD                  15     18  0
     D  USG                   19     21
     D  UOPD                  22     28
     C*
     C                   EXSR      INIT
     C*
     C     START         TAG
     C                   EXFMT     BILDE
     C   03              GOTO      SLUTT
     C     SAMLBK        CHAIN     SAMLB                              99
     C  N99SMST          COMP      'U'                                    99
     C   99              GOTO      START
     C*
     C* HENTE INNENLANDS FRAKTBREV
     C     DOKUMK        CHAIN     DOKUM                              33
     C   05
     COR 33              EXSR      MOVB
     C  N05
     CANN33              EXSR      MOVDWS
     C*
     C     START1        TAG
     C     DMFUEL        IFEQ      0
     C                   EXSR      FSC
     C                   ENDIF
     C                   EXFMT     BILDE01
     C                   SETOFF                                       30
     C*
     C   04              DO
     C                   EXSR      SØKKUN
     C                   GOTO      START1
     C                   END
     C   12              DO
     C  N33              UPDATE    DOKUMR
     C                   GOTO      START
     C                   END
     C*
     C                   EXSR      TESTB1
     C  N03
     COR 30              GOTO      START1
     C*
     C                   EXSR      MOVWSD
     C   33              WRITE     DOKUMR
     C  N33              UPDATE    DOKUMR
     C   03              GOTO      START
     C*
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
     C*
     C***********************************************
     C     TESTB1        BEGSR
     C***********************************************
     C*
     C**** TEST KUNDE
     C     WSSKNR        IFNE      0
     C                   MOVE      WSSKNR        KUNDNR
     C     KUND1K        CHAIN     CUNDL01                            96
     C   96              SETON                                        30
     C   96              GOTO      SLT1
     C                   MOVE      KNAVN         WSSNAV
     C                   MOVE      ADR1          WSSAD1
     C                   MOVE      ADR2          WSSAD2
     C                   MOVE      *BLANKS       WSSAD3
     C     POSTNR        IFEQ      0
     C                   MOVEL     ADR3          WSSAD3
     C                   ELSE
     C                   MOVEL     POSTNR        WSSAD3
     C                   MOVE      ADR3          WSSAD3
     C                   END
     C                   MOVE      TLF           WSSTLF
     C                   END
     C*
     C*** HENTE FRAKTBEREGNET VEKT
     C     WSFVKT        IFEQ      0
     C                   MOVE      WSVEKT        WSFVKT
     C                   END
     C*
     C*** HENTE PRIS
     C     WSTARI        IFNE      'M'
     C                   Z-ADD     IP(XNR)       WSPRIS
     C                   END
     C*
     C     WSFVKT        MULT      WSPRIS        XINPR            10 2
     C     KIPB1         ADD(H)    XINPR         WSFRAK
     C*
     C     SLT1          ENDSR
     C*
     C***********************************************
     C     MOVB          BEGSR
     C***********************************************
     C                   MOVE      SMROB1        WSRUNR
     C                   MOVE      SMROC1        WSDAG1
     C                   MOVE      SMROB2        WSRUN2
     C                   MOVE      SMROC2        WSDAG2
     C                   MOVE      SMROB3        WSRUN3
     C                   MOVE      SMROC3        WSDAG3
     C                   MOVE      'PP'          WSCCPP
     C                   MOVE      *ZEROS        WSSKNR
     C                   MOVE      XNAS          WSSNAV
     C                   MOVE      XAD1S         WSSAD1
     C                   MOVE      XAD2S         WSSAD2
     C                   MOVE      XAD3S         WSSAD3
     C                   MOVE      *BLANKS       WSSTLF
     C                   MOVE      *BLANKS       WSKTLF
     C                   MOVE      ' '           WSST
     C                   MOVE      *BLANKS       WSSLAG
     C                   MOVE      ' '           WSTARI
     C                   MOVE      *ZEROS        WSPRIS
     C                   MOVE      *BLANKS       WSMERK
     C                   MOVE      *ZEROS        WSFORS
     C                   MOVE      *BLANKS       WSFORA
     C                   MOVE      *ZEROS        WSFRAK
     C                   MOVE      *ZEROS        WSFS
     C                   MOVE      *ZEROS        WSFPRE
     C                   MOVE      *ZEROS        WSVGEB
     C                   MOVE      *ZEROS        WSMVA
     C                   MOVE      *BLANKS       WSVSL2
     C*
     C* HENTER SAMLASTER NAVN/ADRESSE
     C                   MOVE      SMKNS         KUNDNR
     C     KUND1K        CHAIN     CUNDL01                            34
     C                   MOVE      KNAVN         WSKNAV
     C                   MOVE      ADR1          WSKAD1
     C                   MOVE      ADR2          WSKAD2
     C                   MOVE      *BLANKS       WSKAD3
     C     POSTNR        IFEQ      0
     C                   MOVEL     ADR3          WSKAD3
     C                   ELSE
     C                   MOVEL     POSTNR        WSKAD3
     C                   MOVE      ADR3          WSKAD3
     C                   END
     C*
     C     HEAD7K        SETLL     HEAD7
     C                   MOVE      *ZEROS        WSANT
     C                   MOVE      *ZEROS        WSVEKT
     C                   MOVE      *ZEROS        WSFVKT
     C                   SETOFF                                       34
     C     STMB          TAG
     C     HEAD7K        READE     HEAD7                                  34
     C  N34              DO
     C                   ADD       HENT          WSANT
     C                   ADD       HEVKT         WSVEKT
     C                   ADD       HEFBV         WSFVKT
     C                   GOTO      STMB
     C                   END
     C*
     C                   MOVE      HEGN          WSAWB
     C                   MOVE      HESDF         WSFRA
     C                   MOVE      HESDT         WSTIL
     C     XGN3          IFGT      '000'
     C     XGN3          ANDLE     '999'
     C                   MOVE      XGN3          WSPREF
     C                   END
     C*
     C     KODFSK        CHAIN     KODTFS                             34
     C  N34              MOVE      KFSNAV        WSFNAV
     C   34              MOVE      *BLANKS       WSFNAV
     C*
     C                   EXSR      HSONE
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     HSONE         BEGSR
     C***********************************************
     C     WSFRA         CHAIN     STED2                              34
     C     ST2SON        COMP      1                                      41
     C     ST2SON        COMP      2                                      42
     C     ST2SON        COMP      3                                      43
     C     ST2SON        COMP      4                                      44
     C     WSTIL         CHAIN     STED2                              34
     C     ST2SON        COMP      2                                      52
     C     ST2SON        COMP      3                                      53
     C     ST2SON        COMP      4                                      54
     C                   Z-ADD     1             XNR               2 0
     C  N41
     CANN52              Z-ADD     2             XNR
     C  N41
     CANN53              Z-ADD     3             XNR
     C  N41
     CANN54              Z-ADD     4             XNR
     C  N42
     CANN52              Z-ADD     5             XNR
     C  N42
     CANN53              Z-ADD     6             XNR
     C  N42
     CANN54              Z-ADD     7             XNR
     C  N43
     CANN53              Z-ADD     8             XNR
     C  N43
     CANN54              Z-ADD     9             XNR
     C  N44
     CANN54              Z-ADD     10            XNR
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVDWS        BEGSR
     C***********************************************
     C                   MOVE      DMST          WSST
     C                   MOVE      DMAWB         WSAWB
     C                   MOVE      DMFRA         WSFRA
     C                   MOVE      DMTIL         WSTIL
     C                   MOVE      DMRUNR        WSRUNR
     C                   MOVE      DMCCPP        WSCCPP
     C                   MOVE      DMPREF        WSPREF
     C                   MOVE      DMFNAV        WSFNAV
     C                   MOVE      DMSKNR        WSSKNR
     C                   MOVE      DMSNAV        WSSNAV
     C                   MOVE      DMSAD1        WSSAD1
     C                   MOVE      DMSAD2        WSSAD2
     C                   MOVE      DMSAD3        WSSAD3
     C                   MOVE      DMSTLF        WSSTLF
     C                   MOVE      DMKNAV        WSKNAV
     C                   MOVE      DMKAD1        WSKAD1
     C                   MOVE      DMKAD2        WSKAD2
     C                   MOVE      DMKAD3        WSKAD3
     C                   MOVE      DMKTLF        WSKTLF
     C                   MOVE      DMANT         WSANT
     C                   MOVE      DMVEKT        WSVEKT
     C                   MOVE      DMSLAG        WSSLAG
     C                   MOVE      DMFVKT        WSFVKT
     C                   MOVE      DMTARI        WSTARI
     C                   MOVE      DMPRIS        WSPRIS
     C                   MOVE      DMMERK        WSMERK
     C                   MOVE      DMFORS        WSFORS
     C                   MOVE      DMFORA        WSFORA
     C                   MOVE      DMFRAK        WSFRAK
     C                   MOVE      DMFS          WSFS
     C                   MOVE      DMFPRE        WSFPRE
     C                   MOVE      DMDAG1        WSDAG1
     C                   MOVE      DMDAG2        WSDAG2
     C                   MOVE      DMDAG3        WSDAG3
     C                   MOVE      DMVGEB        WSVGEB
     C                   MOVE      DMMVA         WSMVA
     C                   MOVE      DMVSL2        WSVSL2
     C                   MOVE      DMRUN2        WSRUN2
     C                   MOVE      DMRUN3        WSRUN3
     C                   MOVE      DMFT          WSFT
     C                   MOVE      DMFUEL        WSFUEL
     C*
     C                   EXSR      HSONE
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVWSD        BEGSR
     C***********************************************
     C                   MOVE      WSST          DMST
     C                   MOVE      WSAWB         DMAWB
     C                   MOVE      WSFRA         DMFRA
     C                   MOVE      WSTIL         DMTIL
     C                   MOVE      WSRUNR        DMRUNR
     C                   MOVE      WSCCPP        DMCCPP
     C                   MOVE      WSPREF        DMPREF
     C                   MOVE      WSFNAV        DMFNAV
     C                   MOVE      WSSKNR        DMSKNR
     C                   MOVE      WSSNAV        DMSNAV
     C                   MOVE      WSSAD1        DMSAD1
     C                   MOVE      WSSAD2        DMSAD2
     C                   MOVE      WSSAD3        DMSAD3
     C                   MOVE      WSSTLF        DMSTLF
     C                   MOVE      WSKNAV        DMKNAV
     C                   MOVE      WSKAD1        DMKAD1
     C                   MOVE      WSKAD2        DMKAD2
     C                   MOVE      WSKAD3        DMKAD3
     C                   MOVE      WSKTLF        DMKTLF
     C                   MOVE      WSANT         DMANT
     C                   MOVE      WSVEKT        DMVEKT
     C                   MOVE      WSSLAG        DMSLAG
     C                   MOVE      WSFVKT        DMFVKT
     C                   MOVE      WSTARI        DMTARI
     C                   MOVE      WSPRIS        DMPRIS
     C                   MOVE      WSMERK        DMMERK
     C                   MOVE      WSFORS        DMFORS
     C                   MOVE      WSFORA        DMFORA
     C                   MOVE      WSFRAK        DMFRAK
     C                   MOVE      WSFS          DMFS
     C                   MOVE      WSFPRE        DMFPRE
     C                   MOVE      WSDAG1        DMDAG1
     C                   MOVE      WSDAG2        DMDAG2
     C                   MOVE      WSDAG3        DMDAG3
     C                   MOVE      WSVSL2        DMVSL2
     C                   MOVE      WSRUN2        DMRUN2
     C                   MOVE      WSRUN3        DMRUN3
     C                   MOVE      WSVGEB        DMVGEB
     C                   MOVE      WSMVA         DMMVA
     C                   MOVE      *BLANKS       DMXXX1
     C                   MOVE      *BLANKS       DMXXX2
     C                   MOVE      *BLANKS       DMXXX3
     C                   MOVE      *BLANKS       DMXXX4
     C                   MOVE      WSSD          DMSD
     C                   MOVE      WSLNR         DMLNR
     C                   MOVE      WSFT          DMFT
     C                   MOVE      WSFUEL        DMFUEL
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     SØKKUN        BEGSR
     C***********************************************
     C                   MOVE      *BLANKS       USØKKN           30
     C                   CALL      'SOEKKUN'
     C                   PARM                    WSSKNR
     C                   PARM                    UFIRM
     C                   PARM                    USØKKN           30
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     DOKUMK        KLIST
     C                   KFLD                    WSSD
     C                   KFLD                    WSLNR
     C     SAMLBK        KLIST
     C                   KFLD                    WSSD
     C                   KFLD                    WSLNR
     C     HEAD7K        KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    XRFA
     C     KODINK        KLIST
     C                   KFLD                    XINUNI
     C     KODTAK        KLIST
     C                   KFLD                    XAUNI
     C                   KFLD                    UAVD
     C     KODFSK        KLIST
     C                   KFLD                    XFSUNI
     C                   KFLD                    WSPREF
     C     KUND1K        KLIST
     C                   KFLD                    UFIRM
     C                   KFLD                    KUNDNR
     C*
     C*
     C                   MOVE      'A'           XAUNI             1
     C                   MOVE      'FS'          XFSUNI            2
     C                   MOVE      'INNPR'       XINUNI            5
     C                   MOVE      *BLANKS       WSSD
     C                   MOVE      *ZEROS        WSLNR
     C*
     C     KODINK        CHAIN     KODTINP                            33
     C     KODTAK        CHAIN     KODTA                              33
     C                   MOVE      KOAKNR        KUNDNR
     C     KUND1K        CHAIN     CUNDL01                            33
     C                   MOVE      KNAVN         XNAS             30
     C                   MOVE      ADR1          XAD1S            30
     C                   MOVE      ADR2          XAD2S            30
     C     POSTNR        IFEQ      0
     C                   MOVEL     ADR3          XAD3S            30
     C                   ELSE
     C                   MOVEL     POSTNR        XAD3S
     C                   MOVE      ADR3          XAD3S
     C                   END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     FSC           BEGSR
     C***********************************************
      *
      * AUTOMATISK BEREGNING AV FUEL SURCHARGE
      *
     C                   Z-ADD     WSFUEL        WXFUEL            9 2
     C                   CLEAR                   DSDAT
     C                   MOVE      UYEAR         DSAAR
     C                   MOVEL     '20'          DSAAR
     C                   MOVE      UMONTH        DSMND
     C                   MOVE      UDAY          DSDAG
     C     KODFCK        KLIST
     C                   KFLD                    YFCUNI
     C                   KFLD                    WSPREF
     C                   MOVE      'FC'          YFCUNI            2
     C     KODFCK        CHAIN     KODTFC                             66
     C     *IN66         IFEQ      *OFF
      * SJEKK MINIMUMSVEKT
     C     KFCVKT        IFEQ      'A'
     C     WSVEKT        ANDLT     KFCMIN
     C                   Z-ADD     0             WSFUEL
     C                   MOVEL     *BLANKS       WSFT
     C                   MOVEL     *BLANKS       WYFT
     C                   GOTO      FSCSL
     C                   ENDIF                                                  <KFCVKT
     C     KFCVKT        IFEQ      'C'
     C     WSFVKT        ANDLT     KFCMIN
     C                   Z-ADD     0             WSFUEL
     C                   MOVEL     *BLANKS       WSFT
     C                   MOVEL     *BLANKS       WYFT
     C                   GOTO      FSCSL
     C                   ENDIF                                                  <KFCVKT
      * SJEKK LANDKODE - INCLUDE
     C     KFCIO1        IFEQ      'I'
     C     HELKA         IFNE      KFCLK1
     C     HELKA         ANDNE     KFCLK2
     C     HELKA         ANDNE     KFCLK3
     C     HELKA         ANDNE     KFCLK4
     C     HELKA         ANDNE     KFCLK5
     C                   GOTO      FSCSL
     C                   ENDIF                                                  <HELKA
     C                   ENDIF                                                  <KFCIO1
      * SJEKK LANDKODE - OMIT
     C     KFCIO1        IFEQ      'O'
     C     HELKA         IFEQ      KFCLK1
     C     HELKA         OREQ      KFCLK2
     C     HELKA         OREQ      KFCLK3
     C     HELKA         OREQ      KFCLK4
     C     HELKA         OREQ      KFCLK5
     C                   GOTO      FSCSL
     C                   ENDIF                                                  <HELKA
     C                   ENDIF                                                  <KFCIO1
      * SJEKK GYLDIGHETSDATO
     C     KFCTIL        IFEQ      *ZEROS
     C                   MOVE      99999999      KFCTIL
     C                   END
     C     DSDAT         IFLE      KFCTIL
     C     DSDAT         ANDGE     KFCFRA
      * SJEKK FRAKTBEREGNINGSVEKT ELLER AKTUELL VEKT
     C     KFCVKT        IFEQ      'A'
     C     WSVEKT        MULT      KFCAVG        WSFUEL            9 2
     C                   ELSE
     C     WSFVKT        MULT      KFCAVG        WSFUEL
     C                   ENDIF                                                  <DFCVKT
     C                   MOVE      WSFUEL        DSFUEL
     C                   MOVE      WSFUEL        DSFUEL
     C     DSF1          IFEQ      '0'
     C                   MOVE      ' '           DSF1
     C     DSF2          IFEQ      '0'
     C                   MOVE      ' '           DSF2
     C     DSF3          IFEQ      '0'
     C                   MOVE      ' '           DSF3
     C     DSF4          IFEQ      '0'
     C                   MOVE      ' '           DSF4
     C     DSF5          IFEQ      '0'
     C                   MOVE      ' '           DSF5
     C     DSF6          IFEQ      '0'
     C                   MOVE      ' '           DSF6
     C                   ENDIF                                                  <DSF6
     C                   ENDIF                                                  <DSF5
     C                   ENDIF                                                  <DSF4
     C                   ENDIF                                                  <DSF3
     C                   ENDIF                                                  <DSF2
     C                   ENDIF                                                  <DSF1
     C     WYFT          IFEQ      WSFT
     C     DSFHEL        CAT       ',':0         WSFT14           14
     C     WSFT14        CAT       DSFDES:0      WSFT14
     C     WSFT14        CAT       'NOK':1       WSFT14
     C     KFCFTX        CAT       WSFT14:1      WSFTNY           41
     C                   MOVEL     WSFTNY        WSFT
     C                   MOVEL     WSFTNY        WXFT             41
     C                   ENDIF                                                  <WYFT
     C                   MOVEL     WSFT          WYFT             41
     C                   ENDIF                                                  <DSDAT
     C                   ENDIF                                                  <*IN66
     C     FSCSL         ENDSR
