     H debug(*yes)
      *********************************************************************
      *  RPG ILE MODULE SYSPED/FRBANNERR
      *
      *
      *  After compiling this RPG MODULE,
      *  create the related program with the following command:
      *
CRTPG+*  CRTPGM SYSPED/FRL1ONERR MODULE(*LIBL/FRL1ONERR *LIBL/INCGI)
CRTPGM*         ACTGRP(CGI) AUT(*USE)
      *
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
     FBRIDF     UF   E           K DISK    USROPN
     FBRPARF    IF   E           K DISK    USROPN
     FWOTRAHF   if a e           K DISK    USROPN
     FWOTRALSY  if a e           K DISK    USROPN
      *=====================================================================
      * Includes to be used in all CGIs
      *=====================================================================
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
      *  Fields used for parsing the input string
     D action          s             10a
     D subaction       s             10a
      * aktiv Ord...
     D Ord             s              8a
     D Ordnr           s              8s 0
      * kundnummer
     D Kundnr          s              8  0
     D KUNDna          s              8a
     D LOGO            s             50a
      * Bruker
     D WSUSER          S             10
      * firma
     D FIRMA           s              2a
     D KNAVN           s             30a
     D                 DS
     D  WTIME                  1     14  0
     D  WTID                   1      6  0
     D  WDD                    7      8  0
     D  WMM                    9     10  0
     D  WÅÅ                   11     14  0
     D                 DS
     D  IDAG                   1      8  0
     D  IDAGA                  1      4  0
     D  IDAGM                  5      6  0
     D  IDAGD                  7      8  0
      *--------------------------------------------------------------------
      *  Other program variables and constants
      *=====================================================================
      * Read remote browser input string and parse it
      *=====================================================================
      *=====================================================================
     C                   exsr      SetTimeStr
      *=====================================================================
      /copy syspeds/qrpgsrcpy,prolog3
     C                   exsr      Parse
     C                   EXSR      INIT
      *=====================================================================
      * Main line
      *=====================================================================
      * Ask the service program to load into core
      * the appropriate output skeleton html member
     C                   callp     gethtml('HTMLSRC':'*LIBL':  'FRL1ONERR':
     C                             '/CGITAG/')
      *------------------
      * Action requested
     C                   eval      action = uppify(action)
     C                   eval      subaction = uppify(subaction)
      * Start html
BODY C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updHTMLvar('USER':WSUSER)
     C                   callp     updHTMLvar('FIRMA':FIRMA)
     C                   callp     updHTMLvar('KNAVN':KNAVN)
     C                   callp     updHTMLvar('LOGO':LOGO)
     C                   callp     wrtsection('top')
      * kundenummer
     C                   callp     updHTMLvar('KUNDNA':
     C                             %trim(%editc(Kundnr:'Z')))
      * Gjeldende Ord
     C                   callp     updHTMLvar('ORD':
     C                             %trim(%editc(OrdNr:'Z')))
      *
      * Vis kun når utfylt / men param finnes også som hidden
      *
     C     wsuser        ifne      *blanks
     C                   callp     wrtsection('gjeldende')
     C                   end
      * If 'FRL1O01' og ordrenummer tildelt skriv ordrehode-buttom
     C                   IF        action = 'FRL1O01'
     C                   if        OrdNr  > 0
     C     wsuser        ifne      *blanks
     C                   exsr      buttomoh
     C                   endif
     C                   endif
     C                   ENDIF
      * If 'FRL1O02'
     C                   IF        action = 'FRL1O02'
     C                   if        OrdNr  > 0
     C     wsuser        ifne      *blanks
     C                   exsr      buttomoh
     C                   endif
     C                   endif
     C                   ENDIF
      * If 'FRL1O05'
     C*                  IF        action = 'FRL1O05'
     C*                  if        OrdNr  > 0
     C*    wsuser        ifne      *blanks
     C*                  exsr      buttomoh
     C*                  endif
     C*                  endif
     C*                  ENDIF
      *
      * vise bestillingsbutton hvis der finnes ordrehode og linjer
      *
     C     wsuser        ifne      *blanks
     C     ordnr         andne     0
     c                   exsr      buttombest
     C                   end
      *
     C*
      * End html
     C                   exsr      SetTimeEnd
     C                   callp     updhtmlvar('RUNTIME':
     C                             %trim(%editw(SecElaps:'     0 .   ')))
     C                   callp     wrtsection('runtime')
     C                   callp     wrtsection('end')
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   return
      *=====================================================================
      * Parse the input string
      *=====================================================================
     C     Parse         begsr
     C                   eval      WSUSER     = zhbgetvar('USER      ')
     c     wsuser        ifne      *blanks
     C                   eval      action     = zhbgetvar('action    ')
     C                   eval      subaction  = zhbgetvar('subaction ')
     C                   eval      Ord        = zhbgetvar('ORD       ')
     C                   eval      Ordnr      = c2n2(ord)
     C                   eval      KUNDNA     = zhbgetvar('KUNDNA    ')
     C                   eval      Kundnr     = c2n2(KUNDNA)
     C                   eval      FIRMA      = zhbgetvar('FIRMA     ')
     c                   end
     C                   endsr
      *=====================================================================
      *  Time started
      *=====================================================================
      **********************************************************************
     C     SetTimeStr    begsr
      **********************************************************************
     C                   TIME                    WTIME
     C                   MOVE      WÅÅ           IDAGA
     C                   MOVE      WMM           IDAGM
     C                   MOVE      WDD           IDAGD
     C                   time                    TimeStart
     C                   endsr
      *=====================================================================
      *  Time ended and time elapsed elapsed
      *=====================================================================
      **********************************************************************
     C     SetTimeEnd    begsr
      **********************************************************************
     C                   time                    TimeEnd
     C     TimeEnd       subdur    TimeStart     MsElaps:*ms
     C                   eval      SecElaps = MsElaps / 1000000
     C                   endsr
      **********************************************************************
     C     buttomoh      begsr
      **********************************************************************
      * skal ordrehode-buttom vises   ?  ikke hvis avsluttet
      *
     c                   open      wotrahf
     c                   z-add     ordnr         ordnot
     c     ordkey        chain     wotrahf                            33
     C*  33              callp     wrtsection('ORDREHODE')
     C  N33status        ifne      'F'
     C                   callp     wrtsection('ORDREHODE')
     C                   end
     c                   close     wotrahf
     C                   endsr
      **********************************************************************
     C     buttombest    begsr
      **********************************************************************
      * skal bestillingsbuttom vises   ?  ikke hvis avsluttet
      * både header og linje må eksistere
      *
     C     ordkey        Klist
     C                   KFLD                    FIRMA
     C                   KFLD                    KUNDNR
     C                   KFLD                    ORDNOT
     C     ordkey02      Klist
     C                   KFLD                    FIRMA
     C                   KFLD                    ORDNOT
     c                   open      wotrahf
     c                   open      wotralsy
     c                   z-add     ordnr         ordnot
     c     ordkey        chain     wotrahf                            33
     c     ordkey        chain     wotralsy                           34
      * bestillingsbutton
     c     *in(33)       ifeq      '0'
     c     *in(34)       andeq     '0'
     c     status        ifne      'F'
     C                   callp     wrtsection('SENDBEST')
     c                   end
     c                   end
      * tømme buttom
     c     *in(34)       ifeq      '0'
     c     status        ifne      'F'
     C                   callp     wrtsection('TOMMBEST')
     c                   end
     c                   end
      * loggut buttom
     c     *in(33)       ifeq      '0'
     c     *in(34)       andeq     '1'
     c     status        ifne      'F'
     c*                  dump
     C                   callp     wrtsection('LOGGUT')
     c                   end
     c                   end
     c     *in(33)       ifeq      '1'
     c     *in(34)       andeq     '1'
      * ikke hvis avbryt hvis ordre er avluttett
     C                   callp     wrtsection('SKRIVTR')
     C                   callp     wrtsection('LOGGUT')
     c                   end
     c                   close     wotrahf
     c                   close     wotralsy
     c                   endsr
      *=====================================================================
      *  Initier
      *=====================================================================
     C     INIT          begsr
      * Get program start time for calculating execution time
      *
     c     wsuser        ifne      *blanks
     C                   open      BRIDF                                50
     C     WSUSER        CHAIN(N)  BRIDF                              50
      *
     c                   movel     bibesk        knavn
      *
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
      *
      * HENTE LOGO
      *
     C     PARKEY        KLIST
     C                   KFLD                    WSUSERFI
     C                   KFLD                    KUNULL            8 0
     C                   KFLD                    PAID              5
     c                   movel     '*KUNDFT*'    WSUSERFI         10
     C                   MOVE      FIRMA         WSUSERFI
     C                   open      BRPARF
     c                   moveL     'LOGO '       paID
     c     parkey        chain     brparf                             33
     c  n33              movel     PAVRDA        LOGO
     C                   close     BRPARF
     c                   end
     C                   endsr
