      ***********************************************************
      *** PROGRAM for innlegging av T&T via turnr             ***
      ***********************************************************
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 JOV PTF10 FLERE FILTER - IKKE FORSLAG PÅ TEI - FORSLAG PÅ KLOKKE
10XXX * 02 JOV PTF10 KRAV OM FR.BERVS-RECORD (NR 001) KUN DERSOM FILTER PÅ TRANSP..
      *%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
11XXX * 03 SH  PTF11 endring skjermbildet
11xxx * 04 JOV PTF11 Byttet NO/EN-tekst i VAREL (Nå Norsk Først ..)
********************************************************************
      *
     FHead14    IF   E           K DISK
     FTRACKf    O  A E             DISK
S01  F*                                    RENAME(TRACKFR:TRACKFRX)
N01  FTRACKL02  IF A E           K DISK
N01  F                                     RENAME(TRACKFR:TRACKFR2)
     FKOFAST    IF   E           K DISK
     FDOKUF     IF   E           K DISK
     FDISP      UF   E           K DISK
     FSYTT05FM  CF   E             WORKSTN
N01  F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
N01   * CURSOR LOKASJON AV AKTIV VINDU
N01  D  WSCLOW               382    383B 0
     D TRACK1DS        DS
     D  RECNO                397    400B 0
      *___________________________
      * Finner username
      *"""""""""""""""""""""""""""
     D                SDS
     D  ZUSER                254    263
     D                 DS
     D  TTDATE                 1      8  0
     D  TTDTÅ                  1      4  0
     D  TTDTÅ2                 3      4  0
     D  TTDTM                  5      6  0
     D  TTDTD                  7      8  0
     D                 DS
     D  TTTIME                 1      6  0
     D  TTTIM4                 1      4  0
     D                 DS
     D  TTDATL                 1      8  0
     D  TTDTLÅ                 1      4  0
     D  TTDTLM                 5      6  0
     D  TTDTLD                 7      8  0
     D                 DS
     D  WFDATE                 1      6  0
     D  WFDED6                 1      2  0
     D  WFDEM6                 3      4  0
     D  WFDEÅ6                 5      6  0
     D                 DS
     D  WFDA8E                 1      8  0
     D  WFDEÅ8                 1      4  0
     D  WFDEM8                 5      6  0
     D  WFDED8                 7      8  0
     D                 DS
     D  WFDATL                 1      6  0
     D  WFDLD6                 1      2  0
     D  WFDLM6                 3      4  0
     D  WFDLÅ6                 5      6  0
     D                 DS
     D  WFDA8L                 1      8  0
     D  WFDLÅ8                 1      4  0
     D  WFDLM8                 5      6  0
     D  WFDLD8                 7      8  0
     D                 DS
     D  XULTID                 1     14  0
     D  XULTM                  1      6  0
     D  XULDD                  7      8  0
     D  XULMM                  9     10  0
     D  XULÅÅ                 11     14  0
     D                 DS
     D  DFBREF                 1     17
     D  DFBRTX                 1      5
     D  DFBRAV                 6      9
     D  DFBRST                10     10
     D  DFBROP                11     17
      * medta kun OPPDRAG UTEN følgende T&T-koder (en finnes - kutt )
     D                 DS
     D  TU                     1     15
     D                                     DIM(5)                               Kun UTEN følgende...
     D  WSUTT1                 1      3
     D  WSUTT2                 4      6
     D  WSUTT3                 7      9
     D  WSUTT4                10     12
     D  WSUTT5                13     15
      * medta KUN OPPDRAG MED følgende T&T-koder (alle må finnes)
     D                 DS
     D  TK                     1     15
     D                                     DIM(5)                               KUN oppd MED følgend
     D  WSKTT1                 1      3
     D  WSKTT2                 4      6
     D  WSKTT3                 7      9
     D  WSKTT4                10     12
     D  WSKTT5                13     15
     C*
     C                   EXSR      INIT
     C                   EXSR      TESTB
     C     *IN12         ifne      '1'
     C                   EXSR      SkrivTT
     c                   end
     C*
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
     C***********************************************
     C     TESTB         BEGSR
     C***********************************************
      *
     c                   CLEAR                   TTWINDOW
      *
     c                   move      UUTUNR        WSTUNR
     c                   move      UUKTRA        WSKTRA
     C                   MOVE      'S'           TTMANU
     C                   MOVE      UUACTI        TTACTI
     c     TTacti        ifne      *blanks
     C                   MOVEL     TTACTI        KFKOD
     C     KOKEY         CHAIN     KOFAST                             89
     C     *IN89         IFEQ      '0'
     C                   MOVEL     KFTXT         KODTXT
N03  C                   MOVEL     KFTXTE        KODTXTE
     C                   MOVEL     KFTXT         KODGRP
     C                   MOVE      KFTXT         KODWEB
     C                   ELSE
     C                   MOVE      *BLANKS       KODTXT
N03  C                   MOVE      *BLANKS       KODTXTE
     C                   MOVE      *BLANKS       KODGRP
     C                   MOVE      *BLANKS       KODWEB
     C                   END
     C                   END
     C                   MOVE      ZUSER         TTUSER
     C                   TIME                    XULTID
     C                   MOVE      XULDD         TTDTLD
     C                   MOVE      XULMM         TTDTLM
     C                   MOVE      XULÅÅ         TTDTLÅ
     C                   MOVE      XULTM         TTTIML
     C                   MOVE      XULDD         TTDTD
     C                   MOVE      XULMM         TTDTM
     C                   MOVE      XULÅÅ         TTDTÅ
N01  C                   MOVEL     XULTM         TTTIM4
      *cursor
     C     TTacti        ifeq      *blanks
     C     *IN89         OReq      '1'
     C                   Z-ADD     03            LINNBR
     C                   Z-ADD     20            POSNBR
     c                   else
     C                   Z-ADD     04            LINNBR
     C                   Z-ADD     35            POSNBR
     c                   end
      *
     c     feilTag       tag
     c                   exfmt     TTWINDOW
     c                   move      '0'           *IN30
     c                   movea     '00000'       *IN(85)
     C   12              GOTO      SLTB
      *
N01  C     WSCLOW        DIV       256           LINNBR
N01  C                   MVR                     POSNBR
      *
     c     *in04         ifeq      '1'
N01  c                   select
N01  c     LinNbr        wheneq    3
s01  C*                  CALL      'SYFAKDA'
s01  C*                  PARM                    KFTYP
s01  C*                  PARM                    UUPDAT            1
s01  C*                  PARM                    xxACTI            5
N01  c                   movel     TTACTI        xxacti
N01  c                   exsr      INQACT
     c                   movel     xxacti        TTACTI
N01   * Linje 10 = Kun transp
N01  c     LinNbr        wheneq    10
N01  C                   CALL      'SYTR04A'
N01  C                   PARM                    WSKTRA
N01   * Linje 11 = IKKE T&T-koder
N01  c     LinNbr        wheneq    11
N01  c     Posnbr        ifge      30
N01  c     Posnbr        andle     32
N01  c                   movel     WSUTT1        xxacti
N01  c                   exsr      INQACT
N01  c                   movel     xxacti        WSUTT1
N01  c                   endif
N01  c     Posnbr        ifge      34
N01  c     Posnbr        andle     36
N01  c                   movel     WSUTT2        xxacti
N01  c                   exsr      INQACT
N01  c                   movel     xxacti        WSUTT2
N01  c                   endif
N01  c     Posnbr        ifge      38
N01  c     Posnbr        andle     40
N01  c                   movel     WSUTT3        xxacti
N01  c                   exsr      INQACT
N01  c                   movel     xxacti        WSUTT3
N01  c                   endif
N01  c     Posnbr        ifge      42
N01  c     Posnbr        andle     44
N01  c                   movel     WSUTT4        xxacti
N01  c                   exsr      INQACT
N01  c                   movel     xxacti        WSUTT4
N01  c                   endif
N01  c     Posnbr        ifge      46
N01  c     Posnbr        andle     48
N01  c                   movel     WSUTT5        xxacti
N01  c                   exsr      INQACT
N01  c                   movel     xxacti        WSUTT5
N01  c                   endif
N01   * Linje 12 = KUN  T&T-koder
N01  c     LinNbr        wheneq    12
N01  c     Posnbr        ifge      30
N01  c     Posnbr        andle     32
N01  c                   movel     WSKTT1        xxacti
N01  c                   exsr      INQACT
N01  c                   movel     xxacti        WSKTT1
N01  c                   endif
N01  c     Posnbr        ifge      34
N01  c     Posnbr        andle     36
N01  c                   movel     WSKTT2        xxacti
N01  c                   exsr      INQACT
N01  c                   movel     xxacti        WSKTT2
N01  c                   endif
N01  c     Posnbr        ifge      38
N01  c     Posnbr        andle     40
N01  c                   movel     WSKTT3        xxacti
N01  c                   exsr      INQACT
N01  c                   movel     xxacti        WSKTT3
N01  c                   endif
N01  c     Posnbr        ifge      42
N01  c     Posnbr        andle     44
N01  c                   movel     WSKTT4        xxacti
N01  c                   exsr      INQACT
N01  c                   movel     xxacti        WSKTT4
N01  c                   endif
N01  c     Posnbr        ifge      46
N01  c     Posnbr        andle     48
N01  c                   movel     WSKTT5        xxacti
N01  c                   exsr      INQACT
N01  c                   movel     xxacti        WSKTT5
N01  c                   endif
N01   *
N01  c                   endsl
      *
     c                   goto      FeilTag
     c                   end
      *
      * TID
     C     TTTIME        IFLT      000100
     C     TTTIME        ORGT      235959
     C                   Z-ADD     04            LINNBR
     C                   Z-ADD     35            POSNBR
     C                   SETON                                        8830
     C                   MOVE      'JOV1422'     MSGNR
     C                   END
      * DATO
     C                   MOVE      TTDTÅ2        UÅÅ               2 0
     C                   MOVE      TTDTM         UMM               2 0
     C                   MOVE      TTDTD         UDD               2 0
     C                   CALL      'TESTDATO'
     C                   PARM                    UDD
     C                   PARM                    UMM
     C                   PARM                    UÅÅ
     C                   PARM                    UFLAG2            1 0
     C     UFLAG2        COMP      1                                      87
     C   87              MOVE      'SPD0001'     MSGNR
     C   87              SETON                                        30
     C   87              Z-ADD     04            LINNBR
     C   87              Z-ADD     20            POSNBR
     C*
      * ACT-KOD
     C     TTACTI        IFEQ      *BLANKS
     C                   Z-ADD     03            LINNBR
     C                   Z-ADD     20            POSNBR
     C                   SETON                                        8930
     C                   MOVE      'JOV1421'     MSGNR
     C                   ELSE
     C                   MOVEL     TTACTI        KFKOD
     C     KOKEY         CHAIN     KOFAST                             89
     C     *IN89         IFEQ      '1'
     C                   Z-ADD     03            LINNBR
     C                   Z-ADD     20            POSNBR
     C                   SETON                                        8930
     C                   MOVE      'JOV1421'     MSGNR
     C                   END
     C                   END
     C     *IN89         IFEQ      '0'
     C                   MOVEL     KFTXT         KODTXT
N03  C                   MOVEL     KFTXTE        KODTXTE          50
     C                   MOVEL     KFTXT         KODGRP
     C                   MOVE      KFTXT         KODWEB
     C                   ELSE
     C                   MOVE      *BLANKS       KODTXT
N03  C                   MOVE      *BLANKS       KODTXTE
     C                   MOVE      *BLANKS       KODGRP
     C                   MOVE      *BLANKS       KODWEB
     C                   END
      *
S04  C* N30TTTEXT        IFEQ      *BLANKS
N04  C  N30TTTEXL        IFEQ      *BLANKS
s03  C*                  MOVEL     KODTXT        TTTEXT
n03  C                   MOVEL     KODTXT        TTTEXL
n03  C                   MOVEL     KODTXTE       TTTEXT
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     5             POSNBR
     C                   MOVE      'JOV1423'     MSGNR
     C                   SETON                                          30
     C                   END
      *
     C   30              GOTO      FEILTAG
      *
     C*
     C     SLTB          ENDSR
N01  C***********************************************
N01  c     INQACT        Begsr
N01  C***********************************************
N01  C                   CALL      'SYFAKDA'
N01  C                   PARM                    KFTYP
N01  C                   PARM                    UUPDAT            1
N01  C                   PARM                    xxACTI            5
N01  C                   ENDSR
     C***********************************************
     C     SkrivTT       BEGSR
     C***********************************************
     c     WSTUNR        setll     HEAD14
     c     WSTUNR        reade     Head14                                 94
     c     *in94         doweq     '0'
     C                   MOVE      HEAVD         TTAVD
     C                   MOVE      HEOPD         TTOPD
N04  C                   MOVE      '*LE'         TTEDEV                         Manu=Concat Local/EN
      * div filter
     c                   exsr      Filter
     c     Skipp         cabeq     'Y'           NextOpd
     C                   TIME                    XULTID
     C                   MOVE      XULDD         TTDTLD
     C                   MOVE      XULMM         TTDTLM
     C                   MOVE      XULÅÅ         TTDTLÅ
     C                   MOVE      XULTM         TTTIML
S01  C*                  WRITE     TRACKFRX
N01  C                   WRITE     TRACKFR
      * manuelt tillagt TEI -> fiks dispstat E på evt disp.oppdrag
     c     TTACTI        ifeq      'TEI'
     c                   exsr      TEISR
     C                   end
     c     NextOpd       tag
     c     WSTUNR        reade     Head14                                 94
     C                   end
      *
     c                   endsr
     C***********************************************
     C     Filter        BEGSR
     C***********************************************
     c                   Move      'Y'           Skipp             1
      * transportør
S02  c*    FBNRKE        chain     DOKUF                              33
S02  c*    *in33         cabeq     '1'           UtFilt
     c     WSKTRA        ifne      *zeros
N02  c     FBNRKE        chain     DOKUF                              33
N02  c     *in33         cabeq     '1'           UtFilt
     c     WSKTRA        cabne     DFTRAN        UtFilt
     c                   end
      * ....
      * medta kun OPPDRAG UTEN følgende T&T-koder (en finnes - kutt )  Array TU
     c     1             do        5             nr                1 0
     c     TU(nr)        ifeq      *blanks
     c                   leave
     c                   endif
     c                   move      TU(nr)        ACTI
     c     TTkey         chain     Trackl02                           33
     c     *in33         cabeq     '0'           Utfilt
     c                   enddo
      * medta KUN OPPDRAG MED følgende T&T-koder (alle må finnes)      Array TK
     c     1             do        5             nr                1 0
     c     TK(nr)        ifeq      *blanks
     c                   leave
     c                   endif
     c                   move      TK(nr)        ACTI
     c     TTkey         chain     Trackl02                           33
     c     *in33         cabeq     '1'           Utfilt
     c                   enddo
      * ....
      * er gjennom alle filter - sett N i skipp-flagg
     c                   Move      'N'           Skipp
      *
     c     UtFilt        endsr
     C***********************************************
     C     TEISR         BEGSR
     C***********************************************
     C     FBNRK         setll     DOKUF
     C     FBNRK         reade     DOKUF                                  33
     C     *in33         doweq     *off
     C     DFBRTX        IFEQ      'Utk.op:'
     C     DFBRAV        ANDGE     '0001'
     C     DFBRAV        ANDLE     '9999'
     C     DFBROP        ANDGE     '0000001'
     C     DFBROP        ANDLE     '9999999'
     C                   MOVE      DFBRAV        DIAVD
     C                   MOVE      DFBROP        DIOPD
     C     DISPKE        CHAIN     DISP                               34
     C  N34DISTA1        IFEQ      'E'
     C     DISTA1        OREQ      'V'
     C                   MOVE      ' '           DISTA1
     C                   UPDATE    DISPR
     C                   END
     C                   END
     C     FBNRK         reade     DOKUF                                  33
     C                   END
     c
     C                   ENDSR
     C*
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
      *
     C     FSKEY         KLIST
     C                   KFLD                    TTAVD
     C                   KFLD                    TTOPD
     C     KOKEY         KLIST
     C                   KFLD                    KFTYP
     C                   KFLD                    KFKOD
     C                   MOVEL     'TRACKT'      KFTYP
     C     DISPKE        KLIST
     C                   KFLD                    DIAVD
     C                   KFLD                    DIOPD
     C     FBNRK         KLIST
     C                   KFLD                    YRI
     C                   KFLD                    TTAVD
     C                   KFLD                    TTOPD
     C     FBNRKE        KLIST
     C                   KFLD                    YRI
     C                   KFLD                    TTAVD
     C                   KFLD                    TTOPD
     C                   KFLD                    XXFBNR
     C                   MOVE      'F'           YRI               1
     C                   z-add     1             XXFBNR            3 0
     C     TTKEY         KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C                   KFLD                    NULFBN            3 0
     C                   KFLD                    ACTI              3
      *
     C                   MOVE      'N'           UUPDAT            1
      *
     C     *entry        PLIST
     C                   PARM                    UUTUNR            8 0
     C                   PARM                    UUKTRA            8 0
     C                   PARM                    UUacti            3
      *
     C                   MOVE      *ZEROS        TTDATE
     C                   MOVE      *ZEROS        TTTIME
     C                   MOVE      *ZEROS        WFDATE
     C                   MOVE      *ZEROS        WFDA8E
     C                   MOVE      *ZEROS        WFDATL
     C                   MOVE      *ZEROS        WFDA8L
      *
N01  C                   WRITE     WINDOWSZ
     C                   ENDSR
     C*
