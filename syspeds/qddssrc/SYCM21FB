     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPDOC('STD' 'HELPNU' 'HLP400')
     A                                      HELP
     A                                      ALTHELP
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  8  2VALUES(' ' '5')
     A            FASK           1A  O  8  5
     A            FAVAL          3A  O  8  7
     A            FABELV        13Y 2O  8 11EDTCDE(M)
     A            FABELN        13Y 2O  8 27EDTCDE(M)
     A            FAKUNR         8Y 0O  8 43EDTCDE(4)
     A            FAFAKT         7Y 0O  8 52EDTCDE(4)
     A            WSDATO         6S 0O  8 60
     A            WSMOMS         9Y 2O  8 67EDTCDE(M)
     A            FAST           1A  O  8 79COLOR(RED)
     A            FAAVD          4S 0H
     A            FAOPD          7S 0H
     A            FAJOUR         7S 0H
     A            FAÅR           2S 0H
     A            FAMND          2S 0H
     A            FABELU        13S 2H
     A            WSDAFF         6S 0H
     A            FAMOMS        13Y 2H
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0014)
     A                                      SFLPAG(0013)
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A            DSPRCN         4S 0O  2 11SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYSPED/SYCM21B'
     A                                      COLOR(BLU)
     A                                  1 20MSGCON(032 DIV2052 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            HEOPD          7Y 0O  1 53EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  1 61DATE
     A                                      EDTCDE(Y)
     A                                  1 70TIME
     A                                  2 61MSGCON(008 DIV0131 *LIBL/SYMSGF)
     A            HESG           3A  O  2 70COLOR(WHT)
     A                                  3 61MSGCON(005 DIV0062 *LIBL/SYMSGF)
     A            WSDT           6Y 0O  3 70EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  2  2MSGCON(006 DIV0132 *LIBL/SYMSGF)
     A            HEKNS          8Y 0O  2 21TEXT('Kundenr Selger')
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A            HENAS         30A  O  2 30TEXT('Navn Selger')
     A                                      COLOR(WHT)
     A            HEGN          15A  O  4 66TEXT('Godsnummer')
     A                                      COLOR(WHT)
     A                                  3  2MSGCON(017 DIV0134 *LIBL/SYMSGF)
     A            HEKNSF         8Y 0O  3 21TEXT('Kundenr Selg.fakm')
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A            HENASF        30A  O  3 30TEXT('Navn Selg.fakm')
     A                                      COLOR(WHT)
     A                                  4  2MSGCON(006 DIV0135 *LIBL/SYMSGF)
     A                                  4 61MSGCON(004 TRO0498 *LIBL/SYMSGF)
     A            HEKNK          8Y 0O  4 21TEXT('Kundenr Kjøper')
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A            HENAK         30A  O  4 30TEXT('Navn Kjøper')
     A                                      COLOR(WHT)
     A                                  5 61MSGCON(005 DIV0136 *LIBL/SYMSGF)
     A            HEAVD          4Y 0O  5 70TEXT('Avdeling')
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  5  2MSGCON(017 DIV0137 *LIBL/SYMSGF)
     A            HEKNKF         8Y 0O  5 21TEXT('Kundenr Kjøp.fakm')
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A            HENAKF        30A  O  5 30TEXT('Navn Kjøp.fakm')
     A                                      COLOR(WHT)
     A                                  6  2MSGCON(040 DIV2006 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  6 61MSGCON(006 DIV0138 *LIBL/SYMSGF)
     A            HEFR           3A  O  6 70TEXT('Frankatur')
     A                                      COLOR(WHT)
     A                                  7  2MSGCON(078 DIV2115 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A          R BUNN
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF01(01 'Bytt avdlinger')
     A                                      CF02(02 'Gå til valgbilde')
     A                                      CF03(03 'Avslutt')
     A                                      CF11(11 'Vise TOLLDEKLARASJON)')
     A                                      CF12(12 'Gå til rullbilde')
     A                                      ROLLUP(25 'Bla frem')
     A                                      ROLLDOWN(26 'Bla tilbake')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                               O  2  2MSGCON(18 DIV0067 SYMSGF)
     A                                      COLOR(BLU)
     A                               O  2 22MSGCON(18 DIV0113 SYMSGF)
     A                                      COLOR(BLU)
     A                               O  2 42MSGCON(18 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A                               O  2 62MSGCON(18 DIV2053 SYMSGF)
     A                                      COLOR(BLU)
     A                               O  3  2MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A  30        MSGTXT        78A  O  4  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A          R NYVARE
     A                                      CF01(01 'Bytt avdlinger')
     A                                      CF02(02 'Gå til valgbilde')
     A                                      CF03(03 'Avslutt')
     A                                      CF11(11 'Vise TOLLDEKLARASJON)')
     A                                      CF12(12 'Gå til rullbilde')
     A                                  1  2'SYSPED/SYFM21B'
     A                                      COLOR(BLU)
     A                                  1 20MSGCON(032 DIV0130 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            HEOPD          7Y 0O  1 53EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  1 61DATE
     A                                      EDTCDE(Y)
     A                                  1 70TIME
     A                                  2 61MSGCON(008 DIV0131 *LIBL/SYMSGF)
     A            HESG           3A  O  2 70COLOR(WHT)
     A                                  3 61MSGCON(005 DIV0062 *LIBL/SYMSGF)
     A            WSDT           6Y 0O  3 70EDTCDE(Y)
     A                                      COLOR(WHT)
     A                                  2  3MSGCON(006 DIV0132 *LIBL/SYMSGF)
     A            HEKNS          8Y 0O  2 21TEXT('Kundenr Selger')
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A            HENAS         30A  O  2 30TEXT('Navn Selger')
     A                                      COLOR(WHT)
     A            HEGN          15A  O  4 66TEXT('Godsnummer')
     A                                      COLOR(WHT)
     A                                  3  3MSGCON(017 DIV0134 *LIBL/SYMSGF)
     A            HEKNSF         8Y 0O  3 21TEXT('Kundenr Selg.fakm')
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A            HENASF        30A  O  3 30TEXT('Navn Selg.fakm')
     A                                      COLOR(WHT)
     A                                  4  3MSGCON(006 DIV0135 *LIBL/SYMSGF)
     A            HEKNK          8Y 0O  4 21TEXT('Kundenr Kjøper')
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A            HENAK         30A  O  4 30TEXT('Navn Kjøper')
     A                                      COLOR(WHT)
     A                                  4 61MSGCON(004 TRO0498 *LIBL/SYMSGF)
     A                                  5 61MSGCON(005 DIV0136 *LIBL/SYMSGF)
     A            HEAVD          4Y 0O  5 70TEXT('Avdeling')
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A                                  5  3MSGCON(017 DIV0137 *LIBL/SYMSGF)
     A            HEKNKF         8Y 0O  5 21TEXT('Kundenr Kjøp.fakm')
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
     A            HENAKF        30A  O  5 30TEXT('Navn Kjøp.fakm')
     A                                      COLOR(WHT)
     A                                  6  2MSGCON(040 DIV2006 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  6 61MSGCON(006 DIV0138 *LIBL/SYMSGF)
     A            HEFR           3A  O  6 70TEXT('Frankatur')
     A                                      COLOR(WHT)
     A                                  7  2MSGCON(078 DIV2115 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                 22  2MSGCON(018 DIV0067 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 22 22MSGCON(018 DIV0113 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 22 42MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 22 62MSGCON(018 DIV2053 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A          R VAREL3
     A                                      CF12(12 'Forrige')
     A                                      SLNO(19)
     A                                      CLRL(06)
     A                                  2  2MSGCON(076 DIV2116 SYMSGF)
     A            FASK           1A  O  3  2TEXT('Selger/Kjøper S/K')
     A                                      DSPATR(PC)
     A                                      COLOR(WHT)
     A            FAVAL          3A  O  3  5TEXT('Valutakode')
     A                                      COLOR(WHT)
     A            FABELV        13Y 2O  3  9TEXT('Beløp valuta')
     A                                      EDTCDE(L)
     A                                      COLOR(WHT)
     A            FABELN        13Y 2O  3 25TEXT('Beløp')
     A                                      EDTCDE(L)
     A                                      COLOR(WHT)
     A            FABELU        13Y 2O  3 41TEXT('Beløp')
     A                                      EDTCDE(L)
     A                                      COLOR(WHT)
     A            FAOPKO         1A  O  3 59COLOR(WHT)
     A            FAÅR           2S 0O  3 65COLOR(WHT)
     A            FAMND          2S 0O  3 68COLOR(WHT)
     A                                  4  2MSGCON(006 DIV2007 SYMSGF)
     A            FAKUNR         8Y 0O  4 10COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  4 22MSGCON(006 DIV2008 SYMSGF)
     A            FAFAKT         7Y 0O  4 30COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  4 42MSGCON(006 DIV2009 SYMSGF)
     A            WSDATO         6Y 0O  4 50COLOR(WHT)
     A                                      EDTCDE(Y)
     A                                  4 62MSGCON(006 DIV2010 SYMSGF)
     A            WSDAFF         6Y 0O  4 70COLOR(WHT)
     A                                      EDTCDE(Y)
     A                                  5  2MSGCON(007 DIV2013 SYMSGF)
     A            FAJOUR         7Y 0O  5 10COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  5 22MSGCON(006 DIV2117 SYMSGF)
     A            FAMOMS        13Y 2O  5 30COLOR(WHT)
     A                                      EDTCDE(L)
     A                                  6  2MSGCON(018 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
