     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HELP
     A                                      ALTHELP
      *
     A          R GSS012DA                  SFL
     A            WSNR           1A  B  5  2VALUES(' ' '2' '4')
     A  27                                  DSPATR(PR)
     A  27                                  DSPATR(ND)
     A            OVONO          7Y 0H
     A            OVLNO          3Y 0H
     A            OVFEEC         3A  O  5 10
     A            OVCURC         3A  O  5 18
     A            OVAMT         11Y 2O  5 27EDTCDE(L)
     A            OVFT          20A  O  5 41
     A            OVVAT          1A  O  5 62
     A            OVLC           5A  O  5 66
     A            OVST           1A  O  5 73
      *
     A          R GSS012DB                  SFLCTL(GSS012DA)
     A                                      SFLSIZ(0016)
     A                                      SFLPAG(0015)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                  1  2'SHIPSYS/GSS012D'
     A                                      COLOR(BLU)
     A                                  1 21'Work with invoicebasis'
     A                                      DSPATR(HI)
     A            ZRECNO         4S 0O  1 50SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                  1 70TIME
     A                                  2  2'Ordre No.'
     A            ORONO          7Y 0O  2 12EDTCDE(4)
     A                                      COLOR(BLU)
     A                                  2 22'Vessel'
     A            XBNM          20A     2 29COLOR(BLU)
     A                                  2 52'Supplier'
     A            XSNM          20A     2 61COLOR(BLU)
     A N27                              3  2'SELECT OPTION, PRESS ENTER:'
     A                                      COLOR(BLU)
     A N27                              3 32'2=Change  4=delete'
     A                                      COLOR(BLU)
     A                                  4  2'Option  Feecode Currency Amount   -
     A                                           Free text            VAT Locat-
     A                                      . St.'
     A                                      DSPATR(HI)
      *
     A          R GSS012DC
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF03(03 'AVSLUTT')
     A                                      CF05(05 'Frisk')
     A N27                                  CF06(06 'LAGE NY')
N02  A N27                                  CF07(07 'BEREGN')
     A                                      CF12(12 'FORRIGE')
     A                                      SLNO(23)
     A                                      CLRL(02)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A N27                              1  2'F3=End   F5=Refresh   F6=Create   -
S02  A*                                     F12=Cancel'
N02  A                                      F7=Calc Auto.  F12=Cancel'
     A                                      COLOR(BLU)
     A  27                              1  2'F3=End   F5=Refresh   F12=Cancel'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  2  2MSGID(&MSGNR GSSMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R GSS012DD
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF04(04 'Søking')
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1 10'Feecode Currency Amount         F-
     A                                      ree text           VAT Locat. St.'
     A                                      DSPATR(HI)
     A            WSFEEC         3A  B  2 10DSPATR(UL)
     A  90                                  DSPATR(RI)
     A            WSCURC         3A  B  2 18DSPATR(UL)
     A  89                                  DSPATR(RI)
     A            WSAMT         11Y 2B  2 27DSPATR(UL)
     A  88                                  DSPATR(RI)
     A                                      EDTCDE(L)
     A            WSFT          20A  B  2 41DSPATR(UL)
     A            WSVAT          1A  B  2 62DSPATR(UL)
     A                                      VALUES(' ' 'Y' 'J' 'N')
     A            WSLC           5A  O  2 66DSPATR(UL)
     A                                      DSPATR(HI)
     A            WSST           1A  O  2 73DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  3  2'F4=Browse   F12=Cancel'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  4  2MSGID(&MSGNR GSSMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R GSS012DF
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A                                  1 10'Feecode Currency Amount         F-
     A                                      ree text           VAT Locat. St.'
     A                                      DSPATR(HI)
     A            OVFEEC         3A  O  2 10DSPATR(UL)
     A                                      DSPATR(PC)
     A                                      DSPATR(HI)
     A            OVCURC         3A  O  2 18DSPATR(UL)
     A                                      DSPATR(HI)
     A            OVAMT         11Y 2O  2 27DSPATR(UL)
     A                                      DSPATR(HI)
     A                                      EDTCDE(L)
     A            OVFT          20A  O  2 41DSPATR(UL)
     A                                      DSPATR(HI)
     A            OVVAT          1A  O  2 62DSPATR(UL)
     A                                      DSPATR(HI)
     A            OVLC           5A  O  2 66DSPATR(UL)
     A                                      DSPATR(HI)
     A            OVST           1A  O  2 73DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  3  2'F12=Cancel'
     A                                      COLOR(BLU)
      *
     A          R GSS012DG
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF03(03 'AVSLUTT')
     A                                      CF05(05 'Frisk')
     A N27                                  CF06(06 'LAGE NY')
N02  A N27                                  CF07(07 'NY BERGNING')
     A                                      CF12(12 'AVSLUTT')
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SHIPSYS/GSS012D'
     A                                      COLOR(BLU)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                  1 70TIME
     A                                  1 21'Work with invoicebasis'
     A                                      DSPATR(HI)
     A                                  2  2'Ordre No.'
     A            ORONO          7Y 0O  2 12EDTCDE(4)
     A                                      COLOR(BLU)
     A                                  2 22'Vessel'
     A            XBNM          20A     2 29COLOR(BLU)
     A                                  2 52'Supplier'
     A            XSNM          20A     2 61COLOR(BLU)
     A N27                              3  2'Select option, press ENTER:'
     A                                      COLOR(BLU)
     A N27                              3 32'2=Change  4=Delete'
     A                                      COLOR(BLU)
     A                                  4  2'Option  Feecode Currency Amount   -
     A                                           Free text            VAT Locat-
     A                                      . St.'
     A                                      DSPATR(HI)
     A                                  6  6'No invioce line found'
     A                                      COLOR(RED)
     A N27                             23  2'F3=End   F5=Refresh   F6=Create   -
E02  A*                                     F12=Cancel'
N02  A                                      F7=Calc Auto.  F12=Cancel'
     A                                      COLOR(BLU)
     A  27                             23  2'F3=End   F5=Refresh   F12=Cancel'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR GSSMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
