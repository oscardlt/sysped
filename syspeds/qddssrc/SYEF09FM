     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPDOC('VEDLWID' 'HELPNU' 'HLP400')
     A                                      HELP
     A                                      ALTHELP
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  4  6VALUES(' ' '2' '4')
     A            KPVFRA         3   O  4 15TEXT('STED FRA')
     A            KPVTIL         3   O  4 25TEXT('STED TIL')
     A            KPVSON         2  0O  4 42TEXT('SAS-SONE')
     A                                      EDTCDE(4)
     A            KPVPRI         5  2O  4 57TEXT('KG-PRIS')
     A                                      EDTCDE(4)
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0018)
     A                                      SFLPAG(0017)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A            ZRECNO         4S 0O  2 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  3'SYEF09'
     A                                      COLOR(BLU)
     A                                  2  3MSGCON(030 DIV0016 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  2 36MSGCON(030 DIV0017 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  1 11MSGCON(045 SHU1012 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  3  3MSGCON(070 DIV1958 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A          R BUNN
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF03(03 'Avslutt program')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            MSG201        18A  O  3  1MSGID(DIV 0020 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG202        18A  O  3 20MSGID(DIV 0021 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG203        18A  O  3 40MSGID(DIV 0022 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG204        18A  O  3 60MSGID(DIV 0023 SYMSGF)
     A                                      COLOR(BLU)
     A  98        MSG205        78A  O  4  2MSGID(DIV 0012 SYMSGF)
     A                                      COLOR(WHT)
     A          R VARELI
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A            MSG301        60A  O  1 15MSGID(DIV 1959 SYMSGF)
     A                                      COLOR(WHT)
     A            WSFRA          3   B  2 15TEXT('STED FRA')
     A                                      DSPATR(UL)
     A  40                                  DSPATR(PR)
     A N40N30
     AO 90                                  DSPATR(PC)
     A  90                                  DSPATR(RI)
     A  40                                  COLOR(WHT)
     A N40                                  COLOR(YLW)
     A            WSTIL          3   B  2 25TEXT('STED TIL')
     A                                      DSPATR(UL)
     A  40                                  DSPATR(PR)
     A  89                                  DSPATR(PC)
     A  89                                  DSPATR(RI)
     A  40                                  COLOR(WHT)
     A N40                                  COLOR(YLW)
     A            WSSON          1  0B  2 42TEXT('SAS-SONE')
     A                                      DSPATR(UL)
     A  40N30                               DSPATR(PC)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSPRI          5  2B  2 57TEXT('KG-PRIS')
     A                                      DSPATR(UL)
     A  88                                  DSPATR(PC)
     A  88                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            MSG302        18A  O  3  2MSGID(DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  4  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A          R VAREL2
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A            MSG601        12A  O  2  2MSGID(DIV0026 SYMSGF)
     A            W2FRA          3A  I  2 16DSPATR(UL)
     A                                      DSPATR(PC)
     A            W2TIL          3A  I  2 21DSPATR(UL)
     A            MSG602        18A  O  3  2MSGID(DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A          R VAREL3
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A            MSG401        60A  O  1 15MSGID(DIV 1959 SYMSGF)
     A                                      COLOR(WHT)
     A            KPVFRA         3   O  2 15TEXT('STED FRA')
     A                                      DSPATR(UL)
     A                                      DSPATR(PC)
     A                                      COLOR(WHT)
     A            KPVTIL         3   O  2 25TEXT('STED TIL')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            KPVSON         2  0O  2 42TEXT('SAS-SONE')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            KPVPRI         5  2O  2 57TEXT('KG-PRIS')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            MSG402        18A  O  4  2MSGID(DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A          R NYVARE
     A                                      CF03(03 'Avslutt program')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A            MSG501        60A  O  1 15MSGID(DIV 1953 SYMSGF)
     A                                      COLOR(WHT)
     A            MSG502        30A  O  2  2MSGID(DIV 0016 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG503        30A  O  2 34MSGID(DIV 0017 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG504        30A  O  3  3MSGID(DIV 1958 SYMSGF)
     A                                      COLOR(WHT)
     A            MSG506        18A  O 23  1MSGID(DIV 0020 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG507        18A  O 23 20MSGID(DIV 0021 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG508        18A  O 23 40MSGID(DIV 0022 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG509        18A  O 23 60MSGID(DIV 0023 SYMSGF)
     A                                      COLOR(BLU)
     A  98        MSG510        78A  O 24  2MSGID(DIV 0012 SYMSGF)
     A                                      COLOR(WHT)
