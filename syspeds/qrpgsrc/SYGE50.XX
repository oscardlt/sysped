      *********************************************************************
      *
      *
XXTPG+*  CRTPGM SYSPED/SYOPTT1 MODULE(*LIBL/SYOPTT1 *LIBL/INCGI)
XXTPGM*         ACTGRP(CGI) AUT(*USE)
      *
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FCUNDL01   IF   E           K DISK    USROPN
     FNOPNR     IF   E           K DISK    USROPN
     Fhead38    IF   E           K DISK    usropn                               Oppdr.giv/dato
     Fhead22    IF   E           K DISK    usropn                               Avd/dato
     F                                     RENAME(HEADFR:HEAD22R)
     FheadDT    IF   E           K DISK    usropn                               Dato
     F                                     RENAME(HEADFR:HEADDTR)
     FTURER14   IF   E           K DISK    usropn
     FTRACKl02  IF   E           K DISK    usropn
     FKalender1 IF   E           K DISK    usropn
      *********************************************************************
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      /copy syspeds/qrpgsrcpy,variables3
      *
     D OddEven         s             10
     0* Client input variables
     D user            s             10
     D AVVKNR          S              8
     D AVVKNN          S              8  0
     D AVD             S              4
     D AVDN            S              4  0
     D TRANS           S              8
     D TRANSN          S              8  0
     D DATO            s             10
     D FraDT8          s              8  0
     D DATOT           s             10
     D TilDT8          s              8  0
     D AREF            s             15    VARYING
     D MottNavn        s             20    VARYING
     D MottPost        s             20    VARYING
     D VisKl           s              1
      *
     D ANTSND          s              5  0
     D MaxSN           s              5
     D MaxSND          s              5  0
     D ANTRCD          s              5  0
     D MaxRC           s              5
     D MaxRCD          s              5  0
     D LevPros         s              5  2
     D AntOK           s              9  0
     D AntFeil         s              9  0
     D AntDef          s              9  0
     D AntUdef         s              9  0
     D TSTSCAN15       s             15
     D TSTSCAN30       s             30
     D CSV             s              1
     D CSVFILE         s             50
     D Spaces          s             12a   inz('&nbsp;&nbsp;')
     D ItemCount       s             10i 0
     D i               s             10i 0
     D                 DS
     D TEIDATKL                1     12  0
     D  TEIDAT                 1      8  0
     D  TEITIM                 9     14  0
     D                 DS
     D TEUDATKL                1     12  0
     D  TEUDAT                 1      8  0
     D  TEUTIM                 9     14  0
     D                 DS
     D PODDATKL                1     12  0
     D  PODDAT                 1      8  0
     D  PODTIM                 9     14  0
     D                 DS
     D VI1DATKL                1     12  0
     D  VI1DAT                 1      8  0
     D  VI1TIM                 9     14  0
     D                 DS
     D VI2DATKL                1     12  0
     D  VI2DAT                 1      8  0
     D  VI2TIM                 9     14  0
     D                 DS
     D OMXDATKL                1     12  0
     D  OMXDAT                 1      8  0
     D  OMXTIM                 9     14  0
     D                 DS
     D* DsAvdOpd               1     10
     D  DsAvdOpd               1     12
     D  DsAVD                  1      4
     D  DsSlash                5      5
     D* DsOPD                  6     10
     D  DsOPD                  6     12
     D                 DS
     D  HEADK3                 1     30
     D    HEADK3A              1      4
     D    HEADK3B              6     30
     D                 DS
     D  NOAVGD                 1      7
     D  AV                     1      7
     D                                     DIM(7)
     D                 DS
     D  DGNAVN                 1     14    inz('MaTiOnToFrLøSø')
     D  DGN                    1     14
     D                                     DIM(7)
     D UPC             C                   CONST('ABCDEFGHIJKLMNOPQRST-
     D                                     UVWXYZÆØÅ')
     D LO              C                   CONST('abcdefghijklmnopqrst-
     D                                     uvwxyzæøå')
      *
      /copy syspeds/qrpgsrcpy,prolog3
      *
      * Get program start time for calculating execution time
     C                   time                    timedata1
      * Parse input / cvt to numeric
     C                   exsr      Parse
      * File open / keys
     C                   EXSR      INIT
      * Read output skeleton html member into memory
     C                   exsr      LoadHtml
      * Set section variables
     C                   exsr      Settop
     C                   callp     wrtsection('top')
      *
      * HOVEDRUTINE
      *
      * Vis liste over registrete leveringer
      *
     C     MaxSnd        IFEQ      *ZEROS
     C                   Z-add     500           MaxSnd
     C                   END
     C     MaxRcd        IFEQ      *ZEROS
     C                   Z-add     2000          MaxRcd
     C                   END
      *
     C                   Move      *zeros        AntSnd
     C                   Move      *zeros        AntRcd
      * ulike nøkler etter hva som er oppgitt
     C                   select
     C     BIKUNR        whenne    *zeros
     c     Hea38Key      setll     head38
     C     AVDN          whenne    *zeros
     c     Hea22Key      setll     head22
     c                   other
     c     FraDt8        setll     headDT
     c                   endsl
      *
     C                   select
     C     BIKUNR        whenne    *zeros
     c     BIKUNR        Reade     head38                                 35
     C     AVDN          whenne    *zeros
     c     AVDN          Reade     head22                                 35
     c                   other
     c                   Read      headDT                                 35
     c                   endsl
      *
     c     *IN35         doweq     '0'
     C                   Add       1             AntRcd
     C     HEDTOP        CabGt     TilDT8        Utles
     C     AntRcd        CabGe     MaxRcd        Utles
      * div filter
     c     HEST          CabEq     'S'           NextRec
     C     TRSLAG        CabNe     'HO'          NextRec
     c     AVDN          Ifne      *zeros
     C     AVDN          CabNe     HEAVD         NextRec
     c                   end
      * med tiden bør transp.nr inn i heknt så det blir unødig å chaine TURER
     c     TRANSN        Ifne      *zeros
     C     hepro         Cabeq     *zeros        NextRec
     c     hepro         chain     TURER14                            33
     C     *in33         Cabeq     '1'           NextRec
     C                   move      TUKNT2        HEKNT
     C     TRANSN        CabNe     HEKNT         NextRec
     c                   end
      *
     c     AREF          ifne      *BLANKS
     C     LO:UPC        XLATE     HERFA         HERFA
     C                   eval      TSTSCAN15  = uppify(HERFA)
     C     AREF          SCAN      TSTSCAN15                              33
     c     *IN33         CaBEQ     *OFF          NextRec
     C                   end
      *
      *
     c     MottNavn      ifne      *BLANKS
     C     LO:UPC        XLATE     HENAK         HENAK
     C                   eval      TSTSCAN30  = uppify(HENAK)
     C     MottNavn      SCAN      TSTSCAN30                              33
     c     *IN33         CaBEQ     *OFF          NextRec
     C                   end
      *
     c     MottPost      ifne      *BLANKS
     C     LO:UPC        XLATE     HEADK3        HEADK3
     C                   eval      TSTSCAN30  = uppify(HEADK3)
     C     MottPost      SCAN      TSTSCAN30                              33
     c     *IN33         CaBEQ     *OFF          NextRec
     C                   end
      * Ok - skriv
     C                   exsr      Setrow
     C                   callp     wrtsection('row')
     C                   Add       1             AntSnd
     C     AntSnd        CabGe     MaxSnd        Utles
      *
     c     NextRec       Tag
     C                   select
     C     BIKUNR        whenne    *zeros
     c     BIKUNR        Reade     head38                                 35
     C     AVDN          whenne    *zeros
     c     AVDN          Reade     head22                                 35
     c                   other
     c                   Read      headDT                                 35
     c                   endsl
     c                   enddo
      *
      *
     C     UTLES         TAG
     c                   eval      AntDef = AntOK + AntFeil
     c     AntDef        ifeq      0
     c                   eval      LevPros=0
     c                   else
     c                   eval      LevPros=AntOK  / AntDef * 100
     c                   end
     C                   callp     updHTMLvar('ANTOK ':
     C                             %trim(%editc(ANTOK:'3')))
     C                   callp     updHTMLvar('ANTDEF':
     C                             %trim(%editc(ANTDEF:'3')))
     C                   callp     updHTMLvar('ANTUDEF':
     C                             %trim(%editc(ANTUDEF:'3')))
     C                   callp     updHTMLvar('LEVPROS':
     C                             %trim(%editc(LEVPROS:'J')))
     C                   callp     updHTMLvar('ANTSND':
     C                             %trim(%editc(ANTSND:'3')))
     C                   callp     updHTMLvar('MaxSnd':
     C                             %trim(%editc(MaxSnd:'3')))
     C                   callp     updHTMLvar('ANTRCD':
     C                             %trim(%editc(ANTRCD:'3')))
     C                   callp     updHTMLvar('MaxRcd':
     C                             %trim(%editc(MaxRcd:'3')))
      * Quit
      * Compute run time
     C                   time                    timedata2
     C     timedata2     subdur    timedata1     ms:*ms
     C                   eval      sec = ms / 1000000
     C                   callp     updhtmlvar('runtime':
     C                             %trim(%editw(sec:'     0 .   ')))
      *
     C                   callp     wrtsection('bot')
      *
     C                   exsr      Exit
      *
     C                   eval      *inlr = *on
     C                   return
      *
      *
      *=====================================================================
      * Parse input / cvt to numeric
      *=====================================================================
     C     Parse         begsr
      * Parse variables from QUERY_STRING environment variable
     C                   eval      DATO     = zhbgetvar('DATO')
     C                   eval      Fradt8   = c2n2(DATO)
     C                   eval      DATOT    = zhbgetvar('DATOT')
     C                   eval      Tildt8   = c2n2(DATOT)
     C                   eval      MAXSN    = zhbgetvar('MaxSn')
     C                   eval      MaxSnd   = c2n2(MAXSN)
     C                   eval      MAXRC    = zhbgetvar('MaxRc')
     C                   eval      MaxRcd   = c2n2(MAXRC)
     C                   eval      AREF    = zhbgetvar('AREF')
     C     LO:UPC        XLATE     AREF          AREF
     C                   eval      AREF    = uppify(AREF)
     C                   eval      MottNavn = zhbgetvar('MottNavn')
     C     LO:UPC        XLATE     MottNavn      MottNavn
     C                   eval      MottNavn = uppify(MottNavn)
     C                   eval      MottPost = zhbgetvar('MottPost')
     C     LO:UPC        XLATE     MottPost      MottPost
     C                   eval      MottPost = uppify(MottPost)
      * Userid.....
     C                   eval      user = zhbgetvar('user')
     C                   eval      AVVKNR  = zhbgetvar('AVVKNR')
     C                   eval      AVVKNN  = c2n2(AVVKNR)
     C                   eval      AVD  = zhbgetvar('AVD')
     C                   eval      AVDN  = c2n2(AVD)
     C                   eval      TRANS  = zhbgetvar('TRANS')
     C                   eval      TRANSN = c2n2(TRANS)
     C                   eval      VisKl = zhbgetvar('VisKl')
     C                   eval      CSV  = zhbgetvar('CSV')
     C                   endsr
      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   CLOSE     BRIDF
     C                   CLOSE     cundl01
     C                   CLOSE     head38
     C                   CLOSE     head22
     C                   CLOSE     headDT
     C                   CLOSE     turer14
     C                   CLOSE     trackl02
     C                   CLOSE     NOPNR
     C                   CLOSE     kalender1
     C                   endsr
      *=====================================================================
      * Read output skeleton html member into memory
      *=====================================================================
     C     LoadHtml      begsr
N02  c     CSV           ifeq      'Y'
N02  C                   callp     gethtml('HTMLSRC':
N02  C                             '*LIBL':
N02  C                             'SYOPTT1S':'/CGITAG/')
N02  c                   else
     C                   callp     gethtml('HTMLSRC':
     C                             '*LIBL':
     C                             'SYOPTT1':'/CGITAG/')
N02  c                   end
     C                   endsr
      *=====================================================================
      *  Set variable data for section /$top
      *=====================================================================
     C     SetTop        begsr
      *
     C                   callp     updHTMLvar('ROWHEAD':RowHead)
     C                   callp     updHTMLvar('LogoImg':LogoImg)
     C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updHTMLvar('USER':USER)
     C                   callp     updHTMLvar('BESK':BIBESK)
     C                   callp     updHtmlVar('KUNDE':
     C                             %trim(%editc(BIKUNR:'Z')))
     C                   callp     updHtmlVar('AVVKNR':
     C                             %trim(%editc(AVVKNN:'Z')))
     C                   callp     updHTMLvar('KNAVN':KNAVN)
      * Søkeparam
     C                   callp     updHTMLvar('DATO':
     C                             %trim(%editw(Fradt8:'    .  .  ')))
     C                   callp     updHTMLvar('DATU':DATO)                      UNEDITED
     C                   callp     updHTMLvar('DATOT':
     C                             %trim(%editw(Tildt8:'    .  .  ')))
     C                   callp     updHTMLvar('DATUT':DATOT)                    UNEDITED
     c                   callp     updHtmlVar('AVD':
     C                             %trim(%editc(AVDN:'Z')))
     c                   callp     updHtmlVar('TRANS':
     C                             %trim(%editc(TRANSN:'Z')))
     C                   callp     updHTMLvar('AREF':AREF)
     C                   callp     updHTMLvar('MOTTNAVN':MOTTNAVN)
     C                   callp     updHTMLvar('MOTTPOST':MOTTPOST)
     C                   callp     updHTMLvar('VISKL':VISKL)
     c                   movel     timedata1     time26           26
     c                   eval      csvfile = %trim(USER)+'_'+Time26
     C                   callp     updHTMLvar('CSVFILE':CSVFILE)
      *
     C                   endsr
      *=====================================================================
      *  Set variable data for section /$row
      *=====================================================================
     C     Setrow        begsr
     c     OddEven       IFEQ      ODD
     c                   eval      OddEven = EVEN
     c                   else
     c                   eval      OddEven = ODD
     c                   end
     C                   callp     updHTMLvar('ODDEVEN':OddEven)
      *
     c     HXSNDN        Ifeq      *blanks
     c                   MOVE      HEAVD         DsAvd
     c                   MOVE      '/'           DsSlash
     c                   MOVE      HEOPD         DsOpd
     c                   MOVEL     DsAvdOpd      HXSNDN
     c                   end
     C                   callp     updHTMLvar('HXSNDN':HXSNDN)
     C                   callp     updHTMLvar('HEAVD':
     C                             %trim(%editc(HEAVD:'Z')))
     C                   callp     updHTMLvar('TRKNFA':
     C                             %trim(%editc(TRKNFA:'Z')))
     C                   callp     updHTMLvar('HENAK':HENAK)
     C                   callp     updHTMLvar('HEADK3A':HEADK3A)
     C                   callp     updHTMLvar('HEADK3B':HEADK3B)
     c                   move      *blanks       NOAVGD
     c                   move      *zeros        NOFRFT
     c                   testn                   headk3a              33
     C     *IN33         IFEQ      '1'
     C     HEAVD         ifgt      8000
     c                   move      1000          ZZTERM
     c                   else
     c                   move      3000          ZZTERM
     c                   end
     C                   move      headk3a       NOPONR
     C     InnlKey       CHAIN     NOPNR                              33
     C                   end
     C                   callp     updHTMLvar('NOAVGD':NOAVGD)
     C                   callp     updHTMLvar('NOFRFT':
     C                             %trim(%editc(NOFRFT:'Z')))
      *
     C                   callp     updHTMLvar('HENT':
     C                             %trim(%editc(HENT:'Z')))
     C                   callp     updHTMLvar('HEVKT':
     C                             %trim(%editc(HEVKT:'Z')))
     c                   exsr      GetDates
     c     VisKl         ifne      'Y'
     C                   callp     updHTMLvar('TEIDAT':
     C                             %trim(%editw(TEIDAT:'    .  .  ')))
     C                   callp     updHTMLvar('OMXDAT':
     C                             %trim(%editw(OMXDAT:'    .  .  ')))
     C                   callp     updHTMLvar('TEUDAT':
     C                             %trim(%editw(TEUDAT:'    .  .  ')))
     C                   callp     updHTMLvar('VI1DAT':
     C                             %trim(%editw(VI1DAT:'    .  .  ')))
     C                   callp     updHTMLvar('VI2DAT':
     C                             %trim(%editw(VI2DAT:'    .  .  ')))
     C                   callp     updHTMLvar('PODDAT':
     C                             %trim(%editw(PODDAT:'    .  .  ')))
     C                   else
     C                   callp     updHTMLvar('TEIDAT':
     C                             %trim(%editw(TEIDATKL:'    .  .  _  :  ')))
     C                   callp     updHTMLvar('OMXDAT':
     C                             %trim(%editw(OMXDATKL:'    .  .  _  :  ')))
     C                   callp     updHTMLvar('TEUDAT':
     C                             %trim(%editw(TEUDATKL:'    .  .  _  :  ')))
     C                   callp     updHTMLvar('VI1DAT':
     C                             %trim(%editw(VI1DATKL:'    .  .  _  :  ')))
     C                   callp     updHTMLvar('VI2DAT':
     C                             %trim(%editw(VI2DATKL:'    .  .  _  :  ')))
     C                   callp     updHTMLvar('PODDAT':
     C                             %trim(%editw(PODDATKL:'    .  .  _  :  ')))
     C                   end
      * beregning av leveringskvalitet kun dersom...
     c                   move      *zeros        AntDgr            5 0
     c                   move      *blanks       KvalTxt          10
     c     NOAVGD        ifne      *blanks
     c     NOFRFT        andne     *zeros
     c     TEIDAT        andne     *zeros
     c     PODDAT        andne     *zeros
     c     HESGM         andne     'LEVERT'
     c                   exsr      BerKval
     c                   end
     C                   callp     updHTMLvar('ANTDGR':
     C                             %trim(%editc(ANTDGR:'Z')))
     C                   callp     updHTMLvar('KVALTXT':KVALTXT)
     c                   select
     c     KVALTXT       wheneq    '1'
     c                   add       1             AntOK             9 0
     c     KVALTXT       wheneq    '0'
     c                   add       1             AntFEIL           9 0
     c                   other
     c                   add       1             AntUdef           9 0
     c                   endsl
      *
     C                   endsr
      *
      ***************************************************************************
     C     BerKval       begsr
      ***************************************************************************
      * legg dagnavn for framføringsdager ut i en streng..
     C                   Move      *blanks       AvDG             30
     C     1             DO        7             NR                1 0
     C     AV(NR)        IFGE      '1'
     C     AV(NR)        ANDLE     '7'
     C                   MOVE      AV(NR)        NN                1 0
     C                   CAT       DGN(NN):1     AVDG
     C                   END
     C                   END
      * Dato skannet inn = Avg.dato
     c                   z-add     TEIDAT        AvgDat            8 0
      * men ved levering etter 17.00.00 legg til en,,
     c     TEITIM        ifgt      170000
     c                   add       1             AvgDat
     c                   end
      * men ved levering etter 12.00.00 legg til en,,
     c     AvgDat        setll     Kalender1
     c                   read      Kalender1                              33
     c     *in33         doweq     '0'
     c     KAUDG         scan      AvDG                                   33
     c     *IN33         cabeq     *On           AvgFunnet
     c                   read      Kalender1                              33
     c                   enddo
     c     AvgFunnet     tag
      * Avg.dato som hensyntar inn-klokkeslett/helligdager/avg.dag (ukedag) er funnet
     c                   read      Kalender1                              33
     c     *in33         doweq     '0'
     c                   add       1             AntDgr
     c     KADATE        cabge     PodDAT        PodFunnet
     c                   read      Kalender1                              33
     c                   enddo
     c     PodFunnet     tag
      *
     c*                  eval      KvalTxt = 'OK'
     c                   eval      KvalTxt = '1'
     c     AntDgr        ifgt      NOFRFT
     c*                  eval      KvalTxt = 'FEIL'
     c                   eval      KvalTxt = '0'
     C                   end
      *
     C                   endsr
      *
      ***************************************************************************
     C     GetDates      begsr
      ***************************************************************************
     c                   move      *zeros        TEIDAT            8 0
     c                   move      *zeros        OMXDAT            8 0
     c                   move      *zeros        TEUDAT            8 0
     c                   move      *zeros        VI1DAT            8 0
     c                   move      *zeros        VI2DAT            8 0
     c                   move      *zeros        PODDAT            8 0
     c                   move      *zeros        TEITIM            6 0
     c                   move      *zeros        OMXTIM            6 0
     c                   move      *zeros        TEUTIM            6 0
     c                   move      *zeros        VI1TIM            6 0
     c                   move      *zeros        VI2TIM            6 0
     c                   move      *zeros        PODTIM            6 0
      *
      * TEI
     C                   move      'TEI'         TTACTI
     c     TrackKey      Chain     TrackL02                           33
     c     *in33         ifeq      *off
     c                   move      TTDATE        TEIDAT
     c                   move      TTTIME        TEITIM
     c                   end
      *
      * OMX
      *    ...
      *
      * TEU
     C                   move      'TEU'         TTACTI
     c     TrackKey      Chain     TrackL02                           33
     c     *in33         ifeq      *off
     c                   move      TTDATE        TEUDAT
     c                   move      TTTIME        TEUTIM
     c                   end
      *
      * VIA1
     C                   move      'TE2'         TTACTI
     c     TrackKey      Chain     TrackL02                           33
     c     *in33         ifeq      *off
     c                   move      TTDATE        VI1DAT
     c                   move      TTTIME        VI1TIM
      * VIA2
     c     TrackKey      READE     TrackL02                               33
     c     *in33         ifeq      *off
     c                   move      TTDATE        VI2DAT
     c                   move      TTTIME        VI2TIM
     c                   end
     c                   end
      *
      *
      * POD
     C                   move      'POD'         TTACTI
     c     TrackKey      Chain     TrackL02                           33
     c     *in33         ifeq      *off
     c                   move      TTDATE        PODDAT
     c                   move      TTTIME        PODTIM
     c                   end
      *
     c                   endsr
      *
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
     C                   OPEN      BRIDF
     C     USER          CHAIN     BRIDF                              50
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
      *
      * hent Parametre som går igjen i mange prog:
      *      Odd/Even/Rowhead-farger,Logobilde,Språk,Intern/Ekstern bruker,  mm
     c                   call      'ESGETPAR'
     c                   parm                    BIBRID
     c                   parm                    BIFIRM
     c                   parm                    BIKUNR
     c                   parm                    BIKNG
     c                   parm                    RowHead          10
     c                   parm                    Odd              10
     c                   parm                    Even             10
     c                   parm                    LogoImg          50
     c                   parm                    XSPRÅK            2
     c                   parm                    XINTERN           1
     c                   parm                    ParmDS1        1024
     c                   parm                    ParmDS2        1024
     c                   parm                    ParmDS3        1024
      *
     C                   OPEN      Cundl01
     C                   OPEN      head38
     C                   OPEN      head22
     C                   OPEN      headDT
     C                   OPEN      turer14
     C                   OPEN      trackl02
     C                   OPEN      NOPNR
     C                   OPEN      kalender1
      *
     C     InnlKey       KLIST
     C                   KFLD                    ZZTERM            4 0
     C                   KFLD                    NOPONR
     C     TrackKey      KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C                   KFLD                    TTFBNR
     C                   KFLD                    TTACTI
     C     Hea38Key      KLIST
     C                   KFLD                    BIKUNR
     C                   KFLD                    FraDt8
     C     Hea22Key      KLIST
     C                   KFLD                    AVDN
     C                   KFLD                    FraDt8
     C                   if        (XINTERN = 'J')
     c     AVVKNN        ifne      *zeros
     C                   MOVE      AVVKNN        BIKUNR
     c                   else
     C                   MOVE      *Zeros        BIKUNR
     c                   end
     c                   end
     C     KunKey        KLIST
     C                   KFLD                    BIFIRM
     C                   KFLD                    BIKUNR
      *
     C     KunKey        CHAIN     Cundl01                            33
      *
     C                   endsr
      *
