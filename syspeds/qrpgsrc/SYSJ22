      *
      ***************************************************************
      *** PROGRAM SKAL HENTE NØKKEL FOR SPØRRING PÅ BEKREFTELSE   ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER        ***
      *** LEDIGE INDIKATORER 01 02 05-26                          ***
      *** LEDIGE INDIKATORER 27-29 31-94                          ***
      ***************************************************************
      *
     FKODTA     IF   E           K DISK
     FCDBOAT    IF   E           K DISK
     FSYSJ22FM  CF   E             WORKSTN
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D                 DS
     D  WSDTBD                 1      2  0
     D  WSDTBM                 3      4  0
     D  WSDTBÅ                 5      6  0
     D  XDTBMÅ                 3      6  0
     D  WSDTB                  1      6  0
     D                 DS
     D  WSDTRD                 1      2  0
     D  WSDTRM                 3      4  0
     D  WSDTRÅ                 5      6  0
     D  XDTRMÅ                 3      6  0
     D  WSDTR                  1      6  0
     D                UDS
     D  UFIRM                  1      2
     D  UAVD                  15     18  0
     D  USG                   19     21
     C*
     C                   EXSR      INIT
     C                   Z-ADD     10            LINNBR
     C                   Z-ADD     19            POSNBR
     C*
     C     START         TAG
     C                   EXSR      KDTFMT
     C                   EXFMT     BILDE
     C                   EXSR      KDTFMT
     C   03              GOTO      SLUTT
     C                   MOVEA     '00000'       *IN(95)
     C                   SETOFF                                       30
     C*
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C                   Z-ADD     WSSUBN        XRECNO            4 0
     C*
     C   04              DO
     C                   EXSR      SØKDTA
     C                   GOTO      START
     C                   END
     C*
     C                   EXSR      TESTB
     C   30              GOTO      START
     C*
     C                   Z-ADD     0             UFLAGG
     C                   MOVE      XDTBMÅ        UDTB              6 0
     C                   MOVEL     WSDTBÅ        UDTB
     C                   MOVE      WSDTBD        UDTB
     C                   MOVE      XDTRMÅ        UDTR              6 0
     C                   MOVEL     WSDTRÅ        UDTR
     C                   MOVE      WSDTRD        UDTR
     C                   CALL      'SYSJ22A'
     C                   PARM                    WSAVD
     C                   PARM                    WSOPD
     C                   PARM                    WSOB
     C                   PARM                    UDTB
     C                   PARM                    WSREIS
     C                   PARM                    UDTR
     C                   PARM                    WSBLNR
     C                   PARM                    UFLAGG
     C     UFLAGG        IFEQ      1
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     19            POSNBR
     C                   MOVE      *ZEROS        WSAVD
     C                   GOTO      START
     C                   END
     C     UFLAGG        IFNE      3
     C                   GOTO      START
     C                   END
     C*
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
     C*
     C***********************************************
     C     TESTB         BEGSR
     C***********************************************
     C     KODTAK        CHAIN     KODTA                              99
     C   99              DO
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     19            POSNBR
     C                   MOVE      'SPA0001'     MSGNR
     C                   SETON                                        30
     C                   END
     C*
     C     WSOPD         IFEQ      0
     C     WSOB          ANDEQ     *BLANKS
     C     WSREIS        ANDEQ     *BLANKS
     C     WSBLNR        ANDEQ     *BLANKS
     C  N30              DO
     C                   Z-ADD     10            LINNBR
     C                   Z-ADD     19            POSNBR
     C                   MOVE      'SPV0102'     MSGNR
     C                   END
     C                   SETON                                        9830
     C                   GOTO      SLTB
     C                   END
     C*
     C     WSOPD         IFNE      0
     C                   MOVE      *BLANKS       WSOB
     C                   MOVE      *ZEROS        WSDTB
     C                   MOVE      *BLANKS       WSREIS
     C                   MOVE      *ZEROS        WSDTR
     C                   MOVE      *BLANKS       WSBLNR
     C                   GOTO      SLTB
     C                   END
     C*
     C     WSOB          IFEQ      *BLANKS                                      1<-B
     C                   MOVE      *ZEROS        WSDTB
     C                   ELSE                                                   1
     C     WSOB          CHAIN     CDBOAT                             97
     C   97
     CANN30              DO                                                      2<-B
     C                   Z-ADD     12            LINNBR
     C                   Z-ADD     19            POSNBR
     C                   MOVE      'SPB0104'     MSGNR
     C                   SETON                                        30
     C                   END                                                     2<-E
     C     WSDTB         IFNE      0                                             2<-B
     C                   MOVE      WSDTBÅ        UÅÅ               2 0
     C                   MOVE      WSDTBM        UMM               2 0
     C                   MOVE      WSDTBD        UDD               2 0
     C                   CALL      'TESTDATO'
     C                   PARM                    UDD
     C                   PARM                    UMM
     C                   PARM                    UÅÅ
     C                   PARM                    UFLAGG            1 0
     C     UFLAGG        COMP      1                                      96
     C   96
     CANN30              DO                                                       3<-B
     C                   Z-ADD     12            LINNBR
     C                   Z-ADD     38            POSNBR
     C                   MOVE      'SPD0001'     MSGNR
     C                   SETON                                        30
     C                   END                                                      3<-E
     C                   END                                                     2<-E
     C  N30              DO                                                      2<-B
     C                   MOVE      *BLANKS       WSREIS
     C                   MOVE      *ZEROS        WSDTR
     C                   MOVE      *BLANKS       WSBLNR
     C                   END                                                     2<-E
     C                   END                                                    1<-E
     C*
     C     WSREIS        IFEQ      *BLANKS                                      1<-B
     C                   MOVE      *ZEROS        WSDTR
     C                   ELSE                                                   1
     C     WSDTR         IFNE      0                                             2<-B
     C                   MOVE      WSDTRÅ        UÅÅ               2 0
     C                   MOVE      WSDTRM        UMM               2 0
     C                   MOVE      WSDTRD        UDD               2 0
     C                   CALL      'TESTDATO'
     C                   PARM                    UDD
     C                   PARM                    UMM
     C                   PARM                    UÅÅ
     C                   PARM                    UFLAGG            1 0
     C     UFLAGG        COMP      1                                      95
     C   95
     CANN30              DO                                                       3<-B
     C                   Z-ADD     14            LINNBR
     C                   Z-ADD     38            POSNBR
     C                   MOVE      'SPD0001'     MSGNR
     C                   SETON                                        30
     C                   END                                                      3<-E
     C                   END                                                     2<-E
     C  N30              MOVE      *BLANKS       WSBLNR
     C                   END                                                    1<-E
     C*
     C     SLTB          ENDSR
     C*
     C***********************************************
     C     SØKDTA        BEGSR
     C***********************************************
     C     LINNBR        IFEQ      10
     C     POSNBR        ANDGE     19
     C     POSNBR        ANDLE     23
     C                   Z-ADD     0             UFLAGG
     C                   CALL      'SYSJ52H'
     C                   PARM                    UAVD2             4 0
     C                   PARM                    UOPD2             7 0
     C                   PARM                    UFLAGG
     C                   GOTO      SLSØK
     C                   END
     C*
     C     LINNBR        IFEQ      12
     C     POSNBR        ANDGE     19
     C     POSNBR        ANDLE     23
     C                   CALL      'SYSJ11A'
     C                   PARM                    WSOB
     C                   GOTO      SLSØK
     C                   END
     C*
     C                   MOVE      'DIV1037'     MSGNR
     C                   SETON                                        30
     C*
     C     SLSØK         ENDSR
     C*
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     KODTAK        KLIST
     C                   KFLD                    YAUNI
     C                   KFLD                    WSAVD
     C*
     C                   MOVE      'A'           YAUNI             1
     C                   MOVE      UAVD          WSAVD
     C*
     C                   MOVE      *ZEROS        WSDTB
     C                   MOVE      *ZEROS        WSDTR
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     KDTFMT        BEGSR
     C***********************************************
     C                   MOVE      WSDTRD        UDT1              2 0
     C                   MOVE      WSDTRM        UDT2              2 0
     C                   MOVE      WSDTRÅ        UDT3              2 0
     C                   CALL      'DATOFMT'
     C                   PARM                    UDT1
     C                   PARM                    UDT2
     C                   PARM                    UDT3
     C                   MOVE      UDT1          WSDTRD
     C                   MOVE      UDT2          WSDTRM
     C                   MOVE      UDT3          WSDTRÅ
     C*
     C                   MOVE      WSDTBD        UDT1
     C                   MOVE      WSDTBM        UDT2
     C                   MOVE      WSDTBÅ        UDT3
     C                   CALL      'DATOFMT'
     C                   PARM                    UDT1
     C                   PARM                    UDT2
     C                   PARM                    UDT3
     C                   MOVE      UDT1          WSDTBD
     C                   MOVE      UDT2          WSDTBM
     C                   MOVE      UDT3          WSDTBÅ
     C*
     C                   ENDSR
     C*
