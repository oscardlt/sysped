     A*%%TS  SD  20160615  094834  SVEIN       REL-V7R1M0  5770-WDS
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3                  -
     A                                             27 132 *DS4)
     A                                      PRINT
     A                                      HLPPNLGRP(HLPSCR SYEX10PNL)
     A                                      HLPTITLE('OPPDRAGSREGISTRERING LAND-
     A                                       EKSPORT')
     A                                      HELP
     A                                      ALTHELP
     A          R BILDE01
     A                                      CA12(12 'Avslutt program')
     A                                      CF05(05 'Gå direkte til faktura')
     A                                      CF07(07 'SPØRRING OPPDRAG')
     A                                      CF09(09 'Bytt signatur')
     A                                  1  3'SYSPED/SYEX10'
     A                                      COLOR(BLU)
     A                                  1 21MSGCON(045 DIV0596 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 67DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(GRN)
     A                                  2 21MSGCON(045 DIV0615 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  2 67TIME
     A                                      COLOR(GRN)
     A                                  3 21MSGCON(045 DIV0614 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  9  3MSGCON(018 DIV0599 *LIBL/SYMSGF)
     A            WSSG           3A  B  9 24TEXT('Signatur')
     A                                      DSPATR(UL)
     A  99                                  DSPATR(PC)
     A  99                                  DSPATR(RI)
     A                                 11  3MSGCON(018 DIV0600 *LIBL/SYMSGF)
     A            WSAVD          4Y 0B 11 24TEXT('Avdeling')
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
     A  98                                  DSPATR(PC)
     A  98                                  DSPATR(RI)
     A                                 13  3MSGCON(018 DIV0601 *LIBL/SYMSGF)
     A            WSOPD          7Y 0B 13 24TEXT('Oppdragsnummer')
     A                                      DSPATR(UL)
     A N96                                  DSPATR(PC)
     A  97                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                 13 39MSGCON(020 DIV0604 *LIBL/SYMSGF)
     A                                 15  3MSGCON(018 DIV0602 *LIBL/SYMSGF)
     A            HEAVD          4Y 0B 15 24TEXT('Avdeling')
     A                                      DSPATR(UL)
     A  96                                  DSPATR(PC)
     A  96                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A            HEOPD          7Y 0B 15 31TEXT('Oppdragsnummer')
     A                                      DSPATR(UL)
     A  96                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A                                 15 39MSGCON(020 DIV0036 *LIBL/SYMSGF)
     A                                 18  3MSGCON(018 DIV0603 *LIBL/SYMSGF)
     A            WSOPDG         7Y 0O 18 24COLOR(WHT)
     A                                      EDTCDE(4)
     A            WSMSG         62A  O 20  3COLOR(RED)
     A                                      DSPATR(BL)
     A                                 23  2MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(018 DIV0605 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 42MSGCON(018 DIV0606 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 62MSGCON(018 DIV0033 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A          R BILDE02
     A*%%TS  SD  20160615  094834  SVEIN       REL-V7R1M0  5770-WDS
     A N31                                  CF03(03 'Avslutt registrering av op-
     A                                      pdrag')
     A                                      CF04(04 'SØKING')
     A N31                                  CF05(05 'Gå til faktura')
     A                                      CF06(06 'BEREGN KUBIKK')
     A N31                                  CF16(16 'Neste')
     A                                      CF07(07 'SPØRRING PÅ OPPDRAG')
     A N31                                  CF09(09 'Ny tur')
     A                                      CF11(11 'DUP-OPPDRAG')
     A                                      CA12(12 'Avbryt registrering av opp-
     A                                      drag')
     A                                      CF14(14 'Vise opplysning')
     A                                      CF24(24 'FLERE TASTER')
     A                                      CF18(18 'Freetext')
     A                                      CF15(15 'FrI SØK')
     A                                      CF08(08 'Til SAD-eksport')
     A                                      CF17(17 'til ARKIVET')
     A                                      CF22(22 'Hurtigbooking')
     A  27                                  ALARM
     A                                      CSRLOC(LINNBR     POSNBR)
     A          H                           HLPPNLGRP(SYEX10010 SYEX10PNL)
     A  *DS3                                HLPARA(01 001 01 080)
     A  *DS4                                HLPARA(01 001 01 080)
     A          H                           HLPPNLGRP(SYEX10010 SYEX10PNL)
     A  *DS3                                HLPARA(02 001 02 041)
     A  *DS4                                HLPARA(02 001 02 041)
     A          H                           HLPPNLGRP(SYEX10020 SYEX10PNL)
     A  *DS3                                HLPARA(02 061 02 080)
     A  *DS4                                HLPARA(02 061 02 080)
     A          H                           HLPPNLGRP(SYEX10010 SYEX10PNL)
     A  *DS3                                HLPARA(03 001 03 016)
     A  *DS4                                HLPARA(03 001 03 016)
     A          H                           HLPPNLGRP(SYEX10015 SYEX10PNL)
     A  *DS3                                HLPARA(03 017 03 049)
     A  *DS4                                HLPARA(03 017 03 049)
     A          H                           HLPPNLGRP(SYEX10020 SYEX10PNL)
     A  *DS3                                HLPARA(03 050 03 080)
     A  *DS4                                HLPARA(03 050 03 080)
     A          H                           HLPPNLGRP(SYEX10025 SYEX10PNL)
     A  *DS3                                HLPARA(04 001 04 030)
     A  *DS4                                HLPARA(04 001 04 030)
     A          H                           HLPPNLGRP(SYEX10030 SYEX10PNL)
     A  *DS3                                HLPARA(04 031 04 080)
     A  *DS4                                HLPARA(04 031 04 080)
     A          H                           HLPPNLGRP(SYEX10035 SYEX10PNL)
     A  *DS3                                HLPARA(05 001 06 080)
     A  *DS4                                HLPARA(05 001 06 080)
     A          H                           HLPPNLGRP(SYEX10040 SYEX10PNL)
     A  *DS3                                HLPARA(07 001 07 023)
     A  *DS4                                HLPARA(07 001 07 023)
     A          H                           HLPPNLGRP(SYEX10045 SYEX10PNL)
     A  *DS3                                HLPARA(07 024 07 036)
     A  *DS4                                HLPARA(07 024 07 036)
     A          H                           HLPPNLGRP(SYEX10050 SYEX10PNL)
     A  *DS3                                HLPARA(07 037 07 080)
     A  *DS4                                HLPARA(07 037 07 080)
     A          H                           HLPPNLGRP(SYEX10055 SYEX10PNL)
     A  *DS3                                HLPARA(08 001 09 080)
     A  *DS4                                HLPARA(08 001 09 080)
     A          H                           HLPPNLGRP(SYEX10060 SYEX10PNL)
     A  *DS3                                HLPARA(10 001 10 080)
     A  *DS4                                HLPARA(10 001 10 080)
     A          H                           HLPPNLGRP(SYEX10065 SYEX10PNL)
     A  *DS3                                HLPARA(11 001 13 080)
     A  *DS4                                HLPARA(11 001 13 080)
     A          H                           HLPPNLGRP(SYEX10070 SYEX10PNL)
     A  *DS3                                HLPARA(14 001 14 080)
     A  *DS4                                HLPARA(14 001 14 080)
     A          H                           HLPPNLGRP(SYEX10075 SYEX10PNL)
     A  *DS3                                HLPARA(15 001 15 024)
     A  *DS4                                HLPARA(15 001 15 024)
     A          H                           HLPPNLGRP(SYEX10075 SYEX10PNL)
     A  *DS3                                HLPARA(16 001 16 024)
     A  *DS4                                HLPARA(16 001 16 024)
     A          H                           HLPPNLGRP(SYEX10075 SYEX10PNL)
     A  *DS3                                HLPARA(17 001 17 024)
     A  *DS4                                HLPARA(17 001 17 024)
     A          H                           HLPPNLGRP(SYEX10080 SYEX10PNL)
     A  *DS3                                HLPARA(15 025 15 058)
     A  *DS4                                HLPARA(15 025 15 058)
     A          H                           HLPPNLGRP(SYEX10080 SYEX10PNL)
     A  *DS3                                HLPARA(16 025 16 058)
     A  *DS4                                HLPARA(16 025 16 058)
     A          H                           HLPPNLGRP(SYEX10080 SYEX10PNL)
     A  *DS3                                HLPARA(17 025 17 058)
     A  *DS4                                HLPARA(17 025 17 058)
     A          H                           HLPPNLGRP(SYEX10085 SYEX10PNL)
     A  *DS3                                HLPARA(15 059 15 080)
     A  *DS4                                HLPARA(15 059 15 080)
     A          H                           HLPPNLGRP(SYEX10085 SYEX10PNL)
     A  *DS3                                HLPARA(16 059 16 080)
     A  *DS4                                HLPARA(16 059 16 080)
     A          H                           HLPPNLGRP(SYEX10085 SYEX10PNL)
     A  *DS3                                HLPARA(17 059 17 080)
     A  *DS4                                HLPARA(17 059 17 080)
     A          H                           HLPPNLGRP(SYEX10090 SYEX10PNL)
     A  *DS3                                HLPARA(18 001 18 070)
     A  *DS4                                HLPARA(18 001 18 070)
     A          H                           HLPPNLGRP(SYEX10095 SYEX10PNL)
     A  *DS3                                HLPARA(18 071 18 080)
     A  *DS4                                HLPARA(18 071 18 080)
     A          H                           HLPPNLGRP(SYEX10100 SYEX10PNL)
     A  *DS3                                HLPARA(19 001 19 080)
     A  *DS4                                HLPARA(19 001 19 080)
     A          H                           HLPPNLGRP(SYEX10105 SYEX10PNL)
     A  *DS3                                HLPARA(20 001 21 080)
     A  *DS4                                HLPARA(20 001 21 080)
     A          H                           HLPPNLGRP(SYEX10110 SYEX10PNL)
     A  *DS3                                HLPARA(22 001 22 016)
     A  *DS4                                HLPARA(22 001 22 016)
     A          H                           HLPPNLGRP(SYEX10111 SYEX10PNL)
     A  *DS3                                HLPARA(22 018 22 080)
     A  *DS4                                HLPARA(22 018 22 080)
     A          H                           HLPPNLGRP(SYEX10999 SYEX10PNL)
     A  *DS3                                HLPARA(23 001 24 080)
     A  *DS4                                HLPARA(23 001 24 080)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGLN1         7A  P
     A            MSGLN2         7A  P
     A            MSGLN3         7A  P
     A            MSGLN4         7A  P
     A            MSGNR          7A  P
     A                                  1  2'SYSPED/SYEX10'
     A                                      COLOR(BLU)
     A                                  1 19MSGCON(040 DIV0726 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                  1 69TIME
     A                                  2  2MSGCON(004 DIV0616 *LIBL/SYMSGF)
     A            WSAVD          4Y 0B  2  8TEXT('Avdeling')
     A N58                                  DSPATR(PR)
     A N58                                  COLOR(WHT)
     A  58                                  COLOR(YLW)
     A                                      EDTCDE(4)
     A  58                              2 13MSGCON(005 YBC0035 *LIBL/SYMSGF)
     A                                      DSPATR(RI)
     A                                      COLOR(YLW)
     A N58        WSOPD          7Y 0O  2 13TEXT('Oppdragsnummer')
     A N29                                  DSPATR(BL)
     A                                      EDTCDE(4)
     A N29                                  COLOR(RED)
     A  29                                  COLOR(WHT)
     A                                  2 22MSGCON(003 DIV0072 *LIBL/SYMSGF)
     A            WSSG           3A  B  2 26TEXT('Signatur')
     A                                      COLOR(YLW)
     A                                  2 33MSGCON(004 DIV0073 *LIBL/SYMSGF)
     A            WSSTD          4Y 0B  2 38TEXT('Kostnadssted')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  2 61MSGCON(004 DIV0588 *LIBL/SYMSGF)
     A            WSRFA         15A  B  2 66TEXT('Referanse Agent')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  3  2MSGCON(005 DIV0074 *LIBL/SYMSGF)
     A            WSPRO          8Y 0B  3  8TEXT('Prosjektnummer')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                      CHECK(RB)
     A                                  3 17MSGCON(002 DIV0076 *LIBL/SYMSGF)
     A            WSDT           6Y 0B  3 20DSPATR(UL)
     A  96                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  3 27MSGCON(005 DIV0618 *LIBL/SYMSGF)
     A            WSTLE          6A  B  3 33DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  3 41MSGCON(006 DIV0619 *LIBL/SYMSGF)
     A            WSTLL         10A  B  3 48DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  3 61MSGCON(008 DIV0620 *LIBL/SYMSGF)
     A            WSTLKU         1A  B  3 70DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  4  2MSGCON(004 DIV0589 *LIBL/SYMSGF)
     A            WSTRI          2A  B  4  8TEXT('FRA LAND')
     A                                      DSPATR(UL)
     A  53                                  DSPATR(PR)
     A  53                                  COLOR(WHT)
     A N53                                  COLOR(YLW)
     A            WSSDF          5A  B  4 11TEXT('Frakt fra')
     A                                      DSPATR(UL)
     A  95                                  DSPATR(RI)
     A  53                                  DSPATR(PR)
     A  53                                  COLOR(WHT)
     A N53                                  COLOR(YLW)
     A                                  4 17'-'
     A            WSLKA          2A  B  4 19TEXT('Landkode')
     A                                      DSPATR(UL)
     A  93                                  DSPATR(RI)
     A  53                                  DSPATR(PR)
     A  53                                  COLOR(WHT)
     A N53                                  COLOR(YLW)
     A            WSSDT          5A  B  4 22TEXT('Frakt til')
     A                                      DSPATR(UL)
     A  94                                  DSPATR(RI)
     A  53                                  DSPATR(PR)
     A  53                                  COLOR(WHT)
     A N53                                  COLOR(YLW)
     A                                  4 31MSGCON(008 DIV0617 *LIBL/SYMSGF)
     A            WSVALT         3A  B  4 40TEXT('V.kode tillegg')
     A                                      DSPATR(UL)
     A  76                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSVALP         3Y 1B  4 44DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(M)
     A                                      CHECK(RB)
     A                                  4 50MSGCON(005 DIV0085 *LIBL/SYMSGF)
     A            WSKNT          8Y 0B  4 56TEXT('Kundenr Transportør')
     A                                      DSPATR(UL)
     A  69                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSTRNV        15A  O  4 65COLOR(WHT)
     A                                  5  2MSGCON(005 DIV0084 *LIBL/SYMSGF)
     A            WSKNA          8Y 0B  5  8TEXT('Kundenr Agent')
     A                                      DSPATR(UL)
     A  70                                  DSPATR(RI)
     A                                      EDTCDE(4)
     A  56                                  DSPATR(BL)
     A  56                                  COLOR(RED)
     A N56                                  COLOR(YLW)
     A            WSNAA         30A  O  5 17TEXT('Navn Agent')
     A  56                                  DSPATR(BL)
     A  56                                  COLOR(RED)
     A N56                                  COLOR(WHT)
     A                                  5 50MSGCON(005 SHU1477 *LIBL/SYMSGF)
     A            WSHAWB         9A  B  5 56DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  6  2MSGCON(003 DIV0786 *LIBL/SYMSGF)
     A            WSSDL         20A  B  6  8TEXT('Lossested')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A                                  6 50MSGCON(007 YBC0203 *LIBL/SYMSGF)
     A            W2KDME         1A  B  6 58TEXT('Merkelapp  /X')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'F' 'X' 'L' 'A' 'B')
     A                                  6 60MSGCON(007 YBC0204 *LIBL/SYMSGF)
     A            W2KDE          1A  B  6 68TEXT('Etterkrav  /X')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'X')
     A                                  7  2MSGCON(004 DIV0090 *LIBL/SYMSGF)
     A            WSKNS          8Y 0B  7  8TEXT('Kundenr Selger')
     A                                      DSPATR(UL)
     A  67
     AO 68                                  DSPATR(RI)
     A  34                                  DSPATR(PR)
     A                                      EDTCDE(4)
     A  55                                  DSPATR(BL)
     A  55                                  COLOR(RED)
     A N55                                  COLOR(YLW)
     A                                  7 18MSGCON(003 DIV0091 *LIBL/SYMSGF)
     A            WSANS          1Y 0B  7 22TEXT('Avtalenr Selger')
     A                                      DSPATR(UL)
     A  34                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  7 24MSGCON(002 DIV0092 *LIBL/SYMSGF)
     A            WSKDFS         1A  B  7 27TEXT('Fakturakode Selger')
     A                                      DSPATR(UL)
     A  34                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'X' 'B' 'E')
     A                                  7 29MSGCON(003 DIV0093 *LIBL/SYMSGF)
     A            WSVALS         3A  B  7 33TEXT('Valutakode Selger')
     A                                      DSPATR(UL)
     A  66                                  DSPATR(RI)
     A  34                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                  7 37MSGCON(004 DIV0094 *LIBL/SYMSGF)
     A            WSKNSF         8Y 0B  7 42TEXT('Kundenr Selg.fakm')
     A                                      DSPATR(UL)
     A  65                                  DSPATR(RI)
     A  34                                  DSPATR(PR)
     A                                      EDTCDE(4)
     A  99                                  DSPATR(BL)
     A  99                                  COLOR(RED)
     A N99                                  COLOR(YLW)
     A            WSNASF        30A  O  7 51TEXT('Navn Selg.fakm')
     A                                      DSPATR(UL)
     A  99                                  DSPATR(BL)
     A  99                                  COLOR(RED)
     A N99                                  COLOR(WHT)
     A                                  8  2MSGCON(004 DIV0095 *LIBL/SYMSGF)
     A            WSNAS         30A  B  8  8TEXT('Navn Selger')
     A                                      DSPATR(UL)
     A  67                                  DSPATR(RI)
     A  34                                  DSPATR(PR)
     A  55                                  DSPATR(BL)
     A  55                                  COLOR(RED)
     A N55                                  COLOR(YLW)
     A            WSADS1        30A  B  8 42TEXT('Adresse-1 Selger')
     A                                      DSPATR(UL)
     A  34                                  DSPATR(PR)
     A  99                                  DSPATR(BL)
     A  99                                  COLOR(RED)
     A N99                                  COLOR(YLW)
     A            WSADS2        30A  B  9  8TEXT('Adresse-3 Selger')
     A                                      DSPATR(UL)
     A  34                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A            WSADS3        30A  B  9 42TEXT('Adresse-2 Selger')
     A                                      DSPATR(UL)
     A  34                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                 10  2MSGCON(012 DIV0592 *LIBL/SYMSGF)
     A            WSLKS          2A  B 10 19COLOR(YLW)
     A            WSSDFF         5A  B 10 22TEXT('Forfrakt fra')
     A                                      DSPATR(UL)
     A  84                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSPNFR        30A  B 10 29COLOR(YLW)
     A                                 11  2MSGCON(004 DIV0096 *LIBL/SYMSGF)
     A            WSKNK          8Y 0B 11  8TEXT('Kundenr Kjøper')
     A                                      DSPATR(UL)
     A  62
     AO 61                                  DSPATR(RI)
     A  32                                  DSPATR(PR)
     A                                      EDTCDE(4)
     A  54                                  DSPATR(BL)
     A  54                                  COLOR(RED)
     A N54                                  COLOR(YLW)
     A                                 11 18MSGCON(003 DIV0091 *LIBL/SYMSGF)
     A            WSANK          1Y 0B 11 22TEXT('Avtalenr Kjøper')
     A                                      DSPATR(UL)
     A  32                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 11 24MSGCON(002 DIV0092 *LIBL/SYMSGF)
     A            WSKDFK         1A  B 11 27TEXT('Fakturakode Kjøper')
     A                                      DSPATR(UL)
     A  32                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'X' 'B' 'E')
     A                                 11 29MSGCON(003 DIV0093 *LIBL/SYMSGF)
     A            WSVALK         3A  B 11 33TEXT('Valutakode Kjøper')
     A                                      DSPATR(UL)
     A  60                                  DSPATR(RI)
     A  32                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                 11 37MSGCON(004 DIV0094 *LIBL/SYMSGF)
     A            WSKNKF         8Y 0B 11 42TEXT('Kundenr Kjøp.fakm')
     A                                      DSPATR(UL)
     A  59                                  DSPATR(RI)
     A  32                                  DSPATR(PR)
     A                                      EDTCDE(4)
     A  98                                  DSPATR(BL)
     A  98                                  COLOR(RED)
     A N98                                  COLOR(YLW)
     A            WSNAKF        30A  O 11 51TEXT('Navn Kjøp.fakm')
     A                                      DSPATR(UL)
     A  98                                  DSPATR(BL)
     A  98                                  COLOR(RED)
     A N98                                  COLOR(WHT)
     A                                 12  2MSGCON(004 DIV0097 *LIBL/SYMSGF)
     A            WSNAK         30A  B 12  8TEXT('Navn Kjøper')
     A                                      DSPATR(UL)
     A  62                                  DSPATR(RI)
     A  32                                  DSPATR(PR)
     A  54                                  DSPATR(BL)
     A  54                                  COLOR(RED)
     A N54                                  COLOR(YLW)
     A            WSADK1        30A  B 12 42TEXT('Adresse-1 Kjøper')
     A                                      DSPATR(UL)
     A  32                                  DSPATR(PR)
     A  98                                  DSPATR(BL)
     A  98                                  COLOR(RED)
     A N98                                  COLOR(YLW)
     A            WSADK2        30A  B 13  8TEXT('Adresse-2 Kjøper')
     A                                      DSPATR(UL)
     A  32                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A            WSADK3        30A  B 13 42TEXT('Adresse-3 Kjøper')
     A                                      DSPATR(UL)
     A  32                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                 14  2MSGCON(015 DIV0593 *LIBL/SYMSGF)
     A            WSLKK          2A  B 14 19COLOR(YLW)
     A            WSSDVT         5A  B 14 22TEXT('Viderefrakt til')
     A                                      DSPATR(UL)
     A  83                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSPNTI        30A  B 14 29COLOR(YLW)
     A                                 15  2MSGCON(077 DIV0591 *LIBL/SYMSGF)
     A            WSOT           2A  B 16  2TEXT('Oppdragstype')
     A                                      DSPATR(UL)
     A  89                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSFR           3A  B 16  5TEXT('Frankatur')
     A                                      DSPATR(UL)
     A  88                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSGM1         15A  B 16  9TEXT('Godsmerking-1')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSNT           7Y 0B 16 25TEXT('Antall')
     A                                      DSPATR(UL)
     A  87                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                      CHECK(RB)
     A            WSVS1         25A  B 16 33TEXT('Vareslag-1')
     A                                      DSPATR(UL)
     A  86                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSVKT          9Y 0B 16 59TEXT('Vekt')
     A                                      DSPATR(UL)
     A  85                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSM3           7Y 3B 16 72TEXT('Kubikk')
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                      DSPATR(UL)
     A            WSGM2         15A  B 17  9TEXT('Godsmerking-2')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSVS2         25A  B 17 33TEXT('Vareslag-2')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSLM           4Y 2B 17 66TEXT('Lastemeter')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 18  2MSGCON(006 SHU0150 *LIBL/SYMSGF)
     A            WSKF           2A  B 18  9DSPATR(UL)
     A  74                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSKFT1         1A  B 18 12DSPATR(UL)
     A  75                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSFT1A        30A  B 18 15DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSFT1B        15A  B 18 46DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSUSR         10A  O 18 62COLOR(PNK)
     A  53                             18 73MSGCON(006 YBC0489 *LIBL/SYMSGF)
     A            WSKDPL         1A  B 18 80TEXT('Planleggingskode')
     A                                      DSPATR(UL)
     A  77                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 19  2MSGCON(048 YBC0202 *LIBL/SYMSGF)
     A                                 19 51MSGCON(008 DIV5022 *LIBL/SYMSGF)
     A            WSVERV         3A  B 19 60COLOR(YLW)
     A  71                                  DSPATR(RI)
     A            WSVERB        13Y 2B 19 64COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSPK3          1A  B 20  6TEXT('Printerkode-3')
     A                                      DSPATR(UL)
     A  80                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'J' 'N' 'P')
     A            WSPK6          1A  B 20 16TEXT('Printerkode-6')
     A                                      DSPATR(UL)
     A  79                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'J' 'N' 'P')
     A            WSPK7          1A  B 20 28TEXT('Printerkode-7')
     A                                      DSPATR(UL)
     A  81                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'B' 'C' 'N' 'P')
     A            WSPK1          1A  B 20 42TEXT('Printerkode-1')
     A                                      DSPATR(UL)
     A  82                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'J' 'N' 'P')
     A                                 20 46'Ship-sys:'
     A            WSTSS          1A  B 20 56DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      VALUES('J' 'N')
     A                                 20 60MSGCON(005 DIV0100 *LIBL/SYMSGF)
     A            WSKDL         15A  B 20 66TEXT('Lagerkode')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A*           WSMSG         62   O 21  2DSPATR(BL)
     A            WSMSG         62   O 21  2
     A                                      COLOR(RED)
     A                                 22  2MSGCON(007 DIV0607 *LIBL/SYMSGF)
     A            WSOPDG         7Y 0O 22 10DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                 22 18MSGCON(006 SHU0325 *LIBL/SYMSGF)
     A            WSFA11         7A  B 22 25COLOR(YLW)
     A                                 22 33'-'
     A            WSFA12         4A  B 22 35COLOR(YLW)
     A                                 22 40MSGCON(004 SHU0331 *LIBL/SYMSGF)
     A            WSFLP1         3Y 0B 22 45COLOR(YLW)
     A                                      EDTCDE(M)
     A                                 22 50MSGCON(006 SHU0332 *LIBL/SYMSGF)
     A            WSFA21         7A  B 22 57COLOR(YLW)
     A            WSFA22         4A  B 22 65COLOR(YLW)
     A                                 22 70MSGCON(004 SHU0331 *LIBL/SYMSGF)
     A            WSFLP2         3Y 0B 22 75COLOR(YLW)
     A                                      EDTCDE(M)
     A            MSGL1         18A  O 23  2MSGID(&MSGLN1 SYMSGF)
     A                                      COLOR(BLU)
     A            MSGL2         18A  O 23 22MSGID(&MSGLN2 SYMSGF)
     A                                      COLOR(BLU)
     A            MSGL3         18A  O 23 42MSGID(&MSGLN3 SYMSGF)
     A                                      COLOR(BLU)
     A            MSGL4         18A  O 23 62MSGID(&MSGLN4 SYMSGF)
     A                                      COLOR(BLU)
     A            MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
