      *
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr *Sek Sig Vers Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 YBC PTF10 TEST PÅ ANDREKOSTNAD HVIS LEV.BET. SIER
10XXX * 02 YBC PTF10 TOLLSTEDSKODE ER ENDRET TIL 4.0
10XXX * 03 YBC PTF10 IND. 60 FOR Å VISE FAKTURANR IKKE OPPGITT
10024 * 04 YBC PTF10 SAMSLÅING OG FORHOLD BRT/NET VEKT
10XXX * 05 SH  PTF10 LÅSE DEKLARANT
xxxxxx*xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
11016 * 06 YBC PTF11 OMBEREGNING
11062 * 07 YBC PTF11 TEST PÅ GODKJENNINGSDATO
11102 * 08 YBC PTF11 5. LINJE FOR BEGRUNNELSE AV OMBEREGING
xxxxxx*xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
12XXX * 09 YBC PTF12 R FOR REGENERERING AV VEKT PÅ VARELINJER
12248 * 10 YBC PTF12 VEKT, KOLLI ER UTVIDET.
12XXXA* 11 YBC PTF12 FORHOLD MELLOM BRUTTO OG NETTOVEKT MÅ VÆRE STØRRE ENN 0
12255 * 12 YBC PTF12 Fra midlertidig innførsel til ordinal utførsel
*****A**************************************************************
      *
     A                                      DSPSIZ(24 80 *DS3                  -
     A                                             27 132 *DS4)
     A                                      PRINT
     A                                      HLPPNLGRP(HLPSCR SAD052PNL)
     A                                      HLPTITLE('SAD EKSPORT')
     A                                      HELP
     A                                      ALTHELP
     A*
     A          R BILDE01
     A*%%TS  SD  20151019  082501  SVEIN       REL-V7R1M0  5770-WDS
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF04(04 'HENTE REFERANSE FRA Z-FIL')
     A                                      CF10(10 'Slette ferdigmeldt TVINN')
     A                                      CF12(12 'Forrige')
     A                                      CF17(17 'ARKIV')
     A          H                           HLPPNLGRP(SADE000 SAD052PNL)
     A  *DS3                                HLPARA(01 001 24 080)
     A  *DS4                                HLPARA(01 001 24 080)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  3'SYSPED/SAD052'
     A                                      COLOR(BLU)
     A                                  1 62DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(GRN)
     A                                  1 71TIME
     A                                      COLOR(GRN)
     A                                  1 20'Registrering av tolldeklarasjon (E-
     A                                      KSPORT)'
     A                                      COLOR(WHT)
     A                                  2  3MSGCON(012 DIV1451 *LIBL/SYMSGF)
     A                                      COLOR(TRQ)
     A            FIFIRM         2A  O  2 16TEXT('FIRMAKODE')
     A                                      COLOR(TRQ)
     A                                  2 58MSGCON(022 DIV0047 *LIBL/SYMSGF)
     A                                      COLOR(TRQ)
     A                                  5 18'Avdeling:'
     A            UAVD           4Y 0B  5 34EDTCDE(4)
     A  95                                  DSPATR(PC)
     A  31                                  DSPATR(PR)
     A                                      COLOR(WHT)
     A                                  7 18'Ekstern ref:'
     A            EXTREF        35A  B  7 34DSPATR(PC)
     A                                      COLOR(YLW)
     A                                  9 18'Signatur:'
     A            WSSG           3A  B  9 34DSPATR(UL)
     A  99                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 10 18'Tolldek.nr.'
     A            WSTDN          7Y 0B 10 34DSPATR(UL)
     A  98                                  DSPATR(RI)
     A  32                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                      EDTCDE(L)
S06  A*                                10 42'Tast oppdragsnr.'
     A                                 10 43'Tast oppdragsnr.'
     A                                      COLOR(BLU)
     A  35N32                          11 18'Sjø-oppdrag'
     A  35N32     WSUR           1A  B 11 34DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'G')
     A  35N32                          11 42'G = Sjø-oppdrag'
     A                                      COLOR(BLU)
     A                                 13 18'Sendes TVINN'
     A            WSTOLK         1A  B 13 34VALUES(' ' 'J' 'N')
     A  96                                  DSPATR(PC)
     A                                      COLOR(YLW)
     A                                 13 42'(J/N/blank)'
     A                                      COLOR(BLU)
     A                                      DSPATR(UL)
     A                                 14 18'Skrives nå'
     A            WSTVIN         1A  B 14 34DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'J' 'N')
     A                                 14 42'(J/N/blank - Kun ved J i TVINN)'
     A                                      COLOR(BLU)
     A                                      DSPATR(UL)
     A                                 23  2'F12=Avbryt'
     A                                      COLOR(BLU)
     A                                 23 13'F4=Søk EDI-mottak'
     A                                      COLOR(BLU)
     A N36                             23 31'F7=Søke oppdrag'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A                                 23 48'F17=se arkiv'
     A                                      COLOR(BLU)
      *
     A          R BILDE02
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF04(04 'Søk')
     A N36                                  CF07(07 'Søke oppdrag')
     A                                      CF08(08 'Innstikk')
     A                                      CF10(10 'Slette ferdigmeldt TVINN')
     A                                      CF12(12 'Avbryt program og gå til i-
     A                                      nngangsbilde')
     A                                      CF13(13 'Oppdater reg.nr og konto p-
     A                                      å kunde-register')
     A                                      CF14(14 'KUNDEINFO')
     A                                      CF15(15 'Mer rubr.28')
     A                                      CF24(24 'BLA I F-TASTER')
     A                                      OVERLAY
     A          H                           HLPPNLGRP(SADE001 SAD052PNL)
     A                                      HLPARA(01 041 01 053)
     A          H                           HLPPNLGRP(SADE001A SAD052PNL)
     A                                      HLPARA(01 052 01 053)
     A          H                           HLPPNLGRP(SADE001B SAD052PNL)
     A                                      HLPARA(01 056 01 080)
     A          H                           HLPPNLGRP(SADE002 SAD052PNL)
     A                                      HLPARA(02 001 06 037)
     A          H                           HLPPNLGRP(SADE003 SAD052PNL)
     A                                      HLPARA(02 054 02 068)
     A          H                           HLPPNLGRP(SADE003A SAD052PNL)
     A                                      HLPARA(02 070 02 074)
     A          H                           HLPPNLGRP(SADE003B SAD052PNL)
     A                                      HLPARA(02 076 02 080)
     A          H                           HLPPNLGRP(SADE003C SAD052PNL)
     A                                      HLPARA(03 041 03 061)
     A          H                           HLPPNLGRP(SADE003D SAD052PNL)
     A                                      HLPARA(04 056 04 075)
     A          H                           HLPPNLGRP(SADE004 SAD052PNL)
     A                                      HLPARA(07 001 11 039)
     A          H                           HLPPNLGRP(SADE005 SAD052PNL)
     A                                      HLPARA(05 040 05 080)
     A          H                           HLPPNLGRP(SADE005B SAD052PNL)
     A                                      HLPARA(07 041 07 049)
     A          H                           HLPPNLGRP(SADE005C SAD052PNL)
     A                                      HLPARA(07 052 07 080)
     A          H                           HLPPNLGRP(SADE006 SAD052PNL)
     A                                      HLPARA(08 040 08 080)
     A          H                           HLPPNLGRP(SADE006A SAD052PNL)
     A                                      HLPARA(09 040 09 080)
     A          H                           HLPPNLGRP(SADE006B SAD052PNL)
     A                                      HLPARA(10 040 10 069)
     A          H                           HLPPNLGRP(SADE006C SAD052PNL)
     A                                      HLPARA(10 070 10 080)
     A          H                           HLPPNLGRP(SADE007 SAD052PNL)
     A                                      HLPARA(11 035 11 047)
     A          H                           HLPPNLGRP(SADE007A SAD052PNL)
     A                                      HLPARA(11 049 11 080)
     A          H                           HLPPNLGRP(SADE004B SAD052PNL)
     A                                      HLPARA(12 001 13 031)
     A          H                           HLPPNLGRP(SADE007B SAD052PNL)
     A                                      HLPARA(12 040 12 080)
     A          H                           HLPPNLGRP(SADE007C SAD052PNL)
     A                                      HLPARA(13 035 13 080)
     A          H                           HLPPNLGRP(SADE008 SAD052PNL)
     A                                      HLPARA(14 032 14 048)
     A          H                           HLPPNLGRP(SADE008A SAD052PNL)
     A                                      HLPARA(14 050 14 080)
     A          H                           HLPPNLGRP(SADE008A SAD052PNL)
     A                                      HLPARA(15 050 15 080)
     A          H                           HLPPNLGRP(SADE008B SAD052PNL)
     A                                      HLPARA(15 001 17 049)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGLN1         7A  P
     A            MSGLN2         7A  P
     A            MSGLN3         7A  P
     A            MSGLN4         7A  P
     A            MSGNR          7A  P
     A                                  1  2'SYSPED/SAD052'
     A                                      COLOR(BLU)
     A                                  1 17'Tolldeklarering Eksport'
     A                                      COLOR(WHT)
     A                                  1 41'1.Dekl.'
     A            WSDTY          2A  B  1 49TEXT('Deklarasjonstype')
     A                                      DSPATR(UL)
     A  99                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSDP           2D 0B  1 52TEXT('Dekl.prosedyre')
     A                                      DSPATR(UL)
     A  98                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                  1 56'Extref:'
     A            WSTR17        17   B  1 64
     A  63                                  DSPATR(PR)
     A  63                                  COLOR(WHT)
     A N63                                  COLOR(YLW)
     A                                  2  2'2.Avsender'
     A            WSKNK          8Y 0B  2 13TEXT('Kundenr Kjøper')
     A                                      DSPATR(UL)
     A N62                                  COLOR(YLW)
     A  62                                  DSPATR(BL)
     A  62                                  COLOR(RED)
     A                                      EDTCDE(4)
     A                                  2 23'Regnr'
     A            WSRG          11A  B  2 29TEXT('Regn Fylk.skatt.sjef')
     A                                      DSPATR(UL)
     A  65                                  DSPATR(RI)
     A                                      COLOR(YLW)
N09  A            WSREVL         1   B  2 41COLOR(YLW)
N09  A                                  2 43'(R=Reg VL)'
S10  A*                                 2 54'6.Kolli'
S10  A*           WSNTK          7Y 0B  2 62TEXT('Antall kolli')
N10  A                                  2 54'6.Kl'
N10  A            WSNTK         10Y 0B  2 59TEXT('Antall kolli')
     A                                      DSPATR(UL)
     A  95                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                      COMP(GE 0)
     A                                  2 70'S/K'
     A            WSSKI          1A  B  2 74TEXT('Selg,Kjøper,Ingen')
     A                                      DSPATR(UL)
     A  94                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                  2 76'D/K'
     A            WSKDDK         1A  B  2 80TEXT('Dagsoppgjør/Kontant')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSNAK         30A  B  3  2TEXT('Navn Kjøper')
     A                                      DSPATR(UL)
     A  96                                  DSPATR(RI)
     A N62                                  COLOR(YLW)
     A  62                                  DSPATR(BL)
     A  62                                  COLOR(RED)
S10  A*                                 3 41'Brt vekt'
S10  A*           WSVKB          9Y 0B  3 50TEXT('Bruttovekt')
N10  A                                  3 41'Br.vkt'
N10  A            WSVKB         11Y 0B  3 48TEXT('Bruttovekt')
     A                                      DSPATR(UL)
     A  93                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                      COMP(GE 0)
E06  A                                  3 60'Ref'
E06  A            UAVD           4Y 0O  3 64TEXT('AVDELING')
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
     A                                      COLOR(WHT)
E06  A            WSTDN          7Y 0O  3 69TEXT('Tolldeklarasjonsnr')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
S06  A*                                     EDTCDE(4)
N06  A                                      EDTCDE(L)
     A            SESG           3A  O  3 78TEXT('Signatur')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            WSADK1        30A  B  4  2TEXT('Adresse-1 Kjøper')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  4 40'15.Avs/utf.land'
     A            WSLKA          2A  B  4 56TEXT('AVS/UTF.LAND')
     A                                      COLOR(YLW)
     A  67                                  DSPATR(RI)
     A                                      DSPATR(UL)
     A            WSLKAT        17A  B  4 59TEXT('F.T. AVS/UTF.LAND')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSADK2        30A  B  5  2TEXT('Adresse-2 Kjøper')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  5 40'17.Be.land'
     A            WSLKB          2A  B  5 56TEXT('LANDK TIL B.LAND')
     A                                      DSPATR(UL)
     A  92                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSFTB         17A  B  5 59TEXT('FRITEKST TIL B.LAND')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSADK3        30A  B  6  2TEXT('Adresse-3 Kjøper')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
N12  A                                  6 40'Oppgjør:'
N12  A            WSOPPGJ        1A  B  6 49TEXT('Oppgjør på midlertidig')
N12  A                                      DSPATR(UL)
N12  A                                      COLOR(YLW)
N12  A                                      VALUES('J' 'N')
N12  A                                  6 51'(J/N)'
     A                                  7  2'8.Mottaker'
     A            WSKNS          8Y 0B  7 13TEXT('Kundenr Selger')
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
     A N61                                  COLOR(YLW)
     A  61                                  DSPATR(BL)
     A  61                                  COLOR(RED)
     A                                  7 40'19.Cont.'
     A            WSKDC          1A  B  7 49TEXT('Container 0/1')
     A                                      DSPATR(UL)
     A  90                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                  7 52'20.Lev.'
     A            WSLV           3A  B  7 60TEXT('Leveringsvilkår kod')
     A                                      DSPATR(UL)
     A  89                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSLVT         17A  B  7 64TEXT('Leveringsvilkår txt')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSNAS         30A  B  8  2TEXT('Navn Selger')
     A                                      DSPATR(UL)
     A  88                                  DSPATR(RI)
     A N61                                  COLOR(YLW)
     A  61                                  DSPATR(BL)
     A  61                                  COLOR(RED)
     A                                  8 40'21.Ident.'
     A            WSLKT          2A  B  8 51TEXT('Landk Tr.middel')
     A                                      DSPATR(UL)
     A  87                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSTRID        17A  B  8 55TEXT('Transport ID')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSADS1        30A  B  9  2TEXT('Adresse-1 Selger')
     A                                      DSPATR(UL)
     A  71                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                  9 40'22.Faktura'
     A            WSVAL1         3A  B  9 51TEXT('Val.kode Faktsum')
     A                                      DSPATR(UL)
     A  85                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSBEL1        13Y 2B  9 55TEXT('Beløp Faktsum')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                      COMP(GE ,00)
     A                                  9 70'23'
     A            WSVKU          7Y 3B  9 73TEXT('Valutakurs')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                      COMP(GE ,000)
     A            WSADS2        30A  B 10  2TEXT('Adresse-2 Selger')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 10 40'A.kostnad'
     A            WSVAL2         3A  B 10 51TEXT('Valutakode A.KOST')
     A                                      DSPATR(UL)
     A  84                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSBEL2        11Y 2B 10 55TEXT('Beløp A. kost')
     A                                      DSPATR(UL)
N01  A  64                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                      COMP(GE ,00)
     A            WSFTG2         1A  B 10 68TEXT('Fortegn +/- a.kost')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 10 70'24.Tr.typ'
     A            WSTST          1Y 0B 10 80TEXT('Transaksjonstype')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                      VALUES(0 1 2 3 5 9)
     A            WSADS3        30A  B 11  2TEXT('Adresse-3 Selger')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 11 35'25.Tr.måte'
     A            WSTRM          2D 0B 11 46TEXT('Transportmåte')
     A                                      DSPATR(UL)
     A  83                                  DSPATR(RI)
     A                                      COLOR(YLW)
S02  A*                                11 49'29.Utpass.'
S02  A*           WSKDH          2D 0B 11 60TEXT('Havnekode')
N02  A                                 11 49'29.Utpas'
N02  A            WSKDH          4D 0B 11 58TEXT('Tollstedskode')
     A                                      DSPATR(UL)
     A  82                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSKDFT        17A  B 11 63TEXT('FRITEKST TOLLSTED')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 12  1'14.Deklarant'
     A            WSTLF         12A  B 12 14TEXT('Telefon Deklarant')
S05  A*                                     DSPATR(UL)
N05  A                                      DSPATR(UL PR)
     A                                      COLOR(YLW)
     A                                 12 40'28.Dato'
     A            WSFID          6N 0B 12 49DSPATR(UL)
     A  80                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 12 56'Finans'
     A            WSFIF         17A  B 12 63TEXT('Finans oppl.faknr')
S03  A* 80                                  DSPATR(RI)
N03  A  60                                  DSPATR(RI)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSNAD         30A  B 13  2TEXT('Navn Deklarant')
S05  A*                                     DSPATR(UL)
N05  A                                      DSPATR(UL PR)
     A                                      COLOR(YLW)
     A                                 13 35'30.Lag.sted'
     A            WSLS          16A  B 13 47TEXT('Lagringssted tekst')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSKDLS         2A  B 13 64TEXT('Lagringssted kode')
     A                                      DSPATR(UL)
     A  81                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 14  2'Fritekst ved dataliste/F8'
     A                                      COLOR(WHT)
     A                                 14 32'48.KT.'
     A            WSKTC          1A  B 14 39TEXT('IKKE TOLLKREDITT')
     A  94                                  DSPATR(RI)
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSKTA          5N 0B 14 41TEXT('Tollkontonr-1')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      COMP(GE 0)
     A            WSKTB          2N 0B 14 47TEXT('Tollkontonr-2')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      COMP(GE 0)
     A                                 14 50'49.Godsn'
     A            WSGN          15A  B 14 59TEXT('Godsnummer')
     A                                      DSPATR(UL)
     A  68                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 15  1'31.'
     A            WSFT1         45A  B 15  5TEXT('Fritekst-1')
     A                                      DSPATR(UL)
     A  70                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSPOS          8A  B 15 59TEXT('POSISJON')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A            WSFT2         45A  B 16  5TEXT('Fritekst-2')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
N04  A                                 16 52'Slå sammen J/N:'
N04  A            WSSAM          1A  B 16 70DSPATR(UL)
N04  A                                      COLOR(YLW)
N04  A                                      VALUES('J' 'N')
     A                                 17  1'44.'
     A            WSFT3         45A  B 17  5TEXT('Fritekst-3')
     A                                      DSPATR(UL)
     A  69                                  DSPATR(RI)
     A                                      COLOR(YLW)
N04  A                                 17 52'Forhold B/N-vekt:'
N04  A            WSFVK          3Y 2B 17 70DSPATR(UL)
N04  A                                      COLOR(YLW)
N04  A                                      EDTCDE(4)
S11  A*                                     COMP(GE ,00)
N11  A                                      COMP(GT ,00)
     A* 35                             18 39'52.Garanti'
     A*           WSGFT1        30A  B 18 50TEXT('GARATI FRITEKST-1')
     A*                                     DSPATR(UL)
     A*N35                                  DSPATR(ND)
     A*N35                                  DSPATR(PR)
     A*                                     COLOR(YLW)
     A*           WSGKD          1Y 0B 19 40TEXT('GARATIKODE')
     A*                                     DSPATR(UL)
     A*N35                                  DSPATR(ND)
     A*N35                                  DSPATR(PR)
     A*                                     COLOR(YLW)
     A*                                     EDTCDE(4)
     A*                                     VALUES(0 1 2 3 4 6 7 8 9)
     A*           WSGFT2        30A  B 19 50TEXT('GARATI FRITEKST-2')
     A*                                     COLOR(YLW)
     A*N35                                  DSPATR(ND)
     A*N35                                  DSPATR(PR)
     A*                                     DSPATR(UL)
     A                                 21 30'Dato'
     A            WSDTG         10A  B 21 36TEXT('Dekl. godkj.dato')
     A                                      DSPATR(UL)
N07  A  59                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 21 48'Løpenr'
     A            WSTLL         10A  B 21 55TEXT('Tollnr Løpenr')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 21 67'Eksp.'
     A            WSTLE          6A  B 21 73TEXT('Tollnr Ekspsted')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 22  1'54.Sted'
     A            SEDST         15A  O 22  9TEXT('Deklarantens sted')
     A                                      COLOR(WHT)
     A                                 22 26'Tariffør'
     A            SETARF        16A  O 22 35TEXT('TARIFFØRSNAVN')
     A                                      COLOR(WHT)
     A            MSGL1         18A  O 23  2MSGID(&MSGLN1 SYMSGF)
     A                                      COLOR(BLU)
     A            MSGL2         18A  O 23 22MSGID(&MSGLN2 SYMSGF)
     A                                      COLOR(BLU)
     A            MSGL3         18A  O 23 42MSGID(&MSGLN3 SYMSGF)
     A                                      COLOR(BLU)
     A            MSGL4         18A  O 23 62MSGID(&MSGLN4 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
      *
     A          R BILDE06
     A                                      WINDOW(*DFT 4 40 *NOMSGLIN)
     A                                      OVERLAY
     A                                  1  2'SAD052'
     A                                      COLOR(BLU)
     A                                  3  2'Vil du kopiere vareliner?'
     A            WSJNV          1A  B  3 28DSPATR(UL)
     A                                      VALUES('J' 'N')
     A                                  4  2'Vil du kopiere fakturanr?'
     A            WSJNF          1A  B  4 28DSPATR(UL)
     A                                      VALUES('J' 'N')
N12   *
N12  A          R BILDE08
N12  A                                      WINDOW(*DFT 9 75 *NOMSGLIN)
N12  A                                      CSRLOC(LINNBR     POSNBR)
N12  A                                      OVERLAY
N12  A                                      CF12(12 'Forrige')
N12  A            LINNBR         3S 0H
N12  A            POSNBR         3S 0H
N12  A                                  1  2'SAD052'
N12  A                                      COLOR(BLU)
N12  A                                  1 12'Referere til opprinnelig SAD'
N12  A                                      COLOR(BLU)
N12  A                                  2  2'Eksp.sted:'
N12  A            WSTLE1         6A  B  2 13DSPATR(UL)
N12  A  99                                  DSPATR(RI)
N12  A                                      COLOR(YLW)
N12  A                                  2 22'Løpenr:'
N12  A            WSTLL1        10A  B  2 30DSPATR(UL)
N12  A  99                                  DSPATR(RI)
N12  A                                      COLOR(YLW)
N12  A                                  3  2'M:'
N12  A            WSTXT1        70A  B  3  5DSPATR(UL)
N12  A  98                                  DSPATR(RI)
N12  A                                      COLOR(YLW)
N12  A            WSTXT2        70A  B  4  5DSPATR(UL)
N12  A                                      COLOR(YLW)
N12  A            WSTXT3        70A  B  5  5DSPATR(UL)
N12  A                                      COLOR(YLW)
N12  A            WSTXT4        70A  B  6  5DSPATR(UL)
N12  A                                      COLOR(YLW)
N12  A            WSTXT5        70A  B  7  5DSPATR(UL)
N12  A                                      COLOR(YLW)
N12  A                                  8  2'F12=Avbryt'
N12  A                                      COLOR(BLU)
N12  A            MSGNR          7A  P
N12  A  30        MSGTXT        70A  O  9  2MSGID(&MSGNR SYMSGF)
N12  A                                      COLOR(WHT)
N06   *
N06  A          R BILDEOM01
N06  A                                      CSRLOC(LINNBR     POSNBR)
N06  A                                      CF04(04 'Søk')
N06  A N36                                  CF07(07 'Søke oppdrag')
N06  A                                      CF10(10 'Slette ferdigmeldt TVINN')
N06  A                                      CF12(12 'Avbryt program og gå til i-
N06  A                                      nngangsbilde')
N06  A                                      CF13(13 'Oppdater reg.nr og konto p-
N06  A                                      å kunde-register')
N06  A                                      CF14(14 'KUNDEINFO')
N06  A                                      CF15(15 'Mer rubr.28')
N06  A                                      CF24(24 'BLA I F-TASTER')
N06  A                                      OVERLAY
N06  A          H                           HLPPNLGRP(SADE001 SAD052PNL)
N06  A                                      HLPARA(01 041 01 053)
N06  A          H                           HLPPNLGRP(SADE001A SAD052PNL)
N06  A                                      HLPARA(01 052 01 053)
N06  A          H                           HLPPNLGRP(SADE001B SAD052PNL)
N06  A                                      HLPARA(01 056 01 080)
N06  A          H                           HLPPNLGRP(SADE002 SAD052PNL)
N06  A                                      HLPARA(02 001 06 037)
N06  A          H                           HLPPNLGRP(SADE003 SAD052PNL)
N06  A                                      HLPARA(02 054 02 068)
N06  A          H                           HLPPNLGRP(SADE003A SAD052PNL)
N06  A                                      HLPARA(02 070 02 074)
N06  A          H                           HLPPNLGRP(SADE003B SAD052PNL)
N06  A                                      HLPARA(02 076 02 080)
N06  A          H                           HLPPNLGRP(SADE003C SAD052PNL)
N06  A                                      HLPARA(03 041 03 061)
N06  A          H                           HLPPNLGRP(SADE003D SAD052PNL)
N06  A                                      HLPARA(04 056 04 075)
N06  A          H                           HLPPNLGRP(SADE004 SAD052PNL)
N06  A                                      HLPARA(07 001 11 039)
N06  A          H                           HLPPNLGRP(SADE005 SAD052PNL)
N06  A                                      HLPARA(05 040 05 080)
N06  A          H                           HLPPNLGRP(SADE005B SAD052PNL)
N06  A                                      HLPARA(07 041 07 049)
N06  A          H                           HLPPNLGRP(SADE005C SAD052PNL)
N06  A                                      HLPARA(07 052 07 080)
N06  A          H                           HLPPNLGRP(SADE006 SAD052PNL)
N06  A                                      HLPARA(08 040 08 080)
N06  A          H                           HLPPNLGRP(SADE006A SAD052PNL)
N06  A                                      HLPARA(09 040 09 080)
N06  A          H                           HLPPNLGRP(SADE006B SAD052PNL)
N06  A                                      HLPARA(10 040 10 069)
N06  A          H                           HLPPNLGRP(SADE006C SAD052PNL)
N06  A                                      HLPARA(10 070 10 080)
N06  A          H                           HLPPNLGRP(SADE007 SAD052PNL)
N06  A                                      HLPARA(11 035 11 047)
N06  A          H                           HLPPNLGRP(SADE007A SAD052PNL)
N06  A                                      HLPARA(11 049 11 080)
N06  A          H                           HLPPNLGRP(SADE004B SAD052PNL)
N06  A                                      HLPARA(12 001 13 031)
N06  A          H                           HLPPNLGRP(SADE007B SAD052PNL)
N06  A                                      HLPARA(12 040 12 080)
N06  A          H                           HLPPNLGRP(SADE007C SAD052PNL)
N06  A                                      HLPARA(13 035 13 080)
N06  A          H                           HLPPNLGRP(SADE008 SAD052PNL)
N06  A                                      HLPARA(14 032 14 048)
N06  A          H                           HLPPNLGRP(SADE008A SAD052PNL)
N06  A                                      HLPARA(14 050 14 080)
N06  A          H                           HLPPNLGRP(SADE008A SAD052PNL)
N06  A                                      HLPARA(15 050 15 080)
N06  A          H                           HLPPNLGRP(SADE008B SAD052PNL)
N06  A                                      HLPARA(15 001 17 049)
N06  A            LINNBR         3S 0H
N06  A            POSNBR         3S 0H
N06  A            MSGLN1         7A  P
N06  A            MSGLN2         7A  P
N06  A            MSGLN3         7A  P
N06  A            MSGLN4         7A  P
N06  A            MSGNR          7A  P
N06  A                                  1  2'SYSPED/SAD052'
N06  A                                      COLOR(BLU)
N06  A                                  1 17'Tolldeklarering Eksport'
N06  A                                      COLOR(WHT)
N06  A                                  1 41'1.Dekl.'
N06  A            WSDTY          2A  B  1 49TEXT('Deklarasjonstype')
N06  A                                      DSPATR(UL)
N06  A  99                                  DSPATR(RI)
N06  A                                      COLOR(YLW)
N06  A            WSDP           2D 0B  1 52TEXT('Dekl.prosedyre')
N06  A                                      DSPATR(UL)
N06  A  98                                  DSPATR(RI)
N06  A                                      COLOR(YLW)
N06  A                                  1 56'Extref:'
N06  A            WSTR17        17   B  1 64
N06  A  63                                  DSPATR(PR)
N06  A  63                                  COLOR(WHT)
N06  A N63                                  COLOR(YLW)
N06  A                                  2  2'2.Avsender'
N06  A            WSKNK          8Y 0B  2 13TEXT('Kundenr Kjøper')
N06  A                                      DSPATR(UL)
N06  A N62                                  COLOR(YLW)
N06  A  62                                  DSPATR(BL)
N06  A  62                                  COLOR(RED)
N06  A                                      EDTCDE(4)
N06  A                                  2 23'Regnr'
N06  A            WSRG          11A  B  2 29TEXT('Regn Fylk.skatt.sjef')
N06  A                                      DSPATR(UL)
N06  A  65                                  DSPATR(RI)
N06  A                                      COLOR(YLW)
N06  A                                  2 41'Type'
N06  A            WSTYPE         4A  B  2 46TEXT('Typetilfelle')
N06  A                                      DSPATR(UL)
N06  A  70                                  DSPATR(RI)
N06  A                                      COLOR(YLW)
S10  A*                                 2 54'6.Kolli'
S10  A*           WSNTK          7Y 0B  2 62TEXT('Antall kolli')
N10  A                                  2 54'6.Kl'
N10  A            WSNTK         10Y 0B  2 59TEXT('Antall kolli')
N06  A                                      DSPATR(UL)
N06  A  95                                  DSPATR(RI)
N06  A                                      COLOR(YLW)
N06  A                                      EDTCDE(4)
N06  A                                      COMP(GE 0)
N06  A                                  2 70'S/K'
N06  A            WSSKI          1A  B  2 74TEXT('Selg,Kjøper,Ingen')
N06  A                                      DSPATR(UL)
N06  A  94                                  DSPATR(RI)
N06  A                                      COLOR(YLW)
N06  A                                  2 76'D/K'
N06  A            WSKDDK         1A  B  2 80TEXT('Dagsoppgjør/Kontant')
N06  A                                      DSPATR(UL)
N06  A                                      COLOR(YLW)
N06  A            WSNAK         30A  B  3  2TEXT('Navn Kjøper')
N06  A                                      DSPATR(UL)
N06  A  96                                  DSPATR(RI)
N06  A N62                                  COLOR(YLW)
N06  A  62                                  DSPATR(BL)
N06  A  62                                  COLOR(RED)
S10  A*                                 3 41'Brt vekt'
S10  A*           WSVKB          9Y 0B  3 50TEXT('Bruttovekt')
N10  A                                  3 41'Br.vkt'
N10  A            WSVKB         11Y 0B  3 48TEXT('Bruttovekt')
N06  A                                      DSPATR(UL)
N06  A  93                                  DSPATR(RI)
N06  A                                      COLOR(YLW)
N06  A                                      EDTCDE(4)
N06  A                                      COMP(GE 0)
N06  A                                  3 60'Ref'
N06  A            UAVD           4Y 0O  3 64TEXT('AVDELING')
N06  A                                      DSPATR(UL)
N06  A                                      EDTCDE(4)
N06  A                                      COLOR(WHT)
N06  A            WSTDN          7Y 0O  3 69TEXT('Tolldeklarasjonsnr')
N06  A                                      DSPATR(UL)
N06  A                                      COLOR(WHT)
N06  A                                      EDTCDE(L)
N06  A            SESG           3A  O  3 78TEXT('Signatur')
N06  A                                      DSPATR(UL)
N06  A                                      COLOR(WHT)
N06  A            WSADK1        30A  B  4  2TEXT('Adresse-1 Kjøper')
N06  A                                      DSPATR(UL)
N06  A                                      COLOR(YLW)
N06  A                                  4 40'15.Avs/utf.land'
N06  A            WSLKA          2A  B  4 56TEXT('AVS/UTF.LAND')
N06  A                                      COLOR(YLW)
N06  A  67                                  DSPATR(RI)
N06  A                                      DSPATR(UL)
N06  A            WSLKAT        17A  B  4 59TEXT('F.T. AVS/UTF.LAND')
N06  A                                      DSPATR(UL)
N06  A                                      COLOR(YLW)
N06  A            WSADK2        30A  B  5  2TEXT('Adresse-2 Kjøper')
N06  A                                      DSPATR(UL)
N06  A                                      COLOR(YLW)
N06  A                                  5 40'17.Be.land'
N06  A            WSLKB          2A  B  5 56TEXT('LANDK TIL B.LAND')
N06  A                                      DSPATR(UL)
N06  A  92                                  DSPATR(RI)
N06  A                                      COLOR(YLW)
N06  A            WSFTB         17A  B  5 59TEXT('FRITEKST TIL B.LAND')
N06  A                                      DSPATR(UL)
N06  A                                      COLOR(YLW)
N06  A            WSADK3        30A  B  6  2TEXT('Adresse-3 Kjøper')
N06  A                                      DSPATR(UL)
N06  A                                      COLOR(YLW)
N06  A                                  7  2'8.Mottaker'
N06  A            WSKNS          8Y 0B  7 13TEXT('Kundenr Selger')
N06  A                                      DSPATR(UL)
N06  A                                      EDTCDE(4)
N06  A N61                                  COLOR(YLW)
N06  A  61                                  DSPATR(BL)
N06  A  61                                  COLOR(RED)
N06  A                                  7 40'19.Cont.'
N06  A            WSKDC          1A  B  7 49TEXT('Container 0/1')
N06  A                                      DSPATR(UL)
N06  A  90                                  DSPATR(RI)
N06  A                                      COLOR(YLW)
N06  A                                  7 52'20.Lev.'
N06  A            WSLV           3A  B  7 60TEXT('Leveringsvilkår kod')
N06  A                                      DSPATR(UL)
N06  A  89                                  DSPATR(RI)
N06  A                                      COLOR(YLW)
N06  A            WSLVT         17A  B  7 64TEXT('Leveringsvilkår txt')
N06  A                                      DSPATR(UL)
N06  A                                      COLOR(YLW)
N06  A            WSNAS         30A  B  8  2TEXT('Navn Selger')
N06  A                                      DSPATR(UL)
N06  A  88                                  DSPATR(RI)
N06  A N61                                  COLOR(YLW)
N06  A  61                                  DSPATR(BL)
N06  A  61                                  COLOR(RED)
N06  A                                  8 40'21.Ident.'
N06  A            WSLKT          2A  B  8 51TEXT('Landk Tr.middel')
N06  A                                      DSPATR(UL)
N06  A  87                                  DSPATR(RI)
N06  A                                      COLOR(YLW)
N06  A            WSTRID        17A  B  8 55TEXT('Transport ID')
N06  A                                      DSPATR(UL)
N06  A                                      COLOR(YLW)
N06  A            WSADS1        30A  B  9  2TEXT('Adresse-1 Selger')
N06  A                                      DSPATR(UL)
N06  A  71                                  DSPATR(RI)
N06  A                                      COLOR(YLW)
N06  A                                  9 40'22.Faktura'
N06  A            WSVAL1         3A  B  9 51TEXT('Val.kode Faktsum')
N06  A                                      DSPATR(UL)
N06  A  85                                  DSPATR(RI)
N06  A                                      COLOR(YLW)
N06  A            WSBEL1        13Y 2B  9 55TEXT('Beløp Faktsum')
N06  A                                      DSPATR(UL)
N06  A                                      COLOR(YLW)
N06  A                                      EDTCDE(4)
N06  A                                      COMP(GE ,00)
N06  A                                  9 70'23'
N06  A            WSVKU          7Y 3B  9 73TEXT('Valutakurs')
N06  A                                      DSPATR(UL)
N06  A                                      COLOR(YLW)
N06  A                                      EDTCDE(4)
N06  A                                      COMP(GE ,000)
N06  A            WSADS2        30A  B 10  2TEXT('Adresse-2 Selger')
N06  A                                      DSPATR(UL)
N06  A                                      COLOR(YLW)
N06  A                                 10 40'A.kostnad'
N06  A            WSVAL2         3A  B 10 51TEXT('Valutakode A.KOST')
N06  A                                      DSPATR(UL)
N06  A  84                                  DSPATR(RI)
N06  A                                      COLOR(YLW)
N06  A            WSBEL2        11Y 2B 10 55TEXT('Beløp A. kost')
N06  A                                      DSPATR(UL)
N06  A  64                                  DSPATR(RI)
N06  A                                      COLOR(YLW)
N06  A                                      EDTCDE(4)
N06  A                                      COMP(GE ,00)
N06  A            WSFTG2         1A  B 10 68TEXT('Fortegn +/- a.kost')
N06  A                                      DSPATR(UL)
N06  A                                      COLOR(YLW)
N06  A                                 10 70'24.Tr.typ'
N06  A            WSTST          1Y 0B 10 80TEXT('Transaksjonstype')
N06  A                                      DSPATR(UL)
N06  A                                      COLOR(YLW)
N06  A                                      EDTCDE(4)
N06  A                                      VALUES(0 1 2 3 5 9)
N06  A            WSADS3        30A  B 11  2TEXT('Adresse-3 Selger')
N06  A                                      DSPATR(UL)
N06  A                                      COLOR(YLW)
N06  A                                 11 35'25.Tr.måte'
N06  A            WSTRM          2D 0B 11 46TEXT('Transportmåte')
N06  A                                      DSPATR(UL)
N06  A  83                                  DSPATR(RI)
N06  A                                      COLOR(YLW)
N06  A                                 11 49'29.Utpas'
N06  A            WSKDH          4D 0B 11 58TEXT('Tollstedskode')
N06  A                                      DSPATR(UL)
N06  A  82                                  DSPATR(RI)
N06  A                                      COLOR(YLW)
N06  A            WSKDFT        17A  B 11 63TEXT('FRITEKST TOLLSTED')
N06  A                                      DSPATR(UL)
N06  A                                      COLOR(YLW)
N06  A                                 12  1'14.Deklarant'
N06  A            WSTLF         12A  B 12 14TEXT('Telefon Deklarant')
N06  A                                      DSPATR(UL PR)
N06  A                                      COLOR(YLW)
N06  A                                 12 40'28.Dato'
N06  A            WSFID          6N 0B 12 49DSPATR(UL)
N06  A  80                                  DSPATR(RI)
N06  A                                      COLOR(YLW)
N06  A                                 12 56'Finans'
N06  A            WSFIF         17A  B 12 63TEXT('Finans oppl.faknr')
N06  A  60                                  DSPATR(RI)
N06  A                                      DSPATR(UL)
N06  A                                      COLOR(YLW)
N06  A            WSNAD         30A  B 13  2TEXT('Navn Deklarant')
N06  A                                      DSPATR(UL PR)
N06  A                                      COLOR(YLW)
N06  A                                 13 35'30.Lag.sted'
N06  A            WSLS          16A  B 13 47TEXT('Lagringssted tekst')
N06  A                                      DSPATR(UL)
N06  A                                      COLOR(YLW)
N06  A            WSKDLS         2A  B 13 64TEXT('Lagringssted kode')
N06  A                                      DSPATR(UL)
N06  A  81                                  DSPATR(RI)
N06  A                                      COLOR(YLW)
N06  A                                 14  2'Fritekst ved dataliste/F8'
N06  A                                      COLOR(WHT)
N06  A                                 14 32'48.KT.'
N06  A            WSKTC          1A  B 14 39TEXT('IKKE TOLLKREDITT')
N06  A  94                                  DSPATR(RI)
N06  A                                      DSPATR(UL)
N06  A                                      COLOR(YLW)
N06  A            WSKTA          5N 0B 14 41TEXT('Tollkontonr-1')
N06  A                                      DSPATR(UL)
N06  A                                      COLOR(YLW)
N06  A                                      COMP(GE 0)
N06  A            WSKTB          2N 0B 14 47TEXT('Tollkontonr-2')
N06  A                                      DSPATR(UL)
N06  A                                      COLOR(YLW)
N06  A                                      COMP(GE 0)
N06  A                                 14 50'49.Godsn'
N06  A            WSGN          15A  B 14 59TEXT('Godsnummer')
N06  A                                      DSPATR(UL)
N06  A  68                                  DSPATR(RI)
N06  A                                      COLOR(YLW)
N06  A                                 15  2'Slå sammen J/N:'
N06  A            WSSAM          1A  O 15 18DSPATR(UL)
N06  A                                      COLOR(WHT)
N06  A                                 15 22'Forhold B/N-vekt:'
N06  A            WSFVK          3Y 2O 15 40DSPATR(UL)
N06  A                                      COLOR(WHT)
N06  A                                      EDTCDE(4)
N06  A            WSPOS          8A  B 15 59TEXT('POSISJON')
N06  A                                      DSPATR(UL)
N06  A                                      COLOR(YLW)
N03  A                                 16  1'Årsak:'
N03  A            WSFT01        70A  B 16  9DSPATR(UL)
N03  A  69                                  DSPATR(RI)
N03  A                                      COLOR(YLW)
N03  A            WSFT02        70A  B 17  9DSPATR(UL)
N03  A                                      COLOR(YLW)
N03  A            WSFT03        70A  B 18  9DSPATR(UL)
N03  A                                      COLOR(YLW)
N03  A            WSFT04        70A  B 19  9DSPATR(UL)
N03  A                                      COLOR(YLW)
N08  A            WSFT05        70A  B 20  9DSPATR(UL)
N08  A                                      COLOR(YLW)
N06  A                                 21 30'Dato'
N06  A            WSDTG         10A  O 21 36TEXT('Dekl. godkj.dato')
N06  A                                      DSPATR(UL)
N06  A                                      COLOR(WHT)
N06  A                                 21 48'Løpenr'
N06  A            WSTLL         10A  O 21 55TEXT('Tollnr Løpenr')
N06  A                                      DSPATR(UL)
N06  A                                      COLOR(WHT)
N06  A                                 21 67'Eksp.'
N06  A            WSTLE          6A  O 21 73TEXT('Tollnr Ekspsted')
N06  A                                      DSPATR(UL)
N06  A                                      COLOR(WHT)
N06  A                                 22  1'54.Sted'
N06  A            SEDST         15A  O 22  9TEXT('Deklarantens sted')
N06  A                                      COLOR(WHT)
N06  A                                 22 26'Tariffør'
N06  A            SETARF        16A  O 22 35TEXT('TARIFFØRSNAVN')
N06  A                                      COLOR(WHT)
N06  A            MSGL1         18A  O 23  2MSGID(&MSGLN1 SYMSGF)
N06  A                                      COLOR(BLU)
N06  A            MSGL2         18A  O 23 22MSGID(&MSGLN2 SYMSGF)
N06  A                                      COLOR(BLU)
N06  A            MSGL3         18A  O 23 42MSGID(&MSGLN3 SYMSGF)
N06  A                                      COLOR(BLU)
N06  A            MSGL4         18A  O 23 62MSGID(&MSGLN4 SYMSGF)
N06  A                                      COLOR(BLU)
N06  A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
N06  A                                      COLOR(WHT)
