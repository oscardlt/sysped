      *********************************************************************
      *  After compiling this RPG MODULE,
      *  create the related program with the following command:
      *
CRTPG+*  CRTPGM SYSPED/STSL10 MODULE(*LIBL/STSL10
CRTPGM*        *LIBL/INCGI) ACTGRP(*NEW) AUT(*USE)
      *
      *********************************************************************
      *
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
      *--------------------------------------------------------------------
      * Data base files
      *--------------------------------------------------------------------
     FBRIDF     if   e           k disk    usropn
      *--------------------------------------------------------------------
      * Prototype defintions and standard system API error structure
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      *
      *--------------------------------------------------------------------
      * Variables common to CGI programs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
      * Variables received from the input string
     DWSUSER           s             10
     D FEILMELDING     S             70
     D FEILKODE        S              1
     D Fn              s             10
     D Lib             s             10
     D Mbr             s             10
     D MsgId           s              7
     D State           ds                  based(StateP)
     D  UBRID                        10
     D  XMR                 1001   1200  0 DIM(40)
     D UsrSpcName      s             10
     D UsrSpcLib       c                   'SYCGIUSP'
     D PGM             C                   CONST('SYSPED/CHECKUSR')
      *--------------------------------------------------------------------
      * Prolog common to CGI programs
      * -Receive input from the remote browser
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prolog3
      *=====================================================================******
      * MAIN PROCESS LINE
      *=====================================================================******
      * Parse input string
     C                   EXSR      PARSE
      *
     C                   EXSR      INIT
      *
     C                   IF        ((StateP = *null) OR (UBRID <> WSUSER))
     C                   EXSR      LoadHtmlFeil
     C                   GOTO      SLUTT
     C                   END
      ***
      * Set output skeleton html member name
     C                   exsr      SetSkl
      * Read skeleton output html, etc.
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
      *
      *------------------
      * Write sections of HTML.
      *
      * Her er HOVED logikk-rutine
     C                   exsr      SetTop
     C                   callp     wrtsection('all')
      *
     C     SLUTT         TAG
     C                   callp     wrtsection('endhtml')
     C                   callp     wrtsection('*fini')
      *
      *
     C                   close     BRIDF
     C                   eval      *inlr = *on
     C                   RETURN
      *
      *=====================================================================******
      * Parse input
      *=====================================================================******
     C     Parse         begsr
     C                   eval      WSUSER   = zhbgetvar('USER')
      *
     C                   EVAL      UsrSpcName  = zhbgetvar('UsrSpcName')
     C                   eval      StateP = RtvUsrSpcPtr(UsrSpcName:
     C                             UsrSpcLib)
      *
     C                   endsr
      *=====================================================================
      * Noe feil ved bruker ID
      *=====================================================================
     C     LoadHtmlFeil  begsr
     C                   callp     gethtml('HTMLSRC':
     C                             '*LIBL':
     C                             'STSL10':'/CGITAG/')
     C                   callp     updHTMLvar('FEILTXT':FEILMELDING)
     C                   callp     updHTMLvar('FEILKODE':FEILKODE)
     C                   callp     wrtsection('all')
     C                   endsr
      *=====================================================================
      * EXECUTE LOGIC / Set html output variables for section /$top
      *=====================================================================
     C     SetTop        begsr
      *
BODY C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updHTMLvar('user':BIBRID)
     C                   callp     updHTMLvar('besk':BIBESK)
     C                   movea     *zeros        xmr
     C                   callp     updHTMLvar('UsrSpcName':UsrSpcName)
     C                   callp     updHTMLvar('kunr':
     C                             %trim(%editc(BIKUNR:'Z')))
     C                   callp     updHTMLvar('sdato':
     C                             %trim(%editw(BIDTV:'    .  .  ')))
     C                   callp     updHTMLvar('stid':
     C                             %trim(%editw(BITIDV:'  .  .  ')))
      *
     C                   endsr
      *
      *
      *=====================================================================******
      *  Different User than last invocation
      *=====================================================================******
     C     Init          begsr
     C                   open      BRIDF
     C     WSUSER        CHAIN     BRIDF                              30
      *
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C* (bedre ressursmessig). MODUL må da være med comp. av dette pgm!!
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
      *
     C                   endsr
      *
      *=====================================================================******
      * Set html output skeleton member before its read into memory
      *=====================================================================******
     C     SetSkl        begsr
      *------------------
      * Set html output skeleton member
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC'
     C                   eval      Mbr = 'STSL10'
      *
     C                   endsr
