********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 YBC PTF10 SØK KUN EN KUNDE AV GANGEN
xxxxxx*xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
11011 * 02 YBC PTF11 SKILLE IMPORT OR EKSPORT
********************************************************************
      *
      *****************************************************************
      *** PROGRAM SKAL VEDLIKEHOLDE BILL OF LADING (LOKAL)          ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER          ***
      *** LEDIGE INDIKATORER 01 02 05-09 13 15-17 19-26             ***
      *** LEDIGE INDIKATORER 27 28 31 32 34-50 54-88                ***
      *****************************************************************
      *
     FBOOKING   IF   E           K DISK
     FBILLOF    UF   E           K DISK
     FBLLOKSTD  IF   E           K DISK
     FBLLOK     UF A E           K DISK
     FBLV1      UF A E           K DISK
     FBLV2      UF A E           K DISK
     FCDBLF     IF   E           K DISK
     FCDCON4    IF   E           K DISK
     FKODTP     IF   E           K DISK
     FSTED2     IF   E           K DISK
     FFIRM      IF   E           K DISK
     FCUNDL01   IF   E           K DISK
     FFRATXT    IF   E           K DISK
     FFREETXT   IF   E           K DISK
     FSYSJ50FD  CF   E             WORKSTN
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D                 DS
     D  BONT1                  1      7  0
     D  BONT2                  8     14  0
     D  BONT3                 15     21  0
     D  BONT4                 22     28  0
     D  NT                     1     28  0
     D                                     DIM(4)
     D                 DS
     D  BOKDC1                 1      2
     D  BOKDC2                 3      4
     D  BOKDC3                 5      6
     D  BOKDC4                 7      8
     D  KC                     1      8
     D                                     DIM(4)
     D                 DS
     D  BOMN1                  1     20
     D  BOMN2                 21     40
     D  BOMN3                 41     60
     D  BOMN4                 61     80
     D  MN                     1     80
     D                                     DIM(4)
     D                 DS
     D  BOVS1                  1     25
     D  BOVS2                 26     50
     D  BOVS3                 51     75
     D  BOVS4                 76    100
     D  VS                     1    100
     D                                     DIM(4)
     D                 DS
     D  BOVKT1                 1      9  0
     D  BOVKT2                10     18  0
     D  BOVKT3                19     27  0
     D  BOVKT4                28     36  0
     D  VK                     1     36  0
     D                                     DIM(4)
     D                 DS
     D  BOM31                  1      7  3
     D  BOM32                  8     14  3
     D  BOM33                 15     21  3
     D  BOM34                 22     28  3
     D  M3                     1     28  3
     D                                     DIM(4)
     D                 DS
     D  BODTRÅ                 1      4  0
     D  BODTRM                 5      6  0
     D  BODTRD                 7      8  0
     D  BODTR                  1      8  0
     D                 DS
     D  WSDTRD                 1      2  0
     D  WSDTRM                 3      4  0
     D  WSDTRÅ                 5      6  0
     D  WSDTR                  1      6  0
     D                 DS
     D  BEDTÅ                  1      4  0
     D  BEDTM                  5      6  0
     D  BEDTD                  7      8  0
     D  BEDT                   1      8  0
     D                 DS
     D  WSDTD                  1      2  0
     D  WSDTM                  3      4  0
     D  WSDTÅ                  5      6  0
     D  WSDT                   1      6  0
     C*
     C                   EXSR      INIT
     C*
     C     START         TAG
     C                   EXSR      TSTOPL
     C  N30
     CANN04
     CANN18              Z-ADD     4             LINNBR
     C  N30
     CANN04
     CANN18              Z-ADD     6             POSNBR
     C*
     C*** TEST OM DET ER OPPLYSNINGER TIL OPPDRAG
     C     FREETK        SETLL     FREETXT
     C     FREETK        READE     FREETXT                                29
     C*
     C                   EXSR      KDTFMT
     C                   EXFMT     BILDE01
     C                   EXSR      KDTFMT
     C                   SETOFF                                       30
     C                   MOVEA     '00000000'    *IN(92)
     C                   MOVEA     '000'         *IN(89)
     C*
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C                   Z-ADD     WSSUBN        XRECNO            4 0
     C*
     C   04              DO
     C                   EXSR      SØKDTA
     C                   GOTO      START
     C                   END
     C   12              DO
     C                   Z-ADD     1             UFLAGG
     C                   GOTO      SLUTT
     C                   END
     C   14              DO
     C                   EXSR      VISOPL
     C                   GOTO      START
     C                   END
     C* FREETEXT
     C   18              DO
     C                   EXSR      VISFRE
     C                   GOTO      START
     C                   END
     C*
     C                   EXSR      TESTB
     C   30              GOTO      START
     C     WSOPDL        IFNE      0
     C                   MOVE      *ZEROS        WSOPDL
     C                   GOTO      START
     C                   END
     C                   EXSR      OPDBL
     C*
     C   10
     COR 11              DO
     C                   EXSR      TILV
     C     UFLAGG        IFEQ      3
     C                   GOTO      START2
     C                   ELSE
     C                   GOTO      START
     C                   END
     C                   END
     C*
     C  N03              GOTO      START
     C*
     C     START2        TAG
     C                   EXFMT     BILDE02
     C   03              Z-ADD     3             UFLAGG
     C   12              GOTO      START
     C*
     C*** SKRIVE UT LOKAL B/L
     C     WSPRT         IFEQ      'J'
     C                   MOVE      BOOPD         UOPDT             7 0
     C                   MOVE      *BLANKS       UPRT26           10
     C                   MOVE      *BLANKS       UPT26            10
     C                   Z-ADD     28            YPLNR             3 0
     C     KODTPK        CHAIN     KODTP                              33
     C  N33              MOVE      KOPNVN        UPRT28           10
     C  N33              MOVE      KOPTY         UPT28            10
     C                   CALL      'SYSJ55CL'
     C                   PARM                    BOOPD
     C                   PARM                    UOPDT
     C                   PARM                    UPRT26
     C                   PARM                    UPT26
     C                   PARM                    UPRT28
     C                   PARM                    UPT28
     C                   PARM                    WSJBK
     C                   END
     C*
     C*** SKRIVE UT LASTESEDDEL
     C     WSLSTS        IFEQ      'J'
     C                   MOVE      BOOPD         UOPDT
     C                   Z-ADD     37            YPLNR
     C     KODTPK        CHAIN     KODTP                              33
     C  N33              MOVE      KOPNVN        UPRT37           10
     C  N33              MOVE      KOPTY         UPT37            10
     C                   CALL      'SYSJ65CL'
     C                   PARM                    BOOPD
     C                   PARM                    UOPDT
     C                   PARM                    UPRT37
     C                   PARM                    UPT37
     C                   PARM                    WSJBK
     C                   END
     C*
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
     C*
     C***********************************************
     C     TESTB         BEGSR
     C***********************************************
     C*
     C*** HENTE LOKAL B/L
     C     WSOPDL        IFNE      0
     C     WSOPDL        CHAIN     BLLOKSTD                           89
     C   89              DO
     C                   MOVE      'SPL0106'     MSGNR
     C                   Z-ADD     21            LINNBR
     C                   Z-ADD     76            POSNBR
     C                   SETON                                        30
     C                   GOTO      SLTB
     C                   END
      * DERSOM AVS/MOT GITT (KOPIERT) FØR HENT AV STANDARD-MAL, BEHOLD
     C     WSKNA         IFNE      *ZEROS
     C                   MOVE      WSKNA         BEKNA
     C                   MOVE      *BLANKS       BENAA
     C                   END
     C     WSKNM         IFNE      *ZEROS
     C                   MOVE      WSKNM         BEKNM
     C                   MOVE      *BLANKS       BENAM
     C                   END
     C                   EXSR      MOVBLW
     C                   END
     C*
     C*** AVSENDER
     C     WSKNA         IFNE      0
     C                   MOVE      WSKNA         KUNDNR
     C     KUND1K        CHAIN     CUNDL01                            99
     C   99
     CANN30              DO
     C                   SETON                                        30
     C                   MOVE      'SPK0126'     MSGNR
     C                   Z-ADD     4             LINNBR
     C                   Z-ADD     6             POSNBR
     C                   END
     C  N99WSNAA         IFEQ      *BLANKS
     C                   MOVE      KNAVN         WSNAA
     C                   MOVE      ADR1          WSAD1A
     C                   MOVE      ADR2          WSAD2A
     C                   MOVE      *BLANKS       WSAD3A
     C     POSTNR        IFEQ      0
     C                   MOVEL     ADR3          WSAD3A
     C                   ELSE
     C                   MOVEL     POSTNR        WSAD3A
     C                   MOVE      ADR3          WSAD3A
     C                   END
     C                   END
     C                   END
     C*
     C*** MOTTAGER
     C     WSKNM         IFNE      0
     C                   MOVE      WSKNM         KUNDNR
     C     KUND1K        CHAIN     CUNDL01                            98
     C   98
     CANN30              DO
     C                   SETON                                        30
     C                   MOVE      'SPK0126'     MSGNR
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     6             POSNBR
     C                   END
     C  N98WSNAM         IFEQ      *BLANKS
     C                   MOVE      KNAVN         WSNAM
     C                   MOVE      ADR1          WSAD1M
     C                   MOVE      ADR2          WSAD2M
     C                   MOVE      *BLANKS       WSAD3M
     C     POSTNR        IFEQ      0
     C                   MOVEL     ADR3          WSAD3M
     C                   ELSE
     C                   MOVEL     POSTNR        WSAD3M
     C                   MOVE      ADR3          WSAD3M
     C                   END
     C                   END
     C                   END
     C*
     C*** NOTIFY
     C     WSKNN         IFNE      0
     C                   MOVE      WSKNN         KUNDNR
     C     KUND1K        CHAIN     CUNDL01                            97
     C   97
     CANN30              DO
     C                   SETON                                        30
     C                   MOVE      'SPK0126'     MSGNR
     C                   Z-ADD     8             LINNBR
     C                   Z-ADD     6             POSNBR
     C                   END
     C  N97WSNAN         IFEQ      *BLANKS
     C                   MOVE      KNAVN         WSNAN
     C                   MOVE      ADR1          WSAD1N
     C                   MOVE      ADR2          WSAD2N
     C                   MOVE      *BLANKS       WSAD3N
     C     POSTNR        IFEQ      0
     C                   MOVEL     ADR3          WSAD3M
     C                   ELSE
     C                   MOVEL     POSTNR        WSAD3N
     C                   MOVE      ADR3          WSAD3N
     C                   END
     C                   END
     C                   END
     C*
     C*** LAYOUT AV BILL OF LADING
     C     WSKDP         IFNE      *BLANKS
     C     WSKDP         CHAIN     CDBLF                              96
     C   96
     CANN30              DO
     C                   SETON                                        30
     C                   MOVE      'SPK0130'     MSGNR
     C                   Z-ADD     10            LINNBR
     C                   Z-ADD     19            POSNBR
     C                   END
     C                   END
     C*
     C*** SHIPPERS ACCOUNT
     C     WSKDSA        COMP      ' '                                9595
     C   95WSKDSA        COMP      'X'                                9595
     C   95
     CANN30              DO
     C                   SETON                                        30
     C                   MOVE      'SPK0127'     MSGNR
     C                   Z-ADD     14            LINNBR
     C                   Z-ADD     37            POSNBR
     C                   END
     C*
     C*** RECEIVERS ACCOUNT
     C     WSKDRA        COMP      ' '                                9494
     C   94WSKDRA        COMP      'X'                                9494
     C   94
     CANN30              DO
     C                   SETON                                        30
     C                   MOVE      'SPK0127'     MSGNR
     C                   Z-ADD     15            LINNBR
     C                   Z-ADD     37            POSNBR
     C                   END
     C*
     C*** THROUGHFREIGHT
     C     WSKDTF        COMP      ' '                                9393
     C   93WSKDTF        COMP      'X'                                9393
     C   93
     CANN30              DO
     C                   SETON                                        30
     C                   MOVE      'SPK0127'     MSGNR
     C                   Z-ADD     16            LINNBR
     C                   Z-ADD     37            POSNBR
     C                   END
     C*
     C*** COLL./PPD.
     C     WSKDCP        COMP      ' '                                9292
     C   92WSKDCP        COMP      'C'                                9292
     C   92WSKDCP        COMP      'P'                                9292
     C   92
     CANN30              DO
     C                   SETON                                        30
     C                   MOVE      'SPK0128'     MSGNR
     C                   Z-ADD     17            LINNBR
     C                   Z-ADD     14            POSNBR
     C                   END
     C*
     C*** STED
     C     WSSD          IFNE      *BLANKS
     C     WSSD          CHAIN     STED2                              91
     C   91
     CANN30              DO
     C                   SETON                                        30
     C                   MOVE      'SPS0107'     MSGNR
     C                   Z-ADD     17            LINNBR
     C                   Z-ADD     51            POSNBR
     C                   END
     C                   END
     C*
     C*** TEST DATO
     C     WSDT          IFNE      0
     C                   MOVE      WSDTÅ         UÅÅ               2 0
     C                   MOVE      WSDTM         UMM               2 0
     C                   MOVE      WSDTD         UDD               2 0
     C                   CALL      'TESTDATO'
     C                   PARM                    UDD
     C                   PARM                    UMM
     C                   PARM                    UÅÅ
     C                   PARM                    UFLAGG            1 0
     C     UFLAGG        COMP      1                                      90
     C   90
     CANN30              DO
     C                   MOVE      'SPD0001'     MSGNR
     C                   SETON                                        30
     C                   Z-ADD     17            LINNBR
     C                   Z-ADD     64            POSNBR
     C                   END
     C                   END
     C*
     C     SLTB          ENDSR
     C*
     C***********************************************
     C     TSTOPL        BEGSR
     C***********************************************
     C                   MOVEA     '000'         *IN(51)
     C                   SETOFF                                       33
     C*
     C     WSKNA         IFNE      0
     C                   MOVE      WSKNA         KUNDNR
     C     FRATXK        CHAIN     FRATXT                             33
     C   33FRAT2K        CHAIN     FRATXT                             33
     C  N33              SETON                                        51
     C                   END
     C*
     C     WSKNM         IFNE      0
     C                   SETOFF                                       33
     C                   MOVE      WSKNM         KUNDNR
     C     FRATXK        CHAIN     FRATXT                             33
     C   33FRAT2K        CHAIN     FRATXT                             33
     C  N33              SETON                                        52
     C                   END
     C*
     C     WSKNN         IFNE      0
     C                   SETOFF                                       33
     C                   MOVE      WSKNN         KUNDNR
     C     FRATXK        CHAIN     FRATXT                             33
     C   33FRAT2K        CHAIN     FRATXT                             33
     C  N33              SETON                                        53
     C                   END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     OPDBL         BEGSR
     C***********************************************
     C     WSMN          IFNE      *BLANKS
     C     BLLOKK        CHAIN     BILLOF                             33
     C  N33              MOVE      WSMN          BLMN
     C  N33              EXCEPT    UPDBL
     C                   END
     C*
     C     BLLOKK        CHAIN     BLLOK                              33
     C                   EXSR      MOVWBL
     C  N33              UPDATE    BLLOKR
     C   33              WRITE     BLLOKR
     C*
     C   33              DO
     C                   SETOFF                                       33
     C     BLV1K         SETLL     BLV1
     C     BOAVD         READE     BLV1                                   33
     C  N33              UPDATE    BLV1R
     C  N33BOOPD         COMP      BVOPD                              3333
     C   33BOAVDK        IFNE      0
     C     BOOPDK        ANDNE     0
     C                   EXSR      KOPIBV
     C                   ELSE
     C                   EXSR      KOPIB2
     C                   END
     C                   END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     KOPIBV        BEGSR
     C***********************************************
     C     BLV1K2        SETLL     BLV1
     C                   SETOFF                                       33
     C     STKBV1        TAG
     C     BOAVDK        READE     BLV1                                   33
     C  N33              UPDATE    BLV1R
     C  N33BOOPDK        COMP      BVOPD                              3333
     C  N33              DO
     C                   MOVE      BEAVD         BVAVD
     C                   MOVE      BEOPD         BVOPD
     C                   WRITE     BLV1R
     C                   GOTO      STKBV1
     C                   END
     C*
     C     BLV2K2        SETLL     BLV2
     C                   SETOFF                                       33
     C     STKBV2        TAG
     C     BOAVDK        READE     BLV2                                   33
     C  N33              UPDATE    BLV2R
     C  N33BOOPDK        COMP      BVOPD                              3333
     C  N33              DO
     C                   MOVE      BEAVD         BVAVD
     C                   MOVE      BEOPD         BVOPD
     C                   WRITE     BLV2R
     C                   GOTO      STKBV2
     C                   END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     KOPIB2        BEGSR
     C***********************************************
     C                   Z-ADD     0             BVLI
     C                   MOVE      BEAVD         BVAVD
     C                   MOVE      BEOPD         BVOPD
     C     1             DO        4             XN                4 0          1<-B
     C     NT(XN)        IFNE      0                                             2<-B
     C     MN(XN)        ORNE      *BLANKS
     C     VS(XN)        ORNE      *BLANKS
     C     VK(XN)        ORNE      0
     C     M3(XN)        ORNE      0
     C                   ADD       1             BVLI
     C                   MOVEL     MN(XN)        BVMN
     C                   MOVE      NT(XN)        BVNT
     C                   MOVE      VK(XN)        BVVKT
     C                   MOVE      M3(XN)        BVM3
     C                   MOVE      KC(XN)        BVKDC
     C                   MOVE      *ZEROS        BVENH
     C     KC(XN)        IFEQ      *BLANKS                                        3<-B
     C                   MOVE      VS(XN)        BVVS
     C                   ELSE                                                     3
     C                   MOVE      KC(XN)        YKDC              2
     C     YKDC          CHAIN     CDCON4                             33
     C   33              MOVE      VS(XN)        BVVS
     C  N33              MOVEL     CC4FT         BVVS
     C  N33              MOVE      CC4NT         BVENH
     C  N33VS(XN)        IFNE      *BLANKS                                         4<-B
     C                   WRITE     BLV1R
     C                   MOVE      *BLANKS       BVMN
     C                   MOVE      VS(XN)        BVVS
     C                   ADD       1             BVLI
     C                   MOVE      *ZEROS        BVNT
     C                   MOVE      *BLANKS       BVKDC
     C                   MOVE      *ZEROS        BVENH
     C                   MOVE      *ZEROS        BVVKT
     C                   MOVE      *ZEROS        BVM3
     C                   END                                                       4<-E
     C                   END                                                      3<-E
     C                   WRITE     BLV1R
     C                   END                                                     2<-E
     C                   END                                                    1<-E
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVBLW        BEGSR
     C***********************************************
     C                   MOVE      BEKNA         WSKNA
     C                   MOVE      BENAA         WSNAA
     C                   MOVE      BEAD1A        WSAD1A
     C                   MOVE      BEAD2A        WSAD2A
     C                   MOVE      BEAD3A        WSAD3A
     C                   MOVE      BEKNM         WSKNM
     C                   MOVE      BENAM         WSNAM
     C                   MOVE      BEAD1M        WSAD1M
     C                   MOVE      BEAD2M        WSAD2M
     C                   MOVE      BEAD3M        WSAD3M
     C                   MOVE      BEKNN         WSKNN
     C                   MOVE      BENAN         WSNAN
     C                   MOVE      BEAD1N        WSAD1N
     C                   MOVE      BEAD2N        WSAD2N
     C                   MOVE      BEAD3N        WSAD3N
     C                   MOVE      BEKDP         WSKDP
     C                   MOVE      BEMN          WSMN
     C                   MOVE      BEBLNR        WSBLNR
     C                   MOVE      BEFT          WSFT
     C                   MOVE      BEKDSA        WSKDSA
     C                   MOVE      BEKDRA        WSKDRA
     C                   MOVE      BEKDTF        WSKDTF
     C                   MOVE      BEKDCP        WSKDCP
     C                   MOVE      BENTBS        WSNTBS
     C                   MOVE      BESD          WSSD
     C                   MOVE      BEDTD         WSDTD
     C                   MOVE      BEDTM         WSDTM
     C                   MOVE      BEDTÅ         WSDTÅ
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVWBL        BEGSR
     C***********************************************
     C                   MOVE      UAVD          BEAVD
     C                   MOVE      UOPD          BEOPD
     C                   MOVE      WSKNA         BEKNA
     C                   MOVE      WSNAA         BENAA
     C                   MOVE      WSAD1A        BEAD1A
     C                   MOVE      WSAD2A        BEAD2A
     C                   MOVE      WSAD3A        BEAD3A
     C                   MOVE      WSKNM         BEKNM
     C                   MOVE      WSNAM         BENAM
     C                   MOVE      WSAD1M        BEAD1M
     C                   MOVE      WSAD2M        BEAD2M
     C                   MOVE      WSAD3M        BEAD3M
     C                   MOVE      WSKNN         BEKNN
     C                   MOVE      WSNAN         BENAN
     C                   MOVE      WSAD1N        BEAD1N
     C                   MOVE      WSAD2N        BEAD2N
     C                   MOVE      WSAD3N        BEAD3N
     C                   MOVE      WSKDP         BEKDP
     C                   MOVE      WSMN          BEMN
     C                   MOVE      WSBLNR        BEBLNR
     C                   MOVE      WSFT          BEFT
     C                   MOVE      WSKDSA        BEKDSA
     C                   MOVE      WSKDRA        BEKDRA
     C                   MOVE      WSKDTF        BEKDTF
     C                   MOVE      WSKDCP        BEKDCP
     C                   MOVE      WSNTBS        BENTBS
     C                   MOVE      WSSD          BESD
     C     WSDT          IFEQ      0
     C                   MOVE      *ZEROS        BEDT
     C                   ELSE
     C                   MOVE      WSDTD         BEDTD
     C                   MOVE      WSDTM         BEDTM
     C     WSDTÅ         IFGT      50
     C     WSDTÅ         ADD       1900          BEDTÅ
     C                   ELSE
     C     WSDTÅ         ADD       2000          BEDTÅ
     C                   END
     C                   END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     VISFRE        BEGSR
     C***********************************************
     C                   Z-ADD     0             UFLAGG
     C                   CALL      'SYFA31CL'
     C                   PARM                    UAVD
     C                   PARM                    UOPD
     C                   PARM                    UFLAGG
     C                   ENDSR
     C*
S01  C***********************************************
S01  C*    SØKKUN        BEGSR
S01  C***********************************************
S01  C*                  CALL      'SOEKUN'
S01  C*                  PARM                    FIFIRM
S01  C*                  PARM                    SOANTK
S01  C*                  PARM                    SOTXT1
S01  C*                  PARM                    WSKNA
S01  C*                  PARM                    SOTXT2
S01  C*                  PARM                    WSKNM
S01  C*                  PARM                    SOTXT3
S01  C*                  PARM                    WSKNN
S01  C*                  PARM                    SOTXT4
S01  C*                  PARM                    SOKNR4
S01  C*                  PARM                    SOTXT5
S01  C*                  PARM                    SOKNR5
S01  C*                  PARM                    SOTXT6
S01  C*                  PARM                    SOKNR6
S01  C*                  PARM                    SOTXT7
S01  C*                  PARM                    SOKNR7
S01  C*                  PARM                    SOTXT8
S01  C*                  PARM                    SOKNR8
S01  C*                  Z-ADD     0             UFLAGG
S01  C*
S01  C*                  ENDSR
S01  C*
     C***********************************************
     C     VISOPL        BEGSR
     C***********************************************
     C     POSNBR        IFGE      6
     C     POSNBR        ANDLE     13
     C   51LINNBR        IFEQ      4
     C                   MOVE      SOTXT1        YSKMOT            8
     C                   MOVE      WSKNA         YSKKNR            8 0
     C                   GOTO      STVISO
     C                   END
     C   52LINNBR        IFEQ      6
     C                   MOVE      SOTXT2        YSKMOT
     C                   MOVE      WSKNM         YSKKNR
     C                   GOTO      STVISO
     C                   END
     C   53LINNBR        IFEQ      8
     C                   MOVE      SOTXT3        YSKMOT
     C                   MOVE      WSKNN         YSKKNR
     C                   GOTO      STVISO
     C                   END
     C                   END
     C*
     C                   MOVE      'DIV3730'     MSGNR
     C                   SETON                                        30
     C                   GOTO      SLVISO
     C*
     C     STVISO        TAG
     C                   MOVE      *ZEROS        UFLAGG
     C                   CALL      'FRITXTSK'
     C                   PARM                    FIFIRM
     C                   PARM                    YSKKNR
     C                   PARM                    YSKMOT
     C                   PARM                    UFLAGG
     C                   PARM                    HEUR01
     C*
     C     SLVISO        ENDSR
     C*
     C***********************************************
     C     SØKDTA        BEGSR
     C***********************************************
     C     LINNBR        IFEQ      4
     C     POSNBR        ANDGE     6
     C     POSNBR        ANDLE     13
S01  C*                  EXSR      SØKKUN
N01  C                   CALL      'SOEKKUN'
N01  C                   PARM                    WSKNA
N01  C                   PARM                    FIFIRM
N01  C                   PARM                    WSNAA
     C                   Z-ADD     10            LINNBR
     C                   Z-ADD     19            POSNBR
     C                   GOTO      SLSØK
     C                   END
     C*
     C     LINNBR        IFEQ      6
     C     POSNBR        ANDGE     6
     C     POSNBR        ANDLE     13
S01  C*                  EXSR      SØKKUN
N01  C                   CALL      'SOEKKUN'
N01  C                   PARM                    WSKNM
N01  C                   PARM                    FIFIRM
N01  C                   PARM                    WSNAM
     C                   Z-ADD     10            LINNBR
     C                   Z-ADD     19            POSNBR
     C                   GOTO      SLSØK
     C                   END
     C*
     C     LINNBR        IFEQ      8
     C     POSNBR        ANDGE     6
     C     POSNBR        ANDLE     13
S01  C*                  EXSR      SØKKUN
N01  C                   CALL      'SOEKKUN'
N01  C                   PARM                    WSKNN
N01  C                   PARM                    FIFIRM
N01  C                   PARM                    WSNAM
     C                   Z-ADD     10            LINNBR
     C                   Z-ADD     19            POSNBR
     C                   GOTO      SLSØK
     C                   END
     C*
     C                   Z-ADD     1             UFLAGG
     C     LINNBR        IFEQ      10
     C     POSNBR        ANDGE     19
     C     POSNBR        ANDLE     23
     C                   CALL      'SYSJ14A'
     C                   PARM                    WSKDP
     C                   Z-ADD     37            POSNBR
     C                   GOTO      SLSØK
     C                   END
     C*
     C     LINNBR        IFEQ      17
     C     POSNBR        ANDGE     51
     C     POSNBR        ANDLE     55
     C                   MOVE      *BLANKS       UNVN
     C                   MOVE      *BLANKS       ULK
     C                   CALL      'SYFI10F'
     C                   PARM                    WSSD
     C                   PARM                    UNVN             30
     C                   PARM                    ULK               2
     C                   Z-ADD     64            POSNBR
     C                   GOTO      SLSØK
     C                   END
     C*
     C     LINNBR        IFEQ      21
     C     POSNBR        ANDGE     76
     C     POSNBR        ANDLE     80
     C                   CALL      'SYSJ62'
     C                   PARM                    UFLAGG
     C                   PARM                    WSOPDL
     C                   GOTO      SLSØK
     C                   END
     C*
     C                   MOVE      'DIV1037'     MSGNR
     C                   SETON                                        30
     C*
     C     SLSØK         ENDSR
     C*
     C***********************************************
     C     TILV          BEGSR
     C***********************************************
     C                   MOVE      WSDTR         UDTR              6 0
     C   11              GOTO      STTV2
     C*
     C     STTV1         TAG
     C                   Z-ADD     0             UFLAGG
     C                   CALL      'SYSJ50E'
     C                   PARM                    UAVD
     C                   PARM                    UOPD
     C                   PARM                    UDTR
     C                   PARM                    BOSG
     C                   PARM                    BOUKE
     C                   PARM                    BOAAR
     C                   PARM                    UFLAGG
     C     UFLAGG        IFEQ      2
     C                   GOTO      STTV2
     C                   ELSE
     C                   GOTO      SLTV
     C                   END
     C*
     C     STTV2         TAG
     C                   Z-ADD     0             UFLAGG
     C                   CALL      'SYSJ50F'
     C                   PARM                    UAVD
     C                   PARM                    UOPD
     C                   PARM                    UDTR
     C                   PARM                    BOSG
     C                   PARM                    BOUKE
     C                   PARM                    BOAAR
     C                   PARM                    UFLAGG
     C     UFLAGG        IFEQ      1
     C                   GOTO      STTV1
     C                   END
     C*
     C     SLTV          ENDSR
     C*
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     BOOKK         KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    UOPD
     C     BLLOKK        KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    UOPD
     C     BLV1K         KLIST
     C                   KFLD                    BOAVD
     C                   KFLD                    BOOPD
     C                   KFLD                    YLI
     C     BLV2K         KLIST
     C                   KFLD                    BOAVD
     C                   KFLD                    BOOPD
     C                   KFLD                    YLI
     C                   KFLD                    YLI2
     C     BLV1K2        KLIST
     C                   KFLD                    BOAVDK
     C                   KFLD                    BOOPDK
     C                   KFLD                    YLI
     C     BLV2K2        KLIST
     C                   KFLD                    BOAVDK
     C                   KFLD                    BOOPDK
     C                   KFLD                    YLI
     C                   KFLD                    YLI2
     C     KUND1K        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    KUNDNR
     C     FRATXK        KLIST
     C                   KFLD                    KUNDNR
     C                   KFLD                    HEUR01            1
     C                   MOVE      'G'           HEUR01            1
     C     FRAT2K        KLIST
     C                   KFLD                    KUNDNR
     C                   KFLD                    HEUR02            1
     C     FREETK        KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    UOPD
     C     KODTPK        KLIST
     C                   KFLD                    YPUNI
     C                   KFLD                    BOAVD
     C                   KFLD                    YPLNR
     C*
     C     *ENTRY        PLIST
     C                   PARM                    UAVD              4 0
     C                   PARM                    UOPD              7 0
     C                   PARM                    UFLAGG            1 0
     C     UFLAGG        IFEQ      2
     C                   MOVE      'J'           KOPBOK
     C                   ELSE
     C                   MOVE      'N'           KOPBOK            1
     C                   END
     C                   Z-ADD     0             UFLAGG
     C*
S01  C*                  Z-ADD     3             SOANTK            1 0
S01  C*                  MOVE      *BLANKS       SOTXT4            8
S01  C*                  MOVE      *BLANKS       SOTXT5            8
S01  C*                  MOVE      *BLANKS       SOTXT6            8
S01  C*                  MOVE      *BLANKS       SOTXT7            8
S01  C*                  MOVE      *BLANKS       SOTXT8            8
S01  C*                  MOVE      *ZEROS        SOKNR4            8 0
S01  C*                  MOVE      *ZEROS        SOKNR5            8 0
S01  C*                  MOVE      *ZEROS        SOKNR6            8 0
S01  C*                  MOVE      *ZEROS        SOKNR7            8 0
S01  C*                  MOVE      *ZEROS        SOKNR8            8 0
     C*
     C                   MOVE      *ZEROS        YLI               3 0
     C                   MOVE      *ZEROS        YLI2              1 0
     C                   MOVE      *ZEROS        YFXLNR            2 0
     C                   MOVE      'P'           YPUNI             1
     C                   MOVE      *ZEROS        WSDT
     C                   MOVE      *ZEROS        BEDT
     C                   MOVE      *ZEROS        WSDTR
     C                   MOVE      *ZEROS        BODTR
     C                   MOVE      *ZEROS        NT
     C                   MOVE      *BLANKS       KC
     C                   MOVE      *BLANKS       MN
     C                   MOVE      *BLANKS       VS
     C                   MOVE      *ZEROS        VK
     C                   MOVE      *ZEROS        M3
     C*
     C                   MOVE      'DIV3110'     XMSGNR            7            MELDING-ID
     C                   Z-ADD     3             XLEVEL            1 0          NIVÅ
     C                   Z-ADD     0             XRETUR            1 0          RETURKODE
     C                   Z-ADD     8             XMSGL             4 0          LENGDE PÅ MELDING
     C                   EXSR      HENTMS
     C                   MOVEL     XMSG          SOTXT1            8
     C                   MOVE      'DIV3159'     XMSGNR
     C                   Z-ADD     3             XLEVEL
     C                   Z-ADD     0             XRETUR
     C                   Z-ADD     8             XMSGL
     C                   EXSR      HENTMS
     C                   MOVEL     XMSG          SOTXT2            8
     C                   MOVE      'DIV3160'     XMSGNR
     C                   Z-ADD     3             XLEVEL
     C                   Z-ADD     0             XRETUR
     C                   Z-ADD     8             XMSGL
     C                   EXSR      HENTMS
     C                   MOVEL     XMSG          SOTXT3            8
     C*
     C                   MOVE      *BLANKS       FIFIRM
     C     FIFIRM        SETLL     FIRM
     C                   SETOFF                                       33
     C                   READ      FIRM                                   33
     C* VERDIER HENTET FRA BOOKING->WS.. KUN VED NY LOKALBL
     C* (BLIR OVERSKREVET AV MOVBLW DERSOM BLLOK FINNES...)
     C     BOOKK         CHAIN     BOOKING                            33
     C     *IN33         IFEQ      '0'
     C                   MOVE      BOCPL         WSKDCP
     C     KOPBOK        IFEQ      'J'                                          SPEDITØR=
     C                   MOVE      BOKNA         WSKNA                          AVSEND..
     C     BOKNS         IFNE      *ZEROS                                       SPEDITØR=
     C                   MOVE      BOKNS         WSKNA                              AVSENDER
     C                   END
     C                   MOVE      BOKNR         WSKNM                          REDERI=MOTT.
     C                   END
     C                   END
     C                   EXSR      KDTFM2
     C     BLLOKK        CHAIN     BLLOK                              33
     C  N33              EXSR      MOVBLW
     C  N33              UPDATE    BLLOKR
N02  C     BLLOKK        CHAIN(N)  BILLOF                             33
N02  C  N33              MOVE      BLUR          HEUR01
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     HENTMS        BEGSR
     C***********************************************
     C                   CALL      'SUBR23R3'
     C                   PARM                    XMSGNR
     C                   PARM                    XMSG             62
     C                   PARM                    XLEVEL
     C                   PARM                    XRETUR
     C                   PARM                    XMSGL
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     KDTFMT        BEGSR
     C***********************************************
     C                   MOVE      WSDTD         UDT1              2 0
     C                   MOVE      WSDTM         UDT2              2 0
     C                   MOVE      WSDTÅ         UDT3              2 0
     C                   CALL      'DATOFMT'
     C                   PARM                    UDT1
     C                   PARM                    UDT2
     C                   PARM                    UDT3
     C                   MOVE      UDT1          WSDTD
     C                   MOVE      UDT2          WSDTM
     C                   MOVE      UDT3          WSDTÅ
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     KDTFM2        BEGSR
     C***********************************************
     C                   MOVE      BODTRD        UDT1              2 0
     C                   MOVE      BODTRM        UDT2              2 0
     C                   MOVE      BODTRÅ        UDT3              2 0
     C                   CALL      'DATOFMT'
     C                   PARM                    UDT1
     C                   PARM                    UDT2
     C                   PARM                    UDT3
     C                   MOVE      UDT1          WSDTRD
     C                   MOVE      UDT2          WSDTRM
     C                   MOVE      UDT3          WSDTRÅ
     C*
     C                   ENDSR
     C*
     OBILLOFR   E            UPDBL
     O                       BLMN
