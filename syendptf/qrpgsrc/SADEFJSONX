     H*debug(*yes)
     H Option( *SrcStmt ) DftActGrp( *No ) BndDir( 'QC2LE' )
      *
     FSADEFL02  IF   E           K DISK                                         Turheader
     FSADEFCL1  IF   E           K DISK                                         House/SAD-X-ref
     FSADH      IF   E           K DISK
     FSADV      IF   E           K DISK
     FFIRM      IF   E           K DISK
     FCUNDL01   IF   E           K DISK
     FKODTA     IF   E           K DISK
     FTARI      IF   E           K DISK
     FEDIM4     IF   E           K DISK
     FECMSG1    IF   E           K DISK
     FEDIC      UF   E             DISK
     FEDIS      O  A E           K DISK
     FEDIS2     IF   E           K DISK    RENAME(EDISR:EDISR2)
     F                                     PREFIX(S2_)
     ‚*  Prototype for 'JSONFIX'
     d jsonfix         pr                  extpgm('JSONFIX')
     d jsonstring_                         like(jsonstring)
      *
     D FILE_o          s               *
     D String          s            512a
     D rc              s             10i 0
     D Data            s             80a   Varying
     D codepage        s              5a   Varying
     D CmdLine         S            512
     D CmdLen          S             15  5
     D LF              c                   x'25'
     D CLESERT         S             30
     D CLPRT           S             30
      *
      * Status = Pre-alert  / On-Arrival / Cancelled
     D Status          s             20
     D JSONstring      s          32000
     D WFILNAM         S            200
     D WFILNAM1        S            200
     D WFILNAM2        S            200
     D WFILNAM3        S            200
     D WFILNAM4        S            200
     D WMAPPE          S            100
     D WMAPPE2         S            100
     D XLIB            S             11
     D WFilback        S            512
     D WFIL            S            512
     D timeData        S               Z                                        Z=Timestamfield
      *
     Dopen             Pr              *   ExtProc( '_C_IFS_fopen' )
     D                                 *   Value Options( *String )
     D                                 *   Value Options( *String )
     Dfputs            Pr            10i 0 ExtProc( '_C_IFS_fputs' )
     D                                 *   Value Options( *String )
     D                                 *   Value
     Dclose            Pr            10i 0 ExtProc( '_C_IFS_fclose' )
     D                                 *   Value
     D RunCmd          PR                  EXTPGM('QCMDEXC')
     D  Cmd                         200A   CONST
     D  Len                          15P 5 CONST
      *   EDIDTA data area
     D EDIDTA          DS           256
     D  UELIBF               171    180
     D                 DS
     D  EFETA                  1      8  0
     D  EFETAA                 1      4
     D  EFETAM                 5      6
     D  EFETAD                 7      8
     D                 DS
     D  EFETM                  1      6  0
     D  EFETMH                 1      2
     D  EFETMM                 3      4
     D  EFETMS                 5      6
     D                 DS
     D  EFSJADT                1      8  0
     D  EFSJADTA               1      4
     D  EFSJADTM               5      6
     D  EFSJADTD               7      8
     D                 DS
     D  M0068A                 1      8  0
     D  M0068AA                1      4
     D  M0068AM                5      6
     D  M0068AD                7      8
      *
     c                   exsr      init
      *
      * tEMPORÆRE HARDKODEDE VERDIER
      *
     c                   exsr      LagFil
      *
     c                   exsr      OpdEDIS
      *
     c                   seton                                        lr
     c                   return
      *
      ***************************************************************
     C     LagFil        BEGSR
      ***************************************************************
     C                   time                    Timedata
     C                   movel     timedata      TS               26            må være 26 lang
     c
      *
      * Lag fil og set codepage (pc)
      *
     C                   EVAL      WFil = EFUUID                                1. GANG SENDING
     C                   MOVE      EFPRO         XAN08             8
     C                   EVAL      S0020 = 'EF' + XAN08
     C     S0020         SETLL     EDIS2
     C     S0020         READE     EDIS2                                  33
     C                   DOW       *IN33 = *OFF
     C                   IF        S2_SSR = 'S' AND S2_S0026 = 'SADEFJSON'
     C                             AND S2_SST = 'C'
     C                   EVAL      WFil = 'U_' + EFUUID                         UPDAET
     C                   LEAVE
     C                   END
     C     S0020         READE     EDIS2                                  33
     C                   ENDDO
      *
     C                   EVAL      XLIB = '/' + UELIBF
      * ByggFlr kan gjerne flyttes til annet prog. men det er gjort på millisekunder..
     c                   exsr      ByggFlr
     C                   EVAL      WFilNam  = %TRIM(XLIB) + '/EXPRESSMANIFEST/' Fil i backup
     C                             + 'json_Ut/Backup/' + %TRIM(TS)
     C                             + %TRIM(Wfil) + '.json'
     C                   EVAL      WFilNam1 = %TRIM(XLIB) + '/EXPRESSMANIFEST/' Fil i temp med stemp
     C                             + 'json_Ut/Temp/' + %TRIM(TS)
     C                             + %TRIM(Wfil) + '.json'
     C                   EVAL      WFilNam2 = %TRIM(XLIB) + '/EXPRESSMANIFEST/' Fil i temp uten stem
     C                             + 'json_Ut/Temp/'
     C                             + %TRIM(Wfil) + '.json'
     C                   EVAL      WFilNam3 = %TRIM(Wfil) + '.json'             Filnavn uten stempel
     C                   EVAL      WFilNam4 = %TRIM(XLIB) + '/EXPRESSMANIFEST/' Sjekk tidligere ver.
     C                             + 'json_Ut/'
     C                             + %TRIM(Wfil) + '.json'
     C                   EVAL      WMAPPE   = %TRIM(XLIB) + '/EXPRESSMANIFEST/'
     C                             + 'json_Ut/Temp'
     C                   EVAL      WMAPPE2  = %TRIM(XLIB) + '/EXPRESSMANIFEST/'
     C                             + 'json_Ut'
     C                   EVAL      FILE_o = open( %TrimR( WFilNam )
     C                             : 'w, codepage=865' )
     C                   EVAL      rc = close( FILE_o )
     C                   EVAL      FILE_o = open( %TrimR( WFilNam )
     C                             : 'w' )
      *
     c                   exsr      LagJson
      *
     C                   EVAL      rc = close( FILE_o )
      *
     C                   CALL      'SADEFJSONC'
     C                   PARM                    WFILNAM
     C                   PARM                    WFILNAM1
     C                   PARM                    WFILNAM2
     C                   PARM                    WFILNAM3
     C                   PARM                    WFILNAM4
     C                   PARM                    WMAPPE
     C                   PARM                    WMAPPE2
      * Kopiert til endelig mappe ..
     C                   ENDSR
      *
      /free
        //*********************
        Begsr LagJson;
        //*********************

        // Start JSON
        jsonstring ='{';
        rc = fputs(%trimr(jsonstring) + LF: File_o);

        // Message / UUID
        jsonstring ='¬manifestId¬:¬'+%trim(EFUUID)+'¬,';
        jsonfix ( jsonstring );
        rc = fputs(%trimr(jsonstring) + LF: File_o);

        // Hodeinforasjon (Mandatory)
        exsr HODEINFO;

        // CargoLines
        exsr CargoLines;

        // Bunninforasjon (Mandatory)
        exsr BunnINFO;

        jsonstring = '}';                                    // slutt Hele meldingen
        rc = fputs(%trimr(jsonstring) + LF: File_o);

        endsr;

        //*********************
        Begsr CargoLines;
        //*********************

        clprt = 'IMMEDIATE_RELEASE_IMPORT';   // Midlertidig setting
        setll efpro sadefcl1;
        reade efpro sadefcl1;
        dow not %eof;
          exsr cargoline;
          reade efpro sadefcl1;
        enddo;

        endsr;

        //*********************
        Begsr CargoLine;
        //*********************

        // Hent deklarantnr (org.nr)
        chain (koauni: clavd) kodta;
        chain (fifirm: koaknr) cundl01;
        // Hent kolli, vekt
        chain (clavd: cltdn) sadh;
        // Hent varebeskrivelse fra første varelinje
        chain (clavd: cltdn) sadv;
        if svvt01 = *blanks;
          chain svvnt tari;
        // Hent varebeskrivelse fra tariff-register
          if %found;
            svvt01 = taalfa;
          endif;
        endif;
        // Hent dato og sekvensnr
        setll ('R': clavd: cltdn) edim4;
        reade ('R': clavd: cltdn) edim4;
        dow not %eof;
          if m1001 = '929' and m1n07 = 'DTK';
            leave;
          endif;
          reade ('R': clavd: cltdn) edim4;
        enddo;
        // Sett tekst om sertifisert
        if cleser = 'J';
          clesert = 'true';
        else;
          clesert = 'false';
        endif;

        // CargoLines start
        jsonstring = '¬cargolines¬:[ {';
        jsonfix ( jsonstring );
        rc = fputs(%trimr(jsonstring) + LF: File_o);

        jsonstring =
           '¬typeOfImportProcedure¬:¬'+%trim(CLPRT)+'¬,'
           +'¬numberOfPackages¬:¬'+%trim(%editc(SINTK:'Z'))+'¬,'
           +'¬descriptionOfGoods¬:¬'+%trim(SVVT01)+'¬,'
           +'¬grossMass¬:¬'+%trim(%editc(SIVKB:'Z'))+'¬,';
        jsonfix ( jsonstring );
        rc = fputs(%trimr(jsonstring) + LF: File_o);

        jsonstring =
           '¬importProcedure¬: { '
           +'¬sequenceNumber¬:¬'+%trim(%editc(M0068B:'Z'))+'¬,'
           +'¬declarantNumber¬:¬'+%trim(SYRG)+'¬,'
           +'¬declarationDate¬:¬'+%trim(M0068AD)
           +%trim(M0068AM)+%trim(M0068AA)+'¬'
           +'},';
        jsonfix ( jsonstring );
        rc = fputs(%trimr(jsonstring) + LF: File_o);

        jsonstring =
           '¬exports¬: [ { '
           +'¬typeOfExport¬:¬'+%trim(CLETYPT)+'¬,'
           +'¬exportId¬:¬'+%trim(CLEID)+'¬,'
           +'¬exportCertificate¬: '+%trim(CLESERT)+' } ]';
        jsonfix ( jsonstring );
        rc = fputs(%trimr(jsonstring) + LF: File_o);

        jsonstring = '} ],';                                 // slutt et CargoLines
        rc = fputs(%trimr(jsonstring) + LF: File_o);

        endsr;

        //*********************
        Begsr HODEINFO;
        //*********************

        jsonstring =
           '¬transportationCompanyId¬:¬'+%trim(EFRGD)+'¬,';
        jsonfix ( jsonstring );
        rc = fputs(%trimr(jsonstring) + LF: File_o);

        jsonstring =
              '¬status¬:¬'+'SUBMITTED'+'¬,';
        jsonfix ( jsonstring );
        rc = fputs(%trimr(jsonstring) + LF: File_o);

        jsonstring =
              '¬placeOfEntryCode¬:¬'+%trim(%editc(EFTSD:'Z'))+'¬,';
        jsonfix ( jsonstring );
        rc = fputs(%trimr(jsonstring) + LF: File_o);

        jsonstring =
              '¬estimatedTimeOfArrival¬:¬'
              +%trim(EFETAA)+'-'+%trim(EFETAM)+'-'+%trim(EFETAD)+'T'
              +%trim(EFETMH)+':'+%trim(EFETMM)+':'+%trim(EFETMS)+'.000Z¬,';
        jsonfix ( jsonstring );
        rc = fputs(%trimr(jsonstring) + LF: File_o);

        jsonstring =
              '¬modeOfTransportCode¬:¬'+%trim(EFTMT)+'¬,';
        jsonfix ( jsonstring );
        rc = fputs(%trimr(jsonstring) + LF: File_o);

        endsr;

        //*********************
        Begsr BUNNINFO;
        //*********************

        jsonstring = '¬activeMeansOfTransport¬:{';
        jsonfix ( jsonstring );
        rc = fputs(%trimr(jsonstring) + LF: File_o);

        jsonstring =
           '¬typeOfMeansOfTransport¬:¬'+%trim(EFKTYPT)+'¬,';
        jsonfix ( jsonstring );
        rc = fputs(%trimr(jsonstring) + LF: File_o);

        jsonstring =
              '¬identificationCode¬:¬'+%trim(EFKMRK)+'¬,';
        jsonfix ( jsonstring );
        rc = fputs(%trimr(jsonstring) + LF: File_o);

        jsonstring =
              '¬countryCode¬:¬'+%trim(EFKLK)+'¬,';
        jsonfix ( jsonstring );
        rc = fputs(%trimr(jsonstring) + LF: File_o);

        jsonstring =
              '¬driver¬: { '
              +'¬firstName¬:¬'+%trim(EFSJAF)+'¬,'
              +'¬lastName¬:¬'+%trim(EFSJAE)+'¬,'
              +'¬countryCode¬:¬'+%trim(EFSJALK)+'¬,'
              +'¬dateOfBirth¬:¬'+%trim(EFSJADTA)+'-'
              +%trim(EFSJADTM)+'-'+%trim(EFSJADTD)+'¬,'
              +'¬sendReleasedConfirmation¬:false'+','
              +'¬sendReleasedConfirmationEmail¬:null'
              +'},';
        jsonfix ( jsonstring );
        rc = fputs(%trimr(jsonstring) + LF: File_o);

        jsonstring =
              '¬passiveMeansOfTransports¬:[ { '
              +'¬identificationCode¬:¬'+%trim(EFPMRK)+'¬,'
              +'¬countryCode¬:¬'+%trim(EFPLK)+'¬'
              +'} ]';
        jsonfix ( jsonstring );
        rc = fputs(%trimr(jsonstring) + LF: File_o);

        jsonstring = '}';
        rc = fputs(%trimr(jsonstring) + LF: File_o);

        endsr;
      /end-free

      ***************************************************************
     C     OpdEDIS       BEGSR
      ***************************************************************
     C     ECMSG1K       CHAIN     ECMSG1                             32
     C     1             CHAIN     EDIC                               32
     C                   ADD       1             CSN
     C                   UPDATE    EDICR
     C                   eval      s0004 = ECIIDI
     C                   eval      s0010 = ECIIDC
     C                   eval      s0020 = 'EF' + %TRIM(UPRO)
     C                   eval      s0026 = 'SADEFJSON'
     C                   eval      ssn   = CSN
     C                   eval      ssr   = 'S'
     C                   eval      sst   = 'B'
     C                   move      wdt           sdt
     C                   move      wtm           stm
     C                   eval      slib  = '*LIB'
     C                   eval      sfile = '*IFS'
     C                   eval      smbr  = *BLANKS
     C                   eval      sifs  = WFilNam
     C                   write     edisr
      *
     C                   ENDSR
      *
      ***************************************************************
     C     init          BEGSR
      ***************************************************************
     c     *entry        plist
     c                   parm                    UPRO              8
     c                   move      Upro          EFPRO
      *
      * hent turheader
     c     EFPRO         chain     SADEFL02
      *
      *
      * hent  consingment / xref   - MINST 1 må finnes
     c     EFPRO         chain     SADEFCL1
     c                   if        not %found
     c                   eval      *INLR = *on
     c                   return
     c                   endif
      * hent  sad header-data
      *
     C     ECMSG1K       KLIST
     C                   KFLD                    ECSR
     C                   KFLD                    EC0065
     C                   EVAL      ECSR   = 'S'
     C                   EVAL      EC0065 = 'EXPREM'
      *
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
      *
      * Åpne dataarea
     C     *DTAARA       DEFINE                  EDIDTA
     C                   IN        EDIDTA
      *
     C     *LOVAL        SETLL     FIRM
     C                   READ      FIRM                                   33
     C                   EVAL      KOAUNI = 'A'
      *
     c                   endsr
      *
      **********************************************
     C     ByggFlr       begsr
      **********************************************
     C                   EVAL      Fldr = %TRIM(XLIB) + '/EXPRESSMANIFEST/'
     c                   call      'ARC040C1'
     c                   parm                    Fldr             60
      *
     C                   EVAL      Fldr = %TRIM(XLIB) + '/EXPRESSMANIFEST/'
     C                                      +'json_Ut'
     c                   call      'ARC040C1'
     c                   parm                    Fldr             60
      *
     C                   EVAL      Fldr = %TRIM(XLIB) + '/EXPRESSMANIFEST/'
     C                                      +'json_Ut/Temp'
     c                   call      'ARC040C1'
     c                   parm                    Fldr             60
      *
     C                   EVAL      Fldr = %TRIM(XLIB) + '/EXPRESSMANIFEST/'
     C                                      +'json_Ut/Backup'
     c                   call      'ARC040C1'
     c                   parm                    Fldr             60
      * for Oscar/Java-bruk
     C                   EVAL      Fldr = %TRIM(XLIB) + '/EXPRESSMANIFEST/'
     C                                      +'json_Ut/error'
     c                   call      'ARC040C1'
     c                   parm                    Fldr             60
     C                   EVAL      Fldr = %TRIM(XLIB) + '/EXPRESSMANIFEST/'
     C                                      +'json_Ut/sent'
     c                   call      'ARC040C1'
     c                   parm                    Fldr             60
      * BAT = Batch / for Java sende motoren
     C                   EVAL      Fldr = %TRIM(XLIB) + '/EXPRESSMANIFEST/'
     C                                      +'BAT'
     c                   call      'ARC040C1'
     c                   parm                    Fldr             60
      *  automatistere også denne (lage egen CL ?)
      *  også kopiere  /apache/express-client/upload-engine-expressmanif-client.jar
      *           til  .../BAT/upload-engine-expressmanif-client.jar
      *
     c                   endsr
      *
