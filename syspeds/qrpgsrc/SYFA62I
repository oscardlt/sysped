      *
     F*****************************************************************
     F* MERKER I-LINJER MED EN VISS PERIODE, EVT KUN FRAM TIL DATO.. **
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 JOV PTF10 Linjer som er spesifikt styrt mot oppdrag slipper gjennom uferdig dup/snu
********************************************************************
     H DATEDIT(*DMY)
     F*****************************************************************
     FFAK5      UF   E           K DISK
     F                                     RENAME(FAKTR:FAKTR5)
     FHEADF     IF   E           K DISK
     FDISP      IF   E           K DISK
     FTURER     IF   E           K DISK
     FKODTV     IF   E           K DISK
     FSYFA62FI  CF   E             WORKSTN USROPN
N01  D                 DS
N01  D  FADOCN                 1     11
N01  D  FADOCN5_11             5     11
     D                 DS
     D  FAAVD                  1      4  0
     D  FAOPD                  5     11  0
     D  FAAVOP                 1     11  0
     D                 DS
     D  WSAARH                 1      2  0
     D  WSAAR                  3      4  0
     D  WSMND                  5      6  0
     D  WSDAG                  7      8  0
     D  WSDAT                  1      8  0
     D                 DS
     D  TURACC                 1      2  0
     D  TURAAR                 3      4  0
     D  TURMND                 5      6  0
     D  FILDAG                 7      8  0
     D  FILDAT                 1      8  0
     D                 DS
     D  HEDTP                  1      6  0
     D  HEDTCP                 1      2  0
     D  HEDTÅP                 3      4  0
     D  HEDTMP                 5      6  0
     C*--------------------------------------------------------------
     C* DEFINERER NØKKEL TIL FAKTURAREGISTER: FAK3
     C*--------------------------------------------------------------
     C     *LIKE         DEFINE    FAOPKO        XXOPKO
     C     *LIKE         DEFINE    FAFAKT        XXFAKT
     C     *LIKE         DEFINE    FAKUNR        XXKUNR
     C     *LIKE         DEFINE    FAAVD         XXAVD
     C     *LIKE         DEFINE    FAOPD         XXOPD
     C*
     C     KEYFA3        KLIST
     C                   KFLD                    XXOPKO
     C                   KFLD                    XXFAKT
     C                   KFLD                    XXKUNR
     C                   KFLD                    XXAVD
     C                   KFLD                    XXOPD
     C*--------------------------------------------------------------
     C* DEFINERER NØKKEL TIL FAKTURAREGISTER: FAK5
     C*--------------------------------------------------------------
     C     *LIKE         DEFINE    FAOPKO        YYOPKO
     C     *LIKE         DEFINE    FASK          YYSK
      *
     C     KEYFA5        KLIST
     C                   KFLD                    YYOPKO
     C                   KFLD                    YYSK
      *
     C     KEYHEA        KLIST
     C                   KFLD                    FAAVD
     C                   KFLD                    FAOPD
     C     TURKEY        KLIST
     C                   KFLD                    DITUAV
     C                   KFLD                    DITUR
     C     KODVKE        KLIST
     C                   KFLD                    KOVUNI
     C                   KFLD                    FAAVD
      *
      *
     c     *entry        plist
     c                   parm                    online            1
     c*
     C                   MOVEL     'V'           KOVUNI
      *
     C                   MOVE      *ZEROS        FILDAT
     C                   MOVE      *ZEROS        WSDAT
     C                   MOVE      *ZEROS        FAAVOP
      *
     C     online        ifeq      'J'
     C                   open      syfa62fi
      *
      * LESER INN PERIODEBEGREP FOR MERKING
     C                   EXFMT     BILDE01
     C                   close     syfa62fi
     C   03              GOTO      SLUTT
      *
     C                   else
     C                   move      uyear         wsaar
     C                   move      umonth        wsmnd
     C                   move      uday          wsdag
     C                   end
      *
     C     WSAAR         IFGT      50
     C                   MOVE      19            WSAARH
     C                   ELSE
     C                   MOVE      20            WSAARH
     C                   END
      *
      * MERKER INTERNDEBITERINGER
      *
     C                   MOVE      ' '           YYOPKO
     C                   MOVE      'I'           YYSK
     C     KEYFA5        SETLL     FAK5
     C     STARTI        TAG
     C     KEYFA5        READE     FAK5                                   33
     C   33              GOTO      SLUTT
      *
      * Straks de har beløp ulik 0 regnes de som klare
     C     FABELN        IFEQ      *ZEROS
     C                   UPDATE    FAKTR5
     C                   GOTO      STARTI
     C                   END
      *
     C     KEYHEA        CHAIN     HEADF                              44
     C   44              UPDATE    FAKTR5
     C   44              GOTO      STARTI
      * Kombinasjon under betyr sendt via oppdr.interface men ennå ikke pl.inn
      * (Men hvis fadocn5_11 ulik blank så er den direkte styrt til et oppdrag - OK)
     c     Travd2        IFne      *zeros
     c     Tropd2        andeq     *zeros
     c     Tropd0        andeq     *zeros
N01  c     FADOCN5_11    andeq     *blanks
     C                   UPDATE    FAKTR5
     C                   GOTO      STARTI
     c                   end
     C     HEPRO         COMP      *ZEROS                                 44
     C     HEUR          IFEQ      'H'
     C  N44KEYHEA        CHAIN     DISP                               44
     C  N44TURKEY        CHAIN     TURER                              44
     C                   ENDIF
      *
      * 44 ON = IKKE PÅ TUR    ...  TILLATT??
     C     *IN44         IFEQ      '1'
     C     KODVKE        CHAIN     KODTV                              45
     C     KOVPRO        IFEQ      'J'
     C                   UPDATE    FAKTR5
     C                   GOTO      STARTI
     C                   ELSE
      * ER IKKE PÅ TUR, AVDELINGEN GODTAR DET....
     C                   MOVE      HELKA         TULK
     C     HEUR          IFEQ      'H'
     C                   MOVE      TRSDFD        TUDT
     C                   MOVE      TRSDTD        TUDTT
     C                   ELSE
     C                   MOVE      HEDTOP        TUDT
     C                   MOVE      HEDTOP        TUDTT
     C                   END
      *
      * ALLE INNEN EN MÅNED... (SJEKKER DA KUN MOT TURENS PERIODE)
      *
      * NÅR IKKE PÅ TUR:"LURER DA SYSTEMET" MED OPD-DATO I TUR-PERI-DS
      * (BRUKER REGNSKAPSPERIODE VED STATUS 'O')
     C     WSDAG         IFEQ      *ZEROS
     C     HEST          IFEQ      'O'
     C     HEUR          IFNE      'H'
     C     HEDTCP        IFNE      *ZEROS
     C     HEDTÅP        ANDNE     *ZEROS
     C     HEDTMP        ANDNE     *ZEROS
     C                   MOVEL     HEDTP         TUDT
     C                   MOVEL     HEDTP         TUDTT
     C                   ENDIF
     C                   ENDIF
     C                   ENDIF
      *    eksport eller innland - avgangs-dato
     C     TULK          IFEQ      'NO'                                         fra land
     C     TUDT          IFNE      *ZEROS
     C                   MOVE      TUDT          FILDAT                         fra dato
     C                   END
     C                   ELSE
      *    import ankomst-dato
     C     TUDTT         IFNE      *ZEROS
     C                   MOVE      TUDTT         FILDAT                         fra dato
     C                   END
     C                   END
     C                   END
     C                   END
     C                   END
      *
      *
      * DATO-SJEKK, ENTEN ALT INNEN PERIODEN...
      *    (SJEKKER DA KUN MOT TURENS PERIODE)
     C     WSDAG         IFEQ      *ZEROS
     C                   MOVE      *ZEROS        FILDAG
     C     FILDAT        IFGT      WSDAT
     C                   UPDATE    FAKTR5
     C                   GOTO      STARTI
     C                   END
     C                   ELSE
      * ELLER SJEKK FULL DATO MOT TURENS (ELLER FRA OPPDRAG).
      *    eksport eller innland - avgangs-dato
     C     TULK          IFEQ      'NO'                                         fra land
     C     TUDT          IFNE      *ZEROS
     C                   MOVE      TUDT          FILDAT                         fra dato
     C                   else
     C                   MOVE      *ZEROS        FILDAG
     C                   end
     C                   ELSE
      *    import ankomst-dato
     C     TUDTT         IFNE      *ZEROS
     C                   MOVE      TUDTT         FILDAT                         fra dato
     C                   else
     C                   MOVE      *ZEROS        FILDAG
     C                   end
     C                   END
      *
     C     FILDAT        IFGT      WSDAT
     C*    FILDAT        OREQ      *ZEROS
     C                   UPDATE    FAKTR5
     C                   GOTO      STARTI
     C                   END
      *
     C                   END
      *
      *
      * KOMET GJENNOM HIT.... ALT OK
      * bruk TURENS periode
     C                   MOVE      TURACC        FACC
     C                   MOVE      TURAAR        FAÅR
     C                   MOVE      TURMND        FAMND
     C                   MOVE      'G'           FAOPKO
     C                   UPDATE    FAKTR5
      * NESTE
     C                   GOTO      STARTI
      *
      *
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
