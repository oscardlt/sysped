********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
12XXX * 01 YBC PTF12 Skrive ut forhåndsvarsel
********************************************************************
      *****************************************************************
      *** PROGRAM SKAL KONVERTERE INNGÅENDE IE28/IE04               ***
      *** MRN Allocated/Amendment Acceptance                        ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER          ***
      *** LEDIGE INDIKATORER 01-26                                  ***
      *** LEDIGE INDIKATORER 28-32 34-97                            ***
      *****************************************************************
     H DFTACTGRP(*NO)
     FXMLRCVF   IF   E           K DISK
     FTRACKF    O  A E           K DISK
     FFRISOK    IF A E           K DISK
     FKODFRI    IF   E           K DISK
     FHEADF     IF   E           K DISK
     FFIRM      IF   E           K DISK
     FNCTSEH    UF   E           K DISK
     FNCTSEH05  IF   E           K DISK    RENAME(NCTSEHR:NCTSEHR05)
     FNCTSEC    IF   E           K DISK
     FNCTSERR   UF   E           K DISK
     FTVINF     O  A E             DISK
     FEDIC      UF   E             DISK
     FEDIM      O  A E             DISK
     FEDIM2     IF   E           K DISK    RENAME(EDIMR:EDIMR2)
     FEDIM5     IF   E           K DISK    RENAME(EDIMR:EDIMR5)
     FEDIS4     IF   E           K DISK    RENAME(EDISR:EDISR4)
     D                sds
     D ZUSER                 254    263
      *  Prototype for 'SYGE15C'
     d syge15c         pr                  extpgm('SYGE15C')
     d wdt_                                like(wdt)
     d wtm_                                like(wtm)
N01   *  Prototype for 'NCTS108R'
N01  d ncts108r        pr                  extpgm('NCTS108R')
N01  d uavd_                               like(uavd)
N01  d utdn_                               like(utdn)
N01  d utrnr_                              like(thtrnr)
N01  d utype_                              like(utype)
      *  Prototype for 'ncts104c3'
     d ncts104c3       pr                  extpgm('NCTS104C3')
     d usn_29                              like(usn_29)
      *  Prototype for 'ncts104c6'
     d ncts104c6       pr                  extpgm('NCTS104C6')
     d usn_55                              like(usn_55)
      *  Prototype for 'ncts104c8'
     d ncts104c8       pr                  extpgm('NCTS104C8')
     d usn_60                              like(usn_60)
      *  Prototype for ncts104r1
     d ncts104r1       pr
     d usn_                                like(usn)
      *  *ENTRY Interface for Main Procedure
     d ncts104r1       pi
     d usn                            9
      *----------------------------------------------------------------------
      * Stand Alone Fields - TOP
      *----------------------------------------------------------------------
     d x1_xrlev1       s                   like(xrlev1)
     d x1_xrlev2       s                   like(xrlev2)
     d x2_xrlev1       s                   like(xrlev1)
     d x2_xrlev2       s                   like(xrlev2)
     d x2_xrlev3       s                   like(xrlev3)
     d x3_xrlev1       s                   like(xrlev1)
     d x3_xrlev2       s                   like(xrlev2)
     d x3_xrlev3       s                   like(xrlev3)
     d x3_xrlev4       s                   like(xrlev4)
     d x_thtrdt        s              8
     d x_thtrnr        s                   like(thtrnr)
     d x_tinh          s                   like(thtinh)
     d x_nah           s                   like(thnah)
     d x_ad1h          s                   like(thad1h)
     d x_pnh           s                   like(thpnh)
     d x_psh           s                   like(thpsh)
     d x_lkh           s                   like(thlkh)
     d x_IE28          s              1a
     d x_ref           s              8a
     d x_TIR           s             17a
     d x_IE04          s              1a
N01  d utype           s              1
N01  D                 ds
N01  D thavd                   1      4  0
N01  D uavd                    1      4
N01  D                 ds
N01  D thtdn                   1      7  0
N01  D utdn                    1      7
     D                 ds
     D usn_29                  1      9
     D usn_29_n                1      9  0
     D                 ds
     D usn_55                  1      9
     D usn_55_n                1      9  0
     D                 ds
     D usn_60                  1      9
     D usn_60_n                1      9  0
     D                 ds
     D x_aadt                  1      8  0
     D x_aadt_a                1      8
     D                 ds
     D x_aatm                  1      6  0
     D x_aatm_a                1      6
     D                 ds
     D x_asdt                  1      8  0
     D x_asdt_a                1      8
     D                 ds
     D x_astm                  1      6  0
     D x_astm_a                1      6
      *
     d wdt             s              8
     d wtm             s              6
     D UP              C                   CONST('ABCDEFGHIJKLMNOPQRST-
     D                                     UVWXYZÆØÅ')
     D LO              C                   CONST('abcdefghijklmnopqrst-
     D                                     uvwxyzæøå')
      *----------------------------------------------------------------------
     IEDIMR2
     I              M0004                       X0004
     I              M0010                       X0010
     I              M0035                       X0035
     I              M0062                       X0062
     I              M0065                       X0065
     I              M0068                       X0068
     I              M0068A                      X0068A
     I              M0068B                      X0068B
     I              M0068C                      X0068C
     I              M0068D                      X0068D
     I              M0068E                      X0068E
     I              M0068F                      X0068F
     I              M1001                       X1001
     I              M1004                       X1004
     I              M1225                       X1225
     I              M2005B                      X2005B
     I              M3039D                      X3039D
     I              M3039E                      X3039E
     I              M5004D                      X5004D
     I              M1N07                       X1N07
     I              M1N08                       X1N08
     I              M9N01                       X9N01
     I              MSN                         XSN
     I              MMN                         XMN
     I              MSR                         XSR
     I              MST                         XST
     I              MDT                         XDT
     I              MTM                         XTM
     I              MVEN                        XVEN
     I              MAVD                        XAVD
     I              MTDN                        XTDN
     IEDIMR5
     I              M0004                       X0004
     I              M0010                       X0010
     I              M0035                       X0035
     I              M0062                       X0062
     I              M0065                       X0065
     I              M0068                       X0068
     I              M0068A                      X0068A
     I              M0068B                      X0068B
     I              M0068C                      X0068C
     I              M0068D                      X0068D
     I              M0068E                      X0068E
     I              M0068F                      X0068F
     I              M1001                       X1001
     I              M1004                       X1004
     I              M1225                       X1225
     I              M2005B                      X2005B
     I              M3039D                      X3039D
     I              M3039E                      X3039E
     I              M5004D                      X5004D
     I              M1N07                       X1N07
     I              M1N08                       X1N08
     I              M9N01                       X9N01
     I              MSN                         XSN
     I              MMN                         XMN
     I              MSR                         XSR
     I              MST                         XST
     I              MDT                         XDT
     I              MTM                         XTM
     I              MVEN                        XVEN
     I              MAVD                        XAVD
     I              MTDN                        XTDN
      /free

       exsr init;

       exsr srxml;

       *inlr = *on;
       return;

       //**********************************************
       begsr srxml;
       //**********************************************

         x1_xrlev1 = 0;
         x1_xrlev2 = 0;

         chain (xrsn:x1_xrlev1:x1_xrlev2) xmlrcvf;
         dow %found;
           select;
           when xrnv = 'messageSender';
             m0004  = xrverd;
           when xrnv = 'messageRecipient';
             m0010  = xrverd;
           when xrnv = 'preparationDateAndTime';
           when xrnv = 'messageIdentification';
             m0062  = xrverd;
             m1004  = xrverd;
           when xrnv = 'messageType' and xrverd = 'CC028C';
             x_ie28 = 'J';
             chain 1 edic;
             cmn = cmn + 1;
             update edicr;
             fmn = cmn;
             mmn = cmn;
           when xrnv = 'messageType' and xrverd = 'CC004C';
             x_ie04 = 'J';
             chain 1 edic;
             cmn = cmn + 1;
             update edicr;
             fmn = cmn;
             mmn = cmn;
           when xrnv = 'correlationIdentifier';
           when xrnv = 'TransitOperation';
             exsr srxml1;
           when xrnv = 'CustomsOfficeOfDeparture';
             exsr srxml2;
           when xrnv = 'HolderOfTheTransitProcedure';
             exsr srxml3;
           endsl;

           x1_xrlev1 = x1_xrlev1 + 1;
           chain (xrsn:x1_xrlev1:x1_xrlev2) xmlrcvf;
         enddo;

         if x_ie04 = 'J' and f0077 <> *blanks;
           write tvinff;
         endif;

         if x_ie28 = 'J' or x_ie04 = 'J';
           exsr opdopd;
           exsr ttsr;
           // Sjekk om IE029, IE055, IE060 kommer før IE028, i så fall kall pgm for konvertering
           chain (' ': x_thtrnr) nctserr;
           if %found;
             select;
             when temf = '029';
               usn_29_n = tesn;
               ncts104c3 (usn_29);
               testat = 'O';
               update nctserrr;
             when temf = '055';
               usn_55_n = tesn;
               ncts104c6 (usn_55);
               testat = 'O';
               update nctserrr;
             when temf = '060';
               usn_60_n = tesn;
               ncts104c8 (usn_60);
               testat = 'O';
               update nctserrr;
             endsl;
           endif;
         endif;

         endsr;

       //**********************************************
       begsr srxml1;
       //**********************************************

         x2_xrlev1 = xrlev1;
         x2_xrlev2 = 1;
         x2_xrlev3 = 0;

         chain (xrsn:x2_xrlev1:x2_xrlev2:x2_xrlev3) xmlrcvf;
         dow %found;
           select;
           when xrnv = 'LRN';
             x0068 = xrverd;
           when xrnv = 'MRN';
             x_thtrnr = xrverd;
           when xrnv = 'declarationAcceptanceDate';
             x_thtrdt = %subst(xrverd:1:4) + %subst(xrverd:6:2)
                   + %subst(xrverd:9:2);
           when xrnv = 'amendmentSubmissionDateAndTime';
             x_asdt_a = %subst(xrverd:1:4) + %subst(xrverd:6:2)
                       + %subst(xrverd:9:2);
             x_astm_a = %subst(xrverd:12:2) + %subst(xrverd:15:2)
                       + %subst(xrverd:18:2);
             fln    = 1;
             f0077  = '004';
             f0078a = 'amendmentSubmissionDateAndTime: ' + %trim(xrverd);
           when xrnv = 'amendmentAcceptanceDateAndTime';
             x_aadt_a = %subst(xrverd:1:4) + %subst(xrverd:6:2)
                       + %subst(xrverd:9:2);
             x_aatm_a = %subst(xrverd:12:2) + %subst(xrverd:15:2)
                       + %subst(xrverd:18:2);
             fln    = 1;
             f0077  = '004';
             f0078b = 'amendmentAcceptanceDateAndTime: ' + %trim(xrverd);
           endsl;

           x2_xrlev2 = x2_xrlev2 + 1;
           chain (xrsn:x2_xrlev1:x2_xrlev2:x2_xrlev3) xmlrcvf;
         enddo;

       endsr;

       //**********************************************
       begsr srxml2;
       //**********************************************

         x2_xrlev1 = xrlev1;
         x2_xrlev2 = 1;
         x2_xrlev3 = 0;

         chain (xrsn:x2_xrlev1:x2_xrlev2:x2_xrlev3) xmlrcvf;
         dow %found;
           select;
           when xrnv = 'referenceNumber';
             x_ref = xrverd;
             fln    = 1;
             f0077  = '004';
             f0078c = 'CustomsOfficeOfDeparture: ' + %trim(xrverd);
           endsl;

           x2_xrlev2 = x2_xrlev2 + 1;
           chain (xrsn:x2_xrlev1:x2_xrlev2:x2_xrlev3) xmlrcvf;
         enddo;

       endsr;

       //**********************************************
       begsr srxml3;
       //**********************************************

         x2_xrlev1 = xrlev1;
         x2_xrlev2 = 1;
         x2_xrlev3 = 0;

         chain (xrsn:x2_xrlev1:x2_xrlev2:x2_xrlev3) xmlrcvf;
         dow %found;
           select;
           when xrnv = 'identificationNumber';
             x_tinh = xrverd;
           when xrnv = 'TIRHolderIdentificationNumber';
             x_TIR = xrverd;
           when xrnv = 'name';
             x_nah = xrverd;
           when xrnv = 'Address';
             exsr srxml31;
           endsl;

           x2_xrlev2 = x2_xrlev2 + 1;
           chain (xrsn:x2_xrlev1:x2_xrlev2:x2_xrlev3) xmlrcvf;
         enddo;

       endsr;

       //**********************************************
       begsr srxml31;
       //**********************************************

         x3_xrlev1 = xrlev1;
         x3_xrlev2 = xrlev2;
         x3_xrlev3 = 1;
         x3_xrlev4 = 0;

         chain (xrsn:x3_xrlev1:x3_xrlev2:x3_xrlev3:x3_xrlev4) xmlrcvf;
         dow %found;
           select;
           when xrnv = 'streetAndNumber';
             x_ad1h = xrverd;
           when xrnv = 'postcode';
             x_pnh = xrverd;
           when xrnv = 'city';
             x_psh = xrverd;
           when xrnv = 'country';
             x_lkh = xrverd;
           endsl;

           x3_xrlev3 = x3_xrlev3 + 1;
           chain (xrsn:x3_xrlev1:x3_xrlev2:x3_xrlev3:x3_xrlev4) xmlrcvf;
         enddo;

       endsr;

       //**********************************************
       begsr opdopd;
       //**********************************************

N01      thavd = 0;
N01      thtdn = 0;
         if x0068 = *blanks;
           chain x_thtrnr nctseh05;
           if %found;
             setll (thavd: thtdn: 99999999) edim2;
             readpe (thavd: thtdn) edim2;
           endif;
         endif;

         chain x0068 edim5;
         if %found;
           chain (xavd: xtdn) nctseh;
           if %found;
             select;
             when x_ie28 = 'J';
               if thst009 = 'J' and thtrnr = *blanks;     // IE09 kom inn før IE28
               else;
                 thst = 'U';         // Motta IE28
               endif;
               thst028 = 'J';
               thtrnr = x_thtrnr;
               thtrdt = %dec(x_thtrdt:8:0);
             when thtrnr = *blanks;
               thst = 'c';           // Motta IE04 før IE28
               thst004 = 'J';
             other;
               thst = 'U';           // Motta IE04 etter IE28
               thst004 = 'J';
             endsl;
             thst056 = ' ';
             update nctsehr;
N01          if (thdk = 'EXIT' or thdk = 'ENTRY');
N01            utype = *blanks;
N01            ncts108r ( uavd: utdn: thtrnr: utype );
N01          endif;
             if m0004  = *blanks;
               // IE04 sender ikke m0004
               m0004  = x0004;
             endif;
           endif;

           if x_ie28 = 'J';
             chain (mavd: mtdn) headf;
             if %found;
               chain (thavd: thtdn: 'TET') frisok;
               if not %found;
                 chain fskode kodfri;
                 if %found;
                   fsdokk = kfsodk;
                 else;
                   fsdokk = *blanks;
                 endif;
                 fsavd  = thavd;
                 fsopd  = thtdn;
                 fskode = 'TET';
                 fssok  = thtrnr;
                 fsdtop = hedtop;
                 write frisokr;
               endif;
             endif;

             setll (thavd: thtdn) nctsec;
             reade (thavd: thtdn) nctsec;
             dow not %eof;
               if tcavd2 <> 0 and tctdn2 <> 0 and
                 (tcavd2 <> thavd or tctdn2 <> thtdn);
                 chain (tcavd2: tctdn2: 'TET') frisok;
                 if not %found;
                   chain fskode kodfri;
                   if %found;
                     fsdokk = kfsodk;
                   else;
                     fsdokk = *blanks;
                   endif;
                   fsavd  = tcavd2;
                   fsopd  = tctdn2;
                   fskode = 'TET';
                   fssok  = thtrnr;
                   fsdtop = hedtop;
                   write frisokr;
                 endif;
               endif;
               reade (mavd: mtdn) nctsec;
             enddo;
           endif;

         endif;

         m0065  = 'XMLDEC';
         if x0068 = *blanks;
           m0068  = x_thtrnr;
         else;
           m0068  = x0068;
         endif;
         m1001  = 'TET';
         select;
         when x_IE28 = 'J';
           m1225  = '028';
         when x_IE04 = 'J';
           m1225  = '004';
         endsl;
         msn    = xrsn;
         msr    = 'R';
         mst    = 'C';
         mdt    = %float(wdt);
         mtm    = %float(wtm);
         m0068a = x0068a;
         m0068b = x0068b;
         m1n07  = m1225;
         mavd   = thavd;
         mtdn   = thtdn;
         write edimr;

       endsr;

       //**********************************************
       begsr ttsr;
       //**********************************************

         ttavd  = thavd;
         ttopd  = thtdn;
         ttacti = 'T' + %subst(x1n07:2:2);
         ttdate = %float(wdt);
         tttime = %float(wtm);
         ttuser = zuser;
         select;
         when x_IE28 = 'J';
         tttext = 'Accept customs NCTS '
                  + %trim(%editc(thtrdt:'Z'))+ ' '  + %trim(thtrnr);
         tttexl = 'Godkjent toll.no NCTS'
                  + %trim(%editc(thtrdt:'Z'))+ ' '  + %trim(thtrnr);
         when x_IE04 = 'J';
         tttext = 'Amendment Acceptance NCTS '
                  + %trim(%editc(x_aadt:'Z'))+ ' '  + %trim(thtrnr);
         tttexl = 'Godkjenning av endring NCTS'
                  + %trim(%editc(x_aadt:'Z'))+ ' '  + %trim(thtrnr);
         endsl;
         write trackfr;

       endsr;

       //**********************************************
       begsr init;
       //**********************************************

         xrsn = %dec(usn:9:0);
         syge15c ( wdt : wtm );

         x_asdt = 0;
         x_astm = 0;
         x_aadt = 0;
         x_aatm = 0;

       endsr;

      /end-free
