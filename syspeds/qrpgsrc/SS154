      *********************************************************************
      * Endre mottakers adresse
      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/SS154 MODULE(*LIBL/SS154
CRTPGM*         *LIBL/CHECKUSR *LIBL/INCGI) ACTGRP(CGI) AUT(*USE)
      *
      *********************************************************************
      * MAIN PROGRAM FLOW
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
     FBRIDF     UF   E           K DISK    USROPN
     FVADR      UF   E           k DISK    USROPN
     FCDBOAT    UF   E           k DISK    USROPN
     FKUNKO     UF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Prototype defintions and standard system API error structure
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      *--------------------------------------------------------------------
      * Variables common to CGI programs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
      * Variables received from the input string
     D lng             s              2
     D BckGnd          s             10
     D UsrSpcName      s             10
     D WSUSER          S             10
     D WSNA            S             30
     D WKEY            S            150
     D WSCNO           S              8
     D W2CNO           S              8  0
     D WSNV            S             30
     D WSAD1           S             30
     D WSAD2           S             30
     D WSAD3           S             30
     D LAGREADR        S              1
     D FMELDNR         S              2
     D FEILMELDING     S             60
     D FEIL01          C                   CONST('Name can"t be blank.')
     D FEIL02          C                   CONST('Post address have to be -
     D                                     given.')
     D lstbrd          s              1
     D PGM             C                   CONST('SYSPED/CHECKUSR')
      *
     D Lib             s             10
     D Fn              s             10
     D Mbr             s             10
     D                 DS
     D WSBCD                   1      5
     D WSBCD2                  2      5
      *
      *--------------------------------------------------------------------
      * Prolog common to CGI programs
      * -Receive input from the remote browser
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prolog3
      *=====================================================================******
      * MAIN PROCESS LINE
      *=====================================================================******
      * Get program start time for calculating execution time
     C                   time                    timedata1
      * Parse input
     C                   exsr      Parse
      *
     C                   EXSR      INIT
     C                   CALLB     PGM
     C                   PARM                    BIBRID
     C                   PARM                    UsrSpcName
     C                   PARM                    UINN              1
     C                   IF        UINN = 'N'
     C                   GOTO      SLUTT
     C                   END
     C     WSUSER        CHAIN     BRIDF                              50
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVE      WDT           BIDTF
     C                   MOVE      WTM           BITIDF
     C                   UPDATE    BRIDFR
      *
     c     LAGREADR      IFEQ      'Y'
     c                   exsr      tstfeil
     c                   ELSE
     c     VADRK         chain(N)  VADR                               33
     c                   EVAL      WSNV = VADRNA
     c                   EVAL      WSAD1 = VADRN1
     c                   EVAL      WSAD2 = VADRN2
     c                   EVAL      WSAD3 = VADRN3
     c                   end
      *
      * Set output skeleton html member name
     C                   exsr      SetSkl
      * Read skeleton output html, etc.
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
      *
      * 1-Section /$top
     C                   exsr      SetTop
     C                   callp     wrtsection('top')
      *
     C     SLUTT         TAG
     C                   EXSR      EXIT
     C                   return
      *=====================================================================******
      * Test skjermbilde
      *=====================================================================******
     C     TstFeil       begsr
      * Test artikkelnr
     c     WSNV          COMP      *BLANKS                                33
     C                   IF        *IN33
     C                   MOVEL     FEIL01        FEILMELDING
     C                   MOVE      '01'          FMELDNR
     C                   GOTO      UtTest
     C                   ENDIF
     c     WSAD3         COMP      *BLANKS                                33
     C                   IF        *IN33
     C                   MOVEL     FEIL02        FEILMELDING
     C                   MOVE      '02'          FMELDNR
     C                   GOTO      UtTest
     C                   ENDIF
      *
      * ALT OK - Oppdater  - Deretter avslutt
     C                   MOVE      'Y'           AVSLUTT
      * Hvis endring..
     c     VADRK         chain     VADR                               33
     c                   IF        *IN33 = *OFF
     c                   EVAL      VADRNA = WSNV
     c                   EVAL      SONAVN = WSNV
     c                   EVAL      VADRN1 = WSAD1
     c                   EVAL      VADRN2 = WSAD2
     c                   EVAL      VADRN3 = WSAD3
     c                   EVAL      VAKURE = WSNV
     c                   MOVEL     VADRN3        XAN10            10
     c                   MOVE      XAN10         VAKURE
     c                   UPDATE    VADRR
     c                   end
     c     WSBCD         chain     CDBOAT                             33
     c                   IF        *IN33 = *OFF
     c                   EVAL      CBNAB = WSNV
     c                   MOVEL     VADRN3        XAN10            10
     c                   MOVE      XAN10         CBNAB
     c                   UPDATE    CDBOATR
     c                   end
     c     KUNKOK        chain     KUNKO                              33
     c                   IF        *IN33 = *OFF
     c                   EVAL      KUALFA = WSNV
     c                   EVAL      KUINTN = WSNV
     c                   MOVEL     VADRN3        XAN10            10
     c                   MOVE      XAN10         KUINTN
     c                   UPDATE    KUNKOR
     c                   end
      *
     C     UtTest        endsr
      *=====================================================================******
      * Parse input
      *=====================================================================******
     C     Parse         begsr
     C                   eval      lng     = zhbgetvar('lng')
     C                   eval      BckGnd  = zhbgetvar('bckgnd')
     C                   eval      lstbrd  = zhbgetvar('lstbrd')
     C                   eval      WSUSER  = zhbgetvar('USER')
     C                   EVAL      UsrSpcName  = zhbgetvar('UsrSpcName')
     C                   EVAL      WSNA    = zhbgetvar('WSNA')
     C                   EVAL      WKEY    = zhbgetvar('WKEY')
     C                   EVAL      WSCNO   = zhbgetvar('ORCNO')
     C                   eval      W2CNO   = c2n2(WSCNO)
     C                   EVAL      WSBCD   = zhbgetvar('ORBCD')
     C                   EVAL      WSNV    = zhbgetvar('WSNV')
     C                   EVAL      WSAD1   = zhbgetvar('WSAD1')
     C                   EVAL      WSAD2   = zhbgetvar('WSAD2')
     C                   EVAL      WSAD3   = zhbgetvar('WSAD3')
     C                   eval      LAGREADR= zhbgetvar('LAGREADR')
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$top
      *  (search criteria)
      *=====================================================================******
     C     SetTop        begsr
      * Repeat national language for the next invocation
     C                   callp     updhtmlvar('LNG':lng:
     C                             InitHTMLVars)
      * Color for text, background, links, visited links, active links
     C                   callp     updhtmlvar('TXTCOLOR':'black')
     C                   callp     updhtmlvar('BOLD':' ')
     C                   callp     updhtmlvar('BGCOLOR':BIFARG)
     C                   callp     updhtmlvar('LINK':'0000FF')
     C                   callp     updhtmlvar('VLINK':'FF0000')
     C                   callp     updhtmlvar('ALINK':'00FF00')
BODY C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updhtmlvar('USER':WSUSER)
     C                   callp     updHtmlVar('UsrSpcName':UsrSpcName)
     C                   callp     updhtmlvar('WSNA':WSNA)
     C                   callp     updhtmlvar('WKEY':WKEY)
     C                   callp     updhtmlvar('ORCNO':
     C                             %trim(%editc(W2CNO:'Z')))
     C                   callp     updhtmlvar('ORBCD':WSBCD)
     C                   callp     updHtmlVar('AVSLUTT':AVSLUTT)
     C                   callp     updHtmlVar('WSNV':WSNV)
     C                   callp     updHtmlVar('WSAD1':WSAD1)
     C                   callp     updHtmlVar('WSAD2':WSAD2)
     C                   callp     updHtmlVar('WSAD3':WSAD3)
     C                   callp     updHTMLvar('sdato':
     C                             %trim(%editw(BIDTV:'    .  .  ')))
     C                   callp     updHTMLvar('stid':
     C                             %trim(%editw(BITIDV:'  .  .  ')))
     C                   callp     updHtmlVar('AVSLUTT':AVSLUTT)
     C                   callp     updhtmlvar('FEILMELDING':
     C                             FEILMELDING)
     C                   callp     updhtmlvar('FMELDNR':FMELDNR)
      *
     C                   endsr
      *=====================================================================******
      * Set html output skeleton member before its read into memory
      *=====================================================================******
     C     SetSkl        begsr
      *------------------
      * Set html output skeleton member
     C                   eval      lng = uppify(lng)
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC' + lng
     C                   eval      Mbr = 'SS154'
      *
     C                   endsr
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
     C                   OPEN      BRIDF
     C     WSUSER        CHAIN(N)  BRIDF                              50
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
     C                   OPEN      VADR
     C                   OPEN      CDBOAT
     C                   open      KUNKO
      *
     C     VADRK         klist
     C                   kfld                    BIFIRM
     C                   kfld                    W2CNO
     C                   kfld                    VADRNR
     C     KUNKOK        klist
     C                   kfld                    W2CNO
     C                   kfld                    W2CNO
     C                   kfld                    VADRNR
      *
     c                   move      WSBCD2        VADRNR
      *
     c                   move      *blanks       feilmelding
     c                   move      *blanks       fmeldnr
     C                   MOVE      'N'           AVSLUTT           1
      *
     C                   endsr
      *=====================================================================******
      *  AVSLUTT
      *=====================================================================******
     C     EXIT          begsr
     C                   callp     wrtsection('endhtml')
     C                   callp     wrtsection('*fini')
     C                   close     BRIDF
     C                   close     VADR
     C                   close     CDBOAT
     C                   close     KUNKO
     C                   eval      *inlr = *on
      *
     C                   endsr
