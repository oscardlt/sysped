      *__________________________________
      * SYSPED / Tolldekl. EKSPRESSFORTOLLING
      *""""""""""""""""""""""""""""""""""
     A                                      REF(SR)
     A          R SADEFFR
     A            EFST      R               REFFLD(ST)
     A                                      COLHDG('Status')
      * ' ' --> NY REGISTRERT MANIFEST
      * 'B' --> MANIFEST ER LAGT I UTGÅENDE MAPPE
      * 'C' --> MANIFEST ER SENDT TIL TOLLVESENET
      * 'P' --> MANIFEST ER AVSLUTTET
      * 'X' --> MANIFEST ER UNDERARBEID AV EN BRUKER
     A            EFUUID        36A         COLHDG('MANIFEST-ID')
     A            EFAVD     R               REFFLD(AVD)
     A                                      COLHDG('Avdeling')
     A            EFPRO     R               REFFLD(PRO)
     A                                      COLHDG('Turnummer')
     A            EFDTR     R               REFFLD(DT)
     A                                      COLHDG('Registreringsdato')
     A            EFSG      R               REFFLD(SG)
      *
     A            EFST2     R               REFFLD(ST)
     A                                      COLHDG('Status om manifest')
      * 'U' --> UTKAST
      * 'I' --> INNSENDT
      * 'P' --> PASSERT
      *
      * ----- Passeringsinfo ------
     A            EFTSD          4S 0       COLHDG('Passeringstollsted')
     A            EFST3     R               REFFLD(ST)
     A                                      COLHDG('Status om innpass.')
      * 'O' --> Tillatt
      * 'E' --> Ikke tillatt
     A            EFDTIN    R               REFFLD(DT)
     A                                      COLHDG('Innsendingsdato')
     A            EFETA     R               REFFLD(DT)
     A                                      COLHDG('Oppgitt ETA')
     A            EFETM     R               REFFLD(TM)
     A                                      COLHDG('Oppgitt tid')
     A            EFATA     R               REFFLD(DT)
     A                                      COLHDG('Passert ATA')
     A            EFATM     R               REFFLD(TM)
     A                                      COLHDG('Passert tid')
     A            EF3039E        6S 0       COLHDG('Ekspedisjonsenhet')
     A            EFEID         18A         COLHDG('EksportId')
      *
      *
      * ----- Manifest info ------
      * Deklarant/Transportør
     A            EFKND          8S 0       COLHDG('Deklarant/Tr.sportør')
     A            EFRGD          9A         COLHDG('Org.nr Deklarant')
      * Transportmåte
     A            EFTM           3A         COLHDG('Transportmåte')
     A            EFTMT         50A         COLHDG('Transportmåte tekst')
      * Kjøretøy
     A            EFKTYP         2A         COLHDG('Kjøretøy type kode')
     A            EFKTYPT       50A         COLHDG('Kjøretøy type tekst')
     A            EFKLK          2A         COLHDG('Kjøretøynasjonalitet')
     A            EFKMRK        30A         COLHDG('Kjennemerke')
      * Kjøretøy passiv
     A            EFPLK          2A         COLHDG('Kjøretøynasjonalitet')
     A            EFPMRK        30A         COLHDG('Kjennemerke')
      * Sjåfør
     A            EFSJAF        30A         COLHDG('Sjåfør fornavn')
     A            EFSJAE        30A         COLHDG('Sjåfør ettenavn')
     A            EFSJALK        2A         COLHDG('Sjåfør nasjonalitet')
     A            EFSJADT        8S 0       COLHDG('Sjåfør fødselsdato')
     A            EFBEKR         1A         COLHDG('Sjåfør bekreftelse')
      *
     A            EFERR         50A         COLHDG('Feilmelding ved SND')
