********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 YBC PTF10 HELE BTOT72 FLYTTES 1 LINJE OPP (-1)
********************************************************************
      *------------------------------------------------------------
      * UTSKRIFT AV ENKELTFAKTURAER (SJØ)
      * 96 LINJER MED GIRO ELLER 72 LINJER UTEN GIRO
      *------------------------------------------------------------
     A                                      REF(HEADF)
      *_________________________________
      * Format for markering av ny side.
      *"""""""""""""""""""""""""""""""""
     A          R BKJØP1                    SPACEB(2)
     A  70                                65'** Continues **'
     A N70N79                             65MSGCON(15 DIV1673 SYMSGF)
     A  79                                65'* Fortsetzung *'
      *_____________________________________________
      * Format for hovedopplysninger vanlig faktura.
      *"""""""""""""""""""""""""""""""""""""""""""""
     A          R BKJØP
     A            KFMT1         75      1  1
     A            KFMT2         75      1  1
     A  63                              1  1'¤&a-3R'
     A  63                              1  1'¤(12U¤(s0p10h0s3b4099T'
     A  46        HP1           80      1  1HIGHLIGHT
     A  46        HP2           80      2  1HIGHLIGHT
     A  46        HP3           80      3  1HIGHLIGHT
     A  46        HP4           80      4  1HIGHLIGHT
     A  46        HP5           80      5  1HIGHLIGHT
     A  46        HP6           80      6  1HIGHLIGHT
     A  46        HP7           80      7  1HIGHLIGHT
     A* RYDDET LITT I PARAMETERE - TH. KUN DATOMRK.
     A  63                              8  1'¤(12U¤(s0p10h1s0b4099T'
     A* START BOKS 1
     A  63                              9  1'¤&a1C¤*c1238A¤*c5B¤*c11G¤*c0P'
     A  63 60                           9  1'¤&a1C¤*c5A¤*c229B¤*c11G¤*c0P'
     A  63 60                           9  1'¤&a42C¤*c5A¤*c229B¤*c11G¤*c0P'
     A  63N60                           9  1'¤&a1C¤*c5A¤*c300B¤*c11G¤*c0P'
     A  63N60                           9  1'¤&a42C¤*c5A¤*c300B¤*c11G¤*c0P'
     A  63 60                           9  1'¤&a47C¤*c900A¤*c5B¤*c11G¤*c0P'
     A  63 60                           9  1'¤&a47C¤*c5A¤*c156B¤*c11G¤*c0P'
     A  63 60                           9  1'¤&a77C¤*c5A¤*c156B¤*c11G¤*c0P'
     A  63N60                           9  1'¤&a47C¤*c900A¤*c5B¤*c11G¤*c0P'
     A  63N60                           9  1'¤&a47C¤*c5A¤*c206B¤*c11G¤*c0P'
     A  63N60                           9  1'¤&a77C¤*c5A¤*c206B¤*c11G¤*c0P'
     A N48        KNAVN     R          11 11REFFLD(KNAVN CUNDF)
     A  48        XDINV         30     11 11
     A N88N51N63                       11 48'******************************'
     A N88N51N63                       11 48'******************************'
     A N88N63 65                       11 48'******************************'
     A N88N63 65                       11 48'******************************'
     A N88N63N65
     AA 51N70N79                       11 48MSGCON(30 DIV1674 SYMSGF)
     A N88 63
     AA 51N70N79                       11 48MSGCON(30 SHU0684 SYMSGF)
     A N88N63N65
     AA 51N70N79                       11 48MSGCON(30 DIV1674 SYMSGF)
     A N88 63
     AA 51N70N79                       11 48MSGCON(30 SHU0684 SYMSGF)
     A N63N65
     AAN88 51 70                       11 48'*** COPY  OF   ***************'
     A  63
     AAN88 51 70                       11 48'    COPY  OF                  '
     A  63
     AAN88 51 70                       11 48'    COPY  OF                  '
     A N63N65
     AAN88 51 70                       11 48'*** COPY  OF   ***************'
     A N63N65
     AAN88 51 79                       11 48'*** KOPI  VON  ***************'
     A N63N65
     AAN88 51 79                       11 48'*** KOPI  VON  ***************'
     A  63
     AAN88 51 79                       11 48'    KOPI  VON                 '
     A  63
     AAN88 51 79                       11 48'    KOPI  VON                 '
     A  70 88N63                       11 48'********  P A G E '
     A  70 88N63                       11 48'********  P A G E '
     A  70 88 63                       11 48'          P A G E '
     A  70 88 63                       11 48'          P A G E '
     A N63
     AAN70N79 88                       11 48MSGCON(24 DIV1676 SYMSGF)
     A N63
     AAN70N79 88                       11 48MSGCON(24 DIV1676 SYMSGF)
     A  63
     AAN70N79 88                       11 48MSGCON(24 SHU0685 SYMSGF)
     A  63
     AAN70N79 88                       11 48MSGCON(24 SHU0685 SYMSGF)
     A N63
     AA 79 88N63                       11 48'********  S E I T E'
     A  79 88N63                       11 48'********  S E I T E'
     A  63
     AA 79 88N63                       11 48'          S E I T E'
     A  63
     AA 79 88N63                       11 48'          S E I T E'
     A  88        XSIDE          2  0  11 71EDTCDE(Z)
     A                                      HIGHLIGHT
     A  88N63                          11 75'***'
     A  88N63                          11 75'***'
     A N48        ADR1      R          12 11REFFLD(ADR1 CUNDF)
     A  48        XDIAD1        30     12 11
     A N73N63                          12 48'*'
     A N73N63                          12 48'*'
     A N63                             12 77'*'
     A N63                             12 77'*'
     A N70N73N79  KO2NTX    R          12 54REFFLD(KO2NTX KODTOT2)
     A                                      HIGHLIGHT
     A  70N73     KO2ETX    R          12 54REFFLD(KO2ETX KODTOT2)
     A                                      HIGHLIGHT
     A N63
     AAN70 73N79                       12 48MSGCON(24 DIV1677 SYMSGF)
     A N63
     AAN70 73N79                       12 48MSGCON(24 DIV1677 SYMSGF)
     A  63
     AAN70 73N79                       12 48MSGCON(24 SHU0686 SYMSGF)
     A  63
     AAN70 73N79                       12 48MSGCON(24 SHU0686 SYMSGF)
     A  70 73N63                       12 48'*   C R E D I T N O T E '
     A  70 73N63                       12 48'*   C R E D I T N O T E '
     A  73 79N63                       12 48'*   G U T S C H R I F T '
     A  73 79N63                       12 48'*   G U T S C H R I F T '
     A N73 79N63                       12 48'*   R E C H N U N G     '
     A N73 79N63                       12 48'*   R E C H N U N G     '
     A  70 73 63                       12 48'    C R E D I T N O T E '
     A  70 73 63                       12 48'    C R E D I T N O T E '
     A  73 79 63                       12 48'    G U T S C H R I F T '
     A  73 79 63                       12 48'    G U T S C H R I F T '
     A N73 79 63                       12 48'    R E C H N U N G     '
     A N73 79 63                       12 48'    R E C H N U N G     '
     A N48        ADR2      R          13 11REFFLD(ADR2 CUNDF)
     A  48        XDIAD2        30     13 11
     A N57N63                          13 48'******************************'
     A N57N63                          13 48'******************************'
     A  57 70N63                       13 48'*   CREDIT OF INV:'
     A  57 70N63                       13 48'*   CREDIT OF INV:'
     A N63
     AA 57N70N79                       13 48MSGCON(24 DIV1675 SYMSGF)
     A N63
     AA 57N70N79                       13 48MSGCON(24 DIV1675 SYMSGF)
     A  57 79N63                       13 48'*   GUTSCHRIFT VON:'
     A  57 79N63                       13 48'*   GUTSCHRIFT VON:'
     A  57N63     XKREDA         7S 0  13 71EDTCDE(Z)
     A  63                             13  1'¤&a47C¤*c900A¤*c8B¤*c11G¤*c0P'
     A N37N48     POSTNR    R          14 11REFFLD(POSTNR CUNDF)
     A                                      DLTEDT
     A N37N48     ADR3      R          14 17REFFLD(ADR3 CUNDF)
     A N38N48 37  SYPOGE         9     14 11
     A N38N48 37  ADR3B     R          14 22REFFLD(ADR3 CUNDF)
     A  37 38N48  ADR3C     R          14 11REFFLD(ADR3 CUNDF)
     A  48        XDIAD3        30     14 11
     A N70N79                          14 48MSGCON(15 DIV1667 SYMSGF)
     A                                      HIGHLIGHT
     A  70                             14 48'Invoice No....:'
     A                                      HIGHLIGHT
     A  79                             14 48'Rechnungsnr...:'
     A                                      HIGHLIGHT
     A            XFN            7S 0  14 71EDTCDE(Z)
     A                                      HIGHLIGHT
     A  64                                  FNTCHRSET(CZH400 +
     A                                      T1V10037)
     A  64                                  COLOR(RED)
     A                                      CHRSIZ(1 2)
     A  63                             15  1'¤&a1C¤*c1238A¤*c5B¤*c11G¤*c0P'
     A  63                             15  1'¤&a5C'
S37  A*           KLKNVN    R          15 11REFFLD(KLKNVN KODTLK)
N37  A N70N79     KLKNVN    R          15 11REFFLD(KLKNVN KODTLK)
N37  A  70        KLKNVE    R          15 11REFFLD(KLKNVN KODTLK)
N37  A  79        KLKNVT    R          15 11REFFLD(KLKNVN KODTLK)
     A N70N79                          15 48MSGCON(15 DIV1666 SYMSGF)
     A  70                             15 48'Date of invoice'
     A  79                             15 48'Rechn.datum...:'
     A            IDAGX          6S 0  15 70EDTCDE(Y)
     A                                      HIGHLIGHT
     A N70N79N52                       16  2MSGCON(13 DIV2935 SYMSGF)
     A  70   N52                       16  2'Your ref....:'
     A  79   N52                       16  2'Ihre Pos....:'
     A*      N52  HERFA     R          16 16HIGHLIGHT
S55  A*      N52  HERFA15       15     16 16HIGHLIGHT
N55  A       N52  HERFA30       30     16 16HIGHLIGHT
     A N70N79                          16 48MSGCON(15 DIV1665 SYMSGF)
     A  70                             16 48'Date due......:'
     A  79                             16 48'Verfallstag...:'
     A N39        XFFDT          6S 0  16 70EDTCDE(Y)
     A                                      HIGHLIGHT
     A  39                             16 71'Kontant'
     A                                      HIGHLIGHT
     A N70N79 52                       17  2MSGCON(13 DIV2935 SYMSGF)
     A  70    52                       17  2'Your ref....:'
     A  79    52                       17  2'Ihre Pos....:'
     A        52  XRFA          15     17 16HIGHLIGHT
     A N70N79                          17 48MSGCON(13 DIV1669 SYMSGF)
     A  70                             17 48'Our ref.....:'
     A  79                             17 48'Unsere Ref..:'
     A            HEAVD     R          17 62HIGHLIGHT
     A                                      EDTCDE(Z)
     A                                 17 66'/'
     A                                      HIGHLIGHT
     A            HEOPD     R          17 67HIGHLIGHT
     A                                 17 74'/'
     A                                      HIGHLIGHT
     A            HESG      R          17 75HIGHLIGHT
     A N70N79N52                       18  2MSGCON(13 DIV2936 SYMSGF)
     A  70   N52                       18  2'Arrived by..:'
     A  79   N52                       18  2'Ankunft pe..:'
     A       N52  XLBNA         30     18 16HIGHLIGHT
     A N70N79                          18 48MSGCON(15 DIV2937 SYMSGF)
     A  70                             18 48'Job number....:'
     A  79                             18 48'Prosjekt-No...:'
     A            HEPRO     R          18 70HIGHLIGHT
     A N70N79N52                       19  2MSGCON(13 DIV3816 SYMSGF)
     A  70   N52                       19  2'From..........:'
     A  79   N52                       19  2'Von...........:'
     A       N52  XVHNA         30     19 16HIGHLIGHT
     A N70N79 52                       19  2MSGCON(21 DIV3817 SYMSGF)
     A  70    52                       19  2'Shipped in LCL-unit.:'
     A  79    52                       19  2'Verladen in LCL-cont:'
     A        52  HETRCN    R          19 24HIGHLIGHT
     A N70N79                          19 48MSGCON(15 DIV1668 SYMSGF)
     A  70                             19 48'Customer no...:'
     A  79                             19 48'Kunden Nr.....:'
     A            KUNDNR    R          19 70REFFLD(KUNDNR CUNDF)
     A                                      EDTCDE(Z)
     A                                      HIGHLIGHT
     A N70N79 52                       20  2MSGCON(12 DIV2938 SYMSGF)
     A N70N79N52                       20  2MSGCON(12 YBC0006 SYMSGF)
     A  70                             20  2'Date.......:'
     A  79                             20  2'Datum......:'
     A            XDT            6S 0  20 15EDTCDE(Y)
     A                                      HIGHLIGHT
     A N52N70N79                       21  2MSGCON(13 DIV2939 SYMSGF)
     A N52 70                          21  2'Trans. code.:'
     A N52 79                          21  2'Trans. Kode.:'
     A N52        HETRM     R          21 16EDTCDE(Z)
     A                                      HIGHLIGHT
     A N70N79 52                       21  2MSGCON(11 DIV2940 SYMSGF)
     A  70    52                       21  2'Shipped by:'
     A  79    52                       21  2'Verl. per.:'
     A        52  XLBNA2        30     21 14HIGHLIGHT
     A N52N70N79                       21 24MSGCON(7 DIV3818 SYMSGF)
     A N52 70                          21 24'Nat...:'
     A N52 79                          21 24'Nat...:'
     A N52        HETRI     R          21 32HIGHLIGHT
     A N52                             21 48MSGCON(13 DIV3819 SYMSGF)
     A N52        HETRC     R          21 62HIGHLIGHT
     A N70N79 52                       21 44MSGCON(6 DIV3820 SYMSGF)
     A  70    52                       21 44'From.:'
     A  79    52                       21 44'Von..:'
     A        52  XLSHNA        30     21 51HIGHLIGHT
     A N70N79N52                       22  2MSGCON(13 DIV1661 SYMSGF)
     A  70   N52                       22  2'Terms of del:'
     A  79   N52                       22  2'Frankatur...:'
     A N70   N52  KFRNTX    R          22 16REFFLD(KFRNTX KODTFR)
     A                                      HIGHLIGHT
     A  70   N52  KFRTTX    R          22 16REFFLD(KFRTTX KODTFR)
     A                                      HIGHLIGHT
     A        52                       22  2MSGCON(11 DIV3821 SYMSGF)
     A        52  XVHN2         30     22 14HIGHLIGHT
     A N52N70N79                       22 48MSGCON(13 DIV1653 SYMSGF)
     A N52 70                          22 48'Unloaded....:'
     A N52 79                          22 48'Entladen....:'
     A N52        HESDL     R          22 62HIGHLIGHT
     A  52N70N79                       22 44MSGCON(6 DIV3822 SYMSGF)
     A  52 70                          22 44'To...:'
     A  52 79                          22 44'Nach.:'
     A  52        XLOHNA        30     22 51HIGHLIGHT
     A N52N70N79                       23  2MSGCON(13 DIV3983 SYMSGF)
     A N52 70                          23  2'Cargo reg.no:'
     A N52 79                          23  2'Güter Reg.no:'
     A N52N94     HEGN      R          23 16HIGHLIGHT
     A N52N94                          23 31'-'
     A N52N94     HEPOS1    R          23 32HIGHLIGHT
     A N52N94     HEPOS2    R          23 40EDTCDE(Z)
     A                                      HIGHLIGHT
     A  52N70N79                       23  2MSGCON(6 DIV3823 SYMSGF)
     A  52 70                          23  2'Pr...:'
     A  52 79                          23  2'Per..:'
     A  52        XOBNA         25     23  9HIGHLIGHT
     A N70N79N52                       23 48MSGCON(13 DIV3824 SYMSGF)
     A  70   N52                       23 48'LCL-unit....:'
     A  79   N52                       23 48'LCL-cont....:'
     A       N52  XTRCN         17     23 62HIGHLIGHT
     A  52N70N79                       23 35MSGCON(7 DIV3825 SYMSGF)
     A  52 70                          23 35'Voy...:'
     A  52 79                          23 35'Reise.:'
     A  52        BLREIS        14     23 43HIGHLIGHT
     A  52N70                          23 58'BL nr:'
     A  52 70                          23 58'BL No:'
     A  52        BLBLNR        16     23 65HIGHLIGHT
     A N70N79                          25  2MSGCON(13 DIV1651 SYMSGF)
     A  70                             25  2'Shipper.....:'
     A  79                             25  2'Absender....:'
     A            HENAS     R          25 16HIGHLIGHT
     A N52N70N79                       25 47MSGCON(7 DIV3826 SYMSGF)
     A N52 70                          25 47'From..:'
     A N52 79                          25 47'Von...:'
     A  52N70N79                       25 47MSGCON(7 DIV3827 SYMSGF)
     A  52 70                          25 47'Coll..:'
     A  52 79                          25 47'Lades.:'
     A            XLSTN         26     25 55HIGHLIGHT
     A N70N79                          26  2MSGCON(13 DIV1652 SYMSGF)
     A  70                             26  2'Consignee...:'
     A  79                             26  2'Empfanger...:'
     A            HENAK     R          26 16HIGHLIGHT
     A N52N70N79                       26 47MSGCON(7 DIV3828 SYMSGF)
     A N52 70                          26 47'To....:'
     A N52 79                          26 47'Nach..:'
     A  52N70N79                       26 47MSGCON(7 DIV3829 SYMSGF)
     A  52 70                          26 47'Del...:'
     A  52 79                          26 47'Gelie.:'
     A            XLOSNA        26     26 55HIGHLIGHT
     A N52N70N79                       27  2MSGCON(6 DIV3823 SYMSGF)
     A N52 70                          27  2'Pr...:'
     A N52 79                          27  2'Per..:'
     A N52        XOBNA2        25     27  9HIGHLIGHT
     A N52N70N79                       27 35MSGCON(7 DIV3825 SYMSGF)
     A N52 70                          27 35'Voy...:'
     A N52 79                          27 35'Reise.:'
     A N52        BLREI2        14     27 43HIGHLIGHT
     A N52N70                          27 58'BL nr:'
     A N52 70                          27 58'BL No:'
     A N52        BLBLN2        16     27 65HIGHLIGHT
     A                                 28  2MSGCON(79 DIV3321 SYMSGF)
     A                                      HIGHLIGHT
     A N70N79                          29  2MSGCON(79 DIV3830 SYMSGF)
     A  70                             29  2'Marks              No.Psc Desc.of -
     A                                      goods                  Weight      -
     A                                         M3    '
     A  79                             29  2'Zeichen            Anzahl Warenart-
     A                                                             Gewicht     -
     A                                         M3    '
     A                                 30  2MSGCON(79 DIV3331 SYMSGF)
     A                                      HIGHLIGHT
      *_____________________________________________
      * Format for varelinjer av bill of lading
      *"""""""""""""""""""""""""""""""""""""""""""""
     A          R BDETBL1                   SPACEB(1)
     A            BVMN          17         2
     A            BVNT           5  0     21EDTCDE(Z)
     A            BVVS          25        29
     A            BVVKT          9  0     55EDTCDE(4)
     A            BVM3           9  3     69EDTCDE(4)
     A          R BDETBL2                   SPACEB(1)
     A            XMN           17         2
     A            XFT           51        29
     A          R BDETBLH
     A                                 42  2MSGCON(79 DIV3321 SYMSGF)
     A                                      HIGHLIGHT
     A N70N79                          43  2MSGCON(78 DIV3831 SYMSGF)
     A  70                             43  2'               D E S C R I P T I O-
     A                                       N |                A M O U N T    '
     A  79                             43  2'     K O S T E N B E R E C H N U N-
     A                                       G |                K O S T E N    '
     A                                 44  2'¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯-
     A                                      ¯¯¯|¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯-
     A                                      ¯¯¯¯¯¯¯¯¯¯'
     A                                      HIGHLIGHT
      *_____________________________________________
      * Format for kostnader og opplysninger
      *"""""""""""""""""""""""""""""""""""""""""""""
     A          R BDETK                     SPACEB(1)
     A            XFT37         37         2
     A                                    39'|'
     A N70N79N32  KGENOT        20        41
     A  70   N32  KGEENT        20        41
     A  79   N32  KG2TYT        20        41
     A  70 36N32
     AO 79 36N32  XVAL           3        62
     A N70 36 71
     AA      N32  XVAL2          3        62
     A  36   N32  XPFNO2         9  2     65EDTCDE(J)
     A  34 36N32  XMVAK          1        78
     A          R BDETK2                    SPACEB(1)
     A            XFT37         37         2
     A                                    39'|'
      *_____________________________________________
      * Format for total
      *"""""""""""""""""""""""""""""""""""""""""""""
     A          R BTOT1                     SPACEB(1)
     A            XFT37         37         2
     A                                    39'|'
     A N70N79                             41MSGCON(16 TRO0511 SYMSGF)
     A  70                                41'VAT of (  lines)'
     A  79                                41'Tax von(  linen)'
     A  79        TVATKG         1        49
     A          R BTOT2                     SPACEB(1)
     A            XFT37         37         2
     A                                    39'|'
     A            XMVAG2         9  2     40EDTWRD('       ,  ')
S02  A*N70N79                             51MSGCON(13 DIV3879 SYMSGF)
N02  A N70N79                             51MSGCON(9  SHU1221 SYMSGF)
S02  A* 70                                51'VAT:        %'
S02  A  70                                51'VAT:    %'
S02  A* 79                                51'MWS:        %'
S02  A  79                                51'MWS:    %'
S02  A*           PRITAX         4  2     57EDTCDE(J)
N02  A            PRITAH         2  0     57
     A  70
     AO 79
S02  A*N70 71     XVAL           3        65
N02  A N70 71     XVAL           3        62
S02  A*           XPMOM2         8  2     68EDTWRD('      ,  ')
S02  A            XPMOM2         8  2     66EDTCDE(J)
     A                                      SPACEA(1)
     A N70N79                              2MSGCON(17 SHU1120 SYMSGF)
     A  79                                 2'Taxfree total:  '
     A  70                                 2'Taxfree total:  '
     A            XMVAF2         9  2     22EDTCDE(J)
N02  A          R BTOT3                     SPACEB(1)
N02  A            XFT37         37         2
N02  A                                    39'|'
N02  A N70N79                             41MSGCON(10 SHU1220 SYMSGF)
S41  A* 79                                41'MWS im NOK:'
S41  A* 70                                41'VAT in NOK:'
N41  A  79                                41'MWS im'
N41  A  70                                41'VAT in'
N41  A            FICURR         3        48
N41  A                                    51':'
N02  A            MVANOK        11  2     52EDTWRD('         ,  ')
     A          R BTOT
     A                                 67  2'__________________________________-
     A                                      ___|_______________________________-
     A                                      __________'
     A                                      HIGHLIGHT
     A N70N73N79                       68  2MSGCON(24 DIV1684 SYMSGF)
     A  70N73N79                       68  2'Terms of payment'
     A N70N79                          68 45MSGCON(12 DIV1685 SYMSGF)
     A  70                             68 45'Total:'
     A  79                             68 45'Gesamtbetrag'
     A            XVAL           3     68 58
     A            XBRUTT        13  2  68 61EDTCDE(J)
     A N39N73N79  BETTXT        30     69  2
     A  39N70                          69  2'Kontant'
     A  39 70                          69  2'Cash:'
     A                                 69 45'================================='
     A  44        HP8           80     70  1
     A  44        HP9           80     71  1
     A  44        HP10          80     72  1
     A          R BTOT72
N01   * HELE REKORDEN FLYTTES 1 LINJE OPP (-19
     A                                 66  2'__________________________________-
     A                                      ___|_______________________________-
     A                                      __________'
     A                                      HIGHLIGHT
     A N70N73N79                       67  2MSGCON(24 DIV1684 SYMSGF)
     A  70N73N79                       67  2'Terms of payment'
     A N70N79                          67 45MSGCON(12 DIV1685 SYMSGF)
     A  70                             67 45'Total:'
     A  79                             67 45'Gesamtbetrag'
     A            XVAL           3     67 58
     A            XBRUTT        13  2  67 60EDTCDE(J)
     A N39N73N79  BETTXT        30     68  2
     A  39N70                          68  2'Kontant'
     A  39 70                          68  2'Cash:'
     A                                 68 45'================================='
     A  44        HP8           80     69  1
     A  44        HP9           80     70  1
     A  44        HP10          80     71  1
     A          R BTOTK
     A                                 56  2'__________________________________-
     A                                      ___|_______________________________-
     A                                      __________'
     A                                      HIGHLIGHT
     A N70N73N79                       57  2MSGCON(24 DIV1684 SYMSGF)
     A  70N73N79                       57  2'Terms of payment'
     A N70N79                          57 45MSGCON(12 DIV1685 SYMSGF)
     A  70                             57 45'Total:'
     A  79                             57 45'Gesamtbetrag'
     A            XVAL           3     57 58
     A            XBRUTT        13  2  57 61EDTCDE(J)
     A N39N73N79  BETTXT        30     58  2
     A  39N70                          58  2'Kontant'
     A  39 70                          58  2'Cash:'
     A                                 58 45'================================='
     A  44        HP8           80     59  1
     A  44        HP9           80     60  1
     A  44        HP10          80     61  1
     A          R BTOT72K
     A                                 56  2'__________________________________-
     A                                      ___|_______________________________-
     A                                      __________'
     A                                      HIGHLIGHT
     A N70N73N79                       57  2MSGCON(24 DIV1684 SYMSGF)
     A  70N73N79                       57  2'Terms of payment'
     A N70N79                          57 45MSGCON(12 DIV1685 SYMSGF)
     A  70                             57 45'Total:'
     A  79                             57 45'Gesamtbetrag'
     A            XVAL           3     57 58
     A            XBRUTT        13  2  57 60EDTCDE(J)
     A N39N73N79  BETTXT        30     58  2
     A  39N70                          58  2'Kontant'
     A  39 70                          58  2'Cash:'
     A                                 58 45'================================='
     A  44        HP8           80     59  1
     A  44        HP9           80     60  1
     A  44        HP10          80     61  1
      *________________________________
      * Format for Ordinary giro Norway
      *""""""""""""""""""""""""""""""""
     A*** SKRIVER KIDLINJE PÅ SELVE FAKTURABLANKETT
S39  A*         R SKIDLI                    SKIPB(62)
N39  A          R SKIDLI                    SKIPB(58)
     A  61 63                              1'¤*c2330A¤*c60B¤*c11G¤*c2P'
     A  63                                40'¤(12U¤(s0p10h0s5b24579T'
     A            KIDTXT         4         4
     A                                       SPACEB(1)
     A                                      HIGHLIGHT
     A                                    30'TOTAL:'
     A                                      HIGHLIGHT
     A            XVAL2          3        37HIGHLIGHT
     A            IBATXT         8        52
     A                                      HIGHLIGHT
     A            LIN20         20         4HIGHLIGHT
     A                                      SPACEB(2)
     A            KTOTKR        11  0     30EDTCDE(3)
     A                                      HIGHLIGHT
     A                                    41','
     A                                      HIGHLIGHT
     A            KTOTØR         2  0     42
     A                                      HIGHLIGHT
S20  A*           GIRO20        20        52
N20  A            GIRO25        25        52
     A                                      HIGHLIGHT
     A  63                                50'¤(12U¤(s0p10h1s0b4099T'
     A                                      SPACEB(1)
     A          R CVRPA2S
     A*                                     BOX(2.7 0.5 4 7.0 0.01)
     A* 2.7 ER LINJE PÅ SIDEN FOR FØRSTE BOKS, 4 ER HØYDEN PÅ BOKSEN 7.0 ER
     A* BREDDEN
S39  A*                                     BOX(10  0.2 11.0 7.5 0.01)
N39  A                                      BOX(9.1  0.2 10.0 7.5 0.02)
      * START TOMME 9 0,2 TOMMER MARG
      * BREDDE 7.5 TOMMER SLUTT TOMME 10
     A* 10  ER LINJE PÅ SIDEN FOR ANDRE BOKS,  2.0 ER HØYDEN PÅ BOKSEN 8 ER
     A* BREDDEN  (FUNGERER OGSÅ MED 10.8 OG 7.0)
      *________________________________
      * Format for Ordinary giro Norway
      *""""""""""""""""""""""""""""""""
     A          R BBGIR
     A N70N79                          74  7'Faktura nummer:'
     A  70                             74  7'Invoice number:'
     A  79                             74  7'Rechnungsnr...:'
     A            XFN            7  0  74 29 EDTCDE(3)
     A N39 59     KFFDT          6  0  74 67 EDTCDE(Y)
     A  39 59                          74 67'Kontant'
     A  39 70 59                       74 67'Cash'
     A N70N79                          75  7'Kundenummer...:'
     A  70                             75  7'Customer no...:'
     A  79                             75  7'Kunden nr.....:'
     A            KUNDNR    R          75 28 REFFLD(KUNDNR CUNDF)
     A                                       EDTCDE(3)
     A N70N79                          76  7'Faktura dato..:'
     A  70                             76  7'Invoice date..:'
     A  79                             76  7'Rechnungsdatum'
     A            IDAGX          6  0  76 28 EDTCDE(Y)
     A N70N79                          77  7'Forfallsdato..:'
     A  70                             77  7'Date due......:'
     A  79                             77  7'Verfallstag...:'
     A N39        XFFDT          6  0  77 28 EDTCDE(Y)
     A  39                             77 28'Kontant'
     A  39 70                          77 28'Cash'
     A N70N79                          78  7'Vår Referanse.:'
     A  70                             78  7'Our reference.:'
     A  79                             78  7'Unsere Ref....:'
     A            HEAVD     R          78 22
     A                                 78 26'/'
     A            HEOPD     R          78 27
     A                                 78 34'/'
     A            HESG      R          78 35
     A N48        KNAVN     R          82  7REFFLD(KNAVN CUNDF)
     A  48        XDINV         30     82  7
     A            XFNAVN        30     82 46
     A N48        ADR1      R          83  7REFFLD(ADR1 CUNDF)
     A  48        XDIAD1        30     83  7
     A            XFADR1        30     83 46
     A N48        ADR2      R          84  7REFFLD(ADR2 CUNDF)
     A  48        XDIAD2        30     84  7
     A            XFADR2        30     84 46
     A N37N48     POSTNR    R          85  7REFFLD(POSTNR CUNDF)
     A                                      DLTEDT
     A N37N48     ADR3      R          85 13REFFLD(ADR3 CUNDF)
     A N38N48     SYPOGE         9     85  7
     A N38N48     ADR3B     R          85 18REFFLD(ADR3 CUNDF)
     A  37 38N48  ADR3C     R          85  7REFFLD(ADR3 CUNDF)
     A  48        XDIAD3        30     85  7
     A            XFPONR         4  0  85 46
     A            XFADR3        24     85 52
      *_________________________________________
      * Format for H-line giro, OCR    5224/5225
      *"""""""""""""""""""""""""""""""""""""""""
     A          R BBGIR2A
     A                                      CPI(10)
     A                                      DFNCHR( +
     A                                      X'F0' X'7E818181817E000000' +
     A                                      X'F1' X'00004000FF00000000' +
     A                                      X'F2' X'438589819161000000' +
     A                                      X'F3' X'818191A1D18E000000' +
     A                                      X'F4' X'040C1424448F040000' +
     A                                      X'F5' X'E1A1A1A1928C000000' +
     A                                      X'F6' X'1E2949890906000000' +
     A                                      X'F7' X'80808F90A0C0800000' +
     A                                      X'F8' X'066991919169060000' +
     A                                      X'F9' X'609091929478000000')
     A  59        BANKGK        24     88 17HIGHLIGHT
     A N59        BANKG         15     88 13HIGHLIGHT
     A N65        OCRF          75     90  1
     A                                 91  1'H'
     A                                      HIGHLIGHT
     A N59N62     XWKID         19     91 11HIGHLIGHT
     A N59N62     XBAK           1  0  91 30HIGHLIGHT
     A N59 62     KID8           8     91 16HIGHLIGHT
     A N59        XTOTKR        11  0  91 31EDTCDE(3)
     A                                      HIGHLIGHT
     A N59                             91 31'<'
     A                                      HIGHLIGHT
     A N59        XTOTØR         2  0  91 46HIGHLIGHT
     A  59N62     KWKID         19     91  9HIGHLIGHT
     A  59N62     KBAK           1     91 28HIGHLIGHT
     A  59 62     KID8B          8     91 14HIGHLIGHT
     A  59        KTOTKR        11  0  91 29EDTCDE(3)
     A  59                                  HIGHLIGHT
     A  59        KTOTØR         2  0  91 42HIGHLIGHT
     A  59        KNYMOD         1     91 47HIGHLIGHT
     A            XFBANK        15     91 52HIGHLIGHT
     A N65        OCRE          75     92  1
     A          R BBGIR2D
     A                                      CPI(10)
     A                                      DFNCHR( +
     A                                      X'F0' X'7E818181817E000000' +
     A                                      X'F1' X'00004000FF00000000' +
     A                                      X'F2' X'438589819161000000' +
     A                                      X'F3' X'818191A1D18E000000' +
     A                                      X'F4' X'040C1424448F040000' +
     A                                      X'F5' X'E1A1A1A1928C000000' +
     A                                      X'F6' X'1E2949890906000000' +
     A                                      X'F7' X'80808F90A0C0800000' +
     A                                      X'F8' X'066991919169060000' +
     A                                      X'F9' X'609091929478000000')
     A            BANKG         15     88 13HIGHLIGHT
     A N65        OCRF          75     90  1
     A                                 91  1'H'
     A                                      HIGHLIGHT
     A            XTOTKR        11  0  91 31EDTCDE(3)
     A                                      HIGHLIGHT
     A            XTOTØR         2  0  91 46HIGHLIGHT
     A            XFBANK        15     91 52HIGHLIGHT
     A N65        OCRE          75     92  1
      *___________________________________________
      * Format for H-line giro, OCR    4224(*IPDS)
      *"""""""""""""""""""""""""""""""""""""""""""
     A          R BBGIR2B
     A                                      FONT(011)
     A  59        BANKGK        24     88 17HIGHLIGHT
     A                                      CHRID
     A N59        BANKG         15     88 13HIGHLIGHT
     A                                      CHRID
     A N65        OCRF          75     90  1
     A                                 91  1'H'
     A                                      HIGHLIGHT
     A N59N62     XWKID         19     91 11HIGHLIGHT
     A                                      CHRID
     A N59N62     ABAK           1     91 30HIGHLIGHT
     A                                      CHRID
     A N59 62     KID8           8     91 16HIGHLIGHT
     A                                      CHRID
     A N59        ATOTKR        11     91 31HIGHLIGHT
     A                                      CHRID
     A N59        HAKE           1     91 31HIGHLIGHT
     A                                      CHRID
     A N59        ATOTØR         2     91 46HIGHLIGHT
     A                                      CHRID
     A  59N62     KWKID         19     91  9HIGHLIGHT
     A                                      CHRID
     A  59N62     KBAK           1     91 28HIGHLIGHT
     A                                      CHRID
     A  59 62     KID8B          8     91 14HIGHLIGHT
     A                                      CHRID
     A  59        ATOTKK        11     91 29HIGHLIGHT
     A                                      CHRID
     A  59        ATOTØK         2     91 42HIGHLIGHT
     A                                      CHRID
     A  59        KNYMOD         1     91 47HIGHLIGHT
     A                                      CHRID
     A            XFBANK        15     91 52HIGHLIGHT
     A                                      CHRID
     A N65        OCRE          75     92  1
     A          R BBGIR2E
     A                                      FONT(011)
     A N59        BANKG         15     88 13HIGHLIGHT
     A                                      CHRID
     A N65        OCRF          75     90  1
     A                                 91  1'H'
     A                                      HIGHLIGHT
     A N59        ATOTKR        11     91 31HIGHLIGHT
     A                                      CHRID
     A N59        ATOTØR         2     91 46HIGHLIGHT
     A                                      CHRID
     A  59        ATOTKK        11     91 29HIGHLIGHT
     A                                      CHRID
     A  59        ATOTØK         2     91 42HIGHLIGHT
     A                                      CHRID
     A  59        KNYMOD         1     91 47HIGHLIGHT
     A                                      CHRID
     A            XFBANK        15     91 52HIGHLIGHT
     A                                      CHRID
     A            OCRE          75     92  1
      *_____________________________________________
      * Format for H-line giro, OCR    BELTESSKRIVER
      *"""""""""""""""""""""""""""""""""""""""""""""
     A          R BBGIR2C
     A  59        BANKGK        24     88 17HIGHLIGHT
     A N59        BANKG         15     88 13HIGHLIGHT
     A N65        OCRF          75     90  1
     A                                 91  1'H'
     A                                      HIGHLIGHT
     A N59N62     XWKID         19     91 11HIGHLIGHT
     A N59N62     XBAK           1  0  91 30HIGHLIGHT
     A N59 62     KID8           8     91 16HIGHLIGHT
     A N59        XTOTKR        11  0  91 31EDTCDE(3)
     A                                      HIGHLIGHT
     A N59                             91 31'<'
     A                                      HIGHLIGHT
     A N59        XTOTØR         2  0  91 46HIGHLIGHT
     A  59N62     KWKID         19     91  9HIGHLIGHT
     A  59N62     KBAK           1     91 28HIGHLIGHT
     A  59 62     KID8B          8     91 14HIGHLIGHT
     A  59        KTOTKR        11  0  91 29EDTCDE(3)
     A                                      HIGHLIGHT
     A  59        KTOTØR         2  0  91 42HIGHLIGHT
     A  59        KNYMOD         1     91 47HIGHLIGHT
     A            XFBANK        15     91 52HIGHLIGHT
     A N65        OCRE          75     92  1
      *_____________________________________________
      * Format for H-line giro, OCR    BELTESSKRIVER
      * LE*2391(4214-2)  ¤1=DRAFT, ¤2=LOUVIER 10, ¤3=OCR-B(KODESETT877)
      *"""""""""""""""""""""""""""""""""""""""""""""
     A          R BBGIR2F
     A  59        BANKGK        24     88 17HIGHLIGHT
     A N59        BANKG         15     88 13HIGHLIGHT
     A N65        OCRF          75     90  1
     A                                 91  1'H'
     A                                      HIGHLIGHT
     A N59N62     XWKID         19     91 11HIGHLIGHT
     A N59N62     XBAK           1  0  91 30HIGHLIGHT
     A N59 62     KID8           8     91 16HIGHLIGHT
     A N59        XTOTKR        11  0  91 31EDTCDE(3)
     A                                      HIGHLIGHT
     A N59                             91 31'<'
     A                                      HIGHLIGHT
     A N59        XTOTØR         2  0  91 46HIGHLIGHT
     A  59N62     KWKID         19     91  9HIGHLIGHT
     A  59N62     KBAK           1     91 28HIGHLIGHT
     A  59 62     KID8B          8     91 14HIGHLIGHT
     A  59        KTOTKR        11  0  91 29EDTCDE(3)
     A                                      HIGHLIGHT
     A  59        KTOTØR         2  0  91 42HIGHLIGHT
     A  59        KNYMOD         1     91 47HIGHLIGHT
     A            XFBANK        15     91 52HIGHLIGHT
     A N65        OCRE          75     92  1
      *________________________________
      * Format for H-line giro, Not OCR
      *""""""""""""""""""""""""""""""""
     A          R BBGIR3
     A                                 91  2'H'
     A                                      HIGHLIGHT
     A            XTOTKR        11  0  91 29EDTCDE(3)
     A                                      HIGHLIGHT
     A            XTOTØR         2  0  91 42HIGHLIGHT
     A            XFBANK        15     91 52HIGHLIGHT
