             PGM        PARM(&LIB &NEWOWN &CUROWNAUT &CHGPUBAUT)
             DCL        VAR(&LIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&NEWOWN) TYPE(*CHAR) LEN(10)
             DCL        VAR(&CUROWNAUT) TYPE(*CHAR) LEN(7)
             DCL        VAR(&CHGPUBAUT) TYPE(*CHAR) LEN(8)
             DCL        VAR(&ERROR) TYPE(*CHAR) LEN(1) VALUE('N')
             DCL        VAR(&OBJECTCNT) TYPE(*DEC) LEN(7 0) VALUE(0)
             DCL        VAR(&ERRORCNT) TYPE(*DEC) LEN(7 0) VALUE(0)
             DCL        VAR(&OBJECTCNTA) TYPE(*CHAR) LEN(7)
             DCL        VAR(&ERRORCNTA) TYPE(*CHAR) LEN(7)

             DCLF       FILE(QTEMP/OBJECTS)

             CHKOBJ     OBJ(QSYS/&LIB) OBJTYPE(*LIB) AUT(*ALL)
             MONMSG     MSGID(CPF0000) EXEC(DO)
             SNDPGMMSG  MSG('The command CHGLIBOWN has ended in +
                          error.')
             SNDPGMMSG  MSG('Either the user does not have the +
                          proper authority to change ownership for +
                          this library, or the library name does +
                          not exist on this system.   Please see +
                          the joblog for more info about this error.')
             CHGVAR     VAR(&ERROR) VALUE('Y')
             ENDDO

             CHKOBJ     OBJ(QSYS/&NEWOWN) OBJTYPE(*USRPRF) AUT(*NONE)
             MONMSG     MSGID(CPF0000) EXEC(DO)
             IF         COND(&ERROR *EQ 'N') THEN(SNDPGMMSG MSG('The +
                          command CHGLIBOWN has ended in error.'))
             SNDPGMMSG  MSG('The user profile entered is not valid.  +
                          Check the job log for more info.')
             CHGVAR     VAR(&ERROR) VALUE('Y')
             ENDDO
             IF         COND(&ERROR *EQ 'Y') THEN(GOTO CMDLBL(ENDPGM))
             DSPOBJD    OBJ(&LIB/*ALL) OBJTYPE(*ALL) +
                          OUTPUT(*OUTFILE) OUTFILE(QTEMP/OBJECTS)
RCVFLOOP:
             RCVF
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(ENDPGM))
             CHGVAR     VAR(&OBJECTCNT) VALUE(&OBJECTCNT + 1)
             CHGOBJOWN  OBJ(&LIB/&ODOBNM) OBJTYPE(&ODOBTP) +
                          NEWOWN(&NEWOWN) CUROWNAUT(&CUROWNAUT)
             MONMSG     MSGID(CPF0000) EXEC(CHGVAR VAR(&ERRORCNT) +
                          VALUE(&ERRORCNT + 1))
             IF         COND(&CHGPUBAUT *NE '*NOCHG  ') THEN(DO)
             GRTOBJAUT  OBJ(&LIB/&ODOBNM) OBJTYPE(&ODOBTP) +
                          USER(*PUBLIC) AUT(&CHGPUBAUT) REPLACE(*YES)
             MONMSG     MSGID(CPF0000) EXEC(CHGVAR VAR(&ERRORCNT) +
                          VALUE(&ERRORCNT + 1))
             ENDDO
             GOTO       CMDLBL(RCVFLOOP)
ENDPGM:
             CHGVAR     VAR(&OBJECTCNT) VALUE(&OBJECTCNT + 1)
             CHGOBJOWN  OBJ(QSYS/&LIB) OBJTYPE(*LIB) NEWOWN(&NEWOWN) +
                          CUROWNAUT(&CUROWNAUT)
             MONMSG     MSGID(CPF0000) EXEC(CHGVAR VAR(&ERRORCNT) +
                          VALUE(&ERRORCNT + 1))
             IF         COND(&CHGPUBAUT *NE '*NOCHG  ') THEN(DO)
             GRTOBJAUT  OBJ(QSYS/&LIB) OBJTYPE(*LIB) USER(*PUBLIC) +
                          AUT(&CHGPUBAUT) REPLACE(*YES)
             MONMSG     MSGID(CPF0000) EXEC(CHGVAR VAR(&ERRORCNT) +
                          VALUE(&ERRORCNT + 1))
             ENDDO

             CHGVAR     VAR(&OBJECTCNTA) VALUE(&OBJECTCNT)
             CHGVAR     VAR(&ERRORCNTA) VALUE(&ERRORCNT)
             SNDPGMMSG  MSG(&OBJECTCNTA *BCAT 'objects were process.')
             SNDPGMMSG  MSG(&ERRORCNTA *BCAT 'errors were found +
                          changing ownership.')
             IF         COND(&ERRORCNT *NE 0) THEN(SNDPGMMSG +
                          MSG('Errors have occured changing object +
                          ownership for this library.  Please +
                          review the job log carefully to determine +
                          if other action is required.'))

             ENDPGM
