      *
      **************************************************************
      *** PROGRAM SKAL ENDRE TRIP/ORDRE STATUS FRA 4,5 OR 6 TIL 3
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER
      *** LEDIGE INDIKATORER 01 02 04 06-11 13-24
      *** LEDIGE INDIKATORER 27-29 31-90 95-97
      **************************************************************
      *
     FGSSTRF    UF   E           K DISK
     FGSSORL21  UF   E           K DISK
     FGSSORL24  UF   E           K DISK    RENAME(GSSORFR:GSSORFR24)
     FGSSORL25  IF   E           K DISK    RENAME(GSSORFR:GSSORFR25)
     FGSSLGF    O  A E           K DISK
     D                SDS
     D  UUSER                254    263
      *
     C                   EXSR      INIT
      *
     C     UTNO          CHAIN     GSSTRF                             33
     C                   IF        ((TRBCD = *BLANKS) OR (TRLOCT <> *BLANKS))
      * HVIS ET AV DE ORDRE PÅ TRIP ER MED I EN ANNEN TRIP, KAN IKKE ENDRE STATUS.
     C     TRTNO         SETLL     GSSORL25
     C     TRTNO         READE     GSSORL25                               33
     C     *IN33         CABEQ     *OFF          SLUTT
      * TEST OM ORDRE ER MED PÅ EN ANNEN TRIP
     C     TRTNO         SETLL     GSSORL24
     C     TRTNO         READE(N)  GSSORL24                               33
     C                   DOW       *IN33 = *OFF
     C     ORST          CABNE     '0'           SLUTT
     C     TRTNO         READE(N)  GSSORL24                               33
     C                   ENDDO
      * TRANSFER TRIP
     C     TRTNO         SETLL     GSSORL24
     C     TRTNO         READE     GSSORL24                               33
     C                   DOW       *IN33 = *OFF
     C                   EVAL      ORTNO  = ORTNO2
     C                   EVAL      ORTNO2 = ORTNO3
     C                   EVAL      ORTNO3 = 0
     C                   EVAL      ORLC   = 'ONWAY'
     C                   EVAL      ORST   = '3'
     C                   UPDATE    GSSORFR24
     C     TRTNO         READE     GSSORL24                               33
     C                   ENDDO
     C                   EVAL      LGTXT = 'TRANSFER TRIP'
      *
     C                   ELSE
      * FINAL TRIP
     C     TRTNO         SETLL     GSSORL21
     C     TRTNO         READE     GSSORL21                               33
     C                   DOW       *IN33 = *OFF
     C                   EVAL      ORST   = '3'
     C                   UPDATE    GSSORFR
     C     TRTNO         READE     GSSORL21                               33
     C                   ENDDO
     C                   EVAL      LGTXT = 'FINAL TRIP'
      *
     C                   END
      *
     C                   MOVE      TRST          XST               1
     C                   EVAL      TRST = '3'
     C                   UPDATE    GSSTRFR
     C                   EVAL      UOK = 'J'
      *
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVE      UUSER         LGUSER
     C                   MOVE      WDT           LGDT
     C                   MOVE      WTM           LGTID
     C                   MOVEL     'GSS048R'     LGPGM
     C                   MOVE      9             LGNIVÅ
     C                   MOVE      WDT           LGHDT
     C                   MOVE      WTM           LGHTID
     C                   CAT       'IS CHANGED':1LGTXT
     C                   MOVEL     'TRCH2'       LGFTYP
     C                   Z-ADD     TRTNO         LGNGM
     C                   Z-ADD     TRTNO         LGNNY
     C                   EVAL      LGAGM = 'FROM STATUS ' + XST
     C                             + '  TO STATUS:' + TRST
     C                   EVAL      LGANY = 'VESSEL: ' + TRBCD
     C                             + '  TO LOCATION: ' + TRLOCT
     C                   WRITE     GSSLGFR
      *
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
      *
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
      *
     C     *ENTRY        PLIST
     C                   PARM                    UTNO             11 0
     C                   PARM                    UOK               1
     C                   EVAL      UOK = 'N'
      *
     C                   ENDSR
      *
