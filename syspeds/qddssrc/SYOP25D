     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
      * "NOERASE" MÅ HA FOR Å IKKE TØMME BILDE SOM MAN KOMME FRA
     A          R NOERASE
     A                                      ASSUME
     A                                  1  2' '
      * MAN KUN DEFINNERE VINDU PÅ REKORDNIVÅ
     A          R WINDOWSZ
     A                                      WINDOW(*DFT 14 74 *NOMSGLIN)
     A                                      OVERLAY
     A          R SUBFIL                    SFL
     A            FRTKOD         1A  O  4  2
     A            FRTTXT        70A  O  4  4
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A                                      CA03(03 'Avslutt')
     A                                      CA05(05 'Frisk')
     A                                      CA12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYOP25R'
     A                                      COLOR(BLU)
     A                                  1 13'Vis notisblokk'
     A                                      COLOR(WHT)
     A            ZRECNO         4S 0O  1 50SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  2  2'Referanse:'
     A            UREFF         17A  O  2 13DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  3  2'K Tekster'
     A                                      COLOR(WHT)
     A          R BUNN
     A                                      WINDOW(WINDOWSZ)
     A                                      SLNO(14)
     A                                      CLRL(01)
     A                                      OVERLAY
     A N30                              1  2'F3/F12=Avslutt   F5=Forny'
     A                                      COLOR(BLU)
     A  30        MSGTXT        58A  O  1  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R NYWRK
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A                                      CA03(03 'Forrige')
     A                                      CA05(05 'Frisk')
     A                                      CA12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1  2'SYOP25R'
     A                                      COLOR(BLU)
     A                                  1 13'Vis notisblokk'
     A                                      COLOR(WHT)
     A                                  2  2'Referanse:'
     A            UREFF         17A  O  2 13DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  3  2'K Tekster'
     A                                      COLOR(WHT)
     A N30                             14  2'F3/F12=Avslutt   F5=Forny'
     A                                      COLOR(BLU)
     A  30        MSGTXT        58A  O 14  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
