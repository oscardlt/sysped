********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr *Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 YBC PTF10 OPSJON 8 = FULL MODULS
10318 * 02 YBC PTF10 VEKT ER ENDRET FRA 5.0 TIL 12.3
      *              VAREBESKRIVELSE ER ØKT FRA 15A TIL 30A
      *              MYE ENDRING. INGEN MERKING
xxxxxx*xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
11016 * 03 YBC PTF11 OMBEREGNING
11176 * 04 YBC PTF11 TILLEGGSLINJER MED SAMME POST
xxxxxx*xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
12XXX * 05 YBC PTF12 SVVT ER NÅ SVVT01
12064 * 06 YBC PTF12 FLERE BELØP ER ENDRET FOR Å TA STØRRE VERDI
12XXX * 07 YBC PTF12 VIS OPPRINNELSESLAND
12201 * 08 YBC PTF12 CONTAINERNR I EGET FELT
12230 * 09 YBC PTF12 PREFERANSE
12248 * 10 YBC PTF12 VEKT, KOLLI OG MENDE ER UTVIDET.
12258 * 11 YBC PTF12 NYTT FELT FOR VF-KODE
********************************************************************
     A                                      DSPSIZ(27 132 *DS4)
     A                                      PRINT
     A                                      HLPDOC('STD' 'HELPNU' 'HLP400')
     A                                      HELP
     A                                      ALTHELP
      *
     A          R SUBFIL                    SFL
S01  A*           WSNR           1A  I  5  2VALUES(' ' '5')
N01  A            WSNR           1A  I  5  2VALUES(' ' '5')
     A            SVLI           5Y 0H
     A            SVLN           5Y 0H
N01  A            SVREFL         5Y 0H
S10  A*           SVVKTB        12Y 3H
N10  A            SVVKTB        14Y 3H
     A            SVFT01        28A  H
S10  A*           SVNT01         6Y 0H
N10  A            SVNT01         9Y 0H
     A            SVEH01         4A  H
     A            SVTOP1        17A  H
     A            SVCRE1         3A  H
     A            WXLN           5Y 0O  5  5EDTCDE(4)
     A            SVFYL          2A  O  5 11
     A            SVVNT          8S 0O  5 14TEXT('Varenr Tolltariff')
S10  A*           SVVKTN        12Y 3O  5 23EDTCDE(L)
N10  A            SVVKTN        14Y 3O  5 23EDTCDE(L)
S10  A*           SVNTM          9Y 0O  5 38EDTCDE(L)
N10  A            SVNTM         11Y 0O  5 40EDTCDE(L)
S06  A*           SVBELT        11Y 2O  5 49TEXT('Beløp Tollverdi')
E10  A            SVBELT        13Y 2O  5 53TEXT('Beløp Tollverdi')
     A                                      EDTCDE(L)
     A            SVAVT          1A  H
E10  A            SVAVTS         3A  O  5 69
E10  A            SVAVTP         7Y 3O  5 73TEXT('AVG. FERSK FISK')
     A                                      EDTCDE(4)
S05  A*           SVVT          30A  O  5 76TEXT('Varetekst')
E10  A            SVVT01        30A  O  5 82TEXT('Varetekst')
     A  95                                  COLOR(YLW)
E10  A            SV01           1A  O  5113COLOR(RED)
     A                                      DSPATR(BL)
E10  A            SVNYL          1A  O  5115COLOR(RED)
E10  A            SVLK           2A  O  5117
E10  A            SVPRE          1A  O  5120
E11  A            SVVF           1A  O  5122
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0021)
     A                                      SFLPAG(0020)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A            ZRECNO         4S 0O  1 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2MSGCON(18 DIV1725 SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 29MSGCON(11 DIV1927 SYMSGF)
     A            UAVD           4Y 0O  1 41COLOR(BLU)
     A                                      EDTCDE(4)
     A                                  1 46'-'
     A                                      COLOR(BLU)
     A            UTDN           7Y 0O  1 48COLOR(BLU)
S03  A*                                     EDTCDE(4)
N03  A                                      EDTCDE(L)
     A                                  2  2MSGCON(6 DIV1935 SYMSGF)
     A            UBEL1         13Y 2O  2 10TEXT('Beløp Faktsum')
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  2 27MSGCON(6 DIV1936 SYMSGF)
     A            W2VSUM        13Y 2O  2 35TEXT('Beløp Faktsum')
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  2 52MSGCON(6 DIV1937 SYMSGF)
     A            W2DIFF        13Y 2O  2 60TEXT('Beløp Faktsum')
     A                                      COLOR(WHT)
     A                                      EDTCDE(L)
     A                                  3  2MSGCON(40 DIV1934 SYMSGF)
     A                                      COLOR(BLU)
     A N32                              4  2'Op Linje FK Varenr.. Nettovekt....-
E10  A                                      ... Mengde...... Tollverdi...... FF-
E10  A                                        Promille Varebeskrivelse.........-
S03  A*                                      S'
E11  A                                      ...... S T LK P VF'
     A                                      COLOR(WHT)
     A  32                              4  2'Op Postn FK Varenr.. Nettovekt....-
E10  A                                      ... Mengde...... Tollverdi...... FF-
E10  A                                        Promille Varebeskrivelse.........-
S03  A*                                      S'
E11  A                                      ...... S T LK P VF'
     A                                      COLOR(WHT)
      *
     A          R BUNN
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF01(01 'BYTT AVDELINGER')
     A                                      CF03(03 'Avslutt program')
     A                                      CF05(05 'Frisk')
     A                                      CF08(08 'Hopp til')
     A                                      CF11(11 'Se oppdrag')
     A                                      CF12(12 'FORRIGE')
     A                                      SLNO(25)
     A                                      CLRL(03)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A N27                              1  2'Fersk fisk avgift:'
     A N27        XAVGTT         9Y 0O  1 22COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  2  2MSGCON(18 DIV1914 SYMSGF)
     A                                      COLOR(BLU)
     A                                  2 22MSGCON(18 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A                                  2 42MSGCON(18 DIV0021 SYMSGF)
     A                                      COLOR(BLU)
     A                                  2 62MSGCON(18 DIV0023 SYMSGF)
     A                                      COLOR(BLU)
     A                                  2 82'F11=Se oppdrag'
     A                                      COLOR(BLU)
     A                                  2102MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  3  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R NYVARE
     A                                      CF01(01 'BYTT AVDELNGER')
     A                                      CF03(03 'Avslutt program')
     A                                      CF05(05 'Frisk')
     A                                      CF08(08 'Hopp til')
     A                                      CF11(11 'Se oppdrag')
     A                                      CF12(12 'FORRIGE')
     A                                  1  2MSGCON(18 DIV1725 SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 29MSGCON(11 DIV1927 SYMSGF)
     A            UAVD           4Y 0O  1 41COLOR(BLU)
     A                                      EDTCDE(4)
     A                                  1 46'-'
     A                                      COLOR(BLU)
     A            UTDN           7Y 0O  1 48COLOR(BLU)
S03  A*                                     EDTCDE(4)
N03  A                                      EDTCDE(L)
     A                                  2  2MSGCON(6 DIV1935 SYMSGF)
     A            UBEL1         13Y 2O  2 10TEXT('Beløp Faktsum')
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  2 27MSGCON(6 DIV1936 SYMSGF)
     A            W2VSUM        13Y 2O  2 35TEXT('Beløp Faktsum')
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  2 52MSGCON(6 DIV1937 SYMSGF)
     A            W2DIFF        13Y 2O  2 60TEXT('Beløp Faktsum')
     A                                      COLOR(WHT)
     A                                      EDTCDE(L)
     A                                  3  2MSGCON(40 DIV1934 SYMSGF)
     A                                      COLOR(BLU)
     A N32                              4  2'Op Linje FK Varenr.. Nettovekt....-
E10  A                                      ... Mengde...... Tollverdi...... FF-
E10  A                                        Promille Varebeskrivelse.........-
S03  A*                                      S'
E11  A                                      .... ..S T LK P VF'
     A                                      COLOR(WHT)
     A  32                              4  2'Op Postn FK Varenr.. Nettovekt....-
E10  A                                      ... Mengde...... Tollverdi...... FF-
E10  A                                        Promille Varebeskrivelse.........-
S03  A*                                      S'
E11  A                                      .... ..S T LK P VF'
     A                                      COLOR(WHT)
     A                                 26  2MSGCON(18 DIV1914 SYMSGF)
     A                                      COLOR(BLU)
     A                                 26 22MSGCON(18 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A                                 26 42MSGCON(18 DIV0021 SYMSGF)
     A                                      COLOR(BLU)
     A                                 26 62MSGCON(18 DIV0023 SYMSGF)
     A                                      COLOR(BLU)
     A                                 26 82'F11=Se oppdrag'
     A                                      COLOR(BLU)
     A                                 26102MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 27  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R CLEARS
     A                                      SLNO(20)
     A                                      CLRL(05)
      *
     A          R VAREL2
     A                                      CF12(12 'Forrige')
     A                                      SLNO(23)
     A                                      CLRL(05)
     A                                  1  2'Hopp til linje   -->'
     A            W3LN           5Y 0I  1 24DSPATR(UL)
     A                                      DSPATR(PC)
     A                                  2  2'Hopp til varenr. -->'
     A            W3VNT          8Y 0B  2 24DSPATR(UL)
     A                                  3  2'Hopp til postnr. -->'
     A            W3LI           5S 0I  3 24DSPATR(UL)
     A                                  4  2'F12=Forrige'
     A                                      COLOR(BLU)
N01   *
N01  A          R BILDE
N01  A                                      CF12(12 'Forrige')
N08  A  36                                  CA18(18 'VIS CONTAINERNR')
N01  A                                  1  2'SYSPED/SAD023B'
N01  A                                      COLOR(BLU)
N01  A                                  1 21'TOLLDEKLARASJON'
N01  A                                      COLOR(WHT)
N01  A                                  1 39'Tdeknr.'
N01  A            UAVD           4Y 0O  1 47COLOR(WHT)
N01  A                                      EDTCDE(4)
N01  A                                  1 52'-'
N01  A                                      COLOR(WHT)
N01  A            UTDN           7Y 0O  1 54COLOR(WHT)
S03  A*                                     EDTCDE(4)
N03  A                                      EDTCDE(L)
     A                                  1 63'-'
     A                                      COLOR(WHT)
     A            SVLI           5Y 0O  1 65COLOR(WHT)
     A                                      EDTCDE(4)
     A            SVLN           5Y 0O  1 71COLOR(WHT)
     A                                      EDTCDE(4)
N01  A                                  1 62DATE
N01  A                                      EDTCDE(Y)
N01  A                                  1 72TIME
E10  A                                  4  2'FK Varenr.. Tollverdi...... Brutto-
E10  A                                       v........ Netto vekt...... Mengde.-
S07  A*                                     FF Sats.... Sekv.'
E11  A                                      ..... FF Sats.... Sekv. LK P VF'
N01  A                                      COLOR(WHT)
N01  A            SVFYL          2A  O  5  2TEXT('OPPRINGELSESFYLKE')
N01  A                                      DSPATR(UL)
N01  A                                      COLOR(YLW)
N01  A            SVVNT          8Y 0O  5  5TEXT('Varenr Tolltariff')
N01  A                                      DSPATR(UL)
N01  A                                      COLOR(YLW)
N01  A                                      EDTWRD('0       ')
S06  A*           SVBELT        11Y 2O  5 14TEXT('Beløp Tollverdi')
N06  A            SVBELT        13Y 2O  5 14TEXT('Beløp Tollverdi')
N01  A                                      DSPATR(UL)
N01  A                                      COLOR(YLW)
N01  A                                      EDTCDE(L)
S10  A*           SVVKTB        12Y 3O  5 30TEXT('Vekt Brutto')
N10  A            SVVKTB        14Y 3O  5 30TEXT('Vekt Brutto')
N01  A                                      DSPATR(UL)
N01  A                                      COLOR(YLW)
N01  A                                      EDTCDE(L)
S10  A*           SVVKTN        12Y 3O  5 47TEXT('Vekt Netto')
N10  A            SVVKTN        14Y 3O  5 47TEXT('Vekt Netto')
N01  A                                      DSPATR(UL)
N01  A                                      COLOR(YLW)
N01  A                                      EDTCDE(L)
S10  A*           SVNTM          9Y 0O  5 64TEXT('Mengde')
N10  A            SVNTM         11Y 0O  5 64TEXT('Mengde')
N01  A                                      DSPATR(UL)
N01  A                                      COLOR(YLW)
N01  A                                      EDTCDE(L)
E10  A            SVAVT          1A  O  5 77TEXT('AVG. KONJUNKTURFOND')
N01  A                                      DSPATR(UL)
N01  A                                      COLOR(YLW)
E10  A            SVAVTP         7Y 3O  5 80TEXT('PROSENTSATS AV SVAVT')
N01  A                                      DSPATR(UL)
N01  A                                      COLOR(YLW)
N01  A                                      EDTCDE(4)
E10  A            SVAVTS         3A  O  5 89DSPATR(UL)
N01  A                                      COLOR(YLW)
E10  A            SVLK           2A  O  5 95DSPATR(UL)
N07  A                                      COLOR(YLW)
E10  A            SVPRE          1A  O  5 98DSPATR(UL)
N09  A                                      COLOR(YLW)
N11  A            SVVF           1A  O  5100DSPATR(UL)
N11  A                                      COLOR(YLW)
N01  A                                  7  2'V.B'
N01  A            SVFT01        28A  O  7  6TEXT('Fritekst')
N01  A                                      DSPATR(UL)
N01  A                                      COLOR(YLW)
S10  A*           SVNT01         6Y 0O  7 35TEXT('Antall')
N10  A            SVNT01         9Y 0O  7 35TEXT('Antall')
N01  A                                      DSPATR(UL)
N01  A                                      COLOR(YLW)
N01  A                                      EDTCDE(4)
E10  A            SVEH01         4A  O  7 45TEXT('Enhet')
N01  A                                      DSPATR(UL)
N01  A                                      COLOR(YLW)
E10  A            SVVT01        30A  O  7 50TEXT('Varetekst')
N01  A                                      DSPATR(UL)
N01  A                                      COLOR(YLW)
N01  A            SVFT02        28A  O  8  6TEXT('Fritekst')
N01  A                                      DSPATR(UL)
N01  A                                      COLOR(YLW)
S10  A*           SVNT02         6Y 0O  8 35TEXT('Antall')
N10  A            SVNT02         9Y 0O  8 35TEXT('Antall')
N01  A                                      DSPATR(UL)
N01  A                                      COLOR(YLW)
N01  A                                      EDTCDE(4)
E10  A            SVEH02         4A  O  8 45TEXT('Enhet')
N01  A                                      DSPATR(UL)
N01  A                                      COLOR(YLW)
E10  A            SVVT02        30A  O  8 50TEXT('Varetekst')
N01  A                                      DSPATR(UL)
N01  A                                      COLOR(YLW)
N01  A            SVFT03        28A  O  9  6TEXT('Fritekst')
N01  A                                      DSPATR(UL)
N01  A                                      COLOR(YLW)
S10  A*           SVNT03         6Y 0O  9 35TEXT('Antall')
N10  A            SVNT03         9Y 0O  9 35TEXT('Antall')
N01  A                                      DSPATR(UL)
N01  A                                      COLOR(YLW)
N01  A                                      EDTCDE(4)
E10  A            SVEH03         4A  O  9 45TEXT('Enhet')
N01  A                                      DSPATR(UL)
N01  A                                      COLOR(YLW)
E10  A            SVVT03        30A  O  9 50TEXT('Varetekst')
N01  A                                      DSPATR(UL)
N01  A                                      COLOR(YLW)
N01  A            SVFT04        28A  O 10  6TEXT('Fritekst')
N01  A                                      DSPATR(UL)
N01  A                                      COLOR(YLW)
S10  A*           SVNT04         6Y 0O 10 35TEXT('Antall')
N10  A            SVNT04         9Y 0O 10 35TEXT('Antall')
N01  A                                      DSPATR(UL)
N01  A                                      COLOR(YLW)
N01  A                                      EDTCDE(4)
E10  A            SVEH04         4A  O 10 45TEXT('Enhet')
N01  A                                      DSPATR(UL)
N01  A                                      COLOR(YLW)
E10  A            SVVT04        30A  O 10 50TEXT('Varetekst')
N01  A                                      DSPATR(UL)
N01  A                                      COLOR(YLW)
N01  A            SVFT05        28A  O 11  6TEXT('Fritekst')
N01  A                                      DSPATR(UL)
N01  A                                      COLOR(YLW)
S10  A*           SVNT05         6Y 0O 11 35TEXT('Antall')
N10  A            SVNT05         9Y 0O 11 35TEXT('Antall')
N01  A                                      DSPATR(UL)
N01  A                                      COLOR(YLW)
N01  A                                      EDTCDE(4)
E10  A            SVEH05         4A  O 11 45TEXT('Enhet')
N01  A                                      DSPATR(UL)
N01  A                                      COLOR(YLW)
E10  A            SVVT05        30A  O 11 50TEXT('Varetekst')
N01  A                                      DSPATR(UL)
N01  A                                      COLOR(YLW)
N01  A            SVFT06        28A  O 12  6TEXT('Fritekst')
N01  A                                      DSPATR(UL)
N01  A                                      COLOR(YLW)
S10  A*           SVNT06         6Y 0O 12 35TEXT('Antall')
N10  A            SVNT06         9Y 0O 12 35TEXT('Antall')
N01  A                                      DSPATR(UL)
N01  A                                      COLOR(YLW)
N01  A                                      EDTCDE(4)
E10  A            SVEH06         4A  O 12 45TEXT('Enhet')
N01  A                                      DSPATR(UL)
N01  A                                      COLOR(YLW)
N01  A*           SVVT06        30A  O 12 47TEXT('Varetekst')
N01  A*                                     DSPATR(UL)
N01  A*                                     COLOR(YLW)
N01  A            SVFT07        28A  O 13  6TEXT('Fritekst')
N01  A                                      DSPATR(UL)
N01  A                                      COLOR(YLW)
S10  A*           SVNT07         6Y 0O 13 35TEXT('Antall')
N10  A            SVNT07         9Y 0O 13 35TEXT('Antall')
N01  A                                      DSPATR(UL)
N01  A                                      COLOR(YLW)
N01  A                                      EDTCDE(4)
E10  A            SVEH07         4A  O 13 45TEXT('Enhet')
N01  A                                      DSPATR(UL)
N01  A                                      COLOR(YLW)
N01  A*           SVVT07        30A  O 13 47TEXT('Varetekst')
N01  A*                                     DSPATR(UL)
N01  A*                                     COLOR(YLW)
E10  A                                 13 50'Cont.nr:'
E10  A            SVCNR         17A  O 13 59TEXT('Fritekst')
N08  A                                      DSPATR(UL)
N08  A                                      COLOR(YLW)
E10  A  36                             13 77'Flere'
N08  A                                      COLOR(RED)
N01  A                                 15  2'T.O'
N01  A            SVTOP1        17A  O 15  6DSPATR(UL)
N01  A                                      COLOR(YLW)
N01  A                                 15 25'Ref.'
N01  A            SVCRE1         3A  O 15 30TEXT('KOMMUNIKASJONREF.')
N01  A                                      DSPATR(UL)
N01  A                                      COLOR(YLW)
N01  A                                 15 42'T.O'
N01  A            SVTOP2        17A  O 15 46DSPATR(UL)
N01  A                                      COLOR(YLW)
N01  A                                 15 65'Ref.'
N01  A            SVCRE2         3A  O 15 70TEXT('KOMMUNIKASJONREF.')
N01  A                                      DSPATR(UL)
N01  A                                      COLOR(YLW)
N01  A            SVTOP3        17A  O 16  6DSPATR(UL)
N01  A                                      COLOR(YLW)
N01  A            SVCRE3         3A  O 16 30TEXT('KOMMUNIKASJONREF.')
N01  A                                      DSPATR(UL)
N01  A                                      COLOR(YLW)
N01  A            SVTOP4        17A  O 16 46DSPATR(UL)
N01  A                                      COLOR(YLW)
N01  A            SVCRE4         3A  O 16 70TEXT('KOMMUNIKASJONREF.')
N01  A                                      DSPATR(UL)
N01  A                                      COLOR(YLW)
N01  A            SVTOP5        17A  O 17  6DSPATR(UL)
N01  A                                      COLOR(YLW)
N01  A            SVCRE5         3A  O 17 30TEXT('KOMMUNIKASJONREF.')
N01  A                                      DSPATR(UL)
N01  A                                      COLOR(YLW)
N01  A            SVTOP6        17A  O 17 46DSPATR(UL)
N01  A                                      COLOR(YLW)
N01  A            SVCRE6         3A  O 17 70TEXT('KOMMUNIKASJONREF.')
N01  A                                      DSPATR(UL)
N01  A                                      COLOR(YLW)
N01  A            SVTOP7        17A  O 18  6DSPATR(UL)
N01  A                                      COLOR(YLW)
N01  A            SVCRE7         3A  O 18 30TEXT('KOMMUNIKASJONREF.')
N01  A                                      DSPATR(UL)
N01  A                                      COLOR(YLW)
N01  A            SVTOP8        17A  O 18 46DSPATR(UL)
N01  A                                      COLOR(YLW)
N01  A            SVCRE8         3A  O 18 70TEXT('KOMMUNIKASJONREF.')
N01  A                                      DSPATR(UL)
N01  A                                      COLOR(YLW)
N01  A            SVTOP9        17A  O 19  6DSPATR(UL)
N01  A                                      COLOR(YLW)
N01  A            SVCRE9         3A  O 19 30TEXT('KOMMUNIKASJONREF.')
N01  A                                      DSPATR(UL)
N01  A                                      COLOR(YLW)
N01  A            SVTOP10       17A  O 19 46DSPATR(UL)
N01  A                                      COLOR(YLW)
N01  A            SVCRE10        3A  O 19 70TEXT('KOMMUNIKASJONREF.')
N01  A                                      DSPATR(UL)
N01  A                                      COLOR(YLW)
N01  A                                 26  2'F12=Forrige'
N01  A                                      COLOR(BLU)
N08  A  36                             26 22'F18=Vis containernr'
N08  A                                      COLOR(BLU)
