********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
12209 * 01 CB  PTF12 Oppdater Tullid i FRISOK
********************************************************************
     FEDIM      IF   E           K DISK
     FSVIH      UF   E           K DISK
     FSVEH      UF   E           K DISK
     FSVIHH     O  A E             DISK
     FSVEHH     O  A E             DISK
N01  FFRISOK    IF A E           K DISK
      *________________________
      * DS for decimal handling
      *""""""""""""""""""""""""
     D                 DS
     D  WMN2                   1      9  0
      */////////////
      * MAIN LOGIC /
      */////////////
      *___________
      * Initiering
      *"""""""""""
     C                   EXSR      INIT
      *________
      * CONVERT
      *""""""""
     C  N51              EXSR      SRA

     C                   MOVE      '1'           *INLR
      *////////////////////////////////////////////////////////////
      *                                                      INIT /
      *////////////////////////////////////////////////////////////
     C     INIT          BEGSR
     C     *LIKE         DEFINE    MMN           ZMN
     C     *LIKE         DEFINE    SVIH_SYAV     WVIH_SYAV
     C     *LIKE         DEFINE    SVIH_SYOP     WVIH_SYOP
N01  C     *LIKE         DEFINE    FSSOK         WFSSOK
N01  C     *LIKE         DEFINE    FSKODE        WFSKODE
N01  C                   EVAL      WFSKODE='TID'
      * INIT WORKFIELDS

     C     *ENTRY        PLIST
     C                   PARM                    WMN1              9
     C                   MOVEL     WMN1          WMN2
     C                   Z-ADD     WMN2          ZMN
      * Definiere Nøkkler
     C     SVIH_KEY      KLIST
     C                   KFLD                    WVIH_SYAV
     C                   KFLD                    WVIH_SYOP
N01  C     FRIKEY        KLIST
N01  C                   KFLD                    WVIH_SYAV
N01  C                   KFLD                    WVIH_SYOP
N01  C                   KFLD                    WFSKODE
N01  C                   KFLD                    WFSSOK
      * Get msg and interchange values
     C     ZMN           CHAIN     EDIM                               51

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * Vilken typ av svar ?                                  SRA /
      *////////////////////////////////////////////////////////////
     C     SRA           BEGSR
      *_________________
      * Finnes ærendet ?
      *"""""""""""""""""
     C                   MOVEL     M0062         WVIH_SYAV
     C     7             SUBST     M0062:5       M0062B            7
     C                   MOVE      M0062B        WVIH_SYOP
N01  C                   CLEAR                   FRISOKR
N01  C                   EVAL      FSAVD=WVIH_SYAV
N01  C                   EVAL      FSOPD=WVIH_SYOP
N01  C                   EVAL      FSKODE='TID'
N01  C                   MOVEL     MDT           FSDTOP
N01  C                   EVAL      FSDOKK='G'
     C     SVIH_KEY      CHAIN(N)  SVIH                               51
     C                   IF        *IN51 = *OFF
     C                   EXSR      SRAI
     C                   EVAL      WATYPE='SI'
     C                   EVAL      WUSER=SVIH_SGID
N01  C                   MOVEL     SVIH_TUID     WFSSOK
N01  C     FRIKEY        CHAIN     FRISOK                             59
N01  C                   IF        *IN59
N01  C                   MOVEL     SVIH_TUID     FSSOK
N01  C     FSSOK         CAT       SVIH_MTYP:1   FSSOK
N01  C                   WRITE     FRISOKR
N01  C                   ENDIF
     C                   ELSE
     C     SVIH_KEY      CHAIN(N)  SVEH                               51
     C                   IF        *IN51 = *OFF
     C                   EXSR      SRAE
     C                   EVAL      WATYPE='SE'
     C                   EVAL      WUSER=SVEH_SGID
N01  C                   MOVEL     SVEH_TUID     WFSSOK
N01  C     FRIKEY        CHAIN     FRISOK                             59
N01  C                   IF        *IN59
N01  C                   MOVEL     SVEH_TUID     FSSOK
N01  C     FSSOK         CAT       SVEH_MTYP:1   FSSOK
N01  C                   WRITE     FRISOKR
N01  C                   ENDIF
     C                   ENDIF
     C                   ENDIF
      *_________________________
      * Aktivera arkivfunktionen
      *"""""""""""""""""""""""""
     C                   CALL      'ARC002CLB'
     C                   PARM                    WATYPE            2
     C                   PARM                    WUSER            10
     C
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * Import                                               SRAI /
      *////////////////////////////////////////////////////////////
     C     SRAI          BEGSR
      *______________________
      * Skriv til historikfil
      *""""""""""""""""""""""
     C                   CLEAR                   SVIHHR
     C                   EVAL      SVIHH_SYA = SVIH_SYAV
     C                   EVAL      SVIHH_SYO = SVIH_SYOP
     C                   MOVEL     MDT           SVIHH_DTH
     C                   MOVEL     MTM           SVIHH_TMH
     C                   EVAL      SVIHH_SR  = 'R'
     C                   EVAL      SVIHH_TXH  = 'CUSRESU-IEMD KVT'
     C                   WRITE     SVIHHR
      *__________________
      * Uppdatera ærendet
      *""""""""""""""""""
     C     SVIH_KEY      CHAIN     SVIH                               61
     C                   IF        *IN61 = *OFF
     C                   IF        SVIH_SYST <> 'G'
     C                   EVAL      SVIH_SYST='O'
     C                   UPDATE    SVIHR
     C                   ENDIF
     C                   ENDIF
      *_________________
      * Skriv ut ærendet
      *"""""""""""""""""
     C                   MOVE      'C'           CALLTYP
     C                   MOVE      SVIH_SYAV     SVIHASYAV         4
     C                   MOVE      SVIH_SYOP     SVIHASYOP         7
     C                   CALL      'SVI006R'
     C                   PARM                    SVIHASYAV
     C                   PARM                    SVIHASYOP
     C                   PARM                    CALLTYP           1

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * Export                                               SRAE /
      *////////////////////////////////////////////////////////////
     C     SRAE          BEGSR
      *______________________
      * Skriv til historikfil
      *""""""""""""""""""""""
     C                   CLEAR                   SVEHHR
     C                   EVAL      SVEHH_SYA = SVEH_SYAV
     C                   EVAL      SVEHH_SYO = SVEH_SYOP
     C                   MOVEL     MDT           SVEHH_DTH
     C                   MOVEL     MTM           SVEHH_TMH
     C                   EVAL      SVEHH_SR  = 'R'
     C                   EVAL      SVEHH_TXH  = 'CUSRESU-IEMD KVT'
     C                   WRITE     SVEHHR
      *__________________
      * Uppdatera ærendet
      *""""""""""""""""""
     C     SVIH_KEY      CHAIN     SVEH                               61
     C                   IF        *IN61 = *OFF
     C                   IF        SVEH_SYST <> 'G'
     C                   EVAL      SVEH_SYST='O'
     C                   UPDATE    SVEHR
     C                   ENDIF
     C                   ENDIF
      *_________________
      * Skriv ut ærendet (ikke intill videre, kan sleppes ved behov)
      *"""""""""""""""""
     C                   MOVE      'C'           CALLTYP
     C                   MOVE      SVEH_SYAV     SVIHASYAV
     C                   MOVE      SVEH_SYOP     SVIHASYOP
     C                   CALL      'SVE006R'
     C                   PARM                    SVIHASYAV
     C                   PARM                    SVIHASYOP
     C                   PARM                    CALLTYP           1
     C                   ENDSR
