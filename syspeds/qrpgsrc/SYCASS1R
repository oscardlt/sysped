********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
P10XXX* 01 CB  PTF10 Feil ved moms på DCO/DCR linjer
P10XXX* 02 CB  PTF10 Feil ved - på DCO linjer
P10114* 03 CB  PTF10 Feil ved - på DCO/DCR linjer
******* obs linjer merket ADR er for å test adressekunde  **********
P10XXX* 04 SH  PTF10 merket F-linjer slettet nå behandlet i CASS
      *
      * Dette programmet startes fra CLP: SYCASS1C som i sin tur startes fra
      * SYEDI.
      * Datane kommer via kommunikasjon fra IATA CASS avregningskontor.
      */////////////////////////////////////////////////////////////////////////
     Ffirm      IF   E           K DISK
     Ffakt      IF   E           K DISK
     Fkosbu12   UF   E           K DISK
     FKODTCA    IF   E             DISK
     FKODTA     IF   E           K DISK
     FEDIRE1K   IF   E           K DISK
     FKOSTT     UF   E           K DISK
     FKOSTA     O  A E             DISK
     FKOSTB4    UF A E           K DISK
     FHEADF     IF   E           K DISK
     FHEAD17    IF   E           K DISK
     F                                     RENAME(HEADFR:HEADFR17)
     FHEAD18    IF   E           K DISK
     F                                     RENAME(HEADFR:HEADFR18)
     FFRISOL01  IF   E           K DISK
     FSAMLB     IF   E           K DISK
S04  F*AK2      IF   E           K DISK
N04  FFAK2      UF   E           K DISK
     F                                     RENAME(faktr:faktr2)
     FVALUL01   IF   E           K DISK
     FKODTGE    IF   E           K DISK
     FSYCASS1P  O    E             PRINTER OFLIND(*IN27)
      *_________________
      * Arrays for input
      *"""""""""""""""""
     D                 DS
     D  RDATA                  1   1024
     D  RDATA1                 1      3
     D                 DS
     D  DS_AAA                 1   1024
     D   ARID                  1      3
     D   AACODE                4      5
     D   AAIRPR                6      8S 0
     D   ASTRDT                9     14S 0
     D   AENDDT               15     20S 0
     D   ABLDT                21     26S 0
     D   AFILNR               27     28
     D   ACURR                29     31
     D   ARES1                32    230
     D   ARES2               231    250
     D                 DS
     D  DS_DCO                 1   1024
     D   ORID                  1      3
     D   OFILL1                4      4
     D   OVAT                  5      5
     D   HEKEY_DCO             6     16
     D   OAIRPR                6      8S 0
     D   OAWBNO                9     16S 0
     D   OFILL2               17     18
     D   OORIGN               19     21
     D   OAGNCD               22     32S 0
     D   ODCMN0               33     38
     D   OCURR                39     41
     D   OEXRAT               42     52S 0
     D   OEXDT                53     58S 0
     D   OPRSI1               59     59
     D   OWHGCG               60     71S 2
     D   OPRSI2               72     72
     D   OVOLCG               73     84S 2
     D   OPRSI3               85     85
     D   OTAX                 86     97S 2
     D   OPRCI4               98     98
     D   OAWBN                99    110S 2
     D   OPRSI5              111    111
     D   OCDC                112    123S 2
     D   OVAT1               124    135S 2
     D   OCOMM               136    147S 2
     D   OVAT2               148    159S 2
     D   ODISC               160    171S 2
     D   ODISCI              172    172
     D   OWGHTI              173    173
     D   OWGHT               174    180S 2
     D   ODEST               181    183
     D   OFILL3              184    250
     D                 DS
     D  DS_DCR                 1   1024
     D   RRID                  1      3
     D   RFILL1                4      4
     D   RVATI                 5      5
     D   HEKEY_DCR             6     16
     D   RAIRPR                6      8S 0
     D   RAWBNO                9     16S 0
     D   RFILL2               17     18
     D   RORIGN               19     21
     D   RAGNCD               22     32S 0
     D   RDCMNO               33     38S 0
     D   RCURR                39     41
     D   REXRAT               42     52S 0
     D   REXDT                53     58S 0
     D   RPRSI1               59     59
     D   RWHGCG               60     71S 2
     D   RPRSI2               72     72
     D   RVOLCG               73     84S 0
     D   RPRSI3               85     85
     D   RTAX                 86     97S 0
     D   RPRCI4               98     98
     D   RAWBN                99    110S 2
     D   RPRSI5              111    111
     D   RCDC                112    123S 2
S01  D*  RVAT1               124    135S 0
N01  D   RVAT1               124    135S 2
     D   RCOMM               136    147S 2
S01  D*  RVAT2               148    159S 0
N01  D   RVAT2               148    159S 2
     D   RDISC               160    171S 2
     D   RDISCI              172    172
     D   RWGHTI              173    173
     D   RWGHT               174    180S 0
     D   RDEST               181    183
     D   RFILL3              184    250
     D                 DS
     D  DS_AWM                 1   1024
     D   MRID                  1      3
     D   MFILL1                4      4
     D   MVATI                 5      5
     D   HEKEY                 6     16
     D   MAIRPR                6      8S 0
     D   MAWBNO                9     16S 0
     D   MFILL2               17     18
     D   MORIGN               19     21
     D   MAGNCD               22     32S 0
     D   MUSEI                33     33
     D   MLI                  34     34
     D   MFILL3               35     36
     D   MDEST                37     39
     D   MAWEDT               40     45S 0
     D   MWHGGR               46     52S 1
     D   MWHGI                53     53
     D   MCURR                54     56S 0
     D   MWHGCP               57     68S 2
     D   MVALCP               69     80S 0
     D   MDCCHP               81     92S 2
     D   MDACHP               93    104S 0
     D   MWHGCC              105    116S 0
     D   MVALCC              117    128S 0
     D   MDCCHC              129    140S 0
     D   MDACHC              141    152S 2
     D   MCOMMP              153    156S 0
     D   MDISC               157    168S 2
     D   MTAXI               169    169
     D   MFILL4              170    193
     D   MAWBDT              194    199S 0
     D   MRATE               200    210S 0
     D   MDISC2              211    222S 2
     D   MTAXDC              223    230S 2
     D   MTAXDA              231    238S 2
     D   MRESS               239    249
     D   MFILL5              250    250
     D                 DS
     D  DS_TTT                 1   1024
     D   TRID                  1      3
     D   TFILL1                4      4
     D   TCOD1                 5      6
     D   TCOD2                 7      9S 0
     D   TAIRPR               10     12S 0
     D   TFILL2               13     13
     D   TNAWB                14     20S 0
     D   TNCCA                21     27S 0
     D   TNDCM                28     34S 0
     D   TNREC                35     41S 0
     D   TFILL3               42    250
      */////////////
      * MAIN LOGIC /
      */////////////
     C                   EXSR      SRINIT
     C                   EXSR      SRA
     C                   EVAL      *INLR=*ON
      *////////////////////////////////////////////////////////////
      * SR                                                    SRA /
      *////////////////////////////////////////////////////////////
     C     SRA           BEGSR
      *________________
      * Les CASS record
      *""""""""""""""""
     C     WMBR          SETLL     EDIRE1K
     C     WMBR          READE     EDIRE1K                                51
     C     *IN51         DOWEQ     '0'

     C                   SELECT
     C     RDATA1        WHENEQ    'AAA'
     C                   MOVEL     RDATA         DS_AAA
     C                   EXSR      SR_AAA
     C     RDATA1        WHENEQ    'DCO'
     C     RDATA1        OREQ      'CCO'
     C                   MOVEL     RDATA         DS_DCO
     C                   EXSR      SR_DCO
     C     RDATA1        WHENEQ    'DCR'
     C     RDATA1        OREQ      'CCR'
     C                   MOVEL     RDATA         DS_DCR
     C                   EXSR      SR_DCR
     C     RDATA1        WHENEQ    'AWM'
     C                   MOVEL     RDATA         DS_AWM
     C                   EXSR      SR_AWM
     C     RDATA1        WHENEQ    'TTT'
     C                   MOVEL     RDATA         DS_TTT
     C                   EXSR      SR_TTT
     C                   ENDSL

     C     WMBR          READE     EDIRE1K                                51
     C                   ENDDO
      *______________________
      * Sum per flyselskap LR
      *""""""""""""""""""""""
     C                   WRITE     SYCASS1PC
     C                   IF        *IN27
     C                   EVAL      *IN27=*OFF
     C                   WRITE     SYCASS1PA
     C                   ENDIF
      *____________________
      * Skriv ut bunn liste
      *""""""""""""""""""""
     C                   WRITE     SYCASS1PØ
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                 SR_AAA /
      *////////////////////////////////////////////////////////////
     C     SR_AAA        BEGSR
      *______________
      * Init av KOSTA
      *""""""""""""""
     C                   CLEAR                   KOSTAR
     C                   EVAL      KALNR=KODTCALN                               CASS
     C                   EVAL      KAVAL=ACURR
     C                   EVAL      KAFNR=AFILNR
     C                   EVAL      KAFDT=20000000 + ABLDT
     C                   EVAL      KASG='EDI'
     C                   EVAL      KATXT='CASS Elektronisk mottat'
     C                   EVAL      KAMVA='F'
      *________________
      * Hent valutakurs
      *""""""""""""""""
     C     VALKEY        CHAIN     VALUL01                            35
     C                   IF        *IN35=*OFF
     C                   EVAL      KAVKU=VALKU1
     C                   EVAL      KAOMRF=OMRFAK
     C                   ENDIF
      *__________________
      * Hent bilagsnummer
      *""""""""""""""""""
     C     'Ø'           CHAIN     KOSTT                              20
     C                   IF        *IN20=*OFF
     C                   EVAL      KABNR=KTNR
     C                   EVAL      KTNR=KTNR + 1
     C                   UPDATE    KOSTTR
     C                   ENDIF
      *_______________________
      * Skriv ut heading liste
      *"""""""""""""""""""""""
     C                   WRITE     SYCASS1PA
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                 SR_DCO /
      *////////////////////////////////////////////////////////////
     C     SR_DCO        BEGSR
N01  C                   EVAL      *IN41=*ON
      *_______________
      * Init av KOSTB4
      *"""""""""""""""
     C                   CLEAR                   KOSTBR
     C                   EVAL      KBBNR=KABNR
     C                   EVAL      KBVK=KODTCAFE
      * Hent oppdrag
     C                   MOVE      *BLANKS       HEKEY17
     C                   MOVE      *BLANKS       HEKEY18
     C                   MOVE      *BLANKS       W_FSSOK
XRFA C*                  MOVE      HEKEY_DCO     HEKEY17
XRFA C                   EVAL      HEKEY17 = '    '+HEKEY_DCO
     C                   MOVE      HEKEY_DCO     HEKEY18
     C                   MOVEL     HEKEY_DCO     W_FSSOK
     C     HEKEY18       CHAIN     HEAD18                             20
     C   20HEKEY17       CHAIN     HEAD17                             20
     C                   IF        *IN20=*ON
     C     FRIKEY        CHAIN     FRISOL01                           20
     C  N20HEADKEY       CHAIN     HEADF                              20
     C                   ENDIF
     C                   IF        *IN20=*OFF
     C     TRSTA4        IFEQ      'I'
     C     TRSTA4        OREQ      'J'
     C                   EVAL      KBVK=KODTCAFN
     C                   END
     C                   EVAL      KBAVD=HEAVD
     C                   EVAL      KBOPD=HEOPD
     C                   EVAL      KBNØKK=0
     C                   EXSR      HENTKU
      *

     C                   ELSE

     C                   EVAL      KBNØKK=9

     C                   ENDIF
     C     OPRSI1        IFEQ      'C'
     C                   EVAL      KBBLHB=OAWBN+OCOMM+ODISC
     C                   Z-SUB     KBBLHB        KBBLHB
     C                   ELSE
     C                   EVAL      KBBLHB=OWHGCG+OCDC-OCOMM-ODISC
     C                   ENDIF
S02  C*    ODISCI        IFEQ      '-'
N02  C     ODISCI        IFNE      '-'
     C                   Z-SUB     KBBLHB        KBBLHB
     C                   ENDIF
     C                   EVAL      KABL=KABL-KBBLHB
     C                   EVAL      KBKDPF='F'
     C                   EVAL      KBKDM=0
     C                   EVAL      KBBILT='CASS Avregning'
      *_________________
      * Skriv til KOSTB4
      *"""""""""""""""""
      * Ved Fly eksport samlast skal kostnaden fordeles på alle HAWB, egen SR
     C     HEUR          IFEQ      'D'
     C     HESAML        ANDNE     *BLANKS
     C     HEKEY18       READE     HEAD18                                 21
     C     *IN21         IFEQ      *OFF
     C     HEKEY18       SETLL     HEAD18
     C     HEKEY18       READE     HEAD18                                 21
N01  C                   EVAL      *IN41=*OFF
     C                   EXSR      SR_DCO_M
     C                   ELSE
     C     KBBLHB        MULT      VALKU1        XWORK            17 5
     C     XWORK         DIV(H)    OMRFAK        KBBLHB
     C                   WRITE     KOSTBR
     c                   exsr      printsr
     C                   ENDIF
     C                   ELSE
     C     KBBLHB        MULT      VALKU1        XWORK            17 5
     C     XWORK         DIV(H)    OMRFAK        KBBLHB
     C                   WRITE     KOSTBR
     c                   exsr      printsr
     C                   ENDIF
N01  C                   EVAL      *IN41=*OFF
     c                   endsr
      ********************************************************************************
     c     printsr       begsr
      ********************************************************************************
      *________________
      * Skriv til liste
      *""""""""""""""""
     C     *IN20         IFEQ      *ON
     C                   EVAL      WMARK1='FINNER IKKE OPPDRAG '
     C                   EVAL      WMARK2='                    '
     C                   EVAL      WEAVD=0
     C                   EVAL      WEOPD=0
     C                   ELSE
     C                   EVAL      WMARK1='                    '
     C                   EVAL      WMARK2='                    '
     C                   EVAL      WEAVD=HEAVD
     C                   EVAL      WEOPD=HEOPD
     C                   ENDIF
     C                   EVAL      WWHGCP=0
     C                   EVAL      WDACHC=0
     C                   EVAL      WDISC=0
     C                   EVAL      WKABL=KBBLHB
     C                   EVAL      WWHGGR=0
     C                   EVAL      WDCCHP=0
     C                   EVAL      WDISC2=0
     C                   EVAL      WTAXD=0
     C                   EVAL      MAIRPR=OAIRPR
     C                   EVAL      MAWBNO=OAWBNO
     C                   EVAL      MORIGN=OORIGN
     C                   EVAL      MDEST=ODEST
N01  C                   IF        *IN41
N01  C                   EVAL      WTAXD=OVAT2-OVAT1
N01  C                   SUB       OVAT1         TAX_KBBLHB
N01  C                   ADD       OVAT2         TAX_KBBLHB
N01  C                   ENDIF
      * skal muligens sum per flyselskap ut først ?
     C                   EXSR      SUMAIR
      * henter fakturert og budsjett
     c                   exsr      faktsr
     c                   exsr      budsr
     c*
     C                   WRITE     SYCASS1PB
     C                   IF        *IN27
     C                   EVAL      *IN27=*OFF
     C                   WRITE     SYCASS1PA
     C                   ENDIF
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                               SR_DCO_M /
      *////////////////////////////////////////////////////////////
     C     SR_DCO_M      BEGSR

      * Summer først totalvekt for alle HAWB
      * En post er allerede lest, det er bare å fortsette og lese

     C                   EVAL      WEVKT=0
     C                   EVAL      X_KBBLHB=KBBLHB
     C                   EVAL      W_KBBLHB=0
     C     *IN20         DOWEQ     *OFF
     C                   ADD       HEVKT         WEVKT
     C     HEKEY18       READE     HEAD18                                 20
     C                   ENDDO

      * Fordel etter vekt og skriv en KOSTB4 post for hver HAWB

     C                   EVAL      *IN20=*OFF
     C     HEKEY18       SETLL     HEAD18
     C     HEKEY18       READE     HEAD18                                 20
     C     *IN20         DOWEQ     *OFF

     C     OPRSI1        IFEQ      'C'
     C                   EVAL      KBBLHB=((OAWBN+OCOMM+ODISC)
     C                             / (WEVKT / HEVKT))
     C                   ELSE
     C                   EVAL      KBBLHB=((OWHGCG+OCDC-OCOMM-ODISC)
     C                             / (WEVKT / HEVKT))
     C                   ENDIF

N02  C     ODISCI        IFNE      '-'
     C                   Z-SUB     KBBLHB        KBBLHB
N02  C                   ENDIF

     C                   EVAL      KBNØKK=3
     C                   EVAL      KBKAVD=HEAVD
     C                   EVAL      KBOPD=HEOPD
     C                   EVAL      KBKKEY=HEGN
     C                   EXSR      HENTKU
     C     KBBLHB        MULT      VALKU1        XWORK            17 5
     C     XWORK         DIV(H)    OMRFAK        KBBLHB
     C                   WRITE     KOSTBR
     C                   ADD       KBBLHB        W_KBBLHB
     C                   exsr      printsr

     C     HEKEY18       READE     HEAD18                                 20
     C                   ENDDO
      * legg øresutjevning på  siste post
     C                   EVAL      Y_KBBLHB=X_KBBLHB-W_KBBLHB
     C     Y_KBBLHB      IFNE      0
     C     KOSBKEY       CHAIN     KOSTB4                             20
     C     *IN20         IFEQ      *OFF
     C                   ADD       Y_KBBLHB      KBBLHB
     C                   UPDATE    KOSTBR
     C                   ENDIF
     C                   ENDIF

     C                   EVAL      *IN20=*OFF

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                 SR_DCR /
      *////////////////////////////////////////////////////////////
     C     SR_DCR        BEGSR
N01  C                   EVAL      *IN41=*ON
      *_______________
      * Init av KOSTB4
      *"""""""""""""""
     C                   CLEAR                   KOSTBR
     C                   EVAL      KBBNR=KABNR
     C                   EVAL      KBVK=KODTCAFE
      * Hent oppdrag
     C                   MOVE      *BLANKS       HEKEY17
     C                   MOVE      *BLANKS       HEKEY18
     C                   MOVE      *BLANKS       W_FSSOK
XRFA C*                  MOVE      HEKEY_DCR     HEKEY17
XRFA C                   EVAL      HEKEY17 = '    '+HEKEY_DCR
     C                   MOVE      HEKEY_DCR     HEKEY18
     C                   MOVEL     HEKEY_DCR     W_FSSOK
     C     HEKEY18       CHAIN     HEAD18                             20
     C   20HEKEY17       CHAIN     HEAD17                             20
     C                   IF        *IN20=*ON
     C     FRIKEY        CHAIN     FRISOL01                           20
     C  N20HEADKEY       CHAIN     HEADF                              20
     C                   ENDIF
     C                   IF        *IN20=*OFF
     C     TRSTA4        IFEQ      'I'
     C     TRSTA4        OREQ      'J'
     C                   EVAL      KBVK=KODTCAFN
     C                   END
     C                   EVAL      KBAVD=HEAVD
     C                   EVAL      KBOPD=HEOPD
     C                   EVAL      KBNØKK=0
     C                   EXSR      HENTKU
     C                   ELSE

     C                   EVAL      KBNØKK=9

     C                   ENDIF
N03  C     RDISCI        IFEQ      '-'
N03  C                   Z-SUB     RDISC         RDISC
N03  C                   ENDIF
     C     RPRSI1        IFEQ      'C'
     C                   EVAL      KBBLHB=RAWBN+RCOMM+RDISC
     C                   Z-SUB     KBBLHB        KBBLHB
     C                   ELSE
     C                   EVAL      KBBLHB=RWHGCG+RCDC-RCOMM-RDISC
     C                   ENDIF
S03  C*                  Z-SUB     KBBLHB        KBBLHB
S03  C*    RDISCI        IFEQ      '-'
S03  C*                  Z-SUB     KBBLHB        KBBLHB
S03  C*                  ENDIF
     C                   EVAL      KABL=KABL-KBBLHB
     C                   EVAL      KBKDPF='F'
     C                   EVAL      KBKDM=0
     C                   EVAL      KBBILT='CASS Avregning'
      *________________
      * Skriv til KOSTB4
      *""""""""""""""""
      * Ved Fly eksport samlast skal kostnaden fordeles på alle HAWB, egen SR
     C     HEUR          IFEQ      'D'
     C     HESAML        ANDNE     *BLANKS
     C     HEKEY18       READE     HEAD18                                 21
     C     *IN21         IFEQ      *OFF
     C     HEKEY18       SETLL     HEAD18
     C     HEKEY18       READE     HEAD18                                 21
N01  C                   EVAL      *IN41=*OFF
     C                   EXSR      SR_DCR_M
     C                   ELSE
     C     KBBLHB        MULT      VALKU1        XWORK            17 5
     C     XWORK         DIV(H)    OMRFAK        KBBLHB
     C                   WRITE     KOSTBR
     c                   exsr      print2sr
     C                   ENDIF
     C                   ELSE
     C     KBBLHB        MULT      VALKU1        XWORK            17 5
     C     XWORK         DIV(H)    OMRFAK        KBBLHB
     C                   WRITE     KOSTBR
     c                   exsr      print2sr
     C                   ENDIF
N01  C                   EVAL      *IN41=*OFF
     c                   endsr
      *******************************************************************
     c     print2sr      begsr
      *******************************************************************
      *________________
      * Skriv til liste
      *""""""""""""""""
     C     *IN20         IFEQ      *ON
     C                   EVAL      WMARK1='FINNER IKKE OPPDRAG '
     C                   EVAL      WMARK2='                    '
     C                   EVAL      WEAVD=0
     C                   EVAL      WEOPD=0
     C                   ELSE
     C                   EVAL      WMARK1='                    '
     C                   EVAL      WMARK2='                    '
     C                   EVAL      WEAVD=HEAVD
     C                   EVAL      WEOPD=HEOPD
     C                   ENDIF
     C                   EVAL      WWHGCP=0
     C                   EVAL      WDACHC=0
     C                   EVAL      WDISC=0
     C                   EVAL      WKABL=KBBLHB
     C                   EVAL      WWHGGR=0
     C                   EVAL      WDCCHP=0
     C                   EVAL      WDISC2=0
     C                   EVAL      WTAXD=0
     C                   EVAL      MAIRPR=RAIRPR
     C                   EVAL      MAWBNO=RAWBNO
     C                   EVAL      MORIGN=RORIGN
     C                   EVAL      MDEST=RDEST
N01  C                   IF        *IN41
N01  C                   EVAL      WTAXD=RVAT1-RVAT2
N01  C                   ADD       RVAT1         TAX_KBBLHB
N01  C                   SUB       RVAT2         TAX_KBBLHB
N01  C                   ENDIF
      * skal muligens sum per flyselskap ut først ?
     C                   EXSR      SUMAIR
      * henter fakturert og budsjett
     c                   exsr      faktsr
     c                   exsr      budsr
     C                   WRITE     SYCASS1PB
     C                   IF        *IN27
     C                   EVAL      *IN27=*OFF
     C                   WRITE     SYCASS1PA
     C                   ENDIF
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                               SR_DCR_M /
      *////////////////////////////////////////////////////////////
     C     SR_DCR_M      BEGSR

      * Summer først totalvekt for alle HAWB
      * En post er allerede lest, det er bare å fortsette og lese

     C                   EVAL      WEVKT=0
     C                   EVAL      X_KBBLHB=KBBLHB
     C                   EVAL      W_KBBLHB=0
     C     *IN20         DOWEQ     *OFF
     C                   ADD       HEVKT         WEVKT
     C     HEKEY18       READE     HEAD18                                 20
     C                   ENDDO

      * Fordel etter vekt og skriv en KOSTB4 post for hver HAWB

     C                   EVAL      *IN20=*OFF
     C     HEKEY18       SETLL     HEAD18
     C     HEKEY18       READE     HEAD18                                 20
     C     *IN20         DOWEQ     *OFF

N03  C     RDISCI        IFEQ      '-'
N03  C                   Z-SUB     RDISC         RDISC
N03  C                   ENDIF
     C     RPRSI1        IFEQ      'C'
     C                   EVAL      KBBLHB=((RAWBN+RCOMM+RDISC)
     C                             / (WEVKT / HEVKT))
     C                   ELSE
     C                   EVAL      KBBLHB=((RWHGCG+RCDC-RCOMM-RDISC)
     C                             / (WEVKT / HEVKT))
     C                   ENDIF
S03  C*    RDISCI        IFEQ      '-'
S03  C*                  Z-SUB     KBBLHB        KBBLHB
S03  C*                  ENDIF
     C                   EVAL      KBNØKK=3
     C                   EVAL      KBKAVD=HEAVD
     C                   EVAL      KBOPD=HEOPD
     C                   EVAL      KBKKEY=HEGN
     C                   EXSR      HENTKU
     C     KBBLHB        MULT      VALKU1        XWORK            17 5
     C     XWORK         DIV(H)    OMRFAK        KBBLHB
     C                   WRITE     KOSTBR
     C                   ADD       KBBLHB        W_KBBLHB
     C                   exsr      print2sr
     C     HEKEY18       READE     HEAD18                                 20
     C                   ENDDO
      * legg øresutjevning på  siste post
     C                   EVAL      Y_KBBLHB=X_KBBLHB-W_KBBLHB
     C     Y_KBBLHB      IFNE      0
     C     KOSBKEY       CHAIN     KOSTB4                             20
     C     *IN20         IFEQ      *OFF
     C                   ADD       Y_KBBLHB      KBBLHB
     C                   UPDATE    KOSTBR
     C                   ENDIF
     C                   ENDIF

     C                   EVAL      *IN20=*OFF

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                 SR_AWM /
      *////////////////////////////////////////////////////////////
     C     SR_AWM        BEGSR
      *______________
      * Init av KOSTB4
      *""""""""""""""
     C                   CLEAR                   KOSTBR
     C                   EVAL      KBBNR=KABNR
     C                   EVAL      KBVK=KODTCAFE
      * Hent oppdrag
XRFA C*                  MOVE      *BLANKS       HEKEY17          15
XRFA C                   MOVE      *BLANKS       HEKEY17          35
     C                   MOVE      *BLANKS       HEKEY18          15
     C                   MOVE      *BLANKS       W_FSSOK
XRFA C*                  MOVE      HEKEY         HEKEY17
XRFA C                   EVAL      HEKEY17 = '    '+HEKEY
     C                   MOVE      HEKEY         HEKEY18
     C                   MOVEL     HEKEY         W_FSSOK
     C     HEKEY18       CHAIN     HEAD18                             20
     C   20HEKEY17       CHAIN     HEAD17                             20
     C                   IF        *IN20=*ON
     C     FRIKEY        CHAIN     FRISOL01                           20
     C  N20HEADKEY       CHAIN     HEADF                              20
     C                   ENDIF
     C                   IF        *IN20=*OFF
     C     TRSTA4        IFEQ      'I'
     C     TRSTA4        OREQ      'J'
     C                   EVAL      KBVK=KODTCAFN
     C                   END
     C                   EVAL      KBAVD=HEAVD
     C                   EVAL      KBOPD=HEOPD
     C                   EVAL      KBNØKK=0
     C                   EXSR      HENTKU

     C                   ELSE

     C                   EVAL      KBNØKK=9

     C                   ENDIF
     C     MFILL5        IFEQ      '-'
     C                   Z-SUB     MDISC2        MDISC2
     C                   ENDIF
     C                   EVAL      KBBLHB=MWHGCP+MDCCHP-MDACHC-MDISC-MDISC2
     C                   EVAL      KABL=KABL-KBBLHB
     C                   EVAL      KBKDPF='F'
     C                   EVAL      KBKDM=0
     C                   EVAL      KBBILT='CASS Avregning'
      *________________
      * Skriv til KOSTB4
      *""""""""""""""""
      * Ved Fly eksport samlast skal kostnaden fordeles på alle HAWB, egen SR
     C     HEUR          IFEQ      'D'
     C     HESAML        ANDNE     *BLANKS
     C     HEKEY18       READE     HEAD18                                 21
     C     *IN21         IFEQ      *OFF
     C     HEKEY18       SETLL     HEAD18
     C     HEKEY18       READE     HEAD18                                 21
     C                   EXSR      SR_AWM_M
     C                   ELSE
     C     KBBLHB        MULT      VALKU1        XWORK
     C     XWORK         DIV(H)    OMRFAK        KBBLHB
     C                   WRITE     KOSTBR
     c                   exsr      print3sr
     C                   ENDIF
     C                   ELSE
     C
      * Ved Fly eksport singel og EXW og kreditbeløp skal egen logikk brukes.
     C     HEUR          IFEQ      'D'
     C     HESAML        ANDEQ     *BLANKS
     C     HEFR          ANDEQ     'EXW'
     C     KBBLHB        ANDLT     0
     C                   EXSR      SR_AWM_S
     C                   ELSE
     C     KBBLHB        MULT      VALKU1        XWORK
     C     XWORK         DIV(H)    OMRFAK        KBBLHB
     C                   WRITE     KOSTBR
     c                   exsr      print3sr
     C                   ENDIF
     C                   ENDIF
     c                   endsr
      *************************************************************************
     c     print3sr      begsr
      *************************************************************************
      *________________
      * Skriv til liste
      *""""""""""""""""
     C     *IN20         IFEQ      *ON
     C                   EVAL      WMARK1='FINNER IKKE OPPDRAG '
     C                   EVAL      WMARK2='                    '
     C                   EVAL      WEAVD=0
     C                   EVAL      WEOPD=0
     C                   ELSE
     C                   EVAL      WMARK1='                    '
     C                   EVAL      WMARK2='                    '
     C                   EVAL      WEAVD=HEAVD
     C                   EVAL      WEOPD=HEOPD
     C                   ENDIF
     C                   EVAL      WWHGCP=MWHGCP
     C                   EVAL      WDACHC=MDACHC
     C                   EVAL      WDISC=MDISC
     C                   EVAL      WKABL=MWHGCP+MDCCHP-MDACHC-MDISC-MDISC2
     C                   EVAL      WWHGGR=MWHGGR
     C                   EVAL      WDCCHP=MDCCHP
     C                   EVAL      WDISC2=MDISC2
     C                   EVAL      WTAXD=MTAXDC-MTAXDA
     C                   ADD       MTAXDC        TAX_KBBLHB
     C                   SUB       MTAXDA        TAX_KBBLHB
      * skal muligens sum per flyselskap ut først ?
     C                   EXSR      SUMAIR
      * henter fakturert og budsjett
     c                   exsr      faktsr
     c                   exsr      budsr
     C                   WRITE     SYCASS1PB
     C                   IF        *IN27
     C                   EVAL      *IN27=*OFF
     C                   WRITE     SYCASS1PA
     C                   ENDIF
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                               SR_AWM_M /
      *////////////////////////////////////////////////////////////
     C     SR_AWM_M      BEGSR

      * Summer først totalvekt for alle HAWB
      * En post er allerede lest, det er bare å fortsette og lese

     C                   EVAL      WEVKT=0
     C                   EVAL      X_KBBLHB=KBBLHB
     C                   EVAL      W_KBBLHB=0
     C     *IN20         DOWEQ     *OFF
     C                   ADD       HEVKT         WEVKT
     C     HEKEY18       READE     HEAD18                                 20
     C                   ENDDO

      * Fordel etter vekt og skriv en KOSTB4 post for hver HAWB

     C                   EVAL      *IN20=*OFF
     C     HEKEY18       SETLL     HEAD18
     C     HEKEY18       READE     HEAD18                                 20
     C     *IN20         DOWEQ     *OFF

     C                   EVAL      KBBLHB=((MWHGCP+MDCCHP-MDACHC-MDISC-MDISC2)
     C                             / (WEVKT / HEVKT))
     C                   ADD       KBBLHB        W_KBBLHB
     C                   EVAL      KBNØKK=3
     C                   EVAL      KBKAVD=HEAVD
     C                   EVAL      KBOPD=HEOPD
     C                   EVAL      KBKKEY=HEGN
     C                   EXSR      HENTKU
     C     KBBLHB        MULT      VALKU1        XWORK
     C     XWORK         DIV(H)    OMRFAK        KBBLHB
     C                   WRITE     KOSTBR
     c                   exsr      print3sr

     C     HEKEY18       READE     HEAD18                                 20
     C                   ENDDO
      * legg øresutjevning på  siste post
     C                   EVAL      Y_KBBLHB=X_KBBLHB-W_KBBLHB
     C     Y_KBBLHB      IFNE      0
     C     KOSBKEY       CHAIN     KOSTB4                             20
     C     *IN20         IFEQ      *OFF
     C                   ADD       Y_KBBLHB      KBBLHB
     C                   UPDATE    KOSTBR
     C                   ENDIF
     C                   ENDIF


     C                   EVAL      *IN20=*OFF

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                               SR_AWM_S /
      *////////////////////////////////////////////////////////////
     C     SR_AWM_S      BEGSR

      * Init av arbetsfelt

     C                   Z-SUB     KBBLHB        X_KBBLHB
     C                   EVAL      W_KBBLHB=0

      * Les alle FAKT linjer mot Flyselskap untatt FFE
     C     FAK2KEY       SETLL     FAK2
     C     FAK2KEY       READE     FAK2                                   22
     C     *IN22         DOWEQ     *OFF

     C     KODTGK        CHAIN     KODTGE                             36
     C     KGETYP        IFNE      KODTCAFE
     C     KGETYP        ANDNE     KODTCAFN
     C     KGETYP        ANDNE     'FFT'
     C     KGETYP        ANDNE     'FFF'
     C     KGETYP        ANDNE     'DUC'
     C     KGETYP        ANDNE     'DUA'
     C     FAKDA         ANDNE     'K'
     C                   Z-SUB     FABELN        KBBLHB
     C                   ADD       FABELN        W_KBBLHB
     C                   EVAL      KBVK=FAVK
     C                   WRITE     KOSTBR
N04  C                   MOVE      'D'           faopko
N04  C                   MOVE      'D'           fakda
N04  C                   UPDATE    faktr2
     c                   exsr      print3sr
     C                   ENDIF

     C     FAK2KEY       READE     FAK2                                   22
     C                   ENDDO

      * legg overskudd med kode 54
     C                   EVAL      KBBLHB=X_KBBLHB-W_KBBLHB
     C                   Z-SUB     KBBLHB        kbblhb
     C     KBBLHB        IFNE      0
     C                   EVAL      KBVK=KODTCAOV
     C                   WRITE     KOSTBR
     c                   exsr      print3sr
     C                   ENDIF

     C                   Z-SUB     X_KBBLHB      KBBLHB

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                 SR_TTT /
      *////////////////////////////////////////////////////////////
     C     SR_TTT        BEGSR
      *_________________
      * Add av total MVA
      *"""""""""""""""""
     C                   CLEAR                   KOSTBR
     C                   EVAL      KBBNR=KABNR
     C                   EVAL      KBVK=KODTCAMV
     C     KODTAK        CHAIN     KODTA                              34
     C  N34              Z-ADD     KOAKNR        KBKN
     C   34              Z-ADD     0             KBKN
     C                   EVAL      KBBLHB=TAX_KBBLHB
     C                   EVAL      KABL=KABL-KBBLHB
     C                   EVAL      KBKDPF='F'
     C                   EVAL      KBKDM=9
     C     KBBLHB        MULT      VALKU1        XWORK
     C     XWORK         DIV(H)    OMRFAK        KBBLHB
     C                   WRITE     KOSTBR
      *________________
      * Skriv til KOSTA
      *""""""""""""""""
     C                   WRITE     KOSTAR
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR for init                                        SRINIT /
      *////////////////////////////////////////////////////////////
     C     SRINIT        BEGSR
      *________________
      * Input parameter
      *""""""""""""""""
     C     *ENTRY        PLIST
     C                   PARM                    WMBR             10
      * HENTE DAGENS DATO OG TID
     C                   CALL      'SYGE15C'
     C                   PARM                    WDATE             8
     C                   PARM                    WTIME             6
      * Def av key
     C     FRIKEY        KLIST
     C                   KFLD                    W_FSKODE
     C                   KFLD                    W_FSSOK
     C     HEADKEY       KLIST
     C                   KFLD                    FSAVD
     C                   KFLD                    FSOPD
     C     SAMLKEY       KLIST
     C                   KFLD                    FSAVD
     C                   KFLD                    FSOPD
     C     KOSBKEY       KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C                   KFLD                    KBBNR
     C     FAK2KEY       KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C                   KFLD                    W_FASK
     C     KODTAK        KLIST
     C                   KFLD                    KAFAST            1
     C                   KFLD                    HEAVD
     C                   MOVE      'A'           KAFAST
     C     VALKEY        KLIST
     C                   KFLD                    fifirm
     C                   KFLD                    KAVAL
     c                   read      firm                                   33
     C     KODTGK        KLIST
     C                   KFLD                    W_KGEUNI          2
     C                   KFLD                    FAVK
     C                   MOVE      'GE'          W_KGEUNI
     C     KODTGKB       KLIST
     C                   KFLD                    W_KGEUNI          2
     C                   KFLD                    BUVK
      * Init av arbetsfelt
     C     *LIKE         DEFINE    HEVKT         WEVKT
     C     *LIKE         DEFINE    FSKODE        W_FSKODE
     C     *LIKE         DEFINE    FSSOK         W_FSSOK
     C     *LIKE         DEFINE    KBBLHB        W_KBBLHB
     C     *LIKE         DEFINE    KBBLHB        X_KBBLHB
     C     *LIKE         DEFINE    KBBLHB        Y_KBBLHB
     C     *LIKE         DEFINE    KBBLHB        TAX_KBBLHB
     C     *LIKE         DEFINE    FASK          W_FASK
     C                   EVAL      W_FSKODE='IAW'
     C                   EVAL      W_FASK='F'
     C                   READ      KODTCA                                 33
     C                   ENDSR
     c***********************************************************************
     C     faktsr        begsr
     c***********************************************************************
     c     fakkey        klist
     c                   kfld                    weavd
     c                   kfld                    weopd
      *
     c                   move      *zeros        wfakt
     c                   move      *zeros        wufak             9 2
     C                   setoff                                       33
     C     fakkey        setll     fakt
     C     *in(33)       doweq     '0'
     C     fakkey        reade     fakt                                   33
     C     *in(33)       ifeq      '0'
     C     fakda         andne     'K'
     C     faopko        andne     'D'
     C     KODTGK        CHAIN     KODTGE                             36
     C     KGETYP        ifeq      KODTCAFE
     C     KGETYP        oreq      KODTCAFN
     C     KGETYP        OREQ      'FFT'
     C     KGETYP        OREQ      'FFF'
     C     KGETYP        OREQ      'DUC'
     C     KGETYP        OREQ      'DUA'
     C*
     C     faopko        ifgt      'C'
     C                   add       fabeln        wfakt
     C                   else
     C                   add       fabeln        wufak
     c                   endif
     C                   endif
     C                   endif
     C                   enddo
     C     wfakt         sub       wkabl         wfaktd
     C     wufak         sub       wkabl         wufakd            9 2
      * Summer til brudd per flyselskap
     C                   ADD       WFAKT         WF1
     C                   ADD       WFAKTD        WF2
     C                   endsr
     c***********************************************************************
     C     budsr         begsr
     c***********************************************************************
      *
     c                   move      *zeros        wbudsj
     C                   setoff                                       33
     C     fakkey        setll     kosbu12
     C     *in(33)       doweq     '0'
     C     fakkey        reade     kosbu12                                33
     c     *in33         ifeq      '0'
     c     bust          andne     'F'
     C     KODTGKB       CHAIN     KODTGE                             36
     C     BUVK          ifeq      KODTCAFE
     C     BUVK          oreq      KODTCAFN
     C     KGETYP        oreq      KODTCAFE
     C     KGETYP        oreq      KODTCAFN
     C     KGETYP        OREQ      'FFT'
     C     KGETYP        OREQ      'FFF'
     C     KGETYP        OREQ      'DUC'
     C     KGETYP        OREQ      'DUA'
     C     BUVK          OREQ      'FFT'
     C     BUVK          OREQ      'FFF'
     C     BUVK          OREQ      'DUC'
     C     BUVK          OREQ      'DUA'
     C*
     C                   add       bubl          wbudsj
      *Når budsjettet har vært med på denne liste merkes det med F=ført
     c                   move      'F'           bust
     c                   z-add     kabnr         bubnr2
     c                   z-add     kalnr         bulnr
     c                   update    kosbur
     C                   endif
     C                   endif
     C                   enddo
     C     wbudsj        sub       wkabl         wbudsd
      * Summer til brudd per flyselskap
     C                   ADD       WBUDSJ        WB1
     C                   ADD       WBUDSD        WB2
     C                   endsr
      *////////////////////////////////////////////////////////////
      * SR                                                 SUMAIR /
      *////////////////////////////////////////////////////////////
     C     SUMAIR        BEGSR

     C     WAIRPR        IFEQ      0
     C                   EVAL      WAIRPR=MAIRPR
     C                   ELSE
     C     WAIRPR        IFNE      MAIRPR
     C                   WRITE     SYCASS1PC
     C                   IF        *IN27
     C                   EVAL      *IN27=*OFF
     C                   WRITE     SYCASS1PA
     C                   ENDIF
     C                   EVAL      WK1=0
     C                   EVAL      WB1=0
     C                   EVAL      WB2=0
     C                   EVAL      WF1=0
     C                   EVAL      WF2=0
     C                   EVAL      WAIRPR=MAIRPR
     C                   ENDIF
     C                   ENDIF
     C                   endsr
      *
     c***********************************************************************
     C     HENTKU        begsr
     c***********************************************************************
     C                   EVAL      KBKN=0
     C                   SELECT
      * Fly import
     C     HEUR          WHENEQ    'C'
     C     HEKDFK        IFNE      'X'
     C                   EVAL      KBKN=HEKNKF
     C                   ELSE
     C                   EVAL      KBKN=HEKNSF
     C                   ENDIF
      * Fly eksport singel
     C     HEUR          WHENEQ    'D'
     C     HEKDFS        IFNE      'X'
     C                   EVAL      KBKN=HEKNSF
     C                   ELSE
     C                   EVAL      KBKN=HEKNKF
     C                   ENDIF
      * Fly innland
     C     HEUR          WHENEQ    'E'
     C     HEKDFK        IFNE      'X'
     C                   EVAL      KBKN=HEKNKF
     C                   ELSE
     C                   EVAL      KBKN=HEKNSF
     C                   ENDIF
     C                   EVAL      KBVK='FFN'
     C                   ENDSL
      *
      * import
      *
     C     KBKN          IFEQ      0
     c     heur          ifeq      'A'
     c     heur          oreq      'C'
     c     heur          oreq      'E'
     c     heur          oreq      'F'
     c     heknk         andne     0
     c                   move      heknk         kbkn
     c                   end
     c
      *
      * eksport
      *
     c     heur          ifeq      'B'
     c     heur          oreq      'E'
     c     heur          oreq      'G'
     c     hekns         andne     0
     c                   move      hekns         kbkn
     c                   end
     c                   end
      *
      * generell kjøper
      *
     C     KBKN          IFEQ      0
     c     heknk         andne     0
     c                   move      heknk         kbkn
     c                   end
      *
      * generell selger
      *
     C     KBKN          IFEQ      0
     c     hekns         andne     0
     c                   move      hekns         kbkn
     c                   end
     C                   ENDSR
