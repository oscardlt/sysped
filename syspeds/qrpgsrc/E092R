     H debug(*yes)
L1    * ENDRINGER KUN FOR ELEKTROARMATUR - EILAG
PO    * ENDRINGER KUN FOR POSTEN
N01   * ved booking ikke styrt UNO? - sett UNOC:3
     FEDIMA     IF   E           K DISK
     FEDII      IF   E           K DISK
     FECMSG2    IF   E           K DISK
     FEUNH      IF   E           K DISK
     FEDIC      UF   E             DISK
     FEDIS      O  A E             DISK
     FEUNB      O  A E             DISK
     D                 DS
     D  WMBR                   1     14
     D  WMBRA                  1      1
     D  WMBRB                  2     10  0
      *//////////////
      *  Hodelogikk /
      *//////////////
     C                   EXSR      SRA
     C                   EXSR      SRB
     C                   MOVE      '1'           *INLR
      *////////////////////////////////////////////////////////////
      *  Initiering                                           SRA /
      *////////////////////////////////////////////////////////////
     C     SRA           BEGSR
      *___________________
      * Definiere Workfelt
      *"""""""""""""""""""
     C     *LIKE         DEFINE    MSR           WSR
     C     *LIKE         DEFINE    MST           WST
     C     *LIKE         DEFINE    *IN51         WIN51
     C     *LIKE         DEFINE    INNETW        WNNETW
     C     *LIKE         DEFINE    IN0007        W0007S
     C     *LIKE         DEFINE    INID          WN0010
PO   C     *LIKE         DEFINE    U0010         W0010
      *__________________
      * Definiere Nøkkler
      *""""""""""""""""""
     C     KEYA          KLIST
     C                   KFLD                    WSR
     C                   KFLD                    WST
     C     KEYB          KLIST
     C                   KFLD                    M0010
     C                   KFLD                    M0004
     C                   KFLD                    WSR
     C                   KFLD                    M0065
      *_________________
      * Input parametrer
      *"""""""""""""""""
     C     *ENTRY        PLIST
     C                   PARM      *IN51         WIN51
     C                   PARM      SMBR          SMBR
     C                   PARM      SSN           SSN
     C                   PARM                    WNNETW
     C                   PARM                    WN0010
     C                   PARM                    U0065
     C                   PARM                    U0054
      *_____________
      * Init av felt
      *"""""""""""""
     C                   MOVE      'S'           WSR
     C                   MOVE      '+'           WST
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Bearbeide Formater                                   SRB /
      *////////////////////////////////////////////////////////////
     C     SRB           BEGSR
      *________________________________
      * Oppdatere status i meldingslogg
      *""""""""""""""""""""""""""""""""
     C                   MOVE      '0'           *IN20
     C     KEYA          CHAIN     EDIMA                              51
     C  N51MMN           CHAIN     EUNH                               51
     C     *IN51         DOWEQ     '0'
     C     MVEN          IFEQ      *BLANKS
     C     M0065         ANDNE     'CUSDEC'
     C                   MOVE      '1'           *IN20
     C                   ENDIF
     C  N20KEYA          READE     EDIMA                                  51
     C  N51
     CANN20              ENDDO
     C     *IN51         CABEQ     '1'           SRB9
      *________________________________________
      * Hente og oppdater neste sendningsnummer
      *""""""""""""""""""""""""""""""""""""""""
     C     1             CHAIN     EDIC                               20
     C                   ADD       1             CSN
     C                   UPDATE    EDICR
      *_______________________
      * Hente Interchange ID's
      *"""""""""""""""""""""""
      * Sender
     C     M0004         CHAIN     EDII                               20
     C     *IN20         IFEQ      '0'
     C                   MOVEL     IN0007        W0007S
     C                   ELSE
     C                   MOVE      *BLANKS       W0007S
     C                   ENDIF
      * Receiver
     C     M0010         CHAIN     EDII                               20
     C                   MOVE      INNETW        WNNETW
      * Filnavn
     C                   MOVE      'T'           WMBRA                          TRANSPORT MSG,
      *_________________
      * Logge sendningen
      *"""""""""""""""""
      * EDIS
     C                   MOVE      M0004         S0004
     C                   MOVE      M0010         S0010
     C                   Z-ADD     CSN           WMBRB
     C                   MOVE      WMBR          S0020
     C                   MOVE      M0035         S0035
     C                   Z-ADD     0             S0036
     C                   Z-ADD     CSN           SSN
     C                   MOVE      'S'           SSR
     C                   MOVE      ' '           SST
     C                   MOVEL     WDT           SDT
     C                   MOVEL     WTM           STM
     C                   MOVEL     '*LIBL   '    SLIB
     C                   MOVE      '  '          SLIB
     C                   MOVEL     'EDISE'       SFILE
     C                   MOVEL     WMBR          SMBR
     C                   WRITE     EDISR
      * EUNB
     C                   CLEAR                   UNBR
     C                   Z-ADD     SSN           UIN
     C                   MOVE      ':'           UNA1
     C                   MOVE      '+'           UNA2
     C                   MOVE      ','           UNA3
     C                   MOVE      '?'           UNA4
     C                   MOVE      ' '           UNA5
     C                   BITON     '123457'      UNA6
     C                   BITOFF    '06'          UNA6
     C     KEYB          CHAIN     ECMSG2                             20
     C  N20M1001         IFEQ      '335'
N01  C     EC0001        ANDEQ     *BLANKS
s01  C*                  MOVE      'UNOB'        EC0001
s01  C*                  MOVE      '2'           EC0002
N01  C                   MOVE      'UNOC'        EC0001
N01  C                   MOVE      '3'           EC0002
     C                   END
     C     *IN20         IFEQ      '0'
     C     EC0001        ANDNE     *BLANKS
     C     EC0002        ANDNE     *BLANKS
     C                   MOVE      EC0001        U0001
     C                   MOVE      EC0002        U0002
     C                   ELSE
     C                   MOVE      'UNOA'        U0001
     C                   MOVE      '2'           U0002
     C                   END
     C                   MOVEL     M0004         U0004
     C                   MOVEL     W0007S        U0007S
     C                   MOVEL     M0010         U0010
     C                   MOVEL     IN0007        U0007R
L1   C     U0004         IFEQ      'NOELA101'
L1   C     U0010         ANDEQ     'NOEIL101'
L1   C                   MOVEL     'HANDEL'      U0014
L1   C                   ENDIF
PO   C                   MOVEL     '10297694'    W0010
PO   C     W0010         CAT       '9757':0      W0010
PO   C     U0010         IFEQ      W0010
PO   C                   MOVE      '.'           UNA3
PO   C     S0035         IFEQ      *BLANKS
PO   C                   MOVEL     'EDI01'       U0014
PO   C                   ELSE
PO   C                   MOVEL     'EDI00'       U0014
PO   C                   MOVE      *BLANKS       S0035
PO   C                   ENDIF
PO   C                   ENDIF
     C                   MOVE      WDT           U0017
     C                   MOVEL     WTM           U0019
     C                   MOVEL     S0020         U0020A
     C                   MOVEL     IN0022        U0022
     C                   MOVEL     IN0026        U0026
     C                   MOVEL     IN0032        U0032
     C                   MOVEL     S0035         U0035
     C                   MOVEL     S0036         U0036
     C                   MOVEL     S0020         U0020B
     C                   WRITE     UNBR
     C     SRB9          ENDSR
