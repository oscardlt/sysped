     A                                      DSPSIZ(27 132 *DS4)
     A                                      PRINT
     A                                      HLPPNLGRP(HLPSCR TR065PNL)
     A                                      HLPTITLE('REGISTRERING AV LOSSEMERK-
     A                                      NADER TET IMPORT')
     A                                      HELP
     A                                      ALTHELP
      *
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  4  2VALUES(' ' '2' '4' '5' '8' '9')
     A            TCLI           5Y 0O  4  5EDTCDE(4)
     A  41                                  COLOR(YLW)
     A            TCVKTB        13Y 3O  4 11EDTCDE(4)
     A            NCVKTB        13Y 3O  4 26EDTCDE(4)
     A            TCNAS_30      30A  O  4 41
     A            TCNAK_30      30A  O  4 72
     A            TC01           1A  O  4130COLOR(RED)
     A            TCLN           5Y 0H
     A            TCSECI         1Y 0H
     A            TCTINS        17A  H
     A            TCNAS         70A  H
     A            TCAD1S        70A  H
     A            TCPNS         17A  H
     A            TCPSS         35A  H
     A            TCLKS          2A  H
     A            TCTINK        17A  H
     A            TCNAK         70A  H
     A            TCAD1K        70A  H
     A            TCPNK         17A  H
     A            TCPSK         35A  H
     A            TCLKK          2A  H
     A            TCTATY         2Y 0H
     A            TCTAID        35A  H
     A            TCTALK         2A  H
     A            TCPDTY         4A  H
     A            TCPDRF        70A  H
     A            TCPDIN        35A  H
     A            TCSDTY         4A  H
     A            TCSDRF        70A  H
     A            TCSDLN         5Y 0H
     A            TCSDIN        35A  H
     A            TCTDTY         4A  H
     A            TCTDRF        70A  H
     A            TCADTY         4A  H
     A            TCADRF        70A  H
     A            TCAICD         5A  H
     A            TCAITX       120A  H
     A            NCTATY         2Y 0H
     A            NCTAID        35A  H
     A            NCTALK         2A  H
     A            NCSDTY         4A  H
     A            NCSDRF        70A  H
     A            NCSDIN        35A  H
     A            NCTDTY         4A  H
     A            NCTDRF        70A  H
     A            NCADTY         4A  H
     A            NCADRF        70A  H
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0016)
     A                                      SFLPAG(0015)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A            ZRECNO         4S 0O  1 86SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYSPED/NCTS065R'
     A                                      COLOR(BLU)
     A                                  1 21'NCTS ankomst, House Consignment'
     A                                      COLOR(WHT)
     A                                  1 55'Tdeknr.'
     A            UAVD           4Y 0O  1 63COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  1 68'-'
     A                                      COLOR(WHT)
     A            UTDN           7Y 0O  1 70COLOR(WHT)
     A                                      EDTCDE(4)
     A N27                              2  2'Velg option, 2=Endre 4=Slett 5=Vis-
     A                                       8=Item 9=Angre'
     A                                      COLOR(BLU)
     A  27                              2  2'Velg option, 5=Vis 8=Item'
     A                                      COLOR(BLU)
     A                                  3  2'Op Linje EDI brt.vekt   Mott. brt.-
     A                                      vekt Avsender                      -
     A                                      Mottaker'
     A                                      COLOR(WHT)
      *
     A          R BUNN
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF03(03 'AVSLUTT')
     A                                      CF05(05 'FRISK')
     A N27                                  CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                      CF12(12 'Gå til forrige')
     A                                      SLNO(19)
     A                                      CLRL(06)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A N27                              5  2'F3=Avslutt   F5=Forny   F6=Lag ny -
     A                                        F8=Hopp til   F12=Forrige'
     A                                      COLOR(BLU)
     A  27                              5  2'F3=Avslutt   F5=Forny   F8=Hopp ti-
     A                                      l   F12=Forrige'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  6  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R VARELI
     A                                      WINDOW(*DFT 16 110 *NOMSGLIN)
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF04(04 'Søk')
     A                                      CF15(15 'ARBEIDE MED TILLEGG OPPL')
     A                                      CF12(12 'Forrige')
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/NCTS065R'
     A                                      COLOR(BLU)
     A                                  1 21'NCTS ankomst, House Consignment'
     A                                      COLOR(WHT)
     A                                  1 55'Tdeknr.'
     A            UAVD           4Y 0O  1 63COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  1 68'-'
     A                                      COLOR(WHT)
     A            UTDN           7Y 0O  1 70COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  1 78'-'
     A                                      COLOR(WHT)
     A            WSLI           5Y 0O  1 80COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  3  2'Brt.vekt:'
     A            TCVKTB        13Y 3O  3 12TEXT('Bruttovekt')
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  3 27'Sikkerhet:'
     A            TCSECI         1Y 0O  3 38TEXT('Secure indicator')
     A                                      COLOR(WHT)
     A                                      EDTCDE(3)
     A                                  4  2'Avsender:'
     A            TCNAS         70A  O  4 12TEXT('Avsendersnavn')
     A                                      COLOR(WHT)
     A            TCNAK         70A  O  5 12TEXT('Mottakersnavn')
     A                                      COLOR(WHT)
     A                                  6  2'Tr.middel'
     A            TCTATY         2Y 0O  6 12TEXT('Type')
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            TCTAID        35A  O  6 15TEXT('Identitet')
     A                                      COLOR(WHT)
     A            TCTALK         2A  O  6 51TEXT('Landkode')
     A                                      COLOR(WHT)
     A                                  8  2'Kontroll resultat:'
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  9  2'Brt.vekt:'
     A            WSVKTB        13Y 3B  9 12DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  90                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 10  2'Tr.middel'
     A            WSTATY         2Y 0B 10 12DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  89                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSTAID        35A  B 10 15DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  88                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSTALK         2A  B 10 51TEXT('Landkode')
     A                                      DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  87                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 11  2'St.dok.:'
     A            WSSDTY         4A  B 11 12TEXT('Støttende dokument')
     A                                      DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  86                                  DSPATR(RI)
     A N47                                  COLOR(YLW)
     A  47                                  COLOR(RED)
     A            WSSDRF        70A  B 11 17TEXT('Referansenr')
     A                                      DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  85                                  DSPATR(RI)
     A N47                                  COLOR(YLW)
     A  47                                  COLOR(RED)
     A            WSSDIN        35A  B 12 12TEXT('Komplett informasjon')
     A                                      DSPATR(UL)
     A  27                                  DSPATR(PR)
     A N47                                  COLOR(YLW)
     A  47                                  COLOR(RED)
     A                                 13  2'Tr.dok.:'
     A            WSTDTY         4A  B 13 12TEXT('Transport dokument')
     A                                      DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  84                                  DSPATR(RI)
     A N47                                  COLOR(YLW)
     A  47                                  COLOR(RED)
     A            WSTDRF        70A  B 13 17TEXT('Referansenr')
     A                                      DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  83                                  DSPATR(RI)
     A N47                                  COLOR(YLW)
     A  47                                  COLOR(RED)
     A                                 14  2'Till.ref:'
     A            WSADTY         4A  B 14 12TEXT('Transport dokument')
     A                                      DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  82                                  DSPATR(RI)
     A N47                                  COLOR(YLW)
     A  47                                  COLOR(RED)
     A            WSADRF        70A  B 14 17TEXT('Referansenr')
     A                                      DSPATR(UL)
     A  27                                  DSPATR(PR)
     A  81                                  DSPATR(RI)
     A N47                                  COLOR(YLW)
     A  47                                  COLOR(RED)
     A                                 15  2'F4=Søk   F12=Avbryt   F15=Tillegg -
     A                                      opplysning'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 16  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R VAREL3
     A                                      WINDOW(*DFT 16 110 *NOMSGLIN)
     A                                      CF12(12 'Forrige')
     A                                      CF15(15 'ARBEIDE MED TILLEGG OPPL')
     A                                  1  2'SYSPED/NCTS065R'
     A                                      COLOR(BLU)
     A                                  1 21'NCTS ankomst, HC'
     A                                      COLOR(WHT)
     A                                  1 45'Tdeknr.'
     A            UAVD           4Y 0O  1 53COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  1 58'-'
     A                                      COLOR(WHT)
     A            UTDN           7Y 0O  1 60COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  1 78'-'
     A                                      COLOR(WHT)
     A            TCLI           5Y 0O  1 80COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  3  2'Brt.vekt:'
     A            TCVKTB        13Y 3O  3 12TEXT('Bruttovekt')
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  3 27'Sikkerhet:'
     A            TCSECI         1Y 0O  3 38TEXT('Secure indicator')
     A                                      COLOR(WHT)
     A                                      EDTCDE(3)
     A                                  4  2'Avsender:'
     A            TCNAS         70A  O  4 12TEXT('Avsendersnavn')
     A                                      COLOR(WHT)
     A            TCNAK         70A  O  5 12TEXT('Mottakersnavn')
     A                                      COLOR(WHT)
     A                                  6  2'Tr.middel'
     A            TCTATY         2Y 0O  6 12TEXT('Type')
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            TCTAID        35A  O  6 15TEXT('Identitet')
     A                                      COLOR(WHT)
     A            TCTALK         2A  O  6 51TEXT('Landkode')
     A                                      COLOR(WHT)
     A                                  8  2'Kontroll resultat:'
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  9  2'Brt.vekt:'
     A            NCVKTB        13Y 3O  9 12DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                 10  2'Tr.middel'
     A            NCTATY         2Y 0O 10 12DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            NCTAID        35A  O 10 15DSPATR(UL)
     A                                      COLOR(YLW)
     A            NCTALK         2A  O 10 51TEXT('Landkode')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 11  2'St.dok.:'
     A            NCSDTY         4A  O 11 12TEXT('Støttende dokument')
     A                                      DSPATR(UL)
     A N47                                  COLOR(YLW)
     A  47                                  COLOR(RED)
     A            NCSDRF        70A  O 11 17TEXT('Referansenr')
     A                                      DSPATR(UL)
     A N47                                  COLOR(YLW)
     A  47                                  COLOR(RED)
     A            NCSDIN        35A  O 12 12TEXT('Komplett informasjon')
     A                                      DSPATR(UL)
     A N47                                  COLOR(YLW)
     A  47                                  COLOR(RED)
     A                                 13  2'Tr.dok.:'
     A            NCTDTY         4A  O 13 12TEXT('Transport dokument')
     A                                      DSPATR(UL)
     A N47                                  COLOR(YLW)
     A  47                                  COLOR(RED)
     A            NCTDRF        70A  O 13 17TEXT('Referansenr')
     A                                      DSPATR(UL)
     A N47                                  COLOR(YLW)
     A  47                                  COLOR(RED)
     A                                 14  2'Till.ref:'
     A            NCADTY         4A  O 14 12TEXT('Transport dokument')
     A                                      DSPATR(UL)
     A N47                                  COLOR(YLW)
     A  47                                  COLOR(RED)
     A            NCADRF        70A  O 14 17TEXT('Referansenr')
     A                                      DSPATR(UL)
     A N47                                  COLOR(YLW)
     A  47                                  COLOR(RED)
     A                                 15  2'F12=Forrige   F15=Tillegg opplysni-
     A                                      nger'
     A                                      COLOR(BLU)
      *
     A          R NYWRK
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF03(03 'Be om utskrift')
     A                                      CF05(05 'FRISK')
     A N27                                  CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                      CF12(12 'Gå til bilde02')
     A                                  1  2'SYSPED/NCTS065R'
     A                                      COLOR(BLU)
     A                                  1 21'NCTS ankomst, House Consignment'
     A                                      COLOR(WHT)
     A                                  1 55'Tdeknr.'
     A            UAVD           4Y 0O  1 63COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  1 68'-'
     A                                      COLOR(WHT)
     A            UTDN           7Y 0O  1 70COLOR(WHT)
     A                                      EDTCDE(4)
     A N27                              2  2'Velg option, 2=Endre 4=Slett 5=Vis-
     A                                       8=Item 9=Angre'
     A                                      COLOR(BLU)
     A  27                              2  2'Velg option, 5=Vis 8=Item'
     A                                      COLOR(BLU)
     A                                  3  2'Op Linje EDI brt.vekt   Mott. brt.-
     A                                      vekt Avsender                      -
     A                                      Mottaker'
     A                                      COLOR(WHT)
     A N27                             22  2'F3=Avslutt   F5=Forny   F6=Lag ny -
     A                                        F8=Hopp til   F12=Forrige'
     A                                      COLOR(BLU)
     A  27                             22  2'F3=Avslutt   F5=Forny   F8=Hopp ti-
     A                                      l   F12=Forrige'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R VAREL2
     A                                      CF12(12 'Forrige')
     A                                      SLNO(19)
     A                                      CLRL(06)
     A                                  3  2'Hopp til postnr. -->'
     A            W3LI           5S 0I  3 24DSPATR(UL)
     A                                  4  2'F12=Forrige'
     A                                      COLOR(BLU)
