P0514F* ENDRINGER I DOKUF
N12   *NAVNE-ENDRINGER I DOKUF:
S12   * SELVE ENDRINGENE ER DOKUMENTERT PÅ LINJENIVÅ MED E12
S12   * DFKNV      KUNDENR SENDER    -->    DFKNSS
S12   * DFNAV      SENDERS NAVN      -->    DFNAVS
S12   * DFPNLA     POSTNR LASTESTED  -->    DFPNLS
S12   * DFADL      LEVERINGSADRESSE  -->    DFAD1L
S12   * DFADUP     POSTNR UTL.STED   -->    DFPOUL
S12   * DFADU      UTLEVERINGSSTED   -->    DFADUL
S12   * DFADOT     OVER TOLLSTED     -->    DFOVER (17 LANG)
S12   * DFNAM      NAVN MOTTAKER     -->    DFNAVM
S12   * DFADM1     ADRESSE MOTTAKER  -->    DFAD1M
S12   * DFADM2     P-ADR MOTTAKER    -->    DFAD3M (NY DFAD2M INNFØRT)
S12   * DF84       FAKTURASENDER     -->    DFFASE
S12   *                              -->
S12   * FØLGENDE FELTER ER NYE I DOKUF
S12   * DFDTU      DATO UTSTEDT
S12   * DFSREF     SENDERS REFERANSE
S12   * DFKNSM     KUNDENR MOTTAKER (I SYSPED)
S12   * DFLKM      LANDKODE UTLAND MOTTAKER
S12   * DFPNUM     POSTNR UTLAND
S12   * DFMREF     MOTTAKER-REFERANSE
S12   * DFAD2L     LEVERINGSADR 2.
S12   * DFLKL      LEVERINGSADR LANDKODE (UTLAND)
S12   * DFPNUL     LEVERINGSADR POSTNR UTLAND
S12   * DFNAVX     ANNEN FRAKTBETALER
S12   * DFAD1X     ANNEN ADRESSE 2.
S12   * DFAD2X     ANNEN ADRESSE 2.
S12   * DFAD3X     ANNEN ADRESSE 3.
S12   * DFLKX      ANNEN LANDKODE (UTLAND)
S12   * DFPNUX     ANNEN POSTNR UTLAND
S12   * DFKNX      ANNEN KUNDENR HOS TRANSP.
S12   * DFPROK     PRODUKTKODE (VAR FØR FPPROD FRA FIL DOKUFE)
S12   * DFPRO1     PRODUKTBESKR 1
S12   * DFPRO2     PRODUKTBESKR 2
S12   * DFBELA     FRAKT MM. BELASTES
S12   * DFFKAT     R24.F.VERDI,KATEGORI
S12   * DFPOLI     R26.POSISENR
S12   * DFSKFO     R26.SKAL FORSIKRES
S12   * DFLEVB     R27.LEV.BETINGELSE
S12   * DFLEVT     R27.LEV.BET.TEKST
S12   * DFLEVI     R30.LEV.INNSTRUKS
S12   *
S12   * FØLGENDE FELT ER FJERNET FRA DOKUF...
S12   * DF03       *BLANKS
S12   * DF01       *BLANKS
S12   * DFBHN      BHLDR/PALLBHLDRNR
S12   * DFKDSH     SENDER BET HENTING
S12   * DFKDSF     SENDER BET FRAKT
S12   * DFKDSU     SENDER BET UTKJØR
S12   * DFKDSE     SENDER BET ETTERKRAV
S12   * DFKDSI     SENDER BET INTET
S12   * DFVT1      VARESPESIFIKASJON-1
S12   * DFVT2      VARESPESIFIKASJON-2
S12   * DFVT3      VARESPESIFIKASJON-3
S12   * DFVT4      VARESPESIFIKASJON-4
S12   * DF01B      *BLANKS
S12   * DF01C      *BLANKS
S12   * DF01D      *BLANKS
S12   * DF01D      *BLANKS
S12   * DF51       *BLANKS
S12   * DF0068     SENDRES REF
S12   * DFKDS1
S12   * DFKDF1
S12   * DFKDS2
S12   * DFKDF2
S12   *
     FDOKUFGM   IP   F  937        DISK
     FDOKUF     O    E             DISK
     FDOKUFE    IF   E           K DISK
     FTRACKL02  IF   E           K DISK
     FHEADF     IF   E           K DISK
     IDOKUFGM   AA  01
     I                                  1    1  DFST
     I                                  2    9 0DFKNV
     I                                 10   39  DFNAV
     I                                 40   69  DFAD1S
     I                                 70   99  DFAD2S
     I                                100  129  DFAD3S
     I                                130  133  DFPNLA
     I                                134  135 0DFTOLL
     I                                136  165  DFADL
     I                                166  169  DFADUP
     I                                170  195  DFADU
     I                                170  171  UULK
     I                                172  180  UUUPNR
     I                                170  170  DFADU1
     I                                171  195  DFADU2
     I                                196  225  DFADOT
     I                                196  210  UUAORD
     I                                211  212  UUTJTY
     I                                213  222  UUTRIN
     I                                226  228  DF03
     I                                229  248  DFNAT
     I                                249  250 0DFNTEU
     I                                251  252 0DFNTAU
     I                                253  253  DF01
     I                                254  265  DFKNM
     I                                266  277  DFBHN
     I                                278  278  DFKDSH
     I                                279  279  DFKDSF
     I                                280  280  DFKDSU
     I                                281  281  DFKDSE
     I                                282  282  DFKDSI
     I                                283  322  DFVT1
     I                                323  362  DFVT2
     I                                363  402  DFVT3
     I                                403  442  DFVT4
     I                                443  443  DF01B
     I                                444  455  DFKNS
     I                                456  456  DFKDE
     I                                457  457  DFKDME
     I                                458  487  DFNAM
     I                                488  517  DFADM1
     I                                518  547  DFADM2
     I                                548  559  DFGM
     I                                560  560  DF01C
     I                                561  567 0DFNT
     I                                568  592  DFVS
     I                             P  593  597 0DFVKT
     I                                598  614  DFMÅL
     I                                615  615  DF01D
     I                                616  623 0DFRG
     I                                624  628 0DFKTA
     I                                629  630 0DFKTB
     I                                631  631  DFST2
     I                                632  682  DF51
     I                                683  685  DFSG
     I                                686  702  DFBPNR
     I                                703  714  DFGM2
     I                                715  739  DFVS2
     I                             P  740  744 0DFVKTF
     I                                745  769  DF84
     I                                770  770  DFRI
     I                                771  774 0DFAVD
     I                                775  779 0DFOPD
     I                                780  782 0DFFBNR
     I                                783  790 0DFTRAN
     I                                791  791  DFCMN
     I                                792  792  DFVEN
     I                                793  827  DF0068
     I                                828  862  DF1004
     I                             P  863  867 2DFFVBL
     I                                868  870  DFFVCU
     I                             P  871  873 0DF6060
     I                             P  874  876 0DF500A
     I                             P  877  879 0DF500B
     I                             P  880  884 2DFBELE
     I                                885  888 2DFLM
     I                             P  889  892 3DFM3
     I                                893  893  DFKDS1
     I                                894  908  DFKDF1
     I                                909  909  DFKDS2
     I                                910  924  DFKDF2
     I                                925  928 0DFNTLA
     I                                929  937 0DFFORE
      *
      *
     C     *IN01         IFEQ      '1'
      *
      * PROD-KODE LIGGER I EKSTRAFIL
     C     KEY           KLIST
     C                   KFLD                    DFAVD
     C                   KFLD                    DFOPD
     C                   KFLD                    DFFBNR
     C     KEY           CHAIN     DOKUFE                             33
     C     *IN33         IFEQ      '0'
     C                   MOVE      FPPROD        DFPROK
     C                   ELSE
     C                   MOVE      *BLANKS       DFPROK
     C                   END
      *
      * UTSKR.DATO HENTES ENTEN FRA TRACKF ELLER HEADF
     C     KEYTT         KLIST
     C                   KFLD                    DFAVD
     C                   KFLD                    DFOPD
     C                   KFLD                    DFFBNR
     C                   KFLD                    TTACTI
     C                   MOVE      'PRW'         TTACTI
     C     KEYTT         CHAIN     TRACKL02                           33
     C     *IN33         IFEQ      '0'
     C                   MOVE      TTDATE        DFDTU
     C                   ELSE
     C     KEYHEA        KLIST
     C                   KFLD                    DFAVD
     C                   KFLD                    DFOPD
     C     KEYHEA        CHAIN     HEADF                              33
     C     *IN33         IFEQ      '0'
     C                   MOVE      HEDTOP        DFDTU
     C                   END
     C                   END
      * POSTNR. LASTESTED ER NUMERISK
     C                   MOVE      *ZEROS        DFPNLS
     C                   TESTN                   DFPNLA               33
     C   33              MOVE      DFPNLA        DFPNLS
      * POSTNR AVG.-TERMINAL (VED EDI)
     C*          DFCMN     IFEQ 'Y'
     C*          XXXKEY    CHAINXXXX                 33
     C*                    MOVE XXXXX     DFPNLT
     C*                    END
      * POSTNR. UTLEV-STED ER NUMERISK
     C                   MOVE      *ZEROS        DFPOUL
     C                   TESTN                   DFADUP               33
     C   33              MOVE      DFADUP        DFPOUL
      *
      * FØRSTE FELT I UT-LEV.STEDE ER OFTE BLANK
     C     DFADU1        IFNE      ' '
     C                   MOVEL     DFADU         DFADUL
     C                   ELSE
     C                   MOVEL     DFADU2        DFADUL
     C                   END
      * FELT SOM HAR SKIFTET NAVN
     C     DFKDSF        IFEQ      'X'                                           FRAKTBETALER
     C                   MOVE      'S'           DFBELA                          ->SENDER
     C                   ELSE                                                    ->MOTTAKER
     C                   MOVE      'M'           DFBELA
     C                   END
      *
     C                   MOVEL     DFKNV         DFKNSS                          KNR SYS.SEL
     C                   MOVEL     DFNAV         DFNAVS                          NAVN SENDER
      *
      * MOTTAKERS NAVN VED NORSK FRAKTBREV...
     C     DFST          IFNE      'U'
     C     DFST          ANDNE     'V'
     C     DFST          ANDNE     'W'
      *
     C                   MOVEL     DFNAM         DFNAVM                          NAVN MOTTAK
     C                   MOVEL     DFADM1        DFAD1M                          ADR 1 MOTTA
     C                   MOVEL     DFADM2        DFAD3M                          ADR 3 MOTTA
      *
     C                   MOVEL     DFNAM         DFNAVL                          NAVN LEV.
     C                   MOVEL     DFADL         DFAD1L                          ADR 1 MOTTA
     C                   ELSE
      * MOTTAKERS NAVN VED UTENLANDS      ...
      *
     C                   MOVEL     DFNAM         DFNAVM                          NAVN MOTTAK
     C                   MOVEL     DFADM1        DFAD1M                          ADR 1 MOTTA
     C                   MOVEL     DFADL         DFAD2M                          ADR 2 MOTTA
     C                   MOVEL     DFADM2        DFAD3M                          ADR 3 MOTTA
     C                   MOVEL     UULK          DFLKM                           LANDK.MOTTA
     C                   MOVEL     UUUPNR        DFPNUM                          UT.PN.MOTTA
      *
     C                   MOVEL     DFNAM         DFNAVL                          NAVN LEV
     C                   MOVEL     DFADM1        DFAD1L                          ADR 1 LEADR
     C                   MOVEL     DFADL         DFAD2L                          ADR 2 LEVAD
     C                   MOVEL     UULK          DFLKL                           LANDK.LEADR
     C                   MOVEL     UUUPNR        DFPNUL                          UT.PN.LEVAD
      * AVSENDERS ORDRENR
     C                   MOVEL     UUAORD        DFSREF
      * TRANSP.INSTR.
     C                   MOVEL     UUTRIN        DFLEVI
      * TJENESTE/PRODUKT (TR/VE....)
     C                   MOVEL     UUTJTY        DFPROK
      * VEKT-GRUPPE (STATISTISK)
     C                   MOVEL     DFNTEU        DFPRO1
     C                   MOVE      *ZEROS        DFNTEU
     C                   MOVE      *BLANKS       DFADOT
      *
     C                   END
      *
      *
     C                   MOVEL     DFADOT        DFOVER                          OVER
     C                   MOVEL     DF84          DFFASE                          FAKT.SENDER
      * FELT SOM ER UTGÅTT.......
      * CONT.NR.(=PRKO VED UTENLAND) LAGRES NÅ I PROD.TEKSTLINJE 2
     C                   MOVEL     DFBHN         DFPRO2
      *
      * DFVT1/2/3/4 VED POSTENS BEDRIFTSPAKKE????
     C                   WRITE     DOKUFR
     C                   END
      *
