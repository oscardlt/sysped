********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 SH  PTF10 Ny textrubrikk     .....
********************************************************************
      *
      *
      ***********************************************************
      *** PROGRAM FOR INTERVALLSTYRING AV POSTNUMMER          ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01 02 07 09 11 13-22 24
      *** LEDIGE INDIKATORER 27-29 31 32 34-39 41-69 76-88 95-97
      ***********************************************************
     FKODTA     IF   E           K DISK
     FKODTFR    IF   E           K DISK
     FSONET     UF A E           K DISK
     FFIRM      IF   E           K DISK
     FSONES     UF A E           K DISK
     FSONESL02  IF   E           K DISK
     F                                     RENAME(SONESR:SONESR02)
     FKOFAST    IF   E           K DISK
     FKUFAST    IF   E           K DISK
     F                                     RENAME(KOFASTR:KUFASTR)
     FVALUL01   IF   E           K DISK
     FPRODU     IF   E           K DISK
     FPRODT     IF   E           K DISK
     FSYPO02FM  CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     C*
     C                   EXSR      INIT
     C     SOZONO        IFNE      *BLANKS
     C                   GOTO      CONT
     C                   END
     C*
     C     START0        TAG
     C  N30
     CANN04              Z-ADD     2             LINNBR
     C  N30
     CANN04              Z-ADD     28            POSNBR
     C                   EXFMT     BILDE01
      *
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
      *
     C     CONT          TAG
     C                   SETOFF                                       30
     C                   MOVEA     '000'         *IN(71)
     C   03              GOTO      SLUTT
      *
     C   04              DO
     C                   EXSR      SØKDT2
     C                   GOTO      START0
     C                   END
      *
     C                   MOVEL     SOPROD        KFKOD2
     C     KUFASK        CHAIN     KUFAST                             71
     C     *IN71         IFEQ      '1'
     C                   Z-ADD     2             LINNBR
     C                   Z-ADD     28            POSNBR
     C                   MOVE      '1'           *IN30
     C                   MOVE      'YBC0360'     MSGNR
     C                   GOTO      START0
     C                   END
      *
     C     SOAVDE        IFNE      *ZEROS
     C     KODAVD        CHAIN     KODTA                              72
     C     *IN72         IFEQ      '1'
     C                   MOVE      '1'           *IN30
     C                   Z-ADD     3             LINNBR
     C                   Z-ADD     28            POSNBR
     C                   MOVE      'SPA0001'     MSGNR
     C                   GOTO      START0
     C                   END
     C                   END
     C*
     C     SOZONO        IFEQ      *BLANKS
     C                   MOVE      '1'           *IN73
     C                   MOVE      '1'           *IN30
     C                   Z-ADD     4             LINNBR
     C                   Z-ADD     28            POSNBR
     C                   MOVE      'JOV0268'     MSGNR
     C                   GOTO      START0
     C                   END
     C*
     C                   MOVEL     SOZONO        SOZO1             1
     C     SOZO1         COMP      '0'                                    73
     C     *IN73         IFEQ      '1'
     C                   MOVE      '1'           *IN30
     C                   Z-ADD     4             LINNBR
     C                   Z-ADD     28            POSNBR
     C                   MOVE      'JOV0269'     MSGNR
     C                   GOTO      START0
     C                   END
     C*
     C                   MOVE      SOPROD        SSPROD
     C                   MOVE      SOAVDE        SSAVDE
     C                   MOVE      SOZONO        SSZONO
     C     PRODUK        CHAIN(N)  SONES                              33
     C  N33              DO
     C                   MOVE      SSOMRM        WSOMRM
     C                   MOVE      SSOMRL        WSOMRL
     C                   MOVE      SSMIMA        WSMIMA
     C                   MOVE      SSGREN        WSGREN
     C                   MOVE      SSVALK        WSVALK
     C                   MOVE      SSTYPE        WSTYPE
     C                   MOVE      SSMINI        WSMINI
     C                   MOVE      SSMAXI        WSMAXI
     C                   MOVE      SSGRPR        WSGRPR
     C                   MOVE      SSGRES        WSGRES
     C                   MOVE      SSGREF        WSGREF
     C                   MOVE      SSGREP        WSGREP
     C                   MOVE      SSTEXT        WSTEXT
N01  C                   MOVE      SSTEX2        WSTEX2
     C     PRIKU         SETLL     SONET
     C                   GOTO      STAR0B
     C                   END
     C*
     C* VED NY HENT STANDARDVERDIER FOR HEADER ....
     C     PRODKU        CHAIN     PRODU                              33
     C     *IN33         IFEQ      '0'
     C                   MOVE      PROMRM        WSOMRM
     C                   MOVE      PROMRL        WSOMRL
     C                   MOVE      PRMIMA        WSMIMA
     C                   MOVE      PRGREN        WSGREN
     C                   ELSE
     C                   MOVE      *ZEROS        WSOMRM
     C                   MOVE      *ZEROS        WSOMRL
     C                   MOVE      *ZEROS        WSMIMA
     C                   MOVE      *ZEROS        WSGREN
     C                   END
     C                   MOVE      FICURR        WSVALK
     C*
     C* VIDERE ER FELLES FOR NY / ENDRING....
     C     STAR0B        TAG
     C  N30
     CANN04              Z-ADD     6             LINNBR
     C  N30
     CANN04              Z-ADD     28            POSNBR
     C*
     C                   EXFMT     BILDE1B
     C                   MOVEA     '000000'      *IN(70)
     C                   SETOFF                                       30
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C   03              GOTO      SLUTT
     C   12              GOTO      SLUTT
     C   04              DO
     C                   EXSR      SØKDTA
     C                   GOTO      STAR0B
     C                   END
     C* SLETTING UTFØRES KUN DERSOM VALGT MED F10 + BEKREFTET MED F23
     C     DELMRK        IFEQ      'J'
     C     *IN23         ANDEQ     '1'
     C                   EXSR      DELALL
     C                   GOTO      START0
     C                   END
     C* INITIER FLAGG FOR SLETTEING VALGT/IKKE VALGT
     C                   MOVE      'N'           DELMRK            1
     C     *IN10         IFEQ      '1'
     C                   MOVE      'J'           DELMRK
     C                   MOVE      'JOV0272'     MSGNR
     C                   SETON                                        30
     C                   GOTO      STAR0B
     C                   END
      *
     C                   EXSR      TESTB2
     C   30              GOTO      STAR0B
     C*
     C* ENTER ER UTFØRT .....
     C*
     C     PRODUK        CHAIN(N)  SONES                              33
     C     *IN33         IFEQ      '0'
     C     WSTYPE        ANDEQ     'V'
     C     SSTYPE        ANDNE     'V'
     C     ST0B1         TAG
     C                   EXFMT     BEKREFT
     C   12              GOTO      STAR0B
     C  N23              GOTO      ST0B1
     C                   EXSR      DELAL2
     C                   END
     C     PRODUK        CHAIN     SONES                              33
     C                   MOVE      SOPROD        SSPROD
     C                   MOVE      SOAVDE        SSAVDE
     C                   MOVE      SOZONO        SSZONO
     C                   MOVE      WSOMRM        SSOMRM
     C                   MOVE      WSOMRL        SSOMRL
     C                   MOVE      WSMIMA        SSMIMA
     C                   MOVE      WSGREN        SSGREN
     C                   MOVE      WSVALK        SSVALK
     C                   MOVE      WSTYPE        SSTYPE
     C                   MOVE      WSMINI        SSMINI
     C                   MOVE      WSMAXI        SSMAXI
     C                   MOVE      WSGRPR        SSGRPR
     C                   MOVE      WSGRES        SSGRES
     C                   MOVE      WSGREF        SSGREF
     C                   MOVE      WSGREP        SSGREP
     C                   MOVE      WSTEXT        SSTEXT
N01  C                   MOVE      WSTEX2        SSTEX2
     C   33              WRITE     SONESR
     C  N33              UPDATE    SONESR
     C* DERSOM IKKE "STAFLING" FINNES - HENT STANDARD....
     C     PRIKU         SETLL     SONET                              33
     C     PRIKU         READE(N)  SONET                                  33
     C   33              EXSR      HENTST
     C                   SETOFF                                       33
      *
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
     C                   MOVE      W2VEKT        YTVEKT            9 0
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     PRIKU         SETLL     SONET
     C     START1        TAG
     C     PRIKU         READE(N)  SONET                                  94
     C   94              MOVE      *ZEROS        SOVEKT
     C   94              MOVE      *BLANKS       WSNR
      *
     C     START2        TAG
      *
     C  N94              DO
     C                   DO        16
     C                   ADD       1             ZRECNO
     C                   WRITE     SUBFIL
     C     PRIKU         READE(N)  SONET                                  94
     C  N94              MOVE      SOVEKT        YTVEKT
     C  N94              END
     C                   END
     C*
     C                   Z-ADD     ZRECNO        YRECNO            4 0
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYVARE
     C                   ELSE
     C                   WRITE     SUBCTL
     C                   EXFMT     BUNN
     C                   END
     C                   SETOFF                                       9830
      *
     C   03
     COR 05
     COR 12              EXSR      RMIMA
      *
      *** AVSLUTT
     C   03              GOTO      SLUTT
     C*** FRISK OPP
     C   05              GOTO      START
     C*** LAGE NY
     C   06
     CANN27              GOTO      START5
     C*** SØK FRA
     C   08              GOTO      START6
     C*
     C   12              GOTO      STAR0B
     C*
     C*** BLA VIDERE
     C   25              DO
     C                   Z-ADD     YRECNO        ZRECNO
     C                   SETOFF                                       25
     C  N94PRISK3        SETLL     SONET
     C  N94PRIKU         READE(N)  SONET                                  94
     C                   GOTO      START2
     C                   END
     C*
     C     ZRECNO        IFEQ      0
     C                   GOTO      START3
     C                   ELSE
     C                   EXSR      HENT
     C   98              GOTO      START3
     C                   END
     C*
     C*
     C*** LAGE/ENDRE
     C     START5        TAG
     C                   EXFMT     VARELI
     C                   SETOFF                                       89
     C                   SETOFF                                       30
     C   12              DO
     C   40              UPDATE    SUBFIL
     C                   SETOFF                                       40
     C                   EXSR      MOVB2
     C                   GOTO      START3
     C                   END
     C                   EXSR      TESTB1
     C   30              GOTO      START5
     C     NY            IFEQ      'J'
     C                   GOTO      START5
     C                   END
     C                   GOTO      START3
     C*
     C*** SØK FRA
     C     START6        TAG
     C                   EXFMT     VAREL2
     C   03              GOTO      SLUTT
     C                   GOTO      START
     C*
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
     C*
     C***********************************************
     C     TESTB1        BEGSR
     C***********************************************
     C  N40              DO
     C*
     C     WSVEKT        COMP      0                                    8989
     C   89              MOVE      'SPV0002'     MSGNR
     C   89              SETON                                        30
     C*
     C   30              GOTO      SLTB1
     C                   END
     C*
     C     PRISK         CHAIN     SONET                              33
     C  N33
     CANN40              GOTO      SLTB1
     C                   EXSR      MOVWPR
     C  N33              UPDATE    SONETR
     C   33              WRITE     SONETR
     C   40              UPDATE    SUBFIL
     C  N40              ADD       1             YRECNO
     C  N40              Z-ADD     YRECNO        ZRECNO
     C  N40              WRITE     SUBFIL
     C                   WRITE     SUBCTL
     C   40              MOVE      'N'           NY                1
     C  N40              MOVE      'J'           NY
     C                   SETOFF                                       40
     C                   EXSR      MOVB2
     C*
     C     SLTB1         ENDSR
     C*
     C***********************************************
     C     TESTB2        BEGSR
     C***********************************************
     C     WSTYPE        COMP      'V'                                    27
      * TEST OMREGNINGSFAKTOR (KUBIKKMETTER)
     C  N27WSOMRM        IFEQ      *ZEROS
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     28            POSNBR
     C                   SETON                                        3070
     C                   MOVE      'SHU0066'     MSGNR
     C                   GOTO      SLTB2
     C                   END
      * TEST OMREGNINGSFAKTOR (LASTEMETER)
     C  N27WSOMRL        IFEQ      *ZEROS
     C                   Z-ADD     7             LINNBR
     C                   Z-ADD     28            POSNBR
     C                   SETON                                        3071
     C                   MOVE      'SHU0067'     MSGNR
     C                   GOTO      SLTB2
     C                   END
      * TEST VALUTAKODE
     C     WSTYPE        IFEQ      'V'
     C                   MOVE      FICURR        WSVALK
     C                   ELSE
     C     WSVALK        IFNE      *BLANKS
     C     KODVAK        CHAIN     VALUL01                            73
     C     *IN73         IFEQ      '1'
     C                   Z-ADD     9             LINNBR
     C                   Z-ADD     28            POSNBR
     C                   SETON                                        30
     C                   MOVE      'SPV0001'     MSGNR
     C                   GOTO      SLTB2
     C                   END
     C                   END
     C                   END
      * TEST TABELTYPE
     C                   MOVEL     WSTYPE        KFKOD1
     C     KOFASK        CHAIN     KOFAST                             72
     C     *IN72         IFEQ      '1'
     C                   Z-ADD     10            LINNBR
     C                   Z-ADD     28            POSNBR
     C                   MOVE      '1'           *IN30
     C                   MOVE      'YBC0348'     MSGNR
     C                   GOTO      SLTB2
     C                   END
      * TEST FRANKATURE
     C  N27WSGREF        IFNE      *BLANKS
     C     KODFRK        CHAIN     KODTFR                             74
     C     *IN74         IFEQ      '1'
     C                   Z-ADD     12            LINNBR
     C                   Z-ADD     28            POSNBR
     C                   SETON                                        30
     C                   MOVE      'SPF0006'     MSGNR
     C                   GOTO      SLTB2
     C                   END
     C                   END
      * TEST GRENSE SONETABELL
     C  N27WSGRES        IFNE      *BLANKS
     C     WSGRES        SETLL     SONESL02
     C     WSGRES        READE     SONESL02                               75
     C     *IN75         IFEQ      '1'
     C                   Z-ADD     13            LINNBR
     C                   Z-ADD     28            POSNBR
     C                   SETON                                        30
     C                   MOVE      'YBC3487'     MSGNR
     C                   GOTO      SLTB2
     C                   END
     C                   END
     C*
     C     SLTB2         ENDSR
     C*
     C***********************************************
     C     HENT          BEGSR
     C***********************************************
     C                   MOVEA     '00'          *IN(91)
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C  N98WSNR          IFEQ      '2'
     C                   EXSR      MOVPRW
     C                   MOVE      ' '           WSNR
     C                   SETOFF                                       89
     C                   SETON                                        40
     C     STA5          TAG
     C                   EXFMT     VARELI
     C                   SETOFF                                       89
     C                   SETOFF                                       30
     C   12              DO
     C                   UPDATE    SUBFIL
     C                   SETOFF                                       40
     C                   EXSR      MOVB2
     C                   GOTO      UTLØK
     C                   END
     C                   EXSR      TESTB1
     C   30              GOTO      STA5
     C                   GOTO      UTLØK
     C                   END
     C*
     C* DELETION
     C*
     C  N98WSNR          IFEQ      '4'
     C                   MOVE      ' '           WSNR
     C  N12
     CANN27              DO
     C     PRISK2        CHAIN     SONET                              33
     C  N33              DELETE    SONETR
     C                   EXSR      MOVB1
     C                   Z-ADD     0             SOVEKT
     C                   END
     C                   UPDATE    SUBFIL
     C                   END
     C     UTLØK         TAG
     C                   END
     C*
     C     SLHENT        TAG
     C   98              DO
     C                   MOVE      'DIV0012'     MSGNR
     C                   SETON                                            30
     C                   END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVWPR        BEGSR
     C***********************************************
     C                   MOVE      WSVEKT        SOVEKT
     C                   MOVE      WSPRPR        SOPRPR
     C                   MOVE      WSADJU        SOADJU
     C                   MOVE      WSPRIC        SOPRIC
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVPRW        BEGSR
     C***********************************************
     C                   MOVE      SOVEKT        WSVEKT
     C                   MOVE      SOPRPR        WSPRPR
     C                   MOVE      SOADJU        WSADJU
     C                   MOVE      SOPRIC        WSPRIC
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVB1         BEGSR
     C***********************************************
     C                   MOVE      *ZEROS        SOVEKT
     C                   MOVE      *ZEROS        SOPRPR
     C                   MOVE      *ZEROS        SOADJU
     C                   MOVE      *ZEROS        SOPRIC
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVB2         BEGSR
     C***********************************************
     C                   MOVE      *ZEROS        WSVEKT
     C                   MOVE      *ZEROS        WSPRPR
     C                   MOVE      *ZEROS        WSADJU
     C                   MOVE      *ZEROS        WSPRIC
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     RMIMA         BEGSR
     C***********************************************
     C                   CALL      'SYPO13'
     C                   PARM                    SSPROD
     C                   PARM                    SSAVDE
     C                   PARM                    SSZONO
      *
     C                   ENDSR
     C***********************************************
     C     DELALL        BEGSR
     C***********************************************
      * SLETT HEADER
     C     PRODUK        CHAIN     SONES                              33
     C  N33              DELETE    SONESR
      * SLETT LINJER
     C                   EXSR      DELAL2
     C                   MOVE      'N'           DELMRK            1
     C                   ENDSR
      *
     C***********************************************
     C     DELAL2        BEGSR
     C***********************************************
     C     PRIKU         SETLL     SONET
     C     PRIKU         READE     SONET                                  33
     C     *IN33         DOWEQ     '0'
     C                   DELETE    SONETR
     C     PRIKU         READE     SONET                                  33
     C                   END
      *
     C                   MOVE      *OFF          *IN33
      *
     C                   ENDSR
      *
     C***********************************************
     C     SØKDTA        BEGSR
     C***********************************************
     C     LINNBR        IFEQ      9
     C     POSNBR        ANDGE     28
     C     POSNBR        ANDLE     30
     C                   CALL      'V200SY'
     C                   PARM                    USØK
     C                   PARM                    WSVALK
     C                   Z-ADD     10            LINNBR
     C                   Z-ADD     28            POSNBR
     C                   GOTO      SLSØK
     C                   END
      *
     C     LINNBR        IFEQ      10
     C     POSNBR        ANDEQ     28
     C                   MOVE      *BLANKS       KFKOD1
     C                   CALL      'SYFAKDA'
     C                   PARM                    KFTYP1
     C                   PARM                    UUPDKO
     C                   PARM                    KFKOD1
     C                   MOVEL     KFKOD1        WSTYPE
     C                   Z-ADD     11            LINNBR
     C                   GOTO      SLSØK
     C                   END
      *
     C     LINNBR        IFEQ      12
     C     POSNBR        ANDGE     28
     C     POSNBR        ANDLE     30
     C                   MOVE      'J'           USPED             1
     C                   CALL      'SYFI10G'
     C                   PARM                    WSGREF
     C                   PARM                    USPED
     C                   Z-ADD     13            LINNBR
     C                   Z-ADD     28            POSNBR
     C                   GOTO      SLSØK
     C                   END
      *
     C     LINNBR        IFEQ      13
     C     POSNBR        ANDGE     28
     C     POSNBR        ANDLE     32
     C                   CALL      'SYPO02X'
     C                   PARM                    WSGRES
     C                   Z-ADD     14            LINNBR
     C                   Z-ADD     28            POSNBR
     C                   GOTO      SLSØK
     C                   END
     C*
     C                   MOVE      'DIV1037'     MSGNR
     C                   SETON                                        30
     C*
     C     SLSØK         ENDSR
     C*
     C***********************************************
     C     SØKDT2        BEGSR
     C***********************************************
     C     LINNBR        IFEQ      9
     C     POSNBR        ANDEQ     39
     C                   MOVEL     SOPROD        UPROD             5
     C                   CALL      'SYKUKD'
     C                   PARM                    UTYPE
     C                   PARM                    UUPDKU
     C                   PARM                    UPROD
     C                   MOVEL     UPROD         SOPROD
     C                   Z-ADD     3             LINNBR
     C                   Z-ADD     28            POSNBR
     C                   GOTO      SLSØK2
     C                   END
      *
     C     LINNBR        IFEQ      10
     C     POSNBR        ANDGE     39
     C     POSNBR        ANDLE     42
     C                   CALL      'SYFA14B'
     C                   PARM                    SOAVDE
     C                   Z-ADD     4             LINNBR
     C                   Z-ADD     28            POSNBR
     C                   GOTO      SLSØK2
     C                   END
     C*
     C                   MOVE      'DIV1037'     MSGNR
     C                   SETON                                        30
     C*
     C     SLSØK2        ENDSR
     C*
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     KODAVD        KLIST
     C                   KFLD                    AVUNIK
     C                   KFLD                    SOAVDE
     C     KODFRK        KLIST
     C                   KFLD                    KFRUNI
     C                   KFLD                    WSGREF
     C                   MOVE      'FR'          KFRUNI
     C     KODVAK        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    WSVALK
     C     PRIKU         KLIST
     C                   KFLD                    SOPROD
     C                   KFLD                    SOAVDE
     C                   KFLD                    SOZONO
     C     PRISK         KLIST
     C                   KFLD                    SOPROD
     C                   KFLD                    SOAVDE
     C                   KFLD                    SOZONO
     C                   KFLD                    WSVEKT
     C     PRISK1        KLIST
     C                   KFLD                    SOPROD
     C                   KFLD                    SOAVDE
     C                   KFLD                    SOZONO
     C                   KFLD                    SOVEKT
     C     PRISK2        KLIST
     C                   KFLD                    SOPROD
     C                   KFLD                    SOAVDE
     C                   KFLD                    SOZONO
     C                   KFLD                    SOVEKT
     C     PRISK3        KLIST
     C                   KFLD                    SOPROD
     C                   KFLD                    SOAVDE
     C                   KFLD                    SOZONO
     C                   KFLD                    YTVEKT
     C     PRODUK        KLIST
     C                   KFLD                    SOPROD
     C                   KFLD                    SOAVDE
     C                   KFLD                    SOZONO
     C*
     C     PRODKU        KLIST
     C                   KFLD                    SSPROD
     C                   KFLD                    SSAVDE
     C     KOFASK        KLIST
     C                   KFLD                    KFTYP1           10
     C                   KFLD                    KFKOD1            5
     C                   MOVEL     'TABTYP'      KFTYP1
     C     KUFASK        KLIST
     C                   KFLD                    KFTYP2           10
     C                   KFLD                    KFKOD2            5
     C                   MOVEL     'PRODTYPE'    KFTYP2
     C*
     C     *ENTRY        PLIST
     C                   PARM                    SOPROD
     C                   PARM                    SOAVDE
     C                   PARM                    SOZONO
     C*
     C                   MOVEL     'PRODTYPE'    UTYPE            10
     C                   MOVE      ' '           UUPDKO            1
     C                   MOVE      'N'           UUPDKU            1
     C                   MOVE      'A'           AVUNIK            1
     C                   MOVE      'J'           USØK              1
     C                   READ      FIRM                                   33
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     HENTST        BEGSR
     C***********************************************
     C*
     C*   HENTER STANDARDVERDIER
     C*
     C                   CLEAR                   SONETR
     C                   MOVE      SSAVDE        SOAVDE
     C                   MOVE      SSPROD        SOPROD
     C                   MOVE      SSZONO        SOZONO
      *
     C     SSTYPE        IFEQ      'V'
     C                   Z-ADD     999999999     SOVEKT
     C                   WRITE     SONETR
     C                   ELSE
     C*
     C     PRODKU        SETLL     PRODT
     C     STARTH        TAG
     C     PRODKU        READE     PRODT                                  33
     C  N33              DO
     C                   MOVE      PTVEKT        SOVEKT
     C                   MOVE      PTPRPR        SOPRPR
     C                   MOVE      PTADJU        SOADJU
     C     PRISK2        CHAIN(N)  SONET                              34
     C   34              WRITE     SONETR
     C                   GOTO      STARTH
     C                   END
     C                   END
     C*
     C                   ENDSR
