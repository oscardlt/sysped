      ***********************************************************
      *** PROGRAM SKAL SENDE XML-LOGG TIL LOGISTIKKMOTOR      ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01-26                            ***
      *** LEDIGE INDIKATORER 27-32 34-49 51-99                ***
      ***********************************************************
     FXMLTILP1K O  A E           K DISK    USROPN
     FHEADF     IF   E           K DISK
     FFRISOK    IF   E           K DISK
     FFIRM      IF   E           K DISK
     FCUNDL01   IF   E           K DISK
     FEDIC      UF A E             DISK
     FEDIS      O  A E             DISK
     FEDIM      O  A E             DISK
     FEDISE1K   O  A E             DISK
     D                 DS
     D  XTXT01                 1    256
     D  XT01                   1    256    DIM(256)
     D                 DS
     D  XTXT02                 1    256
     D  XT02                   1    256    DIM(256)
     D                 DS
     D  XMBR                   1     10
     D  XMBRA                  1      1
     D  XMBRB                  2     10S 0
     D  XMBRC                  2     10
     D                 DS
     D  WDT                    1      8
     D  WDTÅ                   1      4
     D  WDTM                   5      6
     D  WDTD                   7      8
     D                 DS
     D  WTM                    1      6
     D  WTMH                   1      2
     D  WTMM                   3      4
     D  WTMS                   5      6
     D                 DS
     D  XEVTDT                 1      8  0
     D  XEVTDTÅ                1      4
     D  XEVTDTM                5      6
     D  XEVTDTD                7      8
     D  XEVTTM                 9     14  0
     D  XEVTTMH                9     10
     D  XEVTTMM               11     12
     D  XEVTTMS               13     14
     D  XEVTDTTM               1     14
     D                 DS
     D  HEAVD                  1      4  0
     D  HEOPD                  5     11  0
     D  HEAVDOPD               1     11
     D  HESG                  12     14
     D  SYSPEDREF              1     14
      *
     C                   EXSR      INIT
      *
     C                   EXSR      SRINIT
     C                   EXSR      SRBYGGF
      *
     C                   EXSR      SRSTART
      *
     C                   EXSR      SRMSGDET
      *
     C                   EXSR      SREVTSTR
      *
     C                   EXSR      SRSLUTT
     C                   EXSR      SRCLOSE
     C                   EXSR      SRSEND
      *
     C                   MOVE      *ON           *INLR
     C                   RETURN
      *
      ***********************************************
     C     SRINIT        BEGSR
      ***********************************************
      *
     C     1             CHAIN     EDIC                               33
     C                   ADD       1             CSN
     C                   ADD       1             CMN
     C                   UPDATE    EDICR
      *
     C                   MOVE      *BLANKS       XMBR
     C                   MOVE      'F'           XMBRA
     C                   MOVE      CSN           XMBRB
     C                   MOVE      XMBR          SEMBR
     C                   MOVE      *BLANKS       SDATA
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT
     C                   PARM                    WTM
      *
     C                   EVAL      S0004 = 'SYSPEDNO'
     C                   EVAL      S0010 = 'MINEDV'
     C                   EVAL      S0020 = FSSOK
     C                   EVAL      S0026 = 'XML-TRACKING'
     C                   EVAL      SSN   = CSN
     C                   EVAL      SSR   = 'S'
     C                   EVAL      SST   = 'C'
     C                   MOVE      WDT           SDT
     C                   MOVE      WTM           STM
     C                   EVAL      SLIB  = '*LIBL'
     C                   EVAL      SFILE = 'EDISE1K'
     C                   EVAL      SMBR  = SEMBR
     C                   EVAL      SMBRS = 0
     C                   EVAL      S0036 = 1
      *
     C                   ENDSR
      *
      ***********************************************
     C     SRBYGGF       BEGSR
      ***********************************************
     C                   CALL      'SYXML21B'
     C                   OPEN      XMLTILP1K
      *
     C                   ENDSR
      *
      ***********************************************
     C     SRSTART       BEGSR
      ***********************************************
     C                   EVAL      SDATA = '<?xml version="1.0" '
     C                             + 'encoding="ISO-8859-1" ?>'
     C                   EXSR      TILFIL
     C                   EVAL      SDATA = '<ns0:EventReport'
     C                             + ' collaboration="Tracking"'
     C                             + ' messageFunction="StatusReport"'
     C                             + ' language="no" profile="bringXML"'
     C                             + ' definedBy="BIG" version="1.0"'
     C                             + ' messageId="' + %trim(SMBR) +'"'
     C                             + ' xmlns:xsi="http://www.w3.org/2001'
     C                             + '/XMLSchema-instance"'
     C                             + ' xmlns:ns0="http://interchange.bring.com/'
     C                             + 'EventReport_v1r1">'
     C                   EXSR      TILFIL
      *
     C                   ENDSR
      *
      ***********************************************
     C     SRMSGDET      BEGSR
      ***********************************************
     C                   EVAL      SDATA = '<MessageDetails>'
     C                   EXSR      TILFIL
     C                   EVAL      SDATA = '<MessageSender partyId="'
     C                             + %trim(S0004) + '" idType="14">'
     C                             + '</MessageSender>'
     C                   EXSR      TILFIL
     C                   EVAL      SDATA = '<MessageRecipient partyId="'
     C                             + %trim(S0010) + '" idType="30">'
     C                             + '</MessageRecipient>'
     C                   EXSR      TILFIL
     C                   EVAL      SDATA = '<MessageDate timeZone="+01:00"'
     C                             + ' dstIndicator="YES">'
     C                             + WDTÅ + '-' + WDTM + '-' + WDTD + 'T'
     C                             + WTMH + ':' + WTMM + ':' + WTMS
     C                             + '</MessageDate>'
     C                   EXSR      TILFIL
     C                   EVAL      SDATA = '</MessageDetails>'
     C                   EXSR      TILFIL
      *
     C                   ENDSR
      *
      ***********************************************
     C     SREVTSTR      BEGSR
      ***********************************************
     C                   EVAL      SDATA = '<EventStructure>'
     C                   EXSR      TILFIL
      * ConsignmentSet
     C                   EXSR      SRCONSET
      *
     C                   EVAL      SDATA = '</EventStructure>'
     C                   EXSR      TILFIL
      *
     C                   ENDSR
      *
      ***********************************************
     C     SRCONSET      BEGSR
      ***********************************************
     C                   EVAL      SDATA = '<ConsignmentSet>'
     C                   EXSR      TILFIL
      *
      * Consignment
     C                   EVAL      SDATA = '<Consignment consignmentId="'
     C                             + %trim(HEAVDOPD)
     C                             + '" idType="XXX" />'
     C                   EXSR      TILFIL
      * Consignor
     C                   EVAL      KUNDNR = HEKNS
     C     CUNDL01K      CHAIN     CUNDL01                            33
     C                   EVAL      SDATA = '<Party subClass="Consignor" '
     C                             + 'partyId="' + %trim(%EDITC(HEKNS:'Z'))
     C                             + '" idType="86">'
     C                   EXSR      TILFIL
     C                   EVAL      XTXT01 = KNAVN
     C                   Z-ADD     50            XN53              3 0
     C                   EXSR      KNVST
     C                   EVAL      SDATA = '<Name>' + %trim(XTXT02) + '</Name>'
     C                   EXSR      TILFIL
     C                   EVAL      SDATA ='<Address subClass="PhysicalAddress">'
     C                   EXSR      TILFIL
     C                   MOVE      POSTNR        XPOSTNR           4
     C                   EVAL      SDATA = '<PostalCode>'
     C                             + %trim(XPOSTNR)
     C                             + '</PostalCode>'
     C                   EXSR      TILFIL
     C                   EVAL      XTXT01 = ADR3
     C                   Z-ADD     50            XN53              3 0
     C                   EXSR      KNVST
     C                   EVAL      SDATA = '<City>' + %trim(XTXT02) + '</City>'
     C                   EXSR      TILFIL
     C                   IF        SYLAND = *BLANKS
     C                   EVAL      SYLAND = FILAND
     C                   END
     C                   EVAL      SDATA = '<CountryCode>'
     C                             + %trim(SYLAND)
     C                             + '</CountryCode>'
     C                   EXSR      TILFIL
     C                   EVAL      XTXT01 = ADR1
     C                   Z-ADD     50            XN53              3 0
     C                   EXSR      KNVST
     C                   EVAL      SDATA = '<Street>' + %trim(XTXT02)
     C                             + '</Street>'
     C                   EXSR      TILFIL
     C                   EVAL      SDATA ='</Address>'
     C                   EXSR      TILFIL
     C                   EVAL      SDATA = '</Party>'
     C                   EXSR      TILFIL
      * EventSet
     C                   EVAL      SDATA = '<EventSet>'
     C                   EXSR      TILFIL
     C                   EVAL      SDATA = '<Event eventId="1">'
     C                   EXSR      TILFIL
     C                   EVAL      SDATA = '<Type>'
     C                             + %TRIM(UEVTTYPE) + '</Type>'
     C                   EXSR      TILFIL
     C                   EVAL      SDATA = '<EventCode>'
     C                             + %TRIM(UEVTKODE) + '</EventCode>'
     C                   EXSR      TILFIL
     C                   EVAL      SDATA = '<DateAndTimes subClass="EventDate" '
     C                             + 'timeZone="+01:00" dstIndicator="YES">'
     C                             + '<DateTime>'
     C                             + XEVTDTÅ + '-' + XEVTDTM + '-' + XEVTDTD
     C                             + 'T'
     C                             + XEVTTMH + ':' + XEVTTMM + ':' + XEVTTMS
     C                             + '</DateTime>'
     C                             + '</DateAndTimes>'
     C                   EXSR      TILFIL
     C                   EVAL      XTXT01 = UEVTTXT
     C                   Z-ADD     120           XN53              3 0
     C                   EXSR      KNVST
     C                   EVAL      SDATA = '<FreeText>'
     C                             + '<Text>' + %TRIM(XTXT02) + '</Text>'
     C                             + '</FreeText>'
     C                   EXSR      TILFIL
     C                   EVAL      SDATA = '</Event>'
     C                   EXSR      TILFIL
     C                   EVAL      SDATA = '</EventSet>'
     C                   EXSR      TILFIL
      *
     C                   EVAL      SDATA = '</ConsignmentSet>'
     C                   EXSR      TILFIL
      *
     C                   ENDSR
      *
      ***********************************************
     C     SRSLUTT       BEGSR
      ***********************************************
      *
     C                   EVAL      SDATA = '</ns0:EventReport>'
     C                   EXSR      TILFIL
      *
      * Oppdater EDIS
     C                   WRITE     EDISR
      * Oppdater EDIM
     C                   EXSR      OPDEDIM
      *
     C                   ENDSR
      *
      ***********************************************
     C     SRCLOSE       BEGSR
      ***********************************************
      *
     C                   ENDSR
      *
      ***********************************************
     C     SRSEND        BEGSR
      ***********************************************
     C                   CLOSE     XMLTILP1k
     C                   EVAL      XFILNVN = 'XXXX'
     C                   CAT       XMBRC:0       XFILNVN          16
     C                   CALL      'SYXML21S'
     C                   PARM                    XFILNVN
      *
     C                   ENDSR
      *
      ***********************************************
     C     OPDEDIM       BEGSR
      ***********************************************
     C     1             CHAIN     EDIC                               33
     C                   ADD       1             CMN
     C                   UPDATE    EDICR
      *
     C                   EVAL      M0004 = S0004
     C                   EVAL      M0010 = S0010
     C                   MOVEL     CMN           M0062
     C                   EVAL      M0065 = 'XMLEVT'
     C                   EVAL      M0068 = FSSOK
     C                   EVAL      M1001 = 'XML'
     C                   EVAL      M1004 = SYSPEDREF
     C                   EVAL      M1225 = 'EVT'
     C                   EVAL      MSN = SSN
     C                   EVAL      MMN = CMN
     C                   EVAL      MSR = 'S'
     C                   EVAL      MST = 'C'
     C                   MOVE      WDT           MDT
     C                   MOVE      WTM           MTM
     C                   MOVE      UAVD          MAVD
     C                   MOVE      UOPD          MTDN
     C                   WRITE     EDIMR
      *
     C                   ENDSR
      *
      ***********************************************
     C     TILFIL        BEGSR
      ***********************************************
     C                   WRITE     EDISER
      *
     C                   MOVE      SDATA         XTPDTA
     C                   WRITE     XMLTILPR
      *
     C                   ENDSR
      *
      ***********************************************
     C     KNVST         BEGSR
      ***********************************************
     C     XN53          SUB       4             XN54              3 0
     C     XN53          SUB       5             XN55              3 0
     C     XN53          SUB       6             XN56              3 0
     C                   Z-ADD     0             XN52              3 0
     C                   MOVE      *BLANKS       XTXT02
     C     1             DO        256           XN51              3 0
     C                   IF        (((((XT01(XN51) = '&') OR
     C                                 (XT01(XN51) = '"')) OR
     C                                 (XT01(XN51) = XAPS)) OR
     C                                 (XT01(XN51) = '<')) OR
     C                                 (XT01(XN51) = '>'))
     C                   IF        XN52 < XN53
     C                   ADD       1             XN52
     C                   SELECT
     C                   WHEN      XT01(XN51) = '&'
     C                   IF        XN51 < XN55
     C                   MOVEA     '&amp;'       XT02(XN52)
     C                   ADD       4             XN52
     C                   ELSE
     C                   SUB       1             XN52
     C                   END
     C                   WHEN      XT01(XN51) = '"'
     C                   IF        XN51 < 75
     C                   MOVEA     '&quot;'      XT02(XN52)
     C                   ADD       5             XN52
     C                   ELSE
     C                   SUB       1             XN52
     C                   END
     C                   WHEN      XT01(XN51) = XAPS
     C                   IF        XN51 < XN56
     C                   MOVEA     '&apos;'      XT02(XN52)
     C                   ADD       5             XN52
     C                   ELSE
     C                   SUB       1             XN52
     C                   END
     C                   WHEN      XT01(XN51) = '>'
     C                   IF        XN51 < XN54
     C                   MOVEA     '&gt;'        XT02(XN52)
     C                   ADD       3             XN52
     C                   ELSE
     C                   SUB       1             XN52
     C                   END
     C                   WHEN      XT01(XN51) = '<'
     C                   IF        XN51 < XN54
     C                   MOVEA     '&lt;'        XT02(XN52)
     C                   ADD       3             XN52
     C                   ELSE
     C                   SUB       1             XN52
     C                   END
     C                   ENDSL
     C                   END
     C                   ELSE
     C                   IF        XN52 < XN53
     C                   ADD       1             XN52
     C                   MOVE      XT01(XN51)    XT02(XN52)
     C                   ELSE
     C                   LEAVE
     C                   END
     C                   END
     C                   ENDDO
      *
     C                   ENDSR
      *
      ***********************************************
     C     INIT          BEGSR
      ***********************************************
     C     HEADFK        KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C     FRISOKK       KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C                   KFLD                    FSKODE
     C     CUNDL01K      KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    KUNDNR
      *
     C     *ENTRY        PLIST
     C                   PARM                    UAVD              4
     C                   PARM                    UOPD              7
     C                   PARM                    UEVTTYPE         30
     C                   PARM                    UEVTKODE         10
     C                   PARM                    UEVTDTTM         14
     C                   PARM                    UEVTTXT         100
      *
     C                   MOVE      UAVD          HEAVD
     C                   MOVE      UOPD          HEOPD
     C                   MOVE      UEVTDTTM      XEVTDTTM
     C                   BITON     '123457'      XAPS              1
     C                   BITOFF    '06'          XAPS
     C                   MOVE      'ORD'         FSKODE
      *
     C     *LOVAL        SETLL     FIRM
     C                   READ      FIRM                                   33
     C     HEADFK        CHAIN     HEADF                              33
     C     FRISOKK       CHAIN     FRISOK                             33
      *
     C                   ENDSR
      *
