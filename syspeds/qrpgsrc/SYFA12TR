********************************************************************
      * EDIFACT IFTMIN (FRAKTBREV) MOT TOLLPOST
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
      * 01 JOV PTF10 R38-tekst også fra Oppdrags notisblokk (R) (G=TRinstruks fantes..)
      * 02 Jov PTF10 dersom kundenr i KODTTR så hent det til DFKNS/NAD+CZ
      * 03 Jov PTF10 BLANKE FOREGÅENDE REF FØR HERFA
********************************************************************
     FDOKUF     IF   E           K DISK
     FDOKUFV    IF   E           K DISK
     FDOKUFM    IF   E           K DISK
     FHEADF     IF   E           K DISK
N02  FKODTA     IF   E           K DISK
N02  FKODTTR    IF   E           K DISK
     FFAK13     IF   E           K DISK
     FCUNDL01   IF   E           K DISK
     FTRAN      IF   E           K DISK
     FTURER14   IF   E           K DISK
     FHETRIU    IF   E           K DISK
     FKOSBU     IF   E           K DISK
     FDOK29     IF   E           K DISK
     FDOK36     IF   E           K DISK
     FFREETXT2  IF   E           K DISK
     FDOKUDGS   IF   E           K DISK
     FEDIC      UF   E             DISK
     FEDIM      IF A E             DISK
     FECNT93A   O  A E             DISK
     FECPI93A   O  A E             DISK
     FEDGS93A   O  A E             DISK
     FEDOC93A   O  A E             DISK
     FEDTM93A   O  A E             DISK
     FEEQD93A   O  A E             DISK
     FEEQN93A   O  A E             DISK
     FEFTX93A   O  A E             DISK
     FEGID93A   O  A E             DISK
     FELOC93A   O  A E             DISK
     FEMEA93A   O  A E             DISK
     FEMOA93A   O  A E             DISK
     FENAD93A   O  A E             DISK
     FEPCI93A   O  A E             DISK
     FEPIA93A   O  A E             DISK
     FERFF93A   O  A E             DISK
     FETDT93A   O  A E             DISK
     FETOD93A   O  A E             DISK
     FETSR93A   O  A E             DISK
     FEUNH      O  A E             DISK
      *___________________________
      * Arrays for string handling
      *"""""""""""""""""""""""""""
     D                 DS
     D  WTIME                  1     14  0
     D  WTID                   1      6  0
     D  WDD                    7      8  0
     D  WMM                    9     10  0
     D  WÅÅ                   11     14  0
     D                 DS
     D  WDATO                  1      8  0
     D  WÅ                     1      4  0
     D  WM                     5      6  0
     D  WD                     7      8  0
      *________________
      * DS for mottaker
      *""""""""""""""""
     D                 DS
     D  DFKNM                  1     12
     D  DFKNM2                 2     12
     D TX              S             70    DIM(26)
     D                 DS
     D  DFAD3M                 1     30
     D  WADM3A                 1      4
     D  WADM3B                 6     30
     D  QAD3M                  1     30
     D                 DS
     D  DFAD3X                 1     30
     D  WADX3A                 1      4
     D  WADX3B                 6     30
      *____________________________________
      * DS for leftjustify numericel fields
      *""""""""""""""""""""""""""""""""""""
     D                 DS
     D  ØN152                  1     15S 2
     D  ØA152H                 1     13
     D  ØA152X                 5     13
     D  ØA152D                14     15
     D                 DS
     D  ØA12                   1     12
     D  Ø12                    1     12
     D                                     DIM(12)
     D                 DS
     D  Wz0068                 1     35
     D  WzAVD                  1      4
     D  WzPRO                  5     12
     D  WzKNA                 13     20
      */////////////
      * MAIN LOGIC /
      */////////////
     C                   EXSR      SRINIT
     C                   EXSR      SRA
     C                   MOVE      '1'           *INLR
      */////////////////////////////////////////////////////////////////////////
      *// HOVEDRUTINE                                                         //
      */////////////////////////////////////////////////////////////////////////
     C     SRA           BEGSR
      * GET RECORDNUMBER FOR EDIM
     C     1             CHAIN     EDIC                               51
     C     *IN51         CABEQ     *ON           SRA9
     C                   ADD       1             CMN
     C                   UPDATE    EDICR
     C                   Z-ADD     CMN           WMN
      * Hent fraktbrev
     C     DOKKEY        CHAIN     DOKUF                              51
     C     *IN51         CABEQ     *ON           SRA9
      * Hent oppdrag
     C     HEAKEY        CHAIN     HEADF                              51
     C     *IN51         CABEQ     *ON           SRA9
      * GET INTERCHG.ID            1
     C     HEPRO         CHAIN     TURER14                            51
     C* ER GODSLISTEMOTTAKER BÅDE AGENT OG TRANSPORTØR???
     C                   MOVE      'N'           AGTRAN            1
     C     WzKNA         IFEQ      '00000000'
     C                   MOVE      'T'           AGTRAN            1
     c                   ELSE
     C     *IN51         IFEQ      '0'
     C     TRAKEY        CHAIN     TRAN                               51
     C     *IN51         IFEQ      '0'
     C     VMTRKU        ANDEQ     HEKNA
     C                   MOVE      'J'           AGTRAN
     C                   END
     C                   END
     C                   END
      *  HENTER EVENTUELLT UTLEGG PÅ FAKTURA
     C     FAKTKE        CHAIN     FAK13                              51
     C                   IF        *IN51
     C                   MOVE      *BLANKS       WSVALK            3
     C                   Z-ADD     0             WSBELV           13 2
     C                   ELSE
     C                   MOVEL     FAVAL         WSVALK            3
     C                   Z-ADD     FABELV        WSBELV
     C                   ENDIF
N02   * hent kundenr selger
N02  c                   move      *blanks       DFKNS
N02  C     KODTAKey      CHAIN     KODTA                              77
N02  C  N77KOTRKE        CHAIN     KODTTRR                            77
N02  C  N77              MOVEL     KTRAK3        DFKNS
      * ADD TO EDI-DB
     C                   EXSR      DTM00                                        Fritekst
     C                   EXSR      FTX00                                        Fritekst
     C                   EXSR      MOA00                                        Fritekst
     C                   EXSR      TSR00                                        Fritekst
     C                   EXSR      RFF03                                        Transportdetaljer
     C                   EXSR      CPI06
     C                   EXSR      TDT08                                        Transportdetaljer
     C                   EXSR      NAD10A                                       Avsender
     C                   EXSR      NAD10B                                       Leveringsadresse
     C                   EXSR      NAD10C                                       Mottaker
     C     DFBELA        IFEQ      'A'
     C                   EXSR      NAD10D                                       Mottaker
     C                   END
     C                   EXSR      GID16                                        Varelinjer
     C                   EXSR      EQD34                                        Transp.utstyr
     C                   EXSR      CNT00                                        Kontrolltotaler
      * WRITE TO EUNH
     C                   CLEAR                   UNHR
     C                   Z-ADD     WMN           UMN
     C                   MOVEL     WMN           U0062A
     C                   MOVEL     'IFTMIN'      U0065
     C                   MOVEL     'S'           U0052
     C                   MOVEL     '93A'         U0054
     C                   MOVEL     'UN'          U0051
     C                   MOVEL     'NOSM12'      U0057
     C                   MOVEL     DF1004        U0068
     C                   MOVEL     '700'         U1001
     C                   MOVEL     DF1004        U1004
     C                   MOVEL     U0062A        U0062B
     C                   WRITE     UNHR
      * WRITE TO EDIM
     C                   CLEAR                   EDIMR
     C                   MOVEL     W0004         M0004
     C                   MOVEL     W0010         M0010
     C                   MOVE      ' '           M0035
     C                   MOVEL     U0062A        M0062
     C                   MOVE      U0065         M0065
      * legg ut avd / tur / agent for å sjekke om sendt før
     C                   MOVEL     W0068         M0068
     C                   MOVEL     U1001         M1001
     C                   MOVEL     U1004         M1004
     C                   Z-ADD     WMN           MMN
     C                   MOVE      'S'           MSR
     C                   Z-ADD     WDATO         MDT
     C                   MOVEL     WTID          MTM
     C                   Z-ADD     DFAVD         MAVD
     C                   Z-ADD     DFOPD         MTDN
     C                   Z-ADD     DFFBNR        MFFBNR
     C                   MOVE      DFVEN         MVEN
     C                   WRITE     EDIMR

     C     SRA9          TAG

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Kontrolltotaler                                    CNT00 /
      *////////////////////////////////////////////////////////////
     C     CNT00         BEGSR
     C                   Z-ADD     0             WREP0
     C                   Z-ADD     0             WREP1
      *
     C                   CLEAR                   CNTR
     C                   Z-ADD     WMN           CMN
     C                   Z-ADD     0             CGN
     C                   ADD       1             WREP0
     C                   Z-ADD     WREP0         CREP0
      *
     C                   MOVEL     '11'          C6069
     C                   Z-ADD     DFNT          ØN152
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           C6066
     C                   MOVEL     'PCE'         C6411
     C                   WRITE     CNTR

     C                   ADD       1             WREP0
     C                   Z-ADD     WREP0         CREP0
     C                   MOVEL     '7 '          C6069
     C                   Z-ADD     DFVKT         ØN152
     C                   EXSR      SRNE
     C                   MOVE      *BLANKS       C6066
     C                   MOVEL     ØAN           C6066
     C                   MOVEL     'KGM'         C6411
     C                   WRITE     CNTR

     C     DFM3          IFGT      0
     C                   ADD       1             WREP0
     C                   Z-ADD     WREP0         CREP0
     C                   MOVEL     '15'          C6069
     C     DFM3          MULT      1000          ØN152
     C                   EXSR      SRNE
     C                   MOVE      *BLANKS       C6066
     C                   MOVEL     ØAN           C6066
     C                   MOVEL     'DMQ'         C6411
     C                   WRITE     CNTR
     C                   ENDIF
      *
     C     DFLM          IFGT      0
     C                   ADD       1             WREP0
     C                   Z-ADD     WREP0         CREP0
     C                   MOVEL     'ZLM'         C6069
     C                   Z-ADD     DFLM          ØN152
     C                   EXSR      SRNE
     C                   MOVE      *BLANKS       C6066
     C                   MOVEL     ØAN           C6066
     C                   MOVEL     'MTR'         C6411
     C                   WRITE     CNTR
     C                   ENDIF

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Betalingsinstruksjoner                             CPI06 /
      *////////////////////////////////////////////////////////////
     C     CPI06         BEGSR
     C                   Z-ADD     0             WREP0
      *
     C                   CLEAR                   CPIR
     C                   Z-ADD     WMN           CMN
     C                   Z-ADD     6             CGN
     C                   ADD       1             WREP0
     C                   Z-ADD     WREP0         CREP0
      *
     C                   MOVEL     '1'           C5237
     C                   SELECT
     C     DFBELA        WHENEQ    'S'
     C                   MOVEL     'P'           C4237
     C     DFBELA        WHENEQ    'M'
     C                   MOVEL     'C'           C4237
     C     DFBELA        WHENEQ    'A'
     C                   MOVEL     'B'           C4237
     C                   ENDSL
     C                   MOVEL     'P'           C4237
      *
     C                   WRITE     CPIR
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Farlig gods                                        DGS29 /
      *////////////////////////////////////////////////////////////
     C     DGS29         BEGSR

     C                   Z-ADD     99            WGSLINR
     C     DGSKEY        CHAIN     DOKUDGS                            52
     C     *IN52         IFEQ      *OFF
     C     DGS8351       ANDNE     *BLANKS

     C                   Z-ADD     0             WREP1

     C                   CLEAR                   DGSR
     C                   Z-ADD     WMN           DMN
     C                   Z-ADD     29            DGN
     C                   ADD       1             WREP1
     C                   Z-ADD     WREP0         DREP0
     C                   Z-ADD     WREP1         DREP1

     C                   MOVEL     DGS8273       D8273
     C                   MOVEL     DGS8351       D8351
     C                   MOVEL     DGS8078       D8078
     C                   MOVEL     DGS8092       D8092
     C                   Z-ADD     DGS7124       ØN152
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           D7124
     C                   MOVEL     DGS7088       D7088
     C                   Z-ADD     DGS7106       ØN152
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           D7106
     C                   MOVEL     DGS6411       D6411
     C                   MOVEL     DGS8339       D8339
     C                   MOVEL     DGS8364       D8364
     C                   MOVEL     DGS8410       D8410
     C                   MOVEL     DGS8126       D8126
     C                   MOVEL     DGS8158       D8158
     C                   MOVEL     DGS8186       D8186

     C                   WRITE     DGSR
      * Fritekst
     C                   EXSR      FTX29
      *
     C                   ENDIF
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Dokumenter                                         DOC12 /
      *////////////////////////////////////////////////////////////
     C     DOC12         BEGSR

     C                   Z-ADD     0             WREP1

      * Farlig gods
     C     DGS8351       IFNE      *BLANKS
     C                   CLEAR                   DOCR
     C                   Z-ADD     WMN           DMN
     C                   Z-ADD     12            DGN
     C                   ADD       1             WREP1
     C                   Z-ADD     WREP0         DREP0
     C                   Z-ADD     WREP1         DREP1
     C                   MOVEL     '890'         D1001
     C                   MOVEL     '2'           D1373
     C                   WRITE     DOCR
     C                   ENDIF
      * Giro produseres og sendes
     C     DFKDE         IFEQ      'X'
     C     WSBELV        ANDGT     0
     C                   CLEAR                   DOCR
     C                   Z-ADD     WMN           DMN
     C                   Z-ADD     12            DGN
     C                   ADD       1             WREP1
     C                   Z-ADD     WREP0         DREP0
     C                   Z-ADD     WREP1         DREP1
     C                   MOVEL     '383'         D1001
     C                   MOVEL     '7'           D1373
     C                   WRITE     DOCR
     C                   ENDIF
      * Giro fremvises
     C     DFKDE         IFEQ      'X'
     C     WSBELV        ANDGT     0
     C                   CLEAR                   DOCR
     C                   Z-ADD     WMN           DMN
     C                   Z-ADD     12            DGN
     C                   ADD       1             WREP1
     C                   Z-ADD     WREP0         DREP0
     C                   Z-ADD     WREP1         DREP1
     C                   MOVEL     '383'         D1001
     C                   MOVEL     '19'          D1373
     C                   WRITE     DOCR
     C                   ENDIF

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Sendningsdato                                      DTM00 /
      *////////////////////////////////////////////////////////////
     C     DTM00         BEGSR
     C                   Z-ADD     0             WREP0
      *
     C                   CLEAR                   DTMR
     C                   Z-ADD     WMN           DMN
     C                   Z-ADD     0             DGN
     C                   ADD       1             WREP0
     C                   Z-ADD     WREP0         DREP0
      *
     C                   MOVEL     '137'         D2005
     C                   MOVEL     WDATO         D2380
     C                   MOVEL     '102'         D2379
      *
     C                   WRITE     DTMR
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Utstyrsdetaljer                                    EQD34 /
      *////////////////////////////////////////////////////////////
     C     EQD34         BEGSR

     C                   Z-ADD     0             WREP0
     C                   Z-ADD     0             WREP1
      * EUR Paller
     C     DFNTEU        IFGT      0
     C                   CLEAR                   EQDR
     C                   Z-ADD     WMN           EMN
     C                   Z-ADD     34            EGN
     C                   ADD       1             WREP0
     C                   Z-ADD     WREP0         EREP0
     C                   MOVEL     'EFP'         E8053                          Palle
     C                   WRITE     EQDR

     C                   EXSR      EQN34

     C                   ENDIF

      * Andre paller
     C     DFNTAU        IFGT      0
     C                   CLEAR                   EQDR
     C                   Z-ADD     WMN           EMN
     C                   Z-ADD     34            EGN
     C                   ADD       1             WREP0
     C                   Z-ADD     WREP0         EREP0
     C                   MOVEL     'PA'          E8053                          Palle
     C                   WRITE     EQDR

     C                   EXSR      EQN34

     C                   ENDIF
      * Containernummer
     C*    DFBHN         IFNE      *BLANKS
     C*                  CLEAR                   EQDR
     C*                  Z-ADD     WMN           EMN
     C*                  Z-ADD     34            EGN
     C*                  ADD       1             WREP0
     C*                  Z-ADD     WREP0         EREP0
     C*                  MOVEL     'CN'          E8053                          Palle
     C*                  MOVEL     DFBHN         E8260                          Palle
     C*                  WRITE     EQDR
      *
     C*                  ENDIF

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Antall utstyr                                      EQN34 /
      *////////////////////////////////////////////////////////////
     C     EQN34         BEGSR
     C                   Z-ADD     0             WREP1
      *
     C                   CLEAR                   EQNR
     C                   Z-ADD     WMN           EMN
     C                   Z-ADD     34            EGN
     C                   ADD       1             WREP1
     C                   Z-ADD     WREP0         EREP0
     C                   Z-ADD     WREP1         EREP1
      *
     C                   SELECT
     C     E8053         WHENEQ    'EFP'
     C                   Z-ADD     DFNTEU        ØN152
     C     E8053         WHENEQ    'PA'
     C                   Z-ADD     DFNTAU        ØN152
     C                   ENDSL
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           E6350
      *
     C                   WRITE     EQNR
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Fritekst                                           FTX00 /
      *////////////////////////////////////////////////////////////
     C     FTX00         BEGSR
      *
     C                   Z-ADD     0             WREP0
      *
     C                   CLEAR                   TX
      * Hent faste tekster til R-38
     C                   CALL      'SYFA12F'
     C                   PARM                    WFIRM
     C                   PARM                    DFAVD
     C                   PARM                    DFOPD
     C                   PARM                    DFFBNR
     C                   PARM                    U38TXT          700
     C                   MOVEA     U38TXT        TX
      * Finn antall brukte array-element
     C                   Z-ADD     10            X                 3 0
     C     TX(X)         DOWEQ     *BLANKS
     C                   SUB       1             X
     C     X             CABEQ     0             UTFAS
     C                   END
     C     UTFAS         TAG
      *
      * Fyll opp videre i array med "ordinær fritekst"
     C     R29KEY        SETLL     DOK29
     C     R29KEY        READE     DOK29                                  90
     C     *IN90         DOWEQ     *OFF
      *
     C                   ADD       1             X
     C     X             cabgt     26            FtxTag1
     C                   MOVEL     D29TXT        TX(X)
     C     R29KEY        READE     DOK29                                  90
     C                   ENDDO
      * fra oppdragets notisblokk kode R - info til kunde
N01  C                   MOVE      'R'           FRTKOD
     C     Free2Key      SETLL     Freetxt2
     C     Free2Key      READE     Freetxt2                               90
     C     *IN90         DOWEQ     *OFF
     C                   ADD       1             X
     C     X             cabgt     26            FtxTag1
     C                   MOVEL     FRTTXT        TX(X)
     C     Free2Key      READE     Freetxt2                               90
     C                   ENDDO
      * Klargjør felt for å legge ut FTX-ICN_segment
     C     FtxTag1       tag
     C                   CLEAR                   FTXR
     C                   Z-ADD     WMN           FMN
     C                   Z-ADD     0             FGN
     C                   ADD       1             WREP0
     C                   Z-ADD     WREP0         FREP0
     C                   MOVEL     'ICN'         F4451
      * Legg ut segment
     C     X             IFGT      0
     C                   EXSR      FTX00B
     C                   END
      *
      * R36 fritekst sendes som "SIC" (Senders Instr. to Carrier)
     C                   Z-ADD     0             X
     C                   CLEAR                   TX
     C     R29KEY        SETLL     DOK36
     C     R29KEY        READE     DOK36                                  90
     C     *IN90         DOWEQ     *OFF
      *
     C                   ADD       1             X
     C     X             cabgt     26            FtxTag2
     C                   MOVEL     D36TXT        TX(X)
     C     R29KEY        READE     DOK36                                  90
     C                   ENDDO
      * fra oppdragets notisblokk kode G - godsliste regnes som info til transportør
N01  C                   MOVE      'G'           FRTKOD
     C     Free2Key      SETLL     Freetxt2
     C     Free2Key      READE     Freetxt2                               90
     C     *IN90         DOWEQ     *OFF
     C                   ADD       1             X
     C     X             cabgt     26            FtxTag2
     C                   MOVEL     FRTTXT        TX(X)
     C     Free2Key      READE     Freetxt2                               90
     C                   ENDDO
      * Klargjør felt for å legge ut FTX-SIC_segment
     C     FtxTag2       tag
     C                   CLEAR                   FTXR
     C                   Z-ADD     WMN           FMN
     C                   Z-ADD     0             FGN
     C                   MOVEL     'SIC'         F4451
      *
     C     X             IFGT      0
     C                   EXSR      FTX00B
     C                   END
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Skriv fritekst fra array til segment (5 og 5)      FTX00B/
      *////////////////////////////////////////////////////////////
     C     FTX00B        BEGSR
      * Når SR er kalt - Alltid minst ett segment a 5. linjer,
     C                   Z-ADD     1             XA                3 0
     C     STRFTX        TAG
     C     XA            ADD       1             XB                3 0
     C     XA            ADD       2             XC                3 0
     C     XA            ADD       3             XD                3 0
     C     XA            ADD       4             XE                3 0
     C*
     C                   ADD       1             WREP0
     C                   Z-ADD     WREP0         FREP0
     C     TX(XA)        IFEQ      *BLANKS
     C                   EVAL      TX(XA) = '.'
     C                   ENDIF
     C                   MOVEL     TX(XA)        F4440A
     C                   MOVEL     TX(XB)        F4440B
     C                   MOVEL     TX(XC)        F4440C
     C                   MOVEL     TX(XD)        F4440D
     C                   MOVEL     TX(XE)        F4440E
     C                   WRITE     FTXR
      * Flere??
     C     XE            IFLT      X
     C                   ADD       5             XA
     C                   GOTO      STRFTX
     C                   END
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Fritekst                                           FTX16 /
      *////////////////////////////////////////////////////////////
     C     FTX16         BEGSR
      *
     C                   Z-ADD     0             WREP1
      *
     C                   CLEAR                   FTXR
     C                   Z-ADD     WMN           FMN
     C                   Z-ADD     16            FGN
     C                   ADD       1             WREP1
     C                   Z-ADD     WREP0         FREP0
     C                   Z-ADD     WREP1         FREP1
     C                   MOVEL     'AAA'         F4451
     C                   movel     hevs1         F4440A
     C                   WRITE     FTXR
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Fritekst                                           FTX29 /
      *////////////////////////////////////////////////////////////
     C     FTX29         BEGSR
      *
     C                   Z-ADD     0             WREP2
      * Erklæring
     C     DGS4440A      IFNE      *BLANKS
     C                   CLEAR                   FTXR
     C                   Z-ADD     WMN           FMN
     C                   Z-ADD     29            FGN
     C                   ADD       1             WREP2
     C                   Z-ADD     WREP0         FREP0
     C                   Z-ADD     WREP1         FREP1
     C                   Z-ADD     WREP2         FREP2
     C                   MOVEL     'AAC'         F4451
     C                   MOVEL     DGS4440A      F4440A
     C                   MOVEL     DGS4440B      F4440B
     C                   MOVEL     DGS4440C      F4440C
     C                   MOVEL     DGS4440D      F4440D
     C                   MOVEL     DGS4440E      F4440E
     C                   WRITE     FTXR
     C                   ENDIF
      * Teknisk navn
     C     DGS4440F      IFNE      *BLANKS
     C                   CLEAR                   FTXR
     C                   Z-ADD     WMN           FMN
     C                   Z-ADD     29            FGN
     C                   ADD       1             WREP2
     C                   Z-ADD     WREP0         FREP0
     C                   Z-ADD     WREP1         FREP1
     C                   Z-ADD     WREP2         FREP2
     C                   MOVEL     'AAD'         F4451
     C                   MOVEL     DGS4440F      F4440A
     C                   WRITE     FTXR
     C                   ENDIF
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Godsdetaljer                                       GID16 /
      *////////////////////////////////////////////////////////////
     C     GID16         BEGSR
      *____________
      * Varelinje-1
      *""""""""""""
     C                   Z-ADD     0             WREP0
     C                   Z-ADD     0             WREP1
     C                   ADD       1             WREP0
      * Other segments
     C                   EXSR      PIA16
     C                   EXSR      FTX16
     C                   EXSR      PCI22
     C                   EXSR      DGS29
      *
     C                   CLEAR                   GIDR
     C                   Z-ADD     WMN           GMN
     C                   Z-ADD     16            GGN
     C                   Z-ADD     WREP0         GREP0
      *
     C                   MOVEL     '99999'       G1496
     C                   Z-ADD     DFNT          ØN152
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           G7224A
     C                   MOVEL     'CT'          G7065A
     C     3             SUBST     DFVS:1        G7064A
      *
     C                   WRITE     GIDR
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Steder                                             LOC08 /
      *////////////////////////////////////////////////////////////
     C     LOC08         BEGSR
     C                   MOVEL     W0010         TSTMOT            4
     C     TSTMOT        CABNE     'EEK '        UTLOC08
     C                   MOVEL     W0004         TSTAVS            4
     C     TSTAVS        IFEQ      'RIKS'
     C                   MOVE      2020          DFPNLS
     C                   ELSE
     C                   MOVE      3013          DFPNLS
     C                   END
      *_____________
      * Førtransport
      *"""""""""""""
     C                   Z-ADD     0             WREP1
      *
     C                   CLEAR                   LOCR
     C                   Z-ADD     WMN           LMN
     C                   Z-ADD     8             LGN
     C                   ADD       1             WREP1
     C                   Z-ADD     WREP0         LREP0
     C                   Z-ADD     WREP1         LREP1
      *
     C                   MOVEL     '5'           L3227                          Hentested
     C                   MOVEL     DFPNLS        L3225                          Lastested
     C                   MOVEL     '16'          L1131A                         Lastested
      *
     C                   WRITE     LOCR
     C     UTLOC08       TAG
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Mål                                                MEA19 /
      *////////////////////////////////////////////////////////////
     C     MEA19         BEGSR
      *___________
      * Bruttovekt
      *"""""""""""
     C                   Z-ADD     0             WREP1
      *
     C                   CLEAR                   MEAR
     C                   Z-ADD     WMN           MMN
     C                   Z-ADD     19            MGN
     C                   ADD       1             WREP1
     C                   Z-ADD     WREP0         MREP0
     C                   Z-ADD     WREP1         MREP1
      *
     C                   MOVEL     'GRO'         M6311
     C                   MOVEL     'KGM'         M6411
     C                   Z-ADD     DFVKT         ØN152
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           M6314
      *
     C                   WRITE     MEAR
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Pengebeløp                                         MOA00 /
      *////////////////////////////////////////////////////////////
     C     MOA00         BEGSR

     C                   Z-ADD     0             WREP0
      * Forsikringsbeløp
     C     DFFVBL        IFGT      0
     C                   CLEAR                   MOAR
     C                   Z-ADD     WMN           MMN
     C                   Z-ADD     0             MGN
     C                   ADD       1             WREP1
     C                   Z-ADD     WREP0         MREP0

     C                   MOVEL     '157'         M5025
     C                   Z-ADD     DFFVBL        ØN152                                      e
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           M5004
     C     DFFVCU        IFEQ      *BLANKS
     C                   MOVEL     'NOK'         M6345
     C                   ELSE
     C                   MOVEL     DFFVCU        M6345
     C                   ENDIF

     C                   WRITE     MOAR
     C                   ENDIF
      * Girobeløp
     C     DFKDE         IFEQ      'X'
     C     WSBELV        ANDGT     0
     C                   CLEAR                   MOAR
     C                   Z-ADD     WMN           MMN
     C                   Z-ADD     0             MGN
     C                   ADD       1             WREP1
     C                   Z-ADD     WREP0         MREP0

     C                   MOVEL     '50'          M5025
     C                   Z-ADD     WSBELV        ØN152                                      e
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           M5004
     C                   MOVEL     'NOK'         M6345

     C                   WRITE     MOAR
     C                   ENDIF
      * Godskriving av agent.
      * AGENT-BELØP FINNES SOM REKV PÅ UTKJØRINGSSIDEN
     C                   MOVE      *ZEROS        ABELØP            9 0
     C     AGTRAN        IFNE      'T'
     C     VFKEY         CHAIN     HETRIU                             33
     C     *IN33         IFEQ      '0'
     C     TIBURF        CHAIN     KOSBU                              33
     C  N33              Z-ADD     BUBL          ABELØP
     C                   end
     C                   end
     C     AGTRAN        IFEQ      'J'
     C     AGTRAN        OREQ      'T'
     C                   CALL      'SYTR12Z'
     C                   PARM                    HEPRO
     C                   PARM                    HEAVD
     C                   PARM                    HEOPD
     C                   PARM                    TRADET            9 2
     C                   ADD       TRADET        ABELØP
     C                   END
     C                   CLEAR                   MOAR
     C                   Z-ADD     WMN           MMN
     C                   Z-ADD     0             MGN
     C                   ADD       1             WREP1
     C                   Z-ADD     WREP0         MREP0

     C                   MOVEL     '209'         M5025
     C                   Z-ADD     ABELØP        ØN152                                      e
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           M5004
     C                   MOVEL     'NOK'         M6345

     C                   WRITE     MOAR
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Navn og adresser Godsavsender                     NAD10A /
      *////////////////////////////////////////////////////////////
     C     NAD10A        BEGSR
     C                   Z-ADD     0             WREP0
     C                   Z-ADD     0             WREP1
     C                   ADD       1             WREP0
      *
     C                   CLEAR                   NADR
     C                   Z-ADD     WMN           NMN
     C                   Z-ADD     10            NGN
     C                   Z-ADD     WREP0         NREP0
      *
bhr  C                   MOVE      hekns         WUNDNR
     C     CUNKEY        CHAIN     CUNDL01                            51
     C                   MOVEL     'CZ'          N3035                          Godsavsender
     C     *IN51         IFEQ      '0'
     C     DKUND         ANDNE     'D'
     C                   MOVEL     HEKNS         N3039                          KNR hos transp.
     C                   MOVEL     '91'          N3055
     C                   MOVEL     KNAVN         N3036A
     C                   MOVEL     ADR1          N3042A
     C                   MOVEL     ADR3          N3164
     C                   MOVEL     POSTNR        N3251
     C                   ELSE
     C                   MOVEL     HENAS         N3036A
     C                   MOVEL     HEADS1        N3042A
     C                   MOVEL     HEADS3        PNR               5
     C                   MOVE      HEADS3        STED             25
     C                   MOVEL     STED          N3164
     C                   MOVEL     PNR           N3251
     C                   end
N02  c     DFKNS         ifne      *blanks
N02  C                   MOVEL     DFKNS         N3039                          KNR hos transp.
N02  C                   MOVEL     '87'          N3055
N02  c                   endif
      *
     C                   WRITE     NADR

     C                   EXSR      DOC12
     C                   EXSR      RFF14

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Navn og adresser Mottaker                         NAD10B /
      *////////////////////////////////////////////////////////////
     C     NAD10B        BEGSR
     C                   ADD       1             WREP0
      *
     C                   CLEAR                   NADR
     C                   Z-ADD     WMN           NMN
     C                   Z-ADD     10            NGN
     C                   Z-ADD     WREP0         NREP0
      *
     C                   MOVEL     'CN'          N3035                          Fakturamott.
     C     DFKNSM        IFGT      0
bhr  C     DFKNSM        ANDNE     1000
     C                   MOVEL     DFKNSM        N3039                          KNR hos transp.
     C                   MOVEL     '91'          N3055
     C                   ENDIF
     C                   MOVEL     DFNAVM        N3036A
     C                   MOVEL     DFAD1M        N3042A
     C                   MOVEL     WADM3B        N3164
     C                   MOVEL     WADM3A        N3251

     C                   WRITE     NADR

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Navn og adresser Avvikende Leveringsadresse       NAD10C /
      *////////////////////////////////////////////////////////////
     C     NAD10C        BEGSR
     C                   MOVE      DFPOUL        XXPOUL            4
     C     DFNAVL        IFEQ      DFNAVM
     C     DFAD1L        ANDEQ     DFAD1M
     C     XXPOUL        ANDEQ     WADM3A
     C     DFADUL        ANDEQ     WADM3B
     C                   GOTO      NAD102
     C                   END
      *
     C                   ADD       1             WREP0
      *
     C                   CLEAR                   NADR
     C                   Z-ADD     WMN           NMN
     C                   Z-ADD     10            NGN
     C                   Z-ADD     WREP0         NREP0
      *
     C                   MOVEL     'DP'          N3035
     C                   MOVEL     DFNAVL        N3036A
     C                   MOVEL     DFAD1L        N3042A
     C                   MOVEL     DFADUL        N3164
     C                   MOVEL     DFPOUL        N3251
      *
     C                   WRITE     NADR
      *
     C     NAD102        TAG
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Navn og adresser ANNEN fraktbetaler               NAD10D /
      *////////////////////////////////////////////////////////////
     C     NAD10D        BEGSR
     C                   ADD       1             WREP0
      *
     C                   CLEAR                   NADR
     C                   Z-ADD     WMN           NMN
     C                   Z-ADD     10            NGN
     C                   Z-ADD     WREP0         NREP0
      *
     C                   MOVEL     'FP'          N3035                          Fakturamott.
     C     DFKNX         IFNE      *BLANKS
     C                   MOVEL     DFKNX         N3039                          KNR hos transp.
     C                   MOVEL     '87'          N3055
     C                   ELSE
     C     DFKNSX        IFGT      0
     C                   MOVEL     DFKNSX        N3039                          KNR hos transp.
     C                   MOVEL     '91'          N3055
     C                   ENDIF
     C                   ENDIF
     C                   MOVEL     DFNAVX        N3036A
     C                   MOVEL     DFAD1X        N3042A
     C                   MOVEL     WADX3B        N3164
     C                   MOVEL     WADX3A        N3251

     C                   WRITE     NADR

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Kolli ID                                           PCI22 /
      *////////////////////////////////////////////////////////////
     C     PCI22         BEGSR
     C                   Z-ADD     0             WREP1
     C     DFGM          IFNE      *BLANKS
     C     DFGM2         ORNE      *BLANKS
      *
     C                   CLEAR                   PCIR
     C                   Z-ADD     WMN           PMN
     C                   Z-ADD     22            PGN
     C                   ADD       1             WREP1
     C                   Z-ADD     WREP0         PREP0
     C                   Z-ADD     WREP1         PREP1
      *
     C     DFGM          IFEQ      *BLANKS
     C                   MOVEL     'FORTS'       DFGM
     C                   ENDIF
     C                   MOVEL     '24'          P4233
     C     DFGM          IFNE      *BLANKS
     C                   MOVEL     DFGM          P7102A
     C                   ENDIF
     C     DFGM2         IFNE      *BLANKS
     C                   MOVEL     DFGM2         P7102B
     C                   ENDIF
      *
     C                   WRITE     PCIR
      *
     C                   ENDIF
      * KOLLI-ID...
     C     DOKUFMKEY     KLIST
     C                   KFLD                    FMRI
     C                   KFLD                    FMAVD
     C                   KFLD                    FMOPD
     C                   KFLD                    FMFBNR
     C                   KFLD                    FMLINR
     C                   MOVE      DFRI          FMRI
     C                   MOVE      DFAVD         FMAVD
     C                   MOVE      DFOPD         FMOPD
     C                   MOVE      DFFBNR        FMFBNR
     C                   MOVE      *ZEROS        FMLINR
      *
     C     DOKUFMKEY     SETLL     DOKUFM
     C     DOKUFMKEY     READE     DOKUFM                                 33
     C     *IN33         DOWEQ     '0'
     C     FMST          IFEQ      'I'
     C     FMST          oreq      'E'
     C                   CLEAR                   PCIR
     C                   Z-ADD     WMN           PMN
     C                   Z-ADD     22            PGN
     C                   ADD       1             WREP1
     C                   Z-ADD     WREP0         PREP0
     C                   Z-ADD     WREP1         PREP1
     C                   MOVEL     '18'          P4233
     C                   MOVEL     FMMRK1        P7102A
     C                   WRITE     PCIR
     C                   END
     C     DOKUFMKEY     READE     DOKUFM                                 33
     C                   END
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Ekstra vareinformasjon                             PIA16 /
      *////////////////////////////////////////////////////////////
     C     PIA16         BEGSR

     C     DFFVBL        IFGT      0

     C                   Z-ADD     0             WREP1
     C                   CLEAR                   PIAR
     C                   Z-ADD     WMN           PMN
     C                   Z-ADD     16            PGN
     C                   ADD       1             WREP1
     C                   Z-ADD     WREP0         PREP0
     C                   Z-ADD     WREP1         PREP1
     C                   MOVEL     '5'           P4347
     C                   MOVEL     '2'           P7140A
     C                   MOVEL     'ZTG'         P7143A
     C                   WRITE     PIAR

     C                   ENDIF

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Referanser                                         RFF03 /
      *////////////////////////////////////////////////////////////
     C     RFF03         BEGSR
     C                   Z-ADD     0             WREP0
      * Godsnummer
     C     HEGN          IFNE      *BLANKS
     C                   CLEAR                   RFFR
     C                   Z-ADD     WMN           RMN
     C                   Z-ADD     3             RGN
     C                   ADD       1             WREP0
     C                   Z-ADD     WREP0         RREP0
     C                   MOVEL     'XC'          R1153
     C                   MOVEL     HEGN          R1154
     C                   WRITE     RFFR
     C                   ENDIF
      * Senders referanse
     C                   CLEAR                   RFFR
     C                   Z-ADD     WMN           RMN
     C                   Z-ADD     3             RGN
     C                   ADD       1             WREP0
     C                   Z-ADD     WREP0         RREP0
      * Freight forewarders ref.
     C                   MOVEL     'FF'          R1153
     C                   MOVEL     DFAVD         R1154
     C     R1154         CAT       '/':0         R1154
     C                   MOVEL     DFOPD         WFOPDA            7
     C     R1154         CAT       WFOPDA:0      R1154
     C     R1154         CAT       '/':0         R1154
     C     R1154         CAT       HESG:0        R1154
     C                   WRITE     RFFR
      * oppr avsenders referanse.
N03  C                   MOVEL     *blanks       R1154
     C                   ADD       1             WREP0
     C                   Z-ADD     WREP0         RREP0
     C                   MOVEL     'CU'          R1153
     C                   MOVEL     HERFA         R1154
     C                   WRITE     RFFR
      * BHR * START
      * UTLEV.FORBEHOLD SENDES SOM REF..
     C     HEKF          IFNE      *BLANKS
     C     HEKFT1        ANDEQ     *BLANKS
     C                   CLEAR                   RFFR
     C                   Z-ADD     WMN           RMN
     C                   Z-ADD     3             RGN
     C                   ADD       1             WREP0
     C                   Z-ADD     WREP0         RREP0
     C                   MOVEL     'ZUF'         R1153
     C                   MOVEL     HEKF          R1154
     C                   WRITE     RFFR
     C                   ENDIF
      * BHR * SLUTT
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Referanser                                         RFF14 /
      *////////////////////////////////////////////////////////////
     C     RFF14         BEGSR
     C                   Z-ADD     0             WREP1
      * Gironummer (bank/post)
     C     DFKDE         IFEQ      'X'
     C     WSBELV        ANDGT     0
     C     BANKG         ANDNE     *BLANKS
     C                   CLEAR                   RFFR
     C                   Z-ADD     WMN           RMN
     C                   Z-ADD     14            RGN
     C                   ADD       1             WREP1
     C                   Z-ADD     WREP0         RREP0
     C                   Z-ADD     WREP1         RREP1
     C                   MOVEL     'ADE'         R1153
     C                   MOVEL     BANKG         R1154
     C                   WRITE     RFFR
     C                   ENDIF
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR for init                                        SRINIT /
      *////////////////////////////////////////////////////////////
     C     SRINIT        BEGSR
      * HENTE DAGENS DATO OG TID
     C                   TIME                    WTIME
     C                   Z-ADD     WÅÅ           WÅ
     C                   Z-ADD     WDD           WD
     C                   Z-ADD     WMM           WM
      * INIT AV ARBEIDSFELT
     C                   Z-ADD     0             ØX                2 0
     C                   MOVE      *BLANKS       ØAN              12
     C     *LIKE         DEFINE    CMN           WMN
     C     *LIKE         DEFINE    DREP0         WREP0
     C     *LIKE         DEFINE    DREP1         WREP1
     C     *LIKE         DEFINE    DREP2         WREP2
     C     *LIKE         DEFINE    DFRI          WFRI
     C     *LIKE         DEFINE    DFAVD         WFAVD
     C     *LIKE         DEFINE    DFOPD         WFOPD
     C     *LIKE         DEFINE    DFFBNR        WFFBNR
     C     *LIKE         DEFINE    FIRMA         WFIRM
     C     *LIKE         DEFINE    KUNDNR        WUNDNR
     C     *LIKE         DEFINE    DGSLINR       WGSLINR

     C                   EVAL      WFRI='F'
      * DEF AV KEYS
     C     DOKKEY        KLIST
     C                   KFLD                    WFRI
     C                   KFLD                    WFAVD
     C                   KFLD                    WFOPD
     C                   KFLD                    WFFBNR
     C     TRAKEY        KLIST
     C                   KFLD                    WFIRM
     C                   KFLD                    TUKNT2
     C     R29KEY        KLIST
     C                   KFLD                    WFAVD
     C                   KFLD                    WFOPD
     C                   KFLD                    WFFBNR
     C     FREE2KEY      KLIST
     C                   KFLD                    WFAVD
     C                   KFLD                    WFOPD
     C                   KFLD                    FRTKOD
N01  C*                  MOVE      'G'           FRTKOD
     C     CUNKEY        KLIST
     C                   KFLD                    WFIRM
     C                   KFLD                    WUNDNR
     C     HEAKEY        KLIST
     C                   KFLD                    WFAVD
     C                   KFLD                    WFOPD
     C     VFKEY         KLIST
     C                   KFLD                    WFAVD
     C                   KFLD                    WFOPD
     C                   KFLD                    TISLAG
     C                   MOVE      'VF'          TISLAG
     C     DGSKEY        KLIST
     C                   KFLD                    WFAVD
     C                   KFLD                    WFOPD
     C                   KFLD                    WFFBNR
     C                   KFLD                    WGSLINR
     C     FAKTKE        KLIST
     C                   KFLD                    WFAVD
     C                   KFLD                    WFOPD
     C                   KFLD                    WFFBNR
N02  C     KodtaKey      KLIST
N02  C                   KFLD                    XAUNI             1
N02  C                   KFLD                    HEAVD
N02  C                   move      'A'           XAUNI
N02  C     KOTRKE        KLIST
N02  C                   KFLD                    WFIRM
N02  C                   KFLD                    KOAKNR
N02  C                   KFLD                    TUKNT2
      * Input parameters
     C     *ENTRY        PLIST
     C                   PARM                    WFIRM
     C                   PARM                    WFAVD
     C                   PARM                    WFOPD
     C                   PARM                    WFFBNR
     C                   PARM                    W0004            35            AVS
     C                   PARM                    W0010            35            MOTT
     C                   PARM                    W0068            35            MSG-ID
     C                   move      W0068         wz0068
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Venstrestill numeriske verdier                      SRNE /
      *////////////////////////////////////////////////////////////
     C     SRNE          BEGSR
      * Minus value ?
     C     ØN152         IFLT      0                                            <-------------I
     C                   Z-SUB     ØN152         ØN152                                        I
     C                   MOVE      '-'           ØSIGN             1                          I
     C                   ELSE                                                   <-------------I
     C                   MOVE      '+'           ØSIGN                                        I
     C                   ENDIF                                                  <-------------I
      * Flytte in rett verdi i Alfa 12-felt
     C                   MOVE      *ZEROS        ØA12
     C     ØA152D        IFEQ      '00'                                         <-------------I
     C                   MOVE      ØA152H        ØA12                                         I
     C                   ELSE                                                   <-------------I
     C                   MOVEL     ØA152X        ØA12                                         I
     C                   MOVE      ',  '         ØA12                                         I
     C                   MOVE      ØA152D        ØA12                                         I
     C                   END                                                    <-------------I
      * lete fra venstre og finn foerste siffer over 0
     C     1             DO        11            ØX                             <-------------I
     C     Ø12(ØX)       IFNE      '0'                                           <-----------II
     C     Ø12(ØX)       IFEQ      ','                                           <-----------II
     C                   SUB       1             ØX
     C                   ENDIF                                                   <-----------II
     C                   GOTO      SRNE1                                                     II
     C                   END                                                     <-----------II
     C                   END                                                    <-------------I
     C     SRNE1         TAG
      * Venstrestill til et nytt felt
     C                   MOVE      *BLANKS       ØAN
     C     ØSIGN         IFEQ      '-'                                          <-------------I
     C                   SUB       1             ØX                                           I
     C                   MOVE      '-'           Ø12(ØX)                                      I
     C                   ENDIF                                                  <-------------I
     C                   MOVEA     Ø12(ØX)       ØAN
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Transportdetaljer                                  TDT08 /
      *////////////////////////////////////////////////////////////
     C     TDT08         BEGSR
     C                   Z-ADD     0             WREP0
     C                   Z-ADD     0             WREP1
     C                   ADD       1             WREP0
      * Other segments
     C                   EXSR      LOC08
      *
     C                   CLEAR                   TDTR
     C                   Z-ADD     WMN           TMN
     C                   Z-ADD     8             TGN
     C                   Z-ADD     WREP0         TREP0
      *
     C                   MOVEL     '20'          T8051
     C                   MOVEL     '3'           T8067
     C                   MOVEL     HEHAWB        T8212
      *
     C                   WRITE     TDTR
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Leveringsbetingelser                               TOD00 /
      *////////////////////////////////////////////////////////////
     C     TOD00         BEGSR
     C                   Z-ADD     0             WREP0
     C                   Z-ADD     0             WREP1
      *
     C                   CLEAR                   TODR
     C                   Z-ADD     WMN           TMN
     C                   Z-ADD     0             TGN
     C                   ADD       1             WREP0
     C                   Z-ADD     WREP0         TREP0
      *
     C                   MOVEL     '6'           T4055
     C                   MOVEL     HEFR          T4053
     C                   MOVEL     '106'         T1131
      *
     C                   WRITE     TODR
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Transportprodukt                                   TSR00 /
      *////////////////////////////////////////////////////////////
     C     TSR00         BEGSR
     C                   Z-ADD     0             WREP0
     C                   Z-ADD     0             WREP1
      * Hent produktkode
     C     DFPROK        IFEQ      *BLANKS
     C                   EVAL      DFPROK='TG9  '
     C                   ENDIF
      * Produkt ?
     C                   CLEAR                   TSRR
     C                   Z-ADD     WMN           TMN
     C                   Z-ADD     0             TGN
     C                   ADD       1             WREP0
     C                   Z-ADD     WREP0         TREP0
     C                   MOVEL     '9'           T4065                          Stckgods
     C                   SELECT
     C     DFPROK        WHENEQ    'TG27 '
     C                   MOVEL     '27'          T4065
     C     DFPROK        WHENEQ    'TG7  '
     C                   MOVEL     '7 '          T4065
     C     DFPROK        WHENEQ    'TG8  '
     C                   MOVEL     '8'           T4065
     C     DFPROK        WHENEQ    'TG9 '
     C                   MOVEL     '9'           T4065
     C     DFPROK        WHENEQ    'TG71'
     C                   MOVEL     '7'           T4065
     C     DFPROK        WHENEQ    'TG91'
     C                   MOVEL     '9'           T4065
     C                   ENDSL
      * Ekspress ?
     C     DFPROK        IFEQ      'TG71'
     C     DFPROK        OREQ      'TG91'
     C                   EVAL      T4219='1'
     C                   ENDIF
      * Farlig gods ?
     C                   Z-ADD     99            WGSLINR
     C     DGSKEY        CHAIN     DOKUDGS                            52
     C     *IN52         IFEQ      *OFF
     C     DGS8351       ANDNE     *BLANKS
     C                   MOVEL     'RMD'         T7085
     C                   ENDIF
      *
     C                   WRITE     TSRR
      * Giro ?
     C     DFKDE         IFEQ      'X'
     C     WSBELV        ANDGT     0
     C                   CLEAR                   TSRR
     C                   Z-ADD     WMN           TMN
     C                   Z-ADD     0             TGN
     C                   ADD       1             WREP0
     C                   Z-ADD     WREP0         TREP0
     C                   MOVEL     'ZAD'         T4065
      *
     C                   WRITE     TSRR
     C                   ENDIF
      * Forsikring
     C     DFFVBL        IFGT      0
     C                   CLEAR                   TSRR
     C                   Z-ADD     WMN           TMN
     C                   Z-ADD     0             TGN
     C                   ADD       1             WREP0
     C                   Z-ADD     WREP0         TREP0
     C                   MOVEL     'ZIN'         T4065
      *
     C                   WRITE     TSRR
     C                   ENDIF
      * Tollpass
     C     DFTOLL        IFGT      0
     C                   CLEAR                   TSRR
     C                   Z-ADD     WMN           TMN
     C                   Z-ADD     0             TGN
     C                   ADD       1             WREP0
     C                   Z-ADD     WREP0         TREP0
     C                   MOVEL     'ZND'         T4065
      *
     C                   WRITE     TSRR
     C                   ENDIF
     C                   ENDSR
