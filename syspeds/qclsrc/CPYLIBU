             PGM        PARM(&FRALIB &TILLIB &TOMME &BEHAN)
             DCL        VAR(&FRALIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&TILLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&PROFIL) TYPE(*CHAR) LEN(10)
             DCL        VAR(&FORSTE) TYPE(*CHAR) LEN(1)
             DCL        VAR(&FINNES) TYPE(*CHAR) LEN(1)
             DCL        VAR(&TOMME)  TYPE(*CHAR) LEN(1)
             DCL        VAR(&BEHAN)  TYPE(*CHAR) LEN(1)
             DCL        VAR(&MESS)   TYPE(*CHAR) LEN(20)
             DCL        VAR(&EDIDAT) TYPE(*CHAR) LEN(256)
             DCL        VAR(&BACK) TYPE(*CHAR) LEN(256)
             DCL        VAR(&TESTUSER) TYPE(*CHAR) LEN(256)
             DCLF       FILE(*LIBL/FILNVNP) RCDFMT(QWHFDBAS)

             IF         COND(&BEHAN = 'J') THEN(DO)
             CALL       PGM(CPYLIBS) PARM(&FRALIB) /* Spesialer +
                          foran cpy. */
             ENDDO

             OVRDBF     FILE(FILNVNP) TOFILE(&TILLIB/FILNVNP)

             CHGJOB     JOB(*) LOG(4 00 *SECLVL) LOGCLPGM(*YES)

             CHKOBJ     OBJ(&TILLIB/PROGRESS) OBJTYPE(*DTAARA)
             MONMSG     MSGID(CPF9801 CPF9810) EXEC(CHGVAR +
                          VAR(&FORSTE) VALUE('J')) /* 'FORSTE får +
                          verdien 'J' hvis PROGRESS ikke finnes fra +
                          før av' */

             IF         COND(&FORSTE = 'J') THEN(DO)
             CRTDTAARA  DTAARA(&TILLIB/PROGRESS) TYPE(*CHAR) LEN(20) +
                          VALUE(' ') TEXT('Inneholder filnavn på +
                          siste OK kopierte fil')
             SNDPGMMSG  MSGID(CPF9898) MSGF(QCPFMSG) MSGDTA('Lager +
                          filoversikt i FILNVNP i til-bibliotek') +
                          TOPGMQ(*EXT) MSGTYPE(*STATUS)

             DSPFD      FILE(&FRALIB/*ALL) TYPE(*BASATR) +
                          OUTPUT(*OUTFILE) FILEATR(*PF) +
                          OUTFILE(&TILLIB/FILNVNP)
             SNDPGMMSG  MSGID(CPF9801) MSGF(QCPFMSG) TOPGMQ(*EXT) +
                          MSGTYPE(*STATUS) /* Blanker ut melding på +
                          skjerm med "tom" melding */
             ENDDO
LOOP1:
             RCVF       DEV(*FILE) RCDFMT(*FILE)
             MONMSG     MSGID(CPF0864) EXEC(GOTO CMDLBL(SLUTT))

             IF         COND(&FORSTE *NE 'J') THEN(DO)
             RTVDTAARA  DTAARA(&TILLIB/PROGRESS (1 10)) RTNVAR(&PROFIL)
             IF         COND(&ATFILE *LE &PROFIL) THEN(GOTO +
                          CMDLBL(LOOP1))
             ENDDO

             IF         COND(&ATNOMB *EQ 0) THEN(GOTO CMDLBL(LOOP1)) +
                          /* Kopierer ikke filen hvis antall +
                          komponenter er null */

             IF         COND(&ATDTAT *NE 'D') THEN(GOTO +
                          CMDLBL(LOOP1)) /* Kopierer bare +
                          datafiler, ikke src filer             */

             CHGVAR     VAR(&FINNES) VALUE('J')
             CHKOBJ     OBJ(&TILLIB/&ATFILE) OBJTYPE(*FILE) +
                          MBR(*NONE) /* Hvis fil ikke finnes i +
                          til-bibliotek opprettes den, hvis finnes +
                          (som den bør gjøre ved normal +
                          oppgradering) kopieres filen med +
                          *replace, - først forsøkes uten FMTOPT - +
                          dersom nødvendig sett FMTOPT(*MAP/*DROP) +
                          (for å kopiere hurtigst mulig) */

             MONMSG     MSGID(CPF9801) EXEC(CHGVAR VAR(&FINNES) +
                          VALUE('N'))
             OVRDBF     FILE(&ATFILE) FRCRATIO(*NONE)

             IF         COND(&ATFILE *EQ 'EDIRE') THEN(DO)
             DLTF       FILE(&TILLIB/EDIRE)
             MONMSG     MSGID(CPF0000)
             MOVOBJ     OBJ(&FRALIB/EDIRE) OBJTYPE(*FILE) +
                          TOLIB(&TILLIB)
             GOTO       CMDLBL(OPDDTAARA)
             ENDDO
             IF         COND(&ATFILE *EQ 'EDISE') THEN(DO)
             DLTF       FILE(&TILLIB/EDISE)
             MONMSG     MSGID(CPF0000)
             MOVOBJ     OBJ(&FRALIB/EDISE) OBJTYPE(*FILE) +
                          TOLIB(&TILLIB)
             GOTO       CMDLBL(OPDDTAARA)
             ENDDO
             IF         COND(&ATFILE *EQ 'EDIRE1K') THEN(DO)
             DLTF       FILE(&TILLIB/EDIRE1K)
             MONMSG     MSGID(CPF0000)
             MOVOBJ     OBJ(&FRALIB/EDIRE1K) OBJTYPE(*FILE) +
                          TOLIB(&TILLIB)
             GOTO       CMDLBL(OPDDTAARA)
             ENDDO
             IF         COND(&ATFILE *EQ 'EDISE1K') THEN(DO)
             DLTF       FILE(&TILLIB/EDISE1K)
             MONMSG     MSGID(CPF0000)
             MOVOBJ     OBJ(&FRALIB/EDISE1K) OBJTYPE(*FILE) +
                          TOLIB(&TILLIB)
             GOTO       CMDLBL(OPDDTAARA)
             ENDDO
             IF         COND(&ATFILE *EQ 'EDI.BACK') THEN(DO)
             DLTF       FILE(&TILLIB/EDI.BACK)
             MONMSG     MSGID(CPF0000)
             MOVOBJ     OBJ(&FRALIB/EDI.BACK) OBJTYPE(*FILE) +
                          TOLIB(&TILLIB)
             GOTO       CMDLBL(OPDDTAARA)
             ENDDO
             IF         COND(&ATFILE *EQ 'EDI.BACK1K') THEN(DO)
             DLTF       FILE(&TILLIB/EDI.BACK1K)
             MONMSG     MSGID(CPF0000)
             MOVOBJ     OBJ(&FRALIB/EDI.BACK1K) OBJTYPE(*FILE) +
                          TOLIB(&TILLIB)
             GOTO       CMDLBL(OPDDTAARA)
             ENDDO

             IF         COND(&FINNES *EQ 'J') THEN(DO)
             CPYF       FROMFILE(&FRALIB/&ATFILE) +
                          TOFILE(&TILLIB/&ATFILE) FROMMBR(*ALL) +
                          TOMBR(*FROMMBR) MBROPT(*REPLACE) FROMRCD(1)
/*DERSOM FMTOPT *MAP, *DROP ELLER *MAP/*DROP TRENGS VIL DE FØRSTE */
/*TEGN AV MELDINGSTEKSTEN VÆRE 'FMTOPT..'(se CMPDTA)->Ny CPYF     */
/*CPF2869= Kopiering av tomt framember med replace */
/*CPF2817= Div.andre feil,som kan passere */
             MONMSG     MSGID(CPF2962 CPF2965 CPF2969 CPF2869 +
                          CPF2870 CPF2817) EXEC(DO)
             CPYF       FROMFILE(&FRALIB/&ATFILE) +
                          TOFILE(&TILLIB/&ATFILE) FROMMBR(*ALL) +
                          TOMBR(*FROMMBR) MBROPT(*REPLACE) +
                          FMTOPT(*MAP *DROP) FROMRCD(1)
             MONMSG     MSGID(CPF2869 CPF2870 CPF2817) /* Kopiering +
                          av tomt fra-member med replace + diverse +
                          feil i CPF2817 */
             ENDDO
             ENDDO
             ELSE       CMD(DO)
             CPYF       FROMFILE(&FRALIB/&ATFILE) +
                          TOFILE(&TILLIB/&ATFILE) FROMMBR(*ALL) +
                          TOMBR(*FROMMBR) MBROPT(*ADD) CRTFILE(*YES) +
                          FROMRCD(1)
             MONMSG     MSGID(CPF2869 CPF2817) /* Kopiering av tomt +
                          fra-member med replace + diverse feil i +
                          CPF2817 */
             ENDDO

             IF         COND(&TOMME = 'J') THEN(DO)
             CLRPFM     FILE(&FRALIB/&ATFILE) MBR(*FIRST)
             MONMSG     MSGID(CPF3133) /* Filen inneholder ingen +
                          komponenter */
             CHGVAR     VAR(&MESS) VALUE(&ATFILE *CAT &FRALIB)
             IF         COND(&ATNOMB *GT 1) THEN(SNDUSRMSG +
                          MSGID(BBA0050) MSGF(SYMSGF) +
                          MSGDTA(&MESS) MSGTYPE(*INFO) +
                          TOUSR(*REQUESTER))
             ENDDO

 OPDDTAARA:
             CHGDTAARA  DTAARA(&TILLIB/PROGRESS (1 10)) VALUE(&ATFILE)
             CHGDTAARA  DTAARA(&TILLIB/PROGRESS (11 10)) VALUE(&TILLIB)

             GOTO       CMDLBL(LOOP1)

SLUTT:
        /* DIVERSE TING SOM SKAL UTFØRES ETTER KONVERTERING    */

             IF         COND(&BEHAN = 'J') THEN(DO)
             CALL       PGM(CPYLIBE) PARM(&FRALIB &TILLIB) /* +
                          Spesialer ETTER cpy. */
        /* FLYTT DATA FRA EDIDTA*/
             RTVDTAARA  DTAARA(&FRALIB/EDIDTA) RTNVAR(&EDIDAT)
             MONMSG     MSGID(CFP0000)
             CHGDTAARA  DTAARA(&TILLIB/EDIDTA) VALUE(&EDIDAT)
             MONMSG     MSGID(CFP0000)
        /* FLYTT DATA FRA BACK */
             RTVDTAARA  DTAARA(&FRALIB/BACK) RTNVAR(&BACK)
             MONMSG     MSGID(CFP0000)
             CHGDTAARA  DTAARA(&TILLIB/BACK) VALUE(&BACK)
             MONMSG     MSGID(CFP0000)
             ENDDO

             CALL       PGM(V10FKC01)
             RCLRSC
ENDPGM:
