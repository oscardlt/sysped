     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      ALTHELP
     A                                      HELP
      *
     A          R SVE051DA                  SFL
     A            ZOP            1A  I  8  2VALUES(' ' '2' '4' '5')
     A            SVEA_SYAV      4Y 0O  8  5EDTCDE(4)
     A            SVEA_SYOP      7Y 0O  8 12EDTCDE(4)
      *
     A          R SVE051DB                  SFLCTL(SVE051DA)
     A                                      SFLSIZ(0011)
     A                                      SFLPAG(0010)
     A  91N93N94                            ROLLUP(25 'Rollup')
     A  91N93N94                            ROLLDOWN(26 'Rolldown')
     A                                      CF03(03 'Gå ut')
     A                                      CF04(04 'Søk')
     A                                      CF05(05 'Refresh')
     A                                      CF06(06 'NY')
     A                                      CF12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/SVE051'
     A                                      COLOR(BLU)
     A                                  1 16'DA'
     A                                      COLOR(BLU)
     A                                  1 29'Underhåll av TDS Export'
     A                                      COLOR(WHT)
     A                                  1 64DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  1 73TIME
     A                                      COLOR(BLU)
     A                                  2 30'Avdelingsupplysningar'
     A                                      COLOR(WHT)
     A            ZRECNO         4S 0O  2 71SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  4  2'Vælj option eller tryck Enter.'
     A                                      COLOR(BLU)
     A                                  5  4'2=Ændra   4=Radera'
     A                                      COLOR(BLU)
     A                                  7  5'Avd'
     A                                      COLOR(WHT)
     A                                  7  9'Uppdragsnr'
     A                                      COLOR(WHT)
      *
     A          R SVE051DC
     A                                      OVERLAY
     A                                 23  2'F3=Avsluta'
     A                                      COLOR(BLU)
     A                                 23 15'F4=Søk'
     A                                      COLOR(BLU)
     A                                 23 24'F6=Skapa'
     A                                      COLOR(BLU)
     A                                 23 35'F12=Avbryt'
     A                                      COLOR(BLU)
      *
     A          R SVE051DD
     A                                      OVERLAY
     A                                      CF03(03 'Gå ut')
     A                                      CF04(04 'Søk')
     A                                      CF12(12 'Forrige')
     A            ZVEA_SYAV      4Y 0B 18  5EDTCDE(4)
     A                                 19  1'EORI..:'
     A            ZVEA_OMEO     17A  B 19  9
     A                                 19 41'Typ:'
     A            ZVEA_OMTY      1A  B 19 46
     A                                 20  1'Handl.:'
     A            ZVEA_OMHA     35A  B 20  9
     A                                 20 47'Tel:'
     A            ZVEA_OMTL     25A  B 20 53DSPATR(PC)
     A                                 23  2'F3=Avsluta'
     A                                      COLOR(BLU)
     A                                 23 15'F4=Søk'
     A                                      COLOR(BLU)
     A                                 23 24'F12=Avbryt'
     A                                      COLOR(BLU)
      *
     A          R SVE051DE
     A                                 20  2' '
     A                                 21  2' '
      *
     A          R SVE051DØ
     A                                      OVERLAY
     A            ZMSGNO         7A  P
     A            ZMSGTX        79   O 24  2MSGID(&ZMSGNO SYMSGF)
     A                                      COLOR(WHT)
