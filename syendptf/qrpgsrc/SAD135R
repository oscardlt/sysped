      *
      ***********************************************************
      *** PROGRAM SKAL OPPDATERE SADEFCF                      ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01-02 06-08 10 11 13-26
      *** LEDIGE INDIKATORER 27-29 31 32 34-48 50-81          ***
      ***********************************************************
      *
     H DFTACTGRP(*NO)
     F*KODTA     IF   E           K DISK
     F*CUNDL01   IF   E           K DISK
     FKOFAST    IF   E           K DISK
     F*FIRM      IF   E           K DISK
     FEDIM4     IF   E           K DISK
     FSTED2     IF   E           K DISK
     FSTED2D    IF   E           K DISK    RENAME(STED2R:STED2RD)
     FHEADF     IF   E           K DISK
     FSADH      IF   E           K DISK
     FSVEH      IF   E           K DISK
     FSVZH      IF   E           K DISK
     FSADEFCL2  UF A E           K DISK
     FTRACKF    O  A E             DISK
     D                SDS
     D  UUSR                 254    263
     D                 DS
     D  WDT                    1      8
     D  WDT_N                  1      8  0
     D                 DS
     D  WTM                    1      6
     D  WTM_N                  1      6  0
      *  Prototype for 'SYGE15C'
     d SYGE15C         pr                  extpgm('SYGE15C')
     d wdt_                                like(wdt)
     d wtm_                                like(wtm)
      *  Prototype for SAD135R
     d sad135r         pr
     d uavd_                               like(uavd)
     d utdn_                               like(utdn)
     d u0068a                              like(u0068a)
     d u0068b                              like(u0068b)
     d untk_                               like(untk)
     d uvkb_                               like(uvkb)
     d utrid_                              like(utrid)
      *  *ENTRY Interface for Main Procedure
     d sad135r         pi
     d uavd                           4a
     d utdn                           7a
     d u0068a                         8a
     d u0068b                         6a
     d untk                           7a
     d uvkb                           9a
     d utrid                         17a
      /free

         exsr init;

         exsr init;
         chain (clavd: cltdn) sadefcl2;

         exsr srhentstd;

         if %found (sadefcl2);
           cl0068a = %dec(u0068a:8:0);
           cl0068b = %dec(u0068b:6:0);
           clntk   = %dec(untk:7:0);
           clvkb   = %dec(uvkb:9:0);
           cltrid  = utrid;
           update sadefcfr;

         else;

         // PROSEDYRE
           clpr  = '01';
           kftyp = 'SADEFPR';
           kfkod = '01';
           chain (kftyp: kfkod) kofast;
           if %found;
             clprt = kftxt;
           else;
             clprt = 'IMMEDIATE_RELEASE_IMPORT';
           endif;

           //EKSPORTTYPE
           chain (clavd: cltdn) sveh;
           select;
           when not %found;
           when sveh_mtyp = 'UGE';
             cletyp = '05';
             kftyp = 'SADEFETYPE';
             kfkod = '05';
             chain (kftyp: kfkod) kofast;
             if %found;
               cletypt = kftxt;
             else;
               cletypt = 'UGE-EXPORT';
             endif;
           endsl;

          // EKSPORT-ID
           if %found (sveh);
             chain sveh_tuid svzh;
             if %found;
               cleid = svzh_mrnn;
             endif;
           endif;

           //chain (clavd: cltdn) sadh;
           //if %found;
           //  setll *loval firm;
           //  read firm;
           //  chain ('A': clavd) kodta;
           //  chain (fifirm: koaknr) cundl01;
           //  clrg = syrg;
           //endif;
           cl0068a = %dec(u0068a:8:0);
           cl0068b = %dec(u0068b:6:0);
           clntk   = %dec(untk:7:0);
           clvkb   = %dec(uvkb:9:0);
           cltrid  = utrid;
           chain ('S': clavd: cltdn: cl0068a: cl0068b: m0068c) edim4;
           clrg = %subst(m0068:1:9);
           cl3039e = m3039e;
           write sadefcfr;
         endif;

         exsr ttsr;

         *inlr = *on;
         return;

      //*********************************************
       begsr srhentstd;

         chain (clavd: cltdn) headf;
         if %found (headf);
         // fra stede
           if cllkf = *blanks;
             cllkf = helka;
           endif;
           if clsdf = *blanks;
             clsdf = hesdf;
           endif;
           if clsdft = *blanks;
             st2nvn = *blanks;
             if cllkf = *blanks;
               chain clsdf sted2;
             else;
               chain (cllkf: clsdf) sted2d;
             endif;
             if clsdft = *blanks;
               clsdft = st2nvn;
             endif;
           endif;
         // til stede
           if cllkt = *blanks;
             cllkt = hetri;
           endif;
           if clsdt = *blanks;
             clsdt = hesdt;
           endif;
           if clsdft = *blanks;
             if cllkt = *blanks;
               chain clsdt sted2;
             else;
               chain (cllkt: clsdt) sted2d;
             endif;
             if clsdtt = *blanks;
               clsdft = st2nvn;
             endif;
           endif;
         endif;

       endsr;

      //*********************************************
       begsr init;

       clavd  = %dec(uavd:4:0);
       cltdn  = %dec(utdn:7:0);
       m0068c = 1;

       endsr;

      //*********************************************
       begsr ttsr;

       syge15c (wdt: wtm);

       ttavd  = clavd;
       ttopd  = cltdn;
       tttur  = 0;
       ttacti = 'EFC';
       ttdate = wdt_n;
       ttdate = wdt_n;
       tttime = wtm_n;
       tttiml = wtm_n;
       ttuser = uusr;
       tttext = 'Oppdrag er klar til E.F. manifest.';
       tttexl = 'Oppdrag er klar til E.F. manifest.';
       ttedev = 'RDY';

       write trackfr;

       endsr;

      /end-free
