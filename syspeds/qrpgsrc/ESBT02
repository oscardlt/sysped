      *********************************************************************
      *
      *
CRTPG+*  CRTPGM SYSPED/ESBT02 MODULE(*LIBL/ESBT02 *LIBL/INCGI)
CRTPGM*         ACTGRP(CGI) AUT(*USE)
      *
********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
********************************************************************
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FBTKAMP    IF   E           K DISK    USROPN
      *********************************************************************
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      /copy syspeds/qrpgsrcpy,variables3
      * Indicators for GetHtmlIfsMult subprocedure
     D IfsMultIndicators...
     d                 ds
     D  NoErrors                       n
     D  NameTooLong                    n
     D  NotAccessible                  n
     D  NoFilesUsable                  n
     D  DupSections                    n
     D  FileIsEmpty                    n

      *
     D OddEven         s             10
     0* Client input variables
     D user            s             10
     D FRADAT          s              8
     D FRADATN         s              8  0
     D TILDAT          s              8
     D TILDATN         s              8  0
N01  D Title           S            100
      *
     D ANTSND          s              5  0
     D MaxSN           s              5
     D MaxSND          s              5  0
     D ANTRCD          s              5  0
     D MaxRC           s              5
     D MaxRCD          s              5  0
     D Spaces          s             12a   inz('&nbsp;&nbsp;')
     D ItemCount       s             10i 0
     D i               s             10i 0
     D UPC             C                   CONST('ABCDEFGHIJKLMNOPQRST-
     D                                     UVWXYZÆØÅ')
     D LO              C                   CONST('abcdefghijklmnopqrst-
     D                                     uvwxyzæøå')
      *
      /copy syspeds/qrpgsrcpy,prolog3
      *
      * Get program start time for calculating execution time
     C                   time                    timedata1
      * Parse input / cvt to numeric
     C                   exsr      Parse
      * File open / keys
     C                   EXSR      INIT
      * Read output skeleton html member into memory
     C                   exsr      LoadHtml
      * Set section variables
     C                   exsr      Settop
N02  C                   callp     wrtsection('StdtopTab')
     C                   callp     wrtsection('top')
      *
      * HOVEDRUTINE
      *
      * Vis liste over registrete saker
      *
     C     MaxSnd        IFEQ      *ZEROS
     C                   Z-add     500           MaxSnd
     C                   END
     C     MaxRcd        IFEQ      *ZEROS
     C                   Z-add     2000          MaxRcd
     C                   END
      *
     C                   Move      *zeros        AntSnd
     C                   Move      *zeros        AntRcd
      *
     c     *loval        setll     BTKAMP
     c                   Read      BTKAMP                                 35
      *
     c     *IN35         doweq     '0'
     C                   Add       1             AntRcd
     C     AntRcd        CabGe     MaxRcd        Utles
      * div filter
     c     FRADATN       Ifne      *zeros
     C     BTDATO        Cablt     FRADATN       NextRec
     c                   end
     c     TILDATN       Ifne      *zeros
     C     BTDATO        Cabgt     TILDATN       NextRec
     c                   end
      * Ok - skriv
     C                   exsr      Setrow
     C                   callp     wrtsection('row')
     C                   Add       1             AntSnd
     C     AntSnd        CabGe     MaxSnd        Utles
      *
     c     NextRec       Tag
     c                   Read      BTKAMP                                 35
     c                   enddo
      *
      *
     C     UTLES         TAG
      * Quit
      * Compute run time
     C                   time                    timedata2
     C     timedata2     subdur    timedata1     ms:*ms
     C                   eval      sec = ms / 1000000
     C                   callp     updhtmlvar('runtime':
     C                             %trim(%editw(sec:'     0 .   ')))
      *
     C                   callp     wrtsection('bot')
N02  C                   callp     wrtsection('botPoweredBy')
N02  C                   callp     wrtsection('botStd')
      *
     C                   exsr      Exit
      *
     C                   eval      *inlr = *on
     C                   return
      *
      *
      *=====================================================================
      * Parse input / cvt to numeric
      *=====================================================================
     C     Parse         begsr
      * Parse variables from QUERY_STRING environment variable
     C                   eval      MAXSN    = zhbgetvar('MaxSn')
     C                   eval      MaxSnd   = c2n2(MAXSN)
     C                   eval      MAXRC    = zhbgetvar('MaxRc')
     C                   eval      MaxRcd   = c2n2(MAXRC)
      * Userid.....
     C                   eval      user = zhbgetvar('user')
     C                   eval      user = uppify(user)
     C                   eval      FRADAT   = zhbgetvar('FRADAT')
     C                   eval      FRADATN  = c2n2(FRADAT)
     C                   eval      TILDAT   = zhbgetvar('TILDAT')
     C                   eval      TILDATN  = c2n2(TILDAT)
     C                   endsr
      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   CLOSE     BRIDF
     C                   CLOSE     BTKAMP
     C                   endsr
      *=====================================================================
      * Read output skeleton html member into memory
      *=====================================================================
     C     LoadHtml      begsr
N02  C                   eval      IfsMultIndicators = gethtmlifsmult(
N02  C                             '/syspeddev/html/eSpedTop.html -
N02  C                             /syspeddev/html/ESBT02.html -
N02  C                             /syspeddev/html/eSpedBot.html':
N02  C                             '/CGITAG/')
     C                   endsr
      *=====================================================================
      *  Set variable data for section /$top
      *=====================================================================
     C     SetTop        begsr
      *
N01  c                   eval      Title='Petter - Bordtennis'
N01  C                   callp     updhtmlvar('TITLE':Title)
     c
     C                   callp     updHTMLvar('LogoImg':LogoImg)
     C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updHTMLvar('USER':USER)
     C                   callp     updHTMLvar('BESK':BIBESK)
     C                   callp     updHtmlVar('KUNDE':
     C                             %trim(%editc(BIKUNR:'Z')))
     C                   callp     updHTMLvar('KNAVN':BIBESK)
      * Søkeparam
     c     FRADATN       ifeq      *zeros
     C                   callp     updHTMLvar('FRADAT':'')
     C                   else
     c                   move      FRADATN       FRADAT
     C                   callp     updHTMLvar('FRADAT':FRADAT)
     C                   endif
     c     TILDATN       ifeq      *zeros
     C                   callp     updHTMLvar('TILDAT':'')
     C                   else
     c                   move      TILDATN       TILDAT
     C                   callp     updHTMLvar('TILDAT':TILDAT)
     C                   endif
      *
     C     XINTERN       Ifeq      'J'
     C                   else
     C                   endif
      *
     C                   endsr
      *=====================================================================
      *  Set variable data for section /$row
      *=====================================================================
     C     Setrow        begsr
      *
     C                   callp     updHTMLvar('BTTURN':BTTURN)
     C                   callp     updHTMLvar('BTDATO':
     C                             %trim(%editw(BTDATO:'    .  .  ')))
     C                   callp     updHTMLvar('BTTID ':
     C                             %trim(%editw(BTTID :'0  :  ')))
     C                   callp     updHTMLvar('BTSP1 ':BTSP1 )
     C                   callp     updHTMLvar('BTSP2 ':BTSP2 )
      * sett 1
     C                   callp     updHTMLvar('BTS1S1':
     C                             %trim(%editc(BTS1S1:'3')))
     C                   callp     updHTMLvar('BTS1S2':
     C                             %trim(%editc(BTS1S2:'3')))
      * sett 2
     C                   callp     updHTMLvar('BTS2S1':
     C                             %trim(%editc(BTS2S1:'3')))
     C                   callp     updHTMLvar('BTS2S2':
     C                             %trim(%editc(BTS2S2:'3')))
      * sett 3
     C                   callp     updHTMLvar('BTS3S1':
     C                             %trim(%editc(BTS3S1:'3')))
     C                   callp     updHTMLvar('BTS3S2':
     C                             %trim(%editc(BTS3S2:'3')))
      * Sluttres.
     C                   callp     updHTMLvar('BTPOS1':
     C                             %trim(%editc(BTPOS1:'3')))
     C                   callp     updHTMLvar('BTPOS2':
     C                             %trim(%editc(BTPOS2:'3')))
      *
     C                   callp     updHTMLvar('BTKOM ':BTKOM )
     C                   endsr
      *
      *
      *
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
     C                   OPEN      BRIDF
     C     USER          CHAIN     BRIDF                              50
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
      *
      * hent Parametre som går igjen i mange prog:
      *      Odd/Even/Rowhead-farger,Logobilde,Språk,Intern/Ekstern bruker,  mm
     c                   call      'ESGETPAR'
     c                   parm                    BIBRID
     c                   parm                    BIFIRM
     c                   parm                    BIKUNR
     c                   parm                    BIKNG
     c                   parm                    RowHead          10
     c                   parm                    Odd              10
     c                   parm                    Even             10
     c                   parm                    LogoImg          50
     c                   parm                    XSPRÅK            2
     c                   parm                    XINTERN           1
     c                   parm                    ParmDS1        1024
     c                   parm                    ParmDS2        1024
     c                   parm                    ParmDS3        1024
      *
     C                   eval      rc = docmd('OVRDBF FILE(BTKAMP ) +
     C                             TOFILE(SYENDRE/BTKAMP )')
     C                   OPEN      BTKAMP
      *
     C                   endsr
      *
