     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPPNLGRP(HLPSCR SYFA52PNL)
     A                                      HLPTITLE('VEDLIKEHOLD AV LASTE/LOSS-
     A                                      ESTEDER')
     A                                      HELP
     A                                      ALTHELP
     A          R SUBFIL                    SFL
     A            WSNR           1A  B  6  5VALUES(' ' '2' '4')
     A            KOTMKO         1   O  6 17TEXT('SIGNATUR')
     A            KOTMNV        20   O  6 39TEXT('NAVN PÅ SIGNATUR')
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0015)
     A                                      SFLPAG(0014)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A            ZRECNO         4S 0O  3 75SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYSPED/SYFA52'
     A                                      COLOR(BLU)
     A            MSG101        50A  O  1 19MSGID(DIV 1582 SYMSGF)
     A                                      DSPATR(HI)
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                  2 70TIME
     A            MSG103        30A  O  3  2MSGID(DIV 0016 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG104        30A  O  4  2MSGID(DIV 0017 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG105        78A  O  5  2MSGID(DIV 1583 SYMSGF)
     A                                      DSPATR(HI)
     A          R BUNN
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF03(03 'EXIT')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSG201        18A  O  3  2MSGID(DIV 0020 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG202        18A  O  3 22MSGID(DIV 0021 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG203        18A  O  3 42MSGID(DIV 0022 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG204        18A  O  3 62MSGID(DIV 0023 SYMSGF)
     A                                      COLOR(BLU)
     A  98        MSG205        78A  O  4  2MSGID(DIV 0012 SYMSGF)
     A                                      COLOR(WHT)
     A N98                              4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A          R VARELI
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSG301        60A  O  1  2MSGID(DIV 1583 SYMSGF)
     A                                      COLOR(WHT)
     A            WSTMKO         1A  B  2 17DSPATR(UL)
     A  40                                  DSPATR(HI)
     A  40                                  DSPATR(PR)
     A  90                                  DSPATR(RI)
     A                                      CHECK(LC)
     A                                      TEXT('LOSSESTEDKODE')
     A            WSTMNV        20A  B  2 39DSPATR(UL)
     A  89                                  DSPATR(RI)
     A                                      CHECK(LC)
     A                                      TEXT('LOSSESTED')
     A            MSG302        18A  O  3  2MSGID(DIV 0024 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  4  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                              4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A          R VAREL2
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            MSG401        12A  O  2  2MSGID(DIV 1216 SYMSGF)
     A            W2TMKO         1A  I  2 16DSPATR(UL)
     A                                      DSPATR(PC)
     A                                      CHECK(LC)
     A            MSG302        18A  O  3  2MSGID(DIV 0024 SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A          R VAREL3
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            MSG401        60A  O  1 15MSGID(DIV 1254 SYMSGF)
     A                                      COLOR(WHT)
     A            KOTMKO         1A  O  2 17DSPATR(PC)
     A                                      COLOR(WHT)
     A            KOTMNV        20A  O  2 39COLOR(WHT)
     A            MSG402        18A  O  3  2MSGID(DIV 0024 SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A          R NYWRK
     A                                      CF03(03 'EXIT')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                  1  2'SYSPED/SYFA52'
     A                                      COLOR(BLU)
     A            MSG501        45A  O  1 19MSGID(DIV 1252 SYMSGF)
     A                                      DSPATR(HI)
     A                                  1 66DATE
     A                                      EDTCDE(Y)
     A                                  2 66TIME
     A            MSG503        30A  O  3  2MSGID(DIV 0016 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG504        30A  O  4  2MSGID(DIV 0017 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG505        78A  O  5  2MSGID(DIV 1253 SYMSGF)
     A                                      DSPATR(HI)
     A            MSG506        18A  O 23  2MSGID(DIV 0020 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG507        18A  O 23 22MSGID(DIV 0021 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG508        18A  O 23 42MSGID(DIV 0022 SYMSGF)
     A                                      COLOR(BLU)
     A            MSG509        18A  O 23 62MSGID(DIV 0023 SYMSGF)
     A                                      COLOR(BLU)
     A                                 24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
