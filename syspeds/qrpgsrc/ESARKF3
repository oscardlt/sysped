      *********************************************************************
      *
      *
CRTPG+*  CRTPGM SYSPED/esarkf3 MODULE(*LIBL/esarkf3 *LIBL/INCGI)
CRTPGM*         ACTGRP(CGI) AUT(*USE)
      *
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FCUNDL01   IF   E           K DISK    USROPN
     FHeadf     IF   E           K DISK    usropn
     Ffak8      IF   E           K DISK    usropn
     FKODTGE    IF   E           K DISK    usropn
     FKODTG2    IF   E           K DISK    usropn
     FKODSAM    IF   E           K DISK    usropn
     FFREETXT2  IF   E           K DISK    usropn
     FFRISOK    IF   E           K DISK    usropn
     FKODFRI    IF   E           K DISK    usropn
     FBRPARF    if   e           k disk    usropn
      *********************************************************************
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      /copy syspeds/qrpgsrcpy,variables3
      *
      * Sammenslåing av F-linjer baserer seg på SAMME varekode/tekst ELLER sammensl.kode/tekst
     D FLI             S             85    DIM(100)
      *
     D OddEven         s             10
      * Client input variables
     D user            s             10
     D UsrSpcName      s             10
     D U_name          S             10
     D FakNr           s              7
     D FakNrN          s              7  0
     D Kunr2           s              8
     D Kunr2N          s              8  0
     D Avd             s              4
     D AvdN            s              4  0
     D Opd             s              7
     D OpdN            s              7  0
     D SeIntChecked    s             10
     D SeIntern        s              1
      *
     D TOTFRI          s             11  2
     D TOTPLI          s             11  2
     D TOTN            s             11  2
     D TOTM            s             11  2
     D TOTB            s             11  2
     D MOMS            s             11  2
     D BelB            s             11  2
     D MKOD            s              1
     D VTXT            s             45
     D DREF            s             35
     D XN01            s              3  0
     D MaxXN           s              3  0
     D StrXN           s              3  0
     D Spaces          s             12a   inz('&nbsp;&nbsp;')
     D ItemCount       s             10i 0
     D i               s             10i 0
     D                 DS
     D  DFS                    1     10    DIM(10)
     D  FSDOKK                 1     10
     D                 DS
     D  XXDATO                 1      8  0
     D  XXDATOD                1      2  0
     D  XXDATOM                3      4  0
     D  XXDATOÅ                5      8  0
     D                 DS
     D  DATON                  1      6  0
     D  DATOND                 1      2  0
     D  DATONM                 3      4  0
     D  DATONÅ                 5      6  0
     D                 DS
     D  VREF                   1     16
     D  HEAVD                  1      4  0
     D  HEAVDa                 1      4
     D  STREK1                 5      5
     D  HEOPD                  6     12  0
     D  HEOPDa                 6     12
     D  STREK2                13     13
     D  HESG                  14     16
     D                 DS
     D  FAVT                   1     20
     D    FAVT1                1      1
     D    FAVT2_20             2     20
     D                 DS
     D  GMDS                   1     85
     D    GM_SORT              1      3
     D    GM_SAMM              5      5
     D    GM_VT                7     47
     D    GM_MK               49     49
     D      GMDS_A             1     49
     D    GM_VAL              51     53
     D    GM_BELV             55     67  2
     D    GM_BELN             69     81  2
     D    GM_VK               83     85
     D                 DS
     D  NYDS                   1     85
     D    NY_SORT              1      3
     D    NY_SAMM              5      5
     D    NY_VT                7     47
     D    FAKDM               49     49
     D      NYDS_A             1     49
     D    FAVAL               51     53
     D    FABELV              55     67  2
     D    FABELN              69     81  2
     D    FAVK                83     85
      *
      /copy syspeds/qrpgsrcpy,prolog3
      *
      * Get program start time for calculating execution time
     C                   time                    timedata1
      * Parse input / cvt to numeric
     C                   exsr      Parse
      * File open / keys
     C                   EXSR      INIT
      * Read output skeleton html member into memory
     C                   exsr      LoadHtml
      * Set section variables
     C                   exsr      Settop
     C                   callp     wrtsection('top')
      *
      * HOVEDRUTINE
      *
     C                   MOVE      *ZEROS        TOTFRI
     C                   MOVE      *ZEROS        TOTPLI
     C                   MOVE      *ZEROS        TOTN
     C                   MOVE      *ZEROS        TOTM
     C                   MOVE      *ZEROS        TOTB
     C     FAKKEY        SETLL     fak8
     C     FAKKEY        READE     fak8                                   33
     C     *in33         DOWEQ     '0'
     c     FASK          cabeq     'I'           NEXTFA
     c     FAKDA         cabeq     'K'           NEXTFA
     c     FAKDA         cabeq     'D'           NEXTFA
     c     FAOPKO        cabeq     'D'           NEXTFA
     c     FAOPKO        cabeq     ' '           NEXTFA
     c     SortSam       ifeq      'J'
     C                   exsr      SortSamSR
     c                   else
     C                   exsr      Setrow
     C                   callp     wrtsection('row')
     c                   end
     C                   MOVE      FASK          XXSK              1            (KEY FRITEKST/FRSOK)
     C     NEXTFA        TAG
     C     FAKKEY        READE     fak8                                   33
     c                   end
      *
      * Hent sortert/sammenslått faktura fra array...
     c     SortSam       ifeq      'J'
     c                   sorta     FLI
     c                   eval      strXn = 100 - MaxXn + 1                      5 lin: elem 96->
     C     strXN         DO        100           XN01
     C     FLI(XN01)     IFNE      *BLANKS
     C                   MOVE      FLI(XN01)     NYDS
     C                   exsr      Setrow
     C                   callp     wrtsection('row')
     C                   END
     C                   ENDDO
     c                   end
      *
     C                   callp     updHTMLvar('TOTN':
     C                             %trim(%editc(TOTN:'J')))
     C                   callp     updHTMLvar('TOTM':
     C                             %trim(%editc(TOTM:'J')))
     C                   callp     updHTMLvar('TOTB':
     C                             %trim(%editc(TOTB:'J')))
     C                   callp     updHTMLvar('TOTPLI':
     C                             %trim(%editc(TOTPLI:'J')))
     C                   callp     updHTMLvar('TOTfri':
     C                             %trim(%editc(TOTfri:'J')))
     C                   callp     wrtsection('tot')
      *
      *
      * Fritekst (S/K/X utfra fakt.linje) eller B = Begge/alle parter
     C                   MOVE      XXSK          FRTKOD
     C     STRFTX        TAG
     C     FREETXT2K     SETLL     FREETXT2
     C     FREETXT2K     READE     FREETXT2                               33
     C                   DOW       *IN33 = *OFF
     C                   callp     updHTMLvar('FRTTXT':FRTTXT)
     C                   callp     wrtsection('TXTROW')
     C     FREETXT2K     READE     FREETXT2                               33
     C  N33              ENDDO
     C     FRTKOD        IFNE      'B'
     C                   MOVE      'B'           FRTKOD
     c                   goto      STRFTX
     C                   end
      *
      *
      * Frie søkeveier
     C     HEAKEY        SETLL     FRISOK
     C     HEAKEY        READE     FRISOK                                 33
     C                   DOW       *IN33 = *OFF                                 1<-B
     C     XXSK          LOOKUP    DFS                                    33
     C  N33'B'           LOOKUP    DFS                                    33
     C     *IN33         CABEQ     '0'           NextFri2
     c     FSKODE        Chain     KODFRI                             33
     C                   callp     updHTMLvar('KFSOTX':KFSOTX)
     C                   callp     updHTMLvar('FSSOK':FSSOK)
     C                   callp     wrtsection('FSOROW')
     C     NextFri2      TAG
     C     HEAKEY        READE     FRISOK                                 33
     C  N33              ENDDO                                                  1<-E
      *
     C     XINTERN       Ifeq      'J'
     C                   callp     wrtsection('SeIntRow')
     c                   end
      * Quit
      * Compute run time
     C                   time                    timedata2
     C     timedata2     subdur    timedata1     ms:*ms
     C                   eval      sec = ms / 1000000
     C                   callp     updhtmlvar('runtime':
     C                             %trim(%editw(sec:'     0 .   ')))
      *
     C                   callp     wrtsection('bot')
      *
     C                   exsr      Exit
      *
     C                   eval      *inlr = *on
     C                   return
      *
      *
      *=====================================================================
      * Parse input / cvt to numeric
      *=====================================================================
     C     Parse         begsr
      * Parse variables from QUERY_STRING environment variable
     C                   eval      FAKNR    = zhbgetvar('FAKNR')
     C                   eval      FAKNRN   = c2n2(FAKNR)
     C                   eval      KUNR2    = zhbgetvar('KUNR2')
     C                   eval      KUNR2N   = c2n2(KUNR2)
     C                   eval      AVD      = zhbgetvar('AVD')
     C                   eval      AVDN     = c2n2(AVD)
     C                   eval      OPD      = zhbgetvar('OPD')
     C                   eval      OPDN     = c2n2(OPD)
      * Userid.....
     C                   eval      user = zhbgetvar('user')
     C                   EVAL      UsrSpcName  = zhbgetvar('UsrSpcName')
      * Eksternt pålogget via access list..- Overstyrer parameter USERID
     C                   eval      U_Name   = getenv('REMOTE_USER':
     C                             qusec)
     C     U_Name        IFNE      *BLANKS
     C                   eval      USER   = uppify(U_Name)
     C                   END
      * Sorter/sammenslå er defauly men intern bruker KAN velge "SeIntern" - får da alle linjer
      *    ved KLIKK på checkboks kommer verdi Y/blank,
      *    ellesr kommer verdi fra siste valg via "SeIntern" som checked / blank(N)
     C     SeIntChecked  Ifeq      'checked'
     C                   eval      SeIntern = 'Y'
     C                   else
     C                   eval      SeIntern = zhbgetvar('SeIntern')
     C                   end
     c     SeIntern      ifne      'Y'
     c                   move      'J'           SortSam           1
     c                   end
     C                   endsr
      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
      *
     C                   CLOSE     BRIDF
     C                   CLOSE     BRPARF
     C                   CLOSE     cundl01
     C                   CLOSE     HEADF
     C                   CLOSE     fak8
     C                   CLOSE     KODTGE
     C                   CLOSE     KODTG2
     C                   CLOSE     KODSAM
     C                   CLOSE     FREETXT2
     C                   CLOSE     FRISOK
     C                   CLOSE     KODFRI
     C                   endsr
      *=====================================================================
      * Read output skeleton html member into memory
      *=====================================================================
     C     LoadHtml      begsr
     C                   callp     gethtml('HTMLSRC':
     C                             '*LIBL':
     C*                            'ESARKF3':'/CGITAG/')
     C                             'ESARKF3'+XSPRÅK:
     C                             '/CGITAG/')
     C                   endsr
      *=====================================================================
      *  Set variable data for section /$top
      *=====================================================================
     C     SetTop        begsr
     C                   callp     updHTMLvar('ROWHEAD':RowHead)
     C                   callp     updHTMLvar('LogoImg':LogoImg)
      *
      * hvis *I*=Intern bruker - finn kunde via egen param (etter 20102007 parm XINTERN)
     c                   Move      BIBESK        TSTINTERN         3
     C     TSTINTERN     Ifeq      '*I*'
     C                   move      'J'           XINTERN
     C                   end
     C     XINTERN       Ifeq      'J'
     C     KUNR2n        Andne     *zeros
     c                   move      KUNR2N        BIKUNR
     C     KunKey        CHAIN     Cundl01                            33
     c                   end
      *
     C     FAKKEY        SETLL     fak8
     C     FAKKEY        READE     fak8                                   33
     C     *in33         DOWEQ     '0'
     C     FASK          cabeq     'I'           NEXTFA8
     C     FAKDA         cabeq     'K'           NEXTFA8
     C     FAKDA         cabeq     'D'           NEXTFA8
     C     FAOPKO        cabeq     'D'           NEXTFA8
     C     FAOPKO        cabeq     ' '           NEXTFA8
     C                   move      FAKUNR        BIKUNR
     C     KunKey        CHAIN     Cundl01                            33
     C                   LEAVE
     C     NEXTFA8       TAG
     C     FAKKEY        READE     fak8                                   33
     C                   ENDDO
      *
     C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updHTMLvar('USER':USER)
     C                   callp     updHtmlVar('UsrSpcName':UsrSpcName)
     C                   callp     updHTMLvar('BESK':BIBESK)
     C                   callp     updHtmlVar('KUNDE':
     C                             %trim(%editc(BIKUNR:'Z')))
     C                   callp     updHTMLvar('KNAVN':KNAVN)
     C                   callp     updHtmlVar('WSNA':KNAVN)
     C                   callp     updHTMLvar('FAKNR':FAKNR)
     C                   callp     updHTMLvar('KUNR2':KUNR2)
     C                   callp     updHTMLvar('FAKDT':
     C                             %trim(%editw(FADATO:'    .  .  ')))
     C                   callp     updHTMLvar('FAKDTFF':
     C                             %trim(%editw(FADAFF:'    .  .  ')))
     C                   EVAL      DREF = HERFA
     C                   callp     updHTMLvar('DREF':DREF)
     C                   callp     updHTMLvar('VREF':VREF)
     C                   callp     updHTMLvar('TUR':
     C                             %trim(%editc(HEPRO:'Z')))
     C                   callp     updHTMLvar('DATO':
     C                             %trim(%editw(HEDTOP:'    .  .  ')))
     C                   callp     updhtmlvar('HENAS':HENAS)
     C                   callp     updhtmlvar('HEADS1':HEADS1)
     C                   callp     updhtmlvar('HEADS2':HEADS2)
     C                   callp     updhtmlvar('HEADS3':HEADS3)
     C                   callp     updhtmlvar('HENAK':HENAK)
     C                   callp     updhtmlvar('HEADK1':HEADK1)
     C                   callp     updhtmlvar('HEADK2':HEADK2)
     C                   callp     updhtmlvar('HEADK3':HEADK3)
     C                   callp     updHTMLvar('KLL':
     C                             %trim(%editc(HENT:'Z')))
     C                   callp     updhtmlvar('VBESK':HEVS1)
     C                   callp     updHTMLvar('VKT':
     C                             %trim(%editc(HEVKT:'Z')))
     C                   callp     updHTMLvar('M3':
     C                             %trim(%editc(HEM3:'3')))
     C                   callp     updHTMLvar('LM':
     C                             %trim(%editc(HELM:'3')))
     C                   callp     updHTMLvar('FVKT':
     C                             %trim(%editc(HEFBV:'Z')))
     C                   callp     updHTMLvar('AVD':
     C                             %trim(%editc(HEAVD:'Z')))
     C                   callp     updHTMLvar('OPD':
     C                             %trim(%editc(HEOPD:'Z')))
     C     SeIntern      Ifeq      'Y'
     C                   eval      SeIntChecked = 'checked'
     C                   end
     C                   callp     updHTMLvar('SeIntChecked':SeIntChecked)
      *
     C                   endsr
      *=====================================================================
      *  Set variable data for section /$row   - NÅR INTERN/IKKE SAMMESLÅTT
      *=====================================================================
     C     Setrow        begsr
     c     OddEven       IFEQ      ODD
     c                   eval      OddEven = EVEN
     c                   else
     c                   eval      OddEven = ODD
     c                   end
     C                   callp     updHTMLvar('ODDEVEN':OddEven)
      * Ved kundeview-sortert/sammenslått så vises avvikende tekst.
     c     SortSam       ifeq      'J'
     C                   eval      VTXT = NY_VT
     c                   else
     C     KGEKey        CHAIN     KODTGE                             33
     c     XSPRÅK        ifeq      'EN'
     C   33              eval      KGEENT = '*Unknown'
     C                   eval      VTXT = KGEENT + FAVT
     c                   else
     C   33              eval      KGENOT = '*Ukjent'
     C                   eval      VTXT = KGENOT + FAVT
     c                   end
     c                   end
     C     XINTERN       IFNE      'J'
     C                   MOVE      *BLANKS       FAVK
     C                   else
     c     SortSam       ifne      'J'
     c     FACD11        andne     ' '
     C                   eval      VTXT = %trim(VTXT)+'  ('+FACD11+')'
     C                   end
     C                   END
     C                   callp     updhtmlvar('GEB':FAVK)
     C                   callp     updhtmlvar('VTXT':VTXT)
     C     FAKDM         IFEQ      'J'
     C                   ADD       FABELN        TOTPLI
     C     FABELN        MULT      0,25          MOMS
     c                   MOVE      '*'           MKOD
     c                   ELSE
     C                   ADD       FABELN        TOTFRI
     C                   MOVE      *ZEROS        MOMS
     c                   MOVE      ' '           MKOD
     c                   end
     C     FABELN        ADD       MOMS          BELB
     C                   callp     updHTMLvar('BELN':
     C                             %trim(%editc(FABELN:'J')))
     C                   callp     updHTMLvar('BELM':
     C                             %trim(%editc(MOMS:'J')))
     C                   callp     updHTMLvar('BELB':
     C                             %trim(%editc(BELB:'J')))
     C                   callp     updhtmlvar('MKOD':MKOD)
      * akkumuler totaler
     C                   ADD       FABELN        TOTN
     C                   ADD       FABELN        TOTB
     C                   ADD       MOMS          TOTM
     C                   ADD       MOMS          TOTB
      *
     C                   endsr
      *
      ***********************************************************
     C     SortSamSR     begsr
      ***********************************************************
      *
      * sort/sammenslå
      * kriterium for sammenslåing  - IKKE FRITEKST
      *            A)  SAMME GEBYRKODE       / FRITEKST  / momskode,
      *  ELLER     B)  SAMME SAMMENSNL.KODE  / FRITEKST  / momskode,
      * SAMMENSLÅTTE LINJER KOMMER FØRST
      * DERETTER LINJER  MED SAM-KODE MEN SOM IKKES SLÅS SAMMEN PGA FRITEKST
      * DERETTER ANDRE LINJER BASERT PÅ SORT-KODE
      *
      * a) klargjør noen variable
      * varetekst
     C     KG2Key        CHAIN     KODTG2                             33
     C     KGEKey        CHAIN     KODTGE                             33
     c     XSPRÅK        ifeq      'EN'
     C                   eval      NY_VT = KGEENT+' '+FAVT
     c                   else
     C                   eval      NY_VT = KGENOT+' '+FAVT
     c                   end
     c     FAVT1         ifeq      'X'
     C                   eval      NY_VT =  FAVT2_20
     c                   end
      * Sort-nr/samkod(+evt tekst derfra.)
     C                   MOVE      KG2SRT        NY_SORT
     C                   MOVE      FACD11        NY_SAMM
     C     NY_SAMM       IFNE      *BLANKS
     C     FAVT          IFNE      *BLANKS
     C                   MOVE      '  1'         NY_SORT
     C                   MOVE      *BLANKS       NY_SAMM
     C                   else
     C                   MOVE      '  2'         NY_SORT
     C     NY_SAMM       CHAIN     KODSAM                             33
     c  N33XSPRÅK        ifeq      'EN'
     C                   eval      NY_VT = SAENGE
     c                   else
     C                   eval      NY_VT = SANORS
     c                   end
     C                   END
     C                   END
      *
      * b) slå sammen eller legg i første ledige
     C     1             DO        100           XN01
     C     FLI(XN01)     IFNE      *BLANKS
     C                   MOVE      FLI(XN01)     GMDS
     C     NYDS_A        IFEQ      GMDS_A
     C                   ADD       FABELV        GM_BELV
     C                   ADD       FABELN        GM_BELN
     C                   move      *blanks       GM_VK                          sammenslått
     C                   MOVE      GMDS          FLI(XN01)
     C                   LEAVE
     C                   END
     C                   ELSE
     C                   MOVE      NYDS          FLI(XN01)
     c                   z-add     XN01          MaxXN
     C                   LEAVE
     C                   END
     C                   ENDDO
      *
     C                   endsr
      *
      *
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
     C                   OPEN      BRIDF
     C     USER          CHAIN     BRIDF                              50
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
      *
      * hent Parametre som går igjen i mange prog:
      *      Odd/Even/Rowhead-farger,Logobilde,Språk,Intern/Ekstern bruker,  mm
     c                   call      'ESGETPAR'
     c                   parm                    BIBRID
     c                   parm                    BIFIRM
     c                   parm                    BIKUNR
     c                   parm                    BIKNG
     c                   parm                    RowHead          10
     c                   parm                    Odd              10
     c                   parm                    Even             10
     c                   parm                    LogoImg          50
     c                   parm                    XSPRÅK            2
     c                   parm                    XINTERN           1
     c                   parm                    ParmDS1        1024
     c                   parm                    ParmDS2        1024
     c                   parm                    ParmDS3        1024
      *
     C                   OPEN      Cundl01
     C                   OPEN      HEADF
     C                   OPEN      fak8
     C                   OPEN      KODTGE
     C                   OPEN      KODTG2
     C                   OPEN      KODSAM
     C                   OPEN      FREETXT2
     C                   OPEN      FRISOK
     C                   OPEN      KODFRI
     C                   OPEN      BRPARF
      *
     C     KGEKey        KLIST
     C                   KFLD                    KGEUNI
     C                   KFLD                    FAVK
     C                   MOVE      'GE'          KGEUNI
     C     KG2Key        KLIST
     C                   KFLD                    KG2UNI
     C                   KFLD                    FAVK
     C                   MOVE      'G2'          KG2UNI
     C     KunKey        KLIST
     C                   KFLD                    BIFIRM
     C                   KFLD                    BIKUNR
     C     HeaKey        KLIST
     C                   KFLD                    AVDN
     C                   KFLD                    OPDN
     C     FakKey        KLIST
     C                   KFLD                    AVDN
     C                   KFLD                    OPDN
     C                   KFLD                    FAKNRN
     C     FREETXT2K     KLIST
     C                   KFLD                    AVDN
     C                   KFLD                    OPDN
     C                   KFLD                    FRTKOD
     C                   KFLD                    FAKNRN
     C     BrParKey      klist
     C                   kfld                    PABRID
     C                   kfld                    PAKUNR
     C                   kfld                    PAID
     C                   EVAL      PABRID = BIBRID
     C                   MOVEL     'SPRÅK'       PAID
     C     BrParKey      chain     BRPARF                             33
     C                   IF        (*IN33 OR (PAVRDA <> 'EN'))
     C                   MOVE      *BLANKS       XSPRÅK            2
     C                   ELSE
     C                   MOVE      'EN'          XSPRÅK            2
     C                   END
      *
     C     KunKey        CHAIN     Cundl01                            33
bhr   * Spesialvri for å kunne deme uten å avsløre kundenavn
bhr  c                   MOVEL     USER          tstjov            4
bhr  c     tstjov        ifeq      'JOVO'
bhr  c                   eval      KNAVN  = 'Jan Ottar Valderhaug'
bhr  c                   end
      *
      *
     C                   EVAL      STREK1  = '/'
     C                   EVAL      STREK2  = '/'
     C     HeaKey        CHAIN     HEADF                              33
     C   33              CLEAR                   HEADFR
      * finn bilagsdato mm.
     C     FakKey        CHAIN     fak8                               33
      *
     C                   endsr
      *
