     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HELP
     A                                      ALTHELP
     A          R NCTS012DE1
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF04(04 'Søking')
     A                                      CA12(12 'Avbryt uten utskrift')
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1 26'Info. om ikke ankommet NCTS  '
     A                                      COLOR(WHT)
     A                                  1 73'NCTS012RE'
     A                                      COLOR(BLU)
     A                                  3  3'Avdeling/oppdragsnr. . . :'
     A            UAVD           4Y 0O  3 32EDTCDE(4)
     A                                  3 37'/'
     A            UTDN           7Y 0O  3 39EDTCDE(4)
     A                                  4  3'Selgers navn . . . . . . :'
     A            THNAS         30A  O  4 32TEXT('Navn Selger')
     A                                  5  3'Kjøpers navn . . . . . . :'
     A            THNAK         30A  O  5 32TEXT('Navn Kjøper')
     A                                  6  3'Tollsted for etterlysning:'
     A            TNTSAV         8A  O  6 32
     A                                  7  3'Dato for etterlysning. . :'
     A            WSDTR          6Y 0O  7 32EDTCDE(Z)
     A                                  8  3'Meldings funksjon  . . . :'
     A            W1N07          3A  O  8 32
     A                                  8 48'141=Info. ikke ankommet send.'
     A                                 10  3'Ventegrupe . . . . . . . :'
     A            WVEN      R        B 10 32REFFLD(MVEN *LIBL/EDIM)
     A                                      TEXT('Ventekode')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 10 48'Blank=Sende direkte '
     A                                 11  3'Testekode  . . . . . . . :'
     A            W0035     R        O 11 32REFFLD(M0035 *LIBL/EDIM)
     A                                 13  2'Mottaker:'
     A            WSKN           8Y 0B 13 17TEXT('Kundenr mottaker')
     A                                      DSPATR(UL)
     A  99                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            WSTIN         17A  B 13 27TEXT('ID mottaker')
     A                                      DSPATR(UL)
     A  98                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 14  2'Navn og adr.:'
     A            WSNA          30A  B 14 17TEXT('Navn mottaker')
     A                                      DSPATR(UL)
     A  97                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSAD1         30A  B 14 48TEXT('Adr1 mottaker')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                 15  2'P.nr/sted/lk'
     A            WSPN          17A  B 15 17TEXT('Postnr mottaker')
     A                                      DSPATR(UL)
     A  96                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSPS          24A  B 15 35TEXT('Poststed mottaker')
     A                                      DSPATR(UL)
     A  95                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A            WSLK           2A  B 15 60TEXT('Landkode mottaker')
     A                                      DSPATR(UL)
     A  94                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 17  3'Leveringsdato  . . . . . :'
     A            WSDTT          6Y 0B 17 32DSPATR(UL)
     A  93                                  DSPATR(RI)
     A                                      EDTCDE(Z)
     A                                      COLOR(YLW)
     A                                 18  3'Ankomst tollsted . . . . :'
     A            WSTSAN         8A  B 18 32DSPATR(UL)
     A  92                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                 19  3'Fritekst . . . . . . . . :'
     A            WSTXT        256A  B 19 32DSPATR(UL)
     A  91                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      CHECK(LC)
     A                                 23  2'F12=Avbryt'
     A                                      COLOR(BLU)
     A                                 23 47'F4=Søk'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
