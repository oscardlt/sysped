      *
      ***********************************************************
      *** PROGRAM FOR INTERVALLSTYRING AV POSTNUMMER          ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01 02 04 07 09-11 13-24          ***
      *** LEDIGE INDIKATORER 27-29 31 32 34-39 41-88 95-97    ***
      ***********************************************************
     FKODTA     IF   E           K DISK
     FKUFAST    IF   E           K DISK
     FPRODT     UF A E           K DISK
     FPRODU     UF A E           K DISK
     FSYPO04FM  CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     C*
     C                   EXSR      INIT
     C*
     C     START0        TAG
     C  N30
     CANN04              Z-ADD     9             LINNBR
     C  N30
     CANN04              Z-ADD     28            POSNBR
     C                   EXFMT     BILDE01
     C                   SETOFF                                       30
     C                   MOVEA     '00'          *IN(98)
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
      *
     C   03              GOTO      SLUTT
     C   04              DO
     C                   EXSR      SØKDTA
     C                   GOTO      START0
     C                   END
      *
     C                   MOVEL     PTPROD        KFKOD
     C     KUFASK        CHAIN     KUFAST                             99
     C     *IN99         IFEQ      '1'
     C                   Z-ADD     9             LINNBR
     C                   Z-ADD     28            POSNBR
     C                   MOVE      '1'           *IN30
     C                   MOVE      'YBC0360'     MSGNR
     C                   GOTO      START0
     C                   END
     C     PTAVDE        IFNE      *ZEROS
     C     KODAVD        CHAIN     KODTA                              98
     C     *IN98         IFEQ      '1'
     C                   Z-ADD     11            LINNBR
     C                   Z-ADD     28            POSNBR
     C                   MOVE      '1'           *IN30
     C                   MOVE      'SPA0001'     MSGNR
     C                   GOTO      START0
     C                   END
     C                   END
     C*
     C                   MOVE      PTPROD        PRPROD
     C                   MOVE      PTAVDE        PRAVDE
     C     PRODUK        CHAIN(N)  PRODU                              33
     C     *IN33         IFEQ      '0'
     C                   MOVE      PROMRM        WSOMRM
     C                   MOVE      PROMRL        WSOMRL
     C                   MOVE      PRMIMA        WSMIMA
     C                   MOVE      PRGREN        WSGREN
     C                   ELSE
     C                   MOVE      *ZEROS        WSOMRM
     C                   MOVE      *ZEROS        WSOMRL
     C                   MOVE      *ZEROS        WSMIMA
     C                   MOVE      *ZEROS        WSGREN
     C                   END
      *
     C                   EXFMT     BILDE1B
     C   12              GOTO      START0
      *
     C     PRODUK        CHAIN     PRODU                              33
     C                   MOVE      WSOMRM        PROMRM
     C                   MOVE      WSOMRL        PROMRL
     C                   MOVE      WSMIMA        PRMIMA
     C                   MOVE      WSGREN        PRGREN
     C   33              WRITE     PRODUR
     C  N33              UPDATE    PRODUR
      *
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
     C                   MOVE      W2VEKT        YTVEKT            9 0
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     PRIKU         SETLL     PRODT
     C     START1        TAG
     C     PRIKU         READE(N)  PRODT                                  94
     C   94              MOVE      *ZEROS        PTVEKT
     C   94              MOVE      *BLANKS       WSNR
      *
     C     START2        TAG
     C                   MOVE      *BLANKS       WSDLT
      *
     C  N94              DO
     C                   DO        16
     C                   ADD       1             ZRECNO
     C                   WRITE     SUBFIL
     C     PRIKU         READE(N)  PRODT                                  94
     C  N94              MOVE      PTVEKT        YTVEKT
     C  N94              END
     C                   END
     C*
     C                   Z-ADD     ZRECNO        YRECNO            4 0
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYVARE
     C                   ELSE
     C                   WRITE     SUBCTL
     C                   EXFMT     BUNN
     C                   END
     C                   SETOFF                                       9830
     C*
     C*** AVSLUTT
     C   03              GOTO      SLUTT
     C*** FRISK OPP
     C   05              GOTO      START
     C*** LAGE NY
     C   06              GOTO      START5
     C*** SØK FRA
     C   08              GOTO      START6
     C*
     C   12              GOTO      START0
     C*
     C*** BLA VIDERE
     C   25              DO
     C                   Z-ADD     YRECNO        ZRECNO
     C                   SETOFF                                       25
     C  N94PRISK3        SETLL     PRODT
     C  N94PRIKU         READE(N)  PRODT                                  94
     C                   GOTO      START2
     C                   END
     C*
     C     ZRECNO        IFEQ      0
     C                   GOTO      START3
     C                   ELSE
     C                   EXSR      HENT
     C   98              GOTO      START3
     C                   END
     C*
     C*** SLETT VARE
     C     WSNR          IFEQ      '4'
     C                   MOVE      ' '           WSNR
     C                   EXFMT     VAREL3
     C  N12              DO
     C     PRISK2        CHAIN     PRODT                              33
     C  N33              DELETE    PRODTR
     C                   EXSR      MOVB1
     C                   Z-ADD     0             PTVEKT
     C                   END
     C                   UPDATE    SUBFIL
     C                   GOTO      START3
     C                   END
     C*
     C*** LAGE/ENDRE
     C     START5        TAG
     C                   EXFMT     VARELI
     C                   SETOFF                                       9089
     C                   SETOFF                                       30
     C   12              DO
     C   40              UPDATE    SUBFIL
     C                   SETOFF                                       40
     C                   EXSR      MOVB2
     C                   GOTO      START3
     C                   END
     C                   EXSR      TESTB1
     C   30              GOTO      START5
     C     NY            IFEQ      'J'
     C                   GOTO      START5
     C                   END
     C                   GOTO      START3
     C*
     C*** SØK FRA
     C     START6        TAG
     C                   EXFMT     VAREL2
     C   03              GOTO      SLUTT
     C                   GOTO      START
     C*
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
     C*
     C***********************************************
     C     TESTB1        BEGSR
     C***********************************************
     C  N40              DO
     C     WSVEKT        COMP      0                                    9090
     C   90              MOVE      'SPV0002'     MSGNR
     C   90              SETON                                        30
     C   30              GOTO      SLTB1
     C                   END
     C*
     C     PRISK         CHAIN     PRODT                              33
     C  N33
     CANN40              GOTO      SLTB1
     C                   EXSR      MOVWPR
     C  N33              UPDATE    PRODTR
     C   33              WRITE     PRODTR
     C   40              UPDATE    SUBFIL
     C  N40              ADD       1             YRECNO
     C  N40              Z-ADD     YRECNO        ZRECNO
     C  N40              WRITE     SUBFIL
     C                   WRITE     SUBCTL
     C   40              MOVE      'N'           NY                1
     C  N40              MOVE      'J'           NY
     C                   SETOFF                                       40
     C                   EXSR      MOVB2
     C*
     C     SLTB1         ENDSR
     C*
     C***********************************************
     C     HENT          BEGSR
     C***********************************************
     C                   MOVEA     '00'          *IN(91)
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
     C  N98WSNR          IFEQ      '2'
     C     WSDLT         COMP      '*DLT'                                 98
     C   98              GOTO      SLHENT
     C                   EXSR      MOVPRW
     C                   MOVE      ' '           WSNR
     C                   SETOFF                                       9089
     C                   SETON                                        40
     C                   GOTO      SLHENT
     C                   END
     C  N98WSNR          IFEQ      '4'
     C     WSDLT         COMP      '*DLT'                                 98
     C                   GOTO      SLHENT
     C                   END
     C                   END
     C*
     C     SLHENT        TAG
     C   98              DO
     C                   MOVE      'DIV0012'     MSGNR
     C                   SETON                                            30
     C                   END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVWPR        BEGSR
     C***********************************************
     C                   MOVE      WSVEKT        PTVEKT
     C                   MOVE      WSPRPR        PTPRPR
     C                   MOVE      WSADJU        PTADJU
     C                   MOVE      *BLANKS       WSDLT
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVPRW        BEGSR
     C***********************************************
     C                   MOVE      PTVEKT        WSVEKT
     C                   MOVE      PTPRPR        WSPRPR
     C                   MOVE      PTADJU        WSADJU
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     MOVB1         BEGSR
     C***********************************************
     C                   MOVE      *ZEROS        PTVEKT
     C                   MOVE      *ZEROS        PTPRPR
     C                   MOVE      *ZEROS        PTADJU
     C                   MOVE      '*DLT'        WSDLT
     C                   ENDSR
     C*
     C***********************************************
     C     SØKDTA        BEGSR
     C***********************************************
     C     LINNBR        IFEQ      9
     C     POSNBR        ANDEQ     28
     C                   MOVEL     PTPROD        KFKOD
     C                   MOVE      'N'           UUPDAT            1
     C                   CALL      'SYKUKD'
     C                   PARM                    KFTYP
     C                   PARM                    UUPDAT
     C                   PARM                    KFKOD
     C                   MOVEL     KFKOD         PTPROD
     C                   Z-ADD     11            LINNBR
     C                   GOTO      SLSØK
     C                   END
      *
     C     LINNBR        IFEQ      11
     C     POSNBR        ANDGE     28
     C     POSNBR        ANDLE     31
     C                   CALL      'SYFA14B'
     C                   PARM                    PTAVDE
     C                   Z-ADD     9             LINNBR
     C                   Z-ADD     28            POSNBR
     C                   GOTO      SLSØK
     C                   END
     C*
     C                   MOVE      'DIV1037'     MSGNR
     C                   SETON                                        30
     C*
     C     SLSØK         ENDSR
     C*
     C***********************************************
     C     MOVB2         BEGSR
     C***********************************************
     C                   MOVE      *ZEROS        WSVEKT
     C                   MOVE      *ZEROS        WSPRPR
     C                   MOVE      *ZEROS        WSADJU
     C                   ENDSR
     C*
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     KUFASK        KLIST
     C                   KFLD                    KFTYP
     C                   KFLD                    KFKOD
     C                   MOVEL     'PRODTYPE'    KFTYP
     C     KODAVD        KLIST
     C                   KFLD                    AVUNIK
     C                   KFLD                    PTAVDE
     C     PRIKU         KLIST
     C                   KFLD                    PTPROD
     C                   KFLD                    PTAVDE
     C     PRISK         KLIST
     C                   KFLD                    PTPROD
     C                   KFLD                    PTAVDE
     C                   KFLD                    WSVEKT
     C     PRISK1        KLIST
     C                   KFLD                    PTPROD
     C                   KFLD                    PTAVDE
     C                   KFLD                    W2VEKT
     C     PRISK2        KLIST
     C                   KFLD                    PTPROD
     C                   KFLD                    PTAVDE
     C                   KFLD                    PTVEKT
     C     PRISK3        KLIST
     C                   KFLD                    PTPROD
     C                   KFLD                    PTAVDE
     C                   KFLD                    YTVEKT
     C     PRODUK        KLIST
     C                   KFLD                    PTPROD
     C                   KFLD                    PTAVDE
     C*
     C*
     C                   MOVE      'A'           AVUNIK            1
     C*
     C                   ENDSR
     C*
