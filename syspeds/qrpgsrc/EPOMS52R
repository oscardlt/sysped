      * UNA:+.? '
      * UNB+UNOC:2+7080003141941:14+7080001240011:14+120305:1400+E3'
      * UNH+1+ORDRSP:D:96A:UN:EAN005'
      * BGM+231+1000248+4+NA'
      * DTM+137:20120305:102'                            G0
      * RFF+ON:1000267'                                  G1
      * RFF+Z1:01'                                       G1
      * NAD+BY+300T01::9'                                G3
      * CTA+OC+:Kjell Arne Reinsbø Lawson'               G6
      * NAD+SU+61904::9'                                 G3
      * TOD+10E++FOB'                                    G12
      * LIN+1+5+7051620005244:EN'                        G26
      * PIA+1+4000052-PK-0001:SA::91'
      * IMD+F++:::Diedre Square Jacket'
      * QTY+21:800:PCE'
      * QTY+113:800:PCE'
      * DTM+110:20120508:102'
      * RFF+ON:1000267:10'                               G31
      * LIN+2+3+7051620005237:EN'
      * PIA+1+4000052800S/M_:SA::91'
      * IMD+F++:::Diedre Square Jacket'
      * QTY+21:100:PCE'
      * QTY+113:60:PCE'
      * DTM+110:20120508:102'
      * QVR+10:21++:::Delayed'
      * RFF+ON:1000267:20'
      * LIN+3+3+7051620005237:EN'
      * PIA+1+4000052800S/M_:SA::91'
      * IMD+F++:::Diedre Square Jacket'
      * QTY+21:100:PCE'
      * QTY+113:40:PCE'
      * DTM+110:20120616:102'
      * RFF+ON:1000267:20:1'
      * UNS+S'                                   G50
      * CNT+2:3'                                 G0
      * UNT+34+1'
      * UNZ+1+E3'
      *
     FEPOH03    UF   E           K DISK
     FEPOL      UF   E           K DISK
     FEDIC      UF   E             DISK
     FEDIM      IF A E             DISK
     FEUNH      O  A E             DISK
     FECNT96A   O  A E             DISK
     FEDTM96A   O  A E             DISK
     FERFF96A   O  A E             DISK
     FENAD96A   O  A E             DISK
     FECTA96A   O  A E             DISK
     FETOD96A   O  A E             DISK
     FELIN96A   O  A E             DISK
     FEPIA96A   O  A E             DISK
     FEIMD96A   O  A E             DISK
     FEQTY96A   O  A E             DISK
     FEQVR96A   O  A E             DISK
     FEUNS      O  A E             DISK
      *____________________________________
      * DS for leftjustify numericel fields
      *""""""""""""""""""""""""""""""""""""
     D                 DS
     D  ØN152                  1     15S 2
     D  ØA152H                 1     13
     D  ØA152X                 5     13
     D  ØA152D                14     15
     D                 DS
     D  ØA12                   1     12
     D  Ø12                    1     12
     D                                     DIM(12)
      *//////////////
      *  Hodelogikk /
      *//////////////
     C                   EXSR      SRA
     C                   EXSR      SRB
     C                   EVAL      *INLR=*ON
      *////////////////////////////////////////////////////////////
      *  Initiering                                           SRA /
      *////////////////////////////////////////////////////////////
     C     SRA           BEGSR
      * HENTE DAGENS DATO OG TID
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
      * INIT AV ARBEIDSFELT
     C                   Z-ADD     0             ØX                2 0
     C                   Z-ADD     0             WC6066            7 0
     C                   Z-ADD     0             WDIFF             9 0
     C                   MOVE      *BLANKS       ØAN              12
     C     *LIKE         DEFINE    CMN           WMN
     C     *LIKE         DEFINE    EHSTA2        WEHSTA2
     C     *LIKE         DEFINE    EHKUND        WEHKUND
     C                   EVAL      WEHSTA2='S'
     C                   EVAL      WEHKUND=21647
      * DEF AV KEYS
     C     EPOHKEY       KLIST
     C                   KFLD                    WEHSTA2
     C                   KFLD                    WEHKUND
     C     EPOLKEY       KLIST
     C                   KFLD                    EHKUND
     C                   KFLD                    EHBES
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Les linjer                                           SRB /
      *////////////////////////////////////////////////////////////
     C     SRB           BEGSR
     C     EPOHKEY       SETLL     EPOH03
     C     EPOHKEY       READE     EPOH03                                 51
     C                   DOW       *IN51=*OFF
     C*                  IF        EHSTA<='2'
     C*                  IF        EHSTA<='4'
     C                   IF        EHSTA>='2'
      * stauser fra 2 og opp sendes ikke som ord-rsp
      * (4=shipped sendes via ASN-direktekal - turavsl-prog / uavhengig av EHSTA )
     C                   EVAL      EHSTA2=' '
     C                   EVAL      EHREAS=*blanks                               ='sendt'
     C                   UPDATE    EPOHR
     c                   ELSE
      * Kun 1 = Contacted(01) og 15=Ready for stuffing (02) skal sendes
     C                   EXSR      SRHOVED
     C                   EVAL      EHSTA2=' '
     C                   UPDATE    EPOHR

     C                   EVAL      *IN52=*OFF
     C     EPOLKEY       SETLL     EPOL
     C     EPOLKEY       READE     EPOL                                   52
     C                   DOW       *IN52=*OFF
     c                   if        ELOPD <> -9999999
     C                   EXSR      SRLINJE
     c                   endif
     C                   EVAL      ELSTA2=' '
     C                   EVAL      ELREAS=*blanks                               ='sendt'
     C                   UPDATE    EPOLR
     C     EPOLKEY       READE     EPOL                                   52
     C                   ENDDO

     C                   EXSR      SRSLUTT

     C                   ENDIF
     C     EPOHKEY       READE     EPOH03                                 51
     C                   ENDDO
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Hoved                                            SRHOVED /
      *////////////////////////////////////////////////////////////
     C     SRHOVED       BEGSR
      * Hent recordnummer for melding
     C     1             CHAIN     EDIC                               51
     C     *IN51         CABEQ     *ON           SRA9
     C                   ADD       1             CMN
     C                   UPDATE    EDICR
     C                   Z-ADD     CMN           WMN
      * Nullstill noen verdier
     C                   EVAL      WC6066=0
      * DTM
     C                   CLEAR                   DTMR
     C                   EVAL      DMN=WMN
     C                   EVAL      DGN=0
     C                   EVAL      DREP0=1
     C                   EVAL      D2005='137'
     C                   EVAL      D2380=WDT
     C                   EVAL      D2379='102'
     C                   WRITE     DTMR
      * RFF
     C                   CLEAR                   RFFR
     C                   EVAL      RMN=WMN
     C                   EVAL      RGN=1
     C                   EVAL      RREP0=1
     C                   EVAL      R1153='ON'
     C                   EVAL      R1154=EHBES
     C                   WRITE     RFFR
      * RFF
     C                   CLEAR                   RFFR
     C                   EVAL      RMN=WMN
     C                   EVAL      RGN=1
     C                   EVAL      RREP0=1
     C                   EVAL      R1153='Z1'
      * fra ORDRSP vil Voice kun ha 00 pending(lite sansynlig) 01=Contacted og 02=Ready for stuffing
      * høyere status får de via ASN/DESADV og det regner de som sin status 03=Shipped
     C                   SELECT
     C     EHSTA         WHENEQ    ' '
     C                   EVAL      R1154='00'
     C     EHSTA         WHENEQ    '0'
     C                   EVAL      R1154='00'
     C     EHSTA         WHENEQ    '1'
     C                   EVAL      R1154='01'
     C                   OTHER
     C                   EVAL      R1154='02'                                   CC:15, 2, 3
     C                   ENDSL
     C                   WRITE     RFFR
      * NAD+BY
     C                   CLEAR                   NADR
     C                   EVAL      NMN=WMN
     C                   EVAL      NGN=3
     C                   EVAL      NREP0=1
     C                   EVAL      N3035='BY'
     C                   EVAL      N3039='300T01'
     C                   EVAL      N3055='9'
     C                   WRITE     NADR
      * CTA+OC+:Kjell Arne Reinsbø Lawson'
     C                   CLEAR                   CTAR
     C                   EVAL      CMN=WMN
     C                   EVAL      CGN=6
     C                   EVAL      CREP0=1
     C                   EVAL      CREP1=1
     C                   EVAL      C3139='OC'
     C                   EVAL      C3412=EHIID
     C                   EVAL      C3413=EHINV
     C                   WRITE     CTAR
      * NAD+SU
     C                   CLEAR                   NADR
     C                   EVAL      NMN=WMN
     C                   EVAL      NGN=3
     C                   EVAL      NREP0=2
     C                   EVAL      N3035='SU'
     C                   EVAL      N3039=EHLEVN
     C                   EVAL      N3055='9'
     C                   WRITE     NADR
      * TOD
     C                   CLEAR                   TODR
     C                   EVAL      TMN=WMN
     C                   EVAL      TGN=12
     C                   EVAL      TREP0=1
     C                   EVAL      T4055='10E'
     C                   EVAL      T4053=EHLBET
     C                   WRITE     TODR

     C     SRA9          TAG

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Linjer                                           SRLINJE /
      *////////////////////////////////////////////////////////////
     C     SRLINJE       BEGSR
     C                   ADD       1             WC6066
      * LIN
     C                   CLEAR                   LINR
     C                   EVAL      LMN=WMN
     C                   EVAL      LGN=26
     C                   EVAL      LREP0=WC6066
     C*                  EVAL      ØN152=ELLIN
     C                   EVAL      ØN152=WC6066
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           L1082A
     C                   EVAL      L1229='5'
     C                   EVAL      L7140=ELART
     C                   EVAL      L7143='EN'
     C                   WRITE     LINR
      * PIA
     C                   CLEAR                   PIAR
     C                   EVAL      PMN=WMN
     C                   EVAL      PGN=26
     C                   EVAL      PREP0=WC6066
     C                   EVAL      PREP1=1
     C                   EVAL      P4347='1'
     C                   EVAL      P7140A=ELARTL
     C                   EVAL      P7143A='SA'
     C                   EVAL      P3055A='91'
     C                   WRITE     PIAR
      * IMD
     C                   CLEAR                   IMDR
     C                   EVAL      IMN=WMN
     C                   EVAL      IGN=26
     C                   EVAL      IREP0=WC6066
     C                   EVAL      IREP1=1
     C                   EVAL      I7077='F'
     C                   EVAL      I7008A=ELABES
     C                   WRITE     IMDR
      * QTY+21  - Bestilt
     C                   CLEAR                   QTYR
     C                   EVAL      QMN=WMN
     C                   EVAL      QGN=26
     C                   EVAL      QREP0=WC6066
     C                   EVAL      QREP1=1
     C                   EVAL      Q6063='21'
     C                   EVAL      ØN152=ELANTS
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           Q6060
     C                   EVAL      Q6411='PCE'
     C                   WRITE     QTYR
      * QTY+113   Levert (to be delivered)
     C                   CLEAR                   QTYR
     C                   EVAL      QMN=WMN
     C                   EVAL      QGN=26
     C                   EVAL      QREP0=WC6066
     C                   EVAL      QREP1=2
     C                   EVAL      Q6063='113'
     C*                  IF        ELNTSL > 0
     C                   EVAL      ØN152=ELNTSL
     C*                  ELSE
     C*                  EVAL      ØN152=ELANTS
     C*                  ENDIF
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           Q6060
     C                   EVAL      Q6411='PCE'
     C                   WRITE     QTYR
      * DTM
     C                   CLEAR                   DTMR
     C                   EVAL      DMN=WMN
     C                   EVAL      DGN=26
     C                   EVAL      DREP0=WC6066
     C                   EVAL      DREP1=1
     C                   EVAL      D2005='110'
     C                   IF        ELETD>0
     C                   EVAL      ØN152=ELETD
     C                   ELSE
     C                   EVAL      ØN152=EHETD
     C                   ENDIF
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           D2380
     C                   EVAL      D2379='102'
     C                   WRITE     DTMR
      * QVR
     C                   IF        ELNTSL>0
     C                   EVAL      WDIFF=ELANTS-ELNTSL
     C                   IF        WDIFF>0
     C                   CLEAR                   QVRR
     C                   EVAL      QMN=WMN
     C                   EVAL      QGN=26
     C                   EVAL      QREP0=WC6066
     C                   EVAL      QREP1=1
     C                   EVAL      ØN152=WDIFF
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           Q6064
     C                   EVAL      Q6063='21'
     C                   EVAL      Q4294='Delayed'
     C                   WRITE     QVRR
     C                   ENDIF
     C                   ENDIF
      * RFF
     C                   CLEAR                   RFFR
     C                   EVAL      RMN=WMN
     C                   EVAL      RGN=31
     C                   EVAL      RREP0=WC6066
     C                   EVAL      RREP1=1
     C                   EVAL      R1153='ON'
     C                   EVAL      R1154=EHBES
     C                   EVAL      ØN152=ELLIN
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           R1156
     C**                 IF        ELULIN>0
     C                   EVAL      ØN152=ELULIN
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           R4000
     C**                 ENDIF
     C                   WRITE     RFFR
      * RFF-status
     c                   if        ELSTA=*blanks
     c                   eval      ELSTA='0'
     c                   endif
     C                   CLEAR                   RFFR
     C                   EVAL      RMN=WMN
     C                   EVAL      RGN=31
     C                   EVAL      RREP0=WC6066
     C                   EVAL      RREP1=2
     C                   EVAL      R1153='Z1'
      * fra ORDRSP vil Voice kun ha 00 pending(lite sansynlig) 01=Contacted og 02=Ready for stuffing
      * høyere status får de via ASN/DESADV og det regner de som sin status 03=Shipped
     C                   SELECT
     C     ELSTA         WHENEQ    '0'
     C                   EVAL      R1154='00'
     C     ELSTA         WHENEQ    '1'
     C                   EVAL      R1154='01'
     C                   OTHER
     C                   EVAL      R1154='02'                                   CC:15, 2, 3
     C                   ENDSL
     C                   WRITE     RFFR
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Meldingsslutt                                    SRSLUTT /
      *////////////////////////////////////////////////////////////
     C     SRSLUTT       BEGSR
      * WRITE TO EUNS
     C                   CLEAR                   UNSR
     C                   EVAL      UMN=WMN
     C                   EVAL      UGN=50
     C                   EVAL      UREP0=1
     C                   EVAL      U0081='S'
     C                   WRITE     UNSR
      * WRITE TO ECNT96A
     C                   CLEAR                   CNTR
     C                   EVAL      CMN=WMN
     C                   EVAL      C6069='2'
     C                   EVAL      ØN152=WC6066
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           C6066
     C                   WRITE     CNTR
      * WRITE TO EUNH
     C                   CLEAR                   UNHR
     C                   EVAL      UMN=WMN
     C                   MOVEL     WMN           U0062A
     C                   MOVEL     'ORDRSP'      U0065
     C                   MOVEL     'D'           U0052
     C                   MOVEL     '96A'         U0054
     C                   MOVEL     'UN'          U0051
     C                   MOVEL     'EAN005'      U0057
     C                   MOVEL     EHBES         U0068
     C                   MOVEL     '231'         U1001
     C                   MOVEL     EHBES         U1004
     C                   MOVEL     '4'           U1225
     C                   MOVEL     'NA'          U4343
     C                   MOVEL     U0062A        U0062B
     C                   WRITE     UNHR
      * WRITE TO EDIM
     C                   CLEAR                   EDIMR
     C                   EVAL      M0004='7080003141941'
     C                   EVAL      M0010='7080001240011'
     C                   MOVEL     U0062A        M0062
     C                   MOVE      U0065         M0065
     C                   MOVEL     U0068         M0068
     C                   MOVEL     U1001         M1001
     C                   MOVEL     U1004         M1004
     C                   MOVEL     U1225         M1225
     C                   Z-ADD     WMN           MMN
     C                   Z-ADD     WMN           MMN
     C                   MOVE      'S'           MSR
     C                   MOVEL     WDT           MDT
     C                   MOVEL     WTM           MTM
     C                   WRITE     EDIMR
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Venstrestill numeriske verdier                      SRNE /
      *////////////////////////////////////////////////////////////
     C     SRNE          BEGSR
      * Minus value ?
     C     ØN152         IFLT      0                                            <-------------I
     C                   Z-SUB     ØN152         ØN152                                        I
     C                   MOVE      '-'           ØSIGN             1                          I
     C                   ELSE                                                   <-------------I
     C                   MOVE      '+'           ØSIGN                                        I
     C                   ENDIF                                                  <-------------I
      * Flytte in rett verdi i Alfa 12-felt
     C                   MOVE      *ZEROS        ØA12
     C     ØA152D        IFEQ      '00'                                         <-------------I
     C                   MOVE      ØA152H        ØA12                                         I
     C                   ELSE                                                   <-------------I
     C                   MOVEL     ØA152X        ØA12                                         I
     C                   MOVE      ',  '         ØA12                                         I
     C                   MOVE      ØA152D        ØA12                                         I
     C                   END                                                    <-------------I
      * lete fra venstre og finn foerste siffer over 0
     C     1             DO        11            ØX                             <-------------I
     C     Ø12(ØX)       IFNE      '0'                                           <-----------II
     C     Ø12(ØX)       IFEQ      ','                                           <-----------II
     C                   SUB       1             ØX
     C                   ENDIF                                                   <-----------II
     C                   GOTO      SRNE1                                                     II
     C                   END                                                     <-----------II
     C                   END                                                    <-------------I
     C     SRNE1         TAG
      * Venstrestill til et nytt felt
     C                   MOVE      *BLANKS       ØAN
     C     ØSIGN         IFEQ      '-'                                          <-------------I
     C                   SUB       1             ØX                                           I
     C                   MOVE      '-'           Ø12(ØX)                                      I
     C                   ENDIF                                                  <-------------I
     C                   MOVEA     Ø12(ØX)       ØAN
      *
     C                   ENDSR
