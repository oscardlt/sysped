********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr *Sek Sig Vers Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
P8068 * 01 TH  PTF8 NY HELPFUNKSJON  KUN DDS
********************************************************************
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      REF(*LIBL/CUFLD CUFLDR)
     A                                      PRINT
     A                                      CF03(03 'Exit')
S01  A*                                     HLPDOC('SYFA57C000' 'SYFA57C' 'SY40-
S01  A*                                     0LO/MAINT')
N01  A                                      HLPPNLGRP(HLPSCR SYFA57CPNL)
N01  A                                      HLPTITLE('VEDLIKEHOLD AV KUNDES SAM-
N01  A                                      LEF.PROFIL - UTPLUKK AV FELT')
     A                                      ALTHELP
     A                                      HELP
     A          R SUBFIL                    SFL
     A                                      TEXT('SUBFILE')
     A            WFRECN        10S 0H
     A            WFN       R        H      REFFLD(CUFLDR/CFN *LIBL/CUFLD)
     A            WFP       R        H      REFFLD(CUFLDR/CFP *LIBL/CUFLD)
     A            WFS       R        H      REFFLD(CUFLDR/CFS *LIBL/CUFLD)
     A            WSFL           1A  B  5  2VALUES(' ' '1' '2' '3')
     A  60                                  DSPATR(RI)
     A            WSFR      R        B  5  4REFFLD(CUFLDR/CFR *LIBL/CUFLD)
     A                                      EDTCDE(4)
     A  60                                  DSPATR(RI)
     A            WSFB      R        B  5  8REFFLD(CUFLDR/CFB *LIBL/CUFLD)
     A                                      EDTCDE(4)
     A            WSOVER    R        B  5 12REFFLD(CUFLDR/CFOVER *LIBL/CUFLD)
     A                                      CHECK(LC)
     A  60                                  DSPATR(RI)
     A            WSFLEN    R        B  5 35REFFLD(CUFLDR/CFLEN *LIBL/CUFLD)
     A                                      EDTCDE(4)
     A  61N66                               COLOR(WHT)
     A  66                                  DSPATR(PR)
     A  66                                  DSPATR(ND)
     A            WSFDEC    R        B  5 40REFFLD(CUFLDR/CFDEC *LIBL/CUFLD)
     A  62N65                               COLOR(WHT)
     A  65                                  DSPATR(PR)
     A  65                                  DSPATR(ND)
     A            WSEDIT    R        B  5 44REFFLD(CUFLDR/CFEDIT *LIBL/CUFLD)
     A  63N65                               COLOR(WHT)
     A  64N65                               DSPATR(RI)
     A  65                                  DSPATR(PR)
     A  65                                  DSPATR(ND)
     A            SFBESK    R        O  5 48REFFLD(SYFLDR/SFBESK *LIBL/SYFLD)
     A            SFLEN     R        O  5 69REFFLD(SYFLDR/SFLEN *LIBL/SYFLD)
     A                                      EDTCDE(4)
     A            SFDEC     R        O  5 74REFFLD(SYFLDR/SFDEC *LIBL/SYFLD)
     A            SFEDIT    R        O  5 77REFFLD(SYFLDR/SFEDIT *LIBL/SYFLD)
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      TEXT('SUBCTL')
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                      CF04(04 'SØK')
     A                                      CF05(05 'Forny')
     A                                      CF12(12 'Annuller')
     A                                      CSRLOC(LINNBR POSNBR)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A                                      SFLSIZ(0012)
     A                                      SFLPAG(0011)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/SYFA57C'
     A                                      COLOR(BLU)
     A                                  1 21MSGCON(045 JOV0497 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 72DATE
     A                                      EDTCDE(Y)
     A            ZRECNO         4S 0O  2 63SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  2 72TIME
     A                                  3  1MSGCON(079 JOV0498 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            TOPL           1Y 0B  4  2EDTCDE(4)
     A          R NOREC
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      TEXT('NOREC ')
     A                                      CF05(05 'Forny')
     A                                      CF12(12 'Annuller')
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/SYFA57C'
     A                                      COLOR(BLU)
     A                                  1 21MSGCON(045 JOV0497 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 73DATE
     A                                      EDTCDE(Y)
     A                                  2 73TIME
     A                                  5  1MSGCON(079 JOV0498 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            TOPL           1Y 0B  6  2EDTCDE(4)
     A  99                             14  5MSGCON(070 JOV0570 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A  99                             15  5MSGCON(070 JOV0571 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A  99                             16  5MSGCON(070 JOV0572 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 21MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 61MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A          R BOTREC
     A                                      TEXT('INFO')
     A                                      OVERLAY
     A                                 16  2MSGCON(078 JOV0500 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A            HE1           79   O 17  1
     A                                      COLOR(WHT)
     A            HE2           79   O 18  1
     A                                      COLOR(WHT)
     A            HE3           79   O 19  1
     A                                      COLOR(WHT)
     A                                      DSPATR(UL)
     A            LI1           79   O 20  1
     A            LI2           79   O 21  1
     A            LI3           79   O 22  1
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 21MSGCON(018 DIV0032 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 41MSGCON(025 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A          R MSGREC                    TEXT('Message')
     A                                      OVERLAY
     A            ZMSGTX        79A  O 24  2MSGID(&ZMSGNO SYMSGF)
     A                                      COLOR(WHT)
     A            ZMSGNO         7A  P
