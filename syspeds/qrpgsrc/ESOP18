      *********************************************************************
      * Lage ny mottagersadresse
      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/ESOP18 MODULE(*LIBL/ESOP18
CRTPGM*        *LIBL/CHECKUSR *LIBL/INCGI) ACTGRP(CGI) AUT(*USE)
      *
      *********************************************************************
      * MAIN PROGRAM FLOW
      *********************************************************************
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
     FBRIDF     UF   E           K DISK    USROPN
     FBRPARF    if   e           k disk    usropn
     FVADR      IF A E           K DISK    USROPN
     FKUNKO2    IF A E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Prototype defintions and standard system API error structure
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      *--------------------------------------------------------------------
      * Variables common to CGI programs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
      * Variables received from the input string
     D lng             s              2
     D UsrSpcName      s             10
     D RETURNR         S              1
     D WSUSER          S             10
     D WSADRN          S              5  0
     D WSKURE          S             17
     D WSDRNA          S             30
     D WSDRN1          S             30
     D WSDRN2          S             30
     D WSDRN3          S             30
     D LAGREMAD        S              1
     D PGM             C                   CONST('SYSPED/CHECKUSR')
     D FMELDNR         S              2
     D FEILMELDING     S             60
     D FEIL011         C                   CONST('Mottakersnr kan ikke være bla-
     D                                     nk.')
     D FEILEN011       C                   CONST('Number of goods receiver can -
     D                                     not be blanks.')
     D FEIL012         C                   CONST('Mottakersnr finnes fra før.')
     D FEILEN012       C                   CONST('Number of goods receiver is -
     D                                     already registered.')
     D FEIL02          C                   CONST('Mottakersnavn må oppgis.')
     D FEILEN02        C                   CONST('Name of goods receiver can -
     D                                     not be blanks.')
     D FEIL03          C                   CONST('Mottakersadresse1 må oppgis.')
     D FEILEN03        C                   CONST('Address 1 has to be given.')
     D FEIL04          C                   CONST('Mottakers postadresse må oppg-
     D                                     is.')
     D FEILEN04        C                   CONST('Post address has to be given-
     D                                     .')
      *
     D Lib             s             10
     D Fn              s             10
     D Mbr             s             10
      *
     D Spaces          s             12a   inz('&nbsp;&nbsp;')
      *
      *--------------------------------------------------------------------
      * Prolog common to CGI programs
      * -Receive input from the remote browser
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prolog3
      *=====================================================================******
      * MAIN PROCESS LINE
      *=====================================================================******
      * Get program start time for calculating execution time
     C                   time                    timedata1
      * Parse input
     C                   exsr      Parse
      *
     C                   EXSR      INIT
     C                   CALLB     PGM
     C                   PARM                    BIBRID
     C                   PARM                    UsrSpcName
     C                   PARM                    UINN              1
     C                   IF        UINN = 'N'
     C                   GOTO      SLUTT
     C                   END
     C     WSUSER        CHAIN     BRIDF                              50
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVE      WDT           BIDTF
     C                   MOVE      WTM           BITIDF
     C                   UPDATE    BRIDFR
      * hvis en har tastet submit fra vedlikehodsbildet, test/lagre
      * (men bildet vises på ny dersom det er feil ...)
     c     LAGREMAD      IFEQ      'Y'
     c                   exsr      tstfeil
     c                   end
      *
      * Set output skeleton html member name
     C                   exsr      SetSkl
      * Read skeleton output html, etc.
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
      *
      * 1-Section /$top
     C                   exsr      SetTop
     C                   callp     updhtmlvar('FEILMELDING':
     C                             FEILMELDING)
     C                   callp     updhtmlvar('FMELDNR':FMELDNR)
     C                   callp     wrtsection('top')
      *
     C     SLUTT         TAG
     C                   EXSR      EXIT
     C                   return
      *=====================================================================******
      * Test skjermbilde
      *=====================================================================******
     C     TstFeil       begsr
      * Er MOTTAKERSNR BLANK
     C     WSKURE        IFEQ      *BLANKS
     C                   IF        XSPRÅK = 'EN'
     C                   MOVEL     FEILEN011     FEILMELDING
     C                   ELSE
     C                   MOVEL     FEIL011       FEILMELDING
     C                   END
     C                   MOVE      '01'          FMELDNR
     C                   GOTO      UtTest
     C                   ENDIF
      * Er MOTTAKERSNR FINNES FRA FØR
     C     KUNKO2K       CHAIN     KUNKO2                             33
     C                   IF        *IN33 = *OFF
     C                   IF        XSPRÅK = 'EN'
     C                   MOVEL     FEILEN012     FEILMELDING
     C                   ELSE
     C                   MOVEL     FEIL012       FEILMELDING
     C                   ENDIF
     C                   MOVE      '01'          FMELDNR
     C                   GOTO      UtTest
     C                   ENDIF
      * Test NAVN PÅ MOTTAKER
     c     WSDRNA        IFEQ      *BLANKS
     C                   IF        XSPRÅK = 'EN'
     C                   MOVEL     FEILEN02      FEILMELDING
     C                   ELSE
     C                   MOVEL     FEIL02        FEILMELDING
     C                   ENDIF
     C                   MOVE      '02'          FMELDNR
     C                   GOTO      UtTest
     C                   ENDIF
      *
      * Test ADRESSE 1
     c     WSDRN1        IFEQ      *BLANKS
     C                   IF        XSPRÅK = 'EN'
     C                   MOVEL     FEILEN03      FEILMELDING
     C                   ELSE
     C                   MOVEL     FEIL03        FEILMELDING
     C                   ENDIF
     C                   MOVE      '03'          FMELDNR
     C                   GOTO      UtTest
     C                   ENDIF
      *
      * Test MOTTAKERS POSTADRESSE
     c     WSDRN3        IFEQ      *BLANKS
     C                   IF        XSPRÅK = 'EN'
     C                   MOVEL     FEILEN04      FEILMELDING
     C                   ELSE
     C                   MOVEL     FEIL04        FEILMELDING
     C                   ENDIF
     C                   MOVE      '04'          FMELDNR
     C                   GOTO      UtTest
     C                   ENDIF
      *
      * ALT OK - eksisterend tur eller lag ny - Deretter avslutt
     C                   MOVE      'Y'           AVSLUTT
      * HENT SISTADRESSENR
     C     2             DO        99998         XN                5 0
     C     VADRK         CHAIN(N)  VADR                               33
     C                   IF        *IN33
     C                   MOVE      XN            WSADNR            5 0
     C                   LEAVE
     C                   END
     C                   ENDDO
      *
     C                   CLEAR                   VADRR
     C                   MOVE      'A'           AKTKOD
     C                   MOVE      BIFIRM        FIRMA
     C                   MOVE      BIKUNR        KUNDNR
     C                   MOVE      XN            VADRNR
     C                   MOVE      WSDRNA        VADRNA
     C                   MOVEL     WSDRNA        SONAVN
     C                   MOVE      WSDRN1        VADRN1
     C                   MOVE      WSDRN2        VADRN2
     C                   MOVE      WSDRN3        VADRN3
     C                   MOVE      WSKURE        VAKURE
     C                   WRITE     VADRR
      *
     C                   MOVE      BIKUNR        KUKUN1
     C                   MOVE      BIKUNR        KUKUN2
     c                   MOVE      WSKURE        KUINTN
     c                   MOVE      SONAVN        KUALFA
     c                   MOVE      VADRNR        KUVADR
     C                   WRITE     KUNKOR

      *
     C     UtTest        endsr
      *=====================================================================******
      * Parse input
      *=====================================================================******
     C     Parse         begsr
     C                   eval      LAGREMAD = zhbgetvar('LAGREMAD')
     C                   eval      lng     = zhbgetvar('lng')
     C                   eval      WSUSER  = zhbgetvar('USER')
     C                   EVAL      UsrSpcName  = zhbgetvar('UsrSpcName')
     C                   eval      RETURNR = zhbgetvar('RETURNR')
     C                   eval      WSKURE  = zhbgetvar('WSKURE')
     C                   eval      WSKURE  = uppify(WSKURE)
     C                   IF        WSKURE  = 'UNDEFINED'
     C                   eval      WSKURE  = *BLANKS
     C                   END
     C                   eval      WSDRNA  = zhbgetvar('WSDRNA')
     C                   eval      WSDRNA  = uppify(WSDRNA)
     C                   eval      WSDRN1  = zhbgetvar('WSDRN1')
     C                   eval      WSDRN1  = uppify(WSDRN1)
     C                   eval      WSDRN2  = zhbgetvar('WSDRN2')
     C                   eval      WSDRN2  = uppify(WSDRN2)
     C                   eval      WSDRN3  = zhbgetvar('WSDRN3')
     C                   eval      WSDRN3  = uppify(WSDRN3)
      *
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$top
      *  (search criteria)
      *=====================================================================******
     C     SetTop        begsr
      * Repeat national language for the next invocation
     C                   callp     updhtmlvar('LNG':lng:
     C                             InitHTMLVars)
      * Color for text, background, links, visited links, active links
     C                   callp     updhtmlvar('TXTCOLOR':'black')
     C                   callp     updhtmlvar('BOLD':' ')
     C                   callp     updhtmlvar('BGCOLOR':BIFARG)
     C                   callp     updhtmlvar('LINK':'0000FF')
     C                   callp     updhtmlvar('VLINK':'FF0000')
     C                   callp     updhtmlvar('ALINK':'00FF00')
     C                   callp     updhtmlvar('WSKURE':WSKURE)
     C                   callp     updhtmlvar('WSDRNA':WSDRNA)
     C                   callp     updhtmlvar('WSDRN1':WSDRN1)
     C                   callp     updhtmlvar('WSDRN2':WSDRN2)
     C                   callp     updhtmlvar('WSDRN3':WSDRN3)
BODY C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updHtmlVar('USER':WSUSER)
     C                   callp     updHtmlVar('UsrSpcName':UsrSpcName)
     C                   callp     updHtmlVar('RETURNR':RETURNR)
     C                   callp     updHTMLvar('sdato':
     C                             %trim(%editw(BIDTV:'    .  .  ')))
     C                   callp     updHTMLvar('stid':
     C                             %trim(%editw(BITIDV:'  .  .  ')))
     C                   callp     updHtmlVar('AVSLUTT':AVSLUTT)
      *
     C                   endsr
      *=====================================================================******
      * Set html output skeleton member before its read into memory
      *=====================================================================******
     C     SetSkl        begsr
      *------------------
      * Set html output skeleton member
     C                   eval      lng = uppify(lng)
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC' + lng
     C                   eval      Mbr = 'ESOP18'
     C                   CAT       XSPRÅK:0      MBR
      *
     C                   endsr
      *=====================================================================******
      *  INITIER
      *=====================================================================******
     C     INIT          begsr
     C                   OPEN      BRIDF
     C     WSUSER        CHAIN(N)  BRIDF                              50
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
     C                   OPEN      VADR
     C                   open      KUNKO2
     C                   open      BRPARF                               50
      *
     C     VADRK         klist
     C                   kfld                    BIFIRM
     C                   kfld                    BIKUNR
     C                   kfld                    XN
     C     KUNKO2K       klist
     C                   kfld                    BIKUNR
     C                   kfld                    WSKURE
     C     BRPARFK       klist
     C                   kfld                    PABRID
     C                   kfld                    PAKUNR
     C                   kfld                    PAID
      *
     C                   EVAL      PABRID = WSUSER
     C                   MOVEL     'SPRÅK'       PAID
     C     BRPARFK       chain     BRPARF                             33
     C                   IF        (*IN33 OR (PAVRDA <> 'EN'))
     C                   MOVE      *BLANKS       XSPRÅK            2
     C                   ELSE
     C                   MOVE      'EN'          XSPRÅK            2
     C                   END
      *
     C                   MOVE      *BLANKS       FEILMELDING
     C                   move      *BLANKS       FMELDNR
     C                   MOVE      'N'           AVSLUTT           1
      *
     C                   endsr
      *=====================================================================******
      *  AVSLUTT
      *=====================================================================******
     C     EXIT          begsr
     C                   callp     wrtsection('endhtml')
     C                   callp     wrtsection('*fini')
     C                   close     BRIDF
     C                   close     BRPARF
     C                   close     VADR
     C                   close     KUNKO2
     C                   eval      *inlr = *on
      *
     C                   endsr
