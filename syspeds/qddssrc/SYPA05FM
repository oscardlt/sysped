     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      REF(*LIBL/PALLD)
     A                                      PRINT
     A                                      CF03(03 'Exit')
     A                                      ALTHELP(CA01)
     A                                      HELP
      * "NOERASE" MÅ HA FOR Å IKKE TØMME BILDE SOM MAN KOMME FRA
     A          R NOERASE
     A                                      ASSUME
     A                                  1  2' '
      * MAN KUN DEFINNERE VINDU PÅ REKORDNIVÅ
     A          R WINDOWSZ
     A                                      WINDOW(2 3 14 72 *NOMSGLIN)
     A                                      OVERLAY
     A          R RECB                      SFL
     A                                      TEXT('SUBFILE')
     A            PAKVDT    R        H      REFFLD(PALLDR/PAKVDT *LIBL/PALLD)
     A            PABUNR    R        O  4  2REFFLD(PALLDR/PABUNR *LIBL/PALLD)
     A                                      EDTCDE(4)
     A            PAKVNR    R        O  4 10REFFLD(PALLDR/PAKVNR *LIBL/PALLD)
     A                                      EDTCDE(4)
     A            PALØNR    R        O  4 20REFFLD(PALLDR/PALØNR *LIBL/PALLD)
     A                                      EDTCDE(4)
     A            PAPPNR    R        O  4 28REFFLD(PALLDR/PAPPNR *LIBL/PALLD)
     A                                      EDTCDE(4)
     A            XXNAVN        15A  O  4 37
     A            PAANT     R        O  4 55REFFLD(PALLDR/PAANT *LIBL/PALLD)
     A                                      EDTCDE(L)
     A            PASTAT    R        O  4 63REFFLD(PALLDR/PASTAT *LIBL/PALLD)
     A            WAKVDT         6Y 0O  4 65EDTWRD('  .  .  ')
     A          R RECC                      SFLCTL(RECB)
     A                                      SFLSIZ(0010)
     A                                      SFLPAG(0009)
     A                                      WINDOW(WINDOWSZ)
     A                                      TEXT('SUBCTL')
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                      CF05(05 'Forny')
     A                                      CF23(23 'AKSEPTER')
     A                                      CF12(12 'Annuller')
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/SYPA05'
     A                                      COLOR(BLU)
     A                                  1 20MSGCON(045 JOV0666 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A  50                              2 20MSGCON(035 JOV0975 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A            ZRECNO         4S 0O  2 60SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  3  3MSGCON(070 JOV0974 *LIBL/SYMSGF)
     A                                      COLOR(WHT)
     A          R RECD
     A                                      WINDOW(WINDOWSZ)
     A                                      TEXT('INFO')
     A                                      SLNO(13)
     A                                      CLRL(02)
     A                                      OVERLAY
     A                                  1  2MSGCON(031 JOV0665 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  1 33MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  1 52MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A            ZMSGTX        68A  O  2  2MSGID(&ZMSGNO SYMSGF)
     A                                      COLOR(WHT)
     A            ZMSGNO         7A  P
