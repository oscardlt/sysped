      * SYSPED-SYEDI For TG INVOIC 921
      *
      * 1. SYSPED SYFA27 legger ut poster i SYFA27W.
      *
      * 2. Dette programmet leser fra SYFA27W
      *
      * 3. Dette programmet legger ut til EDI-filer i SYEDI.
      *
      * 4. SYEDI lager automatisk EDIFACT utifra EDI-filene.
      *
     FSYFA27W   UF   E           K DISK
     FEDIC      UF   E             DISK
     FDOKUF     IF   E           K DISK
     FEUNH      O  A E             DISK
     FEDIM      O  A E             DISK
     FEUNS      O  A E             DISK
     FECNT921   O  A E             DISK
     FECTA921   O  A E             DISK
     FEDTM921   O  A E             DISK
     FEFII921   O  A E             DISK
     FELIN921   O  A E             DISK
     FEMEA921   O  A E             DISK
     FEMOA921   O  A E             DISK
     FENAD921   O  A E             DISK
     FEPAT921   O  A E             DISK
     FEQTY921   O  A E             DISK
     FERFF921   O  A E             DISK
     FETAX921   O  A E             DISK
      *___________________________
      * Arrays for string handling
      *"""""""""""""""""""""""""""
     D                 DS
     D  WTID                   1      6  0
     D  WDATO                  7     14  0
     D                 DS
     D  WDTN                   1      6  0
     D  WDTA                   1      6
     D  WDTAD                  1      2
     D  WDTAM                  3      4
     D  WDTAY                  5      6
     D                 DS
     D  WKID                   1     19
     D  WKID1                  1      8  0
     D  WKID2                  9     15  0
     D  WKID3                 16     19  0
     D                 DS
     D  XKID                   1     20
     D  XKID1                  1     19
     D  XKID2                 20     20  0
      *____________________________________
      * DS for leftjustify numericel fields
      *""""""""""""""""""""""""""""""""""""
     D                 DS
     D  ØN153                  1     15  3
     D  ØA153H                 1     12
     D  ØA153X                 5     12
     D  ØA153D                13     15
     D                 DS
     D  ØA12                   1     12
     D  Ø12                    1     12
     D                                     DIM(12)
      */////////////
      * MAIN LOGIC /
      */////////////
     C                   EXSR      SRINIT
     C                   EXSR      SRA
     C                   MOVE      '1'           *INLR
      *////////////////////////////////////////////////////////////
      * SR                                                  CTA05 /
      *////////////////////////////////////////////////////////////
     C     CTA05         BEGSR
     C                   Z-ADD     1             WREP1
     C                   Z-ADD     0             WREP2
     C                   CLEAR                   CTAR
     C                   Z-ADD     WMN           CMN
     C                   Z-ADD     5             CGN
     C                   Z-ADD     WREP0         CREP0
     C                   Z-ADD     WREP1         CREP1
      *_____________
      * Ordrekontakt
      *"""""""""""""
     C                   MOVEL     'OC'          C3139
     C                   MOVEL     'Anne Aamodt' C3412
     C                   WRITE     CTAR
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  CNT00 /
      *////////////////////////////////////////////////////////////
     C     CNT00         BEGSR
     C                   Z-ADD     1             WREP0
     C                   Z-ADD     0             WREP2
     C                   CLEAR                   CNTR
     C                   Z-ADD     WMN           CMN
     C                   Z-ADD     0             CGN
     C                   Z-ADD     WREP0         CREP0
      *__________________
      * Antall varelinjer
      *""""""""""""""""""
     C                   MOVEL     '2'           C6069
     C                   MOVEL     L1082         C6066
     C                   WRITE     CNTR
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  DTM00 /
      *////////////////////////////////////////////////////////////
     C     DTM00         BEGSR
     C                   Z-ADD     1             WREP0
     C                   Z-ADD     0             WREP1
     C                   Z-ADD     0             WREP2
     C                   CLEAR                   DTMR
     C                   Z-ADD     WMN           DMN
     C                   Z-ADD     0             DGN
     C                   Z-ADD     WREP0         DREP0
      *____________
      * Fakturadato
      *""""""""""""
     C                   MOVEL     '3'           D2005
     C                   Z-ADD     WB08          WDTN
     C     '20'          CAT       WDTAY:0       D2380
     C     D2380         CAT       WDTAM:0       D2380
     C     D2380         CAT       WDTAD:0       D2380
     C                   MOVEL     '102'         D2379
     C                   WRITE     DTMR
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  DTM08 /
      *////////////////////////////////////////////////////////////
     C     DTM08         BEGSR
     C                   Z-ADD     1             WREP1
     C                   Z-ADD     0             WREP2
     C                   CLEAR                   DTMR
     C                   Z-ADD     WMN           DMN
     C                   Z-ADD     8             DGN
     C                   Z-ADD     WREP0         DREP0
     C                   Z-ADD     WREP1         DREP1
      *_____________
      * Forfallsdato
      *"""""""""""""
     C                   MOVEL     '13'          D2005
     C                   Z-ADD     WB09          WDTN
     C     '20'          CAT       WDTAY:0       D2380
     C     D2380         CAT       WDTAM:0       D2380
     C     D2380         CAT       WDTAD:0       D2380
     C                   MOVEL     '102'         D2379
     C                   WRITE     DTMR
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * EDIM0                                                EDIM0/
      *////////////////////////////////////////////////////////////
     C     EDIM0         BEGSR
     C                   CLEAR                   EDIMR
     C                   MOVEL     WA01          M0004
     C                   MOVEL     WA02          M0010
     C                   MOVEL     U0062A        M0062
     C                   MOVEL     U0065         M0065
     C                   MOVEL     U1004         M0068
     C                   MOVEL     U1001         M1001
     C                   MOVEL     U1004         M1004
     C                   Z-ADD     WMN           MMN
     C                   MOVE      'S'           MSR
     C                   MOVEL     WDATO         MDT
     C                   MOVEL     WTID          MTM
     C                   WRITE     EDIMR
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  FII02 /
      *////////////////////////////////////////////////////////////
     C     FII02         BEGSR
     C                   Z-ADD     1             WREP1
     C                   Z-ADD     0             WREP2
     C                   CLEAR                   FIIR
     C                   Z-ADD     WMN           FMN
     C                   Z-ADD     2             FGN
     C                   Z-ADD     WREP0         FREP0
     C                   Z-ADD     WREP1         FREP1
      *____________________
      * Kontonummer
      *""""""""""""""""""""
     C                   MOVEL     'BF'          F3035
     C                   MOVEL     '50010503660' F3194
     C                   WRITE     FIIR
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  LIN22 /
      *////////////////////////////////////////////////////////////
     C     LIN22         BEGSR
      *____________
      * Les oppdrag
      *""""""""""""
     C                   Z-ADD     0             WREP0
     C                   READ      SYFA27W                                51
     C  N51WB06          COMP      WWB06                              5151
     C     *IN51         DOWEQ     '0'

     C     WA04          CABNE     'FDET'        LIN22B
     C                   DELETE    SYFA27WR

     C                   ADD       1             WREP0
     C                   Z-ADD     0             WREP1
     C                   Z-ADD     0             WREP2
     C                   CLEAR                   LINR
     C                   Z-ADD     WMN           LMN
     C                   Z-ADD     22            LGN
     C                   Z-ADD     WREP0         LREP0
     C                   Z-ADD     WREP0         ØN153
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           L1082
     C                   WRITE     LINR
     C                   EXSR      MEA22
     C                   EXSR      QTY22
     C                   EXSR      MOA23
     C                   EXSR      RFF26
     C                   EXSR      NAD31
      *__________________
      * Les neste oppdrag
      *""""""""""""""""""
     C                   READ      SYFA27W                                51
     C  N51WB06          COMP      WWB06                              5151
     C  N51              ENDDO
      *_________________
      * Les forrige post
      *"""""""""""""""""
     C     LIN22B        TAG
     C                   READP     SYFA27W                                51
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  MEA22 /
      *////////////////////////////////////////////////////////////
     C     MEA22         BEGSR
     C                   Z-ADD     0             WREP1
      *_____
??    * Vekt
      *"""""
     C                   ADD       1             WREP1
     C                   Z-ADD     0             WREP2
     C                   CLEAR                   MEAR
     C                   Z-ADD     WMN           MMN
     C                   Z-ADD     22            MGN
     C                   Z-ADD     WREP0         MREP0
     C                   Z-ADD     WREP1         MREP1
     C                   MOVEL     'WT'          M6311
     C                   MOVEL     'G'           M6313
     C                   MOVEL     'KGM'         M6411
     C                   EVAL      ØN153=WC02
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           M6314
     C                   WRITE     MEAR
      *_____________
??    * Antall kolli
      *"""""""""""""
     C                   ADD       1             WREP1
     C                   Z-ADD     0             WREP2
     C                   CLEAR                   MEAR
     C                   Z-ADD     WMN           MMN
     C                   Z-ADD     22            MGN
     C                   Z-ADD     WREP0         MREP0
     C                   Z-ADD     WREP1         MREP1
     C                   MOVEL     'AAU'         M6311
     C                   MOVEL     'PCE'         M6411
     C                   EVAL      ØN153=WC03
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           M6314
     C                   WRITE     MEAR
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  MOA23 /
      *////////////////////////////////////////////////////////////
     C     MOA23         BEGSR
     C                   Z-ADD     0             WREP1
      *____________
??    * Bruttofrakt
      *""""""""""""
     C                   ADD       1             WREP1
     C                   Z-ADD     0             WREP2
     C                   CLEAR                   MOAR
     C                   Z-ADD     WMN           MMN
     C                   Z-ADD     23            MGN
     C                   Z-ADD     WREP0         MREP0
     C                   Z-ADD     WREP1         MREP1
     C                   MOVEL     '203'         M5025
     C                   EVAL      ØN153=WC06
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           M5004
     C                   WRITE     MOAR
      *__________________
??    * Totalt fraktbeløp
      *""""""""""""""""""
     C                   ADD       1             WREP1
     C                   Z-ADD     0             WREP2
     C                   CLEAR                   MOAR
     C                   Z-ADD     WMN           MMN
     C                   Z-ADD     23            MGN
     C                   Z-ADD     WREP0         MREP0
     C                   Z-ADD     WREP1         MREP1
     C                   MOVEL     '38'          M5025
     C                   EVAL      ØN153=WC06
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           M5004
     C                   WRITE     MOAR
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  MOA45 /
      *////////////////////////////////////////////////////////////
     C     MOA45         BEGSR
      *____________
      * Les totalpost
      *""""""""""""
     C                   MOVE      *OFF          *IN51
     C     WA03          SETLL     SYFA27W
     C                   READ      SYFA27W                                51
     C  N51WB06          COMP      WWB06                              5151
     C  N51WA04          COMP      'FTOT'                             5151
     C     *IN51         IFEQ      *OFF
     C                   DELETE    SYFA27WR
      *____________
      * Bruttofrakt
      *""""""""""""
     C                   Z-ADD     1             WREP0
     C                   Z-ADD     0             WREP1
     C                   CLEAR                   MOAR
     C                   Z-ADD     WMN           MMN
     C                   Z-ADD     45            MGN
     C                   Z-ADD     WREP0         MREP0
     C                   MOVEL     '79'          M5025
     C                   Z-ADD     WD02          ØN153
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           M5004
     C                   WRITE     MOAR
      *_______________
      * Varelinjebeløp
      *"""""""""""""""
     C                   ADD       1             WREP0
     C                   Z-ADD     0             WREP1
     C                   CLEAR                   MOAR
     C                   Z-ADD     WMN           MMN
     C                   Z-ADD     45            MGN
     C                   Z-ADD     WREP0         MREP0
     C                   MOVEL     '125'         M5025
     C                   Z-ADD     WD02          ØN153
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           M5004
     C                   WRITE     MOAR
      *_____________
      * Fakturabeløp
      *"""""""""""""
     C                   ADD       1             WREP0
     C                   Z-ADD     0             WREP1
     C                   CLEAR                   MOAR
     C                   Z-ADD     WMN           MMN
     C                   Z-ADD     45            MGN
     C                   Z-ADD     WREP0         MREP0
     C                   MOVEL     '77'          M5025
     C                   Z-ADD     WE01          ØN153
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           M5004
     C                   WRITE     MOAR

     C                   ENDIF
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  MOA47 /
      *////////////////////////////////////////////////////////////
     C     MOA47         BEGSR
      *_______________
      * Merverdiavgift
      *"""""""""""""""
     C                   Z-ADD     1             WREP1
     C                   CLEAR                   MOAR
     C                   Z-ADD     WMN           MMN
     C                   Z-ADD     47            MGN
     C                   Z-ADD     WREP0         MREP0
     C                   MOVEL     '150'         M5025
     C                   Z-ADD     WD03          ØN153
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           M5004
     C                   WRITE     MOAR
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  NAD02 /
      *////////////////////////////////////////////////////////////
     C     NAD02         BEGSR
     C                   Z-ADD     0             WREP0
     C                   Z-ADD     0             WREP1
      *_______
      * Selger
      *"""""""
     C                   CLEAR                   NADR
     C                   ADD       1             WREP0
     C                   Z-ADD     0             WREP1
     C                   Z-ADD     0             WREP2
     C                   CLEAR                   NADR
     C                   Z-ADD     WMN           NMN
     C                   Z-ADD     2             NGN
     C                   Z-ADD     WREP0         NREP0

     C                   MOVEL     'SE'          N3035
     C                   MOVEL     '912984540'   N3039
     C                   MOVEL     '160'         N1131
     C                   WRITE     NADR

     C                   EXSR      FII02
     C                   EXSR      CTA05
      *_______
      * Kjøper
      *"""""""
     C                   CLEAR                   NADR
     C                   ADD       1             WREP0
     C                   Z-ADD     0             WREP1
     C                   Z-ADD     0             WREP2
     C                   CLEAR                   NADR
     C                   Z-ADD     WMN           NMN
     C                   Z-ADD     2             NGN
     C                   Z-ADD     WREP0         NREP0

     C                   MOVEL     'BY'          N3035
     C                   MOVEL     '00236851'    N3039
     C                   MOVEL     '91'          N3055
     C                   WRITE     NADR
      *________________
      * Fakturamottaker
      *""""""""""""""""
     C                   CLEAR                   NADR
     C                   ADD       1             WREP0
     C                   Z-ADD     0             WREP1
     C                   Z-ADD     0             WREP2
     C                   CLEAR                   NADR
     C                   Z-ADD     WMN           NMN
     C                   Z-ADD     2             NGN
     C                   Z-ADD     WREP0         NREP0

     C                   MOVEL     'IV'          N3035
     C                   MOVEL     '00236851'    N3039
     C                   MOVEL     '91'          N3055
     C                   EVAL      N3036A='Intersped Internasjonal A/S'
     C                   MOVEL     'Oslo'        N3164
     C                   MOVEL     '0511'        N3251
     C                   WRITE     NADR
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  NAD31 /
      *////////////////////////////////////////////////////////////
     C     NAD31         BEGSR
     C                   Z-ADD     0             WREP1
      *_______
      * Kjøper
      *"""""""
     C                   CLEAR                   NADR
     C                   ADD       1             WREP1
     C                   Z-ADD     0             WREP2
     C                   CLEAR                   NADR
     C                   Z-ADD     WMN           NMN
     C                   Z-ADD     31            NGN
     C                   Z-ADD     WREP0         NREP0
     C                   Z-ADD     WREP1         NREP1

     C                   MOVEL     'DP'          N3035
     C                   EVAL      N3036A=DFNAVL
     C                   EVAL      N3164=DFADUL
     C                   MOVEL     DFPOUL        N3251
     C                   WRITE     NADR
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  PAT08 /
      *////////////////////////////////////////////////////////////
     C     PAT08         BEGSR
      *_____________
      * Fakturadato
      *"""""""""""""
     C                   Z-ADD     1             WREP0
     C                   Z-ADD     0             WREP1
     C                   Z-ADD     0             WREP2
     C                   CLEAR                   PATR
     C                   Z-ADD     WMN           PMN
     C                   Z-ADD     8             PGN
     C                   Z-ADD     WREP0         PREP0
     C                   MOVEL     '3'           P4279
     C                   WRITE     PATR
     C                   EXSR      DTM08
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  QTY22 /
      *////////////////////////////////////////////////////////////
     C     QTY22         BEGSR
     C                   Z-ADD     0             WREP1
      *____________________
      * Fraktberegningsvekt
      *""""""""""""""""""""
     C                   ADD       1             WREP1
     C                   Z-ADD     0             WREP2
     C                   CLEAR                   QTYR
     C                   Z-ADD     WMN           QMN
     C                   Z-ADD     22            QGN
     C                   Z-ADD     WREP0         QREP0
     C                   Z-ADD     WREP1         QREP1
     C                   MOVEL     '100'         Q6063
     C                   Z-ADD     WC01          ØN153
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           Q6060
     C                   MOVEL     'KGM'         Q6411
     C                   WRITE     QTYR
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  RFF01 /
      *////////////////////////////////////////////////////////////
     C     RFF01         BEGSR
     C                   Z-ADD     1             WREP0
     C                   Z-ADD     0             WREP1
     C                   Z-ADD     0             WREP2
     C                   CLEAR                   RFFR
     C                   Z-ADD     WMN           RMN
     C                   Z-ADD     1             RGN
     C                   Z-ADD     WREP0         RREP0
      *____
      * KID
      *""""
     C                   EXSR      MOD10
     C                   MOVEL     'SS'          R1153
     C                   MOVEL     XKID          R1154
     C                   WRITE     RFFR
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  RFF26 /
      *////////////////////////////////////////////////////////////
     C     RFF26         BEGSR
     C                   Z-ADD     1             WREP1
     C                   CLEAR                   RFFR
     C                   Z-ADD     WMN           RMN
     C                   Z-ADD     26            RGN
     C                   Z-ADD     WREP0         RREP0
     C                   Z-ADD     WREP1         RREP1
      *________________
      * Sendningsnummer
      *""""""""""""""""
     C     DOKKEY        CHAIN     DOKUF                              59
     C     *IN59         IFEQ      *OFF
     C                   MOVEL     'AAM'         R1153
     C                   MOVEL     DF1004        R1154
     C                   WRITE     RFFR
     C                   ENDIF
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR for init                                        SRINIT /
      *////////////////////////////////////////////////////////////
     C     SRINIT        BEGSR
      *
     C     *ENTRY        PLIST
     C                   PARM                    UKUNDNR           8
      * HENTE DAGENS DATO OG TID
     C                   CALL      'SYGE15C'
     C                   PARM                    WDATO
     C                   PARM                    WTID
      * INIT AV ARBEIDSFELT
     C     *LIKE         DEFINE    DREP0         WREP0
     C     *LIKE         DEFINE    DREP1         WREP1
     C     *LIKE         DEFINE    DREP2         WREP2
     C     *LIKE         DEFINE    UMN           WMN
     C     *LIKE         DEFINE    DFRI          WDFRI
     C     *LIKE         DEFINE    DFFBNR        WDFFBNR
     C     *LIKE         DEFINE    WB06          WWB06
     C                   Z-ADD     0             ØX                2 0
     C                   MOVE      *BLANKS       ØAN              12
     C                   Z-ADD     0             W_ØN153          15 3
     C                   MOVEL     'F'           WDFRI
     C                   Z-ADD     1             WDFFBNR
      * DEF AV KEYS
     C     DOKKEY        KLIST
     C                   KFLD                    WDFRI
     C                   KFLD                    WC04
     C                   KFLD                    WC05
     C                   KFLD                    WDFFBNR
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                    SRA /
      *////////////////////////////////////////////////////////////
     C     SRA           BEGSR
      *_________________________
      * Les fakturatransaksjoner
      *"""""""""""""""""""""""""
     C                   READ      SYFA27W                                51
     C     *IN51         DOWEQ     '0'
      *________________________
      * Spar unna fakturanummer
      *""""""""""""""""""""""""
     C                   EVAL      WWB06=WB06
      *________________________
      * Retreive message number
      *""""""""""""""""""""""""
     C     1             CHAIN     EDIC                               52
     C                   ADD       1             CMN
     C                   UPDATE    EDICR
     C                   Z-ADD     CMN           WMN
     C                   MOVE      '1'           *IN41
      *____________________________
      * Slett transkasjonspost
      *""""""""""""""""""""""""""""
     C                   DELETE    SYFA27WR
      *_________________
      * EDIFACT Detaljer
      *"""""""""""""""""
     C                   EXSR      SRB
      *__________________
      * Les neste faktura
      *""""""""""""""""""
     C                   READ      SYFA27W                                51
     C                   ENDDO
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                    SRB /
      *////////////////////////////////////////////////////////////
     C     SRB           BEGSR
      *_____________
      * ADD SEGMENTS
      *"""""""""""""
     C                   EXSR      DTM00
     C                   EXSR      RFF01
     C                   EXSR      NAD02
     C                   EXSR      PAT08
     C                   EXSR      LIN22
     C                   EXSR      UNS00
     C                   EXSR      CNT00
     C                   EXSR      MOA45
     C                   EXSR      TAX47
      *_____________
      * WRITE TO UNH
      *"""""""""""""
     C                   EXSR      UNH00
      *______________
      * WRITE TO EDIM
      *""""""""""""""
     C                   EXSR      EDIM0
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Leftjustify num.fields                              SRNE /
      *////////////////////////////////////////////////////////////
     C     SRNE          BEGSR
      * Remove neg sign.
     C                   IF        ØN153 < 0
     C                   Z-SUB     ØN153         ØN153
     C                   ENDIF
      * Move correct value into alpha 12-field
     C                   MOVE      *ZEROS        ØA12
     C     ØA153D        IFEQ      '000'
     C                   MOVE      ØA153H        ØA12
     C                   ELSE
     C                   MOVEL     ØA153X        ØA12
     C                   MOVE      ',   '        ØA12
     C                   MOVE      ØA153D        ØA12
     C                   END
      * search from left and find first sign over 0
     C     1             DO        11            ØX
     C     Ø12(ØX)       IFNE      '0'
     C                   GOTO      SRNE1
     C                   END
     C                   END
     C     SRNE1         TAG
      * Leftjustify to a new field
     C     ØA153D        IFNE      '000'
     C     ØA153X        IFEQ      '00000000'
     C                   SUB       1             ØX
     C                   ENDIF
     C     Ø12(12)       IFEQ      '0'
     C                   MOVE      ' '           Ø12(12)
     C     Ø12(11)       IFEQ      '0'
     C                   MOVE      ' '           Ø12(11)
     C                   ENDIF
     C                   ENDIF
     C                   ENDIF
     C                   MOVE      *BLANKS       ØAN
     C                   MOVEA     Ø12(ØX)       ØAN

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  TAX47 /
      *////////////////////////////////////////////////////////////
     C     TAX47         BEGSR
     C                   Z-ADD     1             WREP0
     C                   Z-ADD     0             WREP1
     C                   CLEAR                   TAXR
     C                   Z-ADD     WMN           TMN
     C                   Z-ADD     47            TGN
     C                   Z-ADD     WREP0         TREP0
     C                   Z-ADD     WREP1         TREP1
     C                   MOVEL     '7'           T5283
     C                   MOVEL     'VAT'         T5153
     C                   Z-ADD     WD02          ØN153
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           T5278
     C                   WRITE     TAXR
     C                   EXSR      MOA47
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * UNH00                                               UNH00 /
      *////////////////////////////////////////////////////////////
     C     UNH00         BEGSR
     C                   CLEAR                   UNHR
     C                   Z-ADD     WMN           UMN
     C                   MOVEL     WMN           U0062A
     C                   MOVEL     'INVOIC'      U0065
     C                   MOVEL     '2'           U0052
     C                   MOVEL     '921'         U0054
     C                   MOVEL     'UN'          U0051
     C                   MOVEL     'NODI'        U0057
     C                   MOVEL     '380'         U1001
     C                   Z-ADD     WB06          ØN153
     C                   EXSR      SRNE
     C                   MOVEL     ØAN           U1004
     C                   MOVEL     'U'           U1225
     C                   MOVEL     U0062A        U0062B
     C                   WRITE     UNHR
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      * SR                                                  UNS00 /
      *////////////////////////////////////////////////////////////
     C     UNS00         BEGSR
     C                   Z-ADD     1             WREP0
     C                   Z-ADD     0             WREP1
     C                   Z-ADD     0             WREP2
     C                   CLEAR                   UNSR
     C                   Z-ADD     WMN           UMN
     C                   Z-ADD     50            UGN
     C                   Z-ADD     WREP0         UREP0
     C                   MOVEL     'S'           U0081
     C                   WRITE     UNSR
     C                   ENDSR
      *_________________________________________
      *  MOD10     -    UTREGNING AV MOD-10 KONTROLLFELT (WBAK)
      *""""""""""""""""""""""""""""""""""""""""""
     C     MOD10         BEGSR
     C                   Z-ADD     WB07          WKID1
     C                   Z-ADD     WB06          WKID2
     C                   Z-ADD     0             WKID3
     C                   MOVE      *ZEROS        WKID4            21
     C                   MOVE      WKID          WKID4
     C                   CALL      'SYGE09'
     C                   PARM                    WKID4
     C                   PARM                    WKID5             1 0
     C                   MOVE      WKID5         WKID6             1 0
      *
     C                   MOVEL     WKID          XKID1
     C                   Z-ADD     WKID6         XKID2
     C                   ENDSR
