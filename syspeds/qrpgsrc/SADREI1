********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 YBC PTF10 NY FIL, SADHMD
********************************************************************
     FEDIM2     UF   E           K DISK
     FEDIS      UF   E           K DISK
     FSADF      UF   E           K DISK
     FSADHD     UF   E           K DISK
N01  FSADHMD    UF   E           K DISK
     FSADV      UF   E           K DISK
     FSADW      UF   E           K DISK
     FTVINF     UF   E           K DISK
      *
     FEDIMOLD   O    E             DISK    RENAME(EDIMR:EDIMRO)
     FEDISOLD   O    E             DISK    RENAME(EDISR:EDISRO)
     FSADFOLD   O    E             DISK    RENAME(SADFR:SADFRO)
     FSADHOLD   O    E             DISK    RENAME(SADHR:SADHRO)
N01  FSADHMDOLD O    E             DISK    RENAME(SADHMDR:SADHMDRO)
     FSADVOLD   O  A E             DISK    RENAME(SADVR:SADVRO)
     FSADWOLD   O  A E             DISK    RENAME(SADWR:SADWRO)
      *
     D                 DS
     D  XDT2                   1      6  0
     D  XD                     1      2  0
     D  XM                     3      4  0
     D  XÅ                     5      6  0
     D                 DS
     D  WDT                    1      8  0
     D  WÅ                     1      4  0
     D  WM                     5      6  0
     D  WD                     7      8  0
      *
     C                   EXSR      INIT
      *
      *__________________________________________
      * SETLL, READ AND DELETE RECORDS FROM SADHD
      *""""""""""""""""""""""""""""""""""""""""""
     C     SIDT          SETLL     SADHD
     C                   READ      SADHD                                  51
     C  N51SIDT          COMP      WDT                                51
     C                   DOW       *IN51 = *OFF
     C                   IF        ((XAVD = 0) OR (SIAVD = XAVD))
     C                   EXSR      SRA
     C                   EXSR      SRB
     C                   EXSR      SRC
     C                   EXSR      SRD
N01  C                   EXSR      SRE
     C                   WRITE     SADHRO
     C                   DELETE    SADHR
     C                   END
     C                   READ      SADHD                                  51
     C  N51SIDT          COMP      WDT                                51
     C  N51              END
      *
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
      *
      *_________________________________________
      * SETLL, READ AND DELETE RECORDS FROM SADV
      *"""""""""""""""""""""""""""""""""""""""""
     C     SRA           BEGSR
     C     SADHK         SETLL     SADV
     C     SADHK         READE     SADV                                   52
     C                   DOW       *IN52 = *OFF
     C                   WRITE     SADVRO
     C                   DELETE    SADVR
     C     SADHK         READE     SADV                                   52
     C                   END
     C                   ENDSR
      *_________________________________________
      * SETLL, READ AND DELETE RECORDS FROM SADF
      *"""""""""""""""""""""""""""""""""""""""""
     C     SRB           BEGSR
     C     SADHK         SETLL     SADF
     C     SADHK         READE     SADF                                   52
     C                   DOW       *IN52 = *OFF
     C                   WRITE     SADFRO
     C                   DELETE    SADFR
     C     SADHK         READE     SADF                                   52
     C                   END
     C                   ENDSR
      *_________________________________________
      * SETLL, READ AND DELETE RECORDS FROM SADW
      *"""""""""""""""""""""""""""""""""""""""""
     C     SRC           BEGSR
     C     SADHK         SETLL     SADW
     C     SADHK         READE     SADW                                   52
     C                   DOW       *IN52 = *OFF
     C                   WRITE     SADWRO
     C                   DELETE    SADWR
     C     SADHK         READE     SADW                                   52
     C                   END
     C                   ENDSR
      *_____________________________________________________
      * SETLL, READ AND DELETE RECORDS FROM EDIM2/EDIS/TVINF
      *"""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     SRD           BEGSR
     C     SADHK         SETLL     EDIM2
     C     SADHK         READE     EDIM2                                  52
     C                   DOW       *IN52 = *OFF
     C                   IF        M1001 = '929'
     C     MMN           SETLL     TVINF
     C     MMN           READE     TVINF                                  53
     C                   DOW       *IN53 = *OFF
     C                   DELETE    TVINFF
     C     MMN           READE     TVINF                                  53
     C                   END
     C     MSN           CHAIN     EDIS                               53
     C  N53              WRITE     EDISRO
     C  N53              DELETE    EDISR
     C                   WRITE     EDIMRO
     C                   DELETE    EDIMR
     C                   ELSE
     C                   UPDATE    EDIMR
     C                   END
     C     SADHK         READE     EDIM2                                  52
     C                   END
     C                   ENDSR
N01   *___________________________________________
N01   * SETLL, READ AND DELETE RECORDS FROM SADHMD
N01   *"""""""""""""""""""""""""""""""""""""""""""
N01  C     SRE           BEGSR
N01  C     SADHK         CHAIN     SADHMD                             52
N01  C                   IF        *IN52 = *OFF
N01  C                   WRITE     SADHMDRO
N01  C                   DELETE    SADHMDR
N01  C                   END
N01  C                   ENDSR
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     SADHK         KLIST
     C                   KFLD                    SIAVD
     C                   KFLD                    SITDN
      *_______________
      * PARAMETER LIST
      *"""""""""""""""
     C     *ENTRY        PLIST
     C                   PARM                    XAVD
     C     XDT2          PARM                    XDT
      *____________
      * WORK FIELDS
      *""""""""""""
     C     *LIKE         DEFINE    SIAVD         XAVD
     C     *LIKE         DEFINE    XDT2          XDT
      *____________
      * INIT FIELDS
      *""""""""""""
     C                   Z-ADD     XÅ            WÅ
     C                   Z-ADD     XM            WM
     C                   Z-ADD     XD            WD
     C                   IF        XÅ > 50
     C                   MOVEL     '19'          WÅ
     C                   ELSE
     C                   MOVEL     '20'          WÅ
     C                   ENDIF
      *
     C                   ENDSR
      *
