
      **************************************************************************
      *
      * Merking for versjoner FØR v4.0 er fjernet
      *
      * Endring i v4.0: Alle pakkede felt er gjort sonet
      *
      **************************************************************************

     A                                      REF(IBMFRF)
     A          R WOTRAR                     TEXT('Ordretranser')
     A            TRANS     R               TEXT('Transtype, 1=ordre, +
     A                                      3=lager')
     A                                      COLHDG('Trans-' 'type')
     A            FIRMA     R
     A            KUNDNR    R
     A            KUNREF    R
     A            ORDTYP    R
     A            DIRUTS    R
     A            STYPE     R
     A            VARENR    R
     A            LAGNR     R
     A            LEVAAR    R
     A            LEVMND    R
     A            LEVDAG    R
     A            BESTIL    R     S
     A            PRIS      R     S
     A            BELOP     R     S
     A            VRAB1     R     S         REFFLD(KRRABA)
     A                                      TEXT('Varerabatt 1')
     A                                      COLHDG('Varerab. 1')
     A            VRAB2     R     S         REFFLD(KRRABA)
     A                                      TEXT('Varerabatt 2')
     A                                      COLHDG('Varerab. 2')
     A            VRAB3     R     S         REFFLD(KRRABA)
     A                                      TEXT('Varerabatt 3')
     A                                      COLHDG('Varerab. 3')
     A            LPLASS    R
     A            PFAKOD    R
     A            ENHET     R
     A            VARTE1    R               TEXT('Varetekst 1/Kundenavn')
     A            VARTE2    R               TEXT('Varetekst 2/Adresse 1')
     A            MERK      R               TEXT('Merking/Adresse 2')
     A            OTRSGT    R     S         REFFLD(SISSGT)
     A            SELGER    R
     A            VADRNR    R     S
     A            LEDI10    R
     A            BRKDOT    R               REFFLD(AGIO)
     A                                      TEXT('Bruddkode ordre')
     A                                      COLHDG('Br.kode' 'ordre')
     A            KSTEOT    R               REFFLD(KSTED)
     A            KBAROT    R               REFFLD(KBARER)
     A            NIV1OT    R               REFFLD(NIV1)
     A            NIV2OT    R               REFFLD(NIV2)
     A            VRA4OT    R     S         REFFLD(KRRABA)
     A                                      TEXT('Varerabatt 4')
     A                                      COLHDG('Varerab. 4')
     A            VRA5OT    R     S         REFFLD(KRRABA)
     A                                      TEXT('Varerabatt 5')
     A                                      COLHDG('Varerab. 5')
     A            VRA6OT    R     S         REFFLD(KRRABA)
     A                                      TEXT('Varerabatt 6')
     A                                      COLHDG('Varerab. 6')
     A            RTYPOT    R               REFFLD(RTYPDO)
     A            RTY2OT    R               REFFLD(RTYPDO)
     A            RTY3OT    R               REFFLD(RTYPDO)
     A            RTY4OT    R               REFFLD(RTYPDO)
     A            RTY5OT    R               REFFLD(RTYPDO)
     A            RTY6OT    R               REFFLD(RTYPDO)
     A            ORDNOT    R     S         REFFLD(ORDNR)
     A            FAKAOT    R               REFFLD(FAKAAR)
     A            FAKMOT    R               REFFLD(FAKMND)
     A            FAKDOT    R               REFFLD(FAKDAG)
     A            PERAOT    R               REFFLD(PERAAR)
     A            PERNOT    R     S         REFFLD(PERNR)
     A            FFAAOT    R               REFFLD(FFDAAR)
     A            FFMMOT    R               REFFLD(FFDMND)
     A            FFDDOT    R               REFFLD(FFDDAG)
     A            KOSTOT    R     S         REFFLD(KOSTPR)
     A            DIRLOT    R               REFFLD(DIRLEV)
     A            POSTOT    R               REFFLD(POSTAD)
     A            ADR3OT    R               REFFLD(ADR3)
     A            VADKOT    R               REFFLD(VADKOD)
     A            LEVMOT    R               REFFLD(LEVMAT)

     A            VNTYOT    R               REFFLD(TRANS)
     A                                      COLHDG('Type varenummer' +
     A                                             '(_/L/K/E/S)')
      *                                     - Blank = "Som før",strekkode eller
      *                                     - L     = Leverandørens varenummer
      *                                     - K     = Eksternt varenummer (SE EV
      *                                     - E     = Strekkode
      *                                     - S     = Lev. strekkode
     A            EVKDOT    R               REFFLD(EKVNKD)
      *                                     - Blank = i hht. kunde
     A            BESNOT    R     S         REFFLD(BESTNR)
     A            LIN1OT    R     S         REFFLD(LNRORD)
     A                                      COLHDG('Linjenummer' +
     A                                             'ordre/best./etc')
     A            LIN2OT    R     S         REFFLD(RESLNR)
     A                                      COLHDG('Linjenummer 2' +
     A                                             'restordrelinje/etc.')
     A            BEHKOT    R               REFFLD(TRANS)
     A                                      COLHDG('Kode for oppdatert' +
     A                                             'lagerbeholdning' ' (_/J)')
      *                                     - Blank = Lagerbeholdning ikke oppda
      *                                     - J     = Lagerbeholdning oppdatert
     A            TLAGOT    R               REFFLD(LAGNR)
     A                                      COLHDG('Til-lager')
     A            TPLAOT    R               REFFLD(LPLASS)
     A                                      COLHDG('Til-lokasjon')
     A            PLUTOT    R               REFFLD(ORDTYP)
     A                                      COLHDG('Plukkes til')
     A            PROSOT    R               REFFLD(PROSNR)
     A            BETBOT    R               REFFLD(BETBET)
     A            ORDROT    R     S         REFFLD(ORDRAB)
      *****************************************************************
     A            INDVOT    R               REFFLD(INDVN1)
     A            USEROT    R               REFFLD(USER)
     A            VALKOT    R               REFFLD(VALKOD)
     A            FORSOT    R               REFFLD(KRNR)
     A            TDATOT    R               REFFLD(DATO8)
     A                                      TEXT('Transaksjonsdato')
     A                                      COLHDG('Trans.' 'dato')
     A            TTIDOT    R     S         REFFLD(PTID)
     A                                      TEXT('Transaksjonstidsp.')
     A                                      COLHDG('Trans.' 'tidsp.')
     A            VARROT    R               REFFLD(VARREF)
     A                                      TEXT('Vår referanse')
     A                                      COLHDG('Vår' 'ref.')
     A            HVAROT    R               REFFLD(VARENR)
     A                                      TEXT('Hovedvare struktur')
     A                                      COLHDG('H.vare' 'struktur')
     A            PRECOT         9S 0       COLHDG('Fysisk recordnummer')
     A            PFILOT        10          COLHDG('Fysisk fil' '(se PRECOT)')
     A            UPLAOT         1          COLHDG('Oppdatere' 'lager')
      *                                      - Blank = Ja
      *                                      - N     = Nei
     A            ORDAOT    R               REFFLD(ORDAAR)
     A            ORDMOT    R               REFFLD(ORDMND)
     A            ORDDOT    R               REFFLD(ORDDAG)
     A            VALAOT    R               REFFLD(VALAAR)
     A                                      TEXT('År valuteringsdato')
     A                                      COLHDG('År' 'val.')
     A            VALMOT    R               REFFLD(VALMND)
     A                                      TEXT('Mnd valuteringsdato')
     A                                      COLHDG('Mnd' 'val.')
     A            VALDOT    R               REFFLD(VALDAG)
     A                                      TEXT('Dag valuteringsdato')
     A                                      COLHDG('Dag' 'val.')
     A            EANROT    R     S         REFFLD(EANNKU)
     A            KOFAKT    R               TEXT('Kode k.faktura/mottak/utlån')
     A                                      COLHDG('Kont.' 'faktura')
     A                                      VALUES(' ' 'J' 'M' 'S' 'U')
     A            REFTOT    R               REFFLD(ORDTYP)
     A                                      COLHDG('Dokumenttype,' 'referanse' +
     A                                             'for kopiering')
     A            REFAOT    R     S         REFFLD(ORDAAR)
     A                                      COLHDG('Dokumentår,' 'referanse' +
     A                                             'for kopiering')
     A            REFNOT    R     S         REFFLD(ORDNR)
     A                                      COLHDG('Dokumentnummer,' +
     A                                             'referanse' +
     A                                             'for kopiering')
     A            REFLOT    R     S         REFFLD(LNRORD)
     A                                      COLHDG('Linje,' 'referanse' +
     A                                             'for kopiering')
     A            REFROT    R     S         REFFLD(RESLNR)
     A                                      COLHDG('Restordrelinje' +
     A                                             'referanse' +
     A                                             'for kopiering')
     A            LBA1OT        15
     A                                      TEXT('Lastbærerid 1')
     A            LOTNOT        20
     A            REKDOT         1
