********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 YBC PTF10 FELT FOR KRAV PÅ ANDREKOSTNAD I SAD-EKSPORT
********************************************************************
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HELP(01)
     A                                      CF01(01)
      *
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  5  2VALUES(' ' '2' '4')
     A            KLBKOD         3A  O  5  7TEXT('LEVERINGSBETINGELSE')
     A            KLBKT          3A  O  5 16TEXT('KODE TEKST')
     A            KLBNAV        15A  O  5 22TEXT('BESKRIVELSE AV KODE')
S01  A*           KLBFOK         1A  O  5 45TEXT('FORSIKRINGSKODE')
N01  A            KLBFOK         1A  O  5 42TEXT('FORSIKRINGSKODE')
S01  A*           KLBPRM         3Y 1O  5 56TEXT('PROMILLE')
N01  A            KLBPRM         3Y 1O  5 50TEXT('PROMILLE')
     A                                      EDTCDE(4)
S01  A*           KLBFRK         1A  O  5 66TEXT('FRAKTKODE')
N01  A            KLBFRK         1A  O  5 60TEXT('FRAKTKODE')
N01  A            KLBANK         1A  O  5 68TEXT('FRAKTKODE')
     A            KLBHAD         1   O  5 74
     A            KLBHAE         1   O  5 77
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0017)
     A                                      SFLPAG(0016)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A            ZRECNO         4S 0O  2 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYSPED/SAD012'
     A                                      COLOR(BLU)
     A                                  1 20MSGCON(45 DIV1366 SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 68DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  2  1MSGCON(80 DIV1367 SYMSGF)
     A                                      COLOR(BLU)
     A                                  3  2MSGCON(40 DIV1360 SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 73'IMP/EXP'
     A                                      COLOR(WHT)
     A                                  4  2MSGCON(78 DIV1365 SYMSGF)
     A                                      COLOR(WHT)
      *
     A          R BUNN
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF03(03 'AVSLUTT')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SOEK')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  3  2MSGCON(18 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 22MSGCON(18 DIV0021 SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 42MSGCON(18 DIV0022 SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 62MSGCON(18 DIV0023 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  4  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R VARELI
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSG301        75A  O  1  5MSGID(DIV 1368 SYMSGF)
     A                                      COLOR(WHT)
     A            WLBKOD         3A  B  2  7TEXT('LEVERINGSBETINGELSE')
     A                                      DSPATR(UL)
     A  40                                  DSPATR(HI)
     A  40                                  DSPATR(PR)
     A  89                                  DSPATR(RI)
     A  40                                  COLOR(WHT)
     A            WLBKT          3A  B  2 16TEXT('KODE TEKST')
     A                                      DSPATR(UL)
     A  88                                  DSPATR(RI)
     A            WLBNAV        15A  B  2 22TEXT('BESKRIVELSE AV KODE')
     A                                      DSPATR(UL)
S01  A*           WLBFOK         1A  B  2 45TEXT('FORSIKRINGSKODE')
N01  A            WLBFOK         1A  B  2 42TEXT('FORSIKRINGSKODE')
     A                                      DSPATR(UL)
     A  87                                  DSPATR(RI)
     A                                      VALUES(' ' 'A' 'N' 'F')
S01  A*           WLBPRM         3Y 1B  2 56TEXT('PROMILLE')
N01  A            WLBPRM         3Y 1B  2 50TEXT('PROMILLE')
     A                                      DSPATR(UL)
     A                                      EDTCDE(4)
S01  A*           WLBFRK         1A  B  2 66TEXT('FRAKTKODE')
N01  A            WLBFRK         1A  B  2 60TEXT('FRAKTKODE')
     A                                      DSPATR(UL)
     A  86                                  DSPATR(RI)
     A                                      VALUES(' ' 'J' 'N' 'F')
N01  A            WLBANK         1A  B  2 68TEXT('ANDREKOSTNAD')
N01  A                                      DSPATR(UL)
N01  A                                      VALUES(' ' 'J')
     A            WLBHAD         1A  B  2 74DSPATR(UL)
     A                                      VALUES(' ' 'A' 'M')
     A            WLBHAE         1A  B  2 77DSPATR(UL)
     A                                      VALUES(' ' 'A' 'M')
     A                                  3  2MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  4  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R VAREL3
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            MSG401        75A  O  1  5MSGID(DIV1368 SYMSGF)
     A                                      COLOR(WHT)
     A            KLBKOD         3A  O  2  7TEXT('LEVERINGSBETINGELSE')
     A                                      DSPATR(UL)
     A                                      DSPATR(PC)
     A                                      COLOR(WHT)
     A            KLBKT          3A  O  2 16TEXT('KODE TEKST')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            KLBNAV        15A  O  2 22TEXT('BESKRIVELSE AV KODE')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
S01  A*           KLBFOK         1A  O  2 45TEXT('FORSIKRINGSKODE')
N01  A            KLBFOK         1A  O  2 42TEXT('FORSIKRINGSKODE')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
S01  A*           KLBPRM         3Y 1O  2 56TEXT('PROMILLE')
N01  A            KLBPRM         3Y 1O  2 50TEXT('PROMILLE')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
S01  A*           KLBFRK         1A  O  2 66TEXT('FRAKTKODE')
N01  A            KLBFRK         1A  O  2 60TEXT('FRAKTKODE')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
N01  A            KLBANK         1A  O  2 68TEXT('ANDREKOSTNAD')
N01  A                                      DSPATR(UL)
N01  A                                      COLOR(WHT)
     A            KLBHAD         1A  O  2 74DSPATR(UL)
     A                                      COLOR(WHT)
     A            KLBHAE         1A  O  2 77DSPATR(UL)
     A                                      COLOR(WHT)
     A            MSG402        18A  O  3  2MSGID(DIV0024 SYMSGF)
     A                                      COLOR(BLU)
      *
     A          R NYWRK
     A                                      CF03(03 'AVSLUTT')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SOEK')
     A                                  1  2'SYSPED/SAD012'
     A                                      COLOR(BLU)
     A                                  1 20MSGCON(45 DIV1366 SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 68DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  2  1MSGCON(80 DIV1367 SYMSGF)
     A                                      COLOR(BLU)
     A                                  3  2MSGCON(40 DIV1360 SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 73'IMP/EXP'
     A                                      COLOR(WHT)
     A                                  4  2MSGCON(78 DIV1365 SYMSGF)
     A                                      COLOR(WHT)
     A                                 23  2MSGCON(18 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(18 DIV0021 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 42MSGCON(18 DIV0022 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 62MSGCON(18 DIV0023 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R SOKLB
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A                                  2  2MSGCON(32 DIV1369 SYMSGF)
     A            W2KOD          3A  I  2 35DSPATR(UL)
     A                                      DSPATR(PC)
     A                                  3  2MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
