     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      CF03(03 'Avslutt program')
     A                                      CF12(12 'Annuller')
      *
     A          R SLPROSA
     A                                      OVERLAY
     A                                  1  3'SYSPED/SLPROS'
     A                                      COLOR(BLU)
     A                                  1 30'Merke prosjekter for sletting'
     A                                      COLOR(WHT)
     A                                  1 69DATE
     A                                      EDTCDE(Y)
     A                                  2 69TIME
     A                                  7  2'Fyll ut fra/til prosjektnummer....'
     A                                 10  6'Fra og med prosjektnummer. . . . :'
     A            ZPROSF         8D 0B 10 43CHECK(RB)
     A                                      DSPATR(PC)
     A  31                                  DSPATR(HI)
     A  34                                  ERRMSG('Du må gi ENTEN fra/til pros-
     A                                      jnr eller til dato' 34)
     A  31                                  ERRMSG('Fra prosjnr må oppgis (når -
     A                                      fra/til prosj.benyttes)' 31)
     A                                 12  6'Til og med prosjektnummer. . . . :'
     A            ZPROST         8D 0B 12 43CHECK(RB)
     A                                      DSPATR(PC)
     A  32                                  DSPATR(HI)
     A  32                                  ERRMSG('Til prosjektnr må være høye-
     A                                      re enn fra prosjektnr' 34)
     A                                 15  2'eller...'
     A                                 17  6'Til og med dato. . . . . . . . . :'
     A                                 17 60'(DDMMÅÅ)'
     A            ZDATO          6D 0B 17 43CHECK(RB)
     A                                      DSPATR(PC)
     A  33                                  DSPATR(HI)
     A  33                                  ERRMSG('Dato må være på formen  dag-
     A                                       måned år' 33)
     A  50        ZANT           9Y 0O 20  6COLOR(WHT)
     A                                      EDTCDE(4)
     A  50                             20 16'prosjekt ble merket for sletting.'
     A                                      COLOR(WHT)
     A  50        ZANTTO         9Y 0O 21  6COLOR(WHT)
     A                                      EDTCDE(4)
     A  50                             21 16'prosjekt er merket hittil.'
     A                                      COLOR(WHT)
     A                                 23  2'F3=Gå ut/slett merkede prosjekt'
     A                                      COLOR(BLU)
     A N51                             23 40'F12=Avbryt'
     A N51                                  COLOR(BLU)
