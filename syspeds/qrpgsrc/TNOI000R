      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/TNOI000R MODULE(*LIBL/TNOI000R *LIBL/INCGI)
CRTPGM*        ACTGRP(CGI) AUT(*USE)
      *
      *********************************************************************
      /copy SYSPEDS/qrpgsrcpy,hspecs
      /copy SYSPEDS/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FBRPARF    IF   E           K DISK    USROPN
     FFRISOK    IF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy SYSPEDS/qrpgsrcpy,prototypeb
      /copy SYSPEDS/qrpgsrcpy,usec
      /copy SYSPEDS/qrpgsrcpy,variables3
     ‚*  Prototype for 'JSONFIX'
     d jsonfix         pr                  extpgm('JSONFIX')
     d jsonstring_                         like(jsonstring)
     ‚*  Prototype for 'INCGI'
     d incgi           pr                  extproc('INCGI')
     d bistdl_                             like(bistdl)
     ‚*  Prototype for variabel program
     D Varpgm          Pr                  ExtPgm(BILIBL)
     ‚*  Prototype for 'SYGE20'
     d syge20          pr                  extpgm('SYGE20')
     d ufunk_                              like(ufunk)
     d udat1_                              like(udat1)
     d udat2_                              like(udat2)
     d udgr_                               like(udgr)
     d WhereC          s            512a
     d FirstW          s              1A
     d cQuote          c                   const('''')
     d SqlStm          s            512a
     d myrows          s             10i 0
     D WSUSER          S             10
     D AVD             S              4
     D AVDN            S              4  0
     D SG              S              3
     D OPD             S              7
     D OPDN            S              7S 0
     D REFNR           S             18
     D DATUM           S              8
     D DATUMN          S              8S 0
     D DATUMT          S              8
     D DATUMTN         S              8S 0
     D STATUS          S              1
     D AVSNAVN         S             35
     D MOTNAVN         S             35
     D LOPENR          S             10
     D AART            S              2
     D GODSNR          S             15
     D XREF            S             35
     D INNSTIKK        S              1
     D JSONstring      s          32000
     D Error           S            150
     D AntLest         S              9  0
     D Jn043           S              1
     d X               S             10  0
     d hopp            S              1
     d wspll           S              1
     d STOPP5          S             35
     d pos             S              5U 0
     d udat1           s              8  0
     d udat2           s              8  0
     d udgr            s              5  0
     d ufunk           s              3
     d wpabrid         s                   like(pabrid)
     d wpakunr         s                   like(pakunr)
     d wpasort         s                   like(pasort)
     d MyPoDS          ds                  Qualified  dim(500)
     d  SIAVD                         4S 0
     d  SITDN                         7S 0
     d  SI0035                        1
     d  SITLL                        10
     d  SITLE                         6
     d  SIDTY                         2
     d  SISG                          3
     d  SIDT                          8S 0
     d  SIVKB                         9  0
     d  SIST                          1
     d  SINAS                        30
     d  SINAK                        30
     d  SIGN                         15
     d  SIMI                          1
      *get input-string
      /copy syspeds/qrpgsrcpy,prolog3

      /free
         exsr ParseSr;
         exsr init;

       gethtmlifs(
       '/syspeddev/html/JSON.html':
       '/CGITAG/');
       wrtsection('top');

       //...........................................................................................
       //skriv JSON-Object start (alltid '{' + x ant param. m/komma mellom (IKKE komma etter siste))
       //...........................................................................................

        JSONstring='{'
        +'¬user¬ : ¬'+%trim(WSUSER)+'¬, '
        +'¬avd¬ : ¬'+%trim(%editc(AVDN:'Z'))+'¬, '
        +'¬sg¬ : ¬'+%trim(SG)+'¬, '
        +'¬opd¬ : ¬'+%trim(%editc(OPDN:'Z'))+'¬, '
        +'¬refnr¬ : ¬'+%trim(REFNR)+'¬, '
        +'¬datum¬ : ¬'+%trim(%editc(DATUMN:'Z'))+'¬, '
        +'¬datumy¬ : ¬'+%trim(%editc(DATUMTN:'Z'))+'¬, '
        +'¬status¬ : ¬'+%trim(STATUS)+'¬, '
        +'¬avsNavn¬ : ¬'+%trim(AVSNAVN)+'¬, '
        +'¬motNavn¬ : ¬'+%trim(MOTNAVN)+'¬, '
        +'¬lopenr¬ : ¬'+%trim(LOPENR)+'¬, '
        +'¬aart¬ : ¬'+%trim(AART)+'¬, '
        +'¬godsnr¬ : ¬'+%trim(GODSNR)+'¬, '
        +'¬xref¬ : ¬'+%trim(XREF)+'¬, '
        +'¬innstikk¬ : ¬'+%trim(INNSTIKK)+'¬, '
        +'¬errMsg¬ : ¬'+%trim(Error)+'¬, ';

       //JSONfix escaper " i tekst,  for deretter å bytte ¬->"

       jsonfix ( jsonstring );

       updhtmlvar2('JSONstring'
                  :%addr(jsonstring)
                  :%len(jsonstring));
       wrtsection('JSONlin');

       //...........................................................................................
       // Skriv JSON-LISTE Start (en liste er EN parameter(fra [ til   )
       //..........................................................................................
       jsonstring ='"orderList" : [';

       updhtmlvar2('JSONstring'
                  :%addr(jsonstring)
                  :%len(jsonstring));
       wrtsection('JSONlin');

       if error = *blanks;
         exsr SelSr;
         if myrows > 0;
           exsr LesSr;
         endif;
       endif;
       // ..........................................................................................
       // Skriv JSON-Liste/Array  Avslutning
       // ..........................................................................................
       jsonstring =']';
       updhtmlvar2('JSONstring'
                  :%addr(jsonstring)
                  :%len(jsonstring));
       wrtsection('JSONlin');
       // ..........................................................................................
       // Skriv JSON-string Avsluttning
       // ..........................................................................................
       jsonstring ='}';
       updhtmlvar2('JSONstring'
                  :%addr(jsonstring)
                  :%len(jsonstring));
       wrtsection('JSONlin');
         exsr Exit;

       //-----------------------------------------------------------------
       // Select fra fil via SQL
       //-----------------------------------------------------------------
       begsr     SelSr;

       If    AVDN > 0;
         AVD = %editc(AVDN:'X');
         Exsr  AddAnd;
         Eval  WhereC = ' SIAVD  = ' + AVD;
       EndIf;

       If    SG  <> *blanks;
         Exsr  AddAnd;
         Eval  WhereC =  %trim(WhereC)  +
         ' SISG = ' + cQuote + SG + cQuote +' ';
       EndIf;

       If    OPDN > 0;
         OPD = %editc(OPDN:'X');
         Exsr  AddAnd;
         Eval  WhereC =  %trim(WhereC)  +
         ' SITDN  = ' + OPD;
       EndIf;

       If    DATUMN > 0;
         DATUM = %editc(DATUMN:'X');
         Exsr  AddAnd;
         Eval  WhereC =  %trim(WhereC)  +
         ' SIDT  >= ' + DATUM;
       EndIf;

       If    DATUMTN > 0;
         DATUMT = %editc(DATUMTN:'X');
         Exsr  AddAnd;
         Eval  WhereC =  %trim(WhereC)  +
         ' SIDT  <= ' + DATUMT;
       EndIf;

       If    STATUS  <> *blanks and STATUS <> 'b';
         Exsr  AddAnd;
         Eval  WhereC =  %trim(WhereC)  +
         ' SIST = ' + cQuote + STATUS + cQuote;
        EndIf;

       If    AVSNAVN  <> *blanks;
         Exsr  AddAnd;
         Eval  WhereC =  %trim(WhereC)  +
         ' UPPER(SINAS) LIKE UPPER(' + cQuote + '%' +
         %trim(AVSNAVN) + '%' + cQuote + ')';
        EndIf;

       If    MOTNAVN  <> *blanks;
         Exsr  AddAnd;
         Eval  WhereC =  %trim(WhereC)  +
         ' UPPER(SINAK) LIKE UPPER(' + cQuote + '%' +
         %trim(MOTNAVN) + '%' + cQuote + ')';
        EndIf;

       If    LOPENR  <> *blanks;
         Exsr  AddAnd;
         Eval  WhereC =  %trim(WhereC)  +
         ' SITLL LIKE ' + cQuote + '%' + %trim(LOPENR) + '%' + cQuote;
        EndIf;

       If    GODSNR  <> *blanks;
         Exsr  AddAnd;
         Eval  WhereC =  %trim(WhereC)  +
         ' SIGN LIKE ' + cQuote + '%' + %trim(GODSNR) + '%' + cQuote;
        EndIf;

       If    AART  <> *blanks;
         Exsr  AddAnd;
         Eval  WhereC =  %trim(WhereC)  +
         'SIDTY = ' + cQuote + AART + cQuote +' ';
       EndIf;

       If    INNSTIKK  <> *blanks;
         Exsr  AddAnd;
         Eval  WhereC =  %trim(WhereC)  +
         'SIMI = ' + cQuote + INNSTIKK + cQuote +' ';
       EndIf;

         Eval      WhereC =  ' Where ' + %trim(WhereC);

         Eval       SqlStm = 'Select SIAVD, SITDN, SI0035, SITLL, ' +
                             'SITLE, SIDTY, SISG, SIDT, SIVKB, ' +
                             'SIST, SINAS, SINAK, SIGN, SIMI ' +
                             'from SADH ' +
                              %trim(WhereC)  +
                             ' Order by '       +
                             'SIAVD, SISG, SIDT desc ' +
                             'FOR READ ONLY ';
         Exec SQL PREPARE DynSel from :SqlStm;
         EXEC SQL DECLARE  A33  cursor for DynSel;
         Exec SQL OPEN A33;
         Myrows = %elem(MyPoDS);
         exec sql FETCH A33   for :MyRows rows
                  INTO :MyPoDs;
       endsr;
       //-----------------------------------------------------------------
       // Set inn "AND" mellom where settninger
       //-----------------------------------------------------------------
       begsr     AddAnd;
       If        FirstW =   'N';
         Eval      WhereC =  %trim(WhereC) + ' and ';
       EndIf;
         Eval      FirstW =   'N';
       endsr;
       //-----------------------------------------------------------------
       // hent input
       //-----------------------------------------------------------------
       begsr     ParseSr;
         wsuser  = zhbgetvar('USER');
         avd     = zhbgetvar('AVD');
         avdn    = c2n2(avd);
         sg      = zhbgetvar('SG');
         opd     = zhbgetvar('OPD');
         opdn    = c2n2(opd);
         refnr   = zhbgetvar('REFNR');
         datum   = zhbgetvar('DATUM');
         datumn  = c2n2(datum);
         datumt  = zhbgetvar('DATUMT');
         datumtn = c2n2(datumt);
         status  = zhbgetvar('STATUS');
         avsnavn = zhbgetvar('AVSNAVN');
         motnavn = zhbgetvar('MOTNAVN');
         lopenr  = zhbgetvar('LOPENR');
         aart    = zhbgetvar('AART');
         godsnr  = zhbgetvar('GODSNR');
         xref    = zhbgetvar('XREF');
         innstikk= zhbgetvar('INNSTIKK');


        endsr;
        // __________________________________________________
        // Læs array                                    LESSR
        // """"""""""""""""""""""""""""""""""""""""""""""""""
           begsr LesSr;
       for x = 1 to myrows;

       if mypods(x).sisg <> *blanks;

           chain (mypods(x).SIAVD: mypods(x).SITDN: '*CR') FRISOK;
           if not %found;
              fssok = *blanks;
           endif;

        // Valider extern ref
           error=' ';
           if xref <> *blanks;
             pos = %scan (XREF : FSSOK);
               if pos = 0;
                 error='X';
               endif;
           endif;

           if error = *blanks;

        // Lægg ut Json stræng
           AntLest = AntLest + 1;

        // Skriv en JSON-Array-linje pr menypunkt - komma før hvert nytt element

       if AntLest=1;
          JSONstring=*blanks;
          else;
          JSONstring=', ';
          endif;
       JSONstring=%trim(JSONstring)+'{'
          +'¬avd¬ : ¬'+%trim(%editc(mypods(x).SIAVD:'Z'))+'¬, '
          +'¬opd¬ : ¬'+%trim(%editc(mypods(x).SITDN:'Z'))+'¬, '
          +'¬tst¬ : ¬'+%trim(mypods(x).SI0035)+'¬, '
          +'¬sitll¬ : ¬'+%trim(mypods(x).SITLL)+'¬, '
          +'¬sitle¬ : ¬'+%trim(mypods(x).SITLE)+'¬, '
          +'¬aart¬ : ¬'+%trim(mypods(x).SIDTY)+'¬, '
          +'¬sg¬ : ¬'+%trim(mypods(x).SISG)+'¬, '
          +'¬datum¬ : ¬'+%trim(%editc(mypods(x).SIDT:'Z'))+'¬, '
          +'¬sivkb¬ : ¬'+%trim(%editc(mypods(x).SIVKB:'4'))+'¬, '
          +'¬status¬ : ¬'+%trim(mypods(x).SIST)+'¬, '
          +'¬avsNavn¬ : ¬'+%trim(mypods(x).SINAS)+'¬, '
          +'¬motNavn¬ : ¬'+%trim(mypods(x).SINAK)+'¬, '
          +'¬sign¬ : ¬'+%trim(mypods(x).SIGN)+'¬, '
          +'¬simi¬ : ¬'+%trim(mypods(x).SIMI)+'¬, '
          +'¬h_xref¬ : ¬'+%trim(FSSOK)+'¬}';
         jsonfix (JSONstring);
       updhtmlvar2('JSONstring'
                  :%addr(jsonstring)
                  :%len(jsonstring));
         wrtsection ('JSONlin');

        endif;
        endif;

        endfor;

       endsr;
       //********************************************************
       begsr init;
       //********************************************************
         open bridf;
         // Test innlogging
         chain wsuser bridf;

         if %found;
           if bilibl = *blanks;
             incgi ( bistdl );
           else;

             Monitor;
                   Varpgm();
                 On-Error;
                   //
                 EndMon;

           endif;
         else;

           error = 'Invalid userID';
         endif;
         open brparf;
         open frisok;
         // Er dato= blank eller 0, sett  da dato 7 dager tilbake
         if opdn=*zeros and refnr=*blanks and datumn=*zeros;

           wpabrid=wsuser;
           wpakunr=0;
           wpasort=0;
           chain (WPABRID: WPAKUNR: 'DFTDG': WPASORT) BRPARF;
           if not %found or pavrdn = 0;
              wpabrid='*KUNDFT*' + BIFIRM;
              chain (WPABRID: WPAKUNR: 'DFTDG': WPASORT) BRPARF;
              if not %found or pavrdn = 0;
                pavrdn=7;
              endif;
           endif;

           ufunk = 'SUB';
           udat1 = datumn;
           udgr = pavrdn;
           syge20 ( ufunk : udat1 : udat2 : udgr );
           datumn=udat2;
         endif;

       endsr;
       //=====================================================================
       // Close output html and quit
       //=====================================================================
       begsr exit;
         // Lukk fil
         close bridf;
         close brparf;
         close frisok;

         wrtsection('*fini');
         *inlr = *on;
         return;
       endsr;
      /end-free
