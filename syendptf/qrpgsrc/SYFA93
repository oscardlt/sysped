      ***********************************************************
      *** PROGRAM FOR DELETION OF AN ORDER                    ***
      *** INDICATORER 01 - 26                                 ***
      *** FREE   INDIKATORER 01 02 04-26                      ***
      *** FREE   INDIKATORER 27-29 31 32 34-96                ***
      ***********************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 JOV PTF10 loggfør slettemerking i T&T
      *%%%%%%%%%%%%%%%%%%%%%
10XXX * 02 JOV PTF12 ved fly eksport - sett '    00000000000' i Godsnr
********************************************************************
     FTRACKT    UF   E           K DISK
N01  FTRACKF    O  A E           K DISK
N01  FFIRM      IF   E           K DISK
     FDOKUF     UF   E           K DISK
     FDOKUFV    UF   E           K DISK
     FDOKUDGS   UF   E           K DISK
     FDOKUI     UF   E           K DISK
     FDOKUT     UF   E           K DISK
     FDOKEB     UF   E           K DISK
     FDOKEC     UF   E           K DISK
     FDOKECE    UF   E           K DISK
     FDOKECEL   UF   E           K DISK
     FDOKEF     UF   E           K DISK
     FHEADF     UF   E           K DISK
     FFAK4      UF   E           K DISK
     FSAML1     UF   E           K DISK
     FFREETXT1  UF A E           K DISK
     FKOSBU9    UF   E           K DISK
     FSYFA93FM  CF   E             WORKSTN
      *___________________________
      * Finner username
      *"""""""""""""""""""""""""""
     D                SDS
     D  ZJOB                 244    253
     D  ZUSER                254    263
     D  ZJOBNR               264    269  0
     D                 DS
     D  DELTXT                 1     30
     D  DELUSR                31     40
     D  DELDAT                42     49  0
     D  DELDÅR                42     45  0
     D  DELDMN                46     47  0
     D  DELDDG                48     49  0
     D  DELDS                  1     49
     D                 DS
     D* HERFA                  1     15
     D  HERFA                  1     35
     D  YSD                    1      3
     D  XLNR1                  4      4
     D  XLNR2                  5      5
     D  XLNR3                  6      6
     D  XLNR                   4      6
N01  D                 DS
N01  D  XULTID                 1     14  0
N01  D  XULTM                  1      6  0
N01  D  XULDD                  7      8  0
N01  D  XULMM                  9     10  0
N01  D  XULÅÅ                 11     14  0
N01  D                 DS
N01  D  ULÅÅ                   1      4  0
N01  D  ULMM                   5      6  0
N01  D  ULDD                   7      8  0
N01  D  ULDT                   1      8  0
     D                UDS
     D  UFIRM                  1      2
     C*
     C                   EXSR      INIT
     C*
     C     DIREKT        CABEQ     'J'           OVER
     C     START         TAG
      * KOMMER JEG HIT VED DIREKT ER ENTEN OPPDRAG SLETTET
      * ELLER NOE FEIL.  RETURNERER MED INFO I VAR SLETTE
     C     DIREKT        CABEQ     'J'           SLUTT
     C                   EXFMT     BILDE
     C                   SETOFF                                       30
     C                   SETOFF                                       999897
     C   03              GOTO      SLUTT
     C     OVER          TAG
     C                   MOVE      'DIV0000'     MSGNR
     C     HEADFK        CHAIN(N)  HEADF                              99
     C   99              SETON                                        30
     C   99              MOVE      'SPO0002'     MSGNR
     C   99              GOTO      START
      *
     C     HEST          IFNE      ' '
     C     HEST          ANDNE     'O'
     C                   SETON                                        3097
     C                   MOVE      'SPO0004'     MSGNR
     C                   GOTO      START
     C                   END
      *
     C     HEUR          IFEQ      'H'
     C                   SETON                                        3097
     C                   MOVE      'JOV1082'     MSGNR
     C                   GOTO      START
     C                   END
N01  C     WSTXT         IFEQ      *BLANKS
N01  C                   SETON                                        3098
N01  C                   MOVE      'JOV1610'     MSGNR
N01  C                   GOTO      START
N01  C                   END
      * SJEKK MOT FAKTURALINJER
     C                   CALL      'SYFA93B'
     C                   PARM                    HEAVD
     C                   PARM                    HEOPD
     C                   PARM                    SLETTOK           1
     C     SLETTOK       IFEQ      'N'
     C                   GOTO      START
     C                   END
     C*
     C                   Z-ADD     1             UFLAGG            1 0
     C                   CALL      'SYFM21A'
     C                   PARM                    UFIRM
     C                   PARM                    HEAVD
     C                   PARM                    HEOPD
     C                   PARM                    UFLAGG
     C     UFLAGG        IFEQ      5
     C                   MOVE      'J'           SLETTE
      * HVIS DET VAR SLETTING/TILBAKEFØRING HVOR DET FINNES LINJER
      * FIX STATISTIKK MED EN GANG - KUN ANT./VEKT(BEØP FORUTSETTES OK)
     C     HE01          IFEQ      'J'
     C                   MOVE      'N'           UEND              1
     C                   MOVE      'J'           XXOPD             1
     C                   MOVE      'J'           UTREKK            1
     C                   CALL      'SYST35'
     C                   PARM                    WSAVD
     C                   PARM                    WSOPD
     C                   PARM                    FALI
     C                   PARM                    XXOPD
     C                   PARM                    UEND
     C                   PARM                    UTREKK
      * CLOSE FILER/ *LR I SYST35.
     C                   MOVE      'J'           UEND
     C                   MOVE      'N'           UTREKK
     C                   CALL      'SYST35'
     C                   PARM                    WSAVD
     C                   PARM                    WSOPD
     C                   PARM                    FALI
     C                   PARM                    XXOPD
     C                   PARM                    UEND
     C                   PARM                    UTREKK
     C                   END
     C*
     C     HEADFK        CHAIN     HEADF                              33
N01  C  N33FIURBL        IFEQ      'J'
N01  C                   EXSR      TTSRDEL
N01  C                   END
     C     HE01          IFNE      'J'
     C  N33              MOVE      'S'           HEST
     C  N33              MOVE      'SPO0003'     MSGNR
     C                   ELSE
     C  N33              MOVE      ' '           HE01
     C  N33              MOVE      'O'           HEST
     C  N33              MOVE      'JOV0695'     MSGNR
     C                   END
     C  N33              MOVE      *BLANKS       HEGN
     C  N33              MOVEL     '*DELETE*'    HEGN
N02  C  N33              if        HEUR = 'D'
N02  C                   eval      HEGN = '    00000000000'                     11 siste = AWB
N02  C                   endif
     C  N33              MOVE      99999999      HEPRO
     C  N33              UPDATE    HEADFR
     C*
     C* REMOVE ORDERLINES
     C     HEADFK        SETLL     FAK4
     C     HEADFK        READE     FAK4                                   33
     C     *IN33         DOWEQ     '0'
     C                   DELETE    FAKTR
     C     HEADFK        READE     FAK4                                   33
     C                   END
     C*
     C* FJEREN EVT. FOREKOMSTER I SAML
     C     HEADFK        SETLL     SAML1
     C     HEADFK        READE     SAML1                                  33
     C     *IN33         DOWEQ     '0'
     C                   DELETE    SAMLR
     C     HEADFK        READE     SAML1                                  33
     C                   END
     C*
      * Slettemerk kolli ID ( 0 i fbnr = på alle fraktbrev...)
     C                   MOVE      *ZEROS        XXFBNR            3 0
     C                   Call      'SYFI10Z'
     C                   Parm                    WSAVD
     C                   Parm                    WSOPD
     C                   Parm                    XXFBNR
     C* SKRIV USRID/TID TIL NOTISBLOKK
     C                   EXSR      NOTIS
     C*
     C* DELETE OTHER DOKUMENTS
     C*
     C                   EXSR      DELDOK
     C*
     C                   END                                                        UFLAGG
     c                   exsr      slettlink
     C*
     C                   GOTO      START
     C*
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
     C*
     C***********************************************
     C     NOTIS         BEGSR
     C***********************************************
     C* LOGGE I OPPDRAGSNOTIS: SLETTING UTFØRT AV USERID .......
     C                   MOVE      HEAVD         FRTAVD
     C                   MOVE      HEOPD         FRTOPD
     C                   MOVE      *ZEROS        FRTLI
     C     HEADFK        SETLL     FREETXT1
     C     HEADFK        READE(N)  FREETXT1                               33
     C     *IN33         DOWEQ     '0'
     C     HEADFK        READE(N)  FREETXT1                               33
     C                   END
     C                   ADD       1             FRTLI
     C                   MOVE      DELDAT        FRTDT
     C                   MOVE      ' '           FRTKOD
     C                   MOVEL     DELDS         FRTTXT
     C                   MOVE      *ZEROS        FRTFAK
     C                   WRITE     FREETXTR
     C                   ENDSR
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     *ENTRY        PLIST
     C                   PARM                    WSAVD
     C                   PARM                    WSOPD
     C                   PARM                    SLETTE            1
     C     WSAVD         IFNE      0
     C                   MOVE      'J'           DIREKT            1
     C                   END
     C     HEADFK        KLIST
     C                   KFLD                    WSAVD
     C                   KFLD                    WSOPD
     C     KEYFA2        KLIST
     C                   KFLD                    WSAVD
     C                   KFLD                    WSOPD
     C     DOKEBK        KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C     DOKECK        KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C     DOKEFK        KLIST
     C                   KFLD                    YFRI
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C     DOKUFK        KLIST
     C                   KFLD                    YFRI
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C                   KFLD                    YFBNR
     C     DOKUIK        KLIST
     C                   KFLD                    YIRI
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C                   KFLD                    YFBNR
     C     DOKUTK        KLIST
     C                   KFLD                    YTRI
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C                   KFLD                    YFBNR
     C     KOSBUK        KLIST
     C                   KFLD                    BUST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C                   MOVE      ' '           BUST
     C*
     C                   MOVE      'F'           YFRI              1
     C                   MOVE      'M'           YMRI              1
     C                   MOVE      'I'           YIRI              1
     C                   MOVE      'T'           YTRI              1
     C                   MOVE      ' '           YSK               1
     C                   MOVE      *ZEROS        YLNR              3 0
     C                   MOVE      *ZEROS        YFBNR             3 0
      *
     C* SLETTET AV...... TEKST
     C                   MOVE      'JOV0577'     XMSGNR            7            MELDING-ID
     C                   Z-ADD     3             XLEVEL            1 0          NIVÅ
     C                   Z-ADD     0             XRETUR            1 0          RETURKODE
     C                   Z-ADD     30            XMSGL             4 0          LENGDE PÅ MELDING
     C                   EXSR      GETTXT
     C                   MOVEL     XMSG          DELTXT
     C     UYEAR         IFGT      50
     C     UYEAR         ADD       1900          DELDÅR
     C                   ELSE
     C     UYEAR         ADD       2000          DELDÅR
     C                   END
     C                   MOVE      UMONTH        DELDMN
     C                   MOVE      UDAY          DELDDG
     C                   MOVE      ZUSER         DELUSR
      *
N01  c     *loval        setll     FIRM
N01  c                   read      FIRM
     C                   ENDSR
     C*
     C***********************************************
     C     GETTXT        BEGSR
     C***********************************************
     C                   CALL      'SUBR23R3'
     C                   PARM                    XMSGNR
     C                   PARM                    XMSG            120
     C                   PARM                    XLEVEL
     C                   PARM                    XRETUR
     C                   PARM                    XMSGL
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     DLDUF         BEGSR
     C***********************************************
     C     DOKUFK        SETLL     DOKUF
     C                   SETOFF                                       52
     C     STDDUF        TAG
     C     YFRI          READE     DOKUF                                  52
     C  N52HEAVD         COMP      DFAVD                              5252
     C  N52HEOPD         COMP      DFOPD                              5252
     C  N52              DO
     C                   DELETE    DOKUFR
     C                   GOTO      STDDUF
     C                   END
     C*
     C     DOKUFK        SETLL     DOKUFV
     C                   SETOFF                                       52
     C     STDDFV        TAG
     C     YFRI          READE     DOKUFV                                 52
     C  N52HEAVD         COMP      FVAVD                              5252
     C  N52HEOPD         COMP      FVOPD                              5252
     C  N52              DO
     C                   DELETE    DOKUFVR
     C                   GOTO      STDDFV
     C                   END
      *
     C     KEYFA2        SETLL     DOKUDGS
     C                   SETOFF                                       52
     C     STDDFD        TAG
     C     KEYFA2        READE     DOKUDGS                                52
     C  N52              DO
     C                   DELETE    DOKUDGS
     C                   GOTO      STDDFD
     C                   END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     DLDUI         BEGSR
     C***********************************************
     C     DOKUIK        SETLL     DOKUI
     C                   SETOFF                                       52
     C     STDDUI        TAG
     C     YIRI          READE     DOKUF                                  52
     C  N52HEAVD         COMP      DIAVD                              5252
     C  N52HEOPD         COMP      DIOPD                              5252
     C  N52              DO
     C                   DELETE    DOKUIR
     C                   GOTO      STDDUI
     C                   END
     C*
     C                   ENDSR
     C*
     C*
     C***********************************************
     C     DLDUT         BEGSR
     C***********************************************
     C     DOKUTK        SETLL     DOKUT
     C                   SETOFF                                       52
     C     STDDUT        TAG
     C     YTRI          READE     DOKUT                                  52
     C  N52HEAVD         COMP      DTAVD                              5252
     C  N52HEOPD         COMP      DTOPD                              5252
     C  N52              DO
     C                   DELETE    DOKUTR
     C                   GOTO      STDDUT
     C                   END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     DLDEB         BEGSR
     C***********************************************
     C     DOKEBK        CHAIN     DOKEB                              52
     C  N52              DELETE    DOKEBR
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     DLDEC         BEGSR
     C***********************************************
     C     DOKECK        CHAIN     DOKEC                              52
     C  N52              DELETE    DOKECR
     C     DOKECK        CHAIN     DOKECE                             52
     C  N52              DELETE    DOKECER
     C     DOKECK        SETLL     DOKECEL                            52
     C     STDDEC        TAG
     C     DOKECK        READE     DOKECEL                                52
     C  N52              DO
     C                   DELETE    DOKECELR
     C                   GOTO      STDDEC
     C                   END
     C*
     C                   ENDSR
     C*
     C*
     C***********************************************
     C     DLTRAC        BEGSR
     C***********************************************
     C     HEADFK        SETLL     TRACKT                             52
     C     STDTRAC       TAG
     C     HEADFK        READE     TRACKT                                 52
     C  N52              DO
     C                   DELETE    TRACKTR
     C                   GOTO      STDTRAC
     C                   END
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     DLDEF         BEGSR
     C***********************************************
     C     DOKEFK        CHAIN     DOKEF                              52
     C  N52              DELETE    DOKEFR
     C*
     C                   ENDSR
     C*
     C*
     C***********************************************
     C     DLKOSB        BEGSR
     C***********************************************
      * ÅPNE BUDSJETTPOSTER
     C     KOSBUK        SETLL     KOSBU9
     C     KOSBUK        READE     KOSBU9                                 52
     C     *IN52         DOWEQ     '0'
     C                   MOVE      'S'           BUST
     C                   UPDATE    KOSBUR
     C     KOSBUK        READE     KOSBU9                                 52
     C                   END
     C*
     C                   ENDSR
     C*
     C*
     C***********************************************
     C     DELDOK        BEGSR
     C***********************************************
     C* DELETE OTHER DOCUMENTS
     C*
     C                   EXSR      DLDUF
     C                   EXSR      DLDUI
     C                   EXSR      DLDUT
     C                   EXSR      DLDEB
     C                   EXSR      DLDEC
     C                   EXSR      DLDEF
     C                   EXSR      DLKOSB
     C                   EXSR      DLTRAC
     C                   ENDSR
     C***********************************************
     C     SLETTLINK     BEGSR
     C***********************************************
     c     keymor        klist
     c                   kfld                    travd0
     c                   kfld                    tropd0
      * lenke moroppdrag
     c                   move      heavd         heavdxx           4 0
     c                   move      heopd         heopdxx           7 0
     c     travd0        ifne      0
     c     tropd0        andne     0
     c     keymor        chain     headf                              33
     c     *in(33)       ifeq      '0'
     c     travd1        ifeq      heavdxx
     c     tropd1        andeq     heopdxx
     c                   move      *zeros        travd1
     c                   move      *zeros        tropd1
     c                   update    headfr
     c                   else
     c     travd2        ifeq      heavdxx
     c     tropd2        andeq     heopdxx
     c                   move      *zeros        travd2
     c                   move      *zeros        tropd2
     c                   update    headfr
     c                   end
     c                   end
     c                   end
     c                   end
     C*
     c                   move      heavdxx       heavd
     c                   move      heopdxx       heopd
     C                   ENDSR
     C*
N01
N01  C***********************************************
N01  C     TTSRDEL       BEGSR
N01  C***********************************************
N01  C                   clear                   TRACKFR
N01   * OPPDRAG OPPRETTET (POST I FIL TRACKF)
N01  C                   MOVE      HEAVD         TTAVD
N01  C                   MOVE      HEOPD         TTOPD
N01  C                   TIME                    XULTID
N01  C                   MOVE      XULDD         ULDD
N01  C                   MOVE      XULMM         ULMM
N01  C                   MOVE      XULÅÅ         ULÅÅ
N01  C                   MOVE      'DEL'         TTACTI
N01  C                   MOVE      ULDT          TTDATE
N01  C                   MOVE      XULTM         TTTIME
N01  C                   MOVE      TTDATE        TTDATL
N01  C                   MOVE      TTTIME        TTTIML
N01  C                   MOVE      ZUSER         TTUSER
N01  c                   eval      TTTEXT='Deleted, ' + WSTXT
N01  c                   eval      TTTEXL='Slettet, ' + WSTXT
N01  C                   WRITE     TRACKFR
N01   *
N01  C                   ENDSR
