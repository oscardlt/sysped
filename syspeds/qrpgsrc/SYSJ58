********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 YBC PTF10 ARKIVERING
********************************************************************
      *
      ***********************************************************
      *** PROGRAM SKAL SKRIVE SAMLASTSPESIFIKASJON            ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01-26                            ***
      *** LEDIGE INDIKATORER 28-32 34-40 43-59 62-99          ***
      ***********************************************************
      *
     FBOOKING   UF   E           K DISK
     FBOOKLF09  IF   E           K DISK
     F                                     RENAME(BOOKINGR:BOOKIN9R)
     FBLLOKLF1  IF   E           K DISK
     FBLV1LF    IF   E           K DISK
     FBLV2LF    IF   E           K DISK
     FCDBOAT    IF   E           K DISK
     FSTED2     IF   E           K DISK
N01  FARKTXT    IF   E           K DISK
N01  FFIRMARC   IF   E           K DISK
N01  FARKIVP    O  A E             DISK    USROPN
     FSYSJ58P   O    E             PRINTER
     D MN              S             17    DIM(50)
     D MN2             S             17    DIM(50)
     D FT              S             51    DIM(50)
     D VB              S             25    DIM(50)
     D VK              S              9  0 DIM(50)
     D KU              S              7  3 DIM(50)
     D AN              S              7  0 DIM(50)
     D                 DS
     D  BODT1Å                 1      4  0
     D  BODT1M                 5      6  0
     D  BODT1D                 7      8  0
     D  BODT1                  1      8  0
     D                 DS
     D  XDT1D                  1      2  0
     D  XDT1M                  3      4  0
     D  XDT1Å                  5      6  0
     D  XDT1                   1      6  0
     D                 DS
     D  BODT3Å                 1      4  0
     D  BODT3M                 5      6  0
     D  BODT3D                 7      8  0
     D  BODT3                  1      8  0
     D                 DS
     D  XDT3D                  1      2  0
     D  XDT3M                  3      4  0
     D  XDT3Å                  5      6  0
     D  XDT3                   1      6  0
     D                 DS
     D  XIDAGÅ                 1      4  0
     D  XIDAGM                 5      6  0
     D  XIDAGD                 7      8  0
     D  XIDAG                  1      8  0
     D                 DS
     D  BOAVD                  1      4  0
     D  BEAVD                  1      4  0
     D                 DS
     D  BOOPD                  1      7  0
     D  BEOPD                  1      7  0
N01  D                UDS
N01  D  UFIRM                  1      2
N01  D                SDS
N01  D  UUSER                254    263
N01  D                 DS
N01  D  UPARM1                 1   1024
N01  D  USG1                   1      3
N01  D  USD1                   4      8
N01  D  UDTIN1                 9     14
N01  D  UMN1                  15     31
N01  D  UDTSIN1               32     37
N01  D  UARKIV              1024   1024
N01  D                 DS
N01  D  BOARUKE                1      6
N01  D  BOACC                  1      2  0
N01  D  BOAAR                  3      4  0
N01  D  BOUKE                  5      6  0
     C*
     C                   EXSR      INIT
     C*
     C                   MOVE      *ZEROS        XTOTNT            7 0
     C                   MOVE      *ZEROS        XTOTVK           12 0
     C   41BOOK9K        SETLL     BOOKLF09
     C   42BLOK1K        SETLL     BLLOKLF1
     C*
     C     START         TAG
     C   41BOOK9K        READE     BOOKLF09                               60
     C   42UMN           READE     BLLOKLF1                               60
     C   60              GOTO      SLUTT
     C*
     C     BOOKK         CHAIN     BOOKING                            33
     C   42UDTS8         IFGT      BODT1
     C                   EXCEPT    DUMBK
     C                   GOTO      START
     C                   END
     C                   MOVE      XIDAG         BODTS
     C                   EXCEPT    OPDBK
     C*
     C  N27              DO
     C     BOLB          CHAIN     CDBOAT                             33
     C   33              MOVE      *BLANKS       XLBN             30
     C  N33              MOVE      CBNAB         XLBN
     C     USD           CHAIN     STED2                              33
     C   33              MOVE      *BLANKS       XSDN             30
     C  N33              MOVE      ST2NVN        XSDN
     C                   EXSR      KDTFM1
     C                   Z-ADD     1             XSIDE             2 0
     C                   WRITE     LHEAD
     C                   Z-ADD     7             XANTL             2 0
     C                   Z-ADD     0             XLIN              2 0
     C                   SETON                                        27
     C                   END
     C*
     C                   EXSR      KDTFM2
     C     BOOB          CHAIN     CDBOAT                             33
     C   33              MOVE      *BLANKS       XOBN             30
     C  N33              MOVE      CBNAB         XOBN
     C     BOLOH         CHAIN     STED2                              33
     C   33              MOVE      *BLANKS       XLOHN            30
     C  N33              MOVE      ST2NVN        XLOHN
     C*
     C     BLV1K         SETLL     BLV1LF
     C                   SETOFF                                       61
     C                   Z-ADD     0             X1                2 0
     C                   MOVE      *BLANKS       MN
     C                   MOVE      *BLANKS       MN2
     C                   MOVE      *BLANKS       VB
     C                   MOVE      *BLANKS       FT
     C                   MOVE      *ZEROS        AN
     C                   MOVE      *ZEROS        VK
     C                   MOVE      *ZEROS        KU
     C     START2        TAG
     C     BLV1K         READE     BLV1LF                                 61
     C  N61              DO
     C                   ADD       1             X1
     C                   MOVE      BVMN          MN(X1)
     C                   MOVE      BVVS          VB(X1)
     C                   Z-ADD     BVNT          AN(X1)
     C                   ADD       BVNT          XTOTNT
     C                   Z-ADD     BVVKT         VK(X1)
     C                   ADD       BVVKT         XTOTVK
     C                   Z-ADD     BVM3          KU(X1)
     C                   GOTO      START2
     C                   END
     C*
     C     BLV2K         SETLL     BLV2LF
     C                   SETOFF                                       61
     C                   Z-ADD     0             X2                2 0
     C                   MOVE      *BLANKS       MN2
     C                   MOVE      *BLANKS       FT
     C     START3        TAG
     C     BLV2K         READE     BLV2LF                                 61
     C  N61              DO
     C                   ADD       1             X2
     C                   MOVE      BVMN          MN2(X2)
     C                   MOVE      BVFT          FT(X2)
     C                   GOTO      START3
     C                   END
     C*
     C     X1            ADD       X2            X3                2 0
     C                   ADD       X3            XANTL
     C                   ADD       3             XANTL
     C     XANTL         IFGE      65
     C                   ADD       1             XSIDE
     C                   WRITE     LHEAD
     C                   Z-ADD     0             XLIN
     C     X3            ADD       10            XANTL
     C                   END
     C                   ADD       1             XLIN
     C                   WRITE     LDHEAD
     C                   Z-ADD     1             X3
     C     X3            DOWLE     X1
     C                   MOVE      MN(X3)        XMN
     C                   MOVE      AN(X3)        XNT
     C                   MOVE      VB(X3)        XVS
     C                   MOVE      VK(X3)        XVKT
     C                   MOVE      KU(X3)        XM3
     C                   WRITE     LDET
     C                   ADD       1             X3
     C                   END
     C                   Z-ADD     1             X3
     C     X3            DOWLE     X2
     C                   MOVE      MN2(X3)       XMN2
     C                   MOVE      FT(X3)        XFT
     C                   WRITE     LDET2
     C                   ADD       1             X3
     C                   END
N01   *
N01  C                   IF        UARKIV = ' '
N01  C                   exsr      arkivsr1
N01  C                   END
     C*
     C                   GOTO      START
     C*
     C     SLUTT         TAG
     C   27              WRITE     LBUNN
N01   *
N01  C                   IF        ((UARKIV = ' ') AND (XARKIV = 'J'))
N01  C                   exsr      arkivsr2
N01  C                   END
N01   *
     C*
     C                   SETON                                        LR
     C                   RETURN
     C*
N01   *=====================================================================
N01  C     ARKIVSR1      BEGSR
N01   *=====================================================================
N01  C     UFIRM         CHAIN     FIRMARC                            33
N01  C                   IF        *IN33 = *OFF
N01  C     'ss'          CHAIN     ARKTXT                             33
N01  C  N33              IF        ARKJN = 'J'
N01   *
N01  C                   OPEN      ARKIVP
N01  C                   CALL      'SYGE15C'
N01  C                   PARM                    WDT               8
N01  C                   PARM                    WTM               6
N01  C                   MOVE      UFIRM         ARFIRM
N01  C                   MOVE      BOAVD         ARAVD
N01  C                   MOVE      BOOPD         AROPD
N01  C                   MOVEL     BOARUKE       ARUNDE
N01  C                   MOVE      'ss'          ARTYPE
N01  C                   MOVE      UUSER         ARUSER
N01  C                   MOVE      WDT           ARDATE
N01  C                   MOVE      WTM           ARTIME
N01  C                   MOVE      *ZEROS        ARKUND
N01  C                   WRITE     ARKIVR
N01  C                   CLOSE     ARKIVP
N01  C                   MOVE      'J'           XARKIV            1
N01  C                   END
N01  C                   END
N01   *
N01  C                   ENDSR
N01   *
N01   *=====================================================================
N01  C     ARKIVSR2      BEGSR
N01   *=====================================================================
N01  C                   MOVE      USG2          USG1
N01  C                   MOVE      USD           USD1
N01  C                   MOVE      UDTIN         UDTIN1
N01  C                   MOVE      UMN           UMN1
N01  C                   MOVE      UDTSIN        UDTSIN1
N01  C                   MOVE      'J'           UARKIV
N01  C                   MOVE      UPARM1        UPARM2         1024
N01  C                   CALL      'ARC002CLS'
N01  C                   PARM                    ARTYPE
N01  C                   PARM                    ARUSER
N01  C                   PARM                    UPARM2
N01   *
N01  C                   ENDSR
N01   *
     C******************************************
     C     INIT          BEGSR
     C******************************************
     C     BOOKK         KLIST
     C                   KFLD                    BOAVD
     C                   KFLD                    BOOPD
     C     BOOK9K        KLIST
     C                   KFLD                    UDT8
     C     BLOK1K        KLIST
     C                   KFLD                    UMN
     C                   KFLD                    YAVD
     C                   KFLD                    YOPD
     C     BLV1K         KLIST
     C                   KFLD                    BOAVD
     C                   KFLD                    BOOPD
     C     BLV2K         KLIST
     C                   KFLD                    BOAVD
     C                   KFLD                    BOOPD
     C*
     C     *ENTRY        PLIST
     C                   PARM                    USG2              3
     C                   PARM                    USD               5
     C                   PARM                    UDTIN             6
     C                   PARM                    UMN              17
     C                   PARM                    UDTSIN            6
N01  C                   PARM                    UPARM          1024
     C*
N01  C                   IF        UPARM <> *BLANKS
N01  C                   MOVE      UPARM         UPARM1
N01  C                   MOVE      USG1          USG2
N01  C                   MOVE      USD1          USD
N01  C                   MOVE      UDTIN1        UDTIN
N01  C                   MOVE      UMN1          UMN
N01  C                   MOVE      UDTSIN1       UDTSIN
N01  C                   END
N01  C*                  END
     C                   TESTN                   UDTIN                33
     C   33              MOVE      UDTIN         UDT               6 0
     C     UDT           IFEQ      0
     C                   MOVE      *ZEROS        UDT8              8 0
     C                   ELSE
     C     UDT           IFGT      501231
     C     UDT           ADD       19000000      UDT8
     C                   ELSE
     C     UDT           ADD       20000000      UDT8
     C                   END
     C                   END
     C                   TESTN                   UDTSIN               33
     C   33              MOVE      UDTSIN        UDTS              6 0
     C     UDTS          IFEQ      0
     C                   MOVE      *ZEROS        UDTS8             8 0
     C                   ELSE
     C     UDTS          IFGT      501231
     C     UDTS          ADD       19000000      UDTS8
     C                   ELSE
     C     UDTS          ADD       20000000      UDTS8
     C                   END
     C                   END
     C     UDT           COMP      0                                  4141
     C     UMN           COMP      *BLANKS                            4242
     C*
     C                   MOVE      *ZEROS        YAVD              4 0
     C                   MOVE      *ZEROS        YOPD              7 0
     C                   MOVE      *ZEROS        BODT1
     C                   MOVE      *ZEROS        XDT1
     C                   MOVE      *ZEROS        BODT3
     C                   MOVE      *ZEROS        XDT3
     C     UYEAR         IFGT      50
     C     UYEAR         ADD       1900          XIDAGÅ
     C                   ELSE
     C     UYEAR         ADD       2000          XIDAGÅ
     C                   END
     C                   MOVE      UMONTH        XIDAGM
     C                   MOVE      UDAY          XIDAGD
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     KDTFM1        BEGSR
     C***********************************************
     C                   MOVE      BODT1D        UDT1              2 0
     C                   MOVE      BODT1M        UDT2              2 0
     C                   MOVE      BODT1Å        UDT3              2 0
     C                   CALL      'DATOFMT'
     C                   PARM                    UDT1
     C                   PARM                    UDT2
     C                   PARM                    UDT3
     C                   MOVE      UDT1          XDT1D
     C                   MOVE      UDT2          XDT1M
     C                   MOVE      UDT3          XDT1Å
     C*
     C                   ENDSR
     C*
     C***********************************************
     C     KDTFM2        BEGSR
     C***********************************************
     C                   MOVE      BODT3D        UDT1              2 0
     C                   MOVE      BODT3M        UDT2              2 0
     C                   MOVE      BODT3Å        UDT3              2 0
     C                   CALL      'DATOFMT'
     C                   PARM                    UDT1
     C                   PARM                    UDT2
     C                   PARM                    UDT3
     C                   MOVE      UDT1          XDT3D
     C                   MOVE      UDT2          XDT3M
     C                   MOVE      UDT3          XDT3Å
     C*
     C                   ENDSR
     C*
     OBOOKINGR  E            DUMBK
     OBOOKINGR  E            OPDBK
     O                       BODTS
