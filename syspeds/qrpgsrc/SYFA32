********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers  Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10XXX * 01 sh  PTF10 ny parameter syge13.....
********************************************************************
********************************************************************
      * VEDLIKEHOLD AV FASTE LASTEBÆRERE (EVT KNYTTET TIL KUNDE)
********************************************************************
     FVARELI    UF A E           K DISK
     FFIRM      IF   E           K DISK
     FCUNDL01   IF   E           K DISK
     FKUFAST    IF   E           K DISK
     FKODFRI    IF   E           K DISK
     FHEADF     IF   E           K DISK
     FFRISOK    UF A E           K DISK
     FVARESL01  IF   E           K DISK
     FSYFA32FM  CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     F                                     INFDS(FBAREA)
     D                 DS
     D  VLID                   1     17
     D  SLETT                  1      8
     D                 DS
     D  VLVS                   1     30
     D  XXVS                   1     22
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D*                                   B 382 3830WSCLOW
      *___________________________
      * Finner username
      *"""""""""""""""""""""""""""
     D                SDS
     D  ZJOB                 244    253
     D  ZUSER                254    263
     D  ZJOBNR               264    269  0
      *
     D                 DS
     D  HXHEAD                 1    256
     D  XXUR                   1      1
     D  XXFR                   2      4
     D  XXKDPL                 5      5
     D  XXLKA                  6      7
     D  XXSDF                  8     12
     D  XXTRI                 13     14
     D  XXSDT                 15     19
     D  XXLKS                 20     21
     D  XXSDFF                22     26
     D  XXLKK                 27     28
     D  XXSDVT                29     33
     D  XXVKT                 34     42  0
     D  XXFBV                 43     51  0
     D  XXM3                  52     58  3
     D  XXLM                  59     62  2
     D  XXNT                  63     69  0
     D  XXVERV                70     72
     D  XXVERB                73     75  2
      *
     C                   EXSR      INIT
     C                   EXSR      HOVED
     C                   EXSR      RETPAR
     C                   MOVE      '1'           *INLR
      *
      *
      *////////////////////////////////////////////////////////////
     C     INIT          BEGSR
      *////////////////////////////////////////////////////////////
      *___________________
      * Definiere Workfelt
      *"""""""""""""""""""
     C     *LIKE         DEFINE    *IN03         WIN03
     C     *LIKE         DEFINE    *IN30         WIN30
     C     *ENTRY        PLIST
     C                   PARM                    UAVD              4 0
     C                   PARM                    UOPD              7 0
     C                   PARM                    UDATO             8 0
     C                   PARM                    UKN               8 0
     C                   PARM                    ULKF              2
     C                   PARM                    USDF              5
     C                   PARM                    ULKT              2
     C                   PARM                    USDT              5
     C                   PARM                    UFRA              3
     C                   PARM                    UPROD             1
     C                   PARM                    UPRIS            11 2
     C                   PARM                    UVKT              9 0
     C                   PARM                    UM3               7 3
     C                   PARM                    ULM               4 2
      *__________________
      * Definiere Nøkkler
      *""""""""""""""""""
     C     OPDKEY        KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    UOPD
     C     KUNKEY        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    WSKN
     C     KEY           KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    UOPD
     C                   KFLD                    VLID
     C     KEYW          KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    UOPD
     C                   KFLD                    WSID
     C     TYPKEY        KLIST
     C                   KFLD                    XXTYPE           10
     C                   KFLD                    WSTY
     C                   MOVEL     'VATY'        XXTYPE
     C     FRIKEY        KLIST
     C                   KFLD                    FSAVD
     C                   KFLD                    FSOPD
     C                   KFLD                    FSKODE
     C                   MOVE      UAVD          FSAVD
     C                   MOVE      UOPD          FSOPD
     C                   MOVE      'LAB'         FSKODE
      * VIA KODE-DEFN HENT STANDARD DOKUMENTKODE (FELT KFSODK)
     C     FSKODE        CHAIN     KODFRI                             33
      *
     C                   READ      FIRM                                   55
      *
     C                   MOVE      'H'           XXUR
     C                   MOVE      UFRA          XXFR
     C                   MOVE      UPROD         XXKDPL
     C                   MOVE      ULKF          XXLKA
     C                   MOVE      USDF          XXSDF
     C                   MOVE      ULKT          XXTRI
     C                   MOVE      USDT          XXSDT
     C                   MOVE      *BLANKS       XXLKS
     C                   MOVE      *BLANKS       XXSDFF
     C                   MOVE      *BLANKS       XXLKK
     C                   MOVE      *BLANKS       XXSDVT
     C                   MOVE      *BLANKS       XXVERV
     C                   MOVE      *ZEROS        XXVERB
     C                   MOVE      *ZEROS        XXM3
     C                   MOVE      *ZEROS        XXLM
     C                   MOVE      *ZEROS        N5                5 0
      *
     C                   ENDSR
      *
      *
      *////////////////////////////////////////////////////////////
     C     HOVED         BEGSR
      *////////////////////////////////////////////////////////////
      *______________
      * Tømme Subfile
      *""""""""""""""
      *
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '000'         *IN(93)
     C                   Z-ADD     *ZERO         ZRECNO
     C     OPDKEY        SETLL     VARELI
      *_____________
      * Fyll Subfile (med alle poster uavhengig av linjer pr side)
      *"""""""""""""
     C     OPDKEY        READE(N)  VARELI                                 94
     C     *IN94         DOWEQ     '0'
      *
     C     NESTE         TAG
     C     VLMGW         SUB       VLTARA        XXNETT
     C                   ADD       1             ZRECNO
     C                   WRITE     SUBFIL
      *
     C     OPDKEY        READE(N)  VARELI                                 94
     C  N94              END
      *
     C     UT            TAG
      *
      *_______________
      * Ingen record fantes??
      *_______________
     C     ZRECNO        IFEQ      0
     C                   WRITE     BLANKREC
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     4             POSNBR
     C                   EXFMT     NOREC
     C                   MOVE      '0'           *IN30
     C                   ELSE
      *
      *_______________
      * Bevar sist brukt recno, vis subfila fra post begynnelsen
      *_______________
      *
     C     *LIKE         DEFINE    ZRECNO        XRECNO
     C                   MOVE      ZRECNO        XRECNO
     C                   Z-ADD     1             ZRECNO
      *
     C     STBILD        TAG
      *
     C                   MOVEA     '11'          *IN(91)
     C  N30              MOVE      'SY00000'     ZMSGNO
     C                   WRITE     BLANKREC
     C                   WRITE     BOTREC
     C                   Z-ADD     6             LINNBR
     C                   Z-ADD     2             POSNBR
     C                   EXFMT     SUBCTL
     C                   END
      *
     C     *IN12         CABEQ     '1'           UTHOV
     C     *IN03         CABEQ     '1'           UTHOV
      *
     C                   MOVEA     '00'          *IN(91)
      *_____________
      * F5=Frisk opp
      *"""""""""""""
     C     *IN05         CABEQ     '1'           START
      *_____________
      * F6=Lag ny
      *"""""""""""""
     C     *IN06         IFEQ      '1'
     C                   MOVE      'J'           NY                1
     C                   EXSR      CRTCHG
     C                   GOTO      START
     C                   END
      *_______
      * Valg ?
      *"""""""
     C                   MOVE      '0'           *IN52
     C                   MOVE      '0'           *IN30
     C                   MOVE      '0'           *IN95
     C     *IN52         DOWEQ     '0'
      *
     C                   READC     SUBFIL                                 52
     C     *IN52         IFEQ      '0'
     C                   EXSR      OPTION
     C                   END
      *
     C  N30
     CANN52              END
      *
     C     *IN03         CABEQ     '1'           UTHOV
     C     *IN95         CABEQ     '1'           START
     C     *IN95         CABEQ     '0'           STBILD
      *
     C     UTHOV         ENDSR
      *
      *
      *////////////////////////////////////////////////////////////
     C     OPTION        BEGSR
      *////////////////////////////////////////////////////////////
     C*
     C     WSNR          IFEQ      '4'
     C     SLETT         IFNE      '*SLETTET'
     C     KEY           CHAIN     VARELI                             55
     C  N55              DELETE    VARELIR
     C                   MOVE      ' '           WSNR
     C                   MOVE      *BLANKS       VLID
     C                   MOVEL     '*SLETTET'    VLID
     C                   UPDATE    SUBFIL
     C                   END
     C                   END
      *
      *
     C     WSNR          IFEQ      '2'
     C*          UPDATE    ANDEQ'J'
     C     SLETT         IFNE      '*SLETTET'
     C     KEY           CHAIN(N)  VARELI                             55
     C     *IN55         IFEQ      '0'
     C                   MOVE      'N'           NY                1
     C                   EXSR      CRTCHG
     C                   END
     C                   MOVE      ' '           WSNR
     C                   UPDATE    SUBFIL
     C                   END
     C                   END
      *
     C                   ENDSR
      *
      *
      *////////////////////////////////////////////////////////////
     C     CRTCHG        BEGSR
      *////////////////////////////////////////////////////////////
     C* 51 ON = PROTECT KEY-FELT
     C     NY            IFEQ      'N'
     C                   SETON                                            51
     C                   EXSR      MOVVWS
     C                   ELSE
     C                   SETOFF                                           51
     C                   CLEAR                   CHGREC
     C                   END
      *
     C                   Z-ADD     19            LINNBR
     C                   Z-ADD     13            POSNBR
     C     STCHG         TAG
     C                   EXFMT     CHGREC
     C     *IN12         IFNE      '1'
      *
     C     *IN04         IFEQ      '1'
     C                   EXSR      SØKDTA
     C                   GOTO      STCHG
     C                   END
      *
     C                   EXSR      TEST
     C   30              GOTO      STCHG
      *
     C     KEYW          CHAIN     VARELI                             55
     C                   EXSR      MOVWSV
     C   55              WRITE     VARELIR
     C  N55              UPDATE    VARELIR
     C                   END
      *
     C                   ENDSR
      *
      *
     C***********************************************
     C     SØKDTA        BEGSR
     C***********************************************
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
      *
     C     LINNBR        IFLE      18
      * KUNDENR...
     C                   MOVE      *BLANKS       USØKKN           30
     C                   CALL      'SOEKKUN'
     C                   PARM                    WSKN
     C                   PARM                    FIFIRM
     C                   PARM                    USØKKN
     C                   MOVE      USØKKN        XXNAVN
     C     USØKKN        IFNE      *BLANKS
     C                   SETOFF                                       7030
     C                   END
     C                   Z-ADD     19            LINNBR
     C                   Z-ADD     13            POSNBR
     C                   ELSE
      * LASTEBÆRERTYPE...
     C                   MOVE      'N'           UUPDAT            1
     C                   CALL      'SYKUKD'
     C                   PARM                    XXTYPE
     C                   PARM                    UUPDAT
     C                   PARM                    WSTY
     C     TYPKEY        CHAIN     KUFAST                             33
     C                   MOVE      *BLANKS       XXVT
     C     *IN33         IFEQ      '0'
     C                   MOVEL     KFTXT         XXVT
     C                   SETOFF                                       7230
     C                   END
     C                   Z-ADD     20            LINNBR
     C                   Z-ADD     49            POSNBR
     C                   END
     C*
     C     SLSØK         ENDSR
     C*
      *
      *
      *////////////////////////////////////////////////////////////
     C     TEST          BEGSR
      *////////////////////////////////////////////////////////////
      *
     C                   MOVEA     '00000000'    *IN(70)
     C                   MOVEA     '00'          *IN(78)
     C                   SETOFF                                       30
      * ID-GITT, HENT TYPE/MÅL M.M.
     C     WSID          CHAIN     VARESL01                           71
     C  N71              EXSR      MOVSTD
      * UTLEDEDE VERDIER..
     C     WSVOL         IFEQ      *ZEROS
     C     WSLEN         MULT(H)   WSBRD         WSVOL
     C                   MULT(H)   WSHOY         WSVOL
     C                   END
     C     WSMGW         SUB       WSTARA        XXNETT
      *
      *
     C                   MOVE      *BLANKS       XXNAVN
     C     WSKN          IFNE      *ZEROS
     C     KUNKEY        CHAIN     CUNDL01                            70
     C  N70              MOVE      KNAVN         XXNAVN
     C                   END
     C     TYPKEY        CHAIN     KUFAST                             72
     C                   MOVE      *BLANKS       XXVT
     C     *IN72         IFEQ      '0'
     C                   MOVEL     KFTXT         XXVT
     C                   END
     C     WSVOL         COMP      *ZEROS                                 73
     C     WSMGW         COMP      *ZEROS                                 74
     C     WSTARA        COMP      *ZEROS                                 75
     C     WSFBV         COMP      *ZEROS                                 76
     C     WSVS          COMP      *BLANKS                                77
     C     WSVKT         COMP      *ZEROS                                 78
     C     WSPRIS        IFEQ      *ZEROS
     C                   EXSR      BERFRP
     C                   END
     C     WSPRIS        COMP      *ZEROS                                 79
      *
      * FEILMELDINGER
      * KUNDE
     C     *IN70         IFEQ      '1'
     C                   MOVE      'JOV0936'     MSGNR
     C                   MOVE      '1'           *IN30
     C                   Z-ADD     18            LINNBR
     C                   Z-ADD     40            POSNBR
     C                   GOTO      UTTST
     C                   END
      * ID
     C     *IN71         IFEQ      '1'
     C                   MOVE      'JOV1002'     MSGNR
     C                   MOVE      '1'           *IN30
     C                   Z-ADD     19            LINNBR
     C                   Z-ADD     13            POSNBR
     C                   GOTO      UTTST
     C                   END
      * TYPE
     C     *IN72         IFEQ      '1'
     C                   MOVE      'JOV0937'     MSGNR
     C                   MOVE      '1'           *IN30
     C                   Z-ADD     19            LINNBR
     C                   Z-ADD     43            POSNBR
     C                   GOTO      UTTST
     C                   END
      * VAREBESKRIVELSE
     C     *IN77         IFEQ      '1'
     C                   MOVE      'JOV0942'     MSGNR
     C                   MOVE      '1'           *IN30
     C                   Z-ADD     20            LINNBR
     C                   Z-ADD     49            POSNBR
     C                   GOTO      UTTST
     C                   END
      * VOLUM
     C     *IN73         IFEQ      '1'
     C                   MOVE      'JOV0938'     MSGNR
     C                   MOVE      '1'           *IN30
     C                   Z-ADD     21            LINNBR
     C                   Z-ADD     48            POSNBR
     C                   GOTO      UTTST
     C                   END
      * MGW
     C     *IN74         IFEQ      '1'
     C                   MOVE      'JOV0939'     MSGNR
     C                   MOVE      '1'           *IN30
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     13            POSNBR
     C                   GOTO      UTTST
     C                   END
      * TARA
     C     *IN75         IFEQ      '1'
     C                   MOVE      'JOV0940'     MSGNR
     C                   MOVE      '1'           *IN30
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     27            POSNBR
     C                   GOTO      UTTST
     C                   END
      * BRT VKT
     C     *IN78         IFEQ      '1'
     C                   MOVE      'JOV0944'     MSGNR
     C                   MOVE      '1'           *IN30
     C                   Z-ADD     22            LINNBR
     C                   Z-ADD     69            POSNBR
     C                   GOTO      UTTST
     C                   END
      * F.BER.VEKT
     C     *IN76         IFEQ      '1'
     C                   MOVE      'JOV0941'     MSGNR
     C                   MOVE      '1'           *IN30
     C                   Z-ADD     23            LINNBR
     C                   Z-ADD     46            POSNBR
     C                   GOTO      UTTST
     C                   END
      * F.BER.VEKT
     C     *IN79         IFEQ      '1'
     C                   MOVE      'JOV0943'     MSGNR
     C                   MOVE      '1'           *IN30
     C                   Z-ADD     23            LINNBR
     C                   Z-ADD     64            POSNBR
     C                   GOTO      UTTST
     C                   END
      *
     C     UTTST         ENDSR
      *
     C******************************************
     C     BERFRP        BEGSR
     C******************************************
     C* POSTNUMMERBASERT KALKULASJON
      * Finn frakt i kundersfraktavtale
     C                   MOVE      *BLANKS       UUVAL             3
     C                   MOVE      'FRA'         UGEB              3
     C                   MOVE      'FRA'         UGEBKO            3
     C                   MOVE      *ZEROS        UUBELØ           13 2
     C                   MOVE      'S'           USKA              1
     C                   Z-ADD     1             XXNT
     C                   Z-ADD     WSFBV         XXVKT
     C                   Z-ADD     WSFBV         XXFBV
     C                   Z-ADD     UKN           UUKN              8 0
     C                   MOVE      HXHEAD        UHEAD           256
     C                   CALL      'SYGE14'
     C                   PARM                    UGEBKO
     C                   PARM                    UGEB
     C                   PARM                    UAVD
     C                   PARM                    UOPD
     C                   PARM                    UUKN
     C                   PARM                    UUVAL
     C                   PARM                    UUBELØ
     C                   PARM                    UAVNR             9 0
     C                   PARM                    UAVMSG           50
     C                   PARM                    USKA
     C                   PARM                    UHEAD
      *
     C     UUBELØ        IFEQ      0
     C                   MOVE      *BLANKS       UUVAL
     C                   MOVE      *ZEROS        URABAT
     C                   Z-ADD     1             XXNT
     C                   Z-ADD     WSFBV         XXVKT
     C                   Z-ADD     WSFBV         XXFBV
     C                   Z-ADD     UKN           UUKN              8 0
     C                   MOVE      HXHEAD        UHEAD           256
     C                   CALL      'SYGE13'
     C                   PARM                    UGEB
     C                   PARM                    UAVD
     C                   PARM                    UOPD
     C                   PARM                    UUVAL
     C                   PARM                    UUBELØ
     C                   PARM                    USONE             5
     C                   PARM                    URABAT            5 2
     C                   PARM                    N5                5 0
     C                   PARM                    N5                5 0
     C                   PARM                    N5                5 0
     C                   PARM                    N5                5 0
     C                   PARM                    UUKN
     C                   PARM                    UHEAD
n01  C                   PARM                    atdum             1
     C                   END
     C                   Z-ADD     UUBELØ        WSPRIS
     C                   ENDSR
     C*
      *////////////////////////////////////////////////////////////
     C     MOVVWS        BEGSR
      *////////////////////////////////////////////////////////////
      *
     C                   MOVE      VLKN          WSKN
     C                   MOVE      VLID          WSID
     C                   MOVE      VLTY          WSTY
     C                   MOVE      VLVS          WSVS
     C                   MOVE      VLLEN         WSLEN
     C                   MOVE      VLBRD         WSBRD
     C                   MOVE      VLHOY         WSHOY
     C                   MOVE      VLVOL         WSVOL
     C                   MOVE      VLLM          WSLM
     C                   MOVE      VLVKT         WSVKT
     C                   MOVE      VLFBV         WSFBV
     C                   MOVE      VLMGW         WSMGW
     C                   MOVE      VLTARA        WSTARA
     C                   MOVE      VLPRIS        WSPRIS
     C                   MOVE      *BLANKS       XXNAVN
     C     KUNKEY        CHAIN     CUNDL01                            33
     C  N33              MOVE      KNAVN         XXNAVN
     C                   MOVE      *BLANKS       XXVT
     C     TYPKEY        CHAIN     KUFAST                             33
     C  N33              MOVEL     KFTXT         XXVT
     C     VLMGW         SUB       VLTARA        XXNETT
     C                   ENDSR
      *////////////////////////////////////////////////////////////
     C     MOVWSV        BEGSR
      *////////////////////////////////////////////////////////////
      *
     C                   MOVE      UAVD          VLAVD
     C                   MOVE      UOPD          VLOPD
     C                   MOVE      WSKN          VLKN
     C                   MOVE      WSKN          VLKN
     C                   MOVE      WSID          VLID
     C                   MOVE      WSTY          VLTY
     C                   MOVE      WSVS          VLVS
     C                   MOVE      WSLEN         VLLEN
     C                   MOVE      WSBRD         VLBRD
     C                   MOVE      WSHOY         VLHOY
     C                   MOVE      WSVOL         VLVOL
     C                   MOVE      WSLM          VLLM
     C                   MOVE      WSVKT         VLVKT
     C                   MOVE      WSFBV         VLFBV
     C                   MOVE      WSMGW         VLMGW
     C                   MOVE      WSTARA        VLTARA
     C                   MOVE      WSPRIS        VLPRIS
     C                   ENDSR
      *////////////////////////////////////////////////////////////
     C     MOVSTD        BEGSR
      *////////////////////////////////////////////////////////////
      *
     C                   MOVE      VAKN          WSKN
     C                   MOVE      VAID          WSID
     C                   MOVE      VATY          WSTY
     C                   MOVE      *BLANKS       XXNAVN
     C     KUNKEY        CHAIN     CUNDL01                            33
     C  N33              MOVE      KNAVN         XXNAVN
     C                   MOVE      *BLANKS       XXVT
     C     TYPKEY        CHAIN     KUFAST                             33
     C  N33              MOVEL     KFTXT         XXVT
      * EGENTLIG LITT TVILSOMT Å KUNNE OVERSTYRE VERDIENE PÅ EN
      * REGISTRERT/DEFINERT LASTEBÆRER?????
     C     WSLEN         IFEQ      *ZEROS
     C                   MOVE      VALEN         WSLEN
     C                   END
     C     WSBRD         IFEQ      *ZEROS
     C                   MOVE      VABRD         WSBRD
     C                   END
     C     WSHOY         IFEQ      *ZEROS
     C                   MOVE      VAHOY         WSHOY
     C                   END
     C     WSVOL         IFEQ      *ZEROS
     C                   MOVE      VAVOL         WSVOL
     C                   END
     C     WSLM          IFEQ      *ZEROS
     C                   MOVE      VALM          WSLM
     C                   END
     C     WSFBV         IFEQ      *ZEROS
     C                   MOVE      VAFBV         WSFBV
     C                   END
     C     WSMGW         IFEQ      *ZEROS
     C                   MOVE      VAMGW         WSMGW
     C                   END
     C     WSTARA        IFEQ      *ZEROS
     C                   MOVE      VATARA        WSTARA
     C                   END
     C                   ENDSR
      *////////////////////////////////////////////////////////////
     C     RETPAR        BEGSR
      *////////////////////////////////////////////////////////////
      *
      *______________
      * SLETT FRIE SØKEVEIER
      *""""""""""""""
     C     FRIKEY        SETLL     FRISOK
     C     FRIKEY        READE     FRISOK                                 33
     C     *IN33         DOWEQ     '0'
     C                   DELETE    FRISOKR
     C     FRIKEY        READE     FRISOK                                 33
     C                   END
      *______________
      * AKKUMULER RETUR-PARAMETER, OPPDATER DATO, LEGG UT I FRIE SØKEV.
      *""""""""""""""
     C                   MOVE      *ZEROS        UPRIS
     C                   MOVE      *ZEROS        UVKT
     C                   MOVE      *ZEROS        UM3
     C                   MOVE      *ZEROS        ULM
      *
     C     OPDKEY        SETLL     VARELI
     C     OPDKEY        READE     VARELI                                 33
     C     *IN33         DOWEQ     '0'
      *
     C                   ADD       VLPRIS        UPRIS
     C                   ADD       VLVKT         UVKT
     C                   ADD       VLVOL         UM3
     C                   ADD       VLLM          ULM
      *
     C                   MOVE      UDATO         VLDTOP
     C                   UPDATE    VARELIR
      *
     C                   MOVEL     VLID          FSSOK
     C     OPDKEY        CHAIN     HEADF                              34
     C     *IN34         IFEQ      '0'
     C                   MOVE      HEDTOP        FSDTOP
     C                   ELSE
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVE      WDT           FSDTOP
     C                   END
     C                   MOVEL     KFSODK        FSDOKK
     C                   WRITE     FRISOKR
      *
     C     OPDKEY        READE     VARELI                                 33
     C  N33              END
      *
     C                   ENDSR
