      * ***************************************************************
      *    DETTE PROGRAM SKRIVER ETIKETT PÅ ELTRON SKRIVER
      * ***************************************************************
      *
      * COMMAND Q=LENGDE / MELLOMROM M.M
      * Q p1,p2 (p1=lengde i dots, p2=mellomrom i dots )
      *
      * MÅLES I "DOTS",  PÅ ORION: 8 DOT PR MM
      * TOTALLENGDE MELLOM PERFORERING = 195MM  = 1560 DOTS
      * LENGDE PÅ DEN ENKELT LAPP      = 192MM  = 1536 DOTS
      * LENGDE PÅ MELLOMROM(GAP)       =   3MM  =   24 DOTS
      * (LENGDE FRA PERF.TIL START LAPP= 1,5MM  =   12 DOTS)
      * SE EPL2en.pdf side 244
     FLLHEAD    IF   E           K DISK
     FCUNDL01   IF   E           K DISK
     FKODTA     IF   E           K DISK
     FTRAN      IF   E           K DISK
     FSYLA03P   O    F   80        PRINTER OFLIND(*INOF)
     D F1              S             36    DIM(28) CTDATA PERRCD(1)             Kommandoer
      *
     D A4              S              1    DIM(4)                               Input string
     D B4              S              1    DIM(4)                               Output string
     D Æ               S              1    DIM(20)                              Input string
     D Å               S              1    DIM(20)                              Output string
     D                 DS
      * Make "No. of Pieces" Zoned numeric
     D  LANUMA                 1      4  0
     D                 DS
     D  WPCN                   1      4  0
     D  WPCA                   1      4
     D  WPCB                   2      4
     D                 DS
     D  ØN151                  1     15  1
     D  ØA151H                 1     14
     D  ØA151X                 5     14
     D  ØA151D                15     15
     D                 DS
     D  ØA12                   1     12
     D  Ø12                    1     12
     D                                     DIM(12)                              Num edit
     D                 DS
     D  LHTDT                  1      8  0
     D  LHTDTÅ                 3      4
     D  LHTDTM                 5      6
     D  LHTDTD                 7      8
     D                UDS
     D  UFIRM                  1      2
     D  UAVD                  15     18  0
      * Input Parameter
     C     *ENTRY        PLIST
     C                   PARM                    WMN               7
     C                   MOVE      WMN           LHMN
      *
     C                   EXSR      SRINIT
      *
     C     LHMN          CHAIN     LLHEAD                             55
     C     CUND1k        CHAIN     CUNDL01                            55
     C     TRANK         CHAIN     TRAN                               55
     C  N55              MOVEL     VMNAVN        KOANVN
      * LOOP FOR NO OF COPIES
     C                   MOVE      *ZEROS        WPCN2             5 0
     C     1             DO        LHMLP         WPCN
      * Send field data to printer
     C                   EXSR      SR100
      * END LOOP
     C                   ENDDO
      * End Program
     C     ENDPGM        TAG
     C                   SETON                                        LR
      *_____________________________________________________
      * Send all data fields
      *"""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     SR100         BEGSR
      *
      ******************************************************
      * Her begynner selve produksjon av utdata til merkelapp
      ******************************************************
      * Ved lapp 2,3,4, endres kun variable data ( xx AV YY og kol-ID)
     C     *IN50         CABEQ     '1'           VARI1
      * FRA (AVSENDER)
      * Ledetekst
     C                   MOVE      *BLANKS       DATA
     C                   MOVEL     F1(01)        DATA
     C                   EXCEPT    SYLA01
      * Data
     C                   MOVE      *BLANKS       DATA
     C                   MOVEL     F1(02)        DATA
     C     DATA          CAT       KNAVN:0       DATA
     C     DATA          CAT       '"':0         DATA
     C                   EXCEPT    SYLA01
     C                   MOVE      *BLANKS       DATA
     C                   MOVEL     F1(03)        DATA
     C     DATA          CAT       ADR1:0        DATA
     C     DATA          CAT       '"':0         DATA
     C                   EXCEPT    SYLA01
     C                   MOVE      *BLANKS       DATA
     C                   MOVEL     F1(04)        DATA
     C     DATA          CAT       ADR2:0        DATA
     C     DATA          CAT       '"':0         DATA
     C                   EXCEPT    SYLA01
     C                   MOVE      *BLANKS       PNRALF            9
     C                   IF        POSTNR = 0
     C                   MOVE      SYPOGE        PNRALF
     C                   ELSE
     C                   MOVEL     POSTNR        PNRALF
     C                   END
     C                   MOVE      *BLANKS       DATA
     C                   MOVEL     F1(05)        DATA
     C     DATA          CAT       PNRALF:0      DATA
     C     DATA          CAT       ADR3:1        DATA
     C     DATA          CAT       '"':0         DATA
     C                   EXCEPT    SYLA01
      *
      * STREK
     C                   MOVE      *BLANKS       DATA
     C                   MOVEL     F1(06)        DATA
     C                   EXCEPT    SYLA01
      *
      * Til (MOTTAKER)
      * Ledetekst
     C                   MOVE      *BLANKS       DATA
     C                   MOVEL     F1(07)        DATA
     C                   EXCEPT    SYLA01
      * Data
     C                   MOVE      *BLANKS       DATA
     C                   MOVEL     F1(08)        DATA
     C     DATA          CAT       LHBNA:0       DATA
     C     DATA          CAT       '"':0         DATA
     C                   EXCEPT    SYLA01
     C                   MOVE      *BLANKS       DATA
     C                   MOVEL     F1(09)        DATA
     C     DATA          CAT       LHBA1:0       DATA
     C     DATA          CAT       '"':0         DATA
     C                   EXCEPT    SYLA01
     C                   MOVE      *BLANKS       DATA
     C                   MOVEL     F1(10)        DATA
     C     DATA          CAT       LHBA2:0       DATA
     C     DATA          CAT       '"':0         DATA
     C                   EXCEPT    SYLA01
     C                   MOVE      *BLANKS       PNRALF            9
     C                   IF        LHBPN = 0
     C                   MOVE      LHBPU         PNRALF
     C                   ELSE
     C                   MOVEL     LHBPN         PNRALF
     C                   END
     C                   MOVE      *BLANKS       DATA
     C                   MOVEL     F1(11)        DATA
     C     DATA          CAT       PNRALF:0      DATA
     C     DATA          CAT       LHBA3:1       DATA
     C     DATA          CAT       '"':0         DATA
     C                   EXCEPT    SYLA01
      *
      * STREK
     C                   MOVE      *BLANKS       DATA
     C                   MOVEL     F1(12)        DATA
     C                   EXCEPT    SYLA01
      *
      * TRANSPORTØR
      * Ledetekst
     C                   MOVE      *BLANKS       DATA
     C                   MOVEL     F1(13)        DATA
     C                   EXCEPT    SYLA01
     C                   MOVE      *BLANKS       DATA
     C                   MOVEL     F1(14)        DATA
     C                   EXCEPT    SYLA01
      * Data
     C                   MOVE      *BLANKS       DATA
     C                   MOVEL     F1(15)        DATA
     C     DATA          CAT       KOANVN:0      DATA
     C     DATA          CAT       '"':0         DATA
     C                   EXCEPT    SYLA01
      * Ledetekst
     C                   MOVE      *BLANKS       DATA
     C                   MOVEL     F1(16)        DATA
     C                   EXCEPT    SYLA01
     C                   MOVE      *BLANKS       DATA
     C                   MOVEL     F1(17)        DATA
     C                   EXCEPT    SYLA01
      * Data
     C                   MOVE      *BLANKS       DATA
     C                   MOVEL     F1(18)        DATA
     C     DATA          CAT       LHTDTD:0      DATA
     C     DATA          CAT       '.':0         DATA
     C     DATA          CAT       LHTDTM:0      DATA
     C     DATA          CAT       '.':0         DATA
     C     DATA          CAT       LHTDTÅ:0      DATA
     C     DATA          CAT       '"':0         DATA
     C                   EXCEPT    SYLA01
      * Ledetekst
     C                   MOVE      *BLANKS       DATA
     C                   MOVEL     F1(19)        DATA
     C                   EXCEPT    SYLA01
     C                   MOVE      *BLANKS       DATA
     C                   MOVEL     F1(20)        DATA
     C                   EXCEPT    SYLA01
      * Data
     C                   MOVE      *BLANKS       DATA
     C                   MOVEL     F1(21)        DATA
     C                   MOVE      LHALN         XAN05
     C     DATA          CAT       XAN05:0       DATA
     C     DATA          CAT       '"':0         DATA
     C                   EXCEPT    SYLA01
      *
      * K-REF
     C                   MOVE      *BLANKS       DATA
     C                   MOVEL     F1(27)        DATA
     C                   EXCEPT    SYLA01
      * Data 1
     C                   MOVE      *BLANKS       DATA
     C                   MOVEL     F1(28)        DATA
     C     DATA          CAT       LHPAK:0       DATA
     C     DATA          CAT       '"':0         DATA
     C                   EXCEPT    SYLA01
      *
      * ANTALL ... AV...
      * Ledetekst
     C                   MOVE      *BLANKS       DATA
     C                   MOVEL     F1(22)        DATA
     C                   EXCEPT    SYLA01
      * Data
     C     VARI1         TAG
     C                   MOVE      *BLANKS       DATA
     C                   MOVEL     F1(23)        DATA
     C                   IF        WPCN <= LHKLL
     C                   Z-ADD     WPCN          LANUMA
     C                   ELSE
     C                   ADD       1             WPCN2
     C                   Z-ADD     WPCN2         LANUMA
     C                   ENDIF
     C                   MOVE      LANUMA        CHAR4             4
     C                   MOVEA     CHAR4         A4
     C                   Z-ADD     1             X
     C                   Z-ADD     1             Y
     C                   MOVEA     *BLANKS       B4
     C     A4(X)         DOWEQ     '0'
     C     X             ANDLT     4
     C                   ADD       1             X
     C                   ENDDO
     C     X             DOWLE     4
     C                   MOVEA     A4(X)         B4(Y)
     C                   ADD       1             X
     C                   ADD       1             Y
     C                   ENDDO
     C                   MOVEA     B4            CHAR4
     C     DATA          CAT       CHAR4:0       DATA
     C     DATA          CAT       'AV':1        DATA
     C                   Z-ADD     LHKLL         LANUMA
     C                   MOVE      LANUMA        CHAR4             4
     C                   MOVEA     CHAR4         A4
     C                   Z-ADD     1             X
     C                   Z-ADD     1             Y
     C                   MOVEA     *BLANKS       B4
     C     A4(X)         DOWEQ     '0'
     C     X             ANDLT     4
     C                   ADD       1             X
     C                   ENDDO
     C     X             DOWLE     4
     C                   MOVEA     A4(X)         B4(Y)
     C                   ADD       1             X
     C                   ADD       1             Y
     C                   ENDDO
     C                   MOVEA     B4            CHAR4
     C     DATA          CAT       CHAR4:1       DATA
     C     DATA          CAT       '"':0         DATA
     C                   EXCEPT    SYLA01
      * Ved lapp 2,3,4, endres kun variable data ( xx AV YY og kol-ID)
     C     *IN50         CABEQ     '1'           VARI2
      *
      * STREK
     C                   MOVE      *BLANKS       DATA
     C                   MOVEL     F1(24)        DATA
     C                   EXCEPT    SYLA01
      *
      * Ledetekst
     C                   MOVE      *BLANKS       DATA
     C                   MOVEL     F1(25)        DATA
     C                   EXCEPT    SYLA01
      * Data
     C                   MOVE      *BLANKS       DATA
     C                   MOVEL     F1(26)        DATA
     C                   MOVE      LHALN         XAN05             5
     C     DATA          CAT       XAN05:0       DATA
     C     DATA          CAT       '"':0         DATA
     C                   EXCEPT    SYLA01
      * Print 1 eksemplar
     C     VARI2         TAG
     C                   MOVEL     *BLANKS       DATA
     C                   MOVEL     'P1'          DATA
     C                   EXCEPT    SYLA01
      *
     C                   MOVE      *ON           *IN50                           FØRSTE FERD
      *
     C                   ENDSR
      *_____________________________________________________
      * Leftjustify num.fields
      *"""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     SRNE1         BEGSR
      * Move correct value into alpha 12-field
     C                   MOVE      *ZEROS        ØA12
     C     ØA151D        IFEQ      '0'                                          <-------------I
     C     ØADEC         ANDEQ     'N'                                          <-------------I
     C                   MOVE      ØA151H        ØA12                                         I
     C                   ELSE                                                   <-------------I
     C                   MOVEL     ØA151X        ØA12                                         I
     C                   MOVE      '. '          ØA12                                         I
     C                   MOVE      ØA151D        ØA12                                         I
     C                   END                                                    <-------------I
      * search from left and find first sign over 0
     C     1             DO        11            ØX                             <-------------I
     C     Ø12(ØX)       IFNE      '0'                                           <-----------II
     C                   LEAVE                                                               II
     C                   END                                                     <-----------II
     C                   END                                                    <-------------I
      * Leftjustify to a new field
     C                   MOVE      *BLANKS       ØAN
     C                   MOVEA     Ø12(ØX)       ØAN
      * Reset decimal indicator
     C                   MOVE      'N'           ØADEC
      *
     C                   ENDSR
      *_____________________________________________________
      * Center text
      *"""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     SRCENT        BEGSR
     C                   MOVEA     *BLANKS       Å
      * Find rightmost character of input
     C     SRB1          TAG
     C     ÆX            CABEQ     0             CENT99
     C     Æ(ÆX)         IFEQ      *BLANKS
     C                   SUB       1             ÆX
     C                   GOTO      SRB1
     C                   ENDIF
      * Sub from the rightmost counter
      * This is only valid if formtype A0 us used because blank
      * caracters uses less space in grafic mode.
     C                   SELECT
     C     ÆX            WHENLT    19
     C     ÆX            ANDGE     15
     C                   SUB       2             ÆX
     C     ÆX            WHENLT    14
     C     ÆX            ANDGE     10
     C                   SUB       4             ÆX
     C     ÆX            WHENLT    9
     C     ÆX            ANDGE     5
     C                   SUB       6             ÆX
     C     ÆX            WHENLT    4
     C                   SUB       8             ÆX
     C                   ENDSL
      * Calculate left positions of output
     C     18            SUB       ÆX            ÆX
     C     ÆX            IFGE      2
     C     ÆX            DIV       2             ÆX
     C                   ADD       1             ÆX
     C                   ELSE
     C                   Z-ADD     1             ÆX
     C                   ENDIF
      * Move input to output arrary
     C                   MOVEA     Æ             Å(ÆX)
      *
     C     CENT99        ENDSR
      *_____________________________________________________
      * Initialise subroutine, set character set
      *"""""""""""""""""""""""""""""""""""""""""""""""""""""
     C     SRINIT        BEGSR
     C                   MOVE      *OFF          *IN50
      *
     C                   Z-ADD     0             ØX                3 0          Index for Ø
     C                   MOVE      *BLANKS       ØAN              12            Num alpha
     C                   MOVE      'N'           ØADEC             1            Decimal Indicator
     C                   Z-ADD     0             ÆX                3 0
     C                   Z-ADD     0             X                 1 0
     C                   Z-ADD     0             Y                 1 0
     C                   MOVE      *BLANKS       MSGVAR            7            Message Variabel
     C     CUND1K        KLIST
     C                   KFLD                    UFIRM
     C                   KFLD                    LHPNR
     C     CUND1KAVD     KLIST
     C                   KFLD                    UFIRM
     C                   KFLD                    KOAKNR
     C     KODTAK        KLIST
     C                   KFLD                    KOAUNI
     C                   KFLD                    UAVD
     C     TRANK         KLIST
     C                   KFLD                    UFIRM
     C                   KFLD                    LHTN
      *
     C                   MOVE      'A'           KOAUNI
     C     KODTAK        CHAIN     KODTA                              33
     C     CUND1KAVD     CHAIN     CUNDL01                            33
     C* Initiering/ Printparam / label size
     C                   MOVE      *BLANKS       DATA             80
     C                   EXCEPT    SYLA01
     C                   MOVEL     'N       '    DATA
     C                   EXCEPT    SYLA01
     C                   MOVEL     'D7      '    DATA
     C                   EXCEPT    SYLA01
     C                   MOVEL     'S2      '    DATA
     C                   EXCEPT    SYLA01
     C                   MOVEL     'Q1536,24'    DATA
     C                   EXCEPT    SYLA01
     C                   MOVEL     'q760    '    DATA
     C                   EXCEPT    SYLA01
      *
     C                   ENDSR
     OSYLA03P   E            SYLA01
     O                       DATA                80
      *
      *
      * A=ASCII-TEKST, PARAMETRE...
      *   P1=KOLONNE,P2=LINJE,P3=ROTASJON,P4=FONT,P5=HORIS MULTIPL,
      *   P6=VERTIKAL MULTIPL, P7=N=NORMAL/R=REVERSE,P8=DATA
      * Obs Hvert tab-elem er max 36 long ( t.o.m. kol 44 på skjerm)
**    * KOMMANDO/FAST TEKST         |    DATA SOM SKAL CONCATINERES..
A18,40,0,2,1,1,N,"Fra/From"         | 01
A40,65,0,1,2,2,N,"                  | 02 AVS NAVN
A40,95,0,1,2,2,N,"                  | 03 AVS ADR 1
A40,125,0,1,2,2,N,"                 | 04 AVS ADR 2
A40,155,0,1,2,2,N,"                 | 05 AVS ADR 3
LE10,180,666,6                      | 06 (=strek)
A18,200,0,2,1,1,N,"Til/To"          | 07
A40,220,0,3,2,2,N,"                 | 08 MOT NAVN
A40,270,0,3,2,2,N,"                 | 09 MOT ADR 1
A40,320,0,3,2,2,N,"                 | 10 MOT ADR 2
A40,370,0,3,2,2,N,"                 | 11 MOT ADR 3
LE10,420,666,6                      | 12 (=strek)
A18,440,0,2,1,1,N,"Transportør/C"   | 13
A170,440,0,2,1,1,N,"arrier"         | 14
A40,465,0,4,1,2,N,"                 | 15 TRANSP. NAVN
A18,515,0,2,1,1,N,"Trans.dato/Tr"   | 16
A170,515,0,2,1,1,N,"ans.date"       | 17
A40,535,0,4,1,2,N,"                 | 18 TRANSP. DATO
A18,635,0,2,1,1,N,"Vår ref/Our r"   | 19
A170,635,0,2,1,1,N,"ef"             | 20
A40,655,0,2,1,2,N,"                 | 21 PLUKKLISTENR
A18,690,0,2,1,1,N,"Kolli"           | 22
A110,690,0,3,2,2,N,"                | 23 ANT AV TOT
LE10,730,666,6                      | 24 (=strek)
A18,750,0,2,1,1,N,"Vår ref/Our-ref" | 25
B18,770,0,1,4,8,220,N,"             | 26 PLUKKLISTENR
A450,635,0,2,1,1,N,"K-ref/C-ref"    | 27
A450,655,0,2,1,2,N,"                | 28 K-REF
