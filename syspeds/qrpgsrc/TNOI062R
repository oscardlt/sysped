      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/TNOI062R MODULE(*LIBL/TNOI062R *LIBL/INCGI)
CRTPGM*        ACTGRP(*NEW) AUT(*USE)
      *
      *********************************************************************
      /copy SYSPEDS/qrpgsrcpy,hspecs
      /copy SYSPEDS/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FKODTS2    IF   E           K DISK    USROPN
     FTARI      IF   E           K DISK    USROPN
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy SYSPEDS/qrpgsrcpy,prototypeb
      /copy SYSPEDS/qrpgsrcpy,usec
      /copy SYSPEDS/qrpgsrcpy,variables3
      *
      * Varible for
     D WSUSER          S             10
     D SIDP            S              2
     D SIDPN           S              2  0
     D SITST           S              1
     D SITSTN          S              1  0
     D SVVNT           S              8
     D SVVNTN          S              8  0
     D SVLK            S              2
     D SVTN            S              1
     D SVPRE           S              1
     D JSONstring      s          32000
     D Error           S            150
     D AntLest         S              9  0

      *get input-string
      /copy syspeds/qrpgsrcpy,prolog3

      * Parse input
     C                   exsr      Parse
      * Open files etc
     C                   EXSR      INIT
      *
     c                   callp     gethtmlifs(
     C                             '/syspeddev/html/JSON.html':
     C                             '/CGITAG/')
     C                   callp     wrtsection('top')
      *
      * ............................................................................................
      * skriv JSON-Object start..(alltid '{' + x ant param. m/komma mellom (IKKE komma etter siste))
      * ............................................................................................
      *
      /free
        JSONstring='{'
        +'¬user¬ : ¬'+%trim(WSUSER)+'¬, '
        +'¬sidp¬ : ¬'+%trim(SIDP)+'¬, '
        +'¬sitst¬ : ¬'+%trim(SITST)+'¬, '
        +'¬svvnt¬ : ¬'+%trim(SVVNT)+'¬, '
        +'¬svlk¬ : ¬'+%trim(SVLK)+'¬, '
        +'¬svtn¬ : ¬'+%trim(SVTN)+'¬, '
        +'¬errMsg¬ : ¬'+%trim(Error)+'¬, ';
      /end-free
      * JSONfix escaper " i tekst,  for deretter å bytte ¬->"
     c                   call      'JSONFIX'
     c                   parm                    JSONstring
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')
      *
      * ............................................................................................
      * Skriv JSON-LISTE Start (en liste er EN parameter(fra [ til   )
      * ............................................................................................
     C                   eval      JSONstring ='"getpreferanse" : ['
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')
      *
      * Hæmta detta ena ærendet.
      *
     c     Error         ifeq      *blanks
     C                   EXSR      GETPRE
      * ............................................................................................
      * Skriv en JSON-Array-linje pr menypunkt - komma før hvert nytt element
      * ............................................................................................
      /free
          JSONstring=*blanks;
       JSONstring=%trim(JSONstring)+'{'
          +'¬svpre¬ : ¬'+%trim(SVPRE)+'¬}';
      /end-free
     C                   call      'JSONFIX'
     C                   parm                    JSONstring
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')
      *
     c                   endif
     c                   eval      JSONstring =']'
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')
      * ............................................................................................
      * Skriv JSON-string Avsluttning
      * ............................................................................................
     c                   eval      JSONstring ='}'
     C                   CALLP     updHTMLvar2('JSONstring'
     C                                         :%addr(JSONstring)
     C                                         :%len(JSONstring))
     C                   callp     wrtsection('JSONlin')
      *
      * Quit
     C                   exsr      Exit


      *=====================================================================
      * Close output html and quit
      *=====================================================================
     C     Exit          begsr
      * Lukk fil
     C                   CLOSE     BRIDF
     C  N50              CLOSE     KODTS2
     C  N50              CLOSE     TARI

      * Do not delete the call to wrtsection with section name *fini.  It is needed
      * to ensure that all output html that has been buffered gets output.
     C                   callp     wrtsection('*fini')
      * Quit
     C                   MOVE      *ON           *INLR
     C                   return
     C                   endsr
      *
      *********************************************************
     C     Parse         Begsr
      *********************************************************
      * Get input
     C                   EVAL      WSUSER  = zhbgetvar('USER')
     C                   EVAL      SIDP    = zhbgetvar('SIDP')
     C                   EVAL      SIDPN   = c2n2(SIDP)
     C                   EVAL      SITST   = zhbgetvar('SITST')
     C                   EVAL      SITSTN  = c2n2(SITST)
     C                   EVAL      SVVNT   = zhbgetvar('SVVNT')
     C                   EVAL      SVVNTN  = c2n2(SVVNT)
     C                   EVAL      SVLK    = zhbgetvar('SVLK')
     C                   EVAL      SVTN    = zhbgetvar('SVTN')
     c                   endsr
      *
      *********************************************************
     C     INIT          Begsr
      *********************************************************
     C                   OPEN      BRIDF
      * Test innlogging
     C     WSUSER        CHAIN     BRIDF                              50
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C                   call      BILIBL
     C                   end

     C     KODTS2K       KLIST
     C                   KFLD                    XS2UNI
     C                   KFLD                    SVLK
     C                   MOVE      'S2 '         XS2UNI            3
     C     TARIK         KLIST
     C                   KFLD                    SVVNTN
     C                   KFLD                    X2TAR
     C                   MOVE      *BLANKS       X2TAR             3
     C     TARIKM        KLIST
     C                   KFLD                    SVVNTN
     C                   KFLD                    X2TAR
     C                   KFLD                    XADATO
     C                   MOVE      99999999      XADATO            8 0
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVE      WDT           USIDT             8 0

     C   50              eval      Error = 'Invalid userID'
     C  N50              OPEN      KODTS2
     C  N50              OPEN      TARI

     c                   endsr
      *
      *_________________________________________________
      * Hent preferanse                           GETPRE
      *"""""""""""""""""""""""""""""""""""""""""""""""""
     C     GETPRE        BEGSR
      * CHAIN OPPRINNELSESLAND / TARIFFNR (FOR Å KUNNE SETTE PREF)
     C     KODTS2K       CHAIN     KODTS2

     C     TARIKM        SETLL     TARI
     C     TARIK         READPE    TARI                                   87
     C     *IN87         DOWEQ     '0'
     C     TADATO        IFLE      USIDT
N06  C     TADTS         ANDEQ     0
N06  C     TADATO        ORLE      USIDT
N06  C     TADTS         ANDGE     USIDT
     C                   LEAVE
     C                   ENDIF
     C     TARIK         READPE    TARI                                   87
     C                   ENDDO

      * DERSOM LANDKODEN TILLATER PREFERANSE OG DET IKKE ER BEVISST
      * SATT "N" FOR INGEN PREF. ER PREF "P" DEFAULT (KOLONNE ETTER LK)
      * (MEN PREF SKAL IKKE SETTES DERSOM VARENR ER FRITT "I SEG SELV")
      * (PREF PÅ VARE SOM ER FRI I KOL. 1  GAV FEIL I TVINN - ENNÅ????)
     C     SVTN          IFEQ      ' '
     C     SVPRE         ANDEQ     ' '
     C     SIDPN         IFEQ      71
      * TRANSIT/FRILAGER....
     C     KS2PRE        IFEQ      ' '
     C                   MOVE      'N'           SVPRE
     C                   ELSE
     C                   MOVE      'J'           SVPRE
     C                   ENDIF

     C                   ELSE
      * ... ANNET (IKKE PROS 71.)
     C* VED REISEGODS BEHOLD ' '(->N) I PREF ELLERS SETT PREF ETTER LK
     C     SVVNTN        IFEQ      *ZEROS
     C     SIDPN         ANDEQ     44
     C     SITSTN        ANDEQ     9
     C                   MOVE      'N'           SVPRE
     C                   ELSE
     C                   MOVE      KS2PRE        SVPRE

     C  N87              IF        ((SVPRE ='A') OR (SVPRE = 'B'))
     C                   IF        TAORDB = TAEFB
     C                   EVAL      SVPRE = 'N'
     C                   ENDIF
     C                   ENDIF

     C                   ENDIF

     C     SVPRE         IFEQ      'B'
     C     SVLK          ANDNE     'CH'
     C     SVLK          ANDNE     'LI'
     C     SVLK          ANDNE     'RO'
     C     SVLK          ANDNE     'BG'
     C     TAEØSB        ANDLE     TAEFB
     C                   MOVE      'A'           SVPRE
     C                   ENDIF
     C     SVPRE         IFEQ      'C'
     C     SVLK          ANDNE     'CH'
     C     SVLK          ANDNE     'LI'
     C     TAEØSB        ANDLE     TAEFTB
     C                   MOVE      'A'           SVPRE
     C                   ENDIF
     C                   END
     C                   END
     C                   ENDSR
