     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      REF(*LIBL/CUNDC)
     A                                      PRINT
     A                                      ALTHELP
     A                                      ALTPAGEUP
     A                                      ALTPAGEDWN
     A                                      HELP
      *
     A          R VCUNDCD1
     A                                      CF03(03 'EXIT')
     A  89                                  CF11(11 'DELETE')
     A                                      CF12(12 'CANCEL')
     A                                      SLNO(01)
     A                                      CLRL(24)
     A                                      OVERLAY
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1  2'MAIL05R'
     A                                      COLOR(BLU)
     A                                  1 20'Arbeide med Kunders Kontakter'
     A                                      COLOR(WHT)
     A                                  1 67'Dato:'
     A                                      COLOR(BLU)
     A                                  1 73DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  2  2USER
     A                                      COLOR(BLU)
     A                                  2 67'Tid.:'
     A                                      COLOR(BLU)
     A                                  2 73TIME
     A                                      COLOR(BLU)
     A                                  3  2'Kunde:'
     A            ÆCOMPN         8Y 0O  3  9
     A            KNAVN         30A  O  3 19
     A            WMODE          7A  O  5 27COLOR(WHT)
     A                                  7  2'Kontaktperson  . . . :'
     A            CCONTA    R        B  7 27
     A  61                                  DSPATR(RI)
     A  88                                  DSPATR(PR)
     A                                  8  4'Funksjon . . . . . :'
     A            CTYPE     R        B  8 27
     A  62                                  DSPATR(RI)
     A                                      DSPATR(PR)
     A                                  9  6'Telefon  . . . . :'
     A            CPHONE    R        B  9 27
     A  88                                  DSPATR(PR)
     A                                 10  6'Mobiltelefon . . :'
     A            CMOBIL    R        B 10 27
     A  88                                  DSPATR(PR)
     A                                 11  6'Fax  . . . . . . :'
     A            CFAX      R        B 11 27
     A  88                                  DSPATR(PR)
     A                                 12  6'E-Mail . . . . . :'
     A            CEMAIL    R        B 13  6
     A  88                                  DSPATR(PR)
     A  63                                  DSPATR(RI)
     A                                      CHECK(LC)
     A                                 23  2'F3=Avslutt'
     A                                      COLOR(BLU)
     A  89                             23 15'F11=Bekreft sletting'
     A                                      COLOR(BLU)
     A                                 23 38'F12=Retur '
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                             24 63'Systema A/S'
     A                                      COLOR(WHT)
      *
     A          R VCUNDCD3
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF03(03 'EXIT')
     A N31                                  CF05(05 'REFRESH')
     A                                      CF06(06 'NEW')
     A                                      CF12(12 'CANCEL')
     A                                      SLNO(23)
     A                                      CLRL(02)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  3'F3=Exit'
     A                                      COLOR(BLU)
     A                                  1 28'F6=Lage ny'
     A                                      COLOR(BLU)
     A N31                              1 13'F5=Frisk opp'
     A                                      COLOR(BLU)
     A                                  1 41'F12=Retur'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  2  2MSGID(&MSGNR SYMSGF)
     A            MSGNR          7A  P
     A                                  2 63'Systema A/S'
     A                                      COLOR(WHT)
      *
     A          R VCUNDCD4                  SFL
     A            SFRRN         10S 0H
     A            CURS1          4S 0H
     A            WSNR           2A  I  9  2VALUES('  ' '2 ' '02' ' 2' '3 ' '03-
     A                                      ' ' 3' '4 ' '04' ' 4' '5 ' '05' ' 5-
     A                                      ' '1 ' ' 1' 'F ' ' F' 'E ' ' E')
     A            SFONTA        30A  O  9  6
     A            SFYPE         28A  O  9 37
     A            SFHONE        15A  O  9 66
      *
     A          R VCUNDCD5                  SFLCTL(VCUNDCD4)
     A                                      SFLSIZ(0014)
     A                                      SFLPAG(0013)
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A                                  1  2'MAIL05R'
     A                                      COLOR(BLU)
     A                                  1 20'Arbeide med Kunders Kontakter'
     A                                      COLOR(WHT)
     A                                  1 67'Dato:'
     A                                      COLOR(BLU)
     A                                  1 73DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  2  2USER
     A                                      COLOR(BLU)
     A            ZRECNO         4S 0O  2 15SFLRCDNBR(CURSOR)
     A                                      DSPATR(ND)
     A                                  2 67'Tid.:'
     A                                      COLOR(BLU)
     A                                  2 73TIME
     A                                      COLOR(BLU)
     A                                  3  2'Kunde:'
     A            ÆCOMPN         8Y 0O  3  9
     A            KNAVN         30A  O  3 19
     A                                  5  2'Velg og trykk Enter'
     A                                      COLOR(BLU)
     A                                  6  2'2=Endre   3=Kopier   4=Slett   5=V-
     A                                      ise '
     A                                      COLOR(BLU)
     A                                  8  2'Opt Kontaktperson                 -
     A                                       Funksjon'
     A                                      COLOR(WHT)
     A                                  8 66'Telefon'
     A                                      COLOR(WHT)
     A                                  7  2'1=plukk E=epost F=fax'
     A                                      COLOR(BLU)
     A  55                              4  2MSGCON(015 JOV1393 *LIBL/SYMSGF)
     A  55        KUNFUN        30   O  4 19
