      *
      ***********************************************************
      *                                                        **
      *** PROGRAM SKAL KONVERTER ordre FOR SCHENKER           ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01-24                            ***
      *** LEDIGE INDIKATORER 27-32 34-99                      ***
      ***********************************************************
      *
     FCUNDLFO   if   E           K DISK
     Fheadf     O  A E           K DISK
     Ftell      UF   E           K DISK
     Ffirm      IF   E           K DISK
     FXMLRCAF   IF   E           K DISK
     FXMLRCVF   IF   E           K DISK
     FXMLRCVL01 IF   E           K DISK
     F                                     RENAME(XMLRCVFR:XMLRCVFR1)
      *
     D                 DS
     D  XTXT                   1     15
     D  XT                     1     15    DIM(15)
     D                 DS
     D  XTAL                   1     15  3
     D  XTALH                  1     12  0
     D  XTALD1                13     13
     D  XTALD2                14     14
     D  XTALD3                15     15
N04  D testpath        C                   CONST('/sgi_general/order/package_-
N04  D                                     out2in/goods/goods_description')
     C                   EXSR      INIT
     c                   exsr      HEADINI
     C                   EXSR      mottager
     C                   EXSR      avsender
     c                   EXSR      transport
     C                   EXSR      totaler
     C                   EXSR      varebes
     c                   exsr      HEADend
      *
     C                   EVAL      *INLR = *ON
     C                   RETURN
      *
      ***********************************************
     C     mottager      BEGSR
      ***********************************************
      *
     C                   EVAL      X004_XRLEV1 = 2
     C                   EVAL      X004_XRLEV2 = 16
     C                   EVAL      X004_XRLEV3 = 4
     C                   EVAL      X004_XRLEV4 = 1
     C                   EVAL      X004_XRLEV5 = 2
      *  name
      *  iso_country
      *  region
      *  physical
      *  postal_code
      *  city
      *  street
      *  street_number
      *
      *
      *
      *
     C     XMLKEY04      CHAIN     XMLRCVF                            33
     C                   DOW       *IN33 = *OFF                                 1<-B
     C                   SELECT                                                  2<-B
      * navn
     C                   WHEN      XRNV = 'name'                                 2<-B
     C                   EVAL      henak=xrverd
      * postnummer
     C                   WHEN      XRNV = 'postal_code'                          2<-B
     C                   EVAL      hesdt=xrverd
     C                   EVAL      headk3=xrverd
      * by
     C                   WHEN      XRNV = 'city'                                 2<-B
     C                   cat       xrverd:1      headk3
      * street
     C                   WHEN      XRNV = 'street'                               2<-B
     C                   EVAL      headk1=xrverd
      *street_number
     C                   WHEN      XRNV = 'street_number'                        2<-B
     C                   cat       xrverd:1      headk1
      * land
     C                   WHEN      XRNV = 'iso_country'                          2<-B
     C                   EVAL      helkk=xrverd
     c                   ENDSL                                                   2<-E
     C     XMLKEY04      READE     XMLRCVF                                33
     C                   ENDDO                                                  1<-E
      *  finn aktuell kundenummer
     C                   EVAL      X004_XRLEV1 = 2
     C                   EVAL      X004_XRLEV2 = 16
     C                   EVAL      X004_XRLEV3 = 4
     C                   EVAL      X004_XRLEV4 = 3
     C                   EVAL      X004_XRLEV5 = 1
      *
      * PRØVER Å FINNE KUNDENUMMER
      *
     C     XMLKEY04      CHAIN     XMLRCVF                            33
     C     *IN33         IFEQ      '0'
     c                   MOVEL     XRVERD        DATA11           11
     C                   MOVEL     DATA11        DATA02            2
     C                   MOVE      DATA11        DATA09            9
     C                   MOVEL     DATA09        SYRG
     C     DATA02        IFEQ      'NO'
     c     CUNKEY        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    SYRG
     c                   SETOFF                                       33
     C     CUNKEY        SETLL     CUNDLFO
     C     *IN33         DOWEQ     '0'
     c     CUNKEY        reade     CUNDLFO                                33
     C     *IN33         IFEQ      '0'
     c     knavn         ANDEQ     HENAK
     C                   MOVE      KUNDNR        HEKNK
     C                   MOVE      KUNDNR        HEKNKf
     c                   ENDIF
     C                   ENDDO
     c                   ENDIF
     c                   ENDIF
     C                   ENDSR
      ***********************************************
     C     avsender      BEGSR
      ***********************************************
      *
     C                   EVAL      X004_XRLEV1 = 2
     C                   EVAL      X004_XRLEV2 = 14
     C                   EVAL      X004_XRLEV3 = 12
     C                   EVAL      X004_XRLEV4 = 1
     C                   EVAL      X004_XRLEV5 = 2
      *  name
      *  iso_country
      *  region
      *  physical
      *  postal_code
      *  city
      *  street
      *  street_number
      *
      *
      *
      *
     C     XMLKEY04      CHAIN     XMLRCVF                            33
     C                   DOW       *IN33 = *OFF                                 1<-B
     C                   SELECT                                                  2<-B
      * land
     C                   WHEN      XRNV = 'iso_country'                          2<-B
     C                   EVAL      helka=xrverd
      * navn
     C                   WHEN      XRNV = 'name'                                 2<-B
     C     henas         ifeq      *blanks
     C                   EVAL      henas=xrverd
     c                   end
      * postnummer
     C                   WHEN      XRNV = 'postal_code'                          2<-B
     C                   EVAL      hesdf=xrverd
     C                   EVAL      heads3=xrverd
      * by
     C                   WHEN      XRNV = 'city'                                 2<-B
     C                   cat       xrverd:1      heads3
      * street
     C                   WHEN      XRNV = 'street'                               2<-B
     C                   EVAL      heads1=xrverd
      *street_number
     C                   WHEN      XRNV = 'street_number'                        2<-B
     C                   cat       xrverd:1      heads1
      * land
     C                   WHEN      XRNV = 'iso_country'                          2<-B
     C                   EVAL      helks=xrverd
     C                   ENDSL                                                   2<-E
     C     XMLKEY04      READE     XMLRCVF                                33
     C                   ENDDO                                                  1<-E
      *
     C                   ENDSR
      ***********************************************
     C     transport     BEGSR
      ***********************************************
      *
     C                   EVAL      X001_XRLEV1 = 2
     C                   EVAL      X001_XRLEV2 = 15
      *
     C     XMLKEY01      CHAIN     XMLRCVF                            33
     C                   DOW       *IN33 = *OFF                                 1<-B
     C                   SELECT                                                  2<-B
      *
     C                   WHEN      XRNV = 'conveyance_number'                    2<-B
     C                   EVAL      hehawb=xrverd
     C                   WHEN      XRNV = 'transport_means_name'
     c                   setoff                                       34
     c     XMLKEY09A     setll     XMLRCAF
     c     *in34         doweq     '0'
     c     XMLKEY09A     reade     XMLRCAF                                34
     c     *in34         ifeq      '0'
     c     XRATN         andeq     'nationality'
     C                   EVAL      hetri=xratv
     c                   endif
     c                   enddo
     C                   ENDSL                                                   2<-E
     C     XMLKEY01      READE     XMLRCVF                                33
     C                   ENDDO                                                  1<-E
      *
     C                   ENDSR
      *
      ***********************************************
     C     totaler       BEGSR
      ***********************************************
      *
     C                   EVAL      XRNV='measure_value'
     c                   setoff                                       33
     C     KEYTAG        SETLL     XMLRCVL01
     C                   DOW       *IN33 = *OFF                                 1<-B
     C     KEYTAG        READE     XMLRCVL01                              33
     c     *in33         ifeq      '0'
     C                   SELECT                                                  2<-B
      *
     C                   WHEN      XRPATH='/sgi_general/order/total_measures'
     C                   MOVEL     Xrverd        XTXT
     C                   EXSR      KNVTAL
     c     XMLKEY09A     chain     XMLRCAF                            34
     c     *in34         ifeq      '0'
      *
     c     XRATV         IFEQ      'PK'
     c                   z-add     xtal          hent
     c                   eval      hevs1='PK'
     C                   end
      *
     c     XRATV         IFEQ      'KGM'
     c                   z-add     xtal          hevkt
     c                   z-add     xtal          hefbv
     C                   end
      *
     c     XRATV         IFEQ      'MTQ'
     c                   z-add     xtal          hem3
     C                   end
     c                   end
     c                   end
     C                   END
     C                   enddo
     C                   ENDSR
      ***********************************************
     C     varebes       BEGSR
      ***********************************************
      *
     C                   EVAL      XRNV='text'
     c                   setoff                                       33
     C     KEYTAG        SETLL     XMLRCVL01
     C                   DOW       *IN33 = *OFF                                 1<-B
     C     KEYTAG        READE     XMLRCVL01                              33
     c     *in33         ifeq      '0'
     C                   SELECT                                                  2<-B
      *
     C                   WHEN      XRPATH=testpath
     C                   cat       xrverd:1      hevs1
     C                   ENDSL                                                   2<-E
     C                   END
     C                   enddo
     C                   ENDSR
      ***********************************************
     C     INIT          BEGSR
      ***********************************************
     C     *ENTRY        PLIST
     C                   PARM                    USN               9
      *
     C     keytag        KLIST
     C                   KFLD                    XRSN
     c                   kfld                    XRNV
     C     XMLKEY01      KLIST
     C                   KFLD                    XRSN
     C                   KFLD                    X001_XRLEV1
     C                   KFLD                    X001_XRLEV2
     C     XMLKEY02      KLIST
     C                   KFLD                    XRSN
     C                   KFLD                    X002_XRLEV1
     C                   KFLD                    X002_XRLEV2
     C                   KFLD                    X002_XRLEV3
     C     XMLKEY03      KLIST
     C                   KFLD                    XRSN
     C                   KFLD                    X003_XRLEV1
     C                   KFLD                    X003_XRLEV2
     C                   KFLD                    X003_XRLEV3
     C                   KFLD                    X003_XRLEV4
     C     XMLKEY04      KLIST
     C                   KFLD                    XRSN
     C                   KFLD                    X004_XRLEV1
     C                   KFLD                    X004_XRLEV2
     C                   KFLD                    X004_XRLEV3
     C                   KFLD                    X004_XRLEV4
     C                   KFLD                    X004_XRLEV5
     C     XMLKEY05      KLIST
     C                   KFLD                    XRSN
     C                   KFLD                    X005_XRLEV1
     C                   KFLD                    X005_XRLEV2
     C                   KFLD                    X005_XRLEV3
     C                   KFLD                    X005_XRLEV4
     C                   KFLD                    X005_XRLEV5
     C                   KFLD                    X005_XRLEV6
     C     XMLKEY06      KLIST
     C                   KFLD                    XRSN
     C                   KFLD                    X006_XRLEV1
     C                   KFLD                    X006_XRLEV2
     C                   KFLD                    X006_XRLEV3
     C                   KFLD                    X006_XRLEV4
     C                   KFLD                    X006_XRLEV5
     C                   KFLD                    X006_XRLEV6
     C                   KFLD                    X006_XRLEV7
     C     XMLKEY07      KLIST
     C                   KFLD                    XRSN
     C                   KFLD                    X007_XRLEV1
     C                   KFLD                    X007_XRLEV2
     C                   KFLD                    X007_XRLEV3
     C                   KFLD                    X007_XRLEV4
     C                   KFLD                    X007_XRLEV5
     C                   KFLD                    X007_XRLEV6
     C                   KFLD                    X007_XRLEV7
     C                   KFLD                    X007_XRLEV8
     C     XMLKEY09A     KLIST
     C                   KFLD                    XRSN
     C                   KFLD                    XRLEV1
     C                   KFLD                    XRLEV2
     C                   KFLD                    XRLEV3
     C                   KFLD                    XRLEV4
     C                   KFLD                    XRLEV5
     C                   KFLD                    XRLEV6
     C                   KFLD                    XRLEV7
     C                   KFLD                    XRLEV8
     C                   KFLD                    XRLEV9
      *
     C     *LIKE         DEFINE    XRLEV1        X001_XRLEV1
     C     *LIKE         DEFINE    XRLEV1        X001_XRLEV1
     C     *LIKE         DEFINE    XRLEV2        X001_XRLEV2
     C     *LIKE         DEFINE    XRLEV1        X002_XRLEV1
     C     *LIKE         DEFINE    XRLEV2        X002_XRLEV2
     C     *LIKE         DEFINE    XRLEV3        X002_XRLEV3
     C     *LIKE         DEFINE    XRLEV1        X003_XRLEV1
     C     *LIKE         DEFINE    XRLEV2        X003_XRLEV2
     C     *LIKE         DEFINE    XRLEV3        X003_XRLEV3
     C     *LIKE         DEFINE    XRLEV4        X003_XRLEV4
     C     *LIKE         DEFINE    XRLEV1        X004_XRLEV1
     C     *LIKE         DEFINE    XRLEV2        X004_XRLEV2
     C     *LIKE         DEFINE    XRLEV3        X004_XRLEV3
     C     *LIKE         DEFINE    XRLEV4        X004_XRLEV4
     C     *LIKE         DEFINE    XRLEV5        X004_XRLEV5
     C     *LIKE         DEFINE    XRLEV1        X005_XRLEV1
     C     *LIKE         DEFINE    XRLEV2        X005_XRLEV2
     C     *LIKE         DEFINE    XRLEV3        X005_XRLEV3
     C     *LIKE         DEFINE    XRLEV4        X005_XRLEV4
     C     *LIKE         DEFINE    XRLEV5        X005_XRLEV5
     C     *LIKE         DEFINE    XRLEV6        X005_XRLEV6
     C     *LIKE         DEFINE    XRLEV1        X006_XRLEV1
     C     *LIKE         DEFINE    XRLEV2        X006_XRLEV2
     C     *LIKE         DEFINE    XRLEV3        X006_XRLEV3
     C     *LIKE         DEFINE    XRLEV4        X006_XRLEV4
     C     *LIKE         DEFINE    XRLEV5        X006_XRLEV5
     C     *LIKE         DEFINE    XRLEV6        X006_XRLEV6
     C     *LIKE         DEFINE    XRLEV7        X006_XRLEV7
     C     *LIKE         DEFINE    XRLEV1        X007_XRLEV1
     C     *LIKE         DEFINE    XRLEV2        X007_XRLEV2
     C     *LIKE         DEFINE    XRLEV3        X007_XRLEV3
     C     *LIKE         DEFINE    XRLEV4        X007_XRLEV4
     C     *LIKE         DEFINE    XRLEV5        X007_XRLEV5
     C     *LIKE         DEFINE    XRLEV6        X007_XRLEV6
     C     *LIKE         DEFINE    XRLEV7        X007_XRLEV7
     C     *LIKE         DEFINE    XRLEV8        X007_XRLEV8
      *
     C                   MOVE      USN           XRSN
      *
     C     *LOVAL        SETLL     FIRM
     C                   READ      FIRM                                   33
      *
     C                   ENDSR
      ***********************************************
     C     KNVTAL        BEGSR
      ***********************************************
     C                   MOVE      *ZEROS        XTAL
     C     1             DO        15            XN                3 0          1<-B
     C*                  IF        XT(XN) = *BLANKS OR XT(XN) = '.'              2<-B
     C                   IF        XT(XN) = '.' OR XT(XN) = ','                  2<-B
     C                   LEAVE
     C                   ENDIF                                                   2<-E
     C                   IF        XT(XN) <> *BLANKS                             2<-B
     C                   MULT      10            XTALH
     C                   MOVE      XT(XN)        XTALH
     C                   ENDIF                                                   2<-E
     C                   ENDDO                                                  1<-E
      *
     C                   IF        XN < 16                                      1<-B
     C                   IF        XT(XN) = '.'                                  2<-B
     C                   ADD       1             XN
     C                   MOVE      XT(XN)        XTALD1
     C                   ADD       1             XN
     C                   IF        XT(XN) <> ' '                                  3<-B
     C                   MOVE      XT(XN)        XTALD2
     C                   ADD       1             XN
     C                   IF        XT(XN) <> ' '                                   4<-B
     C                   MOVE      XT(XN)        XTALD3
     C                   END                                                       4<-E
     C                   END                                                      3<-E
     C                   END                                                     2<-E
     C                   END                                                    1<-E
      *
     C                   ENDSR
      *
      ***********************************************
     C     HEADINI       BEGSR
      ***********************************************
      *
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVE      WDT           WDT08             8 0
     C                   Z-ADD     WDT08         HEDTR
     C                   Z-ADD     WDT08         HEDTOP
     c                   MOVE      'A'           HEUR
     C                   MOVE      'N'           HEPK1
     C                   MOVE      'N'           HEPK2
     C                   MOVE      'N'           HEPK3
     C                   MOVE      'J'           HEPK4
     C                   MOVE      'X'           HEKDFS
     c                   z-add     1             heavd
     C     HEAVD         CHAIN     TELL                               33
     C                   MOVE      TEOPDN        heopd
     C                   ADD       1             TEOPDN
     C                   UPDATE    TELLR
     C                   EVAL      X002_XRLEV1 = 2
     C                   EVAL      X002_XRLEV2 = 4
     C                   EVAL      X002_XRLEV3 = 2
     C     XMLKEY02      CHAIN     XMLRCVF                            33
     c  N33              eval      hefr=xrverd
     c                   ENDSR
      ***********************************************
     C     HEADEND       BEGSR
      ***********************************************
     c                   WRITE     HEADFR
     c                   ENDSR
