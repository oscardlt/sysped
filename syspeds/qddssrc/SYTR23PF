*****A**************************************************************
     A* RETTINGER I DETTE PROGRAM:
PTFnrA*Sek Sig Vers Beskrivelse...........................
"""""A*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10261 * 01 YBC PTF10 VEKT/KUBIKK/KOLLI I TURER ER ENDRET TIL 9.0, 7.3 OG 7.0
*****A**************************************************************
      *_________________________________
      * Format for markering av ny side.
      *"""""""""""""""""""""""""""""""""
     A          R KJØP1                     SPACEB(2)
     A  70                                65'** Continues **'
     A N70N79                             65MSGCON(15 DIV1673 SYMSGF)
     A  79                                65'* Fortsetzung *'
      *_____________________________________________
      * Format for hovedopplysninger vanlig faktura.
      *"""""""""""""""""""""""""""""""""""""""""""""
     A          R KJØP
     A            KFMT1         75      1  1
     A  46        HOHT1         80      1  1
     A  46        HOHT2         80      2  1
     A  46        HOHT3         80      3  1
     A  46        HOHT4         80      4  1
     A  46        HOHT5         80      5  1
     A  46        HOHT6         80      6  1
     A  46        HOHT7         80      7  1
     A            YNAVN     R          11 11REFFLD(KNAVN CUNDF)
     A N88N51                          11 48'******************************'
     A N88N51                          11 48'******************************'
     A N88
     AA 51N70N79                       11 48MSGCON(30 DIV1674 SYMSGF)
     A N88
     AA 51N70N79                       11 48MSGCON(30 DIV1674 SYMSGF)
     A N88 51 70                       11 48'*** COPY  OF   ***************'
     A N88 51 70                       11 48'*** COPY  OF   ***************'
     A N88 51 79                       11 48'*** KOPI  VON  ***************'
     A N88 51 79                       11 48'*** KOPI  VON  ***************'
     A  70 88                          11 48'********  P A G E '
     A  70 88                          11 48'********  P A G E '
     A N70N79 88                       11 48MSGCON(24 DIV1676 SYMSGF)
     A N70N79 88                       11 48MSGCON(24 DIV1676 SYMSGF)
     A  79 88                          11 48'********  S E I T E'
     A  79 88                          11 48'********  S E I T E'
     A  88        XSIDE          2  0  11 71EDTCDE(Z)
     A  88                             11 75'***'
     A  88                             11 75'***'
     A            YADR1     R          12 11REFFLD(ADR1 CUNDF)
     A                                 12 48'*'
     A                                 12 48'*'
     A                                 12 77'*'
     A                                 12 77'*'
     A N70N79                          12 50MSGCON(26 DIV2406 SYMSGF)
     A N70N79                          12 50MSGCON(26 DIV2406 SYMSGF)
     A  70                             12 48'*   C R E D I T N O T E '
     A  70                             12 48'*   C R E D I T N O T E '
     A  79                             12 48'*   G U T S C H R I F T '
     A  79                             12 48'*   G U T S C H R I F T '
     A            YADR2     R          13 11REFFLD(ADR2 CUNDF)
     A N57                             13 48'******************************'
     A N57                             13 48'******************************'
     A            YADR3         35     14 11
     A N78
     AAN70N79N77                       14 48MSGCON(15 DIV2411 SYMSGF)
     A  70N78N77                       14 48'Credit note no:'
     A  79N78N77                       14 48'Gutschrift nr.:'
     A N78
     AAN70N79 77                       14 48MSGCON(15 JOV0174 SYMSGF)
     A  70N78 77                       14 48'Voucher no....:'
     A  79N78 77                       14 48'Abrechnungs nr:'
     A N78        XFN            7S 0  14 71EDTCDE(Z)
     A N70N79 78                       14 48MSGCON(30 JOV1164 SYMSGF)
     A  70 78                          14 48'* BASIS FOR TRANSP. INVOICE * '
     A  79 78                          14 48'* UNTERLAG FUR TRANSP-ABRECHN*'
     A            KLKNVN    R          15 11REFFLD(KLKNVN KODTLK)
     A N70N79N77                       15 48MSGCON(15 DIV1668 SYMSGF)
     A  70   N77                       15 48'Customer no...:'
     A  79   N77                       15 48'Kunden nr.....:'
     A N70N79 77                       15 48MSGCON(15 JOV0175 SYMSGF)
     A  70    77                       15 48'Vendor no.....:'
     A  79    77                       15 48'Kunden nr.....:'
     A N77        YKUNDN    R          15 70REFFLD(KUNDNR CUNDF)
     A  77        VMTRLE         8S 0  15 70
     A                                      EDTCDE(Z)
     A N70N79                          16 48MSGCON(15 DIV2254 SYMSGF)
     A  70                             16 48'Transporter no:'
     A  79                             16 48'Befrachter nr.:'
     A            THTNR          8S 0  16 70EDTCDE(Z)
     A N70N79N77                       17 48MSGCON(15 DIV2412 SYMSGF)
     A  70   N77                       17 48'Date cred. note'
     A  79   N77                       17 48'Gutschr. datum:'
     A N70N79 77                       17 48MSGCON(15 JOV0176 SYMSGF)
     A  70    77                       17 48'Voucher date..:'
     A  79    77                       17 48'Datum.........:'
     A            IDAG3          2S 0  17 68
     A                                 17 70'/'
     A            IDAG2          2S 0  17 71
     A                                 17 73'/'
     A            IDAG1          4S 0  17 74
     A N70N79                          18 48MSGCON(15 DIV2367 SYMSGF)
     A  70                             18 48'Inv. periode..:'
     A  79                             18 48'Rechn.periode.:'
     A            W1PERM         2  0  18 73
     A                                 18 75'/'
     A            W1PERÅ         2  0  18 76
     A*
     A N70N79                          20  3MSGCON(78 DIV2407 SYMSGF)
     A  70                             20  3'Roundtrip Dpt.    Trip Dep.date Fr+
     A                                       om                 To '
     A  79                             20  3'Rundreise  Abt.   Reise Datum   Vo+
     A                                       n                  Nach '
S01  A*N70N79                          21  3MSGCON(78 DIV2408 SYMSGF)
N01  A N70N79                          21  3MSGCON(78 YBC2001 SYMSGF)
     A  70                             21  3'                  Unit   Trailer +
S01  A*                                         Orders Weight Loa.m. Cube.m.'
N01  A                                          Orders    Weight Loa.m. Cube.m.'
     A  79                             21  3'                  LKW    Trailer +
S01  A*                                       Anz. Ord  Gewi. Ladem. Kubik'
N01  A                                        Anz. Ord     Gewi. Ladem. Kubik'
     A                                 21  2'_______________________'
     A                                 21 25'________________________'
     A                                 21 49'________________________'
     A                                 21 73'________'
     A                                 22  1' '
      *_______________________________
      * POSTER  FOR GODSKRIVING VED REKV.
      *"""""""""""""""""""""""""""""""
     A          R THEAD                     SPACEB(1)
     A                                     3MSGCON(78 JOV0150 SYMSGF)
     A          R TDET                       SPACEB(1)
     A            BUBILN        10         3
     A            BUDATO         6  0     13EDTWRD('  .  .  ')
     A  62        TVTXT         35        22
     A N62        TVTXTX        25        22
     A N62                                48'Bn:'
     A N62        BUBNR2         7  0     51EDTCDE(Z)
     A            TVBLL         11  2     58EDTCDE(J)
     A  34                                79'*'
     A          R GHEAD                     SPACEB(1)
     A                                     3MSGCON(78 DIV2409 SYMSGF)
     A          R GDET                       SPACEB(1)
     A            BUBILN        10         3
     A            BUDATO         6  0     13EDTWRD('  .  .  ')
     A            TVTXT         35        23
     A          R GDET2                      SPACEB(1)
     A                                    11'Oppd.nr:'
     A            BUOAVD         4  0     19EDTCDE(Z)
     A                                    23'/'
     A            BUOPD          7  0     24EDTCDE(Z)
     A                                    33'Turnr:'
     A            BUTAVD         4  0     39EDTCDE(Z)
     A                                    43'/'
     A            BUTUNR         8  0     44EDTCDE(Z)
     A            TVBLL         11  2     56EDTCDE(J)
     A  34                                79'*'
     A* TOTALSUM - BARE GODSKRIVINGER
     A          R GDETT                      SPACEB(1)
     A                                    11MSGCON(45 DIV2410 SYMSGF)
     A            XPFNOK        13  2     61EDTCDE(J)
     A* TOTALSUM - BARE TREKK
     A          R TDETT                      SPACEB(1)
     A                                    11MSGCON(45 JOV0165 SYMSGF)
     A            XPFNOK        13  2     61EDTCDE(J)
     A* TOTALSUM - BLANDING GODSKRIVINGER / TREKK
     A          R GTDETT                     SPACEB(1)
     A                                    11MSGCON(45 JOV0166 SYMSGF)
     A            XPFNOK        13  2     61EDTCDE(J)
      *_______________________________
      * Format for faktura-varelinjer.
      *"""""""""""""""""""""""""""""""
     A          R DET                       SPACEB(1)
     A N97        TURUND         8  0      3EDTCDE(Z)
     A            TUAVD          4  0     12EDTCDE(Z)
     A            TUPRO          8  0     17EDTCDE(Z)
     A            TUDATO         6  0     26EDTWRD('  .  .  ')
     A            TUSDF         20        35
     A            TUSDT         20        56
     A          R DETDUM
     A N97        XURUND         8  0      3EDTCDE(Z)
     A            XUAVD          4  0     12EDTCDE(Z)
     A            XUPRO          8  0     17EDTCDE(Z)
     A            XUDATO         6  0     26EDTWRD('  .  .  ')
     A            XUSDF         20        35
     A            XUSDT         20        56
     A          R DETOF                     SPACEB(1)
     A                                    11'Oppdrag fra:'
     A            FR01           5        24
     A            FR02           5        30
     A            FR03           5        36
     A            FR04           5        42
     A            FR05           5        48
     A            FR06           5        54
     A            FR07           5        60
     A            FR08           5        66
     A            FR09           5        72
     A            FR10           5        78
     A          R DETOT                     SPACEB(1)
     A                                    11'Oppdrag til:'
     A            TI01           5        24
     A            TI02           5        30
     A            TI03           5        36
     A            TI04           5        42
     A            TI05           5        48
     A            TI06           5        54
     A            TI07           5        60
     A            TI08           5        66
     A            TI09           5        72
     A            TI10           5        78
     A          R DET2                      SPACEB(1)
     A            TUBILN        10        16
     A            TUHENG        10        26
     A            TUAO           5  0     38EDTCDE(3)
S01  A*           TUTVKT         5  0     44EDTCDE(3)
S01  A*           TUTLM          4  2     50EDTWRD(' 0,  ')
S01  A*           TUTM3          5  2     55EDTWRD('  0,  ')
N01  A            TUTVKT         9  0     44EDTCDE(Z)
N01  A            TUTLM          4  2     54EDTWRD(' 0,  ')
N01  A            TUTM3          7  3     59EDTWRD('   0,   ')
     A          R DETTOT                     SPACEB(1)
     A            XPFNOK        13  2     61EDTCDE(J)
     A  34                                79'*'
     A          R STREK                      SPACEB(1)
     A                                     2'_______________________'
     A                                    25'________________________'
     A                                    49'________________________'
     A                                    73'________'
      *_______________________________
      * JUSTERINGER SOM SKAL VISES
      *"""""""""""""""""""""""""""""""
      * 50 ON - REN KOMMENTAR - BELØP ER 0
     A          R JUSDET                     SPACEB(1)
     A N50                                11MSGCON(12 JOV0083 SYMSGF)
     A            TVTXT         35        23
     A N50        TVBLL         11  2     56EDTCDE(J)
      *_______________________________
      * Samlet avregningsgr.lag
      *"""""""""""""""""""""""""""""""
     A          R GRLAVT                     SPACEB(1)
     A                                    11'** Avtalt honorar ** '
     A          R GRLDET                     SPACEB(1)
     A N61                                11'Grl.:'
     A N61        XGRUNN        11  2     16EDTCDE(J)
     A N61                                31'Sats:'
     A N61        XSATS          5  2     36EDTCDE(J)
     A N61                                43'Netto utbet:'
     A  61                                11'Minus innh./utkj......'
     A            XNETTO        11  2     56EDTCDE(J)
      *__________________________________
      * Format for TREKK - HEADING
      *""""""""""""""""""""""""""""""""""
     A          R TRKHEA                     SPACEB(1)
     A                                    11'Minus diverse trekk...'
      *__________________________________
      * Format for TREKK - DETALJERT
      *""""""""""""""""""""""""""""""""""
     A          R TRKDET                     SPACEB(1)
     A            TVTXT         35        11
     A            TVBLL         11  2     56EDTCDE(J)
      *__________________________________
      * Format for faktura-total.
      *""""""""""""""""""""""""""""""""""
     A          R TOTA0                     SPACEB(1)
     A N70N79                             39MSGCON(22 SHU1122 SYMSGF)
     A  70                                39'Total VAT-free amount'
     A  79                                39'Total kosten ohne Tax'
     A            XMVAFR        13  2     61EDTCDE(J)
     A                                      SPACEA(1)
     A N70N79                             39MSGCON(22 SHU1123 SYMSGF)
     A  70                                39'Total aount for VAT'
     A  79                                39'Total kosten fur tax'
     A            XMVAGR        13  2     61EDTCDE(J)
      *__________________________________
      * Format for faktura-total.
      *""""""""""""""""""""""""""""""""""
     A          R TOTA                      SPACEB(1)
     A N70N79                              3MSGCON(16 DIV1682 SYMSGF)
     A  70                                 3'VAT of (* lines)'
     A  79                                 3'Tax von(* linen)'
     A            XMVAGR        13  2     20EDTCDE(J)
     A N70N79                             39MSGCON(20 DIV1683 SYMSGF)
     A  70                                39'VAT Domestic:      %'
     A  79                                39'Kosten stat:       %'
     A            PRITAX         4  2     52EDTCDE(J)
     A            XVAL           3        61
     A            XMOMS         13  2     61EDTCDE(J)
      *___________________________________
      * Format for faktura-total. IND: N35
      *"""""""""""""""""""""""""""""""""""
     A          R TOTB                      SKIPB(44)
     A N70N79                             45MSGCON(12 DIV1685 SYMSGF)
     A  70                                45'Total:'
     A  79                                45'Gesamtbetrag'
     A            XVAL           3        61
     A            XBRUTT        13  2     61EDTCDE(J)
     A                                     2' '
     A                                      SPACEB(1)
     A                                    45'================================='
      *_________________________________________________
      * Format for faktura-total. without Norwegian giro.
      *""""""""""""""""""""""""""""""""""""""""""""""""""
     A          R TOTC                      SPACEB(4)
     A N70N79                             45MSGCON(12 DIV1685 SYMSGF)
     A  70                                45'Total:'
     A  79                                45'Gesamtbetrag'
     A            XVAL           3        61
     A            XBRUTT        13  2     61EDTCDE(J)
     A                                     2' '
     A                                      SPACEB(1)
     A                                    45'================================'
     A          R TOTBERH
     A            KFMT1         75      1  1
     A  46        HOHT1         80      1  1
     A  46        HOHT2         80      2  1
     A  46        HOHT3         80      3  1
     A  46        HOHT4         80      4  1
     A  46        HOHT5         80      5  1
     A  46        HOHT6         80      6  1
     A  46        HOHT7         80      7  1
     A N70N79                           9  3'Oversikt årets avregninger:'
     A  79                              9  3'Ubersicht dieses jahr'
     A  70                              9  3'Invoices this year'
     A            YNAVN     R           9 32REFFLD(KNAVN CUNDF)
     A            IDAG3          2S 0   9 70
     A                                  9 72'/'
     A            IDAG2          2S 0   9 73
     A                                  9 75'/'
     A            IDAG1          4S 0   9 76
     A                                 10  2'_______________________'
     A                                 10 25'________________________'
     A                                 10 49'________________________'
     A                                 10 73'________'
     A  70                             11  3'Inv.no:'
     A  70                             11 17'Val e no TAX'
     A  70                             11 34'Val with TAX'
     A  70                             11 51'Tax amounth'
     A  70                             11 69'Invoice total'
     A  79                             11  3'Rechnung'
     A  79                             11 17'Ohne kosten stat'
     A  79                             11 34'Mit kosten stat'
     A  79                             11 51'Kosten stat'
     A  79                             11 69'Rechnung Total'
     A N70N79                          11  3'Faktnr:'
     A N70N79                          11 17'Grl uten MVA'
     A N70N79                          11 34'Grl med MVA'
     A N70N79                          11 51'Beregnet mva'
     A N70N79                          11 69'Fakturasum'
     A                                 12  2'_______________________'
     A                                 12 25'________________________'
     A                                 12 49'________________________'
     A                                 12 73'________'
     A          R TOTBERD                   SPACEB(1)
     A            WRFAKT         7  0      2EDTCDE(Z)
     A            UMVA          13  2     12EDTCDE(J)
     A            MMVA          13  2     29EDTCDE(J)
     A            MVA           13  2     46EDTCDE(J)
     A            TOT           13  2     63EDTCDE(J)
     A          R TOTBERL                   SPACEB(2)
     A N70N79                              3'Hittil iår'
     A  70                                 3'Accumulated'
     A  79                                 3'Zusammen'
     A            UMVAL         13  2     12EDTCDE(J)
     A            MMVAL         13  2     29EDTCDE(J)
     A            MVAL          13  2     46EDTCDE(J)
     A            TOTL          13  2     63EDTCDE(J)
