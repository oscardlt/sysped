      *
      ***********************************************************
      *** PROGRAM SKAL VEDLIKEHOLDE E.F. MANIFEST - LISTE     ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01 02 07 09-14 17-24             ***
      *** LEDIGE INDIKATORER 29 31 32       52-75 78-89       ***
      ***********************************************************
      *
     FSADEFCL1  UF   E           K DISK
     FTRACKF    O  A E             DISK
     FSAD132D   CF   E             WORKSTN SFILE(SUBFIL:ZRECNO)
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D                SDS
     D  UUSR                 254    263
      *
     C                   EXSR      INIT
      *
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     UPRO          SETLL     SADEFCL1
     C     START1        TAG
     C     UPRO          READE(N)  SADEFCL1                               94
      *
     C     START2        TAG
      *
     C                   DOW       *IN94 = *OFF
     C                   ADD       1             ZRECNO
     C                   WRITE     SUBFIL
     C     UPRO          READE(N)  SADEFCL1                               94
     C                   ENDDO
     C                   Z-ADD     ZRECNO        YRECNO            4 0
      *
     C                   IF        ZRECNO > 12
     C                   EVAL      ZRECNO = 1
     C                   END
      *
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C                   WRITE     BUNN
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYVARE
     C                   ELSE
     C                   EXFMT     SUBCTL
     C                   END
      *
      *** AVSLUTT
     C                   MOVE      '0'           *IN30
     C                   MOVEA     '00'          *IN(98)
     C   03              Z-ADD     3             UFLAGG
     C   03              GOTO      SLUTT
     C   05              GOTO      START
     C     START4        TAG
     C   06              DO
     C                   EXSR      PLKOPD
     C                   GOTO      START
     C                   ENDDO
     C   12              Z-ADD     1             UFLAGG
     C   12              GOTO      SLUTT
      *
     C     ZRECNO        IFNE      0
     C                   EXSR      HENT
     C                   END
     C                   GOTO      START3
      *
     C     SLUTT         TAG
      *
     C                   SETON                                        LR
     C                   RETURN
      *
      ***********************************************
     C     HENT          BEGSR
      ***********************************************
     C                   MOVE      *BLANKS       MSGNR
     C                   MOVEA     '000'         *IN(90)
     C                   MOVEA     '00'          *IN(98)
     C     *IN98         DOWEQ     '0'                                          1<-B
     C                   READC     SUBFIL                                 98
     C     *IN98         IFEQ      '0'                                           2<-B
     C     CLETYPT       ANDNE     '* SLETTET *'
     C                   SELECT                                                   3<-B
     C     WSNR          WHENEQ    2                                              3
     C     *IN49         ANDEQ     *OFF
     C                   MOVE      '1'           *IN99
     C                   EXSR      CHGOPD
     C     WSNR          WHENEQ    4                                              3
     C     *IN49         ANDEQ     *OFF
     C                   MOVE      '1'           *IN99
     C                   EXSR      SLTOPD
     C     WSNR          WHENEQ    5                                              3
     C                   MOVE      '1'           *IN99
     C                   EXSR      DSPOPD
     C     WSNR          WHENEQ    15                                             3
     C                   MOVE      '1'           *IN99
     C                   EXSR      VISSAD
     C                   ENDSL                                                    3<-E
     C                   END                                                     2<-E
     C                   ENDDO                                                  1<-E
      *
     C     SLHENT        TAG
     C   98
     CANN99              DO
     C                   MOVE      'DIV0012'     MSGNR
     C                   MOVE      '1'           *IN30
     C                   END
     C                   MOVE      '0'           *IN99
      *
     C                   ENDSR
      *
     C***********************************************
     C     DSPOPD        BEGSR
     C***********************************************
     C                   MOVE      'DSP'         XTYPE                          SPØRRING
     C                   CALL      'SAD134R'
     C                   PARM                    UPRO
     C                   PARM      CLAVD         U_AVD             4 0
     C                   PARM      CLTDN         U_TDN             7 0
     C                   PARM                    XTYPE             3
     C                   PARM                    UFLAGG            1 0
     C  N90              MOVE      *ZEROS        WSNR
     C  N90              UPDATE    SUBFIL
      *
     C                   ENDSR
      *
     C***********************************************
     C     VISSAD        BEGSR
     C***********************************************
     C                   CALL      'SAD005A'
     C                   PARM      CLAVD         U_AVD
     C                   PARM      CLTDN         U_TDN
     C                   PARM                    UFLAGG
      *
     C  N90              MOVE      *ZEROS        WSNR
     C  N90              UPDATE    SUBFIL
      *
     C                   ENDSR
      *
     C***********************************************
     C     CHGOPD        BEGSR
     C***********************************************
     C                   MOVE      'CHG'         XTYPE             3
     C                   CALL      'SAD134R'
     C                   PARM                    UPRO
     C                   PARM      CLAVD         U_AVD             4 0
     C                   PARM      CLTDN         U_TDN             7 0
     C                   PARM                    XTYPE
     C                   PARM                    UFLAGG            1 0
      *
     C     SADEFCL1K     CHAIN(N)  SADEFCL1                           33
     C  N90              MOVE      *ZEROS        WSNR
     C  N90              UPDATE    SUBFIL
      *
     C                   ENDSR
      *
     C***********************************************
     C     SLTOPD        BEGSR
     C***********************************************
     C     SADEFCL1K     CHAIN     SADEFCL1                           33
     C                   IF        *IN33 = *OFF
     C                   EVAL      CLST   = ' '
     C                   EVAL      CLPRO  = 0
     C                   UPDATE    SADEFCFR
     C                   EVAL      CLETYPT = '* SLETTET *'
     C                   EXSR      TTSR
     C                   END
      *
     C  N90              MOVE      *ZEROS        WSNR
     C  N90              UPDATE    SUBFIL
      *
     C                   ENDSR
      *
      ***********************************************
     C     PLKOPD        BEGSR
      ***********************************************
     C                   CALL      'SAD133R'
     C                   PARM                    UAVD
     C                   PARM                    UPRO
     C                   PARM                    UUUID
     C                   PARM                    UFLAGG            1 0
      *
     C                   ENDSR
      *
      ***********************************************
     C     TTSR          BEGSR
      ***********************************************
     C                   EVAL      TTAVD  = CLAVD
     C                   EVAL      TTOPD  = CLTDN
     C                   EVAL      TTTUR  = 0
     C                   EVAL      TTACTI = 'EFC'
     C                   MOVE      WDT           TTDATE
     C                   MOVE      WTM           TTTIME
     C                   MOVE      TTDATE        TTDATL
     C                   MOVE      TTTIME        TTTIML
     C                   MOVE      UUSR          TTUSER
     C                   EVAL      TTTEXT = 'Oppdrag er fjernet fra E.F. '
     C                             + 'manifest(' + %TRIM(%EDITC(UPRO:'Z'))
     C                             + ').'
     C                   EVAL      TTTEXL = 'Oppdrag er fjernet fra E.F. '
     C                             + 'manifest(' + %TRIM(%EDITC(UPRO:'Z'))
     C                             + ').'
     C                   EVAL      TTEDEV = 'RMV'
      *
     C                   WRITE     TRACKFR
     C                   ENDSR
      *
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     SADEFCL1K     KLIST
     C                   KFLD                    UPRO
     C                   KFLD                    CLAVD
     C                   KFLD                    CLTDN
      *
     C     *ENTRY        PLIST
     C                   PARM                    UAVD              4 0
     C                   PARM                    UPRO              8 0
     C                   PARM                    UUUID            36
     C                   PARM                    UTYPE             3
     C                   PARM                    UFLAGG            1 0
      *
     C     UTYPE         COMP      'DSP'                                  49
      *
     C                   WRITE     WINDOWSZ
      *
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
      *
     C                   ENDSR
      *
