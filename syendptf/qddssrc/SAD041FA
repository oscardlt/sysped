     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPDOC(SADE009 HELPNU HLP400)
     A                                      HELP
     A                                      ALTHELP
      *
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  4  2VALUES(' ' '2' '4' '5' '9')
     A            SVLI           5Y 0H
     A            SVLN           5Y 0H
     A            SVVKTB         9Y 0H
     A            SVNT           6Y 0H
     A            SVEH           4A  H
     A            SVTOA         22A  H
     A            SVTOB         23A  H
     A            WXLN           5Y 0O  4  5EDTCDE(4)
     A            SVVNT          8S 0O  4 11
     A            SVVKTN         9Y 0O  4 20EDTCDE(4)
     A            SVNTM          7Y 0O  4 30EDTCDE(4)
     A            SVVT          15A  O  4 38TEXT('Varetekst')
     A            SVFT          17A  O  4 54
     A            SV01           1A  O  4 72COLOR(RED)
     A                                      DSPATR(BL)
      *
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0016)
     A                                      SFLPAG(0015)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A            ZRECNO         4S 0O  1 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYSPED/SAD041A'
     A                                      COLOR(BLU)
     A                                  1 21'SAD-TRANSITT'
     A                                      COLOR(WHT)
     A                                  1 40'Tdeknr.'
     A            UAVD           4Y 0O  1 48COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  1 53'-'
     A                                      COLOR(WHT)
     A            UTDN           7Y 0O  1 55COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  2  2'Velg option, 2=Endre 4=Slett 5=Vis-
     A                                      e 9=Ny linje'
     A                                      COLOR(BLU)
     A  37                              2 66'Auto kopi T.O.'
     A                                      DSPATR(BL)
     A                                      COLOR(RED)
     A N32                              3  2'Op Linje Varenr.. Nettovekt Mengde-
     A                                      . Varebeskrivelse Merke & nr.      -
     A                                       Sjø'
     A                                      COLOR(WHT)
     A  32                              3  2'Op Postn Varenr.. Nettovekt Mengde-
     A                                      . Varebeskrivelse Merke & nr.      -
     A                                       Sjø'
     A                                      COLOR(WHT)
      *
     A          R BUNN
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF05(05 'FRISK')
     A                                      CF06(06 'LAGE NY')
     A                                      CF14(14 'SØK KUNDERS VAREREGISTER')
     A                                      CF08(08 'SØK')
     A    N36                               CF07(07 'SØKE OPPDRAG')
     A                                      CF03(03 'Be om utskrift')
     A                                      CF15(15 'Automatisk kopiering av T.-
     A                                      O. fra linje til linje.')
     A                                      CF17(17 'Be om utskrift/behold en d-
     A                                      el oppl. for neste SAD')
     A                                      CF12(12 'Gå til bilde02')
     A                                      SLNO(19)
     A                                      CLRL(06)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  4  2'F3=Avslutt  F5=Forny  F6=Lage ny  -
     A                                      F8=Hopp til  F12=Forrige  F15=Auto -
     A                                      kopi T.O'
     A                                      COLOR(BLU)
     A                                  5  2'F14=Søk. kunders vareregister  F17-
     A                                      =Neste sad kopier fra denne'
     A                                      COLOR(BLU)
     A N36                              5 65'F7=Søk. oppdrag'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  6  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R VARELI
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF04(04 'Søk')
     A N40                                  CF14(14 'SØK KUNDERS VAREREGISTER')
     A    N36                               CF07(07 'SØKE OPPDRAG')
     A N40                                  CF03(03 'Be om utskrift')
     A                                      CF15(15 'Automatisk kopiering av T.-
     A                                      O. fra linje til linje.')
     A N40                                  CF17(17 'Be om utskrift/behold en d-
     A                                      el oppl. for neste SAD')
     A                                      CF12(12 'Forrige')
     A                                      CF13(13 'Oppdater søketekst på tari-
     A                                      ff-register')
     A  35                                  CF22(22 'MERK B PÅ STATUS')
     A                                      SLNO(19)
     A                                      CLRL(06)
     A                                      OVERLAY
     A          H                           HLPDOC('SADE009' 'HELPNU' 'HLP400')
     A                                      HLPARA(02 004 02 011)
     A          H                           HLPDOC('SADE010' 'HELPNU' 'HLP400')
     A                                      HLPARA(02 014 02 042)
     A          H                           HLPDOC('SADE011' 'HELPNU' 'HLP400')
     A                                      HLPARA(02 044 02 072)
     A          H                           HLPDOC('SADE012' 'HELPNU' 'HLP400')
     A                                      HLPARA(03 008 03 052)
     A          H                           HLPDOC('SAD015' 'HELPNU' 'HLP400')
     A                                      HLPARA(04 008 05 078)
     A          H                           HLPDOC('SAD016' 'HELPNU' 'HLP400')
     A                                      HLPARA(06 001 06 078)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            W2LI           5S 0H
     A                                  1  2'   Varenr.. Brutto vekt Netto vekt-
     A                                       Mengde.'
     A                                      COLOR(WHT)
     A            W2VNTI         8A  B  2  5TEXT('Varenr Tolltariff')
     A                                      DSPATR(UL)
     A  89                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      DUP(61)
     A            W2VNT          8S 0H
     A            W2VKTB         9Y 0B  2 14TEXT('Vekt Brutto')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            W2VKTN         9Y 0B  2 26TEXT('Vekt Netto')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            W2NTM          7Y 0B  2 36TEXT('Mengde')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A                                  3  2'V.B'
     A            W2FT          17A  B  3  6TEXT('Fritekst')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      DUP
     A            W2NT           6Y 0B  3 24TEXT('Antall')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      EDTCDE(4)
     A            W2EH           4A  B  3 31TEXT('Enhet')
     A                                      DSPATR(UL)
     A  84                                  DSPATR(RI)
     A                                      COLOR(YLW)
     A                                      DUP
     A            W2VT          15A  B  3 36TEXT('Varetekst')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                      DUP
     A                                  4  2'T.O'
     A            W2TOPL        45A  B  4  6TEXT('TILLEGGSOPPLYSNING')
     A                                      DSPATR(UL)
     A                                      COLOR(YLW)
     A                                  5  2'F15=Auto T.O F13=Opd.tar'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  6  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R VAREL3
     A                                      CF12(12 'Forrige')
     A                                      SLNO(19)
     A                                      CLRL(06)
     A                                      OVERLAY
     A                                  1  2'   Varenr.. Brutto vekt Netto vekt-
     A                                       Mengde.'
     A                                      COLOR(WHT)
     A            SVVNT          8  0O  2  5TEXT('Varenr Tolltariff')
     A                                      DSPATR(PC)
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            SVVKTB         9  0O  2 14DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            SVVKTN         9Y 0O  2 26DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            SVNTM          7Y 0O  2 36DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  3  2'V.B'
     A            SVFT          17A  O  3  6TEXT('Fritekst')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            SVNT           6Y 0O  3 24TEXT('Antall')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
     A            SVEH           4A  O  3 31TEXT('Enhet')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A            SVVT          15A  O  3 36TEXT('Varetekst')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  4  2'T.O'
     A            SVTOPL        45A  O  4  6TEXT('TILLEGGSOPPLYSNING')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  5  4'F12 = Forrige'
     A                                      COLOR(BLU)
      *
     A          R NYWRK
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF05(05 'FRISK')
     A                                      CF06(06 'LAGE NY')
     A                                      CF14(14 'SØK KUNDERS VAREREGISTER')
     A    N36                               CF07(07 'SØKE OPPDRAG')
     A                                      CF03(03 'Be om utskrift')
     A                                      CF15(15 'Automatisk kopiering av T.-
     A                                      O. fra linje til linje.')
     A                                      CF17(17 'Be om utskrift/behold en d-
     A                                      el oppl. for neste SAD')
     A                                      CF12(12 'Gå til bilde02')
     A                                  1  2'SYSPED/SAD041A'
     A                                      COLOR(BLU)
     A                                  1 21'SAD-TRANSITT'
     A                                      COLOR(WHT)
     A                                  1 40'Tdeknr.'
     A            UAVD           4Y 0O  1 48COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  1 53'-'
     A                                      COLOR(WHT)
     A            UTDN           7Y 0O  1 55COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  2  2'Velg option, 2=Endre 4=Slett 5=Vis-
     A                                      e 9=Ny linje'
     A                                      COLOR(BLU)
     A  37                              2 66'Auto kopi T.O.'
     A                                      DSPATR(BL)
     A                                      COLOR(RED)
     A N32                              3  2'Op Linje Varenr.. Nettovekt Mengde-
     A                                      . Varebeskrivelse Merke & nr.      -
     A                                       Sjø'
     A                                      COLOR(WHT)
     A  32                              3  2'Op Postn Varenr.. Nettovekt Mengde-
     A                                      . Varebeskrivelse Merke & nr.      -
     A                                       Sjø'
     A                                      COLOR(WHT)
     A                                 22  2'F3=Avslutt  F5=Forny  F6=Lage ny  -
     A                                      F8=Hopp til  F12=Forrige  F15=Auto -
     A                                      kopi T.O.'
     A                                      COLOR(BLU)
     A                                 23  2'F14=Søk. kunders vareregister  F17-
     A                                      =Neste sad kopier fra denne'
     A                                      COLOR(BLU)
     A N36                             23 65'F7=Søk. oppdrag'
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
      *
     A          R VAREL2
     A                                      CF12(12 'Forrige')
     A                                      SLNO(19)
     A                                      CLRL(06)
     A                                  2  2'Hopp til linje   -->'
     A            W3LN           5Y 0I  2 24DSPATR(UL)
     A                                      DSPATR(PC)
     A                                  3  2'Hopp til varenr. -->'
     A            W3VNT          8Y 0B  3 24DSPATR(UL)
     A                                  4  2'Hopp til postnr. -->'
     A            W3LI           5S 0I  4 24DSPATR(UL)
     A                                  5  2'F12=Forrige'
     A                                      COLOR(BLU)
      *
     A          R SUBFIL2                   SFL
     A            WSNR           1A  O  6  2
     A            SVLI           5Y 0H
     A            WXLN           5Y 0O  6  5EDTCDE(4)
     A            SVVNT          8S 0O  6 11TEXT('Varenr Tolltariff')
     A            SVVKTN         9Y 0O  6 20TEXT('Vekt Netto')
     A                                      EDTCDE(4)
     A            SVNTM          7Y 0O  6 30TEXT('Mengde')
     A                                      EDTCDE(4)
     A            SVVT          15A  O  6 38TEXT('Varetekst')
     A            SVFT          17A  O  6 54
     A            SV01           1A  O  6 72COLOR(RED)
     A                                      DSPATR(BL)
      *
     A          R SUBCTL2                   SFLCTL(SUBFIL2)
     A                                      SFLSIZ(0018)
     A                                      SFLPAG(0017)
     A  41                                  SFLDSP
     A  42                                  SFLDSPCTL
     A  43                                  SFLCLR
     A  44                                  SFLEND
     A  41N93N94                            ROLLUP(25)
     A            ZRECN2         4S 0O  1 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYSPED/SAD041A'
     A                                      COLOR(BLU)
     A                                  1 21'SAD-TRANSITT'
     A                                      COLOR(WHT)
     A                                  1 40'Tdeknr.'
     A            UAVD           4Y 0O  1 48COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  1 53'-'
     A                                      COLOR(WHT)
     A            UTDN           7Y 0O  1 55COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  3  2'Trykk Enter for å bekrefte at du v-
     A                                      algte 4=Slette.'
     A                                      COLOR(BLU)
     A                                  4  2'Trykk F12 for å gå tilbake og endr-
     A                                      e valget.'
     A                                      COLOR(BLU)
     A N32                              5  2'Op Linje Varenr.. Nettovekt Mengde-
     A                                      . Varebeskrivelse Merke & nr.      -
     A                                       Sjø'
     A                                      COLOR(WHT)
     A  32                              5  2'Op Postn Varenr.. Nettovekt Mengde-
     A                                      . Varebeskrivelse Merke & nr.      -
     A                                       Sjø'
     A                                      COLOR(WHT)
      *
     A          R BUNN2
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF12(12 'Gå tilbake')
     A                                      SLNO(23)
     A                                      CLRL(02)
     A                                      OVERLAY
     A                                  1  2'F12=Avbryt'
     A                                      COLOR(BLU)
