      *
      ***********************************************************
      *** PROGRAM SKAL SKRIVE BOOKING                         ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER    ***
      *** LEDIGE INDIKATORER 01-03 06-11 13-16 18-26          ***
      *** LEDIGE INDIKATORER 28-32 34-59 61-99                ***
      ***********************************************************
      *
     FCUNDL01   UF   E           K DISK
     FKUNDL01   UF   E           K DISK
     F                                     RENAME(KUNDR:KUNDR2)
     FCUNDC     IF   E             DISK
     FFAXSEN    IF   E           K DISK
     FFAXLK     IF   E           K DISK
     FFIRM      IF   E           K DISK
     FBOOKING   IF   E           K DISK
     FSYSJ56FM  CF   E             WORKSTN
     F                                     INFDS(FBAREA)
     D BT              S              1    DIM(5)
     D F4              S             75    DIM(12)
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D                 DS
     D UP              C                   CONST('ABCDEFGHIJKLMNOPQRST-
     D                                     UVWXYZÆØÅ')
     D LO              C                   CONST('abcdefghijklmnopqrst-
     D                                     uvwxyzæøå')
     D                 DS
     D  WSCVP1                 1     40
     D  WSCVP2                41     80
     D  WSCVP3                81    120
     D  WSCVP4               121    160
     D  WSCVP5               161    200
     D  WSCVP6               201    240
     D  WSCVP7               241    280
     D  WSCVP8               281    320
     D  UCVP1                  1    160
     D  UCVP2                161    320
     D  WSCNV                 41     70
     D  WSCAD1                81    110
     D  WSCAD2               121    150
     D  WSCAD3               161    190
     D  WSCATT               201    230
     D                 DS
     D  WSCF01                 1     75
     D  WSCF02                76    150
     D  WSCF03               151    225
     D  WSCF04               226    300
     D  WSCF05               301    375
     D  WSCF06               376    450
     D  WSCF07               451    525
     D  WSCF08               526    600
     D  WSCF09               601    675
     D  WSCF10               676    750
     D  WSCF11               751    825
     D  WSCF12               826    900
     D  F5                     1    900    DIM(12)
     D                 DS
     D  BOKNTO                 1      8  0
     D  BOKNTL                 9     16  0
     D  BOKNTT                17     24  0
     D  BOKNTP                25     32  0
     D  BOKNTE                33     40  0
     D  KT                     1     40  0 DIM(5)
     D                 DS
     D  BOKNCO                 1      8  0
     D  BOKNCL                 9     16  0
     D  BOKNCT                17     24  0
     D  BOKNCP                25     32  0
     D  BOKNCE                33     40  0
     D  KC                     1     40  0 DIM(5)
     D                 DS
     D  BOTFXO                 1      1
     D  BOTFXL                 2      2
     D  BOTFXT                 3      3
     D  BOTFXP                 4      4
     D  BOTFXE                 5      5
     D  FX                     1      5    DIM(5)
     D                 DS
     D  PM                     1    256    DIM(256)
     D  UPARM                  1    256
     D                 DS
     D  F1                     1     21    DIM(21)
     D  XFAXN                  1     21
     D                 DS
     D  F2                     1     15    DIM(15)
     D  WSFAXN                 1     15
     D  FAX05A                 1      4
     D                 DS
     D  F3                     1      6    DIM(6)
     D  FAXRET                 1      6
     D  FAX05B                 1      4
     D                UDS
     D  UFIRM                  1      2
     D  UAVD                  15     18  0
     D  USG                   19     21
     IKUNDR
     I              BANKG                       SYBAGI
     I              POSTG                       SYPOGI
     C*
     C                   EXSR      INIT
     C*
     C* SKRIV TIL PRINTER
     C*
     C     BOOKK         SETLL     BOOKING
     C                   SETOFF                                       60
     C     START         TAG
     C     UAVD          READE     BOOKING                                60
     C  N60BOOPD         IFGT      UOPDT
     C                   GOTO      SLUTT
     C                   END
     C  N60USG           IFNE      BOSG
     C     USG           ANDNE     'ALL'
     C     UOPDF         ANDNE     UOPDT
     C                   GOTO      START
     C                   END
     C  N60              DO
     C                   EXSR      PRINT
     C                   GOTO      START
     C                   END
     C*
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
     C*
     C******************************************
     C     PRINT         BEGSR
     C******************************************
     C                   MOVE      *BLANKS       UPARM
     C                   MOVE      BOAVD         XAVD              4
     C                   MOVE      BOOPD         XOPD              7
     C                   MOVEA     XAVD          PM(1)
     C                   MOVEA     XOPD          PM(5)
     C                   MOVE      UJOBK         PM(13)
      *
     C     1             DO        5             XN                1 0          1<-B
      *
     C     BT(XN)        IFEQ      'J'                                           2<-B
      *
     C     FX(XN)        IFEQ      'F'                                            3<-B
     C                   MOVE      KT(XN)        KUNDNR
     C     KUND1K        CHAIN(N)  CUNDL01                            33
     C                   END                                                      3<-E
     C                   MOVE      XN            PM(12)                         LINJENR.
     C                   EXSR      PRINT2
      *
     C     KC(XN)        IFNE      KT(XN)                                         3<-B
     C     KC(XN)        ANDNE     0
     C     FX(XN)        IFEQ      'F'                                             4<-B
     C                   MOVE      KC(XN)        KUNDNR
     C     KUND1K        CHAIN(N)  CUNDL01                            33
     C                   END                                                       4<-E
     C                   EXSR      PRINT2
     C                   END                                                      3<-E
      *
     C                   END                                                     2<-E
      *
     C                   END                                                    1<-E
      *
     C                   ENDSR
      *
     C******************************************
     C     PRINT2        BEGSR
     C******************************************
     C     FX(XN)        CASEQ     'F'           FAXFIX
     C                   ENDCS
      *
     C                   MOVE      '.'           F5(12)
     C                   MOVEA     F5            F4
     C                   CALL      'SYSJ57CL'
     C                   PARM                    UPARM
     C                   PARM                    UCVP1
     C                   PARM                    UCVP2
     C                   PARM                    UPRT29
     C                   PARM                    UPT29
     C                   PARM                    F4
     C*
     C                   ENDSR
     C*
     C******************************************
     C     INIT          BEGSR
     C******************************************
     C     KUND1K        KLIST
     C                   KFLD                    UFIRM
     C                   KFLD                    KUNDNR
     C     BOOKK         KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    UOPDF
      *
     C     *ENTRY        PLIST
     C                   PARM                    UOPDF2            7
     C                   PARM                    UOPDT2            7
     C                   PARM                    BT
     C                   PARM                    UPRT29           10
     C                   PARM                    UPT29            10
     C                   PARM                    UJOBK             1
     C                   PARM                    UCVP6            50
      *
     C                   READ      FIRM                                   34
     C                   READ      FAXSEN                                 34
     C     FAXSYS        COMP      ' '                                2828
     C   28FAXSYS        COMP      'F'                                    27
     C   28
     CANN27FAXSYS        COMP      'B'                                    27
     C   28              MOVE      FAXSYS        XFAXSY            1
     C   28
     CAN 27              DO
     C                   MOVE      *BLANKS       UCVP1
     C                   MOVE      *BLANKS       UCVP2
     C                   MOVEL     FIFT          WSCVP5
     C                   MOVEL     UCVP6         WSCVP6
     C                   END
      *
     C                   TESTN                   UOPDF2               33
     C  N33              DO
     C                   MOVE      *ZEROS        UOPDF             7 0
     C                   MOVE      *ZEROS        UOPDT             7 0
     C                   END
     C   33              DO
     C                   MOVE      UOPDF2        UOPDF
     C                   SETOFF                                       33
     C                   TESTN                   UOPDT2               33
     C   33              MOVE      UOPDT2        UOPDT
     C  N33              MOVE      UOPDF         UOPDT
     C                   END
     C*
     C                   ENDSR
     C*
     C*****************************************************
     C     FAXFIX        BEGSR
     C*****************************************************
     C                   MOVE      TFAXNR        WSFAXN
     C                   EVAL      EPOST = SYEPOS
     C     *IN27         IFEQ      '1'                                          1<-B
     C                   MOVE      *BLANKS       WSCVP2
     C                   MOVE      *BLANKS       WSCVP3
     C                   MOVEL     KNAVN         WSCVP2
     C                   MOVEL     ADR1          WSCVP3
     C                   MOVE      *BLANKS       WSCVP4
     C                   MOVE      *BLANKS       F5
     C                   END                                                    1<-E
      *
     C     STSRF         TAG
     C   27              EXFMT     BILDE1
     C  N27              EXFMT     BILDE2
     C                   SETOFF                                       3099
      *
     C   04              DO
     C                   MOVE      *BLANKS       EPOFAX            1
     C     LO:UP         XLATE     EPOST         USOK
     C                   CALL      'VCUNDC2R'
     C                   PARM                    FIFIRM
     C                   PARM                    USOK             30
     C                   PARM                    URRN2            10 0
     C                   PARM                    PARMAX            1
     C                   MOVE      URRN2         RRN              10 0
     C     RRN           CHAIN     CUNDC                              33
     C  N33              DO
     C     EPOFAX        IFNE      'E'
     C                   MOVEL     CFAX          WSFAXN
     C                   ELSE
     C                   MOVE      *BLANKS       WSFAXN
     C                   END
     C     EPOFAX        IFNE      'F'
     C                   MOVEL     CEMAIL        EPOST
     C                   ELSE
     C                   MOVE      *BLANKS       EPOST
     C                   END
     C                   MOVE      CCOMPN        KUNDNR
     C                   END
     C                   END
      *
     C     *IN12         IFEQ      '0'                                          1<-B
     C     WSFAXN        IFEQ      *BLANKS                                       2<-B
     C     EPOST         ANDEQ     *BLANKS
     C                   SETON                                        3099
     C                   MOVE      'SHU0169'     MSGNR
     C                   GOTO      STSRF
     C                   END                                                     2<-E
     C  N05
     CANN17              GOTO      STSRF
      *
     C     EPOST         IFEQ      *BLANKS                                       2<-B
     C     KUND1K        CHAIN     CUNDL01                            33
     C  N33              MOVE      WSFAXN        TFAXNR
     C  N33              EXCEPT    UPD
     C     KUND1K        CHAIN     KUNDL01                            33
     C  N33              MOVE      WSFAXN        TFAXNR
     C  N33              EXCEPT    UPD2
     C                   ELSE
     C                   MOVE      *BLANKS       WSFAXN
     C     *IN05         IFEQ      '1'
     C                   CALL      'EPOCVP'
     C                   PARM                    WSCVP1
     C                   PARM                    WSCVP2
     C                   PARM                    WSCVP3
     C                   PARM                    WSCVP4
     C                   PARM                    WSCVP5
     C                   PARM                    WSCVP6
     C                   PARM                    WSCVP7
     C                   PARM                    WSCVP8
     C                   PARM                    FUNK              1
     C     FUNK          CABEQ     'A'           STSRF
     C                   END
     C                   END
      *
     C     WSLAND        IFNE      *BLANKS                                       2<-B
     C     WSLAND        CHAIN     FAXLK                              33
     C  N33FAX05A        IFEQ      FAX05B                                         3<-B
     C                   MOVE      *BLANKS       FAXRET
     C                   END                                                      3<-E
     C                   END                                                     2<-E
     C                   MOVE      *ZEROS        A1                3 0
     C     1             DO        6             A2                3 0           2<-B
     C     F3(A2)        IFNE      *BLANKS                                        3<-B
     C                   ADD       1             A1
     C                   MOVEL     F3(A2)        F1(A1)
     C                   END                                                      3<-E
     C                   END                                                     2<-E
     C     1             DO        15            A2                              2<-B
     C     F2(A2)        IFNE      *BLANKS                                        3<-B
     C                   ADD       1             A1
     C                   MOVEL     F2(A2)        F1(A1)
     C                   END                                                      3<-E
     C                   END                                                     2<-E
      *
     C                   MOVE      'J'           PM(14)                         SENDE FAX
     C                   MOVEA     XFAXSY        PM(15)                         FAX-SYSTEM
     C   05              MOVE      'J'           PM(16)                         MED COVERPAG
     C   17
     CORN27              MOVE      'N'           PM(16)                         UTEN COVERP.
     C                   MOVE      '*CURRENT'    PTIME             8
     C                   MOVE      '*CURRENT'    PDATE             8
     C     WSTIME        IFNE      *ZEROS                                        2<-B
     C     WSDATE        ANDNE     *ZEROS
     C                   MOVE      *BLANKS       PTIME
     C                   MOVE      *BLANKS       PDATE
     C                   MOVEL     WSTIME        PTIME
     C                   MOVEL     WSDATE        PDATE
     C                   END                                                     2<-E
     C                   MOVEA     XFAXN         PM(17)                         FAXNR.
     C                   MOVEA     'SYSJ57P'     PM(38)                         FILNAVN
     C                   MOVEA     PTIME         PM(50)                         SENDINGSTID
     C                   MOVEA     PDATE         PM(58)                         SENDINGSDATO
     C     EPOST         IFNE      *BLANKS
     C                   MOVEA     'J'           PM(66)                         E-MAIL JA
     C                   ELSE
     C                   MOVEA     'N'           PM(66)                         E-MAIL NEI
     C                   END
     C                   MOVEA     EPOST         PM(67)                         E-MAIL
     C                   ELSE                                                   1
     C                   MOVE      'N'           PM(14)                         PRINT
     C                   END                                                    1<-E
      *
     C                   ENDSR
      *
     OKUNDR     E            UPD
     O                       TFAXNR
     OKUNDR2    E            UPD2
     O                       TFAXNR
