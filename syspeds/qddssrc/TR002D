     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HELP
     A                                      ALTHELP(CA01)
      * "NOERASE" MÅ HA FOR Å IKKE TØMME BILDE SOM MAN KOMME FRA
     A          R NOERASE
     A                                      ASSUME
     A                                  1  2' '
      * MAN KUN DEFINNERE VINDU PÅ REKORDNIVÅ
     A          R WINDOWSZ
     A                                      WINDOW(*DFT 10 60 *NOMSGLIN)
     A                                      OVERLAY
     A          R TR002D11                  SFL
     A            WSNR           1A  I  5  2VALUES(' ' '1')
     A            TKKODE        10A  O  5  5COLOR(WHT)
     A            TKTXT         35A  O  5 16
     A            TKAVG          1A  O  5 52
     A            TKANK          1A  O  5 55
     A            TKTRS          1A  O  5 58
     A          R TR002D12                  SFLCTL(TR002D11)
     A                                      SFLSIZ(0006)
     A                                      SFLPAG(0005)
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A                                      CA03(03 'Forrige')
     A                                      CA05(05 'Frisk')
     A                                      CA06(06 'LAGE NY')
     A                                      CA12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            ZRECNO         4S 0O  2 50SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'TR002R'
     A                                      COLOR(BLU)
     A            TR1MSG        40A  O  1 10MSGID(&MSGNRW SYMSGF)
     A                                      DSPATR(HI)
     A            MSGNRW         7A  P
     A                                  2  2'Velg opsjon:  1=Plukk'
     A                                      COLOR(BLU)
     A N38                              3  2'Op Kode       Beskrivelse'
     A                                      COLOR(WHT)
     A  38                              3  2'Op Kode       Beskrivelse         -
     A                                                      Av An Tr'
     A                                      COLOR(WHT)
     A            W2NR           1A  I  4  2VALUES(' ' '1')
     A                                      DSPATR(UL)
     A            W2KODE        10A  I  4  5DSPATR(UL)
     A            W2TXT         35A  I  4 16DSPATR(UL)
     A            W2TYPE         1A  B  4 52DSPATR(UL)
     A                                      VALUES('N' 'E')
     A                                  4 54'(N/E)'
     A          R TR002D13
     A                                      WINDOW(WINDOWSZ)
     A                                      SLNO(10)
     A                                      CLRL(01)
     A                                      OVERLAY
     A N30                              1  2MSGCON(29 YBC0212 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        55A  O  1  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A          R TR002D14
     A                                      WINDOW(WINDOWSZ)
     A                                      OVERLAY
     A                                      CA03(03 'Forrige')
     A                                      CA05(05 'Frisk')
     A                                      CA06(06 'LAGE NY')
     A                                      CA12(12 'Forrige')
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'TR002R'
     A                                      COLOR(BLU)
     A            TR1MSG        40   O  1 10MSGID(&MSGNRW SYMSGF)
     A                                      DSPATR(HI)
     A            MSGNRW         7A  P
     A                                  2  2'Velg opsjon:  1=Plukk'
     A                                      COLOR(BLU)
     A N38                              3  2'Op Kode       Beskrivelse'
     A                                      COLOR(WHT)
     A  38                              3  2'Op Kode       Beskrivelse         -
     A                                                      Av An Tr'
     A                                      COLOR(WHT)
     A            W2NR           1A  I  4  2VALUES(' ' '1')
     A                                      DSPATR(UL)
     A            W2KODE        10A  I  4  5DSPATR(UL)
     A            W2TXT         35A  I  4 16DSPATR(UL)
     A            W2TYPE         1A  B  4 52DSPATR(UL)
     A                                      VALUES('N' 'E')
     A                                  4 54'(N/E)'
     A N30                             10  2MSGCON(29 YBC0212 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        38A  O 10  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
