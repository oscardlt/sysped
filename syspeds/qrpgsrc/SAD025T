      ************************************************************
      *** PROGRAM SKAL SKRIVE UT LASTELISTE (SAD400) PLUKK OPD ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER     ***
      *** LEDIGE INDIKATORER 01 02 04 06 07 09-11 13-24        ***
      *** LEDIGE INDIKATORER 28 29 32-89 95-97                 ***
      ************************************************************
      *
     FKODTA     IF   E           K DISK
     FKODTFM    IF   E           K DISK
     FKODTSI    IF   E           K DISK
     FSADTRH    UF   E           K DISK
     FTURER     IF   E           K DISK
     FHEADF     UF   E           K DISK
     FHEAD8     IF   E           K DISK
     F                                     RENAME(HEADFR:HEADFR8)
     FSTED2     IF   E           K DISK
     FCUNDL01   IF   E           K DISK
     FFIRM      IF   E           K DISK
     FHESTED    IF   E           K DISK
     FTRANLIST  O    F  130        PRINTER OFLIND(*INOF)
     F                                     FORMLEN(72)
     F                                     FORMOFL(72)
     FSAD025FT  CF   E             WORKSTN
     F                                     SFILE(SUBFIL:ZRECNO)
     F                                     INFDS(FBAREA)
     D FBAREA          DS
     D  WSID                 197    206
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D                 DS
     D  HEVKT                  1      9  0
     D  WSVKT                  5      9  0
     D                 DS
     D  HEM3                   1      7  3
     D  WSM3                   3      6  2
     D                 DS
     D  HENAS                  1     30
     D  WSNAS                  1     25
     D                 DS
     D  HENAK                  1     30
     D  WSNAK                  1     25
     D                 DS
     D  HESV1                  1     25
     D  WSSV1                  1     25
     D                 DS
     D  TUDTÅ                  1      4  0
     D  TUDTM                  5      6  0
     D  TUDTD                  7      8  0
     D  TUDT                   1      8  0
     D                 DS
     D  XUDTD                  1      2  0
     D  XUDTM                  3      4  0
     D  XUDTÅ                  5      6  0
     D  XUDT                   1      6  0
     D                 DS
     D  W1TYPE                 1      2
     D  W1TYP2                 2      2
     D                UDS
     D  USG                   19     21
     D  USGSAD                59     61
      *
     C                   EXSR      INIT
      *
      ***  VIS BILDE FOR LISTETYPE
     C     CHOISE        TAG
     C                   EXFMT     BILDE01
     C   03              MOVE      '1'           UIN01
     C   03
     COR 12              GOTO      SLUTT
      * SØK ETTER KUNDENR
     C   04              EXSR      SØKDTA
      *
     C                   EXSR      TESTB1
     C   30              GOTO      CHOISE
      *
     C     W1KNFA        COMP      *ZEROS                             8989
      *
      ***  KOMMER HIT KUN VED F5 - FRISK OPP = HENT FRA TUR IGJEN
     C     START         TAG
     C                   MOVE      '1'           *IN93
     C                   WRITE     SUBCTL
     C                   MOVEA     '00'          *IN(93)
     C                   Z-ADD     *ZEROS        ZRECNO
     C                   Z-ADD     1             YAVD
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     HEAD8K        SETLL     HEAD8
     C     START1        TAG
     C                   READ      HEAD8                                  94
     C  N94YAVD          IFNE      HEAVD
     C     YAVD          IFEQ      9999
     C                   GOTO      UTLOOP
     C                   END
     C                   MOVE      HEAVD         YAVD
     C     HEAD8K        SETLL     HEAD8
     C                   GOTO      START1
     C                   END
     C  N94UPRO          IFNE      HEPRO
     C     YAVD          IFEQ      9999
     C                   GOTO      UTLOOP
     C                   END
     C                   ADD       1             YAVD
     C     HEAD8K        SETLL     HEAD8
     C                   GOTO      START1
     C                   END
     C   94              MOVE      *BLANKS       WSNR
      *
     C  N94              DO
     C                   DO        1000
     C                   SETOFF                                       908586
     C                   MOVE      ' '           WSMED
     C                   MOVE      ' '           WSTSTA
     C                   MOVE      ' '           TRTSTA
     C                   MOVE      ' '           WSFT
     C                   MOVE      ' '           WSTXT
     C                   MOVE      *BLANKS       WSNAFA
     C                   MOVE      HETLE         WSTLE
     C                   MOVE      HETLL         WSTLL
     C                   MOVE      HETLKU        WSTLKU
     C     HEATRK        CHAIN     HESTEDR                            31
     C     *IN31         IFEQ      '0'
     C                   MOVEL     HSFROM        WSSDF
     C                   MOVEL     HSTO          WSSDT
     C                   ELSE
     C     HESDF         CHAIN     STED2                              33
     C   33              MOVE      *BLANKS       WSSDF
     C  N33              MOVEL     ST2NVN        WSSDF
     C     HESDT         CHAIN     STED2                              33
     C   33              MOVE      *BLANKS       WSSDT
     C  N33              MOVEL     ST2NVN        WSSDT
     C                   END
     C                   SETOFF                                       33
     C  N33              DO
     C* HAR DENNE POSIJON STATUS T-DOK UTSKREVET ?? UTELATES OM IKKE Y/J I W1ALL
     C     W1ALL         IFEQ      ' '
     C     W1ALL         OREQ      'N'
     C     TRTSTA        IFEQ      'A'
     C     TRTSTA        OREQ      'B'
     C     TRTSTA        OREQ      'C'
     C     TRTSTA        OREQ      'D'
     C                   GOTO      ST21
     C                   END
     C                   END
     C     W1TYP2        IFEQ      ' '
     C     TRTSTA        COMP      '3'                                    85    *WSTSTA WHITE
     C  N85TRTSTA        COMP      '4'                                    85
     C                   ELSE
     C     W1TYP2        COMP      TRTSTA                                 85
     C                   END
     C   89W1KNFA        COMP      HEKNA                                  86    *WSNAFA WHITE
     C     HEKNA         IFNE      *ZEROS
     C                   MOVE      HEKNA         YKN
     C     KUND1K        CHAIN     CUNDL01                            33
     C  N33              MOVEL     KNAVN         WSNAFA
     C   33              MOVE      *BLANKS       WSNAFA
     C                   END
     C     TRTSTA        IFEQ      W1TYP2
     C     W1KNFA        IFEQ      *ZEROS
     C     W1KNFA        ORNE      *ZEROS
     C     W1KNFA        ANDEQ     HEKNA
     C                   MOVE      'J'           WSMED
     C                   SETON                                        90
     C                   ADD       1             XANTO
     C                   END
     C                   END
     C                   END
      * POSTEN SKAL MED - ØK TELLER SUBFILREC.NR
     C                   ADD       1             ZRECNO
     C                   WRITE     SUBFIL
     C     ST21          TAG
     C                   READ      HEAD8                                  94
     C  N94YAVD          IFNE      HEAVD
     C     YAVD          IFEQ      9999
     C                   GOTO      UTLOOP
     C                   END
     C                   MOVE      HEAVD         YAVD
     C     HEAD8K        SETLL     HEAD8
     C                   GOTO      ST21
     C                   END
     C  N94UPRO          IFNE      HEPRO
     C     YAVD          IFEQ      9999
     C                   GOTO      UTLOOP
     C                   END
     C                   ADD       1             YAVD
     C     HEAD8K        SETLL     HEAD8
     C                   GOTO      START1
     C                   END
     C  N94              END
     C                   END
      *
     C     UTLOOP        TAG
      *
     C     ZRECNO        IFGT      1
     C                   Z-ADD     1             ZRECNO
     C                   END
     C                   Z-ADD     8             LINNBR
     C                   Z-ADD     2             POSNBR
      * STARTPUNKT FOR NORMAL SYKLUS - RULLING/HENT OPTION....
     C     START3        TAG
     C                   MOVEA     '11'          *IN(91)
     C     ZRECNO        IFEQ      0
     C                   EXFMT     NYVARE
     C                   ELSE
     C                   WRITE     SUBCTL
     C                   EXFMT     BUNN
     C                   END
      *
      ***
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C                   Z-ADD     WSSUBN        XRECNO            4 0
     C     XRECNO        IFEQ      0
     C                   Z-ADD     ZRECNO        XRECNO            4 0
     C                   END
      ***
      *
     C                   SETOFF                                       9830
      *
     C   03              MOVE      '1'           UIN01
     C   03              GOTO      SLUTT
      *** FRISK OPP
     C   05              GOTO      START
      *** SKRIV LISTE   - LAG TRANSITT
     C   07              DO
     C                   EXSR      PRINTL
     C                   GOTO      CHOISE
     C                   END
      *** FORRIGE
     C   12              GOTO      CHOISE
      *
     C     ZRECNO        IFNE      0
     C                   EXSR      VLHENT
     C                   END
     C   98XRECNO        IFNE      0
     C                   Z-ADD     XRECNO        ZRECNO
     C                   END
     C                   GOTO      START3
      *
     C     SLUTT         TAG
     C                   SETON                                        LR
     C                   RETURN
      *
     C***********************************************
     C     PRINTL        BEGSR
     C***********************************************
     C                   EXSR      PRINT
     C                   SETON                                        30
     C  N40              MOVE      'DIV2298'     MSGNR                          IKKE SKREVET
     C   40              DO
     C                   MOVE      'DIV2297'     MSGNR                          *SKREVET
     C     W1TYP2        IFEQ      '2'
     C                   MOVE      'B'           SIN05             1            TRANSIT VIA LISTE
     C                   ELSE
     C                   MOVE      'A'           SIN05                          TRANSIT VIA LISTE
     C                   END
     C     W1KNFA        IFNE      *ZEROS
     C                   MOVE      W1KNFA        UKNFA
     C                   ELSE
     C                   MOVE      FØKNFA        UKNFA                          * FØRSTE PLUKKEDE
     C                   END
     C                   MOVE      *ZEROS        UVKT
     C                   MOVE      *ZEROS        HEOPD
     C                   MOVE      W1SG          USGSAD
     C                   MOVE      'CRT'         UTYPE             3
     C                   CALL      'SAD041'
     C                   PARM      TUAVD         UAVD2
     C                   PARM      HEOPD         UOPD2
     C                   PARM      USG           USG
     C                   PARM                    UTYPE
     C                   PARM                    WSID
     C                   PARM                    SIN05
     C                   PARM                    W1SG
     C                   PARM                    TUAVD
     C                   PARM                    HEOPD
     C                   PARM                    TUPRO
     C                   PARM                    UANTO
     C                   PARM                    UANTKL
     C                   PARM                    UVKT
     C                   PARM                    UKNFA
     C                   END
      *
     C                   MOVE      '1'           UANT
     C                   MOVE      *ZEROS        UANTO
     C                   MOVE      *ZEROS        UANTKL
     C                   MOVE      *ZEROS        UVKT
     C                   MOVE      *ZEROS        FØKNFA
      *
     C                   ENDSR
      *
     C***********************************************
     C     PRINTE        BEGSR
     C***********************************************
      * SKRIVER T-DOKUMENT ENKELVIS
      *
      * OM T-STAUS UDEFINERT I OPPDRAG - SETT INN FRA W1TYP2 EL. T1
     C     TRTSTA        IFEQ      ' '
     C     HEATRK        CHAIN     HEADF                              33
     C  N33W1TYP2        IFEQ      '2'
     C                   MOVE      '4'           TRTSTA                          *T2
     C                   ELSE
     C                   MOVE      '3'           TRTSTA                          *T1
     C                   END
     C  N33              UPDATE    HEADFR
     C                   END
     C     TRTSTA        IFEQ      '1'
     C     TRTSTA        OREQ      '3'
     C     TRTSTA        OREQ      'A'
     C     TRTSTA        OREQ      'C'
     C                   MOVE      'C'           SIN05                          ENK.- T1
     C                   END
     C     TRTSTA        IFEQ      '2'
     C     TRTSTA        OREQ      '4'
     C     TRTSTA        OREQ      'B'
     C     TRTSTA        OREQ      'D'
     C                   MOVE      'D'           SIN05                          ENK.- T2
     C                   END
     C     SIN05         IFEQ      'C'
     C     SIN05         OREQ      'D'
     C                   SETOFF                                       9085
     C                   SETON                                        30
     C                   MOVE      'DIV2307'     MSGNR                          T-DOK SKREVET
      *OPPDATER TRSTAT MED "SKREVET" (C=T1 D=T2)
     C     HEATRK        CHAIN     HEADF                              33
     C  N33              MOVE      SIN05         TRTSTA
     C  N33W1KNFA        IFNE      *ZEROS
     C                   MOVE      W1KNFA        HEKNA
     C                   END
     C  N33              UPDATE    HEADFR
     C     W1NAFA        IFNE      *BLANKS
     C                   MOVEL     W1NAFA        WSNAFA
     C                   END
     C     WSMED         IFEQ      'J'
     C                   SUB       1             XANTO
     C                   MOVE      ' '           WSMED
     C                   MOVE      ' '           WSFT
     C                   MOVE      *BLANKS       WSTXT
     C                   END
     C                   MOVE      HEKNA         UKNFA
     C                   MOVE      *ZEROS        UANTO
     C                   MOVE      W1SG          USGSAD
     C                   MOVE      'CRT'         UTYPE             3
     C                   CALL      'SAD041'
     C                   PARM      HEAVD         UAVD2             4 0
     C                   PARM      HEOPD         UOPD2             7 0
     C                   PARM      USG           USG
     C                   PARM                    UTYPE
     C                   PARM                    WSID
     C                   PARM                    SIN05
     C                   PARM                    W1SG
     C                   PARM                    HEAVD
     C                   PARM                    HEOPD
     C                   PARM                    TUPRO
     C                   PARM                    UANTO
     C                   PARM      HENT          UANTKL
     C                   PARM      HEVKT         UVKT
     C                   PARM                    UKNFA
     C                   END
      *
     C                   MOVE      '1'           UANT
     C                   MOVE      *ZEROS        UANTO
     C                   MOVE      *ZEROS        UANTKL
     C                   MOVE      *ZEROS        UVKT
     C                   MOVE      *ZEROS        FØKNFA
     C                   ENDSR
      *
     C***********************************************
     C     TESTB1        BEGSR
     C***********************************************
     C                   SETOFF                                       308887
     C     UAVD          COMP      *ZEROS                                 30
     C   30              MOVE      'DIV2354'     MSGNR
     C   30              GOTO      ENTSB1
     C     KODSIK        CHAIN     KODTSI                             88
     C   88              DO
     C                   MOVE      'SPS0001'     MSGNR
     C                   SETON                                        30
     C                   END
      *
     C     W1KNFA        IFNE      *ZEROS
     C                   MOVE      W1KNFA        YKN
     C     KUND1K        CHAIN     CUNDL01                            87
     C  N87              MOVE      KNAVN         W1NAFA
     C   87              MOVE      'SPK0001'     MSGNR
     C   87              SETON                                        30
     C                   ELSE
     C                   MOVE      *BLANKS       W1NAFA
     C                   END
     C     ENTSB1        ENDSR
     C***********************************************
     C     SØKDTA        BEGSR
     C***********************************************
     C                   MOVE      *BLANKS       USØKKN           30
     C                   CALL      'SOEKKUN'
     C                   PARM                    W1KNFA
     C                   PARM                    FIFIRM
     C                   PARM                    USØKKN           30
      *
     C                   ENDSR
      *
     C***********************************************
     C     VLHENT        BEGSR
     C***********************************************
     C                   SETOFF                                       919299
     C     *IN98         DOWEQ     '0'
     C                   READC     SUBFIL                                 98
      *
      * INGEN GYLDIGE OPTIONS FORETATT
     C   98              DO
     C   99              SETOFF                                       98
     C   98              MOVE      'DIV0012'     MSGNR
     C   98              SETON                                            30
     C                   Z-ADD     2             LINNBR
     C                   GOTO      SLHENT
     C                   END
      *
      * OPPDATER CURSORPOSISJON PÅ SKJERMBILDE
     C     ZRECNO        DIV       7             LINNBR
     C                   MVR                     LINNBR
     C     LINNBR        MULT      2             XLIN              2 0
     C     XLIN          IFEQ      0
     C                   Z-ADD     14            LINNBR
     C                   ELSE
     C                   Z-ADD     XLIN          LINNBR
     C                   END
     C                   SETON                                        99
      *
      * OPPDATERE HEADF ?
     C     WSTLE         IFNE      *BLANKS
     C     WSTLL         ORNE      *BLANKS
     C     WSTLKU        ORNE      *BLANKS
     C     HEADFK        CHAIN     HEADF                              33
     C  N33              DO
     C                   MOVE      WSTLE         HETLE
     C                   MOVE      WSTLL         HETLL
     C                   MOVE      WSTLKU        HETLKU
     C                   EXCEPT    CHGHEA
     C                   END
     C                   END
     C     WSTLE         IFNE      *BLANKS
     C     WSTLL         ORNE      *BLANKS
     C     SATHK         CHAIN     SADTRH                             33
     C  N33              DO
     C                   MOVE      WSTLE         SETLE
     C                   MOVE      WSTLL         SETLL
     C                   EXCEPT    CHGSAE
     C                   END
     C                   END
      *
      * PLUKKE TIL DENNE LISTE
     C     WSNR          IFEQ      '1'
     C     WSMED         ANDNE     'J'
     C                   ADD       1             XANTO             5 0
     C                   MOVE      'J'           WSMED
     C                   SETON                                        90
      * VED PLUKKING OPPDATERES TRSTAT TIL AKTUELL TYPE
      *     DERSOM EN HAR VALGT FORTOLLINGSAGENT OPPDATERES OPD MED DENNE
     C     TRTSTA        IFNE      W1TYP2
     C     WSNAFA        ORNE      W1NAFA
     C     HEATRK        CHAIN     HEADF                              33
     C  N33              DO
     C     W1KNFA        IFNE      *ZEROS
     C                   Z-ADD     W1KNFA        HEKNA
     C                   MOVEL     W1NAFA        WSNAFA
     C                   END
     C                   MOVE      W1TYP2        TRTSTA
     C                   UPDATE    HEADFR
     C                   END
     C                   END
     C                   END
      *
      * FJERNE FRA DENNE LISTE
     C     WSNR          IFEQ      '4'
     C     WSMED         ANDEQ     'J'
     C                   SUB       1             XANTO             5 0
     C                   MOVE      ' '           WSMED
     C                   MOVE      ' '           WSFT
     C                   MOVE      *BLANKS       WSTXT
     C                   SETOFF                                       90
      * VED FJERNING OPPDATERES TRSTAT MED VERDI FRA OPTFELT 2
     C     HEATRK        CHAIN     HEADF                              33
     C  N33              MOVE      WSTSTA        TRTSTA
     C  N33              UPDATE    HEADFR
     C     W1TYP2        COMP      TRTSTA                                 85
     C                   END
      *
      * SE PÅ OPPDRAG
     C     WSNR          IFEQ      '5'
     C                   EXSR      VISE
     C                   END
      *
      * SKRIVE ENKELT T-DOKUMENT
     C     WSNR          IFEQ      '7'
     C                   EXSR      PRINTE
     C                   END
      *
      *
      * SE PÅ FRITEKST TIL ETT OPPDRAG
     C     WSNR          IFEQ      '9'
     C                   EXFMT     BILDE02
     C     WSTXT         IFNE      *BLANKS
     C                   MOVE      'J'           WSFT
     C                   END
     C                   END
      *
      *   OPTIONS UTFØRT - BLANK INPUT - OPPDAT- SUBFIL
     C                   MOVE      ' '           WSNR
     C                   MOVE      ' '           WSTSTA
     C                   UPDATE    SUBFIL
     C                   END
      *
     C     SLHENT        TAG
     C                   ADD       6             LINNBR
     C                   Z-ADD     2             POSNBR
      *
     C                   ENDSR
      *
     C***********************************************
     C     VISE          BEGSR
     C***********************************************
     C                   Z-ADD     2             UFLAGG            1 0
     C                   CALL      'SYFM21A'
     C                   PARM                    FIFIRM
     C                   PARM                    HEAVD
     C                   PARM                    HEOPD
     C                   PARM                    UFLAGG
      *
     C                   ENDSR
      *
     C***********************************************
     C     PRINT         BEGSR
     C***********************************************
     C     XANTO         COMP      0                                  4040
     C   40              ADD       1             XLNR              2 0
      *
     C                   Z-ADD     0             XANTO
     C                   Z-ADD     21            XANTL             2 0
     C                   Z-ADD     1             XSIDE             2 0
     C                   EXCEPT    LHEAD
      *
     C                   Z-ADD     1             XRECNO            4 0
     C     STPRI         TAG
     C     XRECNO        CHAIN     SUBFIL                             60
     C  N60              DO
     C   40WSMED         IFNE      'J'
     C                   GOTO      STPRI2
     C                   END
     C     WSTLKU        COMP      'X'                                    61
     C     HEVS2         COMP      *BLANKS                                42
     C   42HEGM2         COMP      *BLANKS                                42
     C   42              ADD       1             XANTL
     C  N42              ADD       2             XANTL
     C     WSTXT         COMP      *BLANKS                                43
     C  N43              ADD       1             XANTL
     C     XANTL         IFGE      64
     C  N42
     CAN 27XANTL         IFEQ      64
     C                   EXCEPT    LLINJE
     C                   END
     C                   EXCEPT    LBUNN
     C                   ADD       1             XSIDE
     C                   EXCEPT    LHEAD
     C                   Z-ADD     22            XANTL
     C  N42              ADD       1             XANTL
     C                   END
     C                   ADD       1             XANTO
      * ANTALL KOLLI /ANTALL VAREPOSTER FOR TRANSIT-DOKUM
     C                   ADD       1             UANTO
     C                   ADD       HENT          UANTKL
     C     HEATRK        CHAIN     HEADF                              33
      * NÅR SPESIFIK FORT.AGENT IKKE ER ANGITT - HENT FØRSTE ANGITTE I OPPDRAG
     C  N33W1KNFA        IFEQ      *ZEROS
     C     FØKNFA        ANDEQ     *ZEROS
     C                   MOVE      HEKNA         FØKNFA            8 0
     C                   END
      * OPPDATER STATUS FOR Å VISE AT LISTE ER SKREVET
     C  N33W1TYPE        IFEQ      'T1'
     C                   MOVE      'A'           TRTSTA
     C                   ELSE
     C                   MOVE      'B'           TRTSTA
     C                   END
     C  N33              UPDATE    HEADFR
      *
     C                   EXCEPT    LDET
     C  N42              EXCEPT    LDET2
     C  N43              EXCEPT    LDET3
     C                   END
     C     STPRI2        TAG
     C  N60              ADD       1             XRECNO
     C  N60              GOTO      STPRI
      *
     C   27XANTL         DOWLE     64
     C                   EXCEPT    LLINJE
     C                   ADD       1             XANTL
     C                   END
     C                   EXCEPT    LBUNN
     C                   Z-ADD     0             XANTO
      *
     C                   ENDSR
      *
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
     C     KODTAK        KLIST
     C                   KFLD                    YAUNI
     C                   KFLD                    UAVD
     C     KODSIK        KLIST
     C                   KFLD                    YSUNI
     C                   KFLD                    W1SG
     C     TURERK        KLIST
     C                   KFLD                    UAVD
     C                   KFLD                    UPRO
     C     HEADFK        KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C     HEATRK        KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C     HEAD8K        KLIST
     C                   KFLD                    YAVD
     C                   KFLD                    UPRO
     C     SATHK         KLIST
     C                   KFLD                    HEAVD
     C                   KFLD                    HEOPD
     C     KUND1K        KLIST
     C                   KFLD                    FIFIRM
     C                   KFLD                    YKN
      *
     C     *ENTRY        PLIST
      *** UIN01='1'   --->   AVSLUTT PROGRAM
     C                   PARM                    UIN01             1
      *** UIN02='1'   --->   FORHÅNDSTRYKKEDE FORMULAR BENYTTES
     C                   PARM                    UIN02             1
     C                   PARM                    UAVD              4 0
     C                   PARM                    UPRO              8 0
      *   UANT   ---> ANTALL UTSKRIFTER    - ULIK 0 - KJØR SAD52P
     C                   PARM                    UANT              1
      *
     C     UIN02         COMP      '1'                                    27
      *
     C                   MOVE      '0'           UIN01
     C                   Z-ADD     0             YAVD              4 0
     C                   Z-ADD     0             YKN               8 0
     C                   MOVE      'SI'          YSUNI             2
     C                   MOVE      'A'           YAUNI             1
      *
     C                   MOVE      USG           W1SG
     C                   MOVE      'T1'          W1TYPE
     C                   MOVE      *ZEROS        W1KNFA
      *   TO NESTE EGENTLIG MENT SOM UTPARAMETER FRA SAD052 TIL SAD052CL
      *   INGEN BETYDNING HER
     C                   MOVE      '0'           SIN02             1            NY AVD
      *
     C                   MOVE      '1'           SIN04             1            GÅ I SRU1
     C                   MOVE      '0'           UANT
     C                   MOVE      *ZEROS        UANTO             7 0
     C                   MOVE      *ZEROS        UANTKL            7 0
     C                   MOVE      *ZEROS        UVKT              9 0
     C                   MOVE      *ZEROS        UKNFA             8 0
     C                   MOVE      *ZEROS        HEVKT
     C                   MOVE      *ZEROS        HEM3
      *
     C     FIFIRM        SETLL     FIRM
     C                   READ      FIRM                                   33
     C     KODTAK        CHAIN     KODTA                              33
     C                   MOVE      KOAKNR        YKN
     C     KUND1K        CHAIN     CUNDL01                            33
     C  N33              DO
     C                   MOVE      KNAVN         XKNAVN           30
     C                   MOVE      ADR1          XADR1            30
     C                   MOVE      ADR2          XADR2            30
     C     POSTNR        IFEQ      0
     C                   MOVEL     ADR3          XADR3            30
     C                   ELSE
     C                   MOVEL     POSTNR        XADR3
     C                   MOVE      ADR3          XADR3
     C                   END
     C                   END
     C     TURERK        CHAIN     TURER                              33
     C                   MOVE      TUDTD         XUDTD
     C                   MOVE      TUDTM         XUDTM
     C                   MOVE      TUDTÅ         XUDTÅ
     C*
      * HENT STRINGKODE
     C                   Z-ADD     95            WTSKRN            3 0
     C                   MOVE      UAVD          LOSAV2            4 0
     C                   CALL      'SYGE01'
     C                   PARM                    LOSAV2
     C                   PARM                    WTSKRN
     C                   PARM                    WOPNVN           10
     C                   PARM                    WOPTY            10
     C                   PARM                    WOPHEA            1
     C                   PARM                    WOPFM             5
     C     WOPFM         IFNE      *BLANKS
     C     WOPFM         CHAIN     KODTFM                             33
     C  N33KFMT1         IFNE      *BLANKS
     C                   MOVEL     KFMT1         XTEST1            1
     C     XTEST1        IFEQ      '¤'
     C                   MOVE      '0'           *IN27
     C                   END
     C                   END
     C                   END
     C*
      *
     C                   ENDSR
      *
     OTRANLIST  E            LHEAD            01
     O                       KFMT1               75
     OTRANLIST  E    27      LHEAD            04
     O                                           60 'L A S T E L I S T E'
     O          E    27      LHEAD            05
     O                                           96 '------------------------'
     O                                          120 '------------------------'
     O          E            LHEAD            06
     O               27                          73 '|'
     O               40                          83 'LISTE NR.'
     O               40      XLNR                85
     O                       W1TYPE             119
     O          E            LHEAD            06
     O                       W1TYPE             119
     O          E            LHEAD            07
     O                       XKNAVN              65
     O               27                          73 '|'
     O          E            LHEAD            08
     O                       XADR1               65
     O               27                          73 '|'
     O          E            LHEAD            09
     O                       XADR2               65
     O               27                          96 '------------------------'
     O               27                         120 '------------------------'
     O          E            LHEAD            10
     O                       XADR3               65
     O               27                          73 '|'
     O          E    27      LHEAD            11
     O                                           73 '|'
     O          E    27      LHEAD            12
     O                                           73 '|'
     O          E            LHEAD            13
     O                                           15 'TURNR:'
     O                       UPRO          Z     30
     O                                           37 'DATO:'
     O                       XUDT          Y     46
     O                                           56 'BILNR:'
     O                       TUBILN              67
     O               27                          73 '|'
     O          E            LHEAD            14
     O               89                          15 'AGENT:'
     O               89      W1KNFA        Z     30
     O               89      W1NAFA              62
     O               27                          73 '|'
     O          E            LHEAD            15
     O                                           21 'TRANSPORTØR:'
     O                       TUKNT2        Z     30
     O                       TUNAT               62
     O               27                          73 '|'
     O          E    27      LHEAD            16
     O                                           73 '|'
     O          E    27      LHEAD            17
     O                                           73 '|'
     O          E    27      LHEAD            18
     O                                           24 '------------------------'
     O                                           48 '------------------------'
     O                                           72 '------------------------'
     O                                           96 '------------------------'
     O                                          120 '------------------------'
     O          E    27      LHEAD            19
     O                                           12 'VAREPOST'
     O                                           16 '|'
     O                                           73 '|'
     O                                           82 'AVS/UTF.'
     O                                           83 '|'
     O                                           94 'BRUTTOVEKT'
     O                                           96 '|'
     O          E    27      LHEAD            20
     O                                           12 '   NR   '
     O                                           16 '|'
     O                                           45 'MERKER, NR, ANTALL KOLLI'
     O                                           70 'OG SLAG; VAREBESKRIVELSE'
     O                                           73 '|'
     O                                           82 '  LAND  '
     O                                           83 '|'
     O                                           94 '   (KG)   '
     O                                           96 '|'
     O                                          114 'FOR TOLLVESENET'
     O          E    27      LHEAD            21
     O                                           24 '------------------------'
     O                                           48 '------------------------'
     O                                           72 '------------------------'
     O                                           96 '------------------------'
     O                                          120 '------------------------'
     O          E   N27      LHEAD            21
     O          E            LDET        1
     O                       XANTO         Z     10
     O                       HEGM1               32
     O                       HENT          Z     41
     O                       HEVS1               70
     O                                           79 'NO'
     O                       HEVKT         Z     92
     O                       WSTLE              104
     O                                          106 '-'
     O                       WSTLL              117
     O               61                         109 'K.UTF'
     O               27                          16 '|'
     O               27                          73 '|'
     O               27                          83 '|'
     O               27                          96 '|'
     O          E            LDET2       1
     O                       HEGM2               32
     O                       HEVS2               70
     O               27                          16 '|'
     O               27                          73 '|'
     O               27                          83 '|'
     O               27                          96 '|'
     O          E            LDET3       1
     O                       WSTXT               70
     O               27                          16 '|'
     O               27                          73 '|'
     O               27                          83 '|'
     O               27                          96 '|'
     O          E    27      LLINJE      1
     O                                           16 '|'
     O                                           73 '|'
     O                                           83 '|'
     O                                           96 '|'
     O          E    27      LBUNN            66
     O                                           24 '------------------------'
     O                                           48 '------------------------'
     O                                           72 '------------------------'
     O                                           96 '------------------------'
     O                                          120 '------------------------'
     O          E            LBUNN            69
     O                       KSINAV             116
     O          E            LBUNN            69
     O                                           15 'RG-0199'
     O                                          120 '........................'
     O          E    27      LBUNN            70
     O                                          120 '     (UNDERSKRIFT)      '
     OTRANLIST  E            LBUNN            71
     O                       KFMT2               75
     OHEADFR    E            CHGHEA
     O                       HETLE
     O                       HETLKU
     O                       HETLL
     OSADTRHR   E            CHGSAE
     O                       SETLL
     O                       SETLE
