     FEDI40D2   CF   E             WORKSTN
     F                                     SFILE(EDI40DB:ZRECNO)
     F                                     INFDS(FBAREA)
     FEDIS1     IF   E           K DISK
     F                                     RENAME(EDISR:EDISR1)
     FEDIS      UF   E           K DISK
     FEDII      IF   E           K DISK
     FEDISS     O  A E             DISK
     D ZA              S              1    DIM(14)
     D ZB              S              1    DIM(35)
     D ZC              S              1    DIM(35)
     D                 DS
     D  ZDTTM                  1     14  0
     D  ZDTT                   1      8  0
     D  ZTMT                   9     14  0
     D                 DS
     D  SDTTM                  1     14  0
     D  SDT                    1      8  0
     D  STM                    9     14  0
     D                 DS
     D  WDT                    1      8
     D  WÅ                     1      4  0
     D  WM                     5      6  0
     D  WD                     7      8  0
     D                 DS
     D  WTID                   1      6  0
     D  WTIDH                  1      2  0
     D  WTIDM                  3      4  0
     D  WTIDS                  5      6  0
      * KOPIER MEMBER TIL LØSNING-1 REMOTERING..
     D CMDRE1          C                   CONST('CPYF FROMFILE(')
     D CMDRE2          C                   CONST('/EDIRE) TOFILE-
     D                                     (TELIF) INCCHAR(REMBR 1 *EQ')
     D CMDRE3          C                   CONST(') MBROPT(*REPLACE) -
     D                                     FMTOPT(*NOCHK)')
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
      * Program status data strukur
     D PSDS           SDS
     D   UserName            354    363
      *//////////////
      *  Hodelogikk /
      *//////////////
     C                   EXSR      SRA
     C                   EXSR      SRB
     C                   MOVE      '1'           *INLR
     C                   RETURN
      *////////////////////////////////////////////////////////////
      *  Initiering                                           SRA /
      *////////////////////////////////////////////////////////////
     C     SRA           BEGSR
      *___________________
      * Definiere Workfelt
      *"""""""""""""""""""
     C     *LIKE         DEFINE    *IN03         WIN03
     C     *LIKE         DEFINE    *IN30         WIN30
     C     *LIKE         DEFINE    INID          WINID
     C     *LIKE         DEFINE    S0004         W0004
     C     *LIKE         DEFINE    S0010         W0010
     C     *LIKE         DEFINE    S0020         W0020
     C                   Z-ADD     0             ZAX               2 0
     C                   Z-ADD     0             WAX               2 0
      *__________________
      * Definiere Nøkkler
      *""""""""""""""""""
     C     KEYA          KLIST
     C                   KFLD                    ZDTF
     C                   KFLD                    ZTMF
      *_____________
      * Init av felt
      *"""""""""""""
     C* Hente dagens dato og klokkeslett.
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C                   MOVEL     WDT           ZDTF
     C                   MOVEL     WDT           ZDTT
     C                   Z-ADD     000000        ZTMF
     C                   Z-ADD     235959        ZTMT
     C                   MOVE      'J'           ZJNS
     C                   MOVE      'J'           ZJNR
     C                   MOVEL     '*ALL'        Z0020
     C                   MOVEL     '*ALL'        Z0004
     C                   MOVEL     '*ALL'        Z0010
     C                   Z-ADD     0             WDTS              8 0
     C                   Z-ADD     0             WTMS              6 0
     C                   Z-ADD     5             LINNBR
     C                   Z-ADD     42            POSNBR
      *__________________
      * Output parametrer
      *""""""""""""""""""
     C     PLISTB        PLIST
     C                   PARM      SSN           SSN
     C                   PARM      S0020         S0020
     C                   PARM      S0036         S0036
     C                   PARM      SST           SST
     C                   PARM      WDTS          WDTS
     C                   PARM      WTMS          WTMS
     C     *IN03         PARM                    WIN03
     C     PLISTC        PLIST
     C                   PARM      SSN           SSN
     C                   PARM      SLIB          SLIB
     C                   PARM      SFILE         SFILE
     C                   PARM      SMBR          SMBR
     C                   PARM      INNETW        INNETW
     C                   PARM      WINID         WINID
     C     *IN30         PARM                    WIN30
     C     PLISTD        PLIST
     C                   PARM      SLIB          SLIB
     C                   PARM      SFILE         SFILE
     C                   PARM      SMBR          SMBR
     C     *IN30         PARM                    WIN30
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Bearbeide Formater                                   SRB /
      *////////////////////////////////////////////////////////////
     C     SRB           BEGSR
      *___________
      * Startbilde
      *"""""""""""
     C     SRB1          TAG
     C  N30              MOVE      'SY00000'     ZMSGNO
     C                   WRITE     EDI40DØ
     C                   EXFMT     EDI40DA
     C                   MOVEA     '0000000'     *IN(30)
     C     *IN03         CABEQ     '1'           SRB9
      * Teste dato fra:
     C                   MOVEL     ZDTF          WDT
     C     WM            COMP      1                                    30
     C  N30WM            COMP      12                                 30
     C  N30WD            COMP      1                                    30
     C  N30WD            COMP      31                                 30
     C   30              MOVE      'SPD0001'     ZMSGNO
     C   30              MOVEA     '1100000'     *IN(30)
     C   30              Z-ADD     5             LINNBR
     C   30              GOTO      SRB1
      * Teste dato til:
     C                   MOVEL     ZDTT          WDT
     C     WM            COMP      1                                    30
     C  N30WM            COMP      12                                 30
     C  N30WD            COMP      1                                    30
     C  N30WD            COMP      31                                 30
     C   30              MOVE      'SPD0001'     ZMSGNO
     C   30              MOVEA     '1001000'     *IN(30)
     C   30              Z-ADD     8             LINNBR
     C   30              GOTO      SRB1
      * Fra dato større en til dato.
     C     ZDTF          IFGT      ZDTT
     C                   MOVE      'SPD0002'     ZMSGNO
     C                   MOVEA     '1101000'     *IN(30)
     C                   Z-ADD     5             LINNBR
     C                   GOTO      SRB1
     C                   END
      * Teste tid fra:
     C                   Z-ADD     ZTMF          WTID
     C  N30WTIDH         COMP      24                                 30
     C  N30WTIDM         COMP      60                                 30
     C  N30WTIDS         COMP      60                                 30
     C   30              MOVE      'SPK0006'     ZMSGNO
     C   30              MOVEA     '1010000'     *IN(30)
     C   30              Z-ADD     6             LINNBR
     C   30              GOTO      SRB1
      * Teste tid til:
     C                   Z-ADD     ZTMT          WTID
     C  N30WTIDH         COMP      24                                 30
     C  N30WTIDM         COMP      60                                 30
     C  N30WTIDS         COMP      60                                 30
     C   30              MOVE      'SPK0006'     ZMSGNO
     C   30              MOVEA     '1000100'     *IN(30)
     C   30              Z-ADD     9             LINNBR
     C   30              GOTO      SRB1
      * Klokkeslett fra får ikke vare større en klokkeslett til.
     C     ZDTF          IFEQ      ZDTT
     C     ZTMF          ANDGT     ZTMT
     C                   MOVE      'SPK0007'     ZMSGNO
     C                   MOVEA     '1010100'     *IN(30)
     C                   Z-ADD     6             LINNBR
     C                   GOTO      SRB1
     C                   END
      * En må sette J på minimum 1 av sendn/mottak.
     C     ZJNS          IFEQ      'N'
     C     ZJNR          ANDEQ     'N'
     C                   MOVE      'SPS0005'     ZMSGNO
     C                   MOVEA     '1000011'     *IN(30)
     C                   Z-ADD     11            LINNBR
     C                   GOTO      SRB1
     C                   END
      * Ordne stopper for søkebegrepp.
     C                   MOVEA     Z0020         ZA
     C     ZA(1)         IFNE      '*'
     C     ZA(1)         ANDNE     ' '
     C                   Z-ADD     0             ZAX
     C     1             DO        14            ZAX
     C     15            SUB       ZAX           WAX
     C     ZA(WAX)       CABNE     *BLANKS       SRB2
     C                   MOVE      '9'           ZA(WAX)
     C                   ENDDO
     C                   ENDIF

     C     SRB2          TAG
      *
      * avsender ordne stopper
      *
     C                   MOVEA     Z0004         ZB
     C     ZB(1)         IFNE      '*'
     C     ZB(1)         ANDNE     ' '
     C                   Z-ADD     0             ZAX
     C     1             DO        35            ZAX
     C     36            SUB       ZAX           WAX
     C     ZB(WAX)       CABNE     *BLANKS       SRB3
     C                   MOVE      '9'           ZB(WAX)
     C                   ENDDO
     C                   ENDIF

     C     SRB3          TAG
      *
      * mottager ordne stopper
      *
     C                   MOVEA     Z0010         ZC
     C     ZC(1)         IFNE      '*'
     C     ZC(1)         ANDNE     ' '
     C                   Z-ADD     0             ZAX
     C     1             DO        35            ZAX
     C     36            SUB       ZAX           WAX
     C     ZC(WAX)       CABNE     *BLANKS       SRB4
     C                   MOVE      '9'           ZC(WAX)
     C                   ENDDO
     C                   ENDIF
      *
     C     SRB4          TAG
      *
     C                   MOVEA     ZB            W0004
     C                   MOVEA     ZA            W0020
     C                   MOVEA     ZC            W0010
      * Forespørsel Utvekslingsid ?
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
     C     *IN04         IFEQ      '1'
     C     LINNBR        ANDEQ     16
     C                   CALL      'EDI52R'
     C                   PARM                    Z0004
     C                   GOTO      SRB1
     C                   ENDIF
     C     *IN04         IFEQ      '1'
     C     LINNBR        ANDEQ     17
     C                   CALL      'EDI52R'
     C                   PARM                    Z0010
     C                   GOTO      SRB1
     C                   ENDIF
      *______________
      * Tømme Subfile
      *""""""""""""""
     C                   MOVE      '1'           *IN93
     C                   WRITE     EDI40DC
     C                   MOVEA     '000'         *IN(93)
     C                   Z-ADD     *ZERO         ZRECNO
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     KEYA          SETLL     EDIS1
     C                   READ      EDIS1                                  94
     C  N94SDTTM         COMP      ZDTTM                              94
     C     *IN94         DOWEQ     '0'
     C                   MOVE      '0'           *IN21
     C     ZJNS          COMP      'N'                                    20
     C   20SSR           COMP      'S'                                    20
     C  N20ZJNR          COMP      'N'                                    21
     C  N20
     CAN 21SSR           COMP      'R'                                    21
      *
      * utvekslingsref
      *
     C                   MOVE      '0'           *IN22
     C     ZA(1)         IFNE      '*'
     C     ZA(1)         ANDNE     ' '
     C     S0020         IFLT      Z0020
     C     S0020         ORGT      W0020
     C                   MOVE      '1'           *IN22
     C                   ENDIF
     C                   ENDIF
      *
      * fra part
      *
     C                   MOVE      '0'           *IN23
     C     ZB(1)         IFNE      '*'
     C     ZB(1)         ANDNE     ' '
      *
     C     S0004         IFLT      Z0004
     C     S0004         ORGT      W0004
     C                   MOVE      '1'           *IN23
     C                   ENDIF
     C                   ENDIF
      *
      * til part
      *
     C                   MOVE      '0'           *IN23
     C     ZC(1)         IFNE      '*'
     C     ZC(1)         ANDNE     ' '
      *
     C     S0010         IFLT      Z0010
     C     S0010         ORGT      W0010
     C                   MOVE      '1'           *IN23
     C                   ENDIF
     C                   ENDIF

     C     *IN20         IFEQ      '0'
     C     *IN21         ANDEQ     '0'
     C     *IN22         ANDEQ     '0'
     C     *IN23         ANDEQ     '0'
     C                   ADD       1             ZRECNO
      *AVSENDER
     C                   MOVE      *BLANKS       WSAVS
     C                   MOVEL     S0004         WINID
     C     WINID         CHAIN     EDII                               21
     C  N21              MOVEL     INNA          WSAVS
     C   21              MOVEL     '*UKJENT*'    WSAVS
      *MOTTAGER
     C                   MOVE      *BLANKS       WSMOT
     C                   MOVEL     S0010         WINID
     C     WINID         CHAIN     EDII                               21
     C  N21              MOVEL     INNA          WSMOT
     C   21              MOVEL     '*UKJENT*'    WSMOT
     C                   WRITE     EDI40DB
     C                   ENDIF

     C                   READ      EDIS1                                  94
     C  N94SDTTM         COMP      ZDTTM                              94
     C  N94              END
     C     ZRECNO        IFEQ      0
     C                   MOVE      'SPR0001'     ZMSGNO
     C                   MOVE      '1'           *IN30
     C                   GOTO      SRB1
     C                   END
     C                   Z-ADD     1             ZRECNO
      *_______________
      * Behandle bilde
      *"""""""""""""""
     C     SRB5          TAG

     C                   MOVEA     '11'          *IN(91)
     C  N30              MOVE      'SY00000'     ZMSGNO
     C                   WRITE     EDI40DØ
     C                   WRITE     EDI40DD
     C                   EXFMT     EDI40DC

     C     *IN12         CABEQ     '1'           SRB1
     C     *IN03         IFEQ      '0'

     C                   MOVEA     '00'          *IN(91)
      *_____________
      * F5=Frisk opp
      *"""""""""""""
     C     *IN05         CABEQ     '1'           SRB2
      *_______
      * Valg ?
      *"""""""
     C                   MOVE      '0'           *IN52
     C                   MOVE      '0'           *IN30
     C                   MOVE      '0'           *IN95
     C     *IN52         DOWEQ     '0'

     C                   READC     EDI40DB                                52
     C     *IN52         IFEQ      '0'
     C     ZOP           CASEQ     '1'           SRBA
     C     ZOP           CASEQ     '5'           SRBE
     C     ZOP           CASEQ     '6'           SRBF
     C     ZOP           CASEQ     '7'           SRBG
     C     ZOP           CASEQ     '8'           SROC
     C     ZOP           CASEQ     'B'           SRAVR
     C     ZOP           CASEQ     '9'           SRBI
     C     ZOP           CASEQ     'L'           SRBL
     C                   END
     C                   END
      *
     C  N30
     CANN52              END
      *
     C     *IN03         CABEQ     '1'           SRB9
     C     *IN95         CABEQ     '1'           SRB2
     C     *IN95         CABEQ     '0'           SRB5
     C                   END
      *
     C     SRB9          ENDSR
      *////////////////////////////////////////////////////////////
      *  Omsendning av sendning.                             SRBA /
      *////////////////////////////////////////////////////////////
     C     SRBA          BEGSR
      * Finnes komponenten kvar ?
     C     SMBR          IFEQ      'REMOVED'
     C                   MOVE      'SPS0007'     ZMSGNO
     C                   MOVE      '1'           *IN30
     C                   GOTO      SRBA9
     C                   END
     C                   CALL      'EDI70C'      PLISTC
     C     *IN30         IFEQ      '1'
     C                   MOVE      'SPS0007'     ZMSGNO
     C                   GOTO      SRBA9
     C                   END
      *__________
      * Omkjøring.
      *""""""""""
     C     SSR           IFEQ      'S'
     C     SSN           CHAIN     EDIS                               30        Sendning
     C     *IN30         IFEQ      '0'
     C                   MOVE      'A'           SST
     C                   UPDATE    EDISR
      * SKRIV POST TIL DETALJ LOGG
     C                   CLEAR                   EDISSR
     C                   Z-ADD     SSN           SSSN
     C                   MOVEL     WDT           SSDT
     C                   MOVEL     WTM           SSTM
     C                   MOVEL     SST           SSST
     C                   EVAL      SSTEXT='Resend is ordered by:'
     C     SSTEXT        CAT       UserName:1    SSTEXT
     C                   WRITE     EDISSR
     C                   ENDIF
     C                   ELSE
     C                   CALL      'EDI32C'      PLISTC                         Mottak
     C                   END
     C     *IN30         IFEQ      '1'
     C                   MOVE      'SPS0009'     ZMSGNO
     C                   GOTO      SRBA9
     C                   END
      *_________________
      * Oppdatere SUBFIL
      *"""""""""""""""""
     C                   MOVE      *BLANKS       ZOP
     C                   UPDATE    EDI40DB
      *
     C     SRBA9         ENDSR
      *////////////////////////////////////////////////////////////
      *  Vise hele sendningen.                               SRBE /
      *////////////////////////////////////////////////////////////
     C     SRBE          BEGSR
     C                   MOVE      'SY00000'     ZMSGNO
     C                   EXFMT     EDI40DE
      *_________________
      * Oppdatere SUBFIL
      *"""""""""""""""""
     C                   MOVE      *BLANKS       ZOP
     C                   UPDATE    EDI40DB
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Vise alle meldinger i sendningen.                   SRBF /
      *////////////////////////////////////////////////////////////
     C     SRBF          BEGSR
      *________________________
      * Call på eksternt CL-pgm
      *""""""""""""""""""""""""
     C                   Z-ADD     SDT           WDTS
     C                   Z-ADD     STM           WTMS
     C                   MOVEL     S0004         W0004X            6
     C                   MOVEL     S0010         W0010X            6
     C     W0004X        IFEQ      'NO0001'
     C     W0010X        OREQ      'NO0001'
     C                   CALL      'TVI66R'      PLISTB
     C                   ELSE
     C                   CALL      'EDI66R'
     C                   PARM                    SSN
     C                   ENDIF
     C     *IN30         IFEQ      '1'
     C                   MOVE      'SPM0001'     ZMSGNO
     C                   GOTO      SRBF9
     C                   END
      *_________________
      * Oppdatere SUBFIL
      *"""""""""""""""""
     C                   MOVE      *BLANKS       ZOP
     C                   UPDATE    EDI40DB
      *
     C     SRBF9         ENDSR
      *////////////////////////////////////////////////////////////
      *  Vise fysisk komponent.                              SRBG /
      *////////////////////////////////////////////////////////////
     C     SRBG          BEGSR
      * Finnes komponenten kvar ?
     C     SMBR          IFEQ      'REMOVED'
     C                   MOVE      'SPS0007'     ZMSGNO
     C                   MOVE      '1'           *IN30
     C                   GOTO      SRBG9
     C                   END
      * Vise komponent.
     C                   SELECT
     C     SFILE         WHENEQ    'EDIRE'
     C                   CALL      'EDI44R'
     C                   PARM                    SLIB
     C                   PARM                    SFILE
     C                   PARM                    SMBR
     C     SFILE         WHENEQ    'EDIRE1K'
     C                   CALL      'EDI44R1K'
     C                   PARM                    SLIB
     C                   PARM                    SFILE
     C                   PARM                    SMBR
     C     SFILE         WHENEQ    'EDISE'
     C                   CALL      'EDI44S'
     C                   PARM                    SLIB
     C                   PARM                    SFILE
     C                   PARM                    SMBR
     C     SFILE         WHENEQ    'EDISE1K'
     C                   CALL      'EDI44S1K'
     C                   PARM                    SLIB
     C                   PARM                    SFILE
     C                   PARM                    SMBR
     C                   ENDSL
      *_________________
      * Oppdatere SUBFIL
      *"""""""""""""""""
     C                   MOVE      *BLANKS       ZOP
     C                   UPDATE    EDI40DB
      *
     C     SRBG9         ENDSR
      *////////////////////////////////////////////////////////////
      *  Vise fysisk komponent MED LINEFEED                  SRBI /
      *////////////////////////////////////////////////////////////
     C     SRBI          BEGSR
      * Finnes komponenten kvar ?
     C     SMBR          IFEQ      'REMOVED'
     C                   MOVE      'SPS0007'     ZMSGNO
     C                   MOVE      '1'           *IN30
     C                   GOTO      SRBI9
     C                   END
      * Vise komponent.
     C                   SELECT
     C     SFILE         WHENEQ    'EDIRE'
     C                   CALL      'EDI41R'
     C                   PARM                    SLIB
     C                   PARM                    SFILE
     C                   PARM                    SMBR
     C     SFILE         WHENEQ    'EDIRE1K'
     C                   CALL      'EDI41R1K'
     C                   PARM                    SLIB
     C                   PARM                    SFILE
     C                   PARM                    SMBR
     C     SFILE         WHENEQ    'EDISE'
     C                   CALL      'EDI41S'
     C                   PARM                    SLIB
     C                   PARM                    SFILE
     C                   PARM                    SMBR
     C     SFILE         WHENEQ    'EDISE1K'
     C                   CALL      'EDI41S1K'
     C                   PARM                    SLIB
     C                   PARM                    SFILE
     C                   PARM                    SMBR
     C                   ENDSL
      *_________________
      * Oppdatere SUBFIL
      *"""""""""""""""""
     C                   MOVE      *BLANKS       ZOP
     C                   UPDATE    EDI40DB
      *
     C     SRBI9         ENDSR
      *
      *////////////////////////////////////////////////////////////
      *  Overføre member til Løsning 1 OCR/REMITERING        SROC /
      *////////////////////////////////////////////////////////////
     C     SROC          BEGSR
      * Finnes komponenten kvar ?
     C     SMBR          IFEQ      'REMOVED'
     C                   MOVE      'SPS0007'     ZMSGNO
     C                   MOVE      '1'           *IN30
     C                   GOTO      SROC9
     C                   END
      *________________________
      * Er det OCR/Rem.dta?
      *""""""""""""""""""""""""
     C     S0026         IFNE      'REM'
     C     S0026         ANDNE     'OCR'
     C                   MOVE      *ON           *IN30
     C                   MOVE      'JOV0408'     ZMSGNO
     C                   GOTO      SROC9
     C                   END
      *________________________
      * Overfør fila
      *""""""""""""""""""""""""
      * OCR INNBETALING
     C     S0026         IFEQ      'OCR'
     C                   CALL      'EDIOCRCL'
     C                   PARM                    SMBR
     C                   PARM                    SLIB
     C                   MOVE      *ON           *IN30
     C                   MOVE      'JOV0410'     ZMSGNO
     C                   ELSE
      *
      * REMMITERING
      *
     C     CMDRE1        CAT(P)    SLIB:0        QCMREB          256
     C                   CAT       CMDRE2:0      QCMREB
     C                   CAT       SMBR:0        QCMREB
     C                   CAT       CMDRE3:0      QCMREB
     C                   Z-ADD     256           QCMDL            15 5
     C                   CALL      'QCMDEXC'
     C                   PARM                    QCMREB
     C                   PARM                    QCMDL
     C                   MOVE      *ON           *IN30
     C                   MOVE      'JOV0409'     ZMSGNO
     C                   END
      *_______________ _
      * Oppdatere EDIS
      *""""""""""""""" "
     C     SSN           CHAIN     EDIS                               31        Sendning
     C     *IN31         IFEQ      '0'
     C                   MOVE      'C'           SST
     C                   UPDATE    EDISR
     C                   ENDIF
      *_________________
      * Oppdatere SUBFIL
      *"""""""""""""""""
     C                   MOVE      *BLANKS       ZOP
     C                   UPDATE    EDI40DB
      *
     C     SROC9         ENDSR
      *////////////////////////////////////////////////////////////
      *  Overføre BANKAVSTEMNING TIL PCFIL                   SRAVR/
      *////////////////////////////////////////////////////////////
     C     SRAVR         BEGSR
      * Finnes komponenten kvar ?
     C     SMBR          IFEQ      'REMOVED'
     C                   MOVE      'SPS0007'     ZMSGNO
     C                   MOVE      '1'           *IN30
     C                   GOTO      SRAV9
     C                   END
      *________________________
      * Er det OCR/Rem.dta?
      *""""""""""""""""""""""""
     C     S0026         IFNE      'AVR'
     C                   MOVE      *ON           *IN30
     C                   MOVE      'JOV0408'     ZMSGNO
     C                   GOTO      SRAV9
     C                   END
      *________________________
      * Overfør fila
      *""""""""""""""""""""""""
     C                   CALL      'EDIBAV'      PLISTC
      *_______________ _
      * Oppdatere EDIS
      *""""""""""""""" "
     C     SSN           CHAIN     EDIS                               31        Sendning
     C     *IN31         IFEQ      '0'
     C                   MOVE      'C'           SST
     C                   UPDATE    EDISR
     C                   ENDIF
      *_________________
      * Oppdatere SUBFIL
      *"""""""""""""""""
     C                   MOVE      *BLANKS       ZOP
     C                   UPDATE    EDI40DB
      *
     C     SRAV9         ENDSR
      *////////////////////////////////////////////////////////////
      *  Vise loggdetaljer                                   SRBL /
      *////////////////////////////////////////////////////////////
     C     SRBL          BEGSR
      *_____________________
      * Call på eksternt pgm
      *"""""""""""""""""""""
     C                   CALL      'EDI42R'
     C                   PARM                    SSN
      *_________________
      * Oppdatere SUBFIL
      *"""""""""""""""""
     C                   MOVE      *BLANKS       ZOP
     C                   UPDATE    EDI40DB
      *
     C                   ENDSR
