     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HELP
     A          R SUBFIL                    SFL
     A            WSNR           1A  I  5  5VALUES(' ' '1' '2' '4')
     A  98                                  DSPATR(RI)
     A  98                                  DSPATR(PC)
     A            CC2KD          3A  O  5 12
     A            CC2NA         30A  O  5 17
     A            CC2AD1        30A  O  5 48
     A            CC2AD2        30A  H
     A            CC2AD3        30A  H
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0016)
     A                                      SFLPAG(0015)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A            ZRECNO         4S 0O  2 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2'SYSPED/SYSJ02'
     A                                      COLOR(BLU)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                  1 70TIME
     A                                  1 19MSGCON(40 DIV3075 SYMSGF)
     A                                      DSPATR(HI)
     A                                  3  2MSGCON(30 DIV0016 SYMSGF)
     A                                      COLOR(BLU)
     A N27                              3 34MSGCON(30 DIV0017 SYMSGF)
     A                                      COLOR(BLU)
     A  27                              3 34MSGCON(30 DIV3002 SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(78 DIV3087 SYMSGF)
     A                                      DSPATR(HI)
     A          R BUNN
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A N27                                  CF03(03 'AVSLUTT')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A                                      CF12(12 'FORRIGE')
     A                                      SLNO(22)
     A                                      CLRL(03)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A N27                              1  2MSGCON(18 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A                                  2  2MSGCON(18 DIV0021 SYMSGF)
     A                                      COLOR(BLU)
     A                                  2 22MSGCON(18 DIV0022 SYMSGF)
     A                                      COLOR(BLU)
     A                                  2 42MSGCON(18 DIV0023 SYMSGF)
     A                                      COLOR(BLU)
     A                                  2 62MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  3  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                              3 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A          R VARELI
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2MSGCON(3 DIV3088 SYMSGF)
     A            WSKD           3A  B  1  6DSPATR(UL)
     A  40                                  DSPATR(HI)
     A  40                                  DSPATR(PR)
     A  90                                  DSPATR(RI)
     A                                  1 10MSGCON(4 DIV3089 SYMSGF)
     A            WSNA          30A  B  1 15DSPATR(UL)
     A  89                                  DSPATR(RI)
     A                                  1 46MSGCON(4 DIV3090 SYMSGF)
     A            WSAD1         30A  B  1 51DSPATR(UL)
     A                                  2 10MSGCON(4 DIV3091 SYMSGF)
     A            WSAD2         30A  B  2 15DSPATR(UL)
     A                                  2 46MSGCON(4 DIV3092 SYMSGF)
     A            WSAD3         30A  B  2 51DSPATR(UL)
     A                                  3  2MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O  4  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                              4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
     A          R VAREL2
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A                                  2  2MSGCON(24 DIV3093 SYMSGF)
     A            W2NA          30A  I  2 27DSPATR(UL)
     A                                      DSPATR(PC)
     A                                  3  2MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A          R VAREL3
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A                                  1  2MSGCON(3 DIV3088 SYMSGF)
     A            CC2KD          3A  O  1  6DSPATR(UL)
     A                                      DSPATR(PC)
     A                                      DSPATR(HI)
     A                                  1 10MSGCON(4 DIV3089 SYMSGF)
     A            CC2NA         30A  O  1 15DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  1 46MSGCON(4 DIV3090 SYMSGF)
     A            CC2AD1        30A  O  1 51DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  2 10MSGCON(4 DIV3091 SYMSGF)
     A            CC2AD2        30A  O  2 15DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  2 46MSGCON(4 DIV3092 SYMSGF)
     A            CC2AD3        30A  O  2 51DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  3  2MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A                                  4 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A          R NYVARE
     A                                      CSRLOC(LINNBR     POSNBR)
     A N27                                  CF03(03 'AVSLUTT')
     A                                      CF05(05 'Frisk')
     A                                      CF06(06 'LAGE NY')
     A                                      CF08(08 'SØK')
     A  27                                  CF12(12 'AVSLUTT')
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/SYSJ02'
     A                                      COLOR(BLU)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                  1 69TIME
     A                                  1 19MSGCON(40 DIV3075 SYMSGF)
     A                                      DSPATR(HI)
     A                                  3  2MSGCON(30 DIV0016 SYMSGF)
     A                                      COLOR(BLU)
     A N27                              3 34MSGCON(30 DIV0017 SYMSGF)
     A                                      COLOR(BLU)
     A  27                              3 34MSGCON(30 DIV3002 SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(78 DIV3087 SYMSGF)
     A                                      DSPATR(HI)
     A N27                             22  2MSGCON(18 DIV0020 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23  2MSGCON(18 DIV0021 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(18 DIV0022 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 42MSGCON(18 DIV0023 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 62MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A  30        MSGTXT        78A  O 24  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A N30                             24 40'(C) COPYRIGHT  SYSTEMA A/S  1990  '
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
