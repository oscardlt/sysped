********************************************************************
      * RETTINGER I DETTE PROGRAM:
PTFnr:*Sek Sig Vers Beskrivelse...........................
""""""*"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
10028 * 01 YBC PTF10 RESTORDRE
********************************************************************
     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HELP
     A                                      ALTHELP(CA01)
      * "NOERASE" MÅ HA FOR Å IKKE TØMME BILDE SOM MAN KOMME FRA
     A          R NOERASE
     A                                      ASSUME
     A                                  1  2' '
      * MAN KUN DEFINNERE VINDU PÅ REKORDNIVÅ
     A          R WINDOWSZ
S01  A*                                     WINDOW(5 2 13 65 *NOMSGLIN)
N01  A                                      WINDOW(5 2 13 73 *NOMSGLIN)
     A                                      OVERLAY
      *
     A          R BILDE01
     A                                      WINDOW(WINDOWSZ)
     A                                      CF03(03 'AVSLUTT')
     A                                      CF04(04 'Søk')
     A                                      CF12(12 'FORRIGE')
     A                                      OVERLAY
     A                                      CSRLOC(LINNBR     POSNBR)
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/LL036R'
     A                                      COLOR(BLU)
     A                                  1 19'Arbeid med default verdier'
     A                                      DSPATR(HI)
     A                                  1 55DATE
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  2 55TIME
     A                                      COLOR(BLU)
     A                                  2  2'Lagerkunde:'
     A            UKN            8Y 0O  2 14DSPATR(UL)
     A                                      DSPATR(HI)
     A                                      EDTCDE(4)
     A            UKNAVN        30A  O  2 24DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  4  2'Mottaksrapport:'
     A                                  5  4'Adr.:'
     A            WSFMVA         5Y 0B  5 11DSPATR(UL)
     A  99                                  DSPATR(HI)
     A                                      EDTCDE(4)
     A                                  5 21'Sort:'
     A            WSMSO          1A  B  5 27DSPATR(UL)
     A                                      VALUES('A' 'I' ' ')
     A                                  5 29'(A=Art. I=Innl.)'
N01  A                                  5 46'Rest:'
N01  A            WSMRS1         1A  B  5 52DSPATR(UL)
N01  A                                      VALUES('J' 'N' ' ')
N01  A                                  5 54'(J/N)'
N01  A                                  5 60'Tild:'
N01  A            WSMRS2         1A  B  5 66DSPATR(UL)
N01  A                                      VALUES('J' 'N' ' ')
N01  A                                  5 68'(J/N)'
     A                                  6  4'Avd. :'
     A            WSMAVD         4Y 0B  6 11DSPATR(UL)
     A  97                                  DSPATR(HI)
     A                                      EDTCDE(4)
     A                                  6 17'Modul:'
     A            WSMUR          1A  B  6 24DSPATR(UL)
     A                                      VALUES('A' 'B' 'C' 'D' 'H')
     A                                  6 27'O.typ:'
     A            WSMOT          2A  B  6 34DSPATR(UL)
     A  96                                  DSPATR(HI)
     A                                  6 38'Frank:'
     A            WSMFR          3A  B  6 45DSPATR(UL)
     A  95                                  DSPATR(HI)
     A                                  7  4'St.VB:'
     A            WSMVB          1A  B  7 11DSPATR(UL)
     A                                      VALUES('J' 'N')
     A                                  7 17'Fullf:'
     A            WSMAFF         1A  B  7 24DSPATR(UL)
     A                                      VALUES('J' 'N')
     A                                  7 27'A.fak:'
     A            WSMFAK         1A  B  7 34DSPATR(UL)
     A  94                                  DSPATR(HI)
     A                                      VALUES('J' 'N')
     A                                  7 38'S.kø :'
     A            WSMFKØ         1A  B  7 45DSPATR(UL)
     A  93                                  DSPATR(HI)
     A                                      VALUES('J' 'N')
     A                                  7 49'MR.st:'
     A            WSMMRS         1A  B  7 56DSPATR(UL)
     A                                      VALUES('J' 'N')
     A                                  8  2'Plukkliste:'
     A                                  9  2'Adr.:'
     A            WSFAVA         5Y 0B  9 11DSPATR(UL)
     A  98                                  DSPATR(HI)
     A                                      EDTCDE(4)
     A                                  9 21'Måte:'
     A            WSPRF          1A  B  9 27DSPATR(UL)
     A                                      VALUES('I' 'U' ' ')
     A                                  9 29'(I=Innl. U=Utl.dato)'
N01  A                                  9 51'Rest:'
N01  A            WSPRES         1A  B  9 57DSPATR(UL)
N01  A                                      VALUES('J' 'N' ' ')
N01  A                                  9 59'(J/N)'
     A                                 10  4'Avd. :'
     A            WSPAVD         4Y 0B 10 11DSPATR(UL)
     A  92                                  DSPATR(HI)
     A                                      EDTCDE(4)
     A                                 10 17'Modul:'
     A            WSPUR          1A  B 10 24DSPATR(UL)
     A                                      VALUES('A' 'B' 'C' 'D' 'H')
     A                                 10 27'O.typ:'
     A            WSPOT          2A  B 10 34DSPATR(UL)
     A  91                                  DSPATR(HI)
     A                                 10 38'Frank:'
     A            WSPFR          3A  B 10 45DSPATR(UL)
     A  90                                  DSPATR(HI)
     A                                 11  4'St.VB:'
     A            WSPVB          1A  B 11 11DSPATR(UL)
     A                                      VALUES('J' 'N')
     A                                 11 17'Fullf:'
     A            WSPAFF         1A  B 11 24DSPATR(UL)
     A                                      VALUES('J' 'N')
     A                                 11 27'A.fak:'
     A            WSPFAK         1A  B 11 34DSPATR(UL)
     A  89                                  DSPATR(HI)
     A                                      VALUES('J' 'N')
     A                                 11 38'S.kø :'
     A            WSPFKØ         1A  B 11 45DSPATR(UL)
     A  88                                  DSPATR(HI)
     A                                      VALUES('J' 'N')
     A                                 12  4'Fr.br:'
     A            WSPFB          1A  B 12 11DSPATR(UL)
     A                                      VALUES('J' 'N')
     A                                 12 17'M.lap:'
     A            WSPML          1A  B 12 24DSPATR(UL)
     A                                      VALUES('J' 'N')
     A                                 12 27'Pl.st:'
     A            WSPPLS         1A  B 12 34DSPATR(UL)
     A                                      VALUES('J' 'N')
     A N30                             13  2'F3/F12=Avbryt   F4=Søk'
     A                                      COLOR(BLU)
     A  30        MSGTXT        58A  O 13  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
     A            MSGNR          7A  P
