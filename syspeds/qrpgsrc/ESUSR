      **********************************************************************
      * Seperat prog for å teste gyldig Switch user, Kalles fra HTML SYMN1 *
      *   Dersom det samme skulle oppnås gjennom parametre/kal til SYMN0   *
      *   måtte symn0 kalles med passord / sikkerhet..                     *
      * MULIG DETTE PROG KAN SKRIVES VEKK / TAS INN I SYMN0                *
      **********************************************************************
      *  After compiling this RPG MODULE,
      *  create the related program with the following command:
      *
CRTPG+*  CRTPGM SYSPED/ESUSR MODULE(*LIBL/ESUSR *LIBL/CHECKUSR
CRTPGM*         *LIBL/INCGI) ACTGRP(*NEW) AUT(*USE)
      *
      *********************************************************************
      *
      /copy syspeds/qrpgsrcpy,hspecs
      /copy syspeds/qrpgsrcpy,hspecsbnd
      *--------------------------------------------------------------------
      * Data base files
      *--------------------------------------------------------------------
     FBRIDF     IF   E           K DISK    usropn
     FBRPARF    IF   E           k disk    usropn
      *--------------------------------------------------------------------
      * Prototype defintions and standard system API error structure
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prototypeb
      /copy syspeds/qrpgsrcpy,usec
      *--------------------------------------------------------------------
      * Variables common to CGI programs
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,variables3
      *--------------------------------------------------------------------
      * Variables received from the input string
     D WSUSER          s             10
     D UsrSpcName      s             10
     D SwitchTo        s             10
      * andre variabler
     D Fn              s             10
     D Lib             s             10
     D Mbr             s             10
     D FEILTXT         s             70
     D SwitchOK        s              1
     D SwitchPW        s             10
      *
     D PGM             C                   CONST('SYSPED/CHECKUSR')
      *
     D                 DS
     D  PAVRDA                 1     50
     D  PAVRDA_01_10           1     10
     D  PAVRDA_11_50          11     50
      *--------------------------------------------------------------------
      * Prolog common to CGI programs
      * -Receive input from the remote browser
      *--------------------------------------------------------------------
      /copy syspeds/qrpgsrcpy,prolog3
      *=====================================================================******
      * MAIN PROCESS LINE
      *=====================================================================******
      * Parse input string
     C                   EXSR      PARSE
     C                   EXSR      INIT
     C                   CALLB     PGM
     C                   PARM                    BIBRID
     C                   PARM                    UsrSpcName
     C                   PARM                    UINN              1
     C                   IF        UINN = 'N'
     C                   GOTO      SLUTT
     C                   END
      *
     C                   exsr      SetSkl
      * Read skeleton output html, etc.
     C                   callp     gethtml(fn:lib:mbr:'/CGITAG/')
     C                   exsr      SetTop
     C                   callp     wrtsection('top')
     C                   callp     wrtsection('foundsome')
     c                   exsr      switchSR
     C                   callp     wrtsection('foundsome2')
     C                   callp     wrtsection('endhtml')
     C                   callp     wrtsection('*fini')
      *
     C     slutt         tag
     C                   close     BRIDF
     C                   close     BRPARF
     C                   eval      *inlr = *on
     C                   RETURN
      *
      *****************************************************
     C     SWITCHSR      begsr
      *****************************************************
      * Hvis det finnes minst en Multi-ID-parameter (SwitchTo-user) så vis pulldown
     c                   move      'MLTID'       PAID
     C     BRPARFK       setll     BRPARF
     C     BRPARFK       reade     BRPARF                                 40
     C     *in40         cabeq     '1'           UtSwitch
     C                   callp     wrtsection('switchtop')
     C     *in40         doweq     '0'
     C                   callp     updhtmlvar('ID':PAVRDA_01_10)
     C                   callp     updhtmlvar('IDTEXT':PAVRDA_11_50)
     C                   callp     wrtsection('switchrow')
     c     SwitchTo      ifne      *blanks
     c     SwitchTo      andeq     PAVRDA_01_10
     c     SwitchOK      andne     'Y'
     C     SwitchTo      CHAIN     BRIDF                              33
     c     *in33         IFEQ      *OFF
     c                   eval      SwitchPW = BIPO
     c                   eval      FEILTXT='Switching to '+SwitchTO+' / '
     c                             + SwitchPW
     c                   eval      SwitchOK ='Y'
     c                   end
     c                   end
     C     BRPARFK       reade     BRPARF                                 40
     C                   enddo
      *
     c     SwitchTo      ifne      *blanks
     c     SwitchOK      andne     'Y'
     c                   eval      FEILTXT='Switch to user MUST be in the list!'
     c                             + ' Try again, or logg off.'
     c                   end
     C                   callp     updhtmlvar('FEILTXT'  :FEILTXT)
     C                   callp     updhtmlvar('SWITCHOK' :SwitchOK)
     C                   callp     updhtmlvar('SWITCHTO' :SwitchTO)
     C                   callp     updhtmlvar('SWITCHPW' :SwitchPW)
     C                   callp     updhtmlvar('XSPRÅK'   :'  ')
      *
     C                   callp     wrtsection('switchbot')
     C     UtSwitch      endsr
      *=====================================================================******
      * Parse input
      *=====================================================================******
     C     Parse         begsr
     C                   eval      wsuser = zhbgetvar('wsUSER')
     C                   eval      SwitchTo = zhbgetvar('SwitchTo')
     C                   eval      UsrSpcName  = zhbgetvar('UsrSpcName')
     C                   endsr
      *=====================================================================******
      * Set html output skeleton member before its read into memory
      *=====================================================================******
     C     SetSkl        begsr
     C                   eval      Lib = '*LIBL'
     C                   eval      Fn  = 'HTMLSRC'
     C                   eval      Mbr = 'SYMN1'
     C                   endsr
      *=====================================================================******
      *  Set output variables for section /$top
      *  (search criteria)
      *=====================================================================******
     C     SetTop        begsr
BODY C                   callp     updhtmlvar('BODYCLASS':'class='+BIFARG)
     C                   callp     updhtmlvar('USER':WSUSER)
     C                   callp     updHTMLvar('WSNA':BIBESK)
     C                   callp     updHTMLvar('kunr':
     C                             %trim(%editc(BIKUNR:'Z')))
     C                   callp     updHTMLvar('sdato':
     C                             %trim(%editw(BIDTV:'    .  .  ')))
     C                   callp     updHTMLvar('stid':
     C                             %trim(%editw(BITIDV:'  .  .  ')))
      *
     C                   endsr
     C***********************************************
     C     INIT          BEGSR
     C***********************************************
      *  Også wrkcgi ligger i sycgi -
     C                   open      BRIDF
      * MEN chain BRIDF for å validere user..
     C     WSUSER        CHAIN     BRIDF                              33
     C* En "standardvariant" libl: call bound (INCGI=*MODULE) med parameter.
     C     BILIBL        ifeq      *blanks
     C                   callb     'INCGI'
     C                   parm                    BISTDL
     C                   else
     C* Helt egen bibl.liste satt på user - normal CALL (BILIBL er "*pgm")
     C                   call      BILIBL
     C                   end
     C                   OPEN      BRPARF
     C     BRPARFK       klist
     C                   kfld                    BIBRID
     C                   kfld                    PAKUNR
     C                   kfld                    PAID
     c                   move      'MLTID'       PAID
      *
     C                   ENDSR
