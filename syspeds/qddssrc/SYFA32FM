     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      REF(*LIBL/VARELI VARELIR)
     A                                      PRINT
     A                                      CF03(03 'Exit')
     A          R NOREC
     A                                      TEXT('Select')
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A                                      CF12(12)
     A                                      CF06(06 'LAG NY')
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/SYFA32'
     A                                      COLOR(BLU)
     A                                  1 20'Vedlikeh. Lastebærere/varelinjer f-
     A                                      or oppdrag '
     A                                      COLOR(WHT)
     A                                  1 73DATE
     A                                      EDTCDE(Y)
     A                                  2  2'Avd./oppdr.nr:'
     A            UAVD           4Y 0O  2 20EDTCDE(4)
     A            UOPD           7Y 0O  2 25EDTCDE(4)
     A                                  2 73TIME
     A                                  3  2MSGCON(040 DIV2057 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  4  2MSGCON(030 DIV0017 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  2'Op Enhets ID        Type  Vareslag-
     A                                                     Brt.vkt Fr.b.vk  Fra-
     A                                      ktpris   '
     A                                      COLOR(WHT)
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 21MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 41MSGCON(018 DIV0022 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 61MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A          R SUBFIL                    SFL
     A                                      TEXT('SUBFILE')
     A            WSNR           1A  B  6  2VALUES(' ' '2' '4' '5')
     A            VLID      R        O  6  4
     A            VLTY      R        O  6 22
     A            XXVS          22A  O  6 28
     A            VLVKT     R        O  6 51EDTCDE(4)
     A            VLFBV     R        O  6 59EDTCDE(4)
     A            VLPRIS    R        O  6 67EDTCDE(M)
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0016)
     A                                      SFLPAG(0015)
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      TEXT('SUBCTL')
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A                                      CF05(05 'Forny')
     A                                      CF06(06 'LAG NY')
     A                                      CF12(12 'Annuller')
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A                                  1  2'SYSPED/SYFA32'
     A                                      COLOR(BLU)
     A                                  1 20'Vedlikeh. Lastebærere/varelinjer f-
     A                                      or oppdrag '
     A                                      COLOR(WHT)
     A                                  1 73DATE
     A                                      EDTCDE(Y)
     A                                  2  2'Avd./oppdr.nr:'
     A            UAVD           4Y 0O  2 20EDTCDE(4)
     A            UOPD           7Y 0O  2 25EDTCDE(4)
     A                                  2 73TIME
     A                                  3  2MSGCON(040 DIV2057 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A            ZRECNO         4S 0O  3 68SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  4  2MSGCON(030 DIV0017 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                  5  2'Op Enhets ID        Type  Vareslag-
     A                                                     Brt.vkt Fr.b.vk  Fra-
     A                                      ktpris   '
     A                                      COLOR(WHT)
     A          R BOTREC
     A                                      TEXT('INFO')
     A                                      OVERLAY
     A                                 23  2MSGCON(018 DIV0020 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 21MSGCON(018 DIV0021 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 41MSGCON(018 DIV0022 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 61MSGCON(018 DIV0024 *LIBL/SYMSGF)
     A                                      COLOR(BLU)
     A          R BLANKREC                  TEXT('Message')
     A                                      OVERLAY
     A            ZMSGTX        79A  O 24  2MSGID(&ZMSGNO SYMSGF)
     A N30                                  COLOR(WHT)
     A  30                                  COLOR(RED)
     A            ZMSGNO         7A  P
     A          R CHGREC
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      CF04(04 'SØKING')
     A                                      CF12(12 'Annuller')
     A                                      SLNO(18)
     A                                      CLRL(07)
     A                                      TEXT('INFO')
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1  2'Kundenr for eier (hvis ønskelig)..-
     A                                      ..:'
     A            WSKN      R        B  1 40REFFLD(VARELIR/VLKN *LIBL/VARELI)
     A                                      EDTCDE(4)
     A  51                                  DSPATR(PR)
     A  70                                  DSPATR(RI)
     A            XXNAVN        30A  O  1 49
     A                                  2  2'Enhets ID:'
     A            WSID      R        B  2 13REFFLD(VARELIR/VLID *LIBL/VARELI)
     A  51                                  DSPATR(PR)
     A  71                                  DSPATR(RI)
     A                                  2 32'Enh.type.:'
     A            WSTY      R        B  2 43REFFLD(VARELIR/VLTY *LIBL/VARELI)
     A  72                                  DSPATR(RI)
     A            XXVT          30A  O  2 49
     A                                  3 32'Varebeskrivelse:'
     A            WSVS      R        B  3 49REFFLD(VARELIR/VLVS *LIBL/VARELI)
     A  77                                  DSPATR(RI)
     A                                  4  2'LxBxH....:'
     A            WSLEN     R        B  4 13REFFLD(VARELIR/VLLEN *LIBL/VARELI)
     A                                      EDTCDE(4)
     A                                  4 20'x'
     A            WSBRD     R        B  4 22REFFLD(VARELIR/VLBRD *LIBL/VARELI)
     A                                      EDTCDE(4)
     A                                  4 29'x'
     A            WSHOY     R        B  4 31REFFLD(VARELIR/VLHOY *LIBL/VARELI)
     A                                      EDTCDE(4)
     A                                  4 40'Kubikk:'
     A            WSVOL     R        B  4 48REFFLD(VARELIR/VLVOL *LIBL/VARELI)
     A                                      EDTWRD('    ,   ')
     A  73                                  DSPATR(RI)
     A                                  4 59'Lastemtr:'
     A            WSLM      R        B  4 71REFFLD(VARELIR/VLLM *LIBL/VARELI)
     A                                      EDTWRD('  ,  ')
     A                                  5  2'Max G.W..:'
     A            WSMGW     R        B  5 13REFFLD(VARELIR/VLMGW *LIBL/VARELI)
     A                                      EDTCDE(4)
     A  74                                  DSPATR(RI)
     A                                  5 21'Tara:'
     A            WSTARA    R        B  5 27REFFLD(VARELIR/VLTARA *LIBL/VARELI)
     A                                      EDTCDE(4)
     A  75                                  DSPATR(RI)
     A                                  5 35'Lastekap.:'
     A            XXNETT    R        B  5 46REFFLD(VARELIR/VLTARA *LIBL/VARELI)
     A                                      EDTCDE(4)
     A                                  5 55'Reell vekt..:'
     A            WSVKT     R        B  5 69REFFLD(VARELIR/VLVKT *LIBL/VARELI)
     A                                      EDTCDE(4)
     A  78                                  DSPATR(RI)
     A                                  6  2'F4=Søk  F12=Forrige'
     A                                      COLOR(BLU)
     A                                  6 32'Fraktber.vkt:'
     A            WSFBV     R        B  6 46REFFLD(VARELIR/VLFBV *LIBL/VARELI)
     A                                      EDTCDE(4)
     A  76                                  DSPATR(RI)
     A                                  6 55'Pris:'
     A            WSPRIS    R        B  6 64REFFLD(VARELIR/VLPRIS *LIBL/VARELI)
     A                                      EDTCDE(M)
     A  79                                  DSPATR(RI)
     A  30        MSGTXT        78A  O  7  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
