     A          R LL014P1
     A            KFMT1         75      1  1
     A  88                              1 81'¤(s15H¤&l0O¤&a-3R'
     A            KFMT2         75      2  1
     A  88                              2 76'¤(12U¤(s0p15h0s3b4099T'
     A N46                              3  2MSGCON(20 YBC1107 SYMSGF)
     A  46                              3  2MSGCON(20 YBC1108 SYMSGF)
     A  45        UNR            1  0   3 21
     A  45                              3 23MSGCON(02 YBC1109 SYMSGF)
     A  45        UTOT           1  0   3 26
     A                                  3 85MSGCON(05 YBC1110 SYMSGF)
     A            XIDAG          8  0   3 91EDTWRD('  .  .    ')
     A                                  3103MSGCON(04 YBC1111 SYMSGF)
     A            XTID           4  0   3109EDTWRD('  :  ')
     A                                  4 85MSGCON(08 YBC1112 SYMSGF)
     A            LHALN          7  0   4 94EDTCDE(Z)
     A                                  4103MSGCON(05 YBC1113 SYMSGF)
     A                                  4110PAGNBR
     A                                  6  2MSGCON(11 YBC1114 SYMSGF)
     A            LHPNR          8  0   6 14EDTCDE(Z)
     A            KNAVN         30A     6 23
     A                                  6 56MSGCON(13 YBC1115 SYMSGF)
     A            LHPAK         17A     6 70
     A                                  6 89MSGCON(14 YBC1116 SYMSGF)
     A            LHTDT          8  0   6104EDTWRD('    .  .  ')
     A                                  7  2MSGCON(11 YBC1117 SYMSGF)
     A            LHBNR         17      7 14
     A            LHBNA         30A     7 33
     A            LHBA1         30A     7 64
     A            LHBA2         30A     8 33
     A            XHBA3         34A     8 64
     A                                  9  2'-------------------------'
     A                                  9 27'-------------------------'
     A                                  9 52'-- Best. -- Lev. --- F O '
     A                                  9 77'R P A K N I N G ---------'
     A                                  9102'------------------'
     A                                 10  2MSGCON(15 YBC1118 SYMSGF)
     A                                 10 18MSGCON(34 YBC1119 SYMSGF)
     A                                 10 54MSGCON(07 YBC1120 SYMSGF)
     A                                 10 62MSGCON(07 YBC1120 SYMSGF)
     A                                 10 70MSGCON(05 YBC1121 SYMSGF)
     A                                 10 76MSGCON(05 YBC1122 SYMSGF)
     A                                 10 82MSGCON(05 YBC1123 SYMSGF)
     A                                 10 88MSGCON(07 YBC1124 SYMSGF)
     A                                 10 96MSGCON(21 YBC1125 SYMSGF)
     A                                 11  2'-------------------------'
     A                                 11 27'-------------------------'
     A                                 11 52'-------------------------'
     A                                 11 77'-------------------------'
     A                                 11102'------------------'
     A                                 12  2' '
      *
     A          R LL014P2                   SPACEB(1)
     A N41        LLANR         15A        2
     A N41        LLVB          35A       18
     A N41        LLANT0         7  0     54EDTCDE(Z)
     A            XXLANT         7  0     62EDTCDE(Z)
     A            XXSAE3         5  0     70EDTCDE(Z)
     A            XXSAE2         5  0     76EDTCDE(Z)
     A            XXSAE1         5  0     82EDTCDE(Z)
     A            XXSAE0         7  0     88EDTCDE(Z)
     A            XXSEN2        21A       96
     A                                      SPACEA(1)
      *
     A          R LL014P3                   SPACEB(2)
     A                                     2'-------------------------'
     A                                    27'-------------------------'
     A                                    52'-------------------------'
     A                                    77'-------------------------'
     A                                   102'------------------'
     A                                      SPACEA(1)
     A                                     2MSGCON(10 YBC1126 SYMSGF)
     A            TLWNT0         7  0     62EDTCDE(Z)
     A            TLWNT3         5  0     70EDTCDE(Z)
     A            TLWNT2         5  0     76EDTCDE(Z)
     A            TLWNT1         5  0     82EDTCDE(Z)
     A            TLWNTL         7  0     88EDTCDE(Z)
     A                                      SPACEA(2)
     A                                     2MSGCON(24 YBC1127 SYMSGF)
     A            LHKLL          5  0     27EDTCDE(Z)
     A                                    33'/'
     A            LHMLP          5  0     35EDTCDE(Z)
     A                                    52MSGCON(25 YBC1128 SYMSGF)
     A            LHBPAL         5  0     78EDTCDE(Z)
     A                                    84'/'
     A            LHAPAL         5  0     86EDTCDE(Z)
     A                                      SPACEA(1)
     A                                     2'Pakket av'
     A            LHPSG         30A       12SPACEA(1)
      *
     A          R LL014P4                   SPACEB(1)
     A N42                                18MSGCON(08 YBC1129 SYMSGF)
     A            SNSN          35A       27
     A                                      SPACEA(1)
