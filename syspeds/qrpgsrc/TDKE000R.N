      *********************************************************************
      *
CRTPG+*  CRTPGM SYSPED/TDKE000R MODULE(*LIBL/TDKE000R *LIBL/INCGI)
CRTPGM*        ACTGRP(CGI) AUT(*USE)
      *
      *********************************************************************
      /copy SYSPEDS/qrpgsrcpy,hspecs
      /copy SYSPEDS/qrpgsrcpy,hspecsbnd
      *********************************************************************
     FBRIDF     IF   E           K DISK    USROPN
     FBRPARF    IF   E           K DISK    USROPN
     FFRISOK    IF   E           K DISK    USROPN
     FDKEV      IF   E             DISK    USROPN
      *--------------------------------------------------------------------
      * Variables common to all CGIs
      *--------------------------------------------------------------------
      /copy SYSPEDS/qrpgsrcpy,prototypeb
      /copy SYSPEDS/qrpgsrcpy,usec
      /copy SYSPEDS/qrpgsrcpy,variables3
     ‚*  Prototype for 'JSONFIX'
     d jsonfix         pr                  extpgm('JSONFIX')
     d jsonstring_                         like(jsonstring)
     ‚*  Prototype for 'INCGI'
     d incgi           pr                  extproc('INCGI')
     d bistdl_                             like(bistdl)
     ‚*  Prototype for variabel program
     D Varpgm          Pr                  ExtPgm(BILIBL)
     ‚*  Prototype for 'SYGE20'
     d syge20          pr                  extpgm('SYGE20')
     d ufunk_                              like(ufunk)
     d udat1_                              like(udat1)
     d udat2_                              like(udat2)
     d udgr_                               like(udgr)
     d WhereC          s            512a
     d FirstW          s              1A
     d cQuote          c                   const('''')
     d SqlStm          s            512a
     d myrows          s             10i 0
     D WSUSER          S             10
     D AVD             S              4
     D AVDN            S              4  0
     D SIGN            S              3
     D OPD             S              7
     D OPDN            S              7S 0
     D REFNR           S             13
     D XREF            S             35
     D DATUM           S              8
     D DATUMN          S              8S 0
     D DATUMT          S              8
     D DATUMTN         S              8S 0
     D STATUS          S              1
     D AVSNAVN         S             35
     D MOTNAVN         S             35
     D AART            S              2
     D FAKT            S             17
     D JSONstring      s          32000
     D Error           S            150
     D AntLest         S              9  0
     D Jn043           S              1
     d X               S             10  0
     d hopp            S              1
     d wspll           S              1
     d STOPP5          S             35
     d pos             S              5U 0
     d udat1           s              8  0
     d udat2           s              8  0
     d udgr            s              5  0
     d ufunk           s              3
     d wpabrid         s                   like(pabrid)
     d wpakunr         s                   like(pakunr)
     d wpasort         s                   like(pasort)
     d MyPoDS          ds                  Qualified  dim(100)
     d DKEH_SYAV                      4S 0
     d DKEH_SYOP                      7S 0
     d DKEH_0035                      1
     d DKEH_07                       13
     d DKEH_AART                      2
     d DKEH_SYSG                      3
     d DKEH_SYDT                      8S 0
     d DKEH_SYST                      1
     d DKEH_02B                      35
     d DKEH_08B                      35
     d DKEH_49                       17
      *get input-string
      /copy syspeds/qrpgsrcpy,prolog3

      /free
         exsr ParseSr;
         exsr init;

       gethtmlifs(
       '/syspeddev/html/JSON.html':
       '/CGITAG/');
       wrtsection('top');

       //...........................................................................................
       //skriv JSON-Object start (alltid '{' + x ant param. m/komma mellom (IKKE komma etter siste))
       //...........................................................................................

        JSONstring='{'
        +'¬user¬ : ¬'+%trim(WSUSER)+'¬, '
        +'¬avd¬ : ¬'+%trim(%editc(AVDN:'Z'))+'¬, '
        +'¬sign¬ : ¬'+%trim(SIGN)+'¬, '
        +'¬opd¬ : ¬'+%trim(%editc(OPDN:'Z'))+'¬, '
        +'¬refnr¬ : ¬'+%trim(REFNR)+'¬, '
        +'¬xref¬ : ¬'+%trim(XREF)+'¬, '
        +'¬datum¬ : ¬'+%trim(%editc(DATUMN:'Z'))+'¬, '
        +'¬status¬ : ¬'+%trim(STATUS)+'¬, '
        +'¬avsNavn¬ : ¬'+%trim(AVSNAVN)+'¬, '
        +'¬motNavn¬ : ¬'+%trim(MOTNAVN)+'¬, '
        +'¬aart¬ : ¬'+%trim(AART)+'¬, '
        +'¬fakt¬ : ¬'+%trim(FAKT)+'¬, '
        +'¬errMsg¬ : ¬'+%trim(Error)+'¬, ';

       //JSONfix escaper " i tekst,  for deretter å bytte ¬->"

       jsonfix ( jsonstring );

       updhtmlvar2('JSONstring'
                  :%addr(jsonstring)
                  :%len(jsonstring));
       wrtsection('JSONlin');

       //...........................................................................................
       // Skriv JSON-LISTE Start (en liste er EN parameter(fra [ til   )
       //..........................................................................................
       jsonstring ='"orderList" : [';

       updhtmlvar2('JSONstring'
                  :%addr(jsonstring)
                  :%len(jsonstring));
       wrtsection('JSONlin');

       if error = *blanks;
         exsr SelSr;
         if myrows > 0;
           exsr LesSr;
         endif;
       endif;
       // ..........................................................................................
       // Skriv JSON-Liste/Array  Avslutning
       // ..........................................................................................
       jsonstring =']';
       updhtmlvar2('JSONstring'
                  :%addr(jsonstring)
                  :%len(jsonstring));
       wrtsection('JSONlin');
       // ..........................................................................................
       // Skriv JSON-string Avsluttning
       // ..........................................................................................
       jsonstring ='}';
       updhtmlvar2('JSONstring'
                  :%addr(jsonstring)
                  :%len(jsonstring));
       wrtsection('JSONlin');
         exsr Exit;

       //-----------------------------------------------------------------
       // Select fra fil via SQL
       //-----------------------------------------------------------------
       begsr     SelSr;

       If    AVDN > 0;
         AVD = %editc(AVDN:'X');
         Exsr  AddAnd;
         Eval  WhereC = ' DKEH_SYAV  = ' + AVD;
       EndIf;

       If    SIGN  <> *blanks;
         Exsr  AddAnd;
         Eval  WhereC =  %trim(WhereC)  +
         ' DKEH_SYSG = ' + cQuote + SIGN + cQuote +' ';
       EndIf;

       If    OPDN > 0;
         OPD = %editc(OPDN:'X');
         Exsr  AddAnd;
         Eval  WhereC =  %trim(WhereC)  +
         ' DKEH_SYOP  = ' + OPD;
       EndIf;

       If    REFNR  <> *blanks;
         Exsr  AddAnd;
         Eval  WhereC =  %trim(WhereC)  +
         ' DKEH_07 LIKE ' + cQuote + '%' + %trim(REFNR) + '%' + cQuote;
        EndIf;

       If    DATUMN > 0;
         DATUM = %editc(DATUMN:'X');
         Exsr  AddAnd;
         Eval  WhereC =  %trim(WhereC)  +
         ' DKEH_SYDT  >= ' + DATUM;
       EndIf;

       If    DATUMTN > 0;
         DATUMT = %editc(DATUMTN:'X');
         Exsr  AddAnd;
         Eval  WhereC =  %trim(WhereC)  +
         ' DKEH_SYDT  <= ' + DATUMT;
       EndIf;

       If    STATUS  <> *blanks and STATUS <> 'b';
         Exsr  AddAnd;
         Eval  WhereC =  %trim(WhereC)  +
         ' DKEH_SYST = ' + cQuote + STATUS + cQuote;
        EndIf;

       If    AVSNAVN  <> *blanks;
         Exsr  AddAnd;
         Eval  WhereC =  %trim(WhereC)  +
         ' DKEH_02B LIKE ' + cQuote + '%' + %trim(AVSNAVN) + '%' + cQuote;
        EndIf;

       If    MOTNAVN  <> *blanks;
         Exsr  AddAnd;
         Eval  WhereC =  %trim(WhereC)  +
         ' DKEH_08B LIKE ' + cQuote + '%' + %trim(MOTNAVN) + '%' + cQuote;
        EndIf;

       If    AART  <> *blanks;
         Exsr  AddAnd;
         Eval  WhereC =  %trim(WhereC)  +
         ' DKEH_AART = ' + cQuote + AART + cQuote +' ';
       EndIf;

         Eval      WhereC =  ' Where ' + %trim(WhereC);

         Eval       SqlStm = 'Select DKEH_SYAV, DKEH_SYOP, DKEH_0035, ' +
                             'DKEH_07, DKEH_AART, DKEH_SYSG, DKEH_SYDT, ' +
                             'DKEH_SYST, DKEH_02B, DKEH_08B, DKEH_49 ' +
                             'from DKEH ' +
                              %trim(WhereC)  +
                             ' Order by '       +
                             'DKEH_SYDT desc ' +
                             'FOR READ ONLY ';
         Exec SQL PREPARE DynSel from :SqlStm;
         EXEC SQL DECLARE  A33  cursor for DynSel;
         Exec SQL OPEN A33;
         Myrows = %elem(MyPoDS);
         exec sql FETCH A33   for :MyRows rows
                  INTO :MyPoDs;
       endsr;
       //-----------------------------------------------------------------
       // Set inn "AND" mellom where settninger
       //-----------------------------------------------------------------
       begsr     AddAnd;
       If        FirstW =   'N';
         Eval      WhereC =  %trim(WhereC) + ' and ';
       EndIf;
         Eval      FirstW =   'N';
       endsr;
       //-----------------------------------------------------------------
       // hent input
       //-----------------------------------------------------------------
       begsr     ParseSr;
         WSUSER  = zhbgetvar('USER');
         AVD     = zhbgetvar('AVD');
         AVDN    = c2n2(AVD);
         SIGN    = zhbgetvar('SIGN');
         OPD     = zhbgetvar('OPD');
         OPDN    = c2n2(OPD);
         REFNR   = zhbgetvar('REFNR');
         XREF    = zhbgetvar('XREF');
         DATUM   = zhbgetvar('DATUM');
         DATUMN  = c2n2(DATUM);
         datumt  = zhbgetvar('DATUMT');
         datumtn = c2n2(datumt);
         STATUS  = zhbgetvar('STATUS');
         AVSNAVN = zhbgetvar('AVSNAVN');
         MOTNAVN = zhbgetvar('MOTNAVN');
         AART    = zhbgetvar('AART');
         FAKT    = zhbgetvar('FAKT');
        endsr;
        // __________________________________________________
        // Læs array                                    LESSR
        // """"""""""""""""""""""""""""""""""""""""""""""""""
           begsr LesSr;
       for x = 1 to myrows;

       if mypods(x).DKEH_sysg = *blanks;
         leave;
       endif;

           chain (mypods(x).THAVD: mypods(x).THTDN: '*CE') FRISOK;
           if not %found;
              fssok = *blanks;
           endif;


        // Valider extern ref
           error=' ';
           if error = *blanks;

        // Lægg ut Json stræng
           AntLest = AntLest + 1;

        // Skriv en JSON-Array-linje pr menypunkt - komma før hvert nytt element

       if AntLest=1;
          JSONstring=*blanks;
          else;
          JSONstring=', ';
          endif;
       JSONstring=%trim(JSONstring)+'{'
          +'¬avd¬ : ¬'+%trim(%editc(mypods(x).DKEH_SYAV:'Z'))+'¬, '
          +'¬opd¬ : ¬'+%trim(%editc(mypods(x).DKEH_SYOP:'Z'))+'¬, '
          +'¬tst¬ : ¬'+%trim(mypods(x).DKEH_0035)+'¬, '
          +'¬refnr¬ : ¬'+%trim(mypods(x).DKEH_07)+'¬, '
          +'¬dkeh_xref¬ : ¬'+%trim(FSSOK)+'¬, '
          +'¬aart¬ : ¬'+%trim(mypods(x).DKEH_AART)+'¬, '
          +'¬sign¬ : ¬'+%trim(mypods(x).DKEH_SYSG)+'¬, '
          +'¬datum¬ : ¬'+%trim(%editc(mypods(x).DKEH_SYDT:'Z'))+'¬, '
          +'¬status¬ : ¬'+%trim(mypods(x).DKEH_SYST)+'¬, '
          +'¬avsNavn¬ : ¬'+%trim(mypods(x).DKEH_02B)+'¬, '
          +'¬motNavn¬ : ¬'+%trim(mypods(x).DKEH_08B)+'¬, '
          +'¬godsnr¬ : ¬'+%trim(mypods(x).DKEH_49)+'¬}';
         jsonfix (JSONstring);
       updhtmlvar2('JSONstring'
                  :%addr(jsonstring)
                  :%len(jsonstring));
         wrtsection ('JSONlin');

        endif;

        endfor;

       endsr;
       //********************************************************
       begsr init;
       //********************************************************
         open bridf;
         open brparf;
         open frisok;
         // Test innlogging
         chain wsuser bridf;

         if %found;
           if bilibl = *blanks;
             incgi ( bistdl );
           else;

             Monitor;
                   Varpgm();
                 On-Error;
                   //
                 EndMon;

           endif;
         else;

           error = 'Invalid userID';
         endif;

         // Er dato= blank eller 0, sett  da dato 7 dager tilbake
         if opdn=*zeros and datumn=*zeros;

           wpabrid=wsuser;
           wpakunr=0;
           wpasort=0;
           chain (WPABRID: WPAKUNR: 'DFTDG': WPASORT) BRPARF;
           if not %found or pavrdn = 0;
              pavrdn=7;
           endif;

           ufunk = 'SUB';
           udat1 = datumn;
           udgr = pavrdn;
           syge20 ( ufunk : udat1 : udat2 : udgr );
           datumn=udat2;
         endif;

       endsr;
       //=====================================================================
       // Close output html and quit
       //=====================================================================
       begsr exit;
         // Lukk fil
         close bridf;
         close brparf;
         close frisok;

         wrtsection('*fini');
         *inlr = *on;
         return;
       endsr;
      /end-free
