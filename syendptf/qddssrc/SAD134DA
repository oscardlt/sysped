     A                                      DSPSIZ(27 132 *DS4)
     A                                      PRINT
     A                                      HELP
     A                                      ALTHELP
      *
     A          R WINDOWSZ
     A                                      WINDOW(*DFT 12 80 *NOMSGLIN)
     A                                      OVERLAY
      *
     A          R BILDE01
     A                                      WINDOW(WINDOWSZ)
     A N49                                  CF04(04 'Søking')
     A                                      CF09(09 'Hent UUID')
     A                                      CA12(12 'Avbryt')
     A                                      CSRLOC(LINNBR     POSNBR)
     A                                      OVERLAY
     A            LINNBR         3S 0H
     A            POSNBR         3S 0H
     A            MSGNR          7A  P
     A                                  1  2'SYSPED/SAD134RA'
     A                                      COLOR(BLU)
     A                                  1 20'Arbeid med oppdrag'
     A                                      COLOR(WHT)
     A                                  2  2'Avd.:'
     A            CLAVD          4Y 0O  2  8TEXT('AVDELINGSNR')
     A                                      COLOR(BLU)
     A                                      EDTCDE(4)
     A                                  2 13'Opdnr:'
     A            CLTDN          7Y 0O  2 20TEXT('Turnr')
     A                                      COLOR(BLU)
     A                                      EDTCDE(4)
     A                                  2 32'Linjenr:'
     A            ULI            2Y 0O  2 42TEXT('Deklarantnr')
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                      EDTCDE(4)
      *
     A                                  4  2'Kolli:'
     A            WSNTK          7Y 0B  4 15TEXT('Vekt')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N99                                  COLOR(YLW)
     A  99                                  COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  4 23'Vekt:'
     A            WSVKB          9Y 0B  4 29TEXT('Vekt')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N98                                  COLOR(YLW)
     A  98                                  COLOR(WHT)
     A                                      EDTCDE(4)
     A                                  4 40'V.beskr:'
     A            WSVT          30A  B  4 49TEXT('Vekt')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N97                                  COLOR(YLW)
     A  97                                  COLOR(WHT)
      *
     A                                  5  2'Prosedyre:'
     A            WSPR           2A  B  5 15TEXT('Type')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N96                                  COLOR(YLW)
     A  96                                  COLOR(WHT)
     A            WSPRT         45A  O  5 21COLOR(WHT)
      *
     A                                  6  2'Deklarant:'
     A            WSRG          11A  B  6 15TEXT('Senders org.nr')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N95                                  COLOR(YLW)
     A  95                                  COLOR(WHT)
     A                                  6 27'Dato:'
     A            WS0068A        6Y 0B  6 33TEXT('Sendingsdato')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N94                                  COLOR(YLW)
     A  94                                  COLOR(WHT)
     A                                      EDTWRD('  .  .  ')
     A                                  6 42'Sekvens:'
     A            WS0068B        6Y 0B  6 51TEXT('Sendingsdato')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N93                                  COLOR(YLW)
     A  96                                  COLOR(WHT)
     A                                      EDTCDE(4)
      *
     A                                  7  2'MRN-nr:'
     A            WSTRNR        18A  B  7 15TEXT('MRN-nr')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N92                                  COLOR(YLW)
     A  92                                  COLOR(WHT)
     A                                  7 34'Avs:'
     A            WSNAS         30A  B  7 39TEXT('Avsender')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N91                                  COLOR(YLW)
     A  91                                  COLOR(WHT)
     A                                  8 34'Mot:'
     A            WSNAK         30A  B  8 39TEXT('Mottaker')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N90                                  COLOR(YLW)
     A  90                                  COLOR(WHT)
      *
     A                                  9  2'Eksp.type:'
     A            WSETYP         2A  B  9 15TEXT('Type')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N89                                  COLOR(YLW)
     A  89                                  COLOR(WHT)
     A            WSETYPT       45A  O  9 21COLOR(WHT)
     A                                 10  2'Eksport Id:'
     A            WSEID         18A  B 10 15TEXT('Eksport Id')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A N88                                  COLOR(YLW)
     A  88                                  COLOR(WHT)
     A                                 10 35'Sertifisert:'
     A            WSESER         1A  B 10 48TEXT('Adr-1 Deklarant')
     A                                      DSPATR(UL)
     A  49                                  DSPATR(PR)
     A                                      COLOR(YLW)
     A                                      VALUES(' ' 'J' 'N')
     A                                 10 50'(J/N)'
     A N49                             11  2'F4=Søk   F12=Avbryt'
     A                                      COLOR(BLU)
     A  49                             11  2'F12=Avbryt'
     A                                      COLOR(BLU)
     A  30        MSGTXT        68A  O 12  2MSGID(&MSGNR SYMSGF)
     A                                      COLOR(WHT)
