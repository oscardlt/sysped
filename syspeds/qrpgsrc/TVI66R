     FTVI66D    CF   E             WORKSTN
     F                                     SFILE(TVI66DA:ZRECNO)
     FEDIM6     IF   E           K DISK
      *//////////////
      *  Hodelogikk /
      *//////////////
     C                   EXSR      SRA
     C                   EXSR      SRB
     C                   MOVE      '1'           *INLR
      *////////////////////////////////////////////////////////////
      *  Initiering                                           SRA /
      *////////////////////////////////////////////////////////////
     C     SRA           BEGSR
      *___________________
      * Definiere Workfelt
      *"""""""""""""""""""
     C     *LIKE         DEFINE    MSN           WSN
     C     *LIKE         DEFINE    *IN03         WIN03
     C     *LIKE         DEFINE    *IN30         WIN30
      *__________________
      * Definiere Nøkkler
      *""""""""""""""""""
     C     KEYA          KLIST
     C                   KFLD                    WSN
      *_________________
      * Input parametrer
      *"""""""""""""""""
     C     *ENTRY        PLIST
     C     WSN           PARM                    WSN
     C     W0020         PARM                    W0020
     C     W0036         PARM                    W0036
     C     WST           PARM                    WST
     C     WDT           PARM                    WDT
     C     WTM           PARM                    WTM
     C                   PARM      *IN03         WIN03
      *__________________
      * Output parametrer
      *""""""""""""""""""
     C     PLISTA        PLIST
     C                   PARM      MMN           MMN
     C     *IN03         PARM                    WIN03
     C     PLISTB        PLIST
     C                   PARM      MMN           MMN
     C                   PARM      MAVD          MAVD
     C                   PARM      MTDN          MTDN
     C                   PARM      M0068A        M0068A
     C                   PARM      M0068B        M0068B
     C                   PARM      M0068C        M0068C
     C     *IN03         PARM                    WIN03
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Bearbeide Formater                                   SRB /
      *////////////////////////////////////////////////////////////
     C     SRB           BEGSR
      *
     C     SRB1          TAG
      *______________
      * Tømme Subfile
      *""""""""""""""
     C                   MOVE      '1'           *IN30
     C                   MOVE      '1'           *IN93
     C                   WRITE     TVI66DB
     C                   MOVEA     '000'         *IN(93)
     C                   Z-ADD     *ZERO         ZRECNO
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C     KEYA          CHAIN     EDIM6                              94
      *
     C     *IN94         DOWEQ     '0'
     C                   MOVE      '0'           *IN30
     C                   ADD       1             ZRECNO
      *
     C     M1001         IFEQ      '929'
     C                   MOVE      'TVIN-I'      WSIMEX
     C                   ELSE
     C     M1001         IFEQ      '830'
     C                   MOVE      'TVIN-E'      WSIMEX
     C                   ELSE
     C                   MOVE      M0065         WSIMEX
     C                   END
     C                   END
      *
     C                   WRITE     TVI66DA
     C     KEYA          READE     EDIM6                                  94
     C  N94              END
     C     ZRECNO        CABEQ     0             SRB9
     C                   Z-ADD     1             ZRECNO
      *_______________
      * Behandle bilde
      *"""""""""""""""
     C     SRB3          TAG
      *
     C                   MOVEA     '11'          *IN(91)
     C  N30              MOVE      'SY00000'     ZMSGNO
     C                   WRITE     TVI66DØ
     C                   WRITE     TVI66DC
     C                   EXFMT     TVI66DB
      *
     C     *IN03         IFEQ      '0'
     C     *IN12         ANDEQ     '0'
      *
     C                   MOVEA     '00'          *IN(91)
      *_____________
      * F5=Frisk opp
      *"""""""""""""
     C     *IN05         CABEQ     '1'           SRB1
      *_______
      * Valg ?
      *"""""""
     C                   MOVE      '0'           *IN52
     C                   MOVE      '0'           *IN30
     C                   MOVE      '0'           *IN95
     C     *IN52         DOWEQ     '0'
      *
     C                   READC     TVI66DA                                52
     C     *IN52         IFEQ      '0'
     C     ZOP           CASEQ     '5'           SRBE
     C     ZOP           CASEQ     '6'           SRBF
     C                   END
     C                   END
      *
     C  N30
     CANN52              END
      *
     C     *IN03         CABEQ     '1'           SRB9
     C     *IN95         CABEQ     '1'           SRB1
     C     *IN95         CABEQ     '0'           SRB3
     C                   END
      *
     C     SRB9          ENDSR
      *////////////////////////////////////////////////////////////
      *  Vise hele meldingsloggen.                           SRBE /
      *////////////////////////////////////////////////////////////
     C     SRBE          BEGSR
      *________________________
      * Call på eksternt CL-pgm
      *""""""""""""""""""""""""
     C                   CALL      'TVI67R'      PLISTA
      *_________________
      * Oppdatere SUBFIL
      *"""""""""""""""""
     C                   MOVE      *BLANKS       ZOP
     C                   UPDATE    TVI66DA
      *
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Vise feilmeldingstekst                              SRBF /
      *////////////////////////////////////////////////////////////
     C     SRBF          BEGSR
      *________________________
      * Call på eksternt CL-pgm
      *""""""""""""""""""""""""
     C                   CALL      'TVI68R'      PLISTB
      *_________________
      * Oppdatere SUBFIL
      *"""""""""""""""""
     C                   MOVE      *BLANKS       ZOP
     C                   UPDATE    TVI66DA
      *
     C                   ENDSR
