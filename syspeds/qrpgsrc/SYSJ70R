     FSYSJ70D   CF   E             WORKSTN
     F                                     SFILE(SYSJ70DA:ZRECNO)
     F                                     INFDS(FBAREA)
     FZILLOFL2  IF   E           K DISK
     F                                     RENAME(BILLOFR:BILLOFR2)
     FZILLOFL3  IF   E           K DISK
     F                                     RENAME(BILLOFR:BILLOFR3)
     FZILLOFL4  IF   E           K DISK
     F                                     RENAME(BILLOFR:BILLOFR4)
     FZILLOF    UF   E           K DISK
     FFIRM      IF   E           K DISK
      *___________________________
      * Arrays for string handling
      *"""""""""""""""""""""""""""
     D FBAREA          DS
     D  WSCLOC               370    371B 0
     D  WSSUBN               378    379B 0
     D                 DS
     D  WAAMD                  1      8  0
     D  WA                     3      4
     D  WM                     5      6
     D  WD                     7      8
     D                 DS
     D  TXT                    1     64
     D  SOTXT1                 1      8
     D  SOTXT2                 9     16
     D  SOTXT3                17     24
     D  SOTXT4                25     32
     D  SOTXT5                33     40
     D  SOTXT6                41     48
     D  SOTXT7                49     56
     D  SOTXT8                57     64
     D                UDS
     D  UDAVD                 15     18
     D  UDSG                  19     21
     D                 DS
     D  WSKNR                  1      8  0
     D  WSKNA                  9     16  0
     D  WSKNM                 17     24  0
     D  WSKNN                 25     32  0
     D  WSKNSA                33     40  0
     D  WSKNK                 41     48  0
     D  WSKNKA                49     56  0
     D  WSKNRA                57     64  0
     D  KN                     1     64
      *//////////////
      *  Hodelogikk /
      *//////////////
     C                   EXSR      SRA
     C                   EXSR      SRB
     C                   MOVE      '1'           *INLR
     C                   RETURN
      *////////////////////////////////////////////////////////////
      *  Initiering                                           SRA /
      *////////////////////////////////////////////////////////////
     C     SRA           BEGSR
      *___________________
      * Definiere Workfelt
      *"""""""""""""""""""
     C     *LIKE         DEFINE    BLUNIK        WUNIK1
      *__________________
      * Definiere Nøkkler
      *""""""""""""""""""
      *_____________
      * Init av felt
      *"""""""""""""
     C* Hente dagens dato og klokkeslett.
     C                   CALL      'SYGE15C'
     C                   PARM                    WDT               8
     C                   PARM                    WTM               6
     C* Hente firmaopplysninger
     C                   MOVE      *BLANKS       FIFIRM
     C     FIFIRM        SETLL     FIRM                               30
     C                   READ      FIRM                                   30
      * Hent overskrifter
     C                   MOVE      *BLANKS       TXT
     C                   MOVE      *ZEROS        KN
     C                   MOVE      'DIV3114'     XMSGNR            7            MELDING-ID
     C                   Z-ADD     3             XLEVEL            1 0          NIVÅ
     C                   Z-ADD     0             XRETUR            1 0          RETURKODE
     C                   Z-ADD     8             XMSGL             4 0          LENGDE PÅ MELDING
     C                   EXSR      HENTMS
     C                   MOVEL     XMSG          SOTXT1            8
     C                   MOVE      'DIV3110'     XMSGNR
     C                   EXSR      HENTMS
     C                   MOVEL     XMSG          SOTXT2            8
     C                   MOVE      'DIV3159'     XMSGNR
     C                   EXSR      HENTMS
     C                   MOVEL     XMSG          SOTXT3            8
     C                   MOVE      'DIV3160'     XMSGNR
     C                   EXSR      HENTMS
     C                   MOVEL     XMSG          SOTXT4            8
     C                   MOVE      'DIV3194'     XMSGNR
     C                   EXSR      HENTMS
     C                   MOVEL     XMSG          SOTXT5            8
     C                   MOVE      'DIV1062'     XMSGNR
     C                   EXSR      HENTMS
     C                   MOVEL     XMSG          SOTXT6            8
     C                   MOVE      'DIV3196'     XMSGNR
     C                   EXSR      HENTMS
     C                   MOVEL     XMSG          SOTXT7            8
     C                   MOVE      'YBC0198'     XMSGNR
     C                   EXSR      HENTMS
     C                   MOVEL     XMSG          SOTXT8            8
     C                   MOVE      'DIV1064'     XMSGNR
     C                   EXSR      HENTMS
     C                   MOVEL     XMSG          SOTXT9            8
     C                   Z-ADD     8             SOANTK            1 0
     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Bearbeide Formater                                   SRB /
      *////////////////////////////////////////////////////////////
     C     SRB           BEGSR

     C     SRB1          TAG
      *______________
      * Tømme Subfile
      *""""""""""""""
     C                   MOVE      '1'           *IN93
     C                   WRITE     SYSJ70DB
     C                   MOVEA     '000'         *IN(93)
     C                   Z-ADD     *ZERO         ZRECNO
      *_____________
      * Fyll Subfile
      *"""""""""""""
     C
     C                   MOVEL     XCONT         ØCONT            30
     C                   MOVEL     XLBLNR        ØLBLNR           16
     C                   MOVEL     XREFF         ØREFF            17
     C                   MOVEA     '00'          *IN(83)
     C     XLBLNR        COMP      *BLANKS                            8383
     C  N82XCONT         COMP      *BLANKS                            8282
     C  N82
     CANN83XREFF         COMP      *BLANKS                            8484
     C  N82
     CANN84
     CANN83              SETON                                        83
     C   82ØCONT         SETLL     ZILLOFL2
     C   83ØLBLNR        SETLL     ZILLOFL3
     C   84ØREFF         SETLL     ZILLOFL4
     C   82              READ      ZILLOFL2                               94
     C   83              READ      ZILLOFL3                               94
     C   84ØREFF         READE     ZILLOFL4                               94
     C     *IN94         DOWEQ     '0'
     C     BLST          IFEQ      *BLANKS
      *
     C                   ADD       1             ZRECNO
     C                   Z-ADD     BLDTA         WAAMD
     C                   MOVEL(P)  WD            ZLDTA
     C     ZLDTA         CAT       WM:0          ZLDTA
     C     ZLDTA         CAT       WA:0          ZLDTA
     C                   MOVEL(P)  BLNTBS        ZHAVN
     C                   MOVEL(P)  BLBBNA        ZBAAT
     C                   MOVEL(P)  BLAD4G        ZREDERI
     C                   MOVEL(P)  BLBLNR        ZLBLNR
     C                   MOVEL(P)  BLAD3G        ZCONT
     C                   MOVEL(P)  BLAD1G        ZLAD2G
     C                   WRITE     SYSJ70DA

     C                   ENDIF
     C   82              READ      ZILLOFL2                               94
     C   83              READ      ZILLOFL3                               94
     C   84ØREFF         READE     ZILLOFL4                               94
     C  N94              ENDDO

     C     ZRECNO        IFEQ      0
     C                   MOVE      'SPR0001'     ZMSGNO
     C                   MOVE      '1'           *IN30
     C                   MOVE      *BLANKS       XCONT
     C                   MOVE      *BLANKS       XLBLNR
     C                   MOVE      *BLANKS       XREFF
     C                   GOTO      SRB5
     C                   END
     C                   Z-ADD     1             ZRECNO
      *_______________
      * Behandle bilde
      *"""""""""""""""
     C     SRB5          TAG

     C                   MOVEA     '11'          *IN(91)
     C  N30              MOVE      'SY00000'     ZMSGNO
     C                   WRITE     SYSJ70DØ
     C                   WRITE     SYSJ70DC
     C     ZRECNO        IFEQ      0
     C                   EXFMT     SYSJ70DE
     C                   ELSE
     C                   EXFMT     SYSJ70DB
     C                   END

     C     *IN12         CABEQ     '1'           SRB1
     C     *IN03         IFEQ      '0'

     C                   MOVEA     '00'          *IN(91)
      *_____________
      * F5=Frisk opp
      *"""""""""""""
     C     *IN05         CABEQ     '1'           SRB1
      *_______
      * SØKING
      *"""""""
     C     XCONT         COMP      *BLANKS                            8282
     C     XLBLNR        COMP      *BLANKS                            8383
     C     XREFF         COMP      *BLANKS                            8484
     C   82
     COR 84
     COR 83              GOTO      SRB1
      *_______
      * Valg ?
      *"""""""
     C                   MOVE      '0'           *IN52
     C                   MOVE      '0'           *IN30
     C                   MOVE      '0'           *IN95
     C     *IN52         DOWEQ     '0'

     C                   READC     SYSJ70DA                               52
     C     *IN52         IFEQ      '0'
     C     ZOP           CASEQ     '1'           SRBA
     C     ZOP           CASEQ     '2'           SRBB
     C     ZOP           CASEQ     '4'           SRBD
     C     ZOP           CASEQ     '5'           SRBE
     C     ZOP           CASEQ     '6'           SRBE
     C     ZOP           CASEQ     'X'           SRBX
     C                   END
     C                   END

     C  N30
     CANN52              END

     C     *IN03         CABEQ     '1'           SRB9
     C     *IN95         CABEQ     '1'           SRB1
     C     *IN95         CABEQ     '0'           SRB5
     C                   END

     C     SRB9          ENDSR
      *////////////////////////////////////////////////////////////
      *  Plukking                                            SRBA /
      *////////////////////////////////////////////////////////////
     C     SRBA          BEGSR
      *_________________________________
      * Er Avdeling og signatur ifyllt ?
      *"""""""""""""""""""""""""""""""""
     C     BLUNIK        CHAIN(N)  ZILLOF                             30
     C     BLAVD         IFEQ      0
     C                   EXSR      SRBB
     C     *IN12         CABEQ     '1'           SRBA9
     C                   ENDIF
      *_________________________________
      * Flytt fra Z-filer til produksjon
      *"""""""""""""""""""""""""""""""""
     C                   CALL      'SYSJ71R'
     C                   PARM                    BLUNIK
      *______________________________
      * Over til registreringsprogram
      *""""""""""""""""""""""""""""""
     C     BLUNIK        CHAIN(N)  ZILLOF                             30
     C                   CALL      'SYSJ50I'
     C                   PARM                    BLAVD
     C                   PARM                    BLOPD
     C                   PARM                    BLSG
     C                   PARM      'N'           UKUNBL            1
     C                   PARM      0             UAVD2             4 0
     C                   PARM      0             UOPD2             7 0
     C                   PARM      1             UFLAGG            1 0
     C                   PARM                    BLUNIK
      *_____________
      * Oppdater fil
      *"""""""""""""
     C     BLUNIK        CHAIN     ZILLOF                             30
     C     *IN30         IFEQ      '0'
     C                   MOVE      'A'           BLST
     C                   UPDATE    BILLOFR
     C                   ENDIF
      *_________________
      * Oppdatere SUBFIL
      *"""""""""""""""""
     C*                  MOVE      *BLANKS       ZOP
     C*                  MOVE      'Plukket'     BLNAM
     C*                  UPDATE    SYSJ70DA

     C     SRBA9         ENDSR
      *////////////////////////////////////////////////////////////
      *  Endre fellesopplysninger                            SRBB /
      *////////////////////////////////////////////////////////////
     C     SRBB          BEGSR
      *__________
      * Hent data
      *""""""""""
     C     BLUNIK        CHAIN(N)  ZILLOF                             30
     C                   MOVEL     BLAAR         ZLAAR
     C                   MOVEL     BLUKE         ZLUKE
     C                   MOVEL     BLAVD         ZLAVD
     C                   MOVEL     BLSG          ZLSG
     C                   MOVEL     BLOB          ZLOB
     C                   Z-ADD     BLKNR         ZLKNR
     C                   MOVEL     BLTR          ZLTR
     C                   MOVEL     BLLSS         ZLLSS
     C                   MOVEL     BLLSH         ZLLSH
     C                   MOVEL     BLVH          ZLVH
     C                   MOVEL     BLLOH         ZLLOH
      *______________________________
      * Eksternt program hvis samlast
      *""""""""""""""""""""""""""""""
     C     BLAD2G        IFGT      '1'
     C                   CALL      'SYSJ73R'
     C                   PARM                    BLAD3G
     C                   GOTO      SRBB9
     C                   ENDIF
      *__________
      * Vis Bilde
      *""""""""""
     C     SRBB1         TAG
     C                   EXFMT     SYSJ70DD
     C     *IN04         IFEQ      *ON

      * Hvilket felt ?
     C     WSCLOC        DIV       256           LINNBR
     C                   MVR                     POSNBR
      * Båt
     C     LINNBR        IFEQ      11
     C     POSNBR        ANDGE     29
     C     POSNBR        ANDLE     31
     C                   CALL      'SYSJ11A'
     C                   PARM                    ZLOB
     C                   GOTO      SRBB1
     C                   ENDIF
      * Rederi
     C     LINNBR        IFEQ      12
     C     POSNBR        ANDGE     29
     C     POSNBR        ANDLE     36
     C                   EXSR      SØKKUN
     C                   GOTO      SRBB1
     C                   ENDIF
      * TR
     C     LINNBR        IFEQ      13
     C     POSNBR        ANDGE     29
     C     POSNBR        ANDLE     33
     C                   CALL      'SYSJ17A'
     C                   PARM      ZLKNR         UKNR              8 0
     C                   PARM                    ZLTR
     C     UKNR          IFNE      0
     C     ZLTR          ANDNE     *BLANKS
     C                   MOVE      UKNR          ZLKNR
     C                   END
     C                   GOTO      SRBB1
     C                   ENDIF
      * LSST
     C     LINNBR        IFEQ      14
     C     POSNBR        ANDGE     29
     C     POSNBR        ANDLE     33
     C                   MOVE      *BLANKS       UNVN
     C                   MOVE      *BLANKS       ULK2
     C                   CALL      'SYFI10F'
     C                   PARM                    ZLLSS
     C                   PARM                    UNVN             30
     C                   PARM                    ULK2              2
     C                   GOTO      SRBB1
     C                   ENDIF
      * LASTH
     C     LINNBR        IFEQ      15
     C     POSNBR        ANDGE     29
     C     POSNBR        ANDLE     33
     C                   MOVE      *BLANKS       UNVN
     C                   MOVE      *BLANKS       ULK2
     C                   CALL      'SYFI10F'
     C                   PARM                    ZLLSH
     C                   PARM                    UNVN             30
     C                   PARM                    ULK2              2
     C                   GOTO      SRBB1
     C                   ENDIF
      * VIAH
     C     LINNBR        IFEQ      16
     C     POSNBR        ANDGE     29
     C     POSNBR        ANDLE     33
     C                   MOVE      *BLANKS       UNVN
     C                   MOVE      *BLANKS       ULK2
     C                   CALL      'SYFI10F'
     C                   PARM                    ZLVH
     C                   PARM                    UNVN             30
     C                   PARM                    ULK2              2
     C                   GOTO      SRBB1
     C                   ENDIF
      * LOSSH
     C     LINNBR        IFEQ      17
     C     POSNBR        ANDGE     29
     C     POSNBR        ANDLE     33
     C                   MOVE      *BLANKS       UNVN
     C                   MOVE      *BLANKS       ULK2
     C                   CALL      'SYFI10F'
     C                   PARM                    ZLLOH
     C                   PARM                    UNVN             30
     C                   PARM                    ULK2              2
     C                   GOTO      SRBB1
     C                   ENDIF

     C                   GOTO      SRBB1
     C                   ENDIF
      *____________
      * Oppdater BL
      *""""""""""""
     C     BLUNIK        CHAIN     ZILLOF                             30
     C     *IN30         IFEQ      '0'
     C                   MOVEL     ZLAAR         BLAAR
     C                   MOVEL     ZLUKE         BLUKE
     C                   MOVEL     ZLAVD         BLAVD
     C                   MOVEL     ZLSG          BLSG
     C                   MOVEL     ZLOB          BLOB
     C                   Z-ADD     ZLKNR         BLKNR
     C                   MOVEL     ZLTR          BLTR
     C                   MOVEL     ZLLSS         BLLSS
     C                   MOVEL     ZLLSH         BLLSH
     C                   MOVEL     ZLVH          BLVH
     C                   MOVEL     ZLLOH         BLLOH
     C                   UPDATE    BILLOFR
     C                   ENDIF
      *_________________
      * Oppdatere SUBFIL
      *"""""""""""""""""
     C                   MOVE      *BLANKS       ZOP
     C                   UPDATE    SYSJ70DA

     C     SRBB9         ENDSR
      *////////////////////////////////////////////////////////////
      *  Slett                                               SRBD /
      *////////////////////////////////////////////////////////////
     C     SRBD          BEGSR
      *____________
      * Slett i fil
      *""""""""""""
     C     BLUNIK        CHAIN     ZILLOF                             30
     C     *IN30         IFEQ      '0'
     C                   DELETE    BILLOFR
     C                   ENDIF
      *_________________
      * Oppdatere SUBFIL
      *"""""""""""""""""
     C                   MOVE      *BLANKS       ZOP
     C                   MOVE      'Slettet'     BLREIS
     C                   MOVE      *BLANKS       BLBLNR
     C                   MOVE      *BLANKS       BLOB
     C                   MOVE      *BLANKS       ZLDTA
     C                   MOVE      *BLANKS       BLNAM
     C                   UPDATE    SYSJ70DA

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Vise                                                SRBE /
      *////////////////////////////////////////////////////////////
     C     SRBE          BEGSR
      *________________________________
      * Vise edifact i eksternt program
      *"""""""""""""""""""""""""""""""""
     C     BLUNIK        CHAIN(N)  ZILLOF                             30
     C     *IN30         IFEQ      '0'
     C                   CALL      'E112R_93A'
     C                   PARM                    BLSNN
     C                   PARM                    ZOP
     C                   ENDIF
      *_________________
      * Oppdatere SUBFIL
      *"""""""""""""""""
     C                   MOVE      *BLANKS       ZOP
     C                   UPDATE    SYSJ70DA

     C                   ENDSR
      *////////////////////////////////////////////////////////////
      *  Slå sammen to B/L                                   SRBX /
      *////////////////////////////////////////////////////////////
     C     SRBX          BEGSR
      *__________________
      * Spare første key.
      *""""""""""""""""""
     C                   MOVEL     BLUNIK        WUNIK1
      *_______________________
      * Les neste subfilrecord
      *"""""""""""""""""""""""
     C                   READC     SYSJ70DA                               52
     C     *IN52         IFEQ      '0'
     C                   CALL      'SYSJ72R'
     C                   PARM                    WUNIK1
     C                   PARM                    BLUNIK
     C                   ENDIF
     C                   MOVE      *ON           *IN95
     C                   ENDSR
     C***********************************************
     C     SØKKUN        BEGSR

     C                   MOVE      TXT           UTXT             64
     C                   MOVE      *ZEROS        KN
     C                   MOVE      *BLANKS       UKD
     C                   Z-ADD     ZLKNR         WSKNR
     C                   MOVE      KN            UKN              64
     C                   CALL      'SOEKUNBL'
     C                   PARM                    FIFIRM
     C                   PARM                    SOANTK
     C                   PARM                    UTXT
     C                   PARM                    UKN
     C                   PARM                    UKD               8
     C                   MOVE      UKN           KN
     C                   Z-ADD     0             UFLAGG
     C                   MOVE      UTXT          TXT
     C                   MOVE      WSKNR         ZLKNR

     C                   ENDSR
     C***********************************************
     C     HENTMS        BEGSR
     C***********************************************
     C                   CALL      'SUBR23R3'
     C                   PARM                    XMSGNR
     C                   PARM                    XMSG             62
     C                   PARM                    XLEVEL
     C                   PARM                    XRETUR
     C                   PARM                    XMSGL
     C*
     C                   ENDSR
