     A                                      DSPSIZ(24 80 *DS3 27 132 *DS4)
     A                                      PRINT
     A                                      HLPDOC('STD' 'HELPNU' 'HLP400')
     A                                      HELP
     A          R SUBFIL                    SFL
     A            SFTXT         17A  O  5 12TEXT('TXT AV FINANS. OPPL.')
EV5  A            SFDT           8Y 0H
NV5  A            WFDT           6Y 0O  5 32EDTCDE(Y)
     A            SFBL28        12Y 2O  5 41TEXT('BELØP FOR RUBR.28')
     A                                      EDTCDE(4)
     A            SFVK28         3A  O  5 58TEXT('VALUTAKODE RUBR.28')
     A            SFKR28         6Y 3O  5 64TEXT('KURS PÅ RUBR.28')
     A                                      EDTCDE(4)
     A          R SUBCTL                    SFLCTL(SUBFIL)
     A                                      SFLSIZ(0017)
     A                                      SFLPAG(0016)
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND
     A  91N93N94                            ROLLUP(25)
     A  91N93N94                            ROLLDOWN(26)
     A            ZRECNO         4S 0O  2 76SFLRCDNBR
     A                                      DSPATR(ND)
     A                                  1  2MSGCON(40 DIV1834 SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 58MSGCON(22 DIV0047 SYMSGF)
     A                                      COLOR(TRQ)
     A                                  4 11MSGCON(65 DIV1832 SYMSGF)
     A                                      COLOR(WHT)
     A          R BUNN
     A                                      ROLLUP(25)
     A                                      ROLLDOWN(26)
     A                                      CF05(05 'Frisk')
     A                                      CF08(08 'SOEK')
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A                                  3  2MSGCON(18 DIV0021 SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 22MSGCON(18 DIV0023 SYMSGF)
     A                                      COLOR(BLU)
     A                                  3 42MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A          R NYWRK
     A                                      CF05(05 'Frisk')
     A                                      CF08(08 'SOEK')
     A                                      CF12(12 'Forrige')
     A                                  1  2MSGCON(40 DIV1834 SYMSGF)
     A                                      COLOR(WHT)
     A                                  1 58MSGCON(22 DIV0047 SYMSGF)
     A                                      COLOR(TRQ)
     A                                  4 11MSGCON(65 DIV1832 SYMSGF)
     A                                      COLOR(WHT)
     A                                 23  2MSGCON(18 DIV0021 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 22MSGCON(18 DIV0023 SYMSGF)
     A                                      COLOR(BLU)
     A                                 23 42MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
     A          R SOKSDF
     A                                      CF12(12 'Forrige')
     A                                      SLNO(21)
     A                                      CLRL(04)
     A                                      OVERLAY
     A                                  2  2MSGCON(15 DIV1833 SYMSGF)
     A            W2TXT         17A  I  2 18DSPATR(UL)
     A                                      DSPATR(PC)
     A            W2DAG          2D 0I  2 36DSPATR(UL)
     A            W2MND          2D 0I  2 39DSPATR(UL)
     A            W2ÅR           2D 0I  2 42DSPATR(UL)
     A                                  3  2MSGCON(18 DIV0024 SYMSGF)
     A                                      COLOR(BLU)
