      *****************************************************************
      *** PROGRAM SKAL KONVERTERE INNGÅENDE IE25                    ***
      *** Release for transit                                       ***
      *** INDIKATORER 01 - 26 KUN CMD-TASTER / ROLL TASTER          ***
      *** LEDIGE INDIKATORER 01-26                                  ***
      *** LEDIGE INDIKATORER 28-32 34-97                            ***
      *****************************************************************
     H DFTACTGRP(*NO)
     FXMLRCVF   IF   E           K DISK
     FXMLRCLF   IF   E           K DISK
     FTRACKF    O  A E           K DISK
     FNCTSIH05  UF   E           K DISK
     FEDIC      UF   E             DISK
     FEDIM      O  A E             DISK
     FEDIM2     IF   E           K DISK    RENAME(EDIMR:EDIMR2)
     D                sds
     D ZUSER                 254    263
      *  Prototype for 'SYGE15C'
     d syge15c         pr                  extpgm('SYGE15C')
     d wdt_                                like(wdt)
     d wtm_                                like(wtm)
      *  Prototype for ncts154r2
     d ncts154r2       pr
     d usn_                                like(usn)
      *  *ENTRY Interface for Main Procedure
     d ncts154r2       pi
     d usn                            9
      *----------------------------------------------------------------------
      * Stand Alone Fields - TOP
      *----------------------------------------------------------------------
     d x1_xrlev1       s                   like(xrlev1)
     d x1_xrlev2       s                   like(xrlev2)
     d x2_xrlev1       s                   like(xrlev1)
     d x2_xrlev2       s                   like(xrlev2)
     d x2_xrlev3       s                   like(xrlev3)
     d x3_xrlev1       s                   like(xrlev1)
     d x3_xrlev2       s                   like(xrlev2)
     d x3_xrlev3       s                   like(xrlev3)
     d x3_xrlev4       s                   like(xrlev4)
     d x4_xrlev1       s                   like(xrlev1)
     d x4_xrlev2       s                   like(xrlev2)
     d x4_xrlev3       s                   like(xrlev3)
     d x4_xrlev4       s                   like(xrlev4)
     d x4_xrlev5       s                   like(xrlev5)
     d x5_xrlev1       s                   like(xrlev1)
     d x5_xrlev2       s                   like(xrlev2)
     d x5_xrlev3       s                   like(xrlev3)
     d x5_xrlev4       s                   like(xrlev4)
     d x5_xrlev5       s                   like(xrlev5)
     d x5_xrlev6       s                   like(xrlev6)
     d x6_xrlev1       s                   like(xrlev1)
     d x6_xrlev2       s                   like(xrlev2)
     d x6_xrlev3       s                   like(xrlev3)
     d x6_xrlev4       s                   like(xrlev4)
     d x6_xrlev5       s                   like(xrlev5)
     d x6_xrlev6       s                   like(xrlev6)
     d x6_xrlev7       s                   like(xrlev7)
     d x_titrnr        s                   like(titrnr)
     d x_tidtf         s              8
     d x_tirind        s                   like(tirind)
     d x_tirtsb        s                   like(tirtsb)
     d x_tirtint       s                   like(tirtint)
     d x_IE25          s              1a
     d wdt             s              8
     d wtm             s              6
     D UP              C                   CONST('ABCDEFGHIJKLMNOPQRST-
     D                                     UVWXYZÆØÅ')
     D LO              C                   CONST('abcdefghijklmnopqrst-
     D                                     uvwxyzæøå')
      *----------------------------------------------------------------------
     IEDIMR2
     I              M0004                       X0004
     I              M0010                       X0010
     I              M0035                       X0035
     I              M0062                       X0062
     I              M0065                       X0065
     I              M0068                       X0068
     I              M0068A                      X0068A
     I              M0068B                      X0068B
     I              M0068C                      X0068C
     I              M0068D                      X0068D
     I              M0068E                      X0068E
     I              M0068F                      X0068F
     I              M1001                       X1001
     I              M1004                       X1004
     I              M1225                       X1225
     I              M2005B                      X2005B
     I              M3039D                      X3039D
     I              M3039E                      X3039E
     I              M5004D                      X5004D
     I              M1N07                       X1N07
     I              M1N08                       X1N08
     I              M9N01                       X9N01
     I              MSN                         XSN
     I              MMN                         XMN
     I              MSR                         XSR
     I              MST                         XST
     I              MDT                         XDT
     I              MTM                         XTM
     I              MVEN                        XVEN
     I              MAVD                        XAVD
     I              MTDN                        XTDN
      /free

       exsr init;

       exsr srxml;

       *inlr = *on;
       return;

       //**********************************************
       begsr srxml;
       //**********************************************

         x1_xrlev1 = 0;
         x1_xrlev2 = 0;

         chain (xrsn:x1_xrlev1:x1_xrlev2) xmlrcvf;
         dow %found;
           select;
           when xrnv = 'messageSender';
             m0004  = xrverd;
           when xrnv = 'messageRecipient';
             m0010  = xrverd;
           when xrnv = 'preparationDateAndTime';
           when xrnv = 'messageIdentification';
             m0062  = xrverd;
             m1004  = xrverd;
           when xrnv = 'messageType' and xrverd = 'CC025C';
             x_ie25 = 'J';
           when xrnv = 'correlationIdentifier';
           when xrnv = 'TransitOperation';
             exsr srxml1;
           when xrnv = 'CustomsOfficeOfDestinationActual';
             exsr srxml2;
           when xrnv = 'TraderAtDestination';
             exsr srxml3;
           when xrnv = 'Consignment';
             exsr srxml4;
           endsl;

           x1_xrlev1 = x1_xrlev1 + 1;
           chain (xrsn:x1_xrlev1:x1_xrlev2) xmlrcvf;
         enddo;

         if x_ie25 = 'J';
           exsr opdopd;
           exsr ttsr;
         endif;

         endsr;

       //**********************************************
       begsr srxml1;
       //**********************************************

         x2_xrlev1 = xrlev1;
         x2_xrlev2 = 1;
         x2_xrlev3 = 0;

         chain (xrsn:x2_xrlev1:x2_xrlev2:x2_xrlev3) xmlrcvf;
         dow %found;
           select;
           when xrnv = 'MRN';
             x_titrnr = xrverd;
           when xrnv = 'releaseDate';
             x_tidtf = %subst(xrverd:1:4) + %subst(xrverd:6:2)
                   + %subst(xrverd:9:2);
           when xrnv = 'releaseIndicator';
             x_tirind = %dec(xrverd:1:0);
           endsl;

           x2_xrlev2 = x2_xrlev2 + 1;
           chain (xrsn:x2_xrlev1:x2_xrlev2:x2_xrlev3) xmlrcvf;
         enddo;

         chain x_titrnr nctsih05;
         if not %found;
         // Avslutt når MRN ikke finnes.
           *inlr = *on;
           return;
         endif;

         if x_tirind = 4;
           tist = 'S';
         else;
           tist = 'P';
         endif;
         tidtf   = %dec(x_tidtf:8:0);
         tirind  = x_tirind;

       endsr;

       //**********************************************
       begsr srxml2;
       //**********************************************

         x2_xrlev1 = xrlev1;
         x2_xrlev2 = 1;
         x2_xrlev3 = 0;

         chain (xrsn:x2_xrlev1:x2_xrlev2:x2_xrlev3) xmlrcvf;
         dow %found;
           select;
           when xrnv = 'referenceNumber';
             x_tirtsb = xrverd;
           endsl;

           x2_xrlev2 = x2_xrlev2 + 1;
           chain (xrsn:x2_xrlev1:x2_xrlev2:x2_xrlev3) xmlrcvf;
         enddo;

         tirtsb  = x_tirtsb;

       endsr;

       //**********************************************
       begsr srxml3;
       //**********************************************

         x2_xrlev1 = xrlev1;
         x2_xrlev2 = 1;
         x2_xrlev3 = 0;

         chain (xrsn:x2_xrlev1:x2_xrlev2:x2_xrlev3) xmlrcvf;
         dow %found;
           select;
           when xrnv = 'identificationNumber';
             x_tirtint = xrverd;
           endsl;

           x2_xrlev2 = x2_xrlev2 + 1;
           chain (xrsn:x2_xrlev1:x2_xrlev2:x2_xrlev3) xmlrcvf;
         enddo;

         tirtint = x_tirtint;

       endsr;

       //**********************************************
       begsr srxml4;
       //**********************************************

         x2_xrlev1 = xrlev1;
         x2_xrlev2 = 1;
         x2_xrlev3 = 0;

         chain (xrsn:x2_xrlev1:x2_xrlev2:x2_xrlev3) xmlrcvf;
         dow %found;
           select;
           when xrnv = 'HouseConsignment';
             exsr srxml401;
           endsl;

           x2_xrlev2 = x2_xrlev2 + 1;
           chain (xrsn:x2_xrlev1:x2_xrlev2:x2_xrlev3) xmlrcvf;
         enddo;

       endsr;

       //**********************************************
       begsr srxml401;
       //**********************************************

         x3_xrlev1 = xrlev1;
         x3_xrlev2 = xrlev2;
         x3_xrlev3 = 1;
         x3_xrlev4 = 0;

         chain (xrsn:x3_xrlev1:x3_xrlev2:x3_xrlev3:x3_xrlev4) xmlrcvf;
         dow %found;
           select;
           when xrnv = 'sequenceNumber';
           when xrnv = 'releaseType';
           when xrnv = 'ConsignmentItem';
             exsr srxml4011;
           endsl;

           x3_xrlev3 = x3_xrlev3 + 1;
           chain (xrsn:x3_xrlev1:x3_xrlev2:x3_xrlev3:x3_xrlev4) xmlrcvf;
         enddo;

       endsr;

       //**********************************************
       begsr srxml4011;
       //**********************************************

         x4_xrlev1 = xrlev1;
         x4_xrlev2 = xrlev2;
         x4_xrlev3 = xrlev3;
         x4_xrlev4 = 1;
         x4_xrlev5 = 0;

         chain (xrsn:x4_xrlev1:x4_xrlev2:x4_xrlev3:x4_xrlev4
               :x4_xrlev5) xmlrcvf;
         dow %found;
           select;
           when xrnv = 'goodsItemNumber';
           when xrnv = 'declarationGoodsItemNumber';
           when xrnv = 'releaseType';
           when xrnv = 'Commodity';
             exsr srxml40111;
           when xrnv = 'Packaging';
             exsr srxml40112;
           endsl;

           x4_xrlev4 = x4_xrlev4 + 1;
           chain (xrsn:x4_xrlev1:x4_xrlev2:x4_xrlev3:x4_xrlev4
                 :x4_xrlev5) xmlrcvf;
         enddo;

       endsr;

       //**********************************************
       begsr srxml40111;
       //**********************************************

       endsr;

       //**********************************************
       begsr srxml40112;
       //**********************************************

       endsr;

       //**********************************************
       begsr opdopd;
       //**********************************************

         update nctsihr;

         setll (tiavd: titdn: 99999999) edim2;
         readpe (tiavd: titdn) edim2;

         chain 1 edic;
         cmn = cmn + 1;
         update edicr;
         mmn = cmn;
         m0065  = 'XMLDEC';
         m0068  = x0068;
         m1001  = 'TEI';
         m1225  = '025';
         msn    = xrsn;
         msr    = 'R';
         mst    = 'C';
         mdt    = %float(wdt);
         mtm    = %float(wtm);
         m0068a = x0068a;
         m0068b = x0068b;
         m1n07  = '025';
         mavd   = tiavd;
         mtdn   = titdn;
         write edimr;

       endsr;

       //**********************************************
       begsr ttsr;
       //**********************************************

         ttavd  = tiavd;
         ttopd  = titdn;
         ttacti = 'T25';
         ttdate = %float(wdt);
         tttime = %float(wtm);
         ttuser = zuser;
         tttext = 'Goods release notification NCTS '
                  + %trim(%editc(tidtf:'Z'))+ ' '  + %trim(titrnr);
         tttexl = 'Transit avsluttet NCTS'
                  + %trim(%editc(tidtf:'Z'))+ ' '  + %trim(titrnr);
         write trackfr;

       endsr;

       //**********************************************
       begsr init;
       //**********************************************

         xrsn = %dec(usn:9:0);
         syge15c ( wdt : wtm );

       endsr;

      /end-free
